     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VU100R
      * Beskrivelse . . : Struktur-vare, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290502 AMD  Nytt program
6.10  * R6.10 240909 BSA  Endret beskrivelsen av varenummer og lagt til nye felter
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvstrl1    if   e           k disk    rename(vstrpfr:vstrl1r)
     fvstrl2    if   e           k disk    rename(vstrpfr:vstrl2r)
     fvstrl3    if   e           k disk    rename(vstrpfr:vstrl3r)
     fvstrlr    if   e           k disk    rename(vstrpfr:vstrlrr)
     fvstrlu    uf a e           k disk    rename(vstrpfr:vstrlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvu100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vstrl1_vare     s                   like(vuvare)
     d vstrl1_vars     s                   like(vuvars)
     d vstrl2_vars     s                   like(vuvars)
     d vstrl3_besk     s                   like(vubesk)
     d vstrlr_vare     s                   like(vuvare)
     d vstrlr_vars     s                   like(vuvars)
     d vstrlu_vare     s                   like(vuvare)
     d vstrlu_vars     s                   like(vuvars)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vufirm)
     d w_vare          s                   like(vvvare)
     d w_vars          s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
      *
     d s_vare          s                   like(vuvare)
     d s_vars          s                   like(vuvars)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
6.10 c                   if        b2vare <> *blank or
6.10 c**                           b2vars <> *blank or
     c                             b2besk <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * Subrutine for behandling av struktur
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     struktur      begsr
      *
      * Kaller opp program for behandling av struktur
      *
     c                   call      'VU101R'
     c                   parm                    b1vare
     c                   parm                    b1besk
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spøring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Spørring på hovedvarenummer
      *
     c                   if        w_arcd = 'C1BLD'
     c                   if        w_afld = 'C1VARE'
     c                   move      c1vare        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
      *
     c                   movel     w_vare        c1vare
     c                   goto      end_spØrr
     c                   endif
     c                   endif
6.10  *
6.10  * Spørring på strukturvarenummer
6.10  *
6.10 c*                  if        w_arcd = 'C1BLD'
6.10 c*                  if        w_afld = 'C1VARS'
6.10 c*                  move      c1vars        w_vars
6.10 c*                  call      'VV500R'
6.10 c*                  parm                    w_firm
6.10 c*                  parm                    w_vars
6.10 c*                  parm                    w_tek1
6.10  *
6.10 c*                  movel     w_vars        c1vars
6.10 c*                  goto      end_spØrr
6.10 c*                  endif
6.10 c*                  endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      vstrl3_besk = b1besk
     c     vstrl3_key    setll     vstrl3
     c                   when      w_seqe = 'B'
6.10 c                   eval      vstrl2_vars = *blank
     c     vstrl2_key    setll     vstrl2
     c                   other
     c                   eval      vstrl1_vare = b1vare
     c     vstrl1_key    setll     vstrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Hovedvare
      *
6.10 c                   if        b2vare <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vstrl1_vare = b2vare
6.10 c                   eval      vstrl1_vars = *blanks
     c     vstrl1_key    setll     vstrl1
     c                   goto      end_pos
     c                   endif
      *
      * Strukturvare
      *
6.10 c*                  if        b2vars <> *blanks
6.10 c*                  eval      w_seqe = 'B'
6.10 c*                  eval      vstrl2_vars = b2vars
6.10 c*    vstrl2_key    setll     vstrl2
6.10 c*                  goto      end_pos
6.10 c*                  endif
      *
      * Beskrivelse
      *
     c                   if        b2besk <> *blank
     c                   eval      w_seqe = 'C'
     c                   eval      vstrl3_besk = b2besk
     c     vstrl3_key    setll     vstrl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
6.10 c                   eval      b2vare = *blank
6.10 c**                 eval      b2vars = *blank
     c                   eval      b2besk = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
6.10 c**                 eval      c1vars = b1vars
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1vare = b1vare
6.10 c**                 eval      k1vars = b1vars
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
6.10 c**                 eval      d1vars = b1vars
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b1vare
6.10 c**                 eval      c1vars = b1vars
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Struktur
      *
6.10 c                   if        b1valg = 8
6.10 c                   eval      c1vare = b1vare
6.10 c                   exsr      struktur
6.10 c                   eval      b1valg = 0
6.10 c                   update    b1sfl
6.10 c                   iter
6.10 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vare = b2vare
6.10 c**                 eval      c1vars = b2vars
      *
     c     c1taga        tag
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endif
      *
6.10 c                   if        c1vare = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
      *
      * Hovedvare kan ikke være = 0
      *
6.10 c                   if        c1vare = *blank
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sjekk om hovedvare finnes i vareregister
      *
     c                   movel     c1vare        w_vare
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1r
     c                   if        not %found
     c                   eval      *in32  = *on
     c                   goto      c1taga
     c                   else
     c                   eval      c2besk = vvtek1
     c                   endif
      *
      * Sjekk om strukturvare finnes i vareregister
      *
6.10 c**                 movel     c1vars        w_vars
6.10 c*                  if        c1vars <> *blank
6.10 c*                  eval      vvarl1_vare = w_vars
6.10 c*    vvarl1_key    chain     vvarl1r
6.10 c*                  if        not %found
6.10 c*                  eval      *in33  = *on
6.10 c*                  goto      c1taga
6.10 c*                  else
6.10 c*                  eval      c2besk = vvtek1
6.10 c*                  endif
6.10 c*                  endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vstrlr_vare = c1vare
6.10 c*                  eval      vstrlr_vars = c1vars
     c     vstrlr_key    chain     vstrlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      *in41  = *off
     c                   eval      *in42  = *off
      *
6.10 c*                  if        c1vars = *blank
6.10 c*                  eval      c1vare = *blank
6.10 c*                  eval      *in41  = *on
6.10 c*                  else
     c                   eval      *in42  = *on
6.10 c*                  endif
      *
6.10 c*                  eval      c1vars = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
6.10 c                   eval      b2vare = *blank
6.10 c**                 eval      b2vars = *blank
     c                   eval      b2besk = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vstrlr_vare = c1vare
6.10 c                   eval      vstrlr_vars = *blank
     c     vstrlr_key    chain     vstrlr
      *
     c                   if        %found
6.10 c**                 eval      c2anta = vuanta
     c                   eval      c2eusr = vueusr
6.10 c                   eval      c2kstr = vukstr
     c     *dmy          move      vuodat        c2odat
     c     *dmy          move      vuedat        c2edat
     c                   else
6.10 c**                 eval      c2anta = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1vare = k1vare
6.10 c*                  eval      c1vars = k1vars
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
      * Legg på plass tekst fra vareregister (hovedvare)
      *
     c                   movel     c1vare        w_vare
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   eval      c2besk = vvtek1
     c                   endif
      *
      * Legg på plass tekst fra vareregister (strukturvare)
      *
6.10 c*                  movel     *blank        w_vars
6.10 c*                  if        c1vars <> *blank
6.10 c*                  eval      vvarl1_vare = w_vars
6.10 c*    vvarl1_key    chain     vvarl1r
6.10 c*                  if        %found
6.10 c*                  eval      c2besk = vvtek1
6.10 c*                  endif
6.10 c*                  endif
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
      *
      * Antall skal ikke oppgis på hovedvare
      *
6.10 c*                  if        c1vars =  *blank
6.10 c*                  if        c2anta <> 0
6.10 c*                  eval      *in31  = *on
6.10 c*                  goto      c2taga
6.10 c*                  endif
6.10 c*                  endif
      *
      * Antall må oppgis på strukturvare
      *
6.10 c*                  if        c1vars <> *blank
6.10 c*                  if        c2anta =  0
6.10 c*                  eval      *in32  = *on
6.10 c*                  goto      c2taga
6.10 c*                  endif
6.10 c*                  endif
      *
      * Angitt konstruksjonskode må være gyldig
      *
6.10 c                   if        c2kstr <> 'J' and
6.10 c                             c2kstr <> ' '
6.10 c                   eval      *in34  = *on
6.10 c                   goto      c2taga
6.10 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vstrlur
      *
     c                   eval      vstrlu_vare = c1vare
6.10 c                   eval      vstrlu_vars = *blank
     c     vstrlu_key    chain     vstrlur
      *
     c                   eval      vubesk = c2besk
6.10 c                   eval      vuanta = 0
6.10 c                   eval      vukstr = c2kstr
      *
     c                   time                    vuedat
     c                   time                    vuetim
     c                   eval      vueusr = l_user
      *
     c                   if        %found
     c                   update    vstrlur
     c                   else
     c                   eval      vufirm = w_firm
     c                   eval      vuvare = c1vare
6.10 c                   eval      vuvars = *blank
     c                   time                    vuodat
     c                   time                    vuotim
     c                   eval      vuousr = l_user
     c                   write     vstrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1besk = vubesk
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vstrlr_vare = d1vare
6.10 c                   eval      vstrlr_vars = *blank
     c     vstrlr_key    chain     vstrlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vstrlu_vare = d1vare
6.10 c**                 eval      vstrlu_vars = d1vars
     c     vstrlu_key    chain     vstrlur
      *
     c                   delete    vstrlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_vare = k1vare
6.10 c                   eval      s_vars = *blank
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vstrlr_vare = k1vare
6.10 c                   eval      vstrlr_vars = *blank
     c     vstrlr_key    chain     vstrlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
6.10 c                   if        k1vare = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vstrlr_vare = k1vare
6.10 c                   eval      vstrlr_vars = *blank
     c     vstrlr_key    chain     vstrlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1vare = s_vare
6.10 c**                 eval      c1vars = s_vars
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   read      vstrl3
     c                   when      w_seqe = 'B'
     c                   read      vstrl2
     c                   other
     c                   read      vstrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vufirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      * Legg ikke ut post, hvis ikke strukturvare er blank.
     c                   if        vuvars = *blank
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      vstrl3_besk = vubesk
     c                   when      w_seqe = 'B'
     c                   eval      vstrl2_vars = vuvars
     c                   other
     c                   eval      vstrl1_vare = vuvare
     c                   eval      vstrl1_vars = vuvars
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = vuvare
6.10 c**                 eval      b1vars = vuvars
     c                   eval      b1besk = vubesk
6.10 c**                 eval      b1anta = vuanta
6.10 c                   eval      b1kstr = vukstr
      *
     c                   write     b1sfl
      *
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'C'
     c     vstrl3_key    setgt     vstrl3
     c                   readp     vstrl3
     c                   when      w_seqe = 'B'
     c     vstrl2_key    setgt     vstrl2
     c                   readp     vstrl2
     c                   other
     c     vstrl1_key    setgt     vstrl1
     c                   readp     vstrl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   readp     vstrl3
     c                   when      w_seqe = 'B'
     c                   readp     vstrl2
     c                   other
     c                   readp     vstrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vufirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      vstrl3_besk = vubesk
     c                   when      w_seqe = 'B'
     c                   eval      vstrl2_vars = vuvars
     c                   other
     c                   eval      vstrl1_vare = vuvare
     c                   eval      vstrl1_vars = vuvars
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'C'
     c     vstrl3_key    setll     vstrl3
     c                   when      w_seqe = 'B'
     c     vstrl2_key    setll     vstrl2
     c                   other
     c     vstrl1_key    setll     vstrl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på hovedvarenummer
      *
     c     vstrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vstrl1_vare
     c                   kfld                    vstrl1_vars
      *
      * Key for posisjonering på struktur-vare
      *
     c     vstrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vstrl2_vars
      *
      * Key for posisjonering på beskrivelse
      *
     c     vstrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vstrl3_besk
      *
      * Key for les på strukturtabell
      *
     c     vstrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vstrlr_vare
     c                   kfld                    vstrlr_vars
      *
      * Key for oppdatering av strukturtabell
      *
     c     vstrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vstrlu_vare
     c                   kfld                    vstrlu_vars
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
6.10 c                   eval      vstrl1_vare = *blank
     c                   eval      vstrl1_vars = *blank
     c     vstrl1_key    setll     vstrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
