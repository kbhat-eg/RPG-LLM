     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  System. . . . . : ASLAGR
      *  Program . . . . : LI701R
      *  Beskrivelse . . : Mottak av EDI-meldinger (Type : Ordrebekreftelse)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *  5.60       TSV    Legger inn rutine for desimalfelter og -sign
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.10  *  6.10  020210 BSA  Setter status på varelinja til akseptert (5) hvis den kommer blank
6.11  *  6.10  111010 BSA  Sjekker mot GTIN register, hvis nobb nummer er 0
6.11  *                    på innlest register.
6.12  *  6.10  200111 bsa  GTIN nummer utvidet fra 13 til 14 nummer på enkelte
6.12  *                    ed10 records. Må sjekke og trunkere slik at formatet
6.12  *                    videre blir ok. NB GTIN nummer 0'es ut, slik at det
6.12  *                    ikke kan benyttes til identifisering.
6.13  *  6.10  150211 bsa  Uteglemt 0'stille diverse formater.
6.14  *  6.10  181111 ask  Diverse endringer pga. testing mot NeB
6.15  *  6.10  230512 bsa  NEB standard krever bruk av GTIN. For å dekke opp
6.15  *                    både NEB og TBF, går vi først på GTIN deretter NOBB
6.15  *                    og vedlagte enheter.
6.16  *  6.10  070612 bsa  Setter feil kode på partidentifikator
6.17  *  6.10  080612 bsa  Håndtering av tillegg/fradragslinjer
6.18  *  6.10  110612 bsa  Linjenummer kan være feil i retur. Unngår crash.
6.19  *  6.10  060812 bsa  Endrer håndtering av adresseinformasjon på linja.
6.19  *                    Spesielt håndtering der EAN nr ikke finnes som
6.19  *                    partsindikator.
6.1A  *  6.10  040213 bsa  Legger ut linjer som ikke bekreftes på bestilling
6.1B  *  6.10  120213 bsa  Reverserer endring 6.10. Status settes til "U"=Ukjent
6.1C  *  6.10  120213 bsa  Sjekker leveringsdatoer, og setter statuser.
6.1D  *  6.10  190213 bsa  Nullstiller bestillingsnummer mellom edi meldinger.
6.20  *  6.20  240511 bsa  Avvikende antall behøver ikke være feil. Regner om
6.20  *                    til nytt antall hvis avvikende enheter.
6.21  *  6.20  130112 bsa  Legger ut meldingsnummer fra EDI meldinga på
6.21  *                    bestillingshodet.
6.22  *  6.20  190112 bsa  Synkroniserer lengde på felter
6.23  *  6.20  190613 bsa  Skal ikke synke opp fra bestilling, hvis det ikke
6.23  *                    finnes bestillingsnummer.
6.24  *  6.20  130214 bsa  Nullstiller bestillingsnummer, slik at det ikke
6.24  *                    dras med gamle verdier hvsi ED04 ikke finnes ikke
6.24  *                    på EDI meldinga.
6.25  *  6.20  070314 bsa  Hvis enkelte tester på varelinjenivå slo til
6.25  *                    kunne feil bestillingsnummer henge igjen.
6.26  *  6.20  040714 bsa  Fortsetter med å tette hulll rundt bestillingsnummer
6.27  *  6.20  070814 bsa  I enkelte tilfeller klarer den ikke knytte
6.27  *                    bestillingslinjer til EDI melding og lager
6.27  *                    550000 linjer uten grunn.
6.28  *  6.20  180814 bsa  Forts... problemer med å linke bestilling
6.28  *                    med EDI melding.
6.29  *  6.20  290814 bsa  Legger av bestillingsnummer for nullstilling
6.29  *                    etter kryssjekk.
6.3a  *  6.20  011014 bsa  Forsøker for n'te gang å unngå 55 linjer
6.3b  *  6.20  081014 bsa  MVA satsen kan komme på to formater, over og under
6.3b  *                    10 %. NB Reiser osv har 8% sats.
6.nu  * 6.30  050614 bof   Gjennomgått mtp. nummerutvidelse
6.30  * 6.30  020914 bsa   Logger ikke feilkode på tekstlinjer på bestilling
6.31  * 6.30  250914 bsa   Legger ut tekst fra adresseinfo EDI hvis finnes
6.31  *                    på DP info
6.32  * 6.30  050315 bsa   Logger feiltransaksjoner og forsøker ta seg videre
6.32  *                    uten at det går på "trynet".
6.33  *  6.20  160415 bsa  Linjenummer hentes kun ut ved ED17 og ON
6.34  *  6.20  160415 bsa  Tar vare på tekstlinjer
6.35  *  6.30  030615 bsa  Kryssjekk mot bestilling er ikke helt i orden.
6.36  *  6.30  110615 bsa  Oppdaterer logg
6.37  *  6.30  250615 bsa  Uteglemt å gi årstall verdi.
6.38  *  6.30  220216 bsa  Nullstiller status etter å ha opprettet 55 linjer.
6.39  *  6.30  080316 bsa  Nullstiller linjeteller for fritekst varelinjer.r.
6.3c  *  6.30  120416 bsa  Nullstiller bestillingsnummer i alfatest
6.3d  *  6.30  220616 bsa  Legger ut EDI formatet på 100001 record.
6.3e  *  6.30  230117 bsa  Behandler GTIN nummer for å håndtere at de kan
6.3e  *                    komme med ulik lengde. Ser ut til at startposisjon
6.3e  *                    er fast.
6.3f  *  6.30  281116 bsa  Logger alle EDI meldinger som ikke automatisk
6.3f  *                    godkjennes - dvs har statuskode <> "blank"
6.3i  *  6.30  130617 bsa  Meldingsdato må alltid ha verdi. Problemer hvis
6.3i  *                    info om leveringsdato ikke er med på meldinga.
6.3j  *  6.30  300617 bsa  Feil beregning av rabatt, hvis det er flere rabatter
6.3j  *                    på meldinga.
6.3k  *  6.30  030818 bsa  Tar høyde for leverandørens ordrenummer som egen ID
6.3k  *                    på meldinga. (ED04 - VN)
6.3l  * 6.30  081018 bof   Utvidet med trelast sortiment
6.3m  * 6.30  220219 bsa   Hvis GTIN nummer ikke er utfylt, sjekkes ikke lengden
6.3m  *                    eller videre gyldighet.
6.3n  * 7.00  130120 bsa   Hvis "egenkomponert" GTIN nummer, fyll med 0'er
      *
7.01  *K000   070920 bsa   Endre logikk til å flagge feil, så sant ikke linjenummer
7.01  *K000                finnes på ordrebekreftelsen.
7.02  *K000   080920 bsa   Feil tidspunkt å 0-stille linjenummer fra EDI meldinga.
7.03  *K000   150920 bsa   Fjernet logikk for bare stole på linjenummer. Ref 7.01.
7.03  *K000                Sjekker også mot meldingsformat.
7.04  *K000   160920 bsa   Tar høyde for at ikke beregning skal feile.
7.05  *K000   150621 bsa   Finner laveste leveringsdato, og ikke bare siste
      *
8.01  *K0000  230621 BSA  Utvidet meldingsnummer fra 6-8 siffer
8.02  *K0000  191021 BSA  Håndterer også 11 siffers GTIN nummer. Egentlig
8.02  *K0000              GTIN 12, men foranstilt 0 blir borte når det
8.02  *K0000              kommer fra Logiq.
8.03  *K0000  180221 BSA  Endret sjekk på GTIN nummer
8.04  *K0000  240423 bsa  Innfører switch for å hoppe over sjekk av leveringsdato.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av linjenummersekvenser (2 første siffer i linjenr LIMLIN)
      *
      *    10 : Hodeinformasjon
      *    20 : Partsinformasjon
      *    30 : Referanser
      *    40 : Transportinformasjon
      *    50 : Linjer
      *    55 : Linjer fra best. (ikke match i EDI-melding)
      *    60 : Tillegg/fradrag hodenivå
      *    90 : Totaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flweopf    if   e           k disk
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
6.21 flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl5    if   e           k disk    rename(rlevpfr:rlevl5r)
     fledilu    uf a e           k disk    rename(ledipfr:ledilur)
     fledilr    if a e           k disk    rename(ledipfr:ledilrr)
6.1A fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
6.32 feloglu    uf a e           k disk    rename(elogpfr:eloglur)
6.3f flbedlu    uf a e           k disk    rename(lbedpfr:lbedlur)
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
8.04 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
6.21 d lohelu_numm     s                   like(lonumm)
6.21 d lohelu_suff     s                   like(losuff)
     d lohel1_numm     s                   like(lonumm)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d lodtlv_vare     s                   like(ldvare)
     d rlevl1_levr     s                   like(rllevr)
     d rlevl5_eanl     s                   like(rleanl)
     d ledilu_type     s                   like(litype)
     d ledilu_mnum     s                   like(limnum)
     d ledilu_mlin     s                   like(limlin)
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d vvarl3_nobb     s                   like(vvnobb)
     d votyl1_otyp     s                   like(vaotyp)
     d jvarl3_nobb     s                   like(jvnobb)
     d fenkl1_enhf     s                   like(feenhf)
     d venhl1_eenh     s                   like(vaeenh)
6.1A d ledil1_type     s                   like(litype)
6.1A d ledil1_mnum     s                   like(limnum)
6.1A d ledil1_mlin     s                   like(limlin)
6.32 d eloglu_faar     s                   like(xlfaar)
6.32 d eloglu_type     s                   like(xltype)
6.32 d eloglu_lean     s                   like(xllean)
6.32 d eloglu_refn     s                   like(xlrefn)
6.3f d lbedlu_type     s                   like(xbtype)
6.3f d lbedlu_aarr     s                   like(xbaarr)
6.3f d lbedlu_numm     s                   like(xbnumm)
6.3f d lbedlu_refn     s                   like(xbrefn)
      *
6.11 d p_eann          s             14  0
6.11 d p_vare          s             15
6.11 d p_enhe          s              3
6.11 d p_nobb          s              8  0
6.11 d p_stat          s              1
6.20 d p_antu          s             11  3
6.32 d p_eror          s              1
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lifirm)
     d w_numm          s                   like(lonumm)
6.nu d w_num8          s                   like(lonumm)
     d w_best          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_line          s                   like(liline)
     d w_blin          s                   like(liline)
     d w_fhtx          s                   like(liline)
     d w_evar          s              8  0
6.nu d w_bes_alf       s              8
     d w_bes_inn       s             35
6.nu d w_besa          s              8
6.nu d w_besn          s              8
     d w_rect          s              4
     d w_srec          s              4
     d w_laks          s              3
     d w_lenh          s              3
     d w_stat          s              1
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_dato_6        s              6  0
     d w_dato_alf      s              6
     d w_dato_8        s              8  0
     d w_time_6        s              6  0
     d w_time_alf      s              4
     d w_pris          s             15  3
     d w_rabb          s             15  3
     d w_numm_15       s             15  0
     d w_numm_alf      s             13
     d w_eann          s             13  0
6.22 d w_anta          s             11  3
     d w_pos1          s              2  0
     d w_pos2          s              2  0
6.3e d w_leng          s              2  0
     d w_atel          s              1  0
     d w_numtst        s             15    inz('000000000000000')
     d w_fast          s             10
     d w_fell          s              6
     d w_type          s              2
     d w_kode          s              1
8.01 d w_mnum          s              8  0
     d w_retn          s              2  0
     d w_hftx1         s             80
     d w_hftx2         s             80
6.34 d w_hftx3         s             80
6.34 d w_hftx4         s             80
6.34 d w_hftx5         s             80
6.34 d w_lftx1         s             80
6.34 d w_lftx2         s             80
6.34 d w_lftx3         s             80
6.34 d w_lftx4         s             80
6.34 d w_lftx5         s             80
6.34 d w_lftx6         s             80
6.34 d w_lftx7         s             80
6.34 d w_lftx8         s             80
6.34 d w_lftx9         s             80
6.39 d w_fltx          s                   like(liline)
     d w_head_lin      s              6  0
     d w_part_lin      s              6  0
     d w_refn_lin      s              6  0
     d w_trns_lin      s              6  0
     d w_olin_lin      s              6  0
     d w_oexn_lin      s              6  0
     d w_htlg_lin      s              6  0
     d w_otot_lin      s              6  0
6.32 d w_meld          s            128
6.32 d w_refn          s                   like(xlrefn)
6.32 d w_udat          s               d   datfmt(*iso)
6.32 d w_utim          s               t   timfmt(*iso)
6.32 d w_year          s              4  0
6.34 d w_otxt_lin      s              6  0
6.34 d w_oltx_lin      s              6  0
6.3j d w_brut          s             15  3
6.3j d w_bru2          s             15  3
6.3j d w_rabt          s             15  3
6.3j d w_rbel          s             15  3
6.3l d w_lv_vare       s                   like(vvvare)
      *
     d b_sjek          s              1    inz(*off)
     d b_ohod          s              1    inz(*off)
     d b_olin          s              1    inz(*off)
     d b_otot          s              1    inz(*off)
     d b_oref          s              1    inz(*off)
     d b_oprt          s              1    inz(*off)
     d b_otra          s              1    inz(*off)
     d b_numm          s              1    inz(*off)
6.27 d b_newl          s              1    inz(*off)
6.32 d b_feil          s              1    inz(*off)
6.35 d b_besl          s              1    inz(*off)
7.03 d b_nebf          s              1    inz(*off)
      *
6.29 d s_numm          s                   like(lonumm)
6.29 d s_suff          s                   like(losuff)
6.3f d s_ldor          s                   like(loldor)
      *
5.60 d w_stri          s             20
 .   d w_sign          s              1
 .   d w_blkp          s              2  0
 .   d w_dsmp          s              2  0
 .   d w_ades          s              2  0
 .   d w_resu          s             20  5
 .   d w_hltx          s             15
 .   d w_hltn          s             15  0
 .   d w_dstx          s              5
 .   d w_dstn          s              5  5
 .   d w_bbest         s             15  2
 .   d w_bfakt         s             15  2
 .   d w_ftx1          s             70
6.12 d w_pos           s              3  0
6.3e d w_gtin          s             14
6.3j d w_ol_tpr1       s              5  2
6.3j d w_ol_tbe1       s             15  2
      *
     d b_diff          s              1    inz(*off)
8.04 d u_804           s              1    inz(*off)
      *
     d c_digi          c                   '0123456789'
     d c_char          c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ ab+
     d                                      cdefghijklmnopqrstuvwxyzæøå.,/:-'
5.60 d c_diga          c                   ' ,.0123456789'
      *
      *  Definering av datastrukturer for melding inn
      *
     d o_ed00          ds
     d  o_id00                        4
     d  o_decs00                      1
     d  o_send00                     13
     d  o_mott00                     13
     d  o_gdat00                      6
     d    o_gdax00                    6  0 overlay(o_gdat00:1)
     d  o_gtim00                      4
     d    o_gtix00                    4  0 overlay(o_gtim00:1)
     d  o_test00                      1
      *
     d o_ed01          ds
     d  o_id01                        4
     d  o_mref01                     14
     d  o_mtyp01                      6
     d  o_mver01                      3
     d  o_mrel01                      3
     d  o_mkon01                      2
     d  o_mfor01                      6
     d  o_mkod01                      3
     d  o_benr01                     35
     d  o_aksp01                      3
     d  o_rtyp01                      3
      *
     d o_ed02          ds
     d  o_id02                        4
     d  o_dkva02                      3
     d  o_mdat02                     12
     d    o_mdan02                    8  0 overlay(o_mdat02:1)
     d    o_mtin02                    4  0 overlay(o_mdat02:9)
     d  o_for102                      3
      *
     d o_ed03          ds
     d  o_id03                        4
     d  o_txkv03                      3
     d  o_frtx03                     70
      *
     d o_ed04          ds
     d  o_id04                        4
     d  o_rfkv04                      3
     d  o_refr04                     35
      *
     d o_ed05          ds
     d  o_id05                        4
     d  o_pakv05                      3
     d  o_paid05                     14
     d    o_pean05                   13  0 overlay(o_paid05:1)
     d  o_pako05                      3
     d  o_paor05                      3
     d  o_refq05                      3
     d  o_fore05                     16
     d  o_ctfu05                      3
     d  o_kont05                     30
      *
     d o_ed06          ds
     d  o_id06                        4
     d  o_mtna06                     35
     d  o_mta106                     35
      *
     d o_ed07          ds
     d  o_id07                        4
     d  o_mta207                     35
     d  o_mtpn07                      8
     d  o_mtps07                     30
     d  o_mtlk07                      3
      *
     d o_ed08          ds
     d  o_id08                        4
     d  o_valk08                      3
     d  o_ttyp08                      3
     d  o_fmat08                      3
     d  o_lfun08                      3
     d  o_lbet08                      3
     d  o_lste08                     13
     d    o_lean08                   13  0 overlay(o_lste08:1)
     d  o_stkv08                      3
      *
     d o_ed09          ds
     d  o_id09                        4
     d  o_ftkv09                      3
6.16 d* o_tsek09                      3
     d  o_ftty09                      3
     d  o_fttx09                     35
     d  o_ftbe09                     18
     d    o_ftbh09                   15  0 overlay(o_ftbe09:1)
     d    o_ftbd09                    2  2 overlay(o_ftbe09:17)
      *
     d o_ed10          ds
     d  o_id10                        4
     d  o_vlin10                      6
     d    o_vlix10                    6  0 overlay(o_vlin10:1)
     d  o_noti10                      3
     d  o_eanr10                     15
6.3e d**  o_eanx10                   13  0 overlay(o_eanr10:1)
6.3e d    o_eanx14                   14  0 overlay(o_eanr10:1)
6.3e d    o_eanx13                   13  0 overlay(o_eanr10:1)
6.3e d    o_eanx12                   12  0 overlay(o_eanr10:1)
8.02 d    o_eanx11                   11  0 overlay(o_eanr10:1)
6.3e d    o_eanx08                    8  0 overlay(o_eanr10:1)
     d  o_eank10                      3
     d  o_piq110                      3
     d  o_alt110                     15
     d    o_altx10                    8  0 overlay(o_alt110:1)
     d  o_alk110                      3
     d  o_piq210                      3
     d  o_alt210                     15
     d  o_alk210                      3
6.12  *
6.12 d x_ed10          ds
6.12 d  x_id10                        4
6.12 d  x_vlin10                      6
6.12 d    x_vlix10                    6  0 overlay(x_vlin10:1)
6.12 d  x_noti10                      3
6.12 d  x_eanr10                     15
6.12 d    x_eanx10                   14  0 overlay(x_eanr10:1)
6.12 d  x_eank10                      3
6.12 d  x_piq110                      3
6.12 d  x_alt110                     15
6.12 d    x_altx10                    8  0 overlay(x_alt110:1)
6.12 d  x_alk110                      3
6.12 d  x_piq210                      3
6.12 d  x_alt210                     15
6.12 d  x_alk210                      3
      *
     d o_ed11          ds
     d  o_id11                        4
     d  o_txk111                      3
     d  o_txt111                     35
     d  o_txt211                     35
      *
     d o_ed12          ds
     d  o_id12                        4
     d  o_kva112                      3
     d  o_bkva12                     15
     d    o_ftbh12                   11  0 overlay(o_bkva12:1)
     d    o_ftbd12                    3  3 overlay(o_bkva12:13)
     d  o_benh12                      3
      *
     d o_ed13          ds
     d  o_id13                        4
     d  o_datq13                      3
     d  o_bdat13                      8
     d    o_bdax13                    8  0 overlay(o_bdat13:1)
     d  o_btim13                      2
     d  o_bdaf13                      3
      *
     d o_ed14          ds
     d  o_id14                        4
     d  o_link14                      3
     d  o_linb14                     15
     d    o_linh14                   11  0 overlay(o_linb14:1)
     d    o_lind14                    3  3 overlay(o_linb14:13)
      *
     d                 ds
     d o_ed15                        80
     d  o_id15                        4    overlay(o_ed15)
     d  o_ftkv15                      3    overlay(o_ed15:5)
     d  o_ftxt15                     70    overlay(o_ed15:8)
      *
     d o_ed16          ds
     d  o_id16                        4
     d  o_prkv16                      3
     d  o_pris16                     15
     d    o_prih16                   11  0 overlay(o_pris16:1)
     d    o_prid16                    3  3 overlay(o_pris16:13)
     d  o_prba16                      9
     d    o_prbx16                    9  0 overlay(o_prba16:1)
     d  o_pren16                      3
      *
     d o_ed17          ds
     d  o_id17                        4
     d  o_refq17                      3
     d  o_refb17                     35
     d  o_refl17                      6
     d    o_refx17                    6  0 overlay(o_refl17:1)
      *
     d o_ed18          ds
     d  o_id18                        4
     d  o_sakv18                      3
     d  o_saty18                      3
     d  o_sasa18                     17
     d    o_sash18                    2  0 overlay(o_sasa18:1)
6.3b d    o_sah118                    1  0 overlay(o_sasa18:1)
6.3b d    o_sass18                    1    overlay(o_sasa18:2)
6.3b d    o_sad118                    2  2 overlay(o_sasa18:3)
     d    o_sasd18                    2  2 overlay(o_sasa18:4)
      *
     d o_ed19          ds
     d  o_id19                        4
     d  o_ftkv19                      3
     d  o_ftty19                      3
     d  o_fttx19                     35
     d  o_ftse19                      3
     d  o_ftmk19                     15
     d    o_ftmh19                   11  0 overlay(o_ftmk19:1)
     d    o_ftmd19                    3  3 overlay(o_ftmk19:13)
     d  o_ftme19                      3
      *
     d o_ed20          ds
     d  o_id20                        4
     d  o_ftpr20                      8
     d    o_ftph20                    5  0 overlay(o_ftpr20:1)
     d    o_ftpd20                    2  2 overlay(o_ftpr20:7)
     d  o_ftbe20                     18
     d    o_ftbh20                   15  0 overlay(o_ftbe20:1)
     d    o_ftbd20                    2  2 overlay(o_ftbe20:17)
     d  o_ftsa20                     15
     d    o_ftsh20                   12  0 overlay(o_ftsa20:1)
     d    o_ftsd20                    2  2 overlay(o_ftsa20:14)
      *
     d o_ed21          ds
     d  o_id21                        4
     d  o_toty21                      3
     d  o_tobe21                     18
     d    o_tobh21                   15  0 overlay(o_tobe21:1)
     d    o_tobd21                    2  2 overlay(o_tobe21:17)
      *
      *  Definering av datastrukturer for melding ut
      *
      * ORDREBEKREFTELSE Hode
      *
     d d_obkh          ds
     d  d_oh_obnr                    35
     d  d_oh_olda                      d   datfmt(*iso)
     d  d_oh_llda                      d   datfmt(*iso)
     d  d_oh_sean                    13  0
     d  d_oh_bean                    13  0
     d  d_oh_fean                    13  0
     d  d_oh_kref                    30
     d  d_oh_aksp                     3
     d  d_oh_valk                     3
     d  d_oh_ttyp                     3
     d  d_oh_fmat                     3
     d  d_oh_lbet                     3
     d  d_oh_lste                    13  0
     d  d_oh_txt1                    70
     d  d_oh_txt2                    70
     d  d_oh_txt3                    70
6.3d d  d_oh_mfor                     6
      *
      * ORDREBEKREFTELSE Tilegg/fradragslinjer
      *
     d d_obkh2         ds
     d  d_oh_ftkv                     3
     d  d_oh_tsek                     3
     d  d_oh_ftty                     3
     d  d_oh_fttx                    30
     d  d_oh_tbel                    17  2
      *
      * Fritekst/hodelinje
      *
     d d_obkh3         ds
     d  d_oh_ftxq                     3
     d  d_oh_ftxt                    70
6.34  *
6.34  * Fritekst/varelinjer
6.34  *
6.34 d d_obkl15        ds
6.34 d  d_ol_ftxq                     3
6.34 d  d_ol_ftxt                    70
6.34 d  d_ol_lref                     6  0
      *
      * Ordrebekr.referanser/hode
      *
     d d_obkh4         ds
     d  d_oh_bstn                    35
     d  d_oh_proj                    35
     d  d_oh_kunr                    35
     d  d_oh_konr                    35
6.3k d  d_oh_lonr                    35
      *
      * Ordrebekr. partsinfo./hode
      *
     d d_obkh5         ds
     d  d_oh_part                     3
     d  d_oh_pean                    13  0
     d  d_oh_pnav                    30
     d  d_oh_pad1                    30
     d  d_oh_pad2                    30
     d  d_oh_pste                    30
     d  d_oh_porg                    20
      *
      * Ordrebekr. transport/lev. - hode
      *
     d d_obkh6         ds
     d  d_oh_trmo                     3
     d  d_oh_trna                    35
     d  d_oh_lebe                     3
      *
      * ORDREBEKREFTELSE Linje
      *
     d d_obkl          ds
     d  d_ol_llin                     6  0
     d  d_ol_laks                     3
6.12 d  d_ol_vean                    14  0
     d  d_ol_vnob                     8  0
     d  d_ol_vlev                    15
     d  d_ol_bkva                    15  3
     d  d_ol_benh                     3
     d  d_ol_lkva                    15  3
     d  d_ol_lenh                     3
     d  d_ol_llda                      d   datfmt(*iso)
     d  d_ol_pris                    15  3
     d  d_ol_penh                     3
     d  d_ol_prpr                     9  0
     d  d_ol_prkv                     3
     d  d_ol_epri                    15  3
     d  d_ol_evar                     8  0
     d  d_ol_linr                     6  0
     d  d_ol_linb                    15  3
     d  d_ol_mvap                     4  2
     d  d_ol_tkv1                     3
     d  d_ol_tty1                     3
     d  d_ol_ttx1                    35
     d  d_ol_tse1                     3
     d  d_ol_tme1                    11  3
     d  d_ol_ten1                     3
     d  d_ol_tpr1                     5  2
     d  d_ol_tbe1                    15  2
     d  d_ol_tkv2                     3
     d  d_ol_tty2                     3
     d  d_ol_ttx2                    35
     d  d_ol_tse2                     3
     d  d_ol_tme2                    11  3
     d  d_ol_ten2                     3
     d  d_ol_tpr2                     5  2
     d  d_ol_tbe2                    15  2
     d  d_ol_txt1                    35
     d  d_ol_txt2                    35
      *
      * ORDREBEKREFTELSE Ordretotaler
      *
     d d_obks          ds
     d  d_os_suml                    17  2
     d  d_os_ntot                    17  2
     d  d_os_mvag                    17  2
     d  d_os_mvat                    17  2
     d  d_os_btot                    17  2
     d  d_os_tilt                    17  2
     d  d_os_frat                    17  2
6.14  *
6.14  * Definering av konstanter
6.14  *
6.14 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
6.14 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
8.04  *
8.04  * Definering av eksterne prg
8.04  *
8.04   dcl-s co402_verdi1  char(1);
8.04   dcl-s co402_verdi2  char(2048);
8.04   DCL-PR CO402R EXTPGM('CO402R');
8.04     p_filgrp            char(3)     const;
8.04     p_firm              packed(3:0) const;
8.04     p_lager             packed(2:0) const;
8.04     p_nokkel            char(50)    const;
8.04     p_verdi1            char(1);
8.04     p_verdi2            char(2048);
8.04   END-PR;
      *
6.3l C/Exec SQL
6.3l C+  Set Option DatFmt=*ISO
6.3l C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hoved program styrerutine - les av innposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      lweopf
      *
     c                   dow       not %eof
     c                   eval      w_rect = %subst(obklin:1:4)
      *
     c*                  exsr      sjekk_brudd
      *
     c                   if        w_rect = 'ED01' and
     c                             b_sjek = *on
     c                   exsr      kryss_sjekk
     c                   endif
      *
     c                   if        b_ohod = *on
     c                   if        b_olin = *on
     c                   eval      limeld = d_obkh
     c                   exsr      skriv_hobkr
     c                   endif
     c                   endif
      *
     c                   select
     c                   when      w_rect = 'ED01'
     c                   exsr      ed01
     c                   when      w_rect = 'ED02'
     c                   exsr      ed02
     c                   when      w_rect = 'ED03'
     c                   exsr      ed03
     c                   when      w_rect = 'ED04'
     c                   exsr      ed04
     c                   when      w_rect = 'ED05'
     c                   exsr      ed05
     c                   when      w_rect = 'ED06'
     c                   exsr      ed06
     c                   when      w_rect = 'ED07'
     c                   exsr      ed07
     c                   when      w_rect = 'ED08'
     c                   exsr      ed08
     c                   when      w_rect = 'ED09'
     c                   exsr      ed09
     c                   when      w_rect = 'ED10'
     c                   exsr      ed10
     c                   when      w_rect = 'ED11'
     c                   exsr      ed11
     c                   when      w_rect = 'ED12'
     c                   exsr      ed12
     c                   when      w_rect = 'ED13'
     c                   exsr      ed13
     c                   when      w_rect = 'ED14'
     c                   exsr      ed14
     c                   when      w_rect = 'ED15'
     c                   exsr      ed15
     c                   when      w_rect = 'ED16'
     c                   exsr      ed16
     c                   when      w_rect = 'ED17'
     c                   exsr      ed17
     c                   when      w_rect = 'ED18'
     c                   exsr      ed18
     c                   when      w_rect = 'ED19'
     c                   exsr      ed19
     c                   when      w_rect = 'ED20'
     c                   exsr      ed20
     c                   when      w_rect = 'ED21'
     c                   exsr      ed21
     c                   endsl
      *
6.32 c     neste_lweo    tag
      *
     c                   read      lweopf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   if        b_ohod = *on
     c                   exsr      skriv_hobkr
     c                   endif
      *
     c                   if        b_olin = *on
     c                   exsr      skriv_lobkr
     c                   endif
      *
     c                   if        b_otot = *on
     c                   exsr      skriv_sobkr
     c                   endif
      *
     c                   if        b_sjek = *on
     c                   exsr      kryss_sjekk
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke brudd i melding.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_brudd   begsr
      *
     c                   if        w_srec = *blank
     c                   goto      end_brudd
     c                   endif
      *
     c                   if        w_rect < w_srec
      *
     c                   if        b_ohod = *on
     c                   exsr      skriv_hobkr
     c                   goto      end_brudd
     c                   endif
      *
     c                   if        b_olin = *on
     c                   exsr      skriv_lobkr
     c                   goto      end_brudd
     c                   endif
      *
     c                   if        b_otot = *on
     c                   exsr      skriv_sobkr
     c                   goto      end_brudd
     c                   endif
      *
     c                   endif
      *
     c     end_brudd     tag
     c                   eval      w_srec = w_rect
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED01  - Meldingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed01          begsr
      *
     c                   if        b_otot = *on
     c                   exsr      skriv_sobkr
     c                   endif
      *
     c                   exsr      hent_mnum
      *
     c                   exsr      null_hobkr
     c                   exsr      null_tobkr
     c                   exsr      null_sobkr
     c                   eval      o_ed01 = obklin
     c                   eval      d_oh_obnr = o_benr01
     c                   eval      d_oh_aksp = o_aksp01
6.3d c                   eval      d_oh_mfor = o_mfor01
7.03 c                   eval      b_nebf = *on
7.03 c                   if        d_oh_mfor = %trim('TBF01')
7.03 c                   eval      b_nebf = *off
7.03 c                   endif
      *
     c                   eval      b_ohod = *on
     c                   eval      w_hftx1 = *blank
     c                   eval      w_hftx2 = *blank
6.34 c                   eval      w_hftx3 = *blank
6.34 c                   eval      w_hftx4 = *blank
6.34 c                   eval      w_hftx5 = *blank
6.34 c                   eval      w_fhtx = 0
6.3c c                   eval      w_bes_inn = *blanks
      *
6.24 c                   eval      w_best = 0
6.24 c                   eval      w_besa = *blank
6.nu c                   eval      w_besn = '00000000'
6.26 c                   eval      linumm = 0
6.32 c                   eval      b_feil = *off
6.36 c                   eval      w_refn = o_benr01
6.3i c                   eval      limdat = w_date
6.3i c                   eval      limtid = w_time
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED02 - Datoinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed02          begsr
      *
     c                   eval      o_ed02 = obklin
      *
      * Setter i utgangspunktet meldingsdato og tid til "current"
      *
     c                   eval      limdat = w_date
     c                   eval      limtid = w_time
      *
      * Meldingsdato/tid
      *
     c                   if        o_dkva02 = '137'
     c                   if        o_mdan02 <> 0
     c     *ymd          move      o_mdan02      limdat
     c                   endif
      *
     c                   if        o_for102 = '203' and
     c                             o_mtin02 <> 0
     c                   movel     o_mtin02      w_time_6
     c     *hms          move      w_time_6      limtid
     c                   endif
     c                   endif
      *
      * Ønsket leveringsdato
      *
     c                   if        o_dkva02 = '2  ' and
     c                             o_mdan02 <> 0
     c     *ymd          move      o_mdan02      d_oh_olda
     c                   else
     c                   move      *loval        d_oh_olda
     c                   endif
      *
      * Lovet leveringsdato
      *
     c                   if        o_dkva02 = '69 ' and
     c                             o_mdan02 <> 0
     c     *ymd          move      o_mdan02      d_oh_llda
     c                   else
     c                   move      *loval        d_oh_llda
     c                   endif
      *
     c                   eval      b_ohod = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED03 - Fritekst på hodenivå  (Lagrer intil 3 fritekstlinjer)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed03          begsr
      *
     c                   eval      w_fhtx = w_fhtx + 1
      *
6.34 c*                  if        w_hftx1 <> ''
6.34 c*                  eval      w_hftx2 = obklin
6.34 c*                  else
6.34 c*                  eval      w_hftx1 = obklin
6.34 c*                  endif
6.34 c                   select
6.34 c                   when      w_fhtx = 1
6.34 c                   eval      w_hftx1 = obklin
6.34 c                   when      w_fhtx = 2
6.34 c                   eval      w_hftx2 = obklin
6.34 c                   when      w_fhtx = 3
6.34 c                   eval      w_hftx3 = obklin
6.34 c                   when      w_fhtx = 4
6.34 c                   eval      w_hftx4 = obklin
6.34 c                   when      w_fhtx = 5
6.34 c                   eval      w_hftx5 = obklin
6.34 c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED04 - Referanse til bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed04          begsr
      *
     c                   eval      o_ed04 = obklin
      *
      *  Referansekvalifikator (o_rfkv04)                 Lagres i felt
      *  --------------------------------------------------------------
      *  ON  = Bestillingsnr. (kjøpers)                   d_oh_bstn
      *  AEP = Prosjektnr.                                d_oh_proj
      *  CR  = Kundereferansenr.                          d_oh_kunr
      *  CT  = Kontraktsnr.                               d_oh_konr
6.3k  *  VN  = Leverandørens ordrenummer                  d_oh_lonr
      *
     c                   eval      b_ohod = *on
     c                   eval      b_oref = *on
6.1D c                   eval      w_bes_inn = *blanks
      *
     c                   if        o_rfkv04 = 'ON '
     c                   eval      w_bes_inn = o_refr04
     c                   eval      d_oh_bstn = o_refr04
     c                   exsr      finn_besnr
     c                   exsr      sjekk_best
     c                   eval      b_numm = *on
     c                   endif
      *
     c                   if        o_rfkv04 = 'CR '
     c                   eval      d_oh_kunr = o_refr04
     c                   endif
      *
     c                   if        o_rfkv04 = 'AEP'
     c                   eval      d_oh_proj = o_refr04
     c                   endif
      *
     c                   if        o_rfkv04 = 'CT '
     c                   eval      d_oh_konr = o_refr04
     c                   endif
6.3k  *
6.3k c                   if        o_rfkv04 = 'VN '
6.3k c                   eval      d_oh_lonr = o_refr04
6.3k c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED05 - Partsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed05          begsr
      *
      *  Partskvalifikator (o_pakv05)
      *  ----------------------------------
      *  BY  = Kjøper
      *  SU  = Selger, leverandør
      *  DP  = Mottager
      *  IV  = Fakturamottager
      *  OB  = Bestiller
      *  CA  = Transportør
      *  SF  = Hentested
      *  UD  = Sluttmottager
      *  PD  = Avdelingskontor hos kjøper
      *
6.19 c*                  if        b_oprt = *on
6.19 c*                  eval      limeld = d_obkh5
6.19 c*                  exsr      skriv_opart
6.19 c*                  endif
      *
     c                   if        b_numm = *off
     c                   eval      w_bes_inn = *blank
     c                   exsr      finn_besnr
     c                   exsr      sjekk_best
     c                   eval      b_numm = *on
     c                   endif
      *
     c                   eval      o_ed05 = obklin
     c     ' ':'0'       xlate     o_paid05      o_paid05
      *
     c                   if        o_pakv05 = 'SU '
     c                   eval      d_oh_sean = o_pean05
     c                   eval      w_eann = o_pean05
     c                   exsr      sjekk_levr
     c*                  eval      lildor = loldor
     c                   if        o_ctfu05 = 'PD '
     c                   eval      d_oh_kref = %trim(o_kont05)
     c                   endif
     c                   endif
      *
     c                   if        o_pakv05 = 'BY '
     c                   eval      d_oh_bean = o_pean05
     c                   endif
      *
     c                   if        o_pakv05 = 'IV '
     c                   eval      d_oh_fean = o_pean05
     c                   endif
      *
     c                   eval      d_oh_part = o_pakv05
     c                   eval      d_oh_pean = o_pean05
      *
     c                   eval      b_oprt = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED06 - Partsinformasjon (navn og adresse1)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed06          begsr
      *
     c                   eval      o_ed06 = obklin
      *
6.16 c*                  if        o_mtna06 <> *blank
6.31 c                   if        o_mtna06 <> *blank
6.31 c**                 if        o_mtna06 <> *blank and
6.19 c**                           d_oh_part = *blank and
6.31 c**                           d_oh_pean = 0
     c                   eval      d_oh_pnav = o_mtna06
     c                   eval      d_oh_pad1 = o_mta106
6.19 c                   if        d_oh_part = *blank
     c                   eval      d_oh_part = 'DP'
6.19 c                   endif
     c                   eval      b_oprt = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED07 - Partsinformasjon (adresse2 og postadresse)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed07          begsr
      *
     c                   eval      o_ed07 = obklin
      *
6.19 c                   if        o_mta207 <> *blank or
6.19 c                             o_mtpn07 <> *blank or
6.19 c                             o_mtps07 <> *blank
     c                   eval      d_oh_pad2 = o_mta207
     c                   eval      d_oh_pste = %trim(o_mtpn07) + ' ' +
     c                                         %trim(o_mtps07)
     c                   eval      b_oprt = *on
     c                   endif
      *
6.19 c                   if        b_oprt = *on
6.19 c                   eval      limeld = d_obkh5
6.19 c                   exsr      skriv_opart
6.19 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED08 - Valuta, transport, forsendelse, lev.bet og lev.sted
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed08          begsr
      *
     c                   eval      o_ed08 = obklin
     c                   eval      d_oh_valk = o_valk08
     c                   eval      d_oh_ttyp = o_ttyp08
     c                   eval      d_oh_fmat = o_fmat08
     c                   eval      d_oh_lbet = o_lbet08
      *
     c                   if        o_stkv08 = '9  '
     c                   eval      d_oh_lste = o_lean08
     c                   endif
      *
     c                   exsr      skriv_hobkr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED09 - Fradrags og tilleggsposter på hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed09          begsr
6.17  * Flyttet
6.17 c                   exsr      skriv_hobkr
      *
     c                   eval      o_ed09 = obklin
     c                   eval      d_oh_ftkv = o_ftkv09
6.16 c*                  eval      d_oh_tsek = o_tsek09
6.16 c                   eval      d_oh_tsek = *blank
     c                   eval      d_oh_ftty = o_ftty09
     c                   eval      d_oh_fttx = o_fttx09
     c     ' ':'0'       xlate     o_ftbe09      o_ftbe09
5.60 c                   eval      w_stri = o_ftbe09
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_oh_tbel = w_resu
      *
     c                   eval      limeld = d_obkh2
6.17 c                   exsr      skriv_tilfra
      *
6.17  * Skriv også tekstlinjer til hodet - sekvens er flyttet
6.17 c*                  if        w_hftx1 <> ''
6.17 c*                  eval      o_ed03 = w_hftx1
6.17 c*                  eval      d_oh_ftxq = o_txkv03
6.17 c*                  eval      d_oh_ftxt = o_frtx03
6.17 c*                  eval      limeld = d_obkh3
6.17 c*                  exsr      skriv_oftxt
6.17  *
6.17 c*                  if        w_hftx2 <> ''
6.17 c*                  eval      o_ed03 = w_hftx2
6.17 c*                  eval      d_oh_ftxq = o_txkv03
6.17 c*                  eval      d_oh_ftxt = o_frtx03
6.17 c*                  eval      limeld = d_obkh3
6.17 c*                  exsr      skriv_oftxt
6.17 c*                  endif
6.17  *
6.17 c*                  endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED10 - Varelinjer/vareidentifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed10          begsr
      *
      * Er forrige linje skrevet?
      *
     c                   if        b_olin = *on
     c                   exsr      skriv_lobkr
     c                   endif
      *
     c                   eval      w_pris = *zero
     c                   eval      w_rabb = *zero
      *
     c                   exsr      null_lobkr
6.12  * Sjekk hvor 'EN' identifikatoren ligger. Hvis 14 siffers GTIN kode
6.12  * må vi bruke en temporær ds, samt sette GTIN nummer til 0
6.3e  * GTIN begynner bestandig i samme posisjon, og fylles med blanke
6.3e c**                 eval      w_pos = %scan('EN':obklin)
6.3e c**                 if        w_pos = 30
6.3e c**                 eval      x_ed10 = obklin
6.3e c**                 exsr      oblin_flytt
6.3e c**                 else
     c                   eval      o_ed10 = obklin
6.3e c**                 endif
     c     ' ':'0'       xlate     o_vlin10      o_vlin10
     c                   eval      d_ol_llin = o_vlix10
     c                   eval      d_ol_laks = o_noti10
     c                   eval      w_laks = o_noti10
6.10 c                   if        w_laks = *blank
6.1B c                   eval      w_laks = 'U'
6.10 c                   endif
6.3e  * Finn ut av lengden på GTIN nummer.
6.3e c                   eval      w_gtin = *blanks
6.3e c                   eval      w_leng = %len(%trim(o_eanr10))
8.03 c**                 if        w_leng > 0
8.03 c                   if        w_leng = 0
8.03 c                   eval      w_gtin = '00000000000000'
8.03 c                   elseif    w_leng = 13
6.3e c                   move      o_eanx13      w_gtin
6.3e c                   elseif    w_leng = 12
6.3e c                   move      o_eanx12      w_gtin
8.02 c                   elseif    w_leng = 11
8.02 c                   move      o_eanx11      w_gtin
6.3e c                   elseif    w_leng = 8
6.3e c                   move      o_eanx08      w_gtin
6.3n c                   elseif    w_leng = 14
6.3n c                   move      o_eanx14      w_gtin
6.3e c                   else
6.3n c     ' ':'0'       xlate     o_eanr10      o_eanr10
6.3n c                   move      o_eanr10      w_gtin
6.3e c                   endif
8.03 c**                 endif
6.3e c     ' ':'0'       xlate     w_gtin        w_gtin
      *
6.3e c**   ' ':'0'       xlate     o_eanr10      o_eanr10
6.3e c**                 eval      d_ol_vean = o_eanx10
6.3e c                   eval      d_ol_vean = %int(w_gtin)
      *
     c     ' ':'0'       xlate     o_alt110      o_alt110
     c     c_digi        check     o_alt110                               80
     c                   if        *in80 = *on
     c                   eval      o_altx10 = 0
     c                   endif
     c                   eval      d_ol_vnob = o_altx10
      *
     c                   eval      d_ol_vlev = o_alt210
     c                   eval      b_olin = *on
      *
     c                   endsr
      *
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  * Flytter felter til riktig datastruktur
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.3e c**   oblin_flytt   begsr
6.12  *
6.3e c**                 eval      o_id10 = x_id10
6.3e c**                 eval      o_vlin10 = x_vlin10
6.3e c**                 eval      o_noti10 = x_noti10
6.3e c**                 eval      o_eanr10 = '0000000000000'
6.3e c**                 eval      o_eank10 = x_eank10
6.3e c**                 eval      o_piq110 = x_piq110
6.3e c**                 eval      o_alt110 = x_alt110
6.3e c**                 eval      o_alk110 = x_alk110
6.3e c**                 eval      o_piq210 = x_piq210
6.3e c**                 eval      o_alt210 = x_alt210
6.3e c**                 eval      o_alk210 = x_alk210
6.12  *
6.3e c**                 endsr
6.12  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED11 - Varelinjer/varetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed11          begsr
      *
     c                   eval      o_ed11 = obklin
     c                   eval      d_ol_txt1 = o_txt111
     c                   eval      d_ol_txt2 = o_txt211
     c                   eval      b_olin = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED12 - Varelinjer/kvantumsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed12          begsr
      *
     c                   eval      o_ed12 = obklin
     c     ' ':'0'       xlate     o_bkva12      o_bkva12
      *
     c                   if        o_kva112 = '21 '
5.60 c                   eval      w_stri = o_bkva12
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_ol_bkva = w_resu
     c                   eval      d_ol_benh = o_benh12
6.14 c     lo:up         xlate     d_ol_benh     d_ol_benh
     c                   endif
      *
     c                   if        o_kva112 = '113'
5.60 c                   eval      w_stri = o_bkva12
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_ol_lkva = w_resu
     c                   eval      d_ol_lenh = o_benh12
6.14 c     lo:up         xlate     d_ol_lenh     d_ol_lenh
     c                   endif
      *
     c                   exsr      sjk_enh_konv
      *
     c                   eval      w_anta = d_ol_lkva
     c                   eval      w_lenh = d_ol_lenh
     c                   eval      b_olin = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED13 - Varelinjer/datoinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed13          begsr
      *
     c                   eval      o_ed13 = obklin
     c                   if        o_datq13 = '69 ' and
     c                             o_bdat13 <> *blank
     c     *ymd          move      o_bdax13      d_ol_llda
     c                   exsr      oppd_hobkr
     c                   else
     c                   move      *loval        d_ol_llda
     c                   endif
      *
     c                   eval      b_olin = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED14 - Varelinjer/varelinjebeløp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed14          begsr
      *
     c                   eval      o_ed14 = obklin
     c     ' ':'0'       xlate     o_linb14      o_linb14
      *
     c                   if        o_link14 = '203'
5.60 c                   eval      w_stri = o_linb14
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_ol_linb = w_resu
     c                   endif
      *
     c                   eval      b_olin = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED15 - Varelinjer/fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed15          begsr
      *
     c                   eval      o_ed15 = obklin
6.39 c**                 eval      w_fhtx = w_fhtx + 1
6.39 c                   eval      w_fltx = w_fltx + 1
      *
6.34 c                   select
6.39 c                   when      w_fltx = 1
6.34 c                   eval      w_lftx1 = obklin
6.39 c                   when      w_fltx = 2
6.34 c                   eval      w_lftx2 = obklin
6.39 c                   when      w_fltx = 3
6.34 c                   eval      w_lftx3 = obklin
6.39 c                   when      w_fltx = 4
6.34 c                   eval      w_lftx4 = obklin
6.39 c                   when      w_fltx = 5
6.34 c                   eval      w_lftx5 = obklin
6.39 c                   when      w_fltx = 6
6.34 c                   eval      w_lftx6 = obklin
6.39 c                   when      w_fltx = 7
6.34 c                   eval      w_lftx7 = obklin
6.39 c                   when      w_fltx = 8
6.34 c                   eval      w_lftx8 = obklin
6.39 c                   when      w_fltx = 9
6.34 c                   eval      w_lftx9 = obklin
6.34 c                   endsl
      *
     c                   eval      b_olin = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED16 - Varelinjer/pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed16          begsr
      *
     c                   eval      o_ed16 = obklin
     c     ' ':'0'       xlate     o_pris16      o_pris16
5.60 c                   eval      w_stri = o_pris16
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_ol_pris = w_resu
     c                   eval      w_pris = d_ol_pris
     c                   eval      d_ol_penh = o_pren16
     c     ' ':'0'       xlate     o_prba16      o_prba16
5.60 c                   eval      w_stri = o_prba16
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_ol_prpr = w_resu
     c                   eval      d_ol_prkv = o_prkv16
      *
     c                   eval      b_olin = *on
6.3j  *
6.3j c                   eval      w_brut = w_pris * w_anta
6.3j c                   if        w_brut = 0
6.3j c                   eval      w_brut = d_ol_linb
6.3j c                   endif
6.3j c                   eval      w_rabt = 0
6.3j c                   eval      w_bru2 = 0
6.3j  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED17 - Varelinjer/kjøpers referanse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed17          begsr
      *
     c                   eval      o_ed17 = obklin
     c     ' ':'0'       xlate     o_refl17      o_refl17
6.33 c                   if        o_refq17 = 'ON '
     c                   eval      d_ol_linr = o_refx17
6.18 c                   if        o_refx17 > 9999
6.18 c                   eval      w_blin = 0
6.18 c                   else
     c                   eval      w_blin = o_refx17
6.18 c                   endif
6.33 c                   endif
      *
     c                   eval      b_olin = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED18 - Varelinjer/mva prosent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed18          begsr
      *
     c                   eval      o_ed18 = obklin
     c     ' ':'0'       xlate     o_sasa18      o_sasa18
     c                   if        o_saty18 = 'VAT'
6.3b c                   if        o_sass18 = '.'
6.3b c                   eval      d_ol_mvap = o_sah118 + o_sad118
6.3b c                   else
     c                   eval      d_ol_mvap = o_sash18 + o_sasd18
6.3b c                   endif
     c                   endif
      *
     c                   eval      b_olin = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED19 - Varelinjer/rabatter og gebyrer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed19          begsr
      *
     c                   eval      o_ed19 = obklin
     c     ' ':'0'       xlate     o_ftmk19      o_ftmk19
      *
     c                   if        o_ftkv19 = 'A  '
     c                   eval      d_ol_tkv1 = o_ftkv19
     c                   eval      d_ol_tty1 = o_ftty19
     c                   eval      d_ol_ttx1 = o_fttx19
     c                   eval      d_ol_tse1 = o_ftse19
5.60 c                   eval      w_stri = o_ftmk19
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_ol_tme1 = w_resu
     c                   eval      d_ol_ten1 = o_ftme19
     c                   endif
      *
     c                   if        o_ftkv19 = 'C  '
     c                   eval      d_ol_tkv2 = o_ftkv19
     c                   eval      d_ol_tty2 = o_ftty19
     c                   eval      d_ol_ttx2 = o_fttx19
     c                   eval      d_ol_tse2 = o_ftse19
5.60 c                   eval      w_stri = o_ftmk19
5.60 c                   exsr      desm_sjk
5.60 c                   eval(h)   d_ol_tme2 = w_resu
     c                   eval      d_ol_ten2 = o_ftme19
     c                   endif
      *
     c                   eval      b_olin = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED20 - Varelinjer/rabatt og gebyr, prosent og satser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed20          begsr
      *
5.60  * Ikke aktuellt med sign ifg kode mestegruppen LI783R (ED24).
     c                   eval      o_ed20 = obklin
     c     ' ':'0'       xlate     o_ftpr20      o_ftpr20
     c     ' ':'0'       xlate     o_ftbe20      o_ftbe20
     c     ' ':'0'       xlate     o_ftsa20      o_ftsa20
      *
     c                   if        o_ftkv19 = 'A  '
6.3j  *
6.3j c                   eval      w_ol_tpr1 = o_ftph20 + o_ftpd20
6.3j c                   eval      w_ol_tbe1 = o_ftbh20 + o_ftbd20
6.3j  *
6.3j c                   if        w_bru2 = 0
6.3j c                   eval      w_bru2 = w_brut
6.3j c                   endif
6.3j c                   if        w_ol_tbe1 <> 0
6.3j c                   eval      w_brut = w_brut - w_ol_tbe1
6.3j c                   eval      w_rabt = w_rabt + w_ol_tbe1
6.3j c                   else
6.3j c                   eval      w_rbel = (w_brut * w_ol_tpr1)/100
6.3j c                   eval      w_rabt = w_rabt + w_rbel
6.3j c                   eval      w_brut = w_brut - w_rbel
6.3j c                   endif
6.3j  *
6.3j  * Kalkuler rabattprosent, da dette vil bli en "snittprosent"
7.04 c                   if        w_bru2 = 0
7.04 c                   eval      w_bru2 = 1
7.04 c                   endif
6.3j c                   eval      d_ol_tpr1 = (w_rabt * 100)/w_bru2
6.3j  *
6.3j c**                 eval      d_ol_tpr1 = d_ol_tpr1 +
6.3j c**                                       o_ftph20 + o_ftpd20
6.3j c**                 eval      d_ol_tbe1 = d_ol_tbe1 +
6.3j c**                                       o_ftbh20 + o_ftbd20
6.3j  *
     c                   endif
      *
     c                   if        o_ftkv19 = 'C  '
     c                   eval      d_ol_tpr2 = d_ol_tpr2 +
     c                                         o_ftph20 + o_ftpd20
     c                   eval      d_ol_tbe2 = d_ol_tbe2 +
     c                                         o_ftbh20 + o_ftbd20
     c                   endif
      *
     c                   eval      w_rabb = 0
     c*                  if        d_ol_tty1 = 'DI '
     c                   if        d_ol_prkv = 'AAB'
     c                   if        d_ol_tpr1 <> 0
     c                   eval      w_rabb = (d_ol_pris * d_ol_tpr1) / 100
     c                   else
     c                   eval      w_rabb = d_ol_tbe1
     c                   endif
      *
     c                   if        w_rabb <> *zero
     c                   eval      w_pris = w_pris - w_rabb
     c                   endif
      *
     c                   endif
     c*                  endif
      *
     c                   exsr      sjekk_beslin
     c                   eval      d_ol_evar = w_evar
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED21 - Ordretotaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed21          begsr
      *
     c                   if        b_olin = *on
     c                   exsr      skriv_lobkr
     c                   endif
      *
     c                   eval      o_ed21 = obklin
     c     ' ':'0'       xlate     o_tobe21      o_tobe21
5.60 c                   eval      w_stri = o_tobe21
5.60 c                   exsr      desm_sjk
     c                   eval      b_otot = *on
      *
     c                   if        o_toty21 = '203'
5.60 c                   eval      d_os_suml = w_resu
     c                   endif
      *
     c                   if        o_toty21 = 'NET'
5.60 c                   eval      d_os_ntot = w_resu
     c                   endif
      *
     c                   if        o_toty21 = '125'
5.60 c                   eval      d_os_mvag = w_resu
     c                   endif
      *
     c                   if        o_toty21 = '150'
5.60 c                   eval      d_os_mvat = w_resu
     c                   endif
      *
     c                   if        o_toty21 = '86 '
5.60 c                   eval      d_os_btot = w_resu
     c                   endif
      *
     c                   if        o_toty21 = '259'
5.60 c                   eval      d_os_tilt = w_resu
     c                   endif
      *
     c                   if        o_toty21 = '260'
5.60 c                   eval      d_os_frat = w_resu
     c                   endif
      *
     c                   endsr
      *
     c
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_best    begsr
      *
     c                   eval      listat = *blank
     c                   eval      w_numm = 0
     c                   eval      linumm = 0
     c                   eval      w_suff = 0
     c                   eval      lisuff = 0
     c                   eval      lohel1_numm = w_best
     c     lohel1_key    setll     lohel1r
      *
     c     lohel1_key    reade     lohel1r
     c                   if        %eof
     c                   eval      listat = '1'
     c                   eval      loldor = 0
     c                   eval      lonumm = 0
     c                   eval      losuff = 0
     c                   eval      lootyp = *blank
     c                   eval      lototi = 0
     c                   endif
     c                   exsr      sjekk_otyp
      *
     c                   eval      w_numm = lonumm
6.29 c                   eval      s_numm = lonumm
     c                   eval      linumm = lonumm
     c                   eval      w_suff = losuff
6.29 c                   eval      s_suff = losuff
     c                   eval      lisuff = losuff
6.21  * Hvis vi finner bestilling, oppdater bestilling med EDI meldingsnummer
6.21 c                   eval      lohelu_numm = w_numm
6.21 c                   eval      lohelu_suff = w_suff
6.21 c     lohelu_key    chain     lohelur
6.21 c                   if        %found
6.21 c                   eval      lomnum = w_mnum
6.21 c                   update    lohelur
6.21 c                   endif
      *
     c     end_best      tag
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_levr    begsr
      *
     c                   if        loldor <> 0
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   eval      loldor = 0
     c                   else
     c                   if        rleanl <> w_eann
     c                   eval      listat = '2'
     c                   eval      loldor = 0
     c                   endif
     c                   endif
     c                   eval      lildor = loldor
      *
     c                   else
      *
     c                   eval      rlevl5_eanl = w_eann
     c     rlevl5_key    chain     rlevl5
     c                   if        not %found
     c                   eval      listat = '2'
     c                   else
     c                   eval      lildor = rllevr
     c                   endif
      *
6.3f c                   eval      s_ldor = lildor
      *
     c                   endif
      *
     c     end_levr      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke ordretype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_otyp    begsr
      *
     c                   if        lootyp <> *blank
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             vaosys <> 1
     c                   eval      listat = '4'
     c                   goto      end_otyp
     c                   endif
      *
     c                   if        vaoakk <> 1
     c                   eval      listat = '4'
     c                   endif
     c                   endif
      *
     c     end_otyp      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_beslin  begsr
      *
6.15 c*                  if        w_blin = 0
6.11  * Hvis nobbnr ikke finnes, forsøk å hente dette inn via GTIN nummer
6.15 c                   if        d_ol_vean <> *zero
6.15 c*                  if        d_ol_vean <> *zero and
6.15 c*                            d_ol_vnob = *zero
6.11 c                   exsr      hent_gtin
6.11 c                   endif
7.01  *
7.01  * Varsler feil så fort det ikke er linjeref på EDI meldinga. NB Kun ved NEB01
7.01  *
7.03 c*7.03              if        b_nebf = *on
7.01 c*7.03              if        w_blin = 0
7.01 c*7.03              eval      w_stat = '4'
7.01 c*7.03              goto      endr_beslin
7.01 c*7.03              endif
7.03 c*7.03              endif
      *
7.01  * Koden under merket 6.15, vil aldri bli kjørt hvis linjenummer er 0. Ref
7.01  * testen over. Fjerner ikke koden, siden den forsøker finne linje basert på
7.01  * GTIN/NOBB nummer, og muligens vil det være aktuelt for noen. Det vil
7.01  * imidlertid være avviken for måten Virke tenker på spesielt rundt
7.01  * delleverte linjer.
      *
6.15 c                   if        w_blin = 0
     c                   if        d_ol_vnob <> *zero
     c                   exsr      finn_besnob
     c                   endif
     c                   endif
      *
6.14 c*                  if        w_laks = '3'
6.14 c*                  eval      w_stat = '1'
6.14 c*                  goto      endr_beslin
6.14 c*                  endif
      *
     c                   if        w_laks = '7'
     c                   eval      w_stat = '2'
     c                   goto      endr_beslin
     c                   endif
      *
     c                   eval      lodtlr_numm = w_numm
     c                   eval      lodtlr_suff = w_suff
     c                   eval      lodtlr_line = w_blin
     c     lodtlr_key    chain     lodtlrr
     c                   if        not %found
7.02 c**                 eval      w_stat = '4'
7.02 c                   eval      w_stat = '3'
7.02 c**                 eval      w_blin = 0
     c                   goto      endr_beslin
     c                   endif
      *
6.14 c                   if        d_ol_vnob <> ldnobb
6.14 c                   eval      w_stat = '4'
6.14 c                   eval      w_blin = 0
6.14 c                   goto      endr_beslin
6.14 c                   endif
      *
     c                   eval      d_ol_epri = ldipny
     c                   movel     ldvare        w_evar
      *
     c                   if        w_anta <> ldanta
6.20 c                   if        w_lenh <> ldenh1
6.20 c                   exsr      omr_enhet
6.20 c                   else
     c                   eval      w_stat = '5'
6.20 c                   endif
6.20 c                   if        w_stat = '5'
     c                   goto      endr_beslin
6.20 c                   endif
     c                   endif
      *
     c                   if        w_lenh <> ldenh1
     c                   eval      w_stat = '6'
     c                   goto      endr_beslin
     c                   endif
      *
     c                   if        w_pris <> 0 and
     c                             w_pris <> ldipny
     c                   eval      w_stat = '7'
     c                   goto      endr_beslin
     c                   endif
6.1C  * Sjekk leveringstidspunkt
8.04 c                   if        u_804 = *off
6.1C c                   if        ldldat <> *loval and
6.1C c                             d_ol_llda <> *loval and
6.1C c                             d_ol_llda <> ldldat
6.1C c                   eval      w_stat = 'D'
6.1C c                   goto      endr_beslin
6.1C c                   endif
8.04 c                   endif
      *
     c                   goto      end_beslin
      *
     c     endr_beslin   tag
      *
     c     ledilu_key    chain     ledilur
     c                   if        %found and
     c                             listat = *blank
     c                   eval      listat = '3'
6.32 c                   if        b_feil = *on
6.32 c                   eval      listat = 'E'
6.32 c                   endif
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
      *
     c                   if        listat <> *blank
     c                   eval      ledilu_mlin = 100001
     c     ledilu_key    chain     ledilur
     c                   if        %found
     c                   if        listat = *blank
     c                   eval      listat = '3'
6.32 c                   if        b_feil = *on
6.32 c                   eval      listat = 'E'
6.32 c                   endif
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   endif
     c                   endif
      *
     c     end_beslin    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne bestillingslinje fra NOBB-nr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_besnob   begsr
      *
6.3l  * sjekker om logistikk-vare
6.3l c                   eval      w_lv_vare = *blank
6.3l c/exec sql
6.3l c+ select jvqvnr
6.3l c+ into   :w_lv_vare
6.3l c+ from   jvklpf
6.3l c+ where  jvquno = :d_ol_vnob and jvqfir = :w_firm
6.3l c+ fetch first 1 rows only
6.3l c/end-exec
6.3l  *
6.3l  * Finn varenummer via Nobb-nr
      *
6.3l c                   if        w_lv_vare <> *blank
6.3l c                   eval      lodtlv_vare = w_lv_vare
6.3l c                   else
     c                   eval      vvarl3_nobb = d_ol_vnob
     c     vvarl3_key    chain     vvarl3r
     c                   if        %found
     c                   eval      lodtlv_vare = vvvare
     c                   else
     c                   eval      jvarl3_nobb = d_ol_vnob
     c     jvarl3_key    chain     jvarl3r
     c                   if        %found
     c                   eval      lodtlv_vare = jvvare
     c                   else
     c                   goto      end_besnob
     c                   endif
     c                   endif
6.3l c                   endif
      *
      * Finn bestillingslinje på eget varenummer
      *
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
      *
     c                   dow       not %eof
      *
     c                   if        ldnumm = w_best
     c                   if        ldsuff = w_suff
     c                   move      ldline        w_blin
     c                   goto      end_besnob
     c                   endif
     c                   endif
      *
     c     lodtlv_key    reade     lodtlv
     c                   enddo
      *
     c     end_besnob    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive ordrebekreftelse hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_hobkr   begsr
      *
     c                   eval      lifirm = w_firm
     c                   eval      litype = 'OBKR'
     c                   eval      limnum = w_mnum
      *
     c                   eval      w_head_lin = w_head_lin + 1
     c                   eval      limlin = w_head_lin
      *
     c                   eval      b_sjek = *on
     c                   eval      b_ohod = *off
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur
     c                   if        not %found
     c                   eval      limeld = d_obkh
6.10 c                   time                    liodat
6.10 c                   time                    liotim
6.10 c                   eval      liousr = l_user
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   write     ledilur
     c                   endif
      *
     c                   if        b_oref = *on
     c                   eval      w_refn_lin = w_refn_lin + 1
     c                   eval      limlin = w_refn_lin
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur
     c                   if        not %found
     c                   eval      limeld = d_obkh4
6.10 c                   time                    liodat
6.10 c                   time                    liotim
6.10 c                   eval      liousr = l_user
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   write     ledilur
     c                   endif
     c                   eval      b_oref = *off
     c                   endif
      *
     c                   if        b_otra = *on
     c                   eval      w_trns_lin = w_trns_lin + 1
     c                   eval      limlin = w_trns_lin
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur
     c                   if        not %found
     c                   eval      limeld = d_obkh6
6.10 c                   time                    liodat
6.10 c                   time                    liotim
6.10 c                   eval      liousr = l_user
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   write     ledilur
     c                   endif
     c                   eval      b_otra = *off
     c                   clear                   d_obkh6
     c                   endif
      * Skriv tekstlinjer til heading
6.17 c                   if        w_hftx1 <> ''
6.17 c                   eval      o_ed03 = w_hftx1
6.17 c                   eval      d_oh_ftxq = o_txkv03
6.17 c                   eval      d_oh_ftxt = o_frtx03
6.17 c                   exsr      skriv_oftxt
6.17  *
6.17 c                   if        w_hftx2 <> ''
6.17 c                   eval      o_ed03 = w_hftx2
6.17 c                   eval      d_oh_ftxq = o_txkv03
6.17 c                   eval      d_oh_ftxt = o_frtx03
6.17 c                   exsr      skriv_oftxt
6.17 c                   endif
6.34  *
6.34 c                   if        w_hftx3 <> ''
6.34 c                   eval      o_ed03 = w_hftx3
6.34 c                   eval      d_oh_ftxq = o_txkv03
6.34 c                   eval      d_oh_ftxt = o_frtx03
6.34 c                   exsr      skriv_oftxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_hftx4 <> ''
6.34 c                   eval      o_ed03 = w_hftx4
6.34 c                   eval      d_oh_ftxq = o_txkv03
6.34 c                   eval      d_oh_ftxt = o_frtx03
6.34 c                   exsr      skriv_oftxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_hftx5 <> ''
6.34 c                   eval      o_ed03 = w_hftx5
6.34 c                   eval      d_oh_ftxq = o_txkv03
6.34 c                   eval      d_oh_ftxt = o_frtx03
6.34 c                   exsr      skriv_oftxt
6.34 c                   endif
6.17  *
6.17 c                   endif
      *
6.34 c                   eval      w_fhtx = 0
      *
     c                   exsr      null_hobkr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutiner for å skrive ordrebekr. fritekst/hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_oftxt   begsr
      *
     c                   eval      lifirm = w_firm
     c                   eval      litype = 'OBKR'
     c                   eval      limnum = w_mnum
      *
6.17 c*                  eval      w_htlg_lin = w_htlg_lin + 1
6.34 c**                 eval      w_refn_lin = w_refn_lin + 1
6.34 c                   eval      w_otxt_lin = w_otxt_lin + 1
6.17 c*                  eval      limlin = w_htlg_lin
6.34 c**                 eval      limlin = w_refn_lin
6.34 c                   eval      limlin = w_otxt_lin
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur
     c                   if        not %found
6.17 c                   eval      limeld = d_obkh3
6.10 c                   time                    liodat
6.10 c                   time                    liotim
6.10 c                   eval      liousr = l_user
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   write     ledilur
     c                   endif
      *
     c                   endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *  Subrutiner for å skrive ordrebekr. fritekst/hodenivå
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     skriv_vltxt   begsr
6.34  *
6.34 c                   eval      lifirm = w_firm
6.34 c                   eval      litype = 'OBKR'
6.34 c                   eval      limnum = w_mnum
6.34  *
6.34 c                   eval      w_oltx_lin = w_oltx_lin + 1
6.34 c                   eval      limlin = w_oltx_lin
6.34  *
6.34 c                   eval      d_ol_ftxq = o_ftkv15
6.34 c                   eval      d_ol_ftxt = o_ftxt15
6.34 c                   eval      d_ol_lref = w_olin_lin
6.34  *
6.34 c                   eval      ledilu_type = litype
6.34 c                   eval      ledilu_mnum = limnum
6.34 c                   eval      ledilu_mlin = limlin
6.34 c     ledilu_key    chain     ledilur
6.34 c                   if        not %found
6.34 c                   eval      limeld = d_obkl15
6.34 c                   time                    liodat
6.34 c                   time                    liotim
6.34 c                   eval      liousr = l_user
6.34 c                   time                    liedat
6.34 c                   time                    lietim
6.34 c                   eval      lieusr = l_user
6.34 c                   write     ledilur
6.34 c                   endif
6.34  *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutiner for å skrive ordrebekr. partsinfo./hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_opart   begsr
      *
     c                   eval      lifirm = w_firm
     c                   eval      litype = 'OBKR'
     c                   eval      limnum = w_mnum
      *
     c                   eval      w_part_lin = w_part_lin + 1
     c                   eval      limlin = w_part_lin
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur
     c                   if        not %found
6.10 c                   time                    liodat
6.10 c                   time                    liotim
6.10 c                   eval      liousr = l_user
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   write     ledilur
     c                   endif
      *
     c                   clear                   d_obkh5
     c                   eval      b_oprt = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive ordrebekreftelse linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_lobkr   begsr
      *
     c                   exsr      sjekk_beslin
      *
     c                   eval      lifirm = w_firm
     c                   eval      litype = 'OBKR'
     c                   eval      limeld = d_obkl
     c                   eval      limnum = w_mnum
      *
     c                   eval      w_olin_lin = w_olin_lin + 1
     c                   eval      limlin = w_olin_lin
      *
     c                   eval      liline = w_blin
     c                   eval      listat = w_stat
     c                   eval      b_olin = *off
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur
6.32 c                   if        b_feil = *on
6.32 c                   eval      listat = 'E'
6.32 c                   endif
     c                   if        not %found
6.10 c                   time                    liodat
6.10 c                   time                    liotim
6.10 c                   eval      liousr = l_user
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   write     ledilur
     c                   else
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
6.34  * Fyll på med fritekstlinjer tilknyttet varelinja
6.34 c                   if        w_lftx1 <> ''
6.34 c                   eval      o_ed15 = w_lftx1
6.34 c                   exsr      skriv_vltxt
6.34  *
6.34 c                   if        w_lftx2 <> ''
6.34 c                   eval      o_ed15 = w_lftx2
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_lftx3 <> ''
6.34 c                   eval      o_ed15 = w_lftx3
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_lftx4 <> ''
6.34 c                   eval      o_ed15 = w_lftx4
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_lftx5 <> ''
6.34 c                   eval      o_ed15 = w_lftx5
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_lftx6 <> ''
6.34 c                   eval      o_ed15 = w_lftx6
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_lftx7 <> ''
6.34 c                   eval      o_ed15 = w_lftx7
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_lftx8 <> ''
6.34 c                   eval      o_ed15 = w_lftx8
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   if        w_lftx9 <> ''
6.34 c                   eval      o_ed15 = w_lftx9
6.34 c                   exsr      skriv_vltxt
6.34 c                   endif
6.34  *
6.34 c                   endif
6.34  *
      *
     c                   exsr      null_lobkr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive ordretotaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_sobkr   begsr
      *
     c                   eval      lifirm = w_firm
     c                   eval      litype = 'OBKR'
     c                   eval      limnum = w_mnum
      *
     c                   eval      w_otot_lin = w_otot_lin + 1
     c                   eval      limlin = w_otot_lin
     c                   eval      liline = 0
      *
     c                   eval      limeld = d_obks
     c                   eval      b_sjek = *on
     c                   eval      b_otot = *off
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilur
     c                   if        not %found
6.10 c                   time                    liodat
6.10 c                   time                    liotim
6.10 c                   eval      liousr = l_user
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   write     ledilur
     c                   endif
     c                   unlock    ledilu
6.36  *  Oppdaterer logg
6.36 c                   eval      eloglu_faar = w_year
6.36 c                   eval      eloglu_type = 'OBKR'
6.36 c                   eval      eloglu_lean = w_eann
6.36 c                   eval      eloglu_refn = w_refn
6.36 c     eloglu_key    chain     eloglu
6.36 c                   eval      w_meld = %trim(obklin)
6.36 c                   if        %found
6.36 c                   eval      xlstat = *blanks
6.36 c                   eval      xleror = *blanks
6.36 c                   eval      xlovrf = 1
6.36 c                   eval      xlmnum = w_mnum
6.36 c                   update    eloglur
6.36 c                   endif
      *
     c                   exsr      kall_mktrl
6.3f  *
6.3f  * Sjekk om EDI meldingen har gått i feilkode
6.3f  *
6.3f c                   eval      ledil1_type = 'OBKR'
6.3f c                   eval      ledil1_mnum = w_mnum
6.3f c                   eval      ledil1_mlin = 100001
6.3f c     ledil1_key    chain     ledil1
6.3f c                   if        %found and
6.3f c                             listat <> *blanks
6.3f  *
6.3f  *  Oppdaterer logg
6.3f  *
6.3f c                   eval      lbedlu_type = 'OBKR'
6.3f c                   eval      lbedlu_aarr = w_year
6.3f c                   eval      lbedlu_numm = linumm
6.3f c                   eval      lbedlu_refn = w_refn
6.3f c     lbedlu_key    chain     lbedlu
6.3f c                   if        not %found
6.3f c                   eval      xbfirm = w_firm
6.3f c                   eval      xbtype = 'OBKR'
6.3f c                   eval      xbaarr = w_year
6.3f c                   eval      xbnumm = linumm
6.3f c                   eval      xbrefn = w_refn
6.3f c                   eval      xbstat = listat
6.3f c                   eval      xbldor = s_ldor
6.3f c                   eval      xbmeld = limnum
6.3f  * Utled teskt til statuskode
6.3f c                   select
6.3f c                   when      listat = '1'
6.3f c                   eval      xbtext = 'Feil i best.nr.'
6.3f c                   when      listat = '2'
6.3f c                   eval      xbtext = 'Sjekk lever.   '
6.3f c                   when      listat = '3'
6.3f c                   eval      xbtext = 'Avvik fra best.'
6.3f c                   when      listat = '4'
6.3f c                   eval      xbtext = 'Feil ordretype.'
6.3f c                   when      listat = '5'
6.3f c                   eval      xbtext = 'Avvik totalsum.'
6.3f c                   endsl
6.3f  *
6.3f c                   time                    xbodat
6.3f c                   time                    xbotim
6.3f c                   eval      xbousr = l_user
6.3f c                   time                    xbedat
6.3f c                   time                    xbetim
6.3f c                   eval      xbeusr = l_user
6.3f  *
6.3f c                   write     lbedlur
6.3f c                   endif
6.3f  *
6.3f c                   endif
6.3f  *
6.3f c                   eval      s_ldor = 0
6.36 c                   eval      w_refn = *blanks
      *
     c                   exsr      null_sobkr
      *
     c     end_sobkr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å kryssjekke EDI-meldinger mot bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kryss_sjekk   begsr
      *
6.29 c**                 if        linumm > 0
6.29 c                   if        linumm > 0  and
6.29 c                             s_numm > 0
      *
     c                   eval      ledilr_type = litype
6.35 c                   eval      ledilr_mnum = limnum
6.35 c                   eval      ledilr_mlin = 500000
6.35 c     ledilr_key    setll     ledilu
6.35 c     ledilr_key2   reade     ledilu
6.35 c                   dow       not %eof(ledilu)
6.35  *
6.35 c                   eval      b_besl = *on
6.35  *
6.35 c                   if        limlin >= 500000 and
6.35 c                             limlin <  549999
      *
6.35 c                   eval      lodtl1_numm = s_numm
6.35 c                   eval      lodtl1_suff = s_suff
6.35 c                   eval      lodtl1_line = 0
6.35 c     lodtl1_key2   setll     lodtl1
6.35 c     lodtl1_key    reade     lodtl1
      *
6.35 c                   dow       not %eof(lodtl1)
6.35 c                   if        ldline = liline
6.35 c                   eval      b_besl = *off
6.35 c                   leave
6.35 c                   endif
6.35 c     lodtl1_key    reade     lodtl1
6.35 c                   enddo
      *
6.35 c                   if        b_besl = *on
6.35 c**                 if        listat = *blank
6.35 c                   eval      listat = '3'
6.32 c                   if        b_feil = *on
6.32 c                   eval      listat = 'E'
6.32 c                   endif
6.35 c**                 endif
6.35 c                   time                    liedat
6.35 c                   time                    lietim
6.35 c                   eval      lieusr = l_user
6.35 c                   update    ledilur
6.35 c                   else
6.35 c                   unlock    ledilu
6.35 c                   endif
6.35  *
6.35 c                   else
6.35 c                   unlock    ledilu
6.35 c                   endif
6.35  *
6.35 c     ledilr_key2   reade     ledilu
6.35 c                   enddo
      *
6.1A  * Legg til linjer som ikke finnes fra før
6.1A  *
6.29 c                   eval      lodtl1_numm = s_numm
6.29 c                   eval      lodtl1_suff = s_suff
6.28 c                   eval      lodtl1_line = 0
6.28 c     lodtl1_key2   setll     lodtl1
6.1A c     lodtl1_key    reade     lodtl1
6.27 c                   dow       not %eof(lodtl1)
6.27 c                   eval      b_newl = *on
6.1A c                   eval      ledil1_type = 'OBKR'
6.3a c**                 eval      ledil1_mnum = limnum
6.3a c                   eval      ledil1_mnum = w_mnum
6.1A c                   eval      ledil1_mlin = 500001
6.1A c     ledil1_key    setll     ledil1
6.1A c     ledil1_key2   reade     ledil1
6.1A  *
6.27 c                   dow       not %eof(ledil1)
6.1A c                   if        limlin < 550000 and
6.1A c                             ldline = liline
6.27 c                   eval      b_newl = *off
6.27 c**                 leave
6.1A c                   endif
6.1A c     ledil1_key2   reade     ledil1
6.1A c                   enddo
6.27 c**                 if        %eof
6.27 c                   if        b_newl = *on
6.1A c                   eval      w_oexn_lin = w_oexn_lin + 1
6.1A c                   exsr      bygg_xedil
6.1A c                   endif
6.1A  *
6.1A c     lodtl1_key    reade     lodtl1
6.1A c                   enddo
6.1A  *
6.23 c                   endif
6.1A  *
6.29 c                   eval      s_numm = 0
6.29 c                   eval      s_suff = 0
6.1A  *
     c                   endsr
      *
6.1A  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.1A  *  Subrutine for å skrive ordrebekreftelse linje  (55 linje)
6.1A  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.1A  *
6.1A c     bygg_xedil    begsr
6.1A  *
6.1A c                   eval      ledilu_type = 'OBKR'
6.1A c                   eval      ledilu_mnum = limnum
6.1A c                   eval      ledilu_mlin = w_oexn_lin
6.1A c     ledilu_key    chain     ledilur
6.1A c                   if        not %found
6.1A c                   eval      lifirm = w_firm
6.1A c                   eval      limnum = ledilu_mnum
6.1A c                   eval      litype = 'OBKR'
6.1A c                   eval      limlin = w_oexn_lin
6.1A c                   eval      liline = ldline
6.1A c                   eval      limeld = *blank
6.1A  *
6.1A c                   clear                   d_obkl
6.1A c                   eval      d_ol_txt1 = ldtek1
6.1A c                   eval      d_ol_vean = ldeann
7.01 c**                 eval      d_ol_laks = 'U'
7.01 c                   eval      d_ol_laks = '1'
6.1A c                   eval      limeld = d_obkl
6.1A  *
6.30 c                   if        ldltyp = 'TX'
6.30 c                   eval      listat = *blanks
6.30 c                   else
6.1A c                   eval      listat = '8'
6.30 c                   endif
6.1A  *
6.32 c                   if        b_feil = *on
6.32 c                   eval      listat = 'E'
6.32 c                   endif
6.1A  *
6.1A c                   time                    liodat
6.1A c                   time                    liotim
6.1A c                   eval      liousr = l_user
6.1A c                   time                    liedat
6.1A c                   time                    lietim
6.1A c                   eval      lieusr = l_user
6.1A c                   write     ledilur
6.1A c                   else
6.32 c                   if        b_feil = *on
6.32 c                   eval      listat = 'E'
6.32 c                   endif
6.1A c                   time                    liedat
6.1A c                   time                    lietim
6.1A c                   eval      lieusr = l_user
6.1A c                   update    ledilur
6.1A c                   endif
6.1A  *
6.38 c                   eval      b_newl = *off
6.1A  *
6.1A c                   endsr
6.1A  *
6.17  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.17  *  Subrutiner for å skrive fradrag/tilleggsposter
6.17  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.17  *
6.17 c     skriv_tilfra  begsr
6.17  *
6.17 c                   eval      lifirm = w_firm
6.17 c                   eval      litype = 'OBKR'
6.17 c                   eval      limnum = w_mnum
6.17  *
6.17 c                   eval      w_htlg_lin = w_htlg_lin + 1
6.17 c                   eval      limlin = w_htlg_lin
6.17  *
6.17 c                   eval      ledilu_type = litype
6.17 c                   eval      ledilu_mnum = limnum
6.17 c                   eval      ledilu_mlin = limlin
6.17 c     ledilu_key    chain     ledilur
6.17 c                   if        not %found
6.17 c                   eval      limeld = d_obkh2
6.17 c                   time                    liodat
6.17 c                   time                    liotim
6.17 c                   eval      liousr = l_user
6.17 c                   time                    liedat
6.17 c                   time                    lietim
6.17 c                   eval      lieusr = l_user
6.17 c                   write     ledilur
6.17 c                   endif
6.17  *
6.17 c                   endsr
6.17  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille ordrebekreftelse hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_hobkr    begsr
      *
     c                   clear                   o_ed01
     c                   clear                   o_ed02
     c                   clear                   o_ed03
     c                   clear                   o_ed04
     c                   clear                   o_ed05
     c                   clear                   o_ed06
     c                   clear                   o_ed07
     c                   clear                   o_ed08
      *
     c                   clear                   d_obkh
6.13 c                   clear                   d_obkh2
6.13 c                   clear                   d_obkh3
6.13 c                   clear                   d_obkh4
6.13 c                   clear                   d_obkh5
6.13 c                   clear                   d_obkh6
      *
     c                   eval      w_atel = 0
     c                   eval      w_line = 1
6.3c c                   eval      w_bes_inn = *blanks
      *
      *  Initielle verdier linjenummer - se dokum. i toppen
      *
     c                   eval      w_head_lin = 100000
     c                   eval      w_part_lin = 200000
     c                   eval      w_refn_lin = 300000
6.34 c                   eval      w_otxt_lin = 350000
     c                   eval      w_trns_lin = 400000
     c                   eval      w_olin_lin = 500000
     c                   eval      w_oexn_lin = 550000
     c                   eval      w_htlg_lin = 600000
6.34 c                   eval      w_oltx_lin = 700000
     c                   eval      w_otot_lin = 900000
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille ordrebekreftelse tillegg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_tobkr    begsr
      *
     c                   clear                   o_ed09
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille ordrebekreftelse linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_lobkr    begsr
      *
     c                   clear                   o_ed10
     c                   clear                   o_ed11
     c                   clear                   o_ed12
     c                   clear                   o_ed13
     c                   clear                   o_ed14
     c                   clear                   o_ed15
     c                   clear                   o_ed16
     c                   clear                   o_ed17
     c                   clear                   o_ed18
     c                   clear                   o_ed19
     c                   clear                   o_ed20
      *
     c                   clear                   d_obkl
      *
     c                   eval      w_stat = *blank
     c                   eval      w_blin = *zero
6.34 c                   eval      w_lftx1 = *blanks
6.34 c                   eval      w_lftx2 = *blanks
6.34 c                   eval      w_lftx3 = *blanks
6.34 c                   eval      w_lftx4 = *blanks
6.34 c                   eval      w_lftx5 = *blanks
6.34 c                   eval      w_lftx6 = *blanks
6.34 c                   eval      w_lftx7 = *blanks
6.34 c                   eval      w_lftx8 = *blanks
6.34 c                   eval      w_lftx9 = *blanks
6.39 c                   eval      w_fltx = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille ordretotaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_sobkr    begsr
      *
     c                   clear                   o_ed21
      *
     c                   clear                   d_obks
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne bestillingsnr i en tekst-streng
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_besnr    begsr
      *
     c                   eval      w_best = 0
     c                   eval      w_besa = *blank
6.nu c                   eval      w_besn = '00000000'
      *
     c     c_char        check     w_bes_inn     w_pos1
      *
     c                   if        w_pos1 = 0
     c                   eval      w_best = 0
     c                   goto      end_besnr
     c                   endif
      *
6.nu c                   eval      w_bes_alf = %subst(w_bes_inn:w_pos1:8)
     c     c_digi        check     w_bes_alf     w_pos2
     c                   eval      w_pos2 = w_pos2 - 1
     c                   if        w_pos2 > 0
     c                   eval      w_bes_alf = %subst(w_bes_alf:1:w_pos2)
6.nu c                   eval      w_besa = %subst(w_besn:1:8 - w_pos2) +
     c                                      %trimr(w_bes_alf)
     c                   move      w_besa        w_best
     c                   else
     c                   move      w_bes_alf     w_best
     c                   endif
      *
     c     end_besnr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = 'LEDIML'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_mnum = 0
6.nu c                   eval      w_num8 = 0
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_num8
6.nu  *
6.nu c                   eval      w_mnum = w_num8
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente/oppdatere EDI-meldingsnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_mnum     begsr
      *
     c                   eval      w_fell = 'LEDIML'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_mnum = 0
6.nu c                   eval      w_num8 = 0
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_num8
6.nu  *
6.nu c                   eval      w_mnum = w_num8
      *
     c     end_mnum      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å kalle ekstern meldingskontroll / OBKR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kall_mktrl    begsr
      *
     c                   eval      w_retn = *zero
      *
     c                   call      'LI716R'
     c                   parm                    w_firm
     c                   parm                    litype
     c                   parm                    w_mnum
     c                   parm                    w_retn
      *
     c     end_kall      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av mulig enhetsbytte
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_enh_konv  begsr
      *
      *
     c     end_sjk_enh   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å oppdatering av lev.dato på hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_hobkr    begsr
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = 100001
     c     ledilu_key    chain     ledilur
     c                   if        not %found
7.05 c                   if        d_oh_llda = *loval or
7.05 c                             d_ol_llda < d_oh_llda
     c                   eval      d_oh_llda = d_ol_llda
     c                   endif
     c                   else
     c                   eval      d_obkh = limeld
7.05 c                   if        d_oh_llda = *loval or
7.05 c                             d_ol_llda < d_oh_llda
     c                   eval      d_oh_llda = d_ol_llda
     c                   eval      limeld = d_obkh
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   endif
      *
     c     end_ohobkr    endsr
      *
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .    *  Subrutine for sjekk/uttrekk av desimaltall fra streng
 .    * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 .    *
 .   c     desm_sjk      begsr
 .    *
 .   c                   eval      w_blkp = *zero
 .   c                   eval      w_dsmp = *zero
 .   c                   eval      w_resu = *zero
 .   c                   eval      w_hltx  = *blank
 .   c                   eval      w_dstx  = *blank
 .   c                   eval      w_sign  = *blank
 .    *
 .   c                   eval      w_sign = %subst(w_stri:1:1)
 .   c                   eval      w_stri = %xlate('-':'0':w_stri)
 .    *
 .   c                   if        w_stri = *blank
 .   c                   eval      w_resu = 0
 .   c                   leavesr
 .   c                   endif
 .    *
 .   c                   if        %check(c_diga : w_stri) > 0
 .   c                   eval      w_resu = 0
 .   c                   leavesr
 .   c                   endif
 .    *
 .   c                   eval      w_dsmp = %scan('.':w_stri)
 .   c                   if        w_dsmp = 0
 .   c                   eval      w_dsmp = %scan(',':w_stri)
 .   c                   endif
 .    *
 .   c                   eval      w_blkp = %scan(' ':w_stri)
 .    *
 .   c                   if        w_dsmp > 1
 .   c                   evalr     w_hltx = %subst(w_stri:1:w_dsmp - 1)
 .   c                   endif
 .    *
 .   c                   if        w_dsmp = 1
 .   c                   eval      w_hltn  = *zero
 .   c                   endif
 .    *
 .   c                   if        w_dsmp = 0
 .   c                   evalr     w_hltx = %subst(w_stri:1:w_blkp - 1)
 .   c                   eval      w_dstn  = *zero
 .   c                   endif
 .    *
 .   c                   if        w_dsmp > 0
 .   c                   if        (w_blkp - w_dsmp) < 2
 .   c                   eval      w_dstn  = *zero
 .   c                   else
 .   c                   eval      w_ades = (w_blkp - w_dsmp) - 1
 .   c*                  evalr     w_dstx = %subst(w_stri:w_dsmp + 1:w_ades)
 .   c                   eval      w_dstx = %subst(w_stri:w_dsmp + 1:w_ades)
 .   c                   endif
 .   c                   endif
 .    *
 .   c                   eval      w_hltx = %xlate(' ':'0':w_hltx)
 .   c                   eval      w_dstx = %xlate(' ':'0':w_dstx)
 .    *
 .   c                   if        %check(c_digi : w_hltx) > 0 or
 .   c                             %check(c_digi : w_dstx) > 0
 .   c                   eval      w_resu = 0
 .   c                   leavesr
 .   c                   endif
 .    *
 .   c                   move      w_hltx        w_hltn
 .   c                   move      w_dstx        w_dstn
 .    *
 .   c                   eval(h)   w_resu = w_hltn  + w_dstn
 .   c                   if        w_sign = '-'
 .   c                   eval      w_resu = w_resu * -1
 .   c                   endif
 .    *
5.60 c     end_dsjk      endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Subrutine for å hente nobbnummer via GTIN koden.
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11 c     hent_gtin     begsr
6.11  *
6.11 c                   move      d_ol_vean     p_eann
6.11 c                   eval      p_stat = *blank
6.11 c                   eval      p_enhe = *blank
6.11 c                   eval      p_nobb = *zero
6.11 c                   eval      p_vare = *blank
6.11  *
6.15 c*                  call      'VE715R'
6.15 c                   call      'VE717R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_eann
6.11 c                   parm                    p_vare
6.11 c                   parm                    p_enhe
6.11 c                   parm                    p_nobb
6.11 c                   parm                    p_stat
6.11  *
6.11 c                   if        p_stat = '1'
6.11  * Sjekk at det er nobbnummer som kommer i retur
6.11 c                   if        p_nobb <> 0
6.11 c                   eval      d_ol_vnob = p_nobb
6.11 c                   endif
6.15  * Overstyrer også eventuelle enheter på LEDI, hvis det er avvikende
6.15 c                   if        %trim(p_enhe) <> *blanks
6.15 c     lo:up         xlate     p_enhe        p_enhe
6.15 c                   if        %trim(p_enhe) <> w_lenh
6.15 c                   eval      d_ol_lenh = p_enhe
6.15 c                   eval      w_lenh = p_enhe
6.15 c                   endif
6.15 c                   endif
6.11  *
6.11 c                   endif
6.11  *
6.11 c     end_gtin      endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for omregning mellom enheter
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     omr_enhet     begsr
6.20  *
6.20  * Regn om til bestilt enhet hvis det avvikende enheter
6.20  *
6.20 c                   eval      p_antu = 0
6.20 c                   call      'VA770R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    ldvare
6.20 c                   parm                    w_lenh
6.20 c                   parm                    w_anta
6.20 c                   parm                    ldenh1
6.20 c                   parm                    p_antu
6.20  *
6.20 c                   if        p_antu <> ldanta
6.20 c                   eval      listat = '5'
6.20 c                   endif
6.20  *
6.20 c     end_enhet     endsr
6.20  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * ERROR-rutine
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     *pssr         begsr
6.32  *
6.32  *  Prøv å logge hvilken faktura det er problemer med
6.32  *
6.32 c                   eval      eloglu_faar = w_year
6.32 c                   eval      eloglu_type = 'OBKR'
6.32 c                   eval      eloglu_lean = w_eann
6.32 c                   eval      eloglu_refn = w_refn
6.32 c     eloglu_key    chain     eloglu
6.32 c                   eval      w_meld = %trim(obklin)
6.32 c                   if        %found
6.32 c                   eval      xlstat = '20'
6.32 c                   eval      xleror = 'ER'
6.32 c                   eval      xlmerk = w_meld
6.32 c                   update    eloglur
6.32 c                   else
6.32 c                   eval      xlfirm = w_firm
6.32 c                   eval      xlfaar = w_year
6.32 c                   eval      xltype = w_type
6.32 c                   eval      xllean = w_eann
6.32 c                   eval      xlrefn = w_refn
6.32 c                   eval      xllevn = lildor
6.32 c                   eval      xlmda1 = w_udat
6.32 c                   eval      xlmti1 = w_utim
6.32 c                   eval      xlmott = 1
6.32 c                   eval      xlspli = 0
6.32 c                   eval      xlovrf = 0
6.32 c                   eval      xlstat = '20'
6.32 c                   eval      xleror = 'ER'
6.32 c                   eval      xlmerk = w_meld
6.32 c                   time                    xlodat
6.32 c                   time                    xlotim
6.32 c                   eval      xlousr = l_user
6.32 c                   write     eloglur
6.32 c                   endif
6.32  *
6.32 c                   eval      b_feil = *on
6.32 c                   eval      p_eror = 'E'
6.32 c                   goto      neste_lweo
6.32  *
6.32 c                   endsr
6.32  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.32  *
6.32  * Parameterliste
6.32  *
6.32 c     *entry        plist
6.32 c                   parm                    p_eror
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
6.21  *
6.21  *  Key for les av BESTILLING-HODE
6.21  *
6.21 c     lohelu_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    lohelu_numm
6.21 c                   kfld                    lohelu_suff
      *
      *  Key for les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
6.28  *
6.28  *  Key for les BESTILLING-LINJE
6.28  *
6.28 c     lodtl1_key2   klist
6.28 c                   kfld                    w_firm
6.28 c                   kfld                    lodtl1_numm
6.28 c                   kfld                    lodtl1_suff
6.28 c                   kfld                    lodtl1_line
      *
      *  Key for direkte les BESTILLING-LINJE
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      *  Key for les BESTILLING-LINJE på varenummer
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av leverandør på EAN - lokasjonsnummer
      *
     c     rlevl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl5_eanl
      *
      * Key for oppdatering av EDI-mottaksfil
      *
     c     ledilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilu_type
     c                   kfld                    ledilu_mnum
     c                   kfld                    ledilu_mlin
6.1A  *
6.1A  * Key for oppdatering av EDI-mottaksfil
6.1A  *
6.1A c     ledil1_key    klist
6.1A c                   kfld                    w_firm
6.1A c                   kfld                    ledil1_type
6.1A c                   kfld                    ledil1_mnum
6.1A c                   kfld                    ledil1_mlin
6.1A  *
6.1A  * Key for oppdatering av EDI-mottaksfil
6.1A  *
6.1A c     ledil1_key2   klist
6.1A c                   kfld                    w_firm
6.1A c                   kfld                    ledil1_type
6.1A c                   kfld                    ledil1_mnum
      *
      * Key for les av EDI-mottaksfil
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
6.35  *
6.35  * Key for oppdatering av EDI-mottaksfil
6.35  *
6.35 c     ledilr_key2   klist
6.35 c                   kfld                    w_firm
6.35 c                   kfld                    ledilr_type
6.35 c                   kfld                    ledilr_mnum
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for les av ordretype-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les av vare på nobb-nummer (Vareadm.)
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
6.32  *
6.32  *  Key for oppdatering av LOGFIL
6.32  *
6.32 c     eloglu_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    eloglu_faar
6.32 c                   kfld                    eloglu_type
6.32 c                   kfld                    eloglu_lean
6.32 c                   kfld                    eloglu_refn
6.3f  *
6.3f  *  Key for oppdatering av loggfil - status
6.3f  *
6.3f c     lbedlu_key    klist
6.3f c                   kfld                    w_firm
6.3f c                   kfld                    lbedlu_type
6.3f c                   kfld                    lbedlu_aarr
6.3f c                   kfld                    lbedlu_numm
6.3f c                   kfld                    lbedlu_refn
      *
      *  Legger inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      *  Legger inn "current" dato og tid
      *
     c                   time                    w_date
     c                   time                    w_time
6.32 c                   eval      p_eror = *blanks
6.37 c                   eval      w_year = %subdt(w_date:*years)
8.04  *
8.04  * Endring 8.04 - Switch for styring av test på leveringsdato
8.04  *
8.04   CallP CO402R(l_filg:w_firm:0:'SJEKK_LEVERINGSDATO':
8.04                    co402_verdi1:co402_verdi2);
8.04   if co402_verdi1 = '1';
8.04     u_804 = *on;
8.04   endif;
8.04  *
     c                   endsr
