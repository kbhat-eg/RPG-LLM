     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AN012R
      * Beskrivelse . . : Generering av nattrutine-program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R1.08 151097 AMD  Tekniske og brukermessige forbedringer
5.31  * R5.31 210305 AMD  Lagt inn kall til program som setter opp
5.31  *                   biblioteksliste
5.51  * R5.51 500107 AMD  Endret natjobben slik at også QBATCH starter
      *                   opp igjen sammen med QINTER
6.11  * R6.10 261112 AMD  Lagt inn nytt valg for backup til savefile
6.11  *                   uten å kjøre ut på tape
6.21  * R6.21 171013 AMD  Lagt til ny kode hvor savefile tas ut på tape
6.22  * R6.22 051213 AMD  Utvidet slik at 3 forskjellige nattjobber kan
6.22  *                   kjøres. Dvs. Splitte ANBACK til ANBAC1-2-3
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fqcllesrc  o  a f  096        disk
      *
      *
      * Definering av tabeller
      *
6.22 d rec             s             80    dim(172) ctdata perrcd(1)            Programlinjer
      *
      * Definering av datastrukturer
      *
     d                 ds
     d d_dato                         6  0
     d  d_aaar                        2  0 overlay(d_dato)
     d  d_mmnd                        2  0 overlay(d_dato:3)
     d  d_ddag                        2  0 overlay(d_dato:5)
      *
      * Definering av local data area
      *
     d                 ds
     d d_valg                       128
     d  d_time                        6  0 overlay(d_valg)
     d  d_dens                       11    overlay(d_valg:7)
     d  d_rorg                        1    overlay(d_valg:18)
     d  d_back                        1    overlay(d_valg:19)
     d  d_ssbs                        1    overlay(d_valg:20)
     d  d_prc1                        1    overlay(d_valg:21)
     d  d_prc2                        1    overlay(d_valg:22)
     d  d_genp                        1    overlay(d_valg:23)
     d  d_savf                        1    overlay(d_valg:24)
     d  d_smap                        1    overlay(d_valg:25)
     d  d_natt                        1    overlay(d_valg:26)
     d  d_strt                        1    overlay(d_valg:27)
     d  d_slut                        1    overlay(d_valg:28)
      *
     d  d_pro1                        8    overlay(d_valg:31)
     d  d_bib1                       10    overlay(d_valg:39)
     d  d_sys1                        2    overlay(d_valg:49)
     d  d_pro2                        8    overlay(d_valg:51)
     d  d_bib2                       10    overlay(d_valg:59)
     d  d_sys2                        2    overlay(d_valg:69)
      *
      * Definering av parametere
      *
     d p_valg          s            128
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d n               s              3  0
     d w_dato          s              6  0
     d w_recc          s             84
     d w_tell          s              6  0
      *
      *
      *
      * --------------------------------
      * Legg ut records til Cl-program
      * --------------------------------
      *
      * Legger ut første del av CL-programmet
      *
5.31 c                   for       n = 1 to 7
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
5.31  *
5.31 c                   for       n = 119 to 122
5.31 c                   add       100           w_tell
5.31 c                   movel     rec(n)        w_recc
5.31 c                   except    xrecut
5.31 c                   endfor
5.31  *
5.31 c                   for       n = 8 to 14
5.31 c                   add       100           w_tell
5.31 c                   movel     rec(n)        w_recc
5.31 c                   except    xrecut
5.31 c                   endfor
      *
      * Ta ned SUBSYSTEMER før nattrutiner starter
      *
     c                   if        d_ssbs = '1'
     c                   for       n = 15 to 32
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
      *
      * Ta opp igjen QINTER med det samme
      *
     c                   if        d_ssbs = '1'
     c                   for       n = 33 to 39
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
5.51  *
5.51  * Tar nå også opp igjen QBATCH med det samme
5.51  *
5.51 c                   if        d_ssbs = '1'
5.51 c                   for       n = 93 to 99
5.51 c                   add       100           w_tell
5.51 c                   movel     rec(n)        w_recc
5.51 c                   except    xrecut
5.51 c                   endfor
5.51 c                   endif
      *
      * Legger ut records dersom eget startprogram skal kjøres
      *
     c                   if        d_strt = '1'
     c                   for       n = 40 to 43
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
      *
      * Legger ut records dersom kontroll av tape skal kjøres
6.11  * men ikke dersom valg 2 er foretatt, backup til savefile
6.11  * uten å kjøre dette ut på tape etterpå.
      *
6.11 c                   if        d_savf <> '2'
     c                   if        d_back = '1' or
     c                             d_back = '2' or
     c                             d_prc1 = '1' or
     c                             d_prc2 = '1' or
     c                             d_smap = '1'
     c                   for       n = 44 to 54
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
6.11 c                   endif
      *
      * Legger ut records dersom BACKUP skal kjøres
      *
     c                   if        d_back = '1'
6.22 c                   for       n = 55 to 59
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
6.22  *
6.22  * Styrer om det skal kjøres ANBAC1 - 2 - 3
6.22  *
6.22 c                   if        d_back = '1'
6.22  *
6.22 c                   if        d_natt = '1'
6.22 c                   add       100           w_tell
6.22 c                   movel     rec(60)       w_recc
6.22 c                   except    xrecut
6.22 c                   endif
6.22  *
6.22 c                   if        d_natt = '2'
6.22 c                   add       100           w_tell
6.22 c                   movel     rec(171)      w_recc
6.22 c                   except    xrecut
6.22 c                   endif
6.22  *
6.22 c                   if        d_natt = '3'
6.22 c                   add       100           w_tell
6.22 c                   movel     rec(172)      w_recc
6.22 c                   except    xrecut
6.22 c                   endif
6.22  *
6.22 c                   endif
      *
      * Legger ut records dersom alle brukerbibliotek skal saves
      *
     c                   if        d_back = '2'
     c                   for       n = 61 to 64
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
      *
      * Legger ut records dersom EKSTRA-JOBBER skal kjøres
      *
     c                   if        d_prc1 = '1'
     c                   for       n = 65 to 69
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   if        d_natt = '1'
     c                   add       100           w_tell
     c                   movel     rec(70)       w_recc
     c                   except    xrecut
     c                   endif
     c                   if        d_natt = '2'
     c                   add       100           w_tell
     c                   movel     rec(71)       w_recc
     c                   except    xrecut
     c                   endif
     c                   if        d_natt = '3'
     c                   add       100           w_tell
     c                   movel     rec(72)       w_recc
     c                   except    xrecut
     c                   endif
     c                   endif
      *
      * Legger ut records dersom EKSTRA-JOBBER fra andre system
      *
     c                   if        d_prc2 = '1'
     c                   for       n = 73 to 77
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   if        d_natt = '1'
     c                   add       100           w_tell
     c                   movel     rec(78)       w_recc
     c                   except    xrecut
     c                   endif
     c                   if        d_natt = '2'
     c                   add       100           w_tell
     c                   movel     rec(79)       w_recc
     c                   except    xrecut
     c                   endif
     c                   if        d_natt = '3'
     c                   add       100           w_tell
     c                   movel     rec(80)       w_recc
     c                   except    xrecut
     c                   endif
     c                   endif
      *
      * Legger ut records dersom alle mapper skal saves
      *
     c                   if        d_smap = '1'
     c                   for       n = 81 to 84
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
6.21  *
6.21  * Legger ut records for kopiering av savefil til tape
6.21  *
6.21 c                   if        d_savf <> '2'
6.21 c                   for       n = 167 to 170
6.21 c                   add       100           w_tell
6.21 c                   movel     rec(n)        w_recc
6.21 c                   except    xrecut
6.21 c                   endfor
6.21 c                   endif
      *
      * Legger ut records dersom kontroll av tape skal kjøres
6.11  * men ikke dersom valg 2 er foretatt, backup til savefile
6.11  * uten å kjøre dette ut på tape etterpå.
      *
6.11 c                   if        d_savf <> '2'
     c                   if        d_back = '1' or
     c                             d_back = '2' or
     c                             d_prc1 = '1' or
     c                             d_prc2 = '1' or
     c                             d_smap = '1'
     c                   for       n = 85 to 86
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
6.11 c                   endif
      *
      * Legger ut records dersom REORGANISERING skal kjøres
      *
     c                   if        d_rorg = '1'
     c                   for       n = 87 to 92
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
      *
      * Legger ut records dersom eget sluttprogram skal kjøres
      *
     c                   if        d_slut = '1'
     c                   for       n = 100 to 103
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
      *
      * Legger ut records dersom DSPTAP-kommando skal kjøres
6.11  * men ikke dersom valg 2 er foretatt, backup til savefile
6.11  * uten å kjøre dette ut på tape etterpå.
      *
6.11 c                   if        d_savf <> '2'
     c                   if        d_back = '1' or
     c                             d_back = '2' or
     c                             d_prc1 = '1' or
     c                             d_prc2 = '1' or
     c                             d_smap = '1'
     c                   for       n = 104 to 109
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
     c                   endif
6.11 c                   endif
      *
      * Bekrefte normal avslutning på nattjobb
      *
     c                   for       n = 110 to 114
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
6.11  *
6.11  * Legger ut records dersom savefile (felles)
6.11  *
6.11 c                   if        d_savf = '2'
6.11 c                   for       n = 123 to 130
6.11 c                   add       100           w_tell
6.11 c                   movel     rec(n)        w_recc
6.11 c                   except    xrecut
6.11 c                   endfor
6.11 c                   endif
6.11  *
6.11  * Legger ut records dersom savefile og nattjobb 1
6.11  *
6.11 c                   if        d_savf = '2'
6.11 c                   if        d_natt = '1'
6.11 c                   for       n = 131 to 139
6.11 c                   add       100           w_tell
6.11 c                   movel     rec(n)        w_recc
6.11 c                   except    xrecut
6.11 c                   endfor
6.11 c                   endif
6.11 c                   endif
6.11  *
6.11  * Legger ut records dersom savefile og nattjobb 2
6.11  *
6.11 c                   if        d_savf = '2'
6.11 c                   if        d_natt = '2'
6.11 c                   for       n = 141 to 149
6.11 c                   add       100           w_tell
6.11 c                   movel     rec(n)        w_recc
6.11 c                   except    xrecut
6.11 c                   endfor
6.11 c                   endif
6.11 c                   endif
6.11  *
6.11  * Legger ut records dersom savefile og nattjobb 3
6.11  *
6.11 c                   if        d_savf = '2'
6.11 c                   if        d_natt = '3'
6.11 c                   for       n = 151 to 159
6.11 c                   add       100           w_tell
6.11 c                   movel     rec(n)        w_recc
6.11 c                   except    xrecut
6.11 c                   endfor
6.11 c                   endif
6.11 c                   endif
6.11  *
6.11  * Kvitterer ut at rename er foretatt
6.11  *
6.11 c                   if        d_savf = '2'
6.11 c                   if        d_natt = '1'
6.11 c                   for       n = 161 to 162
6.11 c                   add       100           w_tell
6.11 c                   movel     rec(n)        w_recc
6.11 c                   except    xrecut
6.11 c                   endfor
6.11 c                   endif
6.11 c                   if        d_natt = '2'
6.11 c                   for       n = 163 to 164
6.11 c                   add       100           w_tell
6.11 c                   movel     rec(n)        w_recc
6.11 c                   except    xrecut
6.11 c                   endfor
6.11 c                   endif
6.11 c                   if        d_natt = '3'
6.11 c                   for       n = 165 to 166
6.11 c                   add       100           w_tell
6.11 c                   movel     rec(n)        w_recc
6.11 c                   except    xrecut
6.11 c                   endfor
6.11 c                   endif
6.11 c                   endif
      *
      * Legger ut siste del av CL-programmet
      *
     c                   for       n = 115 to 118
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_recc = *blank
     c                   eval      w_tell = *zero
     c                   eval      w_dato = *zero
     c                   eval      n = *zero
      *
     c                   eval      d_aaar = uyear
     c                   eval      d_mmnd = umonth
     c                   eval      d_ddag = uday
     c                   eval      w_dato = d_dato
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_valg
      *
     c                   eval      d_valg = p_valg
      *
     c                   endsr
      *
     oqcllesrc  eadd         xrecut
     o                       w_recc              96
     o                       w_tell               6
     o                       w_dato              12
**
             PGM                                                 /*01*/
/*                                                                 02*/
/* Definisjon av variabler                                         03*/
/*                                                                 04*/
             DCL        VAR(&KODE) TYPE(*DEC)  LEN(2 0)          /*05*/
             DCL        VAR(&TAPE) TYPE(*DEC)  LEN(1 0)          /*06*/
             DCL        VAR(&BIBL) TYPE(*CHAR) LEN(10)           /*07*/
/*                                                                 08*/
/* Nattrutine starter                                              09*/
/*                                                                 10*/
             CHGVAR     VAR(&KODE)  VALUE(02)                    /*11*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)            /*12*/
/*                                                                 13*/
             WRKACTJOB  OUTPUT(*PRINT) SBS(QINTER QBATCH)        /*14*/
/*                                                                 15*/
/* Stopper QBATCH                                                  16*/
/*                                                                 17*/
WAIT1:       DLYJOB     DLY(31)                                  /*18*/
             ENDSBS     SBS(QBATCH) OPTION(*IMMED)               /*19*/
             MONMSG     MSGID(CPF1054) EXEC(GOTO CMDLBL(WAIT2))  /*20*/
             MONMSG     MSGID(CPF1001)                           /*21*/
             MONMSG     MSGID(CPF1056)                           /*22*/
             GOTO       CMDLBL(WAIT1)                            /*23*/
/*                                                                 24*/
/* Stopper QINTER                                                  25*/
/*                                                                 26*/
WAIT2:       DLYJOB     DLY(32)                                  /*27*/
             ENDSBS     SBS(QINTER) OPTION(*IMMED)               /*28*/
             MONMSG     MSGID(CPF1054) EXEC(GOTO CMDLBL(WAIT3))  /*29*/
             MONMSG     MSGID(CPF1001)                           /*30*/
             MONMSG     MSGID(CPF1056)                           /*31*/
             GOTO       CMDLBL(WAIT2)                            /*32*/
/*                                                                 33*/
/* Starter QINTER                                                  34*/
/*                                                                 35*/
WAIT3:       DLYJOB     DLY(33)                                  /*36*/
             STRSBS     SBSD(QINTER)                             /*37*/
             MONMSG     MSGID(CPF1001) EXEC(GOTO CMDLBL(WAIT3))  /*38*/
             MONMSG     MSGID(CPF1010)                           /*39*/
/*                                                                 40*/
/* Starter egendefinert startprogram                               41*/
/*                                                                 42*/
             CALL       PGM(*LIBL/ANSTRT)                        /*43*/
/*                                                                 44*/
/* Kontroll av tape                                                45*/
/*                                                                 46*/
             CHGVAR     VAR(&TAPE)  VALUE(0)                     /*47*/
             CALL       PGM(ANTAPE) PARM(&TAPE)                  /*48*/
/*                                                                 49*/
/* Gå forbi backup-jobber dersom tape ikke ble godtatt             50*/
/*                                                                 51*/
             IF         COND(&TAPE *EQ 1) THEN(DO)               /*52*/
             GOTO       CMDLBL(FTAPE)                            /*53*/
             ENDDO                                               /*54*/
/*                                                                 55*/
/* Starter Backup standard                                         56*/
/*                                                                 57*/
             CHGVAR     VAR(&KODE)  VALUE(03)                    /*58*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)            /*59*/
             CALL       PGM(ASPGPLK/ANBAC1)                      /*60*/
/*                                                                 61*/
/* Starter Backup av alle brukerbibliotek                          62*/
/*                                                                 63*/
             CALL       PGM(*LIBL/ANUSRB)                        /*64*/
/*                                                                 65*/
/* Starter Spesial-rutiner                                         66*/
/*                                                                 67*/
             CHGVAR     VAR(&KODE)  VALUE(05)                    /*68*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)            /*69*/
             CALL       PGM(ASPGPLK/ANSPS1)                      /*70*/
             CALL       PGM(ASPGPLK/ANSPS2)                      /*71*/
             CALL       PGM(ASPGPLK/ANSPS3)                      /*72*/
/*                                                                 73*/
/* Starter Rutiner fra andre systemer                              74*/
/*                                                                 75*/
             CHGVAR     VAR(&KODE)  VALUE(06)                    /*76*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)            /*77*/
             CALL       PGM(ASPGPLK/ANSYS1)                      /*78*/
             CALL       PGM(ASPGPLK/ANSYS2)                      /*79*/
             CALL       PGM(ASPGPLK/ANSYS3)                      /*80*/
/*                                                                 81*/
/* Starter backup av mapper                                        82*/
/*                                                                 83*/
             CALL       PGM(*LIBL/ANMAPP)                        /*84*/
/*                                                                 85*/
FTAPE:                                                           /*86*/
/*                                                                 87*/
/* Starter Reorganisering                                          88*/
/*                                                                 89*/
             CHGVAR     VAR(&KODE)  VALUE(04)                    /*90*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)            /*91*/
             CALL       PGM(ASPGPLK/ANRZPF)                      /*92*/
/*                                                                 93*/
/* Starter opp igjen QBATCH                                        94*/
/*                                                                 95*/
WAIT4:       DLYJOB     DLY(34)                                  /*96*/
             STRSBS     SBSD(QBATCH)                             /*97*/
             MONMSG     MSGID(CPF1001) EXEC(GOTO CMDLBL(WAIT4))  /*98*/
             MONMSG     MSGID(CPF1010)                           /*99*/
/*                                                                100*/
/* Starter egendefinert sluttprogram                              101*/
/*                                                                102*/
             CALL       PGM(*LIBL/ANSLUT)                       /*103*/
/*                                                                104*/
/* DSPTAP tilslutt                                                105*/
/*                                                                106*/
             IF         COND(&TAPE *EQ 0) THEN(DO)              /*107*/
             DSPTAP     DEV(TAP01) OUTPUT(*PRINT)               /*108*/
             ENDDO                                              /*109*/
/*                                                                110*/
/* Nattrutine avsluttet normalt                                   111*/
/*                                                                112*/
             CHGVAR     VAR(&KODE)  VALUE(10)                   /*113*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)           /*114*/
/*                                                                115*/
             ENDPGM                                             /*116*/
/*                                                                117*/
/*                                                                118*/
/*                                                                119*/
/* Kall til program som setter opp biblioteksliste                120*/
/*                                                                121*/
             CALL       PGM(AA722C)    PARM('ASPNATT   ')       /*122*/
/*                                                                123*/
/* Dersom dette kjøres til savefile, og ikke ut på tape           124*/
/* direkte, renames savefile slik at biblioteket heter :          125*/
/* - ADB.SAVF1 for nattbackup 1                                   126*/
/* - ADB.SAVF2 for nattbackup 2                                   127*/
/* - ADB.SAVF3 for nattbackup 3                                   128*/
/* Melding sendes til loggen dersom savefile blir renamet         129*/
/*                                                                130*/
             CHKOBJ     OBJ(ADB.SAVF1) OBJTYPE(*LIB)            /*131*/
             MONMSG     MSGID(CPF9801) EXEC(DO)                 /*132*/
             GOTO       CMDLBL(NEXT1)                           /*133*/
             ENDDO                                              /*134*/
             DLTLIB     LIB(ADB.SAVF1)                          /*135*/
NEXT1:                                                          /*136*/
             RNMOBJ     OBJ(ADB.SAVF)  OBJTYPE(*LIB) +
                                       NEWOBJ(ADB.SAVF1)        /*138*/
                                                                /*139*/
                                                                /*140*/
             CHKOBJ     OBJ(ADB.SAVF2) OBJTYPE(*LIB)            /*141*/
             MONMSG     MSGID(CPF9801) EXEC(DO)                 /*142*/
             GOTO       CMDLBL(NEXT2)                           /*143*/
             ENDDO                                              /*144*/
             DLTLIB     LIB(ADB.SAVF2)                          /*145*/
NEXT2:                                                          /*146*/
             RNMOBJ     OBJ(ADB.SAVF)  OBJTYPE(*LIB) +
                                       NEWOBJ(ADB.SAVF2)        /*148*/
                                                                /*149*/
                                                                /*150*/
             CHKOBJ     OBJ(ADB.SAVF3) OBJTYPE(*LIB)            /*151*/
             MONMSG     MSGID(CPF9801) EXEC(DO)                 /*152*/
             GOTO       CMDLBL(NEXT3)                           /*153*/
             ENDDO                                              /*154*/
             DLTLIB     LIB(ADB.SAVF3)                          /*155*/
NEXT3:                                                          /*156*/
             RNMOBJ     OBJ(ADB.SAVF)  OBJTYPE(*LIB) +
                                       NEWOBJ(ADB.SAVF3)        /*158*/
                                                                /*159*/
                                                                /*160*/
             CHGVAR     VAR(&KODE)  VALUE(26)                   /*161*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)           /*162*/
             CHGVAR     VAR(&KODE)  VALUE(27)                   /*163*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)           /*164*/
             CHGVAR     VAR(&KODE)  VALUE(28)                   /*165*/
             CALL       PGM(AN050R) PARM(&KODE &BIBL)           /*166*/
/*                                                                167*/
/* Kjøre ut savefilen på tape                                     168*/
/*                                                                169*/
             CALL       PGM(*LIBL/ANSAVF)                       /*170*/
             CALL       PGM(ASPGPLK/ANBAC2)                     /*171*/
             CALL       PGM(ASPGPLK/ANBAC3)                     /*172*/
