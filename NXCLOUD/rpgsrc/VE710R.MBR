     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE710R
      * Beskrivelse . . : Henter varenr + enhet ut fra gitt eannr
      *
      *
      *                   Input : firma, eannr.
      *                   Output: vare, enhet,status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260609 bof  Nytt program
6.11  *       041110 bof  Hvis gtin-nr = 0, skal det ikke gi noe tilbake
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
      *
      * Definering av variable i nøkler
      *
     d vvepl2_pfir     s                   like(vepfir)
     d vvepl2_pgti     s                   like(vepgti)
     d veanl1_firm     s                   like(vxfirm)
     d veanl1_eann     s                   like(vxeann)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vxfirm)
     d p_eann          s                   like(vxeann)
     d p_vare          s                   like(vxvare)
     d p_enhe          s                   like(vxenhe)
     d p_stat          s              1
      *
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Initierer output-parametre
      *
     c                   eval      p_vare = *blank
     c                   eval      p_enhe = *blank
     c                   eval      p_stat = *blank
6.11  *
6.11 c                   if        p_eann = 0
6.11 c                   goto      avslutt
6.11 c                   endif
      *
      * Henter varenr + evt. enhet
      *
     c                   eval      vvepl2_pfir = p_firm
     c                   eval      vvepl2_pgti = p_eann
     c     vvepl2_key    chain     vvepl2
     c                   if        %found(vvepl2)
     c                   eval      p_vare = vepvar
     c                   eval      p_enhe = vepenh
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   if        p_stat = *blank
     c                   eval      veanl1_firm = p_firm
     c                   eval      veanl1_eann = p_eann
     c     veanl1_key    chain     veanl1
     c                   if        %found(veanl1)
     c                   eval      p_vare = vxvare
     c                   eval      p_stat = '1'
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_eann
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vvepl2_key    klist
     c                   kfld                    vvepl2_pfir
     c                   kfld                    vvepl2_pgti
      *
      * Key for les på ean-register
      *
     c     veanl1_key    klist
     c                   kfld                    veanl1_firm
     c                   kfld                    veanl1_eann
      *
      *
     c                   endsr
