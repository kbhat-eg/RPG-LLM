      /TITLE  GL100R Uttrekk av poster til betalingsforslag.
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System . . . . .: ASOKON                                        *
      * Program. . . . .: GL100R                                        *
      * Beskrivelse. . .: Uttrekk av poster til betalingsforslag.       *
      *                 + Overføring av lønnstransaksjoner til          *
      *                   utbetalingsforslag.                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *              NB!  FORSKJELL MELLOM LØNNSFORSLAG og VANLIG       *
      *                   UTBETALINGSFORSLAG ER:                        *
      *                                                                 *
      *               ->  feltet bilagsdato på forslagfile vil          *
      *                   ALLTID være blankt dersom det er lønns-       *
      *                   transaksjoner som er lagt ut i forslaget.     *
      *                   I tillegg vil de aller fleste feltene være    *
      *                   "mangelfult" utfylt:                          *
      *                                                                 *
      * Parameterliste:  'R' - Vanlig uttrekk av poster til utbetaling- *
      *                        forslag                                  *
      *                                                                 *
      *                  'L' - Overføring av lønnstransaksjoner til     *
      *                        utbetalingsforslag.                      *
      *                                                                 *
      *                  'F' - Det blir sendt opp informasjon om at det *
      *                        ikke finnes lønnstransaksjoner til over- *
      *                        føring.                                  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for   :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           260994  Nytt program september-1994              RH   *
      *                                                                 *
      * E26048    260498  DIREM=1 fungerte ikke riktig.  Poster    RH   *
      *                   ble ikke tatt med på nytt så lenge       RH   *
      *                   forslagsnummer var utfylt.               RH   *
      *                   DENNE ENDRINGEN ER IKKE UTFØRT!               *
      *                                                                 *
      * E10068    100698  Lagt inn behandling av ANTALL DAGER      RH   *
      *                   KREDITT og FRI LEVERINGSMÅNED, dersom    RH   *
      *                   dette er utfylt på leverandøren.         RH   *
      *                                                                 *
      * E14068    140698  POST SKAL IKKE TAS MED DERSOM DET ER     RH   *
      *                   VALGT RABATTBEHANDLING, OG HVOR FORUT-   RH   *
      *                   SETNING FOR Å OPPNÅ KONTANTRABATT ER     RH   *
      *                   PASSERT, OG SAMTIDIG AT HOVEDFORFALL     RH   *
      *                   PÅ POSTEN ER STØRRE ENN UTPLUKSDATO      RH   *
      *                   (TIL DATO) I PARAMETERBILDET.            RH   *
      *                                                                 *
      * E13078    130798  Henter betalingsbetingelse fra post,     RH   *
      *                   og IKKE fra leverandørregister.          RH   *
      *                                                                 *
      * E11088    110898  Rettet en del feil i forbindelse med     RH   *
      *                   beregning av kont.rabatt.                RH   *
      *                                                                 *
      * E12088    120898  Ved test på om kontantrabatt eller ikke  RH   *
      *                   må det testes på om rabattdato er yngre  RH   *
      *                   enn kjøredato, d.v.s. at dagens dato     RH   *
      *                   (kjøredato) ikke er passert rabattdato.  RH   *
      *                                                                 *
      * E25098    250998  Det skal kun beregnes forfallsdato ut i  RH   *
      *                   fra bet.bet. dersom forfallsdato IKKE er RH   *
      *                   utfylt på posten.                        RH   *
      *                                                                 *
      * E11118    111198  Endringene etter 10/6-98 har ødelagt     RH   *
      *                   en del av testene som går på FRI LEV MND RH   *
      *                   og post ikke skal tas med dersom beregn. RH   *
      *                   rabattdato er yngre enn utpl.dato.       RH   *
      *                                                                 *
      * E14118    141198  Dersom forfallsdato på post er yngre     RH   *
      *                   enn TIL DATO i parameterbildet, så skal  RH   *
      *                   den IKKE tas med.                        RH   *
      *                                                                 *
      * E19118    191198  Ved FRI LEV MÅNED ble ny forfallsdato    RH   *
      *                   beregnet med EN DAG for mye.             RH   *
      *                                                                 *
      * E21118    211198  Tar med forslagnummer "ut" av programmet RH   *
      *                   som parameter.                           RH   *
      *                                                                 *
      * E07029    070299  Ved utfylt DIREM=1 skal post tas med når RH   *
      *                   restsaldo ulik null OG samtidig at post  RH   *
      *                   er tatt med før OG samtidig at post ikke RH   *
      *                   finnes i utbetalingsforslag.             RH   *
      *                                                                 *
      * E02039    020399  Hjelpefeltet WTAMED innføres i forbindel-RH   *
      *                   se med kontroll på om posten skal tas    RH   *
      *                   med eller ikke.  Feltet har sammenheng   RH   *
      *                   med endringen 07029.                     RH   *
      *                                                                 *
      * E13079    130799  Endret slik at poster som blir beregnet  RH   *
      *                   kontantrabatt, blir tatt med så lenge de RH   *
      *                   har beregnet betalingsdato <= forf.dato  RH   *
      *                   i fra parameterbildet.                   RH   *
      *                                                                 *
      * E13129    131299  En post fra RLTRPF er attestert dersom   RH   *
      *                   feltet ROATTS = "1".                     RH   *
      *                                                                 *
      * E02039    020399  Dersom "DIREM" er valgt i utplukket, så  RH   *
      *                   må feltene ROBFNR, ROBFLI og ROBETK      RH   *
      *                   tømmes.                                  RH   *
      *                                                                 *
      * E20092    200902  Feltet ROSTAT er fjernet fra RLTRPF      HFL  *
      * V5.00     151002  Endring i datofelt og key-felt.          HFL  *
      * V5.00     171002  Ikke lenger key i GUBFPF, bruker L8.     HFL  *
      *                   GLDATO er ISO datofelt ÅÅÅÅ-MM-DD.       HFL  *
      *                                                                 *
      * 11032     110302  En post fra RLTRPF er attestert dersom   RH   *
      *                   feltet ROATTS = "1" eller "J".           RH   *
      *                                                                 *
      *           110803  Fjernet bruk av ASDATO (ikke merket)     KOB
      *
      *           061103  Fjernet betalingskode fra skjermbildet   KOB  *
      *                   Tester nå om leverandøren er merket med  KOB  *
      *                   J i remiteringskode for å utelate fra å  KOB  *
      *                   bli behandlet                            KOB  *
      *                                                                 *
      *           061103  Endret beregning av forfallsdato for     KOB  *
      *                   rabatt/fri leveringsmåte                 KOB  *
      *                                                                 *
      * 07123     071203  Dersom post oppfyller kriterie for       RH   *
      *                   kontantrabatt, OG dato som er beregnet   RH   *
      *                   for å få denne ligger lengre frem i tid  RH   *
      *                   enn "t.o.m. Forfallsdato" i parameter-   RH   *
      *                   bildet, så skal den IKKE tas med, MEN    RH   *
      *                   vente inntil siste mulighet, OG likevel  RH   *
      *                   kunne oppnå rabatt.                      RH   *
      *                                                                 *
      * 03024     030204  Endret til riktig datoformat for 2       RH   *
      *                   hjelpedatofelt.                          RH   *
      *                                                                 *
      * V5.00     220903  Leverandørs fakturanr. i tekstfelt.      HFL  *
      *                                                                 *
      * 27054     270504  Det var brukt feil forfallsdato felt.    RHO  *
      *                   Feilen oppstod i forbindelse med konv.   RHO  *
      *                   til ILERPG.                              RHO  *
      *                                                                 *
      * V5.30     150705  Leverandørkat. fra/til utvidet til 4.0   HFL  *
      *                                                                 *
      * 08095     080905  Setter forslagsteller til 001 etter      RHO  *
      *                   at den passerer 999.                     RHO  *
      *                                                                 *
      * V5.40     030405  Lagt ut dato, tid og bruker på trans.    KOB  *
      *                                                                 *
22106 * 22106     221006  Legger ut "S" i KID-kode dersom det er   RHO  *
      *                   strukturert informasjon i posten. Dette  RHO  *
      *                   betinger selfølgelig at posten IKKE har  RHO  *
      *                   KID.                                     RHO  *
      *                                                                 *
30017 * 30017     300107  Låsesituasjon i gitte tilfeller mot post RHO  *
      *                   i GUBFL2.                                RHO  *
      *                                                                 *
15037 * 15037     150307  Legger Leverandørens-Fakturanummer ut på RHO  *
      *                   i GUBFPF.                                RHO  *
      *                                                                 *
18037 * 18037     180307  Erstatter AP100R med AP600R. Ny standard RHO  *
      *                   for versjon 5.50.                        RHO  *
      *                                                                 *
20028 * 20028     200208  Legger ut "*" i KID-feltet dersom Lev.-  RHO  *
      *                   fakturanumemr er utfylt, dvs på samme    RHO  *
      *                   måte som KID.                            RHO  *
      *                                                                 *
20028 * 17108     171008  Legger ut "999999" i fakturanr på kred.  KOB  *
      *                   nota dersom feltet ikke er utfylt        KOB  *
      *                                                                 *
6.10  *           041109  Det legges ut '*' ut i GAKIDS uansett.   NHO  *
6.10  *                   endret slik at dersom ikke kid og lev.   NHO  *
6.10  *                   faktnr er utfylt så legges det ut 'S'    NHO  *
      *                                                                 *
6.11  *           131010  Lev. fakt.nr skrives i tekst dersom      NHO  *
6.11  *                   utfylt                                   NHO  *
      *                                                                 *
6.12  *           111110  Lev.fakt skal ikke utfylles med 9999999  NHO  *
6.12  *                   Linjer merkes med slettet                NHO  *
6.30  *  6.30     150814  Innført mulighet for arkivering og PDF   Bsa  *
6.31  *  6.30     040615  Antall linjer er oversteget              NHO  *
6.32  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
9.01  *  6.30     290916  Flyttet add 10 til linje                 NHO  *
      *                   før oppdatering...  så ut til at den
      *                   telte poster som ikke skulle med ut
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  BRUK AV INDIKATORER                                            *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Overflow i rapport.                                  *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    31    = Brukes også til meldinger i rapport.                 *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * A1PAR  - Parameterside på rapport
      * A1HDR  - Heading A1 på rapport
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgl100d    cf   e             workstn
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frlbapf    if   e           k disk    rename(rlbapfr:rlbapfr)
     fgfaslu    uf a e           k disk    rename(gfaspfr:gfaslur)
     frltrl9    if   e           k disk    rename(rltrpfr:rltrl9f)
     frltrlu    uf   e           k disk    rename(rltrpfr:rltrpfu)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fra03l1    if   e           k disk    rename(ra03pfr:ra03l1r)
     fgavtl2    if   e           k disk    rename(gavtpfr:gavtl2r)
     fglonl1    if   e           k disk    rename(glonpfr:glonl1r)
     fgubfl8    if   e           k disk    rename(gabfpfr:gabfpfp)
     fgubfl1    if   e           k disk    rename(gabfpfr:gabfpf1)
     fgubfl2    uf a e           k disk    rename(gabfpfr:gabfpf2)
     fgubfl3    uf a e           k disk    rename(gabfpfr:gabfpf3)
     fgubflu    o  a e             disk    rename(gabfpfr:gabfpfu)
     fgl100p    o    e             printer
6.30 f                                     usropn
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kommentarer til program.                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * GUBFL2 har Select på kun negative beløp, d.v.s. kreditnotaer.
      * I tillegg har den Omit på verdien "A" og "X" i statusfelt
      * GASTAT.
      * - - - - - - - - - - - - - - - - -
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * w_arow - Actual line, angir startlinje for vindu.
      * w_acol - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A1BLD  - A1 Hovedbilde for parameterregistrering.
      * B1BLD  - B1 Valgt leverandørintervaller.
      * B2BLD  - B2 Valgte leverandørkategorier.
      * C1WIN  - C1 Informasjonsbilde om "hva som skjer" i utplukket.
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildet C1WIN er referert
      *             til dette.
      *
      /eject
     d levf            s              6  0 dim(50)
     d levt            s              6  0 dim(50)
E5.30d katf            s              4  0 dim(50)
E5.30d katt            s              4  0 dim(50)
     d txt             s             33    dim(4) ctdata perrcd(1)
      *
     d kid             s              1    dim(25)
      *
      * Datastruktur for dagens dato:
      *
     d dsudat          s               d   datfmt(*iso)
      *
      * Datastruktur for kjøredato (DMÅ)
      *
     d dskdmÅ          s               d   datfmt(*iso)
      *
      * Datastruktur for kjøredato (ÅMD)
      *
     d dskÅmd          s               d   datfmt(*iso)
      *
      * Datastruktur for til forf. (ÅMD)
      *
     d dsfÅmd          s               d   datfmt(*iso)
      *
      * Datastruktur for forfallsdato på transaksjonsfile (ÅMD)
      *
     d dsforf          s               d   datfmt(*iso)
      *
      * Datastruktur for bilagsdato (ÅMD)
      *
     d dsbild          s               d   datfmt(*iso)
      *
      * Datastruktur for "bilagsdato" ved FRI LEVERINGSMÅNED:
      *
     d dsbifl          s               d   datfmt(*iso)
      *
      * Datastruktur for forfallsdato på forslagfile (ÅMD)
      *
     d gaforf          s               d   datfmt(*iso)
      *
      * Datastruktur for forfallsdato (hjelpefelt) (ÅMD)
      *
     d wpforf          s               d   datfmt(*iso)
      *
      * Datastruktur for parameterliste externt call av pgm:
      *
     d                 ds
     d dslist                         6
     d  wpfirm                        3  0 overlay(dslist)
     d  wpbfnr                        3  0 overlay(dslist:4)
      *
      * Datastruktur for forfallsdato (hjelpefelt) (ÅMD)
      * Datofelt brukes til å lagre forfallsdato for forrige
      * leste post, d.v.s. IKKE inneværende.
      *
     d wpfor1          s               d   datfmt(*iso)
      *
      * Henter firmanavn.
      *
     d lda            uds
6.30 d l_poff                584    584
6.30 d l_utqm                585    594
6.30 d l_bach                595    635
6.30 d l_arch                636    636  0
6.30 d l_skje                637    637  0
6.30 d l_land                638    638  0
6.30 d l_exlp                639    639  0
6.30 d l_mail                640    640  0
6.30 D l_outq                810    819
     d l_chgv                844    844
     d l_user                911    920
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
18037d l_ruti                800    809
      *
18037d w_in03          s              1
18037d w_tvbt          s              1
18037d w_btch          s              1
      *
     d i               s              3  0
     d nyforf          s               d   datfmt(*iso)
     d v               s              2  0
     d w               s              3  0
     d w_arow          s              2  0
     d w_acol          s              3  0
     d wpanta          s              4  0
     d wpbfli          s                   like(gabfli)
     d wpbdat          s                   like(gabdat)
     d w_fdat          s                   like(gafdat)
     d w_fdat1         s                   like(gafdat)
     d wpblan          s              2
     d wpdato          s               d   datfmt(*iso)
     d wpin03          s              1  0
     d wpin12          s              1  0
     d wpkidi          s                   like(gakids)
     d wpkids          s                   like(gakids)
     d wpkid1          s                   like(gakids)
     d wpkid5          s                   like(gakids)
     d wpklad          s             15  6
     d wplese          s              1
     d wplevr          s                   like(galevr)
     d wplev1          s                   like(galevr)
     d wplev5          s                   like(galevr)
     d wplinj          s                   like(gabfli)
     d wpluft          s              2
     d wpmsgs          s              3
     d wpnume          s              2
     d wpofor          s               d   datfmt(*iso)
     d wpopda          s                   like(gaopda)
     d wpparm          s              1
     d wprabb          s              1
     d wprbn1          s                   like(garbn1)
     d wprbn2          s                   like(garbn2)
     d wprbn3          s                   like(garbn3)
     d wprbv1          s                   like(garbv1)
     d wprbv2          s                   like(garbv2)
     d wprbv3          s                   like(garbv3)
     d wprest          s             11  2
     d wpsun1          s             11  2
     d wpsun2          s             11  2
     d wpsuv1          s             11  2
     d wpsuv2          s             11  2
     d wptest          s              1
     d wpuser          s             10
     d wpvalg          s              1
     d wpvali          s                   like(gavalk)
     d wpvalk          s                   like(gavalk)
     d wpvalr          s             11  2
     d wpval1          s                   like(gavalk)
     d wpval5          s                   like(gavalk)
     d wtamed          s              3
     d wwbfnr          s              3  0
     d x               s              3  0
     d xfootk          s              8  0
     d xfootl          s              8  0
      *
      * Definering av variable for timestamp
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
     d p_stat          s              1
     d p_ckod          s              2  0
     d p_ctxt          s             20
     d p_ctx2          s             20
03024d p_ffda          s               d   datfmt(*iso)
03024d p_fkda          s               d   datfmt(*iso)
6.30 d p_btyp          s            100
6.30 d w_year          s              4  0
      *
      * Rabattdato-1/2/3
      *
     d p_rda1          s               d   datfmt(*iso)
     d p_rda2          s               d   datfmt(*iso)
     d p_rda3          s               d   datfmt(*iso)
      *
     d w_dagr          s              3  0
      *
V5.40d w_foda          s               d   datfmt(*iso)
V5.40d w_ftim          s               t
V5.40d w_fsig          s             10
      *
6.30 d splfname2       s             10a
6.30 d jobname2        s             10a
6.30 d username2       s             10a
6.30 d jobnbr2         s              6a
6.30 d splfnbr2        s             10i 0
6.30 d splfname3       s             10a
6.30 d jobname3        s             10a
6.30 d username3       s             10a
6.30 d jobnbr3         s              6a
6.30 d splfnbr3        s             10i 0
6.30 d p_tagg0         s             20
6.30 d p_tagg0val      s             50
6.30 d p_tagg1         s             20
6.30 d p_tagg1val      s             50
6.30 d p_tagg2         s             20
6.30 d p_tagg2val      s             50
6.30 d p_tagg3         s             20
6.30 d p_tagg3val      s             50
6.30 d p_tagg4         s             20
6.30 d p_tagg4val      s             50
6.30 d p_tagg5         s             20
6.30 d p_tagg5val      s             50
6.30 d p_tagg6         s             20
6.30 d p_tagg6val      s             50
6.30 d p_tagg7         s             20
6.30 d p_tagg7val      s             50
6.30 d p_tagg8         s             20
6.30 d p_tagg8val      s             50
6.30 d p_tagg9         s             20
6.30 d p_tagg9val      s             50
6.30 d p_refn          s             50
6.30 d p_dspl          s            100
      *
6.30 d qsprilsp        pr                  extpgm('QSPRILSP')
6.30 d rcvvar                     32767a   options(*varsize)
6.30 d rcvvarlen                     10i 0 const
6.30 d format                         8a   const
6.30 d errorcode                  32767a   options(*varsize)
      *
6.30 d sprl0100        ds
6.30 d  bytesrtn                     10i 0
6.30 d  bytesavl                     10i 0
6.30 d  splfname                     10a
6.30 d  jobname                      10a
6.30 d  username                     10a
6.30 d  jobnbr                        6a
6.30 d  splfnbr                      10i 0
6.30 d  systemname                    8a
6.30 d  createdate                    7a
6.30 d                                1a
6.30 d  createtime                    6a
      *
6.30 d errorcode       ds
6.30 d  bytesprov                    10i 0 inz(0)
6.30 d  byteavail                    10i 0 inz(0)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Test på parameterinformasjon.                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a1taga        tag
      *
      * Initierer posisjonering av window:
      *
     c                   exsr      xliini
      *
      * WPPARM er "F" dersom det ikke finnes lønnstransaksjoner til
      * overføring.  Informasjonsbilde sendes opp:
      *
     c                   if        wpparm = 'F'
      *
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   exfmt     c5win
      * Avslutter:
     c                   goto      xslutt
     c                   endif
      *
      * WPPARM er "L" dersom det skal overføres lønnstransaksjoner
      * til utbetalingsforslag:
      *
     c                   if        wpparm = 'L'
      *
     c     l_firm        setll     glonl1
     c     l_firm        reade     glonl1                                 60
      *
      * Henter neste forslagsnummer:
      *
     c                   if        *in60 = *off
     c                   exsr      xforsl
     c                   endif
      *
     c                   dow       *in60 = *off
      *
      * Overfører felter fra RLONPF til GUBFPF (forslagsfile):
      *
     c                   eval      gafirm = glfirm
      *
      * Forslagsnummer:
      *
     c                   eval      gabfnr = wpbfnr
      *
      * Linjenummer:
      *
     c                   eval      wplinj = wplinj + 10
     c                   eval      gabfli = wplinj
      *
      * Lønnsnummer legges ut i lev.nr. felt:
      *
     c                   eval      galevr = gllonr
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    gatist
     c     *iso0         move      gatist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     gatist
      *
      * Navn eller "Skattetrekk" legges inn i tekstfelt
      * og navnefelt:
      *
     c                   eval      gatext = *blanks
     c                   eval      ganavn = *blanks
      *
     c                   if        gllonr <> 0
     c                   movel     glnavn        gatext
     c                   movel     glnavn        ganavn
     c                   else
     c                   movel     txt(4)        gatext
     c                   movel     txt(4)        ganavn
     c                   endif
      *
      * Utbetalt beløp:
      *
     c                   eval      gabelØ = glbelØ
     c                   move      *loval        gabdat
     c                   move      gldato        gafdat
     c                   move      gldato        gaopfd
      *
      * Kontonummer:
      *
     c                   eval      gabgir = glbkto
      *
      * Opprettelsesdato:
      *
     c                   eval      gaopda = wpopda
     c                   movel     l_user        gaopav
      *
     c                   eval      c4anta = c4anta + 1
      *
      * Legger ut post på forslagfile:
      *
     c                   write     gabfpfu
      *
      * Leser neste post:
      *
     c     l_firm        reade     glonl1                                 60
      *
     c                   enddo
      *
      * Skriver ut forslaget:
      *
     c                   call      'GL102C'
     c                   parm                    dslist
     c                   parm                    wpin12
     c                   parm                    wpparm
      *
      * Sender opp info.bilde:
      *
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   exfmt     c4win
      *
      * Avslutter:
      *
     c                   goto      xslutt
      *
     c                   endif
      *
      * Send Alt
      *
     c                   exfmt     a1bld
      *
      * Avslutt program ved *IN40, F3 eller F12.
      *
     c     *inkc         cabeq     *on           xslutt
     c     *in40         cabeq     *on           a1taga
     c     *in41         cabeq     *on           a1taga
     c     *in42         cabeq     *on           a1taga
      *
      * Forny (nullstiller bl.a. indikatorer og tabeller)
      *
     c                   if        *inke = *on
     c                   exsr      xforny
     c                   goto      a1taga
     c                   endif
      *
      * Utskriftsformularer.
      *
     c                   if        *inkf = *on
      *
      *
      *  Kaller opp program før utskrift
      *
     c                   eval      l_ruti = 'GL100'
18037c                   call      'AP600R'
18037c                   parm                    l_ruti
18037c                   parm                    l_chgv
18037c                   parm                    w_tvbt
18037c                   parm                    w_in03
18037c                   parm                    w_btch
      *
18037c**   wpin12        cabeq     1             xslutt
     c     w_in03        cabeq     '1'           xslutt
     c                   goto      a1taga
     c                   endif
      *
      * Viser registrerte intervaller.
      *
     c                   if        *inki = *on
     c                   xfoot     levt          xfootl
     c                   xfoot     katt          xfootk
      *
     c                   if        xfootl > 0
     c                   exsr      xb1crt
     c                   eval      w_arow = 3
     c                   eval      w_acol = 8
     c                   exfmt     a2win
     c                   endif
      *
     c                   if        xfootk > 0
     c                   exsr      xb2crt
     c                   eval      w_arow = 3
     c                   eval      w_acol = 35
     c                   exfmt     a3win
     c                   endif
      *
      * Initierer posisjonering av window:
      *
     c                   exsr      xliini
      *
     c                   goto      a1taga
     c                   endif
      *
      * FRA HER!
      *
      * Test på verdier i skjermbildet.
      *
     c                   exsr      xa1tst
      *
      * Sender bildet opp på nytt dersom feil i registrerte verdier.
      *
     c     *in30         cabeq     *on           a1taga
     c     *in33         cabeq     *on           a1taga
     c     *in34         cabeq     *on           a1taga
     c     *in35         cabeq     *on           a1taga
     c     *in37         cabeq     *on           a1taga
     c     *in38         cabeq     *on           a1taga
     c     *in39         cabeq     *on           a1taga
      *
      * Sender advarsel om at det er valgt å ta med poster som
      * er overført tidligere.  Meldingen sendes kun en gang.
      * WPMSGS er hjelpefelt for å indikere om melding er sendt
      * tidligere:
      *
     c                   if        a1betk = 1
     c                             and wpmsgs = 'NEI'
      *
      * Initierer hjelpefelt:
      *
     c                   eval      wpmsgs = *blanks
      *
      * Tester om Window er innenfor grensene:
      *
     c                   exsr      xlitst
      *
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
      *
     c                   exfmt     c3win
      *
     c     *inkc         cabeq     *on           a1taga
      *
     c                   endif
      *
     c/eject
      *
      * Verdier i parameterbildet er nå OK, og det er klart for
      * utplukk.
      *
      * Dersom TIL-verdi er blank kopieres FRA-verdi over.
      * samtidig settes indikatorer på til bruk for protect og
      * posisjonering.
      *
     c                   if        a1levt = 0
     c                   eval      a1levt = a1levf
     c                   endif
      *
     c                   if        a1katt = 0
     c                   eval      a1katt = a1katf
     c                   endif
      *
     c                   if        a1levt <> 0
     c                   move      *on           *in31
     c                   move      *off          *in32
     c                   endif
      *
     c                   if        a1katt <> 0
     c                   move      *off          *in31
     c                   move      *on           *in32
     c                   endif
      *
     c                   if        a1levf <> 0 and
     c                             a1levt = 0
     c                   move      *on           *in31
     c                   move      *off          *in39
     c                   endif
      *
      * Leverandørnummer eller kategorinummer lagres i tabell.
      *
     c                   eval      i = i + 1
      *
      * Tester på at teller "I" ikke er større enn 50, dersom det
      * fortsatt registreres intervaller:
      *
     c                   if        a1levt <> 0 or
     c                             a1katt <> 0
      *
     c                   if        i > 50
     c                   eval      i = i - 1
     c                   move      *on           *in36
     c                   goto      a1taga
     c                   endif
      *
     c                   endif
      *
     c                   if        a1levt > 0
     c                   eval      levf(i) = a1levf
     c                   eval      a1levf = 0
     c                   eval      levt(i) = a1levt
     c                   eval      a1levt = 0
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1katt > 0
     c                   eval      katf(i) = a1katf
     c                   eval      a1katf = 0
     c                   eval      katt(i) = a1katt
     c                   eval      a1katt = 0
     c                   goto      a1taga
     c                   endif
      *
      * Generer utbetalingsforslag dersom enten lev.nr eller kat.nr
      * er utfylt.  De andre parameterene er allered OK dersom tabell
      * for leverandør eller kategori har blitt oppdatert.  "I" er
      * addert med 1 for mye, o g må derfor reduseres med 1.
      *
     c                   eval      i = i - 1
     c                   xfoot     levt          xfootl
     c                   xfoot     katt          xfootk
      *
     c                   if        xfootl > 0 or
     c                             xfootk > 0
      *
     c                   exsr      xlestr
      *
      * Tester om Window er innenfor grensene:
      *
     c                   exsr      xlitst
      *
     c                   eval      w_arow = w_arow + 5
     c                   eval      w_acol = w_acol - 4
     c                   exfmt     c2win
     c                   endif
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   if        wpbfnr <> 0
     c                   eval      wwbfnr = wpbfnr
     c                   endif
      * Generer xml for videre utskrift
6.30 c                   if        l_poff = '1'
6.30 c                   exsr      spoolnbr
6.32 c                   close     gl100p
6.32 c                   exsr      xml_create
6.30 c                   exsr      pdf_preview
6.30  * Avslutt jobbiden
6.30 c                   call      'AB710R'
6.30 c                   parm                    l_bach
6.30 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Initierer rutinen på nytt.                                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xforny        begsr
      *
      * Indikatorer som er brukt til feilmeldinger og posisjoneringer.
      *
     c                   move      *off          *in30
     c                   move      *off          *in31
     c                   move      *off          *in32
     c                   move      *off          *in33
     c                   move      *off          *in34
     c                   move      *off          *in35
     c                   move      *off          *in36
     c                   move      *off          *in37
     c                   move      *off          *in38
      *
      * Teller til tabell.
      *
     c                   eval      i = 0
      *
      * Tabell.
      *
     c                   eval      levf = 0
     c                   eval      levt = 0
     c                   eval      katf = 0
     c                   eval      katt = 0
      *
      * Sum av tabellene.
      *
     c                   eval      xfootl = 0
     c                   eval      xfootk = 0
      *
      * Sum av tabellene.
      *
     c                   eval      a1levf = 0
     c                   eval      a1levt = 0
     c                   eval      a1katf = 0
     c                   eval      a1katt = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Legger over leverandørnummer til skjermbilde B1.                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xb1crt        begsr
      *
     c                   eval      b1lf01 = levf(1)
     c                   eval      b1lf02 = levf(2)
     c                   eval      b1lf03 = levf(3)
     c                   eval      b1lf04 = levf(4)
     c                   eval      b1lf05 = levf(5)
     c                   eval      b1lf06 = levf(6)
     c                   eval      b1lf07 = levf(7)
     c                   eval      b1lf08 = levf(8)
     c                   eval      b1lf09 = levf(9)
     c                   eval      b1lf10 = levf(10)
      *
     c                   eval      b1lf11 = levf(11)
     c                   eval      b1lf12 = levf(12)
     c                   eval      b1lf13 = levf(13)
     c                   eval      b1lf14 = levf(14)
     c                   eval      b1lf15 = levf(15)
     c                   eval      b1lf16 = levf(16)
     c                   eval      b1lf17 = levf(17)
     c                   eval      b1lf18 = levf(18)
     c                   eval      b1lf19 = levf(19)
     c                   eval      b1lf20 = levf(20)
      *
     c                   eval      b1lf21 = levf(21)
     c                   eval      b1lf22 = levf(22)
     c                   eval      b1lf23 = levf(23)
     c                   eval      b1lf24 = levf(24)
     c                   eval      b1lf25 = levf(25)
     c                   eval      b1lf26 = levf(26)
     c                   eval      b1lf27 = levf(27)
     c                   eval      b1lf28 = levf(28)
     c                   eval      b1lf29 = levf(29)
     c                   eval      b1lf30 = levf(30)
      *
     c                   eval      b1lf31 = levf(31)
     c                   eval      b1lf32 = levf(32)
     c                   eval      b1lf33 = levf(33)
     c                   eval      b1lf34 = levf(34)
     c                   eval      b1lf35 = levf(35)
     c                   eval      b1lf36 = levf(36)
     c                   eval      b1lf37 = levf(37)
     c                   eval      b1lf38 = levf(38)
     c                   eval      b1lf39 = levf(39)
     c                   eval      b1lf40 = levf(40)
      *
     c                   eval      b1lf41 = levf(41)
     c                   eval      b1lf42 = levf(42)
     c                   eval      b1lf43 = levf(43)
     c                   eval      b1lf44 = levf(44)
     c                   eval      b1lf45 = levf(45)
     c                   eval      b1lf46 = levf(46)
     c                   eval      b1lf47 = levf(47)
     c                   eval      b1lf48 = levf(48)
     c                   eval      b1lf49 = levf(49)
     c                   eval      b1lf50 = levf(50)
      *
     c                   eval      b1lt01 = levt(1)
     c                   eval      b1lt02 = levt(2)
     c                   eval      b1lt03 = levt(3)
     c                   eval      b1lt04 = levt(4)
     c                   eval      b1lt05 = levt(5)
     c                   eval      b1lt06 = levt(6)
     c                   eval      b1lt07 = levt(7)
     c                   eval      b1lt08 = levt(8)
     c                   eval      b1lt09 = levt(9)
     c                   eval      b1lt10 = levt(10)
      *
     c                   eval      b1lt11 = levt(11)
     c                   eval      b1lt12 = levt(12)
     c                   eval      b1lt13 = levt(13)
     c                   eval      b1lt14 = levt(14)
     c                   eval      b1lt15 = levt(15)
     c                   eval      b1lt16 = levt(16)
     c                   eval      b1lt17 = levt(17)
     c                   eval      b1lt18 = levt(18)
     c                   eval      b1lt19 = levt(19)
     c                   eval      b1lt20 = levt(20)
      *
     c                   eval      b1lt21 = levt(21)
     c                   eval      b1lt22 = levt(22)
     c                   eval      b1lt23 = levt(23)
     c                   eval      b1lt24 = levt(24)
     c                   eval      b1lt25 = levt(25)
     c                   eval      b1lt26 = levt(26)
     c                   eval      b1lt27 = levt(27)
     c                   eval      b1lt28 = levt(28)
     c                   eval      b1lt29 = levt(29)
     c                   eval      b1lt30 = levt(30)
      *
     c                   eval      b1lt31 = levt(31)
     c                   eval      b1lt32 = levt(32)
     c                   eval      b1lt33 = levt(33)
     c                   eval      b1lt34 = levt(34)
     c                   eval      b1lt35 = levt(35)
     c                   eval      b1lt36 = levt(36)
     c                   eval      b1lt37 = levt(37)
     c                   eval      b1lt38 = levt(38)
     c                   eval      b1lt39 = levt(39)
     c                   eval      b1lt40 = levt(40)
      *
     c                   eval      b1lt41 = levt(41)
     c                   eval      b1lt42 = levt(42)
     c                   eval      b1lt43 = levt(43)
     c                   eval      b1lt44 = levt(44)
     c                   eval      b1lt45 = levt(45)
     c                   eval      b1lt46 = levt(46)
     c                   eval      b1lt47 = levt(47)
     c                   eval      b1lt48 = levt(48)
     c                   eval      b1lt49 = levt(49)
     c                   eval      b1lt50 = levt(50)
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Legger over leverandørkategorier til skjermbilde B2.            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xb2crt        begsr
      *
     c                   eval      b2kf01 = katf(1)
     c                   eval      b2kf02 = katf(2)
     c                   eval      b2kf03 = katf(3)
     c                   eval      b2kf04 = katf(4)
     c                   eval      b2kf05 = katf(5)
     c                   eval      b2kf06 = katf(6)
     c                   eval      b2kf07 = katf(7)
     c                   eval      b2kf08 = katf(8)
     c                   eval      b2kf09 = katf(9)
     c                   eval      b2kf10 = katf(10)
      *
     c                   eval      b2kf11 = katf(11)
     c                   eval      b2kf12 = katf(12)
     c                   eval      b2kf13 = katf(13)
     c                   eval      b2kf14 = katf(14)
     c                   eval      b2kf15 = katf(15)
     c                   eval      b2kf16 = katf(16)
     c                   eval      b2kf17 = katf(17)
     c                   eval      b2kf18 = katf(18)
     c                   eval      b2kf19 = katf(19)
     c                   eval      b2kf20 = katf(20)
      *
     c                   eval      b2kf21 = katf(21)
     c                   eval      b2kf22 = katf(22)
     c                   eval      b2kf23 = katf(23)
     c                   eval      b2kf24 = katf(24)
     c                   eval      b2kf25 = katf(25)
     c                   eval      b2kf26 = katf(26)
     c                   eval      b2kf27 = katf(27)
     c                   eval      b2kf28 = katf(28)
     c                   eval      b2kf29 = katf(29)
     c                   eval      b2kf30 = katf(30)
      *
     c                   eval      b2kf31 = katf(31)
     c                   eval      b2kf32 = katf(32)
     c                   eval      b2kf33 = katf(33)
     c                   eval      b2kf34 = katf(34)
     c                   eval      b2kf35 = katf(35)
     c                   eval      b2kf36 = katf(36)
     c                   eval      b2kf37 = katf(37)
     c                   eval      b2kf38 = katf(38)
     c                   eval      b2kf39 = katf(39)
     c                   eval      b2kf40 = katf(40)
      *
     c                   eval      b2kf41 = katf(41)
     c                   eval      b2kf42 = katf(42)
     c                   eval      b2kf43 = katf(43)
     c                   eval      b2kf44 = katf(44)
     c                   eval      b2kf45 = katf(45)
     c                   eval      b2kf46 = katf(46)
     c                   eval      b2kf47 = katf(47)
     c                   eval      b2kf48 = katf(48)
     c                   eval      b2kf49 = katf(49)
     c                   eval      b2kf50 = katf(50)
      *
     c                   eval      b2kt01 = katt(1)
     c                   eval      b2kt02 = katt(2)
     c                   eval      b2kt03 = katt(3)
     c                   eval      b2kt04 = katt(4)
     c                   eval      b2kt05 = katt(5)
     c                   eval      b2kt06 = katt(6)
     c                   eval      b2kt07 = katt(7)
     c                   eval      b2kt08 = katt(8)
     c                   eval      b2kt09 = katt(9)
     c                   eval      b2kt10 = katt(10)
      *
     c                   eval      b2kt11 = katt(11)
     c                   eval      b2kt12 = katt(12)
     c                   eval      b2kt13 = katt(13)
     c                   eval      b2kt14 = katt(14)
     c                   eval      b2kt15 = katt(15)
     c                   eval      b2kt16 = katt(16)
     c                   eval      b2kt17 = katt(17)
     c                   eval      b2kt18 = katt(18)
     c                   eval      b2kt19 = katt(19)
     c                   eval      b2kt20 = katt(20)
      *
     c                   eval      b2kt21 = katt(21)
     c                   eval      b2kt22 = katt(22)
     c                   eval      b2kt23 = katt(23)
     c                   eval      b2kt24 = katt(24)
     c                   eval      b2kt25 = katt(25)
     c                   eval      b2kt26 = katt(26)
     c                   eval      b2kt27 = katt(27)
     c                   eval      b2kt28 = katt(28)
     c                   eval      b2kt29 = katt(29)
     c                   eval      b2kt30 = katt(30)
      *
     c                   eval      b2kt31 = katt(31)
     c                   eval      b2kt32 = katt(32)
     c                   eval      b2kt33 = katt(33)
     c                   eval      b2kt34 = katt(34)
     c                   eval      b2kt35 = katt(35)
     c                   eval      b2kt36 = katt(36)
     c                   eval      b2kt37 = katt(37)
     c                   eval      b2kt38 = katt(38)
     c                   eval      b2kt39 = katt(39)
     c                   eval      b2kt40 = katt(40)
      *
     c                   eval      b2kt41 = katt(41)
     c                   eval      b2kt42 = katt(42)
     c                   eval      b2kt43 = katt(43)
     c                   eval      b2kt44 = katt(44)
     c                   eval      b2kt45 = katt(45)
     c                   eval      b2kt46 = katt(46)
     c                   eval      b2kt47 = katt(47)
     c                   eval      b2kt48 = katt(48)
     c                   eval      b2kt49 = katt(49)
     c                   eval      b2kt50 = katt(50)
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tester på verdier i skjermbilde A1.                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xa1tst        begsr
      *
     c                   exsr      xa1dat
     c                   exsr      xa1lev
      *
     c     xa1tag        tag
      *
     c                   endsr
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tester på posisjonering av window:                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlitst        begsr
      *
     c                   if        w_arow >= 15
     c                   eval      w_arow = 2
     c                   eval      w_acol = 39
     c                   endif
      *
     c                   endsr
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Initierer posisjonering av window:                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xliini        begsr
      *
     c                   eval      w_arow = 2
     c                   eval      w_acol = 43
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tester på datoverdier i skjermbilde A1.                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xa1dat        begsr
      *
      * Tester forfallsdato
      *
     c                   if        a1forf = *zero
     c                   move      udate         a1forf
     c                   endif
      *
     c     *dmy          test(d)                 a1forf                 30
      *
     c                   if        *in30 = *off
     c     *dmy          move      a1forf        dsfÅmd
     c                   endif
      *
      * Tester kjøredato
      *
     c                   if        a1kjØr = *zero
     c                   move      udate         a1kjØr
     c                   endif
      *
     c     *dmy          test(d)                 a1kjØr                 39
      *
     c                   if        *in39 = *off
     c     *dmy          move      a1kjØr        dskÅmd
     c                   endif
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tester på leverandørnummer og leverandørkategori i bilde A1.    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xa1lev        begsr
      *
      * Både leverandørnummer og kategori  i bildet er blankt,
      * samtidig som tabellene er tomme.
      *
     c                   xfoot     levt          xfootl
     c                   xfoot     katt          xfootk
      *
     c                   if        a1levf = 0 and
     c                             a1levt = 0 and
     c                             a1katf = 0 and
     c                             a1katt = 0 and
     c                             xfootl = 0 and
     c                             xfootk = 0
     c                   move      *on           *in33
     c                   endif
      *
      * Både leverandørnummer og kategori  i bildet er utfylt.
      *
     c                   if        a1levf <> 0 or
     c                             a1levt <> 0
      *
     c                   if        a1katf <> 0 or
     c                             a1katt <> 0
     c                   move      *on           *in33
     c                   endif
      *
     c                   endif
      *
      * Dersom leverandørnummer registreres må kategori være blankt.
      *
     c                   if        a1levf <> 0 or
     c                             a1levt <> 0
      *
     c                   if        xfootk <> 0 or
     c                             a1katf <> 0 or
     c                             a1katt <> 0
     c                   move      *on           *in38
     c                   endif
      *
     c                   endif
      *
      * Dersom kategori registreres må leverandørnummer være blankt.
      *
     c                   if        a1katf <> 0 or
     c                             a1katt <> 0
      *
     c                   if        xfootl <> 0 or
     c                             a1levf <> 0 or
     c                             a1levt <> 0
     c                   move      *on           *in37
     c                   endif
      *
     c                   endif
      *
      * TIL leverandør må være > FRA leverandør dersom ikke enkeltutpl.
      *
     c                   if        a1levt <> 0 and
     c                             a1levt < a1levf
     c                   move      *on           *in34
     c                   endif
      *
      * TIL kategori må være > FRA kategori dersom ikke enkeltutplukk.
      *
     c                   if        a1katt <> 0 and
     c                             a1katt < a1katf
     c                   move      *on           *in35
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Leser poster i fra transaksjonsfile inntil EOF.                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlestr        begsr
      *
      * Legger ut "ventetekst" i skjermbildet.
      *
     c                   if        a1raba <> 0
     c                   movel     txt(1)        c1txt1
     c                   endif
      *
      * Tester om Window er innenfor grensene:
      *
     c                   exsr      xlitst
      *
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   write     c1win
      *
     c     l_firm        setll     rltrl9
     c     l_firm        reade     rltrl9                                 60
      *
     c                   dow       *in60 = *off
     c                   move      robdat        dsbild
     c                   move      rofdat        dsforf
      *
     c                   exsr      xwrite
      *
     c     l_firm        reade     rltrl9                                 60
      *
6.31 c                   if        wpbfli = 99990
6.31 c                   eval      *in55 = '1'
6.31 c                   eval      *in60 = '1'
6.31 c                   endif
6.31  *
     c                   enddo
      *
      * Skriver ut sum antall poster som er overført.
      *
     c                   eval      c1anta = c1anta + wpanta
      *
     c                   write     c1win
      *
      * Utplukk av poster i fra Leverandørreskontro er ferdig.
      * Det må nå testes om flest mulige kreditnotaer skal avregnes
      * pr. forfallsdato for faktura (valg 1) eller totalt pr.
      * største forfall (valg 2).
      * PS!  Dette gjøres kun dersom det er funnet poster i utplukket.
      *
     c                   if        wpbfnr <> 0
      *
     c                   if        a1avre = 1 or
     c                             a1avre = 2
     c                   exsr      xavreg
     c                   endif
      *
      * Renummererer linjenummer på utbetalingsforslaget.
      *
     c                   call      'GL101R'
     c                   parm                    dslist
      *
      * Resultatet av utplukket blir skrevet ut.
      *
     c                   exsr      xrappo
      *
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å legge poster ut på forslagsfile.                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xwrite        begsr
      *
      * Restbeløp på post må være ulik null.
      *
     c                   if        rorest = 0
     c                   goto      xuttag
     c                   endif
      *
      * Tester på at post ikke har reklamasjonskode utfylt.
      *
     c                   if        rorekl <> *blank
     c                   goto      xuttag
     c                   endif
      *
      * Tester på at post er attestert. Dersom den er ulik blank
      *
     c                   if        a1atts = 1 and
     c                             roatts <> '1' and
     c                             roatts <> 'J'
     c                   goto      xuttag
     c                   endif
      *
      * Poster hvor det er mottatt 2. gangs retur skal ikke tas med.
      *
     c                   if        robetk = '2'
     c                   goto      xuttag
     c                   endif
      *
      * Poster hvor det er mottatt 1. gangs retur skal tas med i
      * utplukket dersom "1" er valgt i A1BETK.  Dette betinger
      * imidlertid at saldo NOK og Valuta også er ulik null.
      *
     c                   if        a1betk = 1 and
     c                             robetk = '1' and
     c                             rorest = 0 and
     c                             rovalr = 0
     c                   goto      xuttag
     c                   endif
      *
     c                   movel     'JA '         wtamed
      *
      * Poster som er sendt remittering, OG DIREM=1 (i fra parameter-
      * bildet) OG restsaldo er ulik null (er allerede testet ovenfor)
      * OG forslagsnummer er utfylt på posten OG dette forslaget IKKE
      * finnes---> SKAL TAS MED:
      *
     c                   if        a1betk = 1 and
     c                             robfnr <> 0 and
     c                             robfli <> 0
     c     key07         chain     gubfl8                             64
      *
     c                   if        *in64 = *on
     c                   eval      robfnr = 0
     c                   eval      robfli = 0
     c                   eval      robetk = *blank
     c                   goto      xover
     c                   endif
      *
     c                   movel     'NEI'         wtamed
      *
     c                   endif
      *
      * Poster som er sendt remittering, OG ennå ikke mottatt
      * 2.gangsretur, skal IKKE tas med:
      *
     c                   if        a1betk <> 1 and
     c                             robetk = '1'
     c                   goto      xuttag
     c                   endif
      *
     c     xover         tag
      *
      * Poster som er "overført" til utbetalingsforslag tidligere
      * og ennå ikke sendt til remittering, d.v.s. hverken 1.gangs-
      * eller 2.gangs retur, skal IKKE tas med i utplukket.
      *
     c                   if        robfnr <> 0 and
     c                             robetk = *blank
     c                   goto      xuttag
     c                   endif
      *
      * Henter opp bankinfo utenlands dersom dette finnes, og
      * legger over kode for om valutakonto skal benyttes:
      *
     c                   movel     'N'           gabeva
      *
     c     key01         chain     rlbapf                             61
      *
     c                   if        *in61 = *off and
     c                             rovalk <> 'NOK' and
     c                             rovalk <> *blanks
     c                   movel     rvhbvk        gabeva
     c                   endif
      *
      * Dersom blank i feltet for om valutakonto skal benyttes
      * på bankinfo utland, så må "N" legges med i feltet på
      * forslagsfile:
      *
     c                   if        gabeva = *blanks
     c                   movel     'N'           gabeva
     c                   endif
      *
      * Henter opp leverandørinformasjon.
      *
     c     key01         chain     rlevl1                             61
      *
     c     *in61         cabeq     *on           xuttag
      *
      * Tester på at leverandør er merket med J i remiteringskode
      *
     c                   if        rlremk = 'J'
     c                   goto      xuttag
     c                   endif
      *
      * Tester på at leverandørnummer på postering er innenfor det/de
      * intervallet/intervallene som er valgt i parameterbildet.
      *
     c                   xfoot     levt          xfootl
      *
     c                   eval      i = 1
      *
     c                   if        xfootl <> 0
      *
     c     i             do        50
     c     levt(i)       cabeq     0             xuttag
      *
     c                   if        rolevr >= levf(i) and
     c                             rolevr <= levt(i)
     c                   leave
     c                   endif
      *
     c                   eval      i = i + 1
     c                   enddo
      *
     c                   endif
      *
      * Tester på at kategorinummer på postering er innenfor det/de
      * intervallet/intervallene som er valgt i parameterbildet.
      *
     c                   xfoot     katt          xfootk
      *
     c                   eval      i = 1
      *
     c                   if        xfootk <> 0
      *
     c     i             do        50
      *
     c     katt(i)       cabeq     0             xuttag
      *
     c                   if        rlkatg >= katf(i) and
     c                             rlkatg <= katt(i)
     c                   leave
     c                   endif
      *
     c                   eval      i = i + 1
     c                   enddo
      *
     c                   endif
      *
      * Legger over bet.bet. fra leverandør dersom ikke utfylt
      * på transaksjonen:
      *
     c                   if        robetb = 0
     c                   eval      robetb = rlbetb
     c                   endif
      *
      * Finner betalingsbetingelse:
      *
     c     key02         chain     ra03l1                             61
      *
      * "Flagger" feltet WPRABB dersom post skal rabattberegnes:
      *
     c                   move      '0'           wprabb
      *
     c                   if        a1raba = 1
      *
     c                   if        racrab > 0 or
     c                             racra2 > 0 or
     c                             racra3 > 0 or
     c                             racjan = 'JA '
     c                   move      '1'           wprabb
     c                   endif
      *
     c                   endif
      *
      * (Beløp < 0  d.v.s. dette gjelder kun fakturaer)
      * (RACDAG -> Antall dager kreditt MÅ være utfylt)
      * (*IN61  -> Er "OFF ved tilslag på bet.bet.)
      *
     c                   if        racdag <> 0 and
     c                             robelØ < 0 and
     c                             wprabb = '1' and
     c                             *in61 = *off
      *
      * Beregner forfallsdato
      *
     c                   eval      p_ckod = robetb
     c                   move      robdat        p_fkda
      *
     c                   call      'RS203R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
     c                   parm                    p_ctx2
     c                   parm                    p_fkda
     c                   parm                    p_ffda
     c                   parm                    p_rda1
     c                   parm                    p_rda2
     c                   parm                    p_rda3
      *
     c                   move      p_ffda        dsforf
      *
     c                   endif
      *
      * Hjelpefelt for mellomlagring av opprinnelig
      * forfallsdato på transaksjon fra RLTRPF:
      *
     c                   eval      wpofor = dsforf
      *
      * Forfallsdato på transaksjon kan ikke være mindre enn
      * dagens dato.  PS!  Det er i tillegg kontroll på om
      * Kjøredato er mindre enn dagens dato.  Dersom dette er
      * tilfelle, så settes forfallsdato lik Kjøredato.
      * Dette gjør det mulig å trekke rabatt i fra fakturaen
      * ved å "trikse" med kjøredatoen":
      *
      * Kontroll av forfallsdato mot dagens dato:
      *
     c                   if        dsforf < dsudat
     c                   eval      dsforf = dsudat
      *
      * Kontroll av forfallsdato mot kjøredato:
      *
     c                   if        dsforf > dskÅmd
     c                   eval      dsforf = dskÅmd
     c                   endif
      *
     c                   endif
      *
      * Dersom "Spesiell rabattbehandling" i A1 ikke er utfylt, så må
      * forfallsdato på posten være mindre eller lik parameterdato.
      * Dersom forfallsdato i parameterbildet ikke er utfylt, så
      * skal post være med.
      *
     c                   if        a1raba = 0 and
     c                             a1forf <> 0 and
     c                             dsforf >  dsfÅmd
     c                   goto      xuttag
     c                   endif
      *
      * Dersom "Spesiell rabattbehandling" er utfylt i A1, og
      * leverandør har utfylt betalingsbetingelses så må det
      * beregnes optimal rabatt for transaksjonen.
      *
      * Nullstiller hjelpefelter.
      *
     c                   eval      wprbn1 = 0
     c                   eval      wprbn2 = 0
     c                   eval      wprbn3 = 0
     c                   eval      wprbv1 = 0
     c                   eval      wprbv2 = 0
      *
      * Overfører opprinnelig restbeløp.
      *
     c                   eval      wprest = rorest
     c                   eval      wpvalr = rovalr
      *
      * Overfører forfallsdato i fra transaksjon til forslag.
      *
     c                   eval      gaforf = dsforf
     c                   move      gaforf        gafdat
      *
      * Beregner rabattbeløp som ev. skal trekkes i fra fakturabeløp:
      * Kun fakturaer skal behandles, d.v.s. beløp må være < 0.
      *
     c                   if        robelØ < 0
      *
      * Legger over bet.bet. fra leverandør dersom ikke utfylt
      * på transaksjonen:
      *
     c                   if        robetb = 0
     c                   eval      robetb = rlbetb
     c                   endif
      *
      * Beregn forfallsdato for rabatt bare hvis spes-rab
      *
     c                   if        wprabb = '1'
      *
     c     key02         chain     ra03l1
      *
     c                   if        %found
      *
      * Finner den mest gunstige rabatten. - NOK/Valuta
      *
     c                   if        p_rda3 <> *loval
     c                   eval      wprbn3 = racra3 / 100 * rorest
     c                   eval      wprbv3 = racra3 / 100 * rovalr
     c                   endif
      *
     c                   if        p_rda2 <> *loval
     c                   eval      wprbn2 = racra2 / 100 * rorest
     c                   eval      wprbv2 = racra2 / 100 * rovalr
     c                   endif
      *
     c                   if        p_rda1 <> *loval
     c                   eval      wprbn1 = racrab / 100 * rorest
     c                   eval      wprbv1 = racrab / 100 * rovalr
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * DSFÅMD=Parameterdato (til forfall) i fra parameterbildet A1.
      *
      * DSFORF=Forfallsdato på leverandørtransaksjon.  Denne er justert
      *        til dagens dato dersom den er eldre enn dagens dato.
      *        Den er videre justert til kjøredato dersom kjøredato
      *        er eldre enn dagens dato.
      *
      * WPOFOR=Opprinnelig forfallsdato på transaksjonen.
      *
     c                   eval      wpdato = wpofor
      *
      * BEREGNING AV RABATTDATO DERSOM DETTE ER VALGT:
      * Rabattdato beregnes i RS203R
      * Legger over laveste rabattdato dersom denne er utfylt,
      * og samtidig lavere enn opprinnelig forfallsdato:
      *
     c                   if        a1raba = 1
      *
     c                   if        p_rda1 <> *loval and
     c                             p_rda1 < wpdato
     c                   eval      wpdato = p_rda1
     c                   endif
      *
     c                   if        p_rda2 <> *loval and
     c                             p_rda2 < wpdato
     c                   eval      wpdato = p_rda2
     c                   endif
      *
     c                   if        p_rda3 <> *loval and
     c                             p_rda3 < wpdato
     c                   eval      wpdato = p_rda3
     c                   endif
      *
     c                   endif
      *
      * Post skal ikke være med dersom opprinnelig forfallsdato er
      * yngre enn "Til forfallsdato" i fra parameterbildet, d.v.s.
      * at posten ennå ikke har forfalt.
      *
      * WPOFOR er opprinnelig betalingsdato (forfall) for posten.
      * WPDATO er rebattberegnet dato (den beste) for posten.
      * DSFÅMD er kjøredato.
      *
      * Dersom WPDATO = WPOFOR er det IKKE funnet noe rabattdato på
      * posten.  Det skal hoppes ut dersom forfallsdato er yngre enn
      * utplukksdato fra parameterbildet:
      *
     c                   if        wpdato = wpofor
      *
     c                   if        dsfÅmd <> *loval and
     c                             wpdato > dsfÅmd
     c                   goto      xuttag
     c                   endif
      *
     c                   endif
      *
      * Post skal ikke tas med dersom det er valgt rabattbehandling,
      * og hvor forutsetningen for å oppnå kontantrabatt er passert,
      * OG samtidig at HOVEDFORFALL på posten er STØRRE ENN utpl.dato
      * (t.o.m. forfallsdato) i parameterbildet:
      *
      * WPDATO ==  Laveste rabattdato.
      * WPOFOR ==  Opprinnelig forfallsdatobildet.
      * DSFÅMD ==  T.o.m. utplukksdato i fra parameterbildet.
      *
      * Tester først på at en av datoene for å få rabatt ennå ikke
      * er passert.  Dersom en av datoene tilfredstiller å bli
      * rabattbehandlet, så settes WPTEST til "J":
      *
     c                   eval      wptest = *blanks
      *
     c                   if        p_rda1 <> *loval and
     c                             p_rda1 >= dskÅmd
     c                   movel     'J'           wptest
     c                   endif
      *
     c                   if        p_rda2 <> *loval and
     c                             p_rda2 >= dskÅmd
     c                   movel     'J'           wptest
     c                   endif
      *
     c                   if        p_rda3 <> *loval and
     c                             p_rda3 >= dskÅmd
     c                   movel     'J'           wptest
     c                   endif
      *
      * Hopper til behandling av neste post dersom forfall på post
      * ligger lengre frem i tid, og samtidig at ev. rabattdatoer
      * er passert:
      *
     c                   if        wptest = *blanks and
     c                             wpofor > dsfÅmd
     c                   goto      xuttag
     c                   endif
      *
      * Nullstiller statusfelt for om faktura er rabattberegnet.
      *
     c                   eval      garabb = *blank
      *
      * Lagrer opprinnelig fakturabeløp.
      *
     c                   eval      gaopbn = 0 - rorest
     c                   eval      gaopbv = 0 - rovalr
      *
      * Lagrer opprinnelig forfallsdato.
      *
     c                   eval      gaopfd = rofdat
      *
      * Overfører felter.
      *
9.01 c***                eval      wpbfli = wpbfli + 10
     c                   eval      gastat = *blank
     c                   eval      gafirm = rofirm
     c                   eval      galevr = rolevr
     c                   eval      gabdat = robdat
     c                   eval      gatist = rotist
     c                   movel     rlnavn        ganavn
      *
      * De 6 siste posisjonene i navnefeltet inneholder leverandør-
      * nummeret, og skal således IKKE skrives ut.  Feltet brukes i
      * forbindelse med ALFA-sortering på leverandørnavn, for å
      * skille de leverandører som opererer under forskjellig
      * leverandørnummer.
      *
     c                   move      rllevr        ganavn
      *
     c                   movel     rovalk        gavalk
     c                   movel     rovalk        gafval
     c                   movel     roatts        gaatts
     c                   eval      gabilk = robilk
      *
      * Dersom det er valgt "2" i feltet "Avregne kreditnotaer" skal
      * ikke en ev. rabatt trekkes i fra posteringsbeløpet.  Dette
      * p.g.a. at forfallsdatoen senere vil kunne bli endret i rutinen
      * for avregning av kreditnotaer.
      *
     c                   if        a1raba = 1 and
     c                             a1avre <> 2
      *
     c                   if        wprbn1 <> 0 and
     c                             p_rda1 >= dskÅmd
      *
     c                   if        dsfÅmd = *loval or
     c                             dskÅmd <= p_rda1
     c                   eval      gaforf = p_rda1
     c                   move      gaforf        gafdat
     c                   eval      wprest = rorest - wprbn1
     c                   eval      wpvalr = rovalr - wprbv1
     c                   movel     'R'           garabb
     c                   endif
      *
     c                   else
      *
     c                   if        wprbn2 <> 0 and
     c                             p_rda2 >= dskÅmd
      *
     c                   if        dsfÅmd = *loval or
     c                             dskÅmd <= p_rda2
     c                   eval      gaforf = p_rda2
     c                   move      gaforf        gafdat
     c                   eval      wprest = rorest - wprbn2
     c                   eval      wpvalr = rovalr - wprbv2
     c                   movel     'R'           garabb
     c                   endif
      *
     c                   else
      *
     c                   if        wprbn3 <> 0 and
     c                             p_rda3 >= dskÅmd
      *
     c                   if        dsfÅmd = *loval or
     c                             dskÅmd <= p_rda3
     c                   eval      gaforf = p_rda3
     c                   move      gaforf        gafdat
     c                   eval      wprest = rorest - wprbn3
     c                   eval      wpvalr = rovalr - wprbv3
     c                   movel     'R'           garabb
     c                   endif
      *
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * Hopper ut dersom forfallsdato på post er yngre enn TIL DATO
      * i fra parameterbildet:  (Ikke ved rabattbehandling)
      *
     c                   if        garabb <> 'R'
     c     gaforf        cabgt     dsfÅmd        xuttag
     c                   endif
      *
     c                   eval      gabelØ = wprest
     c                   eval      garest = wprest
     c                   eval      gavalb = wpvalr
     c                   eval      gavalr = wpvalr
      *
      * Snur fortegn på beløp.
      *
     c                   eval      gabelØ = gabelØ * -1
     c                   eval      garest = garest * -1
     c                   eval      gavalb = gavalb * -1
     c                   eval      gavalr = gavalr * -1
      *
     c                   movel     robetk        gabetk
     c                   eval      garaar = roraar
     c                   eval      garper = rorper
     c                   eval      gajour = rojour
     c                   eval      gasamb = rosamb
     c                   eval      garefn = rorefn
      *
T5.01 * Leverandørens fakturanr. eller bilagstekst
      *
T5.01c                   eval      gatext = *blanks
6.11  *
6.11 c                   if        rolfak <> *blanks
6.11 c                   movel     rolfak        gatext
6.11 c                   else
     c                   movel     rotext        gatext
6.11 c                   endif
6.11  *
15037c                   movel     rolfak        galfak
      *
17108 ** Hvis fakturanummer er blank og kreditnota, legg inn default verdi
17108 *
6.12 c**                 if        rolfak = *blanks and
6.12 c**                           robelØ < 0
6.12 c**                 movel     '999999'      galfak
6.12 c**                 endif
      *
20028c                   if        rolfak <> *blanks
20028c                   movel     '*'           gakids
20028c                   endif
      *
     c                   movel     rovirk        gavirk
     c                   eval      garrf1 = rorrf1
     c                   eval      garrf2 = rorrf2
      *
     c                   if        rodeck = *blanks
     c                   movel     '14'          gadeck
     c                   else
     c                   movel     rodeck        gadeck
     c                   endif
      *
     c                   eval      gabfli = wpbfli
     c                   movel     rokidi        gakidi
      *
     c                   if        gakidi <> *blanks
     c                   exsr      xkidts
     c                   else
     c                   eval      gakids = *blanks
     c                   endif
      *
      * Venstrejusterer innhold av feltet:
      *
     c                   if        gakids = '*'
     c                   exsr      xvenst
     c                   endif
      *
20028c                   if        rolfak <> *blanks
20028c                   eval      gakids = '*'
20028c                   endif
      *
     c                   eval      gaavde = roavde
     c                   movel     rorekl        garekl
     c                   eval      gadelb = rodelb
     c                   movel     robetb        gabetb
      *
      * Rabattdato 1.
      *
     c                   eval      gabiln = robiln
      *
     c                   if        p_rda1 = *loval
     c                   move      *loval        garda1
     c                   else
     c                   move      p_rda1        garda1
     c                   endif
      *
      * Rabattdato 2.
      *
     c                   if        p_rda2 = *loval
     c                   move      *loval        garda2
     c                   else
     c                   move      p_rda2        garda2
     c                   endif
      *
      * Rabattdato 3.
      *
     c                   if        p_rda3 = *loval
     c                   move      *loval        garda3
     c                   else
     c                   move      p_rda3        garda3
     c                   endif
      *
      * Rabattbeløp 1, 2 og 3 i NOK og valuta.
      *
     c                   eval      garbn1 = wprbn1
     c                   eval      garbn2 = wprbn2
     c                   eval      garbn3 = wprbn3
     c                   eval      garbv1 = wprbv1
     c                   eval      garbv2 = wprbv2
     c                   eval      garbv3 = wprbv3
      *
      * Opprettelsesdato:
      *
     c                   eval      gaopda = wpopda
     c                   movel     l_user        gaopav
      *
      * Hopper ut dersom det er rabattbehandling av post, OG NÅR det
      * kan ventes med å ta denne med til senere....:
      *
07123c                   if        a1raba = 1 and
07123c                             garabb = 'R' and
07123c                             gafdat > dsfÅmd
07123c                   goto      xuttag
07123c                   endif
      *
      * Merker post på RLTRPF med forslagsnummer for å unngå at
      * den blir tatt med i ev. utplukk som kjøres i fra andre
      * terminaler.
      *
     c     key06         chain     rltrlu                             62
      *
      * Ikke tilslag.  (skal IKKE være tilfelle):
      *
     c                   if        *in62 = *on
     c                   goto      xuttag
     c                   endif
      *
     c                   if        *in62 = *off
     c                   move      robdat        dsbild
     c                   move      rofdat        dsforf
      *
      * Dersom forslagsnummer er utfylt, så er posten tatt med
      * i et annet utplukk.
      *
     c                   if        wtamed = 'JA '
     c                   eval      robfnr = 0
     c                   eval      robfli = 0
     c                   endif
      *
      * NB!  Posten skal likevel kunne bli tatt med dersom det
      *      er valgt "DIREM=1" i parameterbildet.
      *
     c                   if        robfnr <> 0
     c                   unlock    rltrlu
     c                   goto      xuttag
     c                   endif
      *
     c                   if        robfnr = 0
      *
      * Henter forslagsnummer en gang i løpet av utplukket,
      * d.v.s. når den første utplukksposten er klar:
      *
     c                   if        wplese = *blanks
      *
     c                   exsr      xforsl
      *
     c                   move      '*'           wplese
     c                   endif
      *
      * Legger forslagsnummer og forslagslinjenummer ut
      * på transaksjonsfile RLTRPF:
      *
     c                   eval      gabfnr = wpbfnr
     c                   eval      robfnr = wpbfnr
      *
      * Hjelpefelt som ble brukt til å mellomlagre opprinnelig
      * forfallsdato på transaksjon, legges nå tilbake:
      *
     c                   eval      dsforf = wpofor
      *
      * Legger ut dato, tid og bruker på leverandørposteringen
      *
V5.40c                   eval      rofoda = w_foda
V5.40c                   eval      roftim = w_ftim
V5.40c                   eval      rofsig = w_fsig
      *
     c                   update    rltrpfu
      *
     c                   endif
      *
     c                   endif
      *
      * Dersom IKKE KID, men det Lev.fak.nr. er utfylt, DA legges
      * det ut "S" i KID-feltet:  (S=Strukturert informasjon)
      *
22106c*** 6.10           if        gakids <> '*' and
6.10 c                   if        rokidi = *blanks and
22106c                             rolfak <> *blanks
22106c                   move      'S'           gakids
22106c                   endif
      *
      * Oppdaterer betalingsforslagsfile.
      *
9.01 c                   eval      wpbfli = wpbfli + 10
     c                   write     gabfpfu
      *
     c                   eval      wpanta = wpanta + 1
      *
     c                   if        wpanta = 15
     c                   eval      c1anta = c1anta + wpanta
     c                   write     c1win
     c                   eval      wpanta = 0
     c                   endif
      *
     c     xuttag        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tester om det er KID eller melding i KID-feltet.                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xkidts        begsr
      *
      * Initierer hjelpefelt.
      *
     c                   eval      v = 25
     c                   eval      wpblan = *blanks
     c                   eval      wpluft = *blanks
     c                   eval      wpnume = *blanks
     c                   movea     gakidi        kid
     c                   move      '*'           gakids
      *
     c                   dow       v >= 1
      *
      * Tester om blank etter at det har vært tall.
      *
     c                   if        wpnume = 'JA'
     c                             and kid(v) = *blank
     c                   move      'JA'          wpluft
     c                   endif
      *
      * Tester på om det er kun tall i KID-feltet som er adskilt
      * med blankt.  I såfall er det IKKE et KID-felt.
      * Hjelpefeltet WPLUFT indikerer at det har forekommet tall
      * og deretter blankt, lset i fra venstre mot høyre.
      * Testen må utføres f.o.m. 2. les, for å ha verdier å teste
      * i mot.
      *
      * Testen fanger opp flg. eksempel '1234 6 8 0 23 5 78901 345'.
      *
     c                   if        v <> 25 and
     c                             wpluft = 'JA' and
     c                             wpnume = 'JA' and
     c                             kid(v) >= '0' and
     c                             kid(v) <= '9'
     c                   eval      gakids = *blank
     c                   leave
     c                   endif
      *
      * Tester om blank.
      *
     c                   if        kid(v) = *blank
     c                   move      'JA'          wpblan
     c                   endif
      *
      * Tester om numerisk.
      *
     c                   if        kid(v) >= '0' and
     c                             kid(v) <= '9'
     c                   move      'JA'          wpnume
     c                   endif
      *
      * Tester om alfa, d.v.s. IKKE numerisk, IKKE blank og
      * IKKE -. "-" skal godkjennes som innhold av KID-felt.
      *        Eks: 'A1342872342389B891H998235'
      *
     c                   if        kid(v) <> *blank
     c                   if        kid(v) <> '-'
     c                   if        kid(v) < '0' or
     c                             kid(v) > '9'
     c                   eval      gakids = *blank
     c                   leave
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      v = v - 1
     c                   enddo
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Venstrejusterer innhold av KID-feltet dersom KID:               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xvenst        begsr
      * Initierer hjelpefelt.
      *
     c                   movea     gakidi        kid
      *
     c                   do        24
      *
      * Tester om KID,V er blank, i såfall flyttes "alt"
      * en posisjon til venstre i feltet:
      *
     c                   if        kid(1) = *blank
     c                   eval      w = 1
      *
     c                   dow       w <= 24
     c                   eval      x = w + 1
     c                   move      kid(x)        kid(w)
      *
     c                   if        x = 25
     c                   eval      kid(x) = *blank
     c                   endif
      *
     c                   eval      w = w + 1
     c                   enddo
      *
     c                   else
     c                   leave
     c                   endif
      *
     c                   enddo
      *
      * Legger tilbake venstrejustert KID:
      *
     c                   movea     kid           gakidi
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avregner kreditnotaer mot fakturaer.                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xavreg        begsr
      *
      * Legger "ventetekst" ut i skjermbildet.
      *
     c                   if        a1raba = 1
     c                   movel     txt(2)        c1txt2
     c                   else
     c                   movel     txt(2)        c1txt1
     c                   endif
      *
     c                   write     c1win
      *
      * Ved "1" skal flest mulige kreditnotaer avregnes på forfalls-
      * dato til fakturaen.
      *
     c                   if        a1avre = 1
     c                   exsr      xavre1
     c                   endif
      *
      * Ved "2" vil alle fakturaer få forfallsdato lik den
      * høyeste forfallsdato, og deretter avregne fakturaene.
      *
     c                   if        a1avre = 2
     c                   exsr      xavre2
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for avregning av kreditnota mot faktura.  V A L G = 1 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xavre1        begsr
      *
     c     avre1a        tag
      *
      * Legger leverandørnummer, forfallsdato, valutakode og KID-
      * status over i KEY03.
      *
     c                   eval      wplevr = wplev1
     c                   move      wpfor1        w_fdat
     c                   movel     wpval1        wpvalk
     c                   movel     wpkid1        wpkids
      *
      * Posisjonerer på Firma, Forslagsnummer og leverandør(=null).
      *
     c     key03         setll     gubfl1
      *
      * Nullstiller summeringsfelter for NOK og Valuta.
      *
     c                   eval      wpsun1 = 0
     c                   eval      wpsuv1 = 0
     c                   eval      wpsun2 = 0
     c                   eval      wpsuv2 = 0
      *
      * Starter les av alle fakturaposter i fra utbetalingsfilen.
      *
     c     avre1b        tag
      *
     c     key04c        reade     gubfl1                                 60
      *
      * Avslutter dersom EOF og samtidig av sum fakturabeløp = 0
      * I såfall er det ingen fakturaposter å avregne.
      *
     c                   if        *in60 = *on
     c                             and wpsun1 = 0
     c                   goto      avre1c
     c                   endif
      *
      * Tester om End Of File ELLER Ny leverandør ELLER Ny forfallsdato.
      * ELLER Ny valutatype ELLER Ny KID-status (Med/Uten KID på post)
      * I såfall må alle kreditnotaer leses for foregående sum av
      * fakturaer.  I påfølgende test har feltene WPVALI og WPKIDI
      * kun betydning for å fange opp første les av GUBFL2.
      *
     c                   if        *in60 = *on or
     c                             galevr <> wplev1 and
     c                             wplev1 <> 0 or
27054c**********************       gaforf <> wpfor1 and
27054c                             gafdat <> wpfor1 and
     c                             wpfor1 <> *loval or
     c                             gavalk <> wpval1 and
     c                             wpvali <> 'INI' or
     c                             gakids <> wpkid1 and
     c                             wpkidi <> 'I'
      *
      * Alle kreditnotaer vil nå bli hentet opp i et forsøk på å
      * bli avregnet mot fakturaen(e). Lev.nr legges inn i KEY5.
      *
     c                   eval      wplev5 = wplev1
     c                   eval      dsforf = wpfor1
     c                   movel     wpval1        wpval5
     c                   movel     wpkid1        wpkid5
      *
      * Legger over verdier i fra sist leste post.
      * Posten som nå allerede er lest fra GUBFL1 indikerer brudd
      * på enten Leverandør, Forfallsdato, Valutakode eller KID-
      * status.  Verdiene vil være startverdi for videre les på
      * GUBFL1 når det er ferdig med søk etter kreditnota.
      *
     c                   eval      wplev1 = galevr
27054c****************** eval      wpfor1 = gaforf
27054c                   eval      wpfor1 = gafdat
     c                   movel     gavalk        wpval1
     c                   movel     gakids        wpkid1
      *
     c     key04         setll     gubfl2
     c     key04         reade     gubfl2                                 61
      *
      * Det testes på at kreditnota som hentes opp har lik valuta-
      * kode og KID-status som faktura den skal avregnes i mot.
      *
     c                   dow       *in61 = *off
      *
     c                   if        gavalk = wpval5 and
     c                             gakids = wpkid5
      *
     c                   eval      wpsun2 = wpsun2 - gabelØ
     c                   eval      wpsuv2 = wpsuv2 - gavalb
      *
      * Endrer forfallsdato på posten dersom fortsatt positivt
      * beløp, og at forfallsdato på post er ulik den det skal
      * endres til:
      *
     c                   if        wpsun2 <= wpsun1 and
27054c*****************            gaforf <> nyforf
27054c                             gafdat <> nyforf
      *
     c                   delete    gabfpf2
      *
27054c*****************  eval      gaforf = nyforf
27054c                   eval      gafdat = nyforf
27054c*****************  move      gaforf        gafdat
     c                   movel     'A'           gastat
      *
     c                   write     gabfpf2
      *
     c                   eval      gastat = *blank
     c                   endif
      *
      * Lest kreditnota kan ikke brukes, da totalsum blir
      * negativt. Beløpet blir trukket ut igjen i fra sum.
      *
     c                   if        wpsun2 > wpsun1
     c                   eval      wpsun2 = wpsun2 + gabelØ
     c                   eval      wpsuv2 = wpsuv2 + gavalb
     c                   endif
      *
     c                   endif
      *
     c     key04         reade     gubfl2                                 61
      *
     c                   enddo
      *
      * Alle fakturaer og kreditnotaer er behandlet.
      *
     c                   if        *in60 = *on and
     c                             *in61 = *on
     c                   goto      avre1c
     c                   endif
      *
     c                   else
      *
      * Legger over Leverandør, Forfallsdato, Valutakode og KID-status.
      * til hjelpefelter.  Feltene inneholder til enhver tid info.
      * om foregående lest post i fra GUBFL1.
      *
     c                   eval      wplev1 = galevr
27054c*****************  eval      wpfor1 = gaforf
27054c                   eval      wpfor1 = gafdat
     c                   movel     gavalk        wpval1
     c                   movel     gakids        wpkid1
      *
27054c*****************  eval      nyforf = gaforf
27054c                   eval      nyforf = gafdat
      *
      * WPVALI (initiell valutakode) og WPKIDI (initiell KID-
      * status) blankes ut.  De har kun verdi i forbindelse med
      * første les på GUBFL1, for å unngå brudd.
      *
     c                   if        wpvali = 'INI'
     c                   eval      wpvali = *blanks
     c                   eval      wpkidi = *blanks
     c                   endif
      *
      * Det er ikke EOF eller Ny Leverandør.  Posten blir derfor
      * sumert dersom den er en fakturatransaksjon.
      *
     c                   if        gabelØ > 0
     c                   eval      wpsun1 = wpsun1 + gabelØ
     c                   eval      wpsuv1 = wpsuv1 + gavalb
     c                   endif
      *
     c                   goto      avre1b
      *
     c                   endif
      *
      * Gå til AVRE1A for å lese inn poster på ny forfallsdato
      *
     c                   goto      avre1a
      *
     c     avre1c        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for avregning av kreditnota mot faktura.  V A L G = 2 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xavre2        begsr
      *
     c     avre2a        tag
      *
      * Legger leverandørnummer, forfallsdato, valutakode og KID-
      * status over i KEY04.
      *
     c                   eval      wplev5 = wplev1
     c                   movel     wpval1        wpvalk
     c                   movel     wpkid1        wpkids
     c                   eval      w_fdat1 = *loval
      *
      * Posisjonerer på Firma, Forslagsnummer, Leverandørnummer,
      * Forfallsdato, Valutakode og KID-status.  1. gang er kun
      * firmanummer og forslagsnummer utfylt:
      *
     c     key04b        setll     gubfl2
      *
      * Nullstiller summeringsfelter for NOK og Valuta.
      *
     c                   eval      wpsun1 = 0
     c                   eval      wpsuv1 = 0
     c                   eval      wpsun2 = 0
     c                   eval      wpsuv2 = 0
      *
      * Starter les av alle kreditposter i fra utbetalingsfilen.
      * Leser med nøkkel på Firma/Forslagsnummer.
      *
     c     avre2b        tag
      *
     c     key04c        reade     gubfl2                                 60
      *
      * Avslutter dersom EOF og samtidig at sum fakturabeløp = 0
      * I såfall er det ingen fakturaposter å avregne.
      *
     c                   if        *in60 = *on
     c                             and wpsun1 = 0
     c                   goto      avre2c
     c                   endif
      *
      * Tester om End Of File ELLER Ny leverandør ELLER Ny valutakode
      * ELLER Ny KID-status.  I såfall skal fakturaene leses ên og ên,
      * (som tilfredstiller nøkkelbegrepet!) inntil sum av fakturaene
      * er større eller lik sum kreditnotaene.
      *
     c                   if        *in60 = *on or
     c                             galevr <> wplev1 and
     c                             wplev1 <> 0 or
     c                             gavalk <> wpval1 and
     c                             wpvali <> 'INI' or
     c                             gakids <> wpkid1 and
     c                             wpkidi <> 'I'
      *
      * Legger over verdier som skal brukes i KEY03, for å hente opp
      * fakturaposter.
      *
     c                   eval      w_fdat = *loval
     c                   eval      wplevr = wplev1
     c                   movel     wpval1        wpvalk
     c                   movel     wpkid1        wpkids
      *
      * Legger over verdier i fra sist leste post.  Posten har
      * indikert enten EOF ELLER brudd på Leverandør ELLER Valuta-
      * type ELLER KID-status.  Dersom ikke EOF vil dette være neste
      * post som skal "posisjoneres" etter dette fakturasøket.
      *
     c                   eval      wplev1 = galevr
     c                   eval      wpfor1 = gaforf
     c                   movel     gavalk        wpval1
     c                   movel     gakids        wpkid1
      *
     c     key03         setll     gubfl1
     c     key04         reade     gubfl1                                 61
      *
     c                   dow       *in61 = *off
      *
      * Avslutter søk etter fakturaer dersom feil Leverandør, men
      * fortsetter les ved feil Valutakode ELLER feil KID-status.
      *
     c     galevr        cabne     wplev5        avre2a
      *
     c                   if        gavalk = wpval5 and
     c                             gakids = wpkid5
      *
      * OK, fakturapost.
      *
     c                   if        gabelØ > 0
     c                   eval      nyforf = gaforf
     c                   eval      wpsun2 = wpsun2 + gabelØ
     c                   eval      wpsuv2 = wpsuv2 + gavalb
     c                   endif
      *
30017c                   unlock    gubfl2
      *
      * Dersom WPSUN2 er større eller lik WPSUN1 skal alle
      * leste kreditnotaer og fakturaer merket med "X" få
      * endret forfallsdato til sist leste faktura.
      *
     c                   if        wpsun2 >= wpsun1
     c     key04         setll     gubfl3
     c     key04         reade     gubfl3                                 63
      *
     c                   dow       *in63 = *off
      *
     c                   if        gastat = 'X'
      *
     c                   delete    gabfpf3
      *
     c                   eval      gastat = *blank
     c                   eval      gaforf = nyforf
     c                   move      gaforf        gafdat
      *
     c                   if        gabelØ < 0
     c                   movel     'A'           gastat
     c                   endif
      *
     c                   write     gabfpf3
      *
     c                   eval      gastat = *blank
      *
     c                   else
      *
     c                   unlock    gubfl3
     c                   endif
      *
     c     key04         reade     gubfl3                                 63
     c                   enddo
      *
     c                   if        *in63 = *on
     c                   goto      avre2a
     c                   endif
     c                   endif
      *
      * Merker faktura med "X" for å indikerer at den senere
      * skal endre forfallsdato til den dato som det finnes frem
      * til i dette fakturasøket.
      *
     c                   if        gabelØ > 0
      *
     c     key05         setll     gubfl3
     c     key05         reade     gubfl3                                 63
      *
     c                   if        *in63 = *off
     c                   movel     'X'           gastat
      *
     c                   update    gabfpf3
      *
     c                   eval      gastat = *blank
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
     c     key04         reade     gubfl1                                 61
      *
     c                   enddo
      *
      * Alle fakturaer og kreditnotaer er behandlet.
      *
     c                   if        *in60 = *on and
     c                             *in61 = *on
     c                   goto      avre2c
     c                   endif
      *
     c                   else
      *
      * Tester på at det må leses kreditnotaer pr. Leverandør/
      * Valutakode og KID-status, og hvor verdien i feltene må være
      * lik verdien i tilsvarende felter for foregående lest post.
      * Til info:  Feltet WPLEV1 vil være 0 kun v/første les!
      *
     c                   if        galevr = wplev1 and
     c                             gavalk = wpval1 and
     c                             gakids = wpkid1 or
     c                             wplev1 = 0
      *
      * Legger over verdier i fra sist leste post  til WPLEV1.
      * Dette vil være neste leverandør som skal behandles
      * etter at søk etter fakturaer (over) skal avsluttes.
      * WPLEV5 vil bli brukt i KEY04 under fakturasøket ved
      * endring av forfallsdato.
      *
     c                   eval      wplev1 = galevr
     c                   eval      wplev5 = galevr
     c                   eval      wpfor1 = gaforf
     c                   movel     gavalk        wpval1
     c                   movel     gavalk        wpval5
     c                   movel     gakids        wpkid1
     c                   movel     gakids        wpkid5
      *
      * WPVALI (initiell valutakode) og WPKIDI (initiell KID-
      * status) blankes ut.  De har kun verdi i forbindelse med
      * første les på GUBFL1, for å unngå brudd.
      *
     c                   if        wpvali = 'INI'
     c                   eval      wpvali = *blanks
     c                   eval      wpkidi = *blanks
     c                   endif
      *
      * Merker kreditnota med "X" for å indikerer at den senere
      * skal endre forfallsdato til den dato som det finnes frem
      * til i fakturasøket (over).
      *
     c                   if        *in60 = *off
     c                   movel     'X'           gastat
      *
     c                   update    gabfpf2
      *
     c                   eval      gastat = *blank
     c                   endif
      *
      * Summerer kreditnotaene.
      *
     c                   eval      wpsun1 = wpsun1 - gabelØ
     c                   eval      wpsuv1 = wpsuv1 - gavalb
      *
     c                   endif
      *
     c                   goto      avre2b
      *
     c                   endif
      *
      * Gå til AVRE2A for å lese kreditnota(er) på ny leverandør
      *
     c                   goto      avre2a
      *
     c     avre2c        tag
      *
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift av forslag til utbetaling.               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xrappo        begsr
      *
      * Legger "ventetekst" ut i skjermbildet.
      *
      * Rabattberegning og avregning av kreditnota mot faktura er
      * ikke valgt.
     c                   if        a1raba = 0 and
     c                             a1avre = 0
     c                   movel     txt(3)        c1txt1
     c                   endif
      * Enten rabattberegning eller avregning av kreditnota mot faktura
      * er valgt.
     c                   if        a1raba = 0 and
     c                             a1avre <> 0
     c                   movel     txt(3)        c1txt2
     c                   endif
      *
     c                   if        a1raba <> 0 and
     c                             a1avre = 0
     c                   movel     txt(3)        c1txt2
     c                   endif
      * Både rabattberegning og avregning av kreditnota mot faktura
      * er valgt.
     c                   if        a1raba <> 0 and
     c                             a1avre <> 0
     c                   movel     txt(3)        c1txt3
     c                   endif
      *
     c                   write     c1win
      *
      * Skriver ut parametersiden på rapport.
      *
     c     *dmy          move      dskÅmd        d1kjØr
     c     *dmy          move      dsfÅmd        d1forf
      *
     c                   eval      d1atts = a1atts
     c                   eval      d1betk = a1betk
     c                   eval      d1raba = a1raba
     c                   eval      d1avre = a1avre
     c                   write     a1par
      *
      * Skriver ut leverandør- eller kategoriintervallene som er
      * valgt:
      *
     c                   xfoot     levt          xfootl
     c                   xfoot     katt          xfootk
      *
      * Leverandørintervall:
      *
     c                   if        xfootl > 0
     c                   write     l1par
      *
     c     1             do        10            i
     c                   eval      v = i
     c                   eval      lk1f = levf(v)
     c                   eval      lk1t = levt(v)
      * Elemente 11:
     c                   eval      v = v + 10
     c                   eval      lk2f = levf(v)
     c                   eval      lk2t = levt(v)
      * Elemente 21:
     c                   eval      v = v + 10
     c                   eval      lk3f = levf(v)
     c                   eval      lk3t = levt(v)
      * Elemente 31:
     c                   eval      v = v + 10
     c                   eval      lk4f = levf(v)
     c                   eval      lk4t = levt(v)
      * Elemente 41:
     c                   eval      v = v + 10
     c                   eval      lk5f = levf(v)
     c                   eval      lk5t = levt(v)
      *
     c                   write     l1det
      *
     c                   enddo
     c                   endif
      *
      * Kategoriintervall:
      *
     c                   if        xfootk > 0
     c                   write     k1par
      *
     c     1             do        10            i
     c                   eval      v = i
     c                   eval      kk1f = katf(v)
     c                   eval      kk1t = katt(v)
      * Elemente 11:
     c                   eval      v = v + 10
     c                   eval      kk2f = katf(v)
     c                   eval      kk2t = katt(v)
      * Elemente 21:
     c                   eval      v = v + 10
     c                   eval      kk3f = katf(v)
     c                   eval      kk3t = katt(v)
      * Elemente 31:
     c                   eval      v = v + 10
     c                   eval      kk4f = katf(v)
     c                   eval      kk4t = katt(v)
      * Elemente 41:
     c                   eval      v = v + 10
     c                   eval      kk5f = katf(v)
     c                   eval      kk5t = katt(v)
      *
     c                   write     k1det
      *
     c                   enddo
     c                   endif
      *
      * Oppkall av rapportprogram.
      *
      * Verdien " " i l_chgv forteller at formularparametre kun skal
      * hentes opp, men ikke vedlikeholdes.
      *
     c                   movel     ' '           l_chgv
     c                   out       *dtaara
      *
      * Skriver ut betalingsforslaget:
      *
     c                   call      'GL102C'
     c                   parm                    dslist
     c                   parm                    wpin12
     c                   parm                    wpparm
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å hente neste forslagnummer.                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xforsl        begsr
      *
     c     l_firm        chain     gfaslu                             62
      *
     c                   if        *in62 = *off
      *
08095c                   if        gefubf = 999
08095c                   eval      gefubf = 0
08095c                   endif
      *
     c                   eval      gefubf = gefubf + 1
     c                   eval      wpbfnr = gefubf
      *
     c                   update    gfaslur
      *
     c                   else
      *
     c                   eval      geffir = l_firm
     c                   eval      gefubf = 1
     c                   eval      wpbfnr = gefubf
      *
     c                   write     gfaslur
      *
     c                   endif
      *
     c                   endsr
      *
      /eject
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Finner spool som er generert av jobben
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     spoolnbr      begsr
6.30  /Free
6.30
6.30           QSPRILSP( SPRL0100
6.30                   : %size(SPRL0100)
6.30                   : 'SPRL0100'
6.30                   : errorcode );
6.30  /End-free
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Kaller eget program for generering av xml fil. Eget pgm         *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     xml_create    begsr
6.30  *
6.30 c                   eval      splfname2 = *blanks
6.30 c                   eval      splfname3 = *blanks
6.30 c                   eval      jobname2 = *blanks
6.30 c                   eval      jobname3 = *blanks
6.30 c                   eval      username2 = *blanks
6.30 c                   eval      username3 = *blanks
6.30 c                   eval      jobnbr2 = *blanks
6.30 c                   eval      jobnbr3 = *blanks
6.30 c                   eval      splfnbr2 = *zeros
6.30 c                   eval      splfnbr3 = *zeros
6.30 c                   eval      p_btyp = 'UTBETALINGSFORSLAG'
6.30 c                   eval      p_dspl = 'Utbetalingsforslag - parametre '+
6.30 c                                      %char(d1kjØr)
6.30 c                   eval      p_refn = %char(w_year)+%char(d1kjØr)
6.30 c                   eval      p_tagg0 = 'KJOREDATO'
6.30 c                   eval      p_tagg0val = %char(d1kjØr)
6.30 c                   eval      p_tagg1 = 'FORFALLSDATO'
6.30 c                   eval      p_tagg1val = %char(d1forf)
6.30 c                   eval      p_tagg2 = *blanks
6.30 c                   eval      p_tagg2val = *blanks
6.30 c                   eval      p_tagg3 = *blanks
6.30 c                   eval      p_tagg3val = *blanks
6.30 c                   eval      p_tagg4 = *blanks
6.30 c                   eval      p_tagg4val = *blanks
6.30 c                   eval      p_tagg5 = *blanks
6.30 c                   eval      p_tagg5val = *blanks
6.30 c                   eval      p_tagg6 = *blanks
6.30 c                   eval      p_tagg6val = *blanks
6.30 c                   eval      p_tagg7 = *blanks
6.30 c                   eval      p_tagg7val = *blanks
6.30 c                   eval      p_tagg8 = *blanks
6.30 c                   eval      p_tagg8val = *blanks
6.30 c                   eval      p_tagg9 = *blanks
6.30 c                   eval      p_tagg9val = *blanks
      *
6.30 c                   call      'AP627R'
6.30 c                   PARM                    splfname
6.30 c                   PARM                    jobname
6.30 c                   PARM                    username
6.30 c                   PARM                    jobnbr
6.30 c                   PARM                    splfnbr
6.30 c                   PARM                    splfname2
6.30 c                   PARM                    jobname2
6.30 c                   PARM                    username2
6.30 c                   PARM                    jobnbr2
6.30 c                   PARM                    splfnbr2
6.30 c                   PARM                    splfname3
6.30 c                   PARM                    jobname3
6.30 c                   PARM                    username3
6.30 c                   PARM                    jobnbr3
6.30 c                   PARM                    splfnbr3
6.30 c                   parm                    p_tagg0
6.30 c                   parm                    p_tagg0val
6.30 c                   parm                    p_tagg1
6.30 c                   parm                    p_tagg1val
6.30 c                   parm                    p_tagg2
6.30 c                   parm                    p_tagg2val
6.30 c                   parm                    p_tagg3
6.30 c                   parm                    p_tagg3val
6.30 c                   parm                    p_tagg4
6.30 c                   parm                    p_tagg4val
6.30 c                   parm                    p_tagg5
6.30 c                   parm                    p_tagg5val
6.30 c                   parm                    p_tagg6
6.30 c                   parm                    p_tagg6val
6.30 c                   parm                    p_tagg7
6.30 c                   parm                    p_tagg7val
6.30 c                   parm                    p_tagg8
6.30 c                   parm                    p_tagg8val
6.30 c                   parm                    p_tagg9
6.30 c                   parm                    p_tagg9val
6.30 c                   parm                    l_bach
6.30 c                   parm                    p_btyp
6.30 c                   parm                    p_refn
6.30 c                   parm                    p_dspl
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     pdf_preview   begsr
6.30 c                   if        l_skje = 1
6.30  * Vi kaller program for launch av PDF i browser
6.30 c                   call      'AP621R'
6.30 c                   parm                    l_bach
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Ved "R" kjøres det vanlig utplukk av leverandørtransaksjoner.
      * Ved "L" overføres lønnstransaksjoner fra 3PLØNN
      * til utbetalingsforslag:
      *
     c     *entry        plist
     c                   parm                    wpparm
     c                   parm                    wpuser
     c                   parm                    wwbfnr
      *
     c     *dtaara       define    *lda          lda
      *
      * Initierer hjelpefelt som brukes i subrutine XKIDTS for å
      * teste på om det innholdet i KID-feltet er KID eller alfa.
      *
     c                   eval      wpnume = *blanks
     c                   eval      wpblan = *blanks
     c                   eval      wpluft = *blanks
     c                   eval      v = 0
      *
6.30 c                   open      gl100p
6.30 c                   eval      w_year = *year
     c                   eval      wpdato = *loval
      *
      * Hjelpefelt for å fortelle om post skal rabattberegnes:
      *
     c                   move      '0'           wprabb
      *
      * Formularparametre skal IKKE endres, kun hentes opp!
      *
     c                   eval      wpvalg = *blanks
      *
      * Initierer hjelpevariable.
      *
     c                   eval      wpfirm = l_firm
     c                   eval      wpin03 = 0
     c                   eval      wpin12 = 0
     c                   eval      i = 0
     c                   eval      x = 0
     c                   eval      w = 0
     c                   eval      xfootl = 0
     c                   eval      xfootk = 0
      *
      * Det sendes varsel dersom det velges å ta med poster som
      * allerede er overført.  WPMSGS brukes for å sende opp
      * varselet kun en gang:
      *
     c                   movel     'NEI'         wpmsgs
      *
      * Felt som inneholder den nye forfallsdato dersom det er valgt
      * "1" i feltet for automatisk avregning av kreditnota mot fakt.
      *
     c                   eval      nyforf = *loval
      *
      * Initierer hjelpevariable som brukes til å hente forslagsnummer
      * kun en gang fra forslagsfile:
      *
     c                   eval      wplese = *blank
      *
      * Initierer hjelpevariable som brukes i rutinen for automatisk
      * avregning av kreditnota mot faktura.
      *
     c                   eval      wpsun1 = 0
     c                   eval      wpsuv1 = 0
     c                   eval      wpsun2 = 0
     c                   eval      wpsuv2 = 0
      *
      * Hjelpefelter for avregning av kreditnota mot faktura.
      * For WPKIDI og WPVALI legges det inn "INI" som initiell-
      * verdi, d.v.s. startverdi.
      *
     c                   movel     'I'           wpkidi
     c                   movel     'INI'         wpvali
     c                   eval      wpkid1 = *blanks
     c                   eval      wpval1 = *blanks
     c                   eval      wpkid5 = *blanks
     c                   eval      wpval5 = *blanks
      *
      * Beløpsfelt for rabattberegning.
      *
      * Brukes til beregning av rabattbeløp.
      *
     c                   eval      wpklad = 0
      *
      * Restsaldo på transaksjon FØR rabattberegning starter.
      *
     c                   eval      wprest = 0
     c                   eval      wpvalr = 0
      *
     c                   eval      w_arow = 2
     c                   eval      w_acol = 43
      *
      *
     c                   eval      wpanta = 0
     c                   eval      wpbfnr = 0
     c                   eval      wpbfli = 0
      *
      * Legger over dagens dato i parameterfelt for forfall.
      *
     c                   eval      a1forf = udate
      *
      * Lagrer dagens dato på file:
      *
     c                   time                    wpopda
      *
      * Legger over dagens dato i parameterfelt for kjøredato.
      *
     c                   eval      a1kjØr = udate
      *
      * Legger over dagens dato i parameterfelt:
      *
     c                   time                    dsudat
      *
      * Hjelpefelt for mellomlagring av opprinnelig
      * forfallsdato på transaksjon fra RLTRPF:
      *
     c                   eval      wpofor = *loval
      *
      * Nullstiller "4040404" for flg. felter:
      *
     c                   eval      robdat = *loval
     c                   eval      wpfor1 = *loval
      *
      * Key for avtaleregister:
      *
     c     key00         klist
     c                   kfld                    l_fgrp
     c                   kfld                    l_firm
      *
      * Key for leverandørfile
      *
     c     key01         klist
     c                   kfld                    rofirm
     c                   kfld                    rolevr
      *
      * Key for koderegister m/betalingsbetingelse.
      *
     c     key02         klist
     c                   kfld                    rofirm
     c                   kfld                    robetb
      *
      * Key for file "Utbetalingsforslag" GUBFL1, og brukes i
      * forbindelse med avregning av kreditnota mot faktura v/VALG=1.
      *
     c     key03         klist
     c                   kfld                    l_firm
     c                   kfld                    wpbfnr
     c                   kfld                    wplevr
     c                   kfld                    w_fdat
     c                   kfld                    wpvalk
     c                   kfld                    wpkids
      *
      * Key for file "Utbetalingsforslag" GUBFL2, og brukes i
      * forbindelse med avregning av kreditnota mot faktura.
      *
     c     key04         klist
     c                   kfld                    l_firm
     c                   kfld                    wpbfnr
     c                   kfld                    wplev5
      *
      * Key for file "Utbetalingsforslag" GUBFL2, og brukes i
      * forbindelse med avregning av kreditnota mot faktura, VALG=2.
      * Nøkkelen inneholder informasjon om "sist leste" post.
      *
     c     key04b        klist
     c                   kfld                    l_firm
     c                   kfld                    wpbfnr
     c                   kfld                    wplev1
     c                   kfld                    w_fdat1
     c                   kfld                    wpval1
     c                   kfld                    wpkid1
      *
      * Key for file "Utbetalingsforslag", og brukes for å kunne lese
      * på firma og forslagsnummer.
      *
     c     key04c        klist
     c                   kfld                    l_firm
     c                   kfld                    wpbfnr
      *
      * Key for file "Utbetalingsforslag" GUBFL3, og brukes i
      * forbindelse med avregning av kreditnota mot faktura v/VALG=2,
      * for å merke faktura med "X" + videre endre forfallsdato.
      *
     c     key05         klist
     c                   kfld                    gafirm
     c                   kfld                    gabfnr
     c                   kfld                    galevr
     c                   kfld                    gafdat
     c                   kfld                    gavalk
     c                   kfld                    gakids
     c                   kfld                    gabelØ
     c                   kfld                    gabdat
     c                   kfld                    gatist
      *
      * Key mot file "Leverandørtransaksjoner" (RLTRPF), og brukes
      * for å legge ut forslagsnummer på filen.  Dette må gjøres for
      * å merke faktura, slik at den ikke blir tatt med i ev. utplukk
      * som kjøres i fra andre terminaler.
      *
     c     key06         klist
     c                   kfld                    l_firm
     c                   kfld                    rolevr
     c                   kfld                    robdat
     c                   kfld                    rotist
      *
      * Key for å kontrollere på utbet.forslag om post er tatt med
      * på utbet.forslag tildigere.
      *
     c     key07         klist
     c                   kfld                    l_firm
     c                   kfld                    robfnr
     c                   kfld                    robfli
      *
     c                   movel     'JA '         wtamed
      *
      * Henter firmaopplysninger, med test på at bankkontonummer
      * er utfylt:
      *
     c     l_firm        chain     afirl1                             60
      *
     c                   if        *in60 = *on or
     c                             aafbgi = 0
     c                   move      *on           *in40
     c                   endif
      *
      * Tester på at faste opplysninger er utylt:
      *
     c     l_firm        chain     gfaslu                             60
      *
     c                   if        *in60 = *on
     c                   move      *on           *in41
     c                   endif
      *
      * Tester på at avtale finnes på avtaleregister:
      *
     c     key00         chain     gavtl2                             60
      *
     c                   if        *in60 = *on
     c                   move      *on           *in42
     c                   endif
      *
V5.40c                   time                    w_foda
V5.40c                   time                    w_ftim
V5.40c                   eval      w_fsig = l_user
      *
     c                   endsr
      /eject
**
* Rabattbehandling               x
* Avregner kreditnota            x
* Utskrift                       x
SKATTEOVERFØRING                 x
