     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : BL710R
6.20  * Beskrivelse . . : Henter varetype,utg.dato og lev.nr
      *                   fra lager
      *
      *                   Input : firma, vare, lager
      *                   Output: varetype, utg.dato, lev.nr
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       101108 bof  Nytt program
6.11  *       310111 bof  Hvis ikke lagerstyring, test kun mot vvarpf
6.20  *       190511 bof  Utvidet med utg.dato og inlr
6.30  *       250118 bof  utvidet med test på nobb.utg.dato vvnuda
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.11 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
      * Definering av variable i nøkler
      *
     d vlagl1_firm     s                   like(vlfirm)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
6.11 d fstsl1_firm     s                   like(faafir)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_lage          s                   like(vllage)
     d p_vtyp          s                   like(vlvtyp)
6.20 d p_udat          s                   like(vludat)
6.20 d p_ldor          s                   like(vlbldo)
6.20 d p_inlr          s              1
      *
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Initierer output-parametre
      *
     c                   eval      p_vtyp = *blank
6.20 c                   eval      p_udat = *loval
6.20 c                   eval      p_ldor = 0
6.11  *
6.11 c                   if        faalag = 1
      *
      * Henter prisgruppe
      *
     c                   eval      vlagl1_firm = p_firm
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found(vlagl1)
     c                   if        vlvtyp <> *blank
     c                   eval      p_vtyp = vlvtyp
     c                   endif
6.20 c                   if        vludat <> *loval
6.20 c                   eval      p_udat = vludat
6.20 c                   endif
6.20 c                   if        vlbldo <> 0
6.20 c                   eval      p_ldor = vlbldo
6.20 c                   endif
     c                   endif
6.11  *
6.11 c                   endif
      *
     c                   if        p_vtyp = *blank
     c                   eval      vvarl1_firm = p_firm
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      p_vtyp = vvvtyp
     c                   endif
     c                   endif
6.20  *
6.20 c                   if        p_udat = *loval
6.20 c                   eval      vvarl1_firm = p_firm
6.20 c                   eval      vvarl1_vare = p_vare
6.20 c     vvarl1_key    chain     vvarl1
6.20 c                   if        %found(vvarl1)
6.20 c                   eval      p_udat = vvudat
6.30 c                   if        p_udat = *loval
6.30 c                   eval      p_udat = vvnuda
6.30 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20 c                   if        p_ldor = 0
6.20 c                   eval      vvarl1_firm = p_firm
6.20 c                   eval      vvarl1_vare = p_vare
6.20 c     vvarl1_key    chain     vvarl1
6.20 c                   if        %found(vvarl1)
6.20 c                   eval      p_ldor = vvldor
6.20 c                   endif
6.20 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
6.20  *
6.20 c                   if        p_inlr <> *on
6.20 c                   eval      *inlr = *on
6.20 c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_vtyp
6.20 c                   parm                    p_udat
6.20 c                   parm                    p_ldor
6.20 c                   parm                    p_inlr
      *
      * Key for les på lager-register
      *
     c     vlagl1_key    klist
     c                   kfld                    vlagl1_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for les på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
6.11  *
6.11  * Key for oppslag på OFAK-kode (til)
6.11  *
6.11 c     fstsl1_key    klist
6.11 c                   kfld                    fstsl1_firm
6.11  *
6.11  *
6.11 c                   eval      fstsl1_firm = p_firm
6.11 c     fstsl1_key    chain     fstsl1                             91
      *
     c                   endsr
