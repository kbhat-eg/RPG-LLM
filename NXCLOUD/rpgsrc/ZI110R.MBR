     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : OFAK
      * Program . . . . : ZI110R Kalkulerer prispåslag
      * Beskrivelse . . : Vare, henter påslagsfaktor på kostpris
      *                   NB Gjelder foreløpig bare varer i EVR.
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160818 bsa  Nytt program
      *
8.01  *PRG2   140622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fzkapiu    uf a e           k disk    rename(zkapst:zkapiur)
     fzvpri1    if   e           k disk    rename(zvprst:zvpri1r)
     fzpfair    if   e           k disk    rename(zpfast:zpfairr)
     fzpfai2    if   e           k disk    rename(zpfast:zpfai2r)
     fzpfai3    if   e           k disk    rename(zpfast:zpfai3r)
     fzpfai4    if   e           k disk    rename(zpfast:zpfai4r)
     fzpfai5    if   e           k disk    rename(zpfast:zpfai5r)
     fzkuni1    if   e           k disk    rename(zkunst:zkuni1r)
     fzagri1    if   e           k disk    rename(zagrst:zagri1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_kund          s                   like(zxkund)
     d p_ordr          s                   like(zxordr)
     d p_vare          s                   like(zpvare)
     d p_enhe          s                   like(zpenhe)
     d p_lage          s              2  0
     d p_avde          s              4  0
      *
      * Definering av variabler i nøkler
      *
     d zkapiu_inst     s                   like(zxinst)
     d zkapiu_kund     s                   like(zxkund)
     d zkapiu_ordr     s                   like(zxordr)
     d zkapiu_vare     s                   like(zxvare)
     d zkapiu_enhe     s                   like(zxenhe)
      *
     d zkuni1_inst     s                   like(zkinst)
     d zkuni1_kund     s                   like(zkkund)
      *
     d zagri1_inst     s                   like(zginst)
     d zagri1_avde     s                   like(zgavde)
      *
     d zvpri1_inst     s                   like(zpinst)
     d zvpri1_vare     s                   like(zpvare)
     d zvpri1_ldor     s                   like(zpldor)
     d zvpri1_enhe     s                   like(zpenhe)
     d zvpri1_kugr     s                   like(zpkugr)
     d zvpri1_kkty     s                   like(zpkkty)
     d zvpri1_agrp     s                   like(zpagrp)
      *
     d vvarl1_vare     s                   like(vvvare)
      *
     d zpfair_inst     s                   like(zcinst)
     d zpfair_kugr     s                   like(zckugr)
     d zpfair_kkty     s                   like(zckkty)
     d zpfair_agrp     s                   like(zcagrp)
     d zpfair_ogrp     s                   like(zcogrp)
     d zpfair_hgrp     s                   like(zchgrp)
     d zpfair_ugrp     s                   like(zcugrp)
     d zpfair_ldor     s                   like(zcldor)
     d zpfair_modn     s                   like(zcmodn)
      *
     d zpfai2_inst     s                   like(zcinst)
     d zpfai2_kugr     s                   like(zckugr)
     d zpfai2_kkty     s                   like(zckkty)
     d zpfai2_agrp     s                   like(zcagrp)
     d zpfai2_ldor     s                   like(zcldor)
     d zpfai2_modn     s                   like(zcmodn)
      *
     d zpfai3_inst     s                   like(zcinst)
     d zpfai3_kugr     s                   like(zckugr)
     d zpfai3_kkty     s                   like(zckkty)
     d zpfai3_agrp     s                   like(zcagrp)
     d zpfai3_modn     s                   like(zcmodn)
      *
     d zpfai4_inst     s                   like(zcinst)
     d zpfai4_kugr     s                   like(zckugr)
     d zpfai4_kkty     s                   like(zckkty)
     d zpfai4_agrp     s                   like(zcagrp)
     d zpfai4_ogrp     s                   like(zcogrp)
     d zpfai4_hgrp     s                   like(zchgrp)
     d zpfai4_ugrp     s                   like(zcugrp)
     d zpfai4_ldor     s                   like(zcldor)
      *
     d zpfai5_inst     s                   like(zcinst)
     d zpfai5_kugr     s                   like(zckugr)
     d zpfai5_kkty     s                   like(zckkty)
     d zpfai5_agrp     s                   like(zcagrp)
     d zpfai5_ogrp     s                   like(zcogrp)
     d zpfai5_hgrp     s                   like(zchgrp)
     d zpfai5_ugrp     s                   like(zcugrp)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
     d b_enh1          s              1    inz(*off)
     d b_enh2          s              1    inz(*off)
     d b_enh3          s              1    inz(*off)
     d b_pasl          s              1    inz(*off)
      *
     d w_firm          s              3  0
8.01 d w_prgr          s              2
     d w_enh1          s              3
     d w_enh2          s              3
     d w_enh3          s              3
     d w_lety          s              1  0
     d w_sapr          s              9  2
     d w_sap1          s              9  2
     d w_sap2          s              9  2
     d w_sap3          s              9  2
     d w_bupr          s              9  2
     d w_bup1          s              9  2
     d w_bup2          s              9  2
     d w_bup3          s              9  2
     d w_kop1          s              9  2
     d w_kop2          s              9  2
     d w_kop3          s              9  2
     d w_inpr          s              9  2
     d w_omr2          s             12  6
     d w_omr3          s             12  6
     d w_flfa          s             12  6
     d w_stfa          s             12  6
     d w_tgfa          s             12  6
     d w_dato          s               d   datfmt(*iso)
     d w_inlr          s              1
     d w_inst          s             10
      *
     d x_stpr          s              9  2
     d x_tgpr          s              9  2
     d x_flpr          s              9  2
     d x_enhe          s              3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent inn vareinfo
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Hent inn kundinfo. NB Kan være at programmet kalles fra andre
      * steder enn ordreregistrering
      *
     c                   if        p_kund <> 0
     c                   eval      zkuni1_kund = p_kund
     c     zkuni1_key    chain     zkuni1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Hent inn releavnt info
      *
     c                   eval      zagri1_avde = p_avde
     c     zagri1_key    chain     zagri1
     c                   if        not %found
     c                   eval      zgavdg = 0
     c                   endif
      *
      * Hent prisgruppe
      *
     c                   exsr      finn_prisgr
      *
      * Hent priser for tre enheter
      *
     c                   exsr      finn_priser
      *
      * Slå opp med informasjon som er funnet, samme runde for alle tre priser
      *
     c                   if        w_enh1 <> *blanks
     c                   eval      b_enh1 = *off
     c                   eval      x_enhe = w_enh1
     c                   exsr      vare_pris
     c                   if        b_pris = *on
     c                   exsr      lag_zilliant
     c                   eval      b_enh1 = *on
     c                   endif
     c                   endif
      *
     c                   if        w_enh2 <> *blanks
     c                   eval      b_enh2 = *off
     c                   eval      x_enhe = w_enh2
     c                   exsr      vare_pris
     c                   if        b_pris = *on
     c                   exsr      lag_zilliant
     c                   eval      b_enh2 = *on
     c                   endif
     c                   endif
      *
     c                   if        w_enh3 <> *blanks
     c                   eval      b_enh3 = *off
     c                   eval      x_enhe = w_enh3
     c                   exsr      vare_pris
     c                   if        b_pris = *on
     c                   exsr      lag_zilliant
     c                   eval      b_enh3 = *on
     c                   endif
     c                   endif
      *
      * Henter påslagsfaktorer
      *
     c                   if        b_enh1 = *off and
     c                             w_enh1 <> *blanks
     c                   exsr      hent_pasl
     c                   if        b_pasl = *on
     c                   eval      x_stpr = w_kop1 * w_stfa
     c                   eval      x_tgpr = w_kop1 * w_tgfa
     c                   eval      x_flpr = w_kop1 * w_flfa
     c                   else
     c                   eval      x_stpr = w_sap1
     c                   eval      x_tgpr = w_sap1
     c                   eval      x_flpr = w_sap1
     c                   endif
     c                   eval      x_enhe = w_enh1
     c                   exsr      lag_zilliant2
     c                   eval      b_enh1 = *on
     c                   endif
      *
     c                   if        b_enh2 = *off and
     c                             w_enh2 <> *blanks
     c                   exsr      hent_pasl
     c                   if        b_pasl = *on
     c                   eval      x_stpr = w_kop2 * w_stfa
     c                   eval      x_tgpr = w_kop2 * w_tgfa
     c                   eval      x_flpr = w_kop2 * w_flfa
     c                   else
     c                   eval      x_stpr = w_sap2
     c                   eval      x_tgpr = w_sap2
     c                   eval      x_flpr = w_sap2
     c                   endif
     c                   eval      x_enhe = w_enh2
     c                   exsr      lag_zilliant2
     c                   eval      b_enh2 = *on
     c                   endif
      *
     c                   if        b_enh3 = *off and
     c                             w_enh3 <> *blanks
     c                   exsr      hent_pasl
     c                   if        b_pasl = *on
     c                   eval      x_stpr = w_kop3 * w_stfa
     c                   eval      x_tgpr = w_kop3 * w_tgfa
     c                   eval      x_flpr = w_kop3 * w_flfa
     c                   else
     c                   eval      x_stpr = w_sap3
     c                   eval      x_tgpr = w_sap3
     c                   eval      x_flpr = w_sap3
     c                   endif
     c                   eval      x_enhe = w_enh3
     c                   exsr      lag_zilliant2
     c                   eval      b_enh3 = *on
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprett tabell med info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_zilliant  begsr
      *
     c                   eval      zkapiu_kund = p_kund
     c                   eval      zkapiu_ordr = p_ordr
     c                   eval      zkapiu_vare = p_vare
     c                   eval      zkapiu_enhe = x_enhe
     c     zkapiu_key    chain     zkapiu
     c                   if        not %found
     c                   eval      zxinst = zkapiu_inst
     c                   eval      zxfirm = w_firm
     c                   eval      zxkund = p_kund
     c                   eval      zxordr = p_ordr
     c                   eval      zxvare = p_vare
     c                   eval      zxenhe = x_enhe
     c                   endif
      *
     c                   eval      zxagrp = zgavdg
     c                   eval      zxkugr = zkkugr
     c                   eval      zxkkty = zkkkty
     c                   eval      zxstpr = x_stpr
     c                   eval      zxtgpr = x_tgpr
     c                   eval      zxflpr = x_flpr
     c                   time                    zxtmst
     c                   time                    zxedat
     c                   time                    zxetim
     c                   eval      zxeusr = l_user
      *
     c                   if        not %found
     c                   time                    zxodat
     c                   time                    zxotim
     c                   eval      zxousr = l_user
     c                   write     zkapiur
     c                   else
     c                   update    zkapiur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprett tabell med info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_zilliant2 begsr
      *
     c                   eval      zkapiu_kund = p_kund
     c                   eval      zkapiu_ordr = p_ordr
     c                   eval      zkapiu_vare = p_vare
     c                   eval      zkapiu_enhe = x_enhe
     c     zkapiu_key    chain     zkapiu
     c                   if        not %found
     c                   eval      zxinst = zkapiu_inst
     c                   eval      zxfirm = w_firm
     c                   eval      zxkund = p_kund
     c                   eval      zxordr = p_ordr
     c                   eval      zxvare = p_vare
     c                   eval      zxenhe = x_enhe
      *
     c                   eval      zxagrp = zgavdg
     c                   eval      zxkugr = zkkugr
     c                   eval      zxkkty = zkkkty
     c                   eval      zxstpr = x_stpr
     c                   eval      zxtgpr = x_tgpr
     c                   eval      zxflpr = x_flpr
     c                   time                    zxtmst
     c                   time                    zxedat
     c                   time                    zxetim
     c                   eval      zxeusr = l_user
      *
     c                   time                    zxodat
     c                   time                    zxotim
     c                   eval      zxousr = l_user
     c                   write     zkapiur
     c                   else
     c                   unlock    zkapiu
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprett tabell med priser for utlegg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vare_pris     begsr
      *
     c                   eval      b_pris = *off
     c                   eval      x_stpr = 0
     c                   eval      x_tgpr = 0
     c                   eval      x_flpr = 0
     c                   eval      zvpri1_vare = p_vare
     c                   eval      zvpri1_ldor = vvldor
     c                   eval      zvpri1_enhe = x_enhe
     c                   eval      zvpri1_kugr = zkkugr
     c                   eval      zvpri1_kkty = zkkkty
     c                   eval      zvpri1_agrp = zgavdg
     c     zvpri1_key    chain     zvpri1
     c                   if        %found
     c                   eval      b_pris = *on
     c                   eval      x_stpr = zpstpr
     c                   eval      x_tgpr = zptgpr
     c                   eval      x_flpr = zpflpr
     c                   else
      * Prøv med avdelingsgruppe 0
     c                   eval      zvpri1_vare = p_vare
     c                   eval      zvpri1_ldor = vvldor
     c                   eval      zvpri1_enhe = x_enhe
     c                   eval      zvpri1_kugr = zkkugr
     c                   eval      zvpri1_kkty = zkkkty
     c                   eval      zvpri1_agrp = 0
     c     zvpri1_key    chain     zvpri1
     c                   if        %found
     c                   eval      b_pris = *on
     c                   eval      x_stpr = zpstpr
     c                   eval      x_tgpr = zptgpr
     c                   eval      x_flpr = zpflpr
      * Prøv å finne pris via påslagstabellen
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne påslagsfaktorer - bruker først funnet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pasl     begsr
      *
      * Leter etter påslag etter følgende rekkefølge:
      *
      * Kundegruppe
      * | Kundekjøpstype
      * | | Avdelingsgruppe
      * | | | Overgruppe
      * | | | | Hovedgruppe
      * | | | | | Undegruppe
      * | | | | | | Leverandør
      * | | | | | | | Modul
      * | | | | | | | |
      * | | | | | | | |
      * _________________
      * x x x - - - x x      1. lev,mod - (agr=0)
      * x x x - - - - x      2. mod - (agr=0)
      * x x x x x x x -      3. ogr, hgr, ugr , lev - (agr=0)
      * x x x x x - x -      4. ogr, hgr, lev - (agr=0)
      * x x x x - - x -      5. ogr, lev - (agr=0)
      * x x x - - - x -      6. lev - (agr=0)
      * x x x x x x - -      7. ogr, hgr, ugr - (agr=0)
      * x x x x x - - -      8. ogr, hgr - (agr=0)
      * x x x x - - - -      9. ogr - (agr=0)
      *
     c                   eval      w_stfa = 0
     c                   eval      w_tgfa = 0
     c                   eval      w_flfa = 0
      *
      * 1 Leverandør/modul
      *
     c                   eval      zpfai2_kugr = zkkugr
     c                   eval      zpfai2_kkty = zkkkty
     c                   eval      zpfai2_agrp = zgavdg
     c                   eval      zpfai2_ldor = vvldor
     c                   eval      zpfai2_modn = vvnmod
     c     zpfai2_key    chain     zpfai2
     c                   if        not %found
     c                   eval      zpfai2_modn = 0
     c     zpfai2_key    chain     zpfai2
     c                   if        not %found
     c                   eval      zpfai2_agrp = 0
     c                   eval      zpfai2_modn = vvnmod
     c     zpfai2_key    chain     zpfai2
     c                   if        not %found
     c                   eval      zpfai2_modn = 0
     c     zpfai2_key    chain     zpfai2
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        %found(zpfai2)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 2 Modul
      *
     c                   eval      zpfai3_kugr = zkkugr
     c                   eval      zpfai3_kkty = zkkkty
     c                   eval      zpfai3_agrp = zgavdg
     c                   eval      zpfai3_modn = vvnmod
     c     zpfai3_key    chain     zpfai3
     c                   if        not %found
     c                   eval      zpfai3_modn = 0
     c     zpfai3_key    chain     zpfai3
     c                   eval      zpfai3_agrp = 0
     c     zpfai3_key    chain     zpfai3
     c                   if        not %found
     c                   endif
     c                   endif
      *
     c                   if        %found(zpfai3)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 3 Overgruppe, Hovedgruppe, Undergruppe, Leverandør
      *
     c                   eval      zpfai4_agrp = zgavdg
     c                   eval      zpfai4_ogrp = vvogrp
     c                   eval      zpfai4_hgrp = vvhgrp
     c                   eval      zpfai4_ugrp = vvugrp
     c                   eval      zpfai4_ldor = vvldor
     c     zpfai4_key    chain     zpfai4
     c                   if        not %found
     c                   eval      zpfai4_agrp = 0
     c     zpfai4_key    chain     zpfai4
     c                   endif
      *
     c                   if        %found(zpfai4)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 4 Overgruppe, Hovedgruppe, leverandør
      *
     c                   eval      zpfair_agrp = zgavdg
     c                   eval      zpfai4_ugrp = 0
     c     zpfai4_key    chain     zpfai4
     c                   if        not %found
     c                   eval      zpfai4_agrp = 0
     c     zpfai4_key    chain     zpfai4
     c                   endif
      *
     c                   if        %found(zpfai4)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 5 Overgruppe, leverandør
      *
     c                   eval      zpfai4_agrp = zgavdg
     c                   eval      zpfai4_hgrp = 0
     c     zpfai4_key    chain     zpfai4
     c                   if        not %found
     c                   eval      zpfai4_agrp = 0
     c     zpfai4_key    chain     zpfai4
     c                   endif
      *
     c                   if        %found(zpfai4)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 6 Leverandør
      *
     c                   eval      zpfai4_agrp = zgavdg
     c                   eval      zpfai4_ogrp = 0
     c     zpfai4_key    chain     zpfai4
     c                   if        not %found
     c                   eval      zpfai4_agrp = 0
     c     zpfai4_key    chain     zpfai4
     c                   endif
      *
     c                   if        %found(zpfai4)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 7 Overgruppe, Hovedgruppe, undergruppe
      *
     c                   eval      zpfai5_agrp = zgavdg
     c                   eval      zpfai5_ogrp = vvogrp
     c                   eval      zpfai5_hgrp = vvhgrp
     c                   eval      zpfai5_ugrp = vvugrp
     c     zpfai5_key    chain     zpfai5
     c                   if        not %found
     c                   eval      zpfai5_agrp = 0
     c     zpfai5_key    chain     zpfai5
     c                   endif
      *
     c                   if        %found(zpfai5)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 8 Overgruppe, Hovedgruppe
      *
     c                   eval      zpfai5_agrp = zgavdg
     c                   eval      zpfai5_ugrp = 0
     c     zpfai5_key    chain     zpfai5
     c                   if        not %found
     c                   eval      zpfai5_agrp = 0
     c     zpfair_key    chain     zpfair
     c                   endif
      *
     c                   if        %found(zpfai5)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * 9 Overgruppe
      *
     c                   eval      zpfai5_agrp = zgavdg
     c                   eval      zpfai5_hgrp = 0
     c     zpfai5_key    chain     zpfai5
     c                   if        not %found
     c                   eval      zpfai5_agrp = 0
     c     zpfai5_key    chain     zpfai5
     c                   endif
      *
     c                   if        %found(zpfair)
     c                   eval      w_stfa = zcstfa
     c                   eval      w_tgfa = zctgfa
     c                   eval      w_flfa = zcflfa
     c                   goto      end_paslag
     c                   endif
      *
      * Ingen gyldige påslagsfaktorer funnet.
      * Kostprisfaktor og salgprisfaktor defaultes til 1.
      *
     c                   eval      w_stfa = 1
     c                   eval      w_tgfa = 1
     c                   eval      w_flfa = 1
     c                   leavesr
      *
      * Tar vare på påslagsfaktor data.
      *
     c     end_paslag    tag
      *
     c                   eval      b_pasl = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finn prisgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_prisgr   begsr
      *
     c                   eval      w_prgr = *blanks
     c                   call      'VL712R   '
     c                   parm                    w_firm
     c                   parm                    p_lage
     c                   parm                    p_avde
     c                   parm                    w_prgr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finn priser på tre enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_priser   begsr
      *
     c                   eval      w_sap1 = 0
     c                   eval      w_bup1 = 0
     c                   eval      w_sap2 = 0
     c                   eval      w_bup2 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_sap3 = 0
     c                   eval      w_bup3 = 0
     c                   eval      w_omr3 = 0
      *
     c                   call      'VP715R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    w_prgr
     c                   parm                    p_lage
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_enh1
     c                   parm                    w_sap1
     c                   parm                    w_bup1
     c                   parm                    w_enh2
     c                   parm                    w_sap2
     c                   parm                    w_bup2
     c                   parm                    w_omr2
     c                   parm                    w_enh3
     c                   parm                    w_sap3
     c                   parm                    w_bup3
     c                   parm                    w_omr3
      *
      * Hent kostpris for enheter
      *
     c                   if        w_enh1 <> *blanks
     c                   eval      w_sapr = 0
     c                   eval      w_bupr = 0
     c                   eval      w_kop1 = 0
     c                   eval      w_inpr = 0
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    w_prgr
     c                   parm                    w_enh1
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kop1
     c                   parm                    w_inpr
     c                   endif
      *
     c                   if        w_enh2 <> *blanks
     c                   eval      w_sapr = 0
     c                   eval      w_bupr = 0
     c                   eval      w_kop2 = 0
     c                   eval      w_inpr = 0
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    w_prgr
     c                   parm                    w_enh2
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kop2
     c                   parm                    w_inpr
     c                   endif
      *
     c                   if        w_enh3 <> *blanks
     c                   eval      w_sapr = 0
     c                   eval      w_bupr = 0
     c                   eval      w_kop3 = 0
     c                   eval      w_inpr = 0
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    w_prgr
     c                   parm                    w_enh3
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kop3
     c                   parm                    w_inpr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kund
     c                   parm                    p_ordr
     c                   parm                    p_lage
     c                   parm                    p_avde
     c                   parm                    p_vare
     c                   parm                    p_enhe
      *
      * Key for oppslag på anbefalt pris
      *
     c     zkapiu_key    klist
     c                   kfld                    zkapiu_inst
     c                   kfld                    w_firm
     c                   kfld                    zkapiu_kund
     c                   kfld                    zkapiu_ordr
     c                   kfld                    zkapiu_vare
     c                   kfld                    zkapiu_enhe
      *
      * Key for oppslag på varepris
      *
     c     zvpri1_key    klist
     c                   kfld                    zvpri1_inst
     c                   kfld                    w_firm
     c                   kfld                    zvpri1_vare
     c                   kfld                    zvpri1_ldor
     c                   kfld                    zvpri1_enhe
     c                   kfld                    zvpri1_kugr
     c                   kfld                    zvpri1_kkty
     c                   kfld                    zvpri1_agrp
      *
      * Key for oppslag på påslagstabell
      *
     c     zpfair_key    klist
     c                   kfld                    zpfair_inst
     c                   kfld                    w_firm
     c                   kfld                    zpfair_kugr
     c                   kfld                    zpfair_kkty
     c                   kfld                    zpfair_agrp
     c                   kfld                    zpfair_ogrp
     c                   kfld                    zpfair_hgrp
     c                   kfld                    zpfair_ugrp
     c                   kfld                    zpfair_ldor
     c                   kfld                    zpfair_modn
      *
      * Key for oppslag på påslagstabell
      *
     c     zpfai2_key    klist
     c                   kfld                    zpfai2_inst
     c                   kfld                    w_firm
     c                   kfld                    zpfai2_kugr
     c                   kfld                    zpfai2_kkty
     c                   kfld                    zpfai2_agrp
     c                   kfld                    zpfai2_ldor
     c                   kfld                    zpfai2_modn
      *
      * Key for oppslag på påslagstabell
      *
     c     zpfai3_key    klist
     c                   kfld                    zpfai3_inst
     c                   kfld                    w_firm
     c                   kfld                    zpfai3_kugr
     c                   kfld                    zpfai3_kkty
     c                   kfld                    zpfai3_agrp
     c                   kfld                    zpfai3_modn
      *
      * Key for oppslag på påslagstabell
      *
     c     zpfai4_key    klist
     c                   kfld                    zpfai4_inst
     c                   kfld                    w_firm
     c                   kfld                    zpfai4_kugr
     c                   kfld                    zpfai4_kkty
     c                   kfld                    zpfai4_agrp
     c                   kfld                    zpfai4_ogrp
     c                   kfld                    zpfai4_hgrp
     c                   kfld                    zpfai4_ugrp
     c                   kfld                    zpfai4_ldor
      *
      * Key for oppslag på påslagstabell
      *
     c     zpfai5_key    klist
     c                   kfld                    zpfai5_inst
     c                   kfld                    w_firm
     c                   kfld                    zpfai5_kugr
     c                   kfld                    zpfai5_kkty
     c                   kfld                    zpfai5_agrp
     c                   kfld                    zpfai5_ogrp
     c                   kfld                    zpfai5_hgrp
     c                   kfld                    zpfai5_ugrp
      *
      * Key for oppslag på kunderegister
      *
     c     zkuni1_key    klist
     c                   kfld                    zkuni1_inst
     c                   kfld                    w_firm
     c                   kfld                    zkuni1_kund
      *
      * Key for oppslag på avdelingsgruppe
      *
     c     zagri1_key    klist
     c                   kfld                    zagri1_inst
     c                   kfld                    w_firm
     c                   kfld                    zagri1_avde
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_inst = 'ADB' + l_filg
     c                   eval      zkapiu_inst = w_inst
     c                   eval      zvpri1_inst = w_inst
     c                   eval      zkuni1_inst = w_inst
     c                   eval      zpfair_inst = w_inst
     c                   eval      zpfai2_inst = w_inst
     c                   eval      zpfai3_inst = w_inst
     c                   eval      zpfai4_inst = w_inst
     c                   eval      zpfai5_inst = w_inst
     c                   eval      zagri1_inst = w_inst
      *
     c                   time                    w_dato
      *
     c                   endsr
