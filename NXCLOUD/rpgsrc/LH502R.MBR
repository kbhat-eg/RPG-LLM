     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LH500R
      * Beskrivelse . . : Spørring på historisk lagerbok
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.10  * 6.10  181010 bsa  Hvis betaling av PN osv i kassa, feiler innhenting av ordreinfo
6.11  * 6.10  061211 bof  Utvidet hent av bong (RT-transer)
6.20  * 6.20  270511 bsa  Gir fornuftig melding på 'SL' transaksjon.
6.21  * 6.20  100714 bof  Fjernet en mulig feil-kilde ved spørring på detaljer
6.nu  * 6.nu  100614 bof  Endringer i forb. med nummerutvidelse
6.30  * 6.30  070116 bsa  Finner ikke riktig ordrereferanse.
6.31  * 6.30  220715 bof  Hvis for stort tall i antall,så legg ut 99999999,999
6.32  * 6.30  261017 nho  Flytter 'ny' ordreref til B1REFR             999,999
      *
7.xx  * 7.00  200516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  200516 bsa  Utvidet test av ref fra tidligere versjoner.
7.02  * K00   290420 bsa  Utvidet til å takle begge tyder skjermer.
      *
8.00  * K01   260722 sst  Egen variant vedr. MalProff (flere opplysninger)
8.01  * K01   040124 bsa  Ordrenummer vises ikke ved bonger.           er)
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhisl3    if   e           k disk    rename(vhispfr:vhisl3r)
     fvhisl1    if   e           k disk    rename(vhispfr:vhisl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     flbchlr    if   e           k disk    rename(lbchpfr:lbchlrr)
     fra24lr    if   e           k disk    rename(ra24pfr:ra24lrr)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
3.31 flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     fsihel3    if   e           k disk    rename(sihepfr:sihel3r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fsohel3    if   e           k disk    rename(sohepfr:sohel3r)
8.00 frmpil1    if   e           k disk    rename(rmpipfr:rmpil1r)
      *
     flh502d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Redefinering av referansefeltet
      *
     d                 ds
     d d_refr                        20
6.30 d  d_gnum                        8  0 overlay(d_refr)
6.30 d  d_glin                        2  0 overlay(d_refr:9)
6.30 d  d_nnum                        8  0 overlay(d_refr:3)
6.30 d  d_nlin                        2  0 overlay(d_refr:14)
6.nu  *
6.nu  * Redefinering av referansefeltet, etter nummerutvidelse
6.nu  *
6.nu d                 ds
6.nu d d_refr2                       20
6.nu d  d_glin2                       2  0 overlay(d_refr2:9)
6.nu d  d_nnum2                       8  0 overlay(d_refr2:3)
6.nu d  d_nlin2                       2  0 overlay(d_refr2:14)
6.32 d                 ds
6.32 d bxrefr                        20
6.32 d  x_ordr                        2    overlay(bxrefr)
6.32 d  x_ordn                        8    overlay(bxrefr:3)
6.32 d  x_ord1                        6    overlay(bxrefr:5)
6.32 d  x_blan                        1    overlay(bxrefr:11)
6.32 d  x_txli                        2    overlay(bxrefr:12)
6.32 d  x_olin                        4    overlay(bxrefr:14)
6.32 d  x_oli1                        2    overlay(bxrefr:16)
      *
6.32 d x_hjo8                         8  0
6.32 d x_hjo4                         4  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vhfirm)
     d p_vare          s                   like(vhvare)
     d p_lage          s              2
     d p_dato          s                   like(vhdato)
     d p_valg          s              1  0
      *
      * Definering av variabler i nøkler
      *
     d vhisl3_vare     s                   like(vhvare)
     d vhisl3_lage     s                   like(vhlage)
     d vhisl3_dato     s                   like(vhdato)
      *
     d vhisl1_lage     s                   like(vhlage)
     d vhisl1_vare     s                   like(vhvare)
     d vhisl1_dato     s                   like(vhdato)
     d vhisl1_time     s                   like(vhtime)
     d vhisl1_sekv     s                   like(vhsekv)
     d vvarl1_vare     s                   like(vvvare)
      *
     d votylr_otyp     s                   like(vaotyp)
     d lbchlr_batc     s                   like(lcbatc)
     d ra24lr_xrko     s                   like(raxrko)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_wsid     s                   like(bowsid)
     d bohel1_bong     s                   like(bobong)
3.31 d lohel1_numm     s                   like(lonumm)
     d sihel3_numm     s                   like(shnumm)
3.31 d fohel1_numm     s                   like(fonumm)
     d sohel3_numm     s                   like(sonumm)
8.00 d rmpil1_fir      s                   like(rmpfir)
8.00 d rmpil1_lag      s                   like(rmplag)
8.00 d rmpil1_var      s                   like(rmpvar)
8.00 d rmpil1_dat      s                   like(rmpkda)
8.00 d rmpil1_tim      s                   like(rmpkti)
8.00 d rmpil1_sek      s                   like(rmpsek)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vhfirm)
     d w_vare          s                   like(vhvare)
     d w_lage          s                   like(vhlage)
     d w_dato          s                   like(vhdato)
     d w_vtxt          s             35
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_time          s              6  0
     d w_edat          s              6  0
     d w_etim          s              6  0
     d w_valg          s              1  0
     d w_kode          s              2
     d w_trko          s              2
     d w_batc          s             10
     d w_bong          s             12
6.nu d w_besa          s              8
6.nu d w_orda          s              8
     d w_lina          s              6
6.nu d w_best          s              8  0
6.nu d w_ordn          s              8  0
     d w_line          s              4  0
     d w_aarr          s              4  0
8.00 d w_mp            s              1    inz(' ')
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
     d c_edit          c                   '  .  .  '
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lage <> *blank
     c                   eval      b_feil = *off
     c                   if        %len(%trim(b2lage)) = 1
     c                   eval      b2lage = '0' + %trim(b2lage)
     c                   endif
     c                   move      b2lage        w_lage
     c                   exsr      les_lager
     c                   if        b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      b2tagb
     c                   endif
     c                   endif
     c                   if        b2dato <> 0
     c     *dmy          test(d)                 b2dato                 33
     c                   if        *in33 = *on
     c                   goto      b2tagb
     c                   endif
     c     *dmy          move      b2dato        w_dato
     c                   endif
      *
     c                   if        b2lage <> *blank or
     c                             b2dato <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     vhisl3_key    setll     vhisl3
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   if        b2lage <> *blank
     c                   eval      vhisl3_lage = w_lage
     c                   endif
     c                   if        b2dato <> 0
     c                   eval      vhisl3_dato = w_dato
     c                   endif
      *
     c     vhisl3_key    setll     vhisl3
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lage = *blank
     c                   eval      b2dato = 0
      *
     c     end_pos       tag
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Vise detaljer
      *
     c                   if        b1valg = 5
     c                   exsr      vis_det
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise varedetaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_det       begsr
      *
6.20 c                   eval      c1inf2 = *blank
6.20 c                   eval      c1inf3 = *blank
6.20 c                   eval      c1inf4 = *blank
      *
      * Legger ut std. info for alle transaksjoner
      *
     c                   eval      vhisl1_lage = b1lage
     c                   eval      vhisl1_vare = b1vare
     c                   eval      vhisl1_dato = b1datk
     c                   eval      vhisl1_time = b1timk
     c                   eval      vhisl1_sekv = b1sekv
     c     vhisl1_key    chain     vhisl1
      *
     c                   if        %found
     c                   eval      c1lage = b1lage
     c                   eval      c1vltk = b2vltk
     c                   eval      c1vvar = b2vvar
     c                   eval      c1vtek = b2vtek
     c                   eval(h)   c1anta = vhanta
     c                   if        b1type = 'U'
     c                   eval      c1anta = c1anta * -1
     c                   endif
     c                   eval(h)   c1behl = vhbehl
     c     *dmy          move      vhdato        c1dato
     c     *hms          move      vhtime        c1time
     c                   eval      c1user = vhuser
     c                   eval      w_aarr = %subdt(b1datk:*years)
     c                   endif
      *
      * Finner transaksjonstype for detalinfo
      *
     c                   eval      w_kode = b1kode
      *
     c                   if        b1kode <> 'T' and
     c                             b1kode <> 'I' and
     c                             b1kode <> 'U' and
6.20 c                             b1kode <> 'J' and
6.20 c                             b1kode <> 'SL'
     c                   exsr      finn_otyp
     c                   endif
      *
      * Henter detaljinfo avhengig av transaksjonstype
      *
      * Tellebatch
      *
     c                   if        b1syst = 'L' and
     c                             w_kode = 'T'
     c                   exsr      finn_batc
     c                   goto      c1ut
     c                   endif
      *
      * Lagertrans
      *
     c                   if        b1syst = 'L' and
     c                             w_kode = 'I' or
     c                             w_kode = 'U' or
     c                             w_kode = 'J'
     c                   exsr      finn_ltrans
     c                   goto      c1ut
     c                   endif
6.20  *
6.20  * Lagertrans
6.20  *
6.20 c                   if        b1syst = 'L' and
6.20 c                             w_kode = 'SL'
6.20 c                   exsr      hnt_ltrans
6.20 c                   goto      c1ut
6.20 c                   endif
      *
      * Kassebong
      *
8.00 c                   if        (b1syst = 'F' and
8.00 c                             w_kode = '4')
8.00 c                             or b1kode = 'MP'
     c                   exsr      finn_bong
     c                   goto      c1ut
     c                   endif
6.10  *
6.10  * PN/P/F(osv) betalt i kassa ??
6.10  *
6.10 c                   if        b1syst = 'F' and
6.10 c                             w_kode = '2'
6.10 c                   if        %subst(b1refr:1:5) = 'Bong:'
6.10 c                   exsr      finn_bong
6.10 c                   goto      c1ut
6.10 c                   endif
6.10 c                   endif
      *
      * Bestilling
      *
     c                   if        b1syst = 'L' and
     c                             w_kode = '2' or
7.01 c                             b1syst = 'L' and
     c                             w_kode = '3'
     c                   exsr      finn_best
     c                   goto      c1ut
     c                   endif
      *
      * Ordre
      *
     c                   if        b1syst = 'F' and
     c                             w_kode = '2' or
7.01 c                             b1syst = 'F' and
     c                             w_kode = '3'
     c                   exsr      finn_ordre
     c                   goto      c1ut
     c                   endif
      *
      * Ingen detaljinfo funnet
      *
     c                   eval      c1head = 'Feil status'
     c                   eval      c1inf1 = 'Ingen detalj informasjon funnet'
     c                   eval      c1inf2 = *blank
     c                   eval      c1inf3 = *blank
     c                   eval      c1inf4 = *blank
      *
      *  Legger ut komplett skjermbilde
      *
     c     c1ut          tag
8.00 c                   if        w_mp = ' '
     c                   exfmt     c1win
8.00 c                   else
8.00 c                   exfmt     c2win
8.00 c                   endif
      *
     c     end_det       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finner informasjon på ordre typer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_otyp     begsr
      *
     c                   eval      votylr_otyp = b1kode
     c     votylr_key    chain     votylr
     c                   if        %found
     c                   select
     c                   when      vaoakk = 2
     c                   eval      w_kode = '2 '
     c                   when      vaoakk = 3
     c                   eval      w_kode = '3 '
     c                   when      vaoakk = 4
     c                   eval      w_kode = '4 '
     c                   endsl
     c                   else
     c                   eval      w_kode = '  '
     c                   endif
      *
     c     end_otyp      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne tellebatc info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_batc     begsr
      *
     c                   if        %subst(b1refr:1:5) = 'Batc:'
     c                   eval      w_batc = %subst(b1refr:7:10)
     c                   else
     c                   eval      w_batc = %subst(b1refr:1:10)
     c                   endif
      *
     c                   eval      c1head = 'Lagertelling'
     c                   eval      c1inf1 = 'Tellebatc: ' + w_batc
      *
     c                   eval      lbchlr_batc = w_batc
     c     lbchlr_key    chain     lbchlr
     c                   if        %found
     c                   eval      c1inf2 = 'Ansv: ' + lcansv
     c     *dmy          move      lcbdat        w_edat
     c     *hms          move      lcbtim        w_etim
     c                   eval      c1inf3 = 'Start: ' +
     c                                       %editw(w_edat:c_edit) +
     c                                      ' Kl: ' + %editw(w_etim:c_edit)
     c     *dmy          move      lcedat        w_edat
     c     *hms          move      lcetim        w_etim
     c                   eval      c1inf4 = 'Slutt: ' +
     c                                       %editw(w_edat:c_edit) +
     c                                      ' Kl: ' + %editw(w_etim:c_edit)
     c                   else
     c                   eval      c1inf2 = 'Batc er slettet!'
     c                   eval      c1inf3 = *blank
     c                   eval      c1inf4 = *blank
     c                   endif
      *
     c     end_batc      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne lagertrans info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ltrans   begsr
      *
     c                   if        %subst(b1refr:1:11) = 'Årsakskode:'
     c                   eval      w_trko = %subst(b1refr:13:2)
     c                   else
     c                   eval      w_trko = %subst(b1refr:1:2)
     c                   endif
      *
     c                   eval      c1head = 'Lagertransaksjon'
     c                   eval      c1inf1 = 'Transaksjonstype: ' + w_kode
      *
     c                   eval      ra24lr_xrko = w_trko
     c     ra24lr_key    chain     ra24lr
     c                   if        %found
     c                   eval      c1inf2 = w_trko + ': ' +  raxtxt
     c                   eval      c1inf3 = *blank
     c                   eval      c1inf4 = *blank
     c                   endif
      *
     c     end_trans     endsr
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å finne lagertrans info
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hnt_ltrans    begsr
6.20  *
6.20 c                   eval      w_trko = %subst(b1refr:1:2)
6.20  *
6.20 c                   eval      c1head = 'Lagertransaksjon'
6.20 c                   eval      c1inf1 = 'Transaksjonstype: ' + w_kode
6.20  *
6.20 c                   eval      c1inf2 = 'Endret varetype fra S til L'
6.20 c                   eval      c1inf3 = *blank
6.20 c                   eval      c1inf4 = *blank
6.20  *
6.20 c                   endsr
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne bong info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_bong     begsr
      *
     c                   if        %subst(b1refr:1:5) = 'Bong:'
     c                   eval      w_bong = %subst(b1refr:7:12)
     c                   else
     c                   eval      w_bong = %subst(b1refr:1:12)
     c                   endif
      *
     c                   eval      c1head = 'Kasse transaksjon'
     c                   eval      c1inf1 = 'Bongnr: ' + w_bong
      *
     c                   eval      bohel1_aarr = %subdt(vhdato:*years)
     c                   eval      bohel1_wsid = %subst(w_bong:1:3)
     c                   eval      bohel1_bong = w_bong
      *
     c     bohel1_key    chain     bohel1
     c                   if        %found
     c                   eval      c1inf2 = 'Kunde: ' + bokuna
     c                   eval      c1inf3 = 'Vår ref: ' + bovref
8.01 c                   if        boornr <> 0
     c                   eval      c1inf4 = 'Ordrenr: ' + %char(boornr)
8.01 c                   else
8.01 c                   eval      c1inf4 = 'Ordrenr: ' + %char(b1onum)
8.01 c                   endif
     c                   else
6.11  ** prøver med RT-bruker som WSID
6.11  *
6.11 c                   eval      bohel1_aarr = %subdt(vhdato:*years)
6.11 c                   eval      bohel1_wsid = 'RT' + vhuser
6.11 c                   eval      bohel1_bong = %subst(w_bong:1:10)
6.11  *
6.11 c     bohel1_key    chain     bohel1
6.11 c                   if        %found
6.11 c                   eval      c1inf2 = 'Kunde: ' + bokuna
6.11 c                   eval      c1inf3 = 'Vår ref: ' + bovref
8.01 c                   if        boornr <> 0
6.11 c                   eval      c1inf4 = 'Ordrenr: ' + %char(boornr)
8.01 c                   else
8.01 c                   eval      c1inf4 = 'Ordrenr: ' + %char(b1onum)
8.01 c                   endif
6.11 c                   else
8.00  * _________________________________________________________________
8.00  * Sjekk om kasse bilag fra Mal Proff
8.00 c                   eval      rmpil1_fir = vhfirm
8.00 c                   eval      rmpil1_lag = vhlage
8.00 c                   eval      rmpil1_var = vhvare
8.00 c                   eval      rmpil1_dat = vhdato
8.00 c                   eval      rmpil1_tim = vhtime
8.00 c                   eval      rmpil1_sek = vhsekv
8.00 c     rmpil1_key    chain     rmpil1
8.00 c                   if        %found(rmpil1)
     c                   eval      c1head = 'Kasse transaksjon   MalProff'
8.00 c                   eval      c1inf2 = 'Kunde:    ' + rmpkun
8.00 c                   eval      c1inf3 = 'Ordre:    ' + %char(rmponr)
8.00 c                   eval      c1inf4 = 'Linje:    ' + %char(rmplin)
8.00 c                   eval      c1inf5 = 'Vår ref.: ' + rmpref
8.00 c                   eval      c1inf6 = 'Kundenr.: ' + %char(rmpknr)
8.00 c                   eval      c1inf7 = 'Avdeling: ' + rmpavd
8.00 c                   eval      c1inf8 = 'TransDat: ' + %char(rmpdat)
8.00  * _________________________________________________________________
8.00 c                   else
     c                   eval      c1inf2 = 'Bong ikke funnet!'
     c                   eval      c1inf3 = *blank
     c                   eval      c1inf4 = *blank
     c                   endif
6.11 c                   endif
8.00 c                   endif
      *
     c     end_bong      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne bestillings info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_best     begsr
      *
     c                   eval      c1head = 'Innkjøps info'
     c                   eval      d_refr = b1refr
6.nu  * Lagt inn ny test i forb. nummerutvidelse
6.nu c                   if        %subst(b1refr:1:2) = 'B:' and
6.nu c                             %subst(b1refr:12:2) = 'L:'
6.nu c                   eval      d_refr2 = b1refr
6.nu c                   eval      w_besa = %subst(b1refr:4:8)
6.nu c                   eval      w_lina = %subst(b1refr:14:4)
6.nu c                   eval      w_best = d_nnum2
6.21 c*                  eval      w_line = d_nlin2
6.nu c                   else
     c                   if        %subst(b1refr:1:2) = 'B:'
     c                   eval      w_besa = %subst(b1refr:3:6)
     c                   eval      w_lina = %subst(b1refr:12:4)
     c                   eval      w_best = d_nnum
6.21 c*                  eval      w_line = d_nlin
     c                   else
     c                   eval      w_besa = %subst(b1refr:1:6)
     c                   eval      w_lina = %subst(b1refr:7:4)
     c                   eval      w_best = d_gnum
6.21 c*                  eval      w_line = d_glin
     c                   endif
6.nu c                   endif
      *
      * Finn bestilling eller inng. faktura
      *
     c                   eval      lohel1_numm = w_best
     c     lohel1_key    chain     lohel1r
     c                   if        %found
     c                   eval      c1inf1 = 'Best: ' + w_besa + ' Lin: ' +
     c                                       w_lina
     c                   eval      c1inf2 = 'Leverandør: ' + lolena
     c                   eval      c1inf3 = 'Best: ' + %char(lobdat) +
     c                                      ' Bekr: ' + %char(lomoda)
     c                   eval      c1inf4 = 'Vår ref: '  + lovref
     c                   else
     c                   eval      sihel3_numm = w_best
     c     sihel3_key    chain     sihel3r
     c                   if        %found
     c                   eval      c1inf1 = 'Best: ' + w_besa + ' Lin: ' +
     c                                       w_lina  + ' Fakt:' +
     c                                       %char(shbinr)
     c                   eval      c1inf2 = 'Leverandør: ' + shlena
     c                   eval      c1inf3 = 'Fakt: ' + %char(shfkda) +
     c                                      ' Lev.fak:' + shlfnr
     c                   eval      c1inf4 = 'Vår ref: '  + shvref
     c                   else
     c                   eval      c1inf2 = 'Bestilling ikke funnet!'
     c                   eval      c1inf3 = *blank
     c                   eval      c1inf4 = *blank
     c                   endif
     c                   endif
      *
     c     end_best      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne ordre info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ordre    begsr
      *
     c                   eval      c1head = 'Ordre info'
6.32  * Flytter ordreinfo for å tilpasse test under
6.32 c                   if        %subst(b1refr:1:2) = 'O:' and
6.32 c                             %subst(b1refr:9:2) = 'L:'
6.32 c                   eval      bxrefr = *blanks
6.32 c                   eval      x_ordn = '00000000'
6.32 c                   eval      x_olin = '0000'
6.32 c                   eval      x_ordr = %subst(b1refr:1:2)
6.32 c                   eval      x_ord1 = %subst(b1refr:3:6)
6.32 c                   eval      x_blan = ' '
6.32 c                   eval      x_txli = 'L:'
6.32 c                   eval      x_oli1 = %subst(b1refr:11:2)
6.32 c                   eval      b1refr = bxrefr
6.32 c                   endif
6.32  *
     c                   eval      d_refr = b1refr
6.nu  * Lagt inn ny test i forb. nummerutvidelse
6.30 c                   if        %subst(b1refr:1:2) = 'O:'
6.30sc                   eval      w_orda = %subst(b1refr:3:8)
6.nu c                   if        %subst(b1refr:12:2) = 'L:'
6.30 c                   eval      w_lina = %subst(b1refr:14:4)
6.30 c                   else
6.30 c                   eval      w_lina = %subst(b1refr:12:4)
6.30 c                   endif
6.nu c                   eval      w_ordn = d_nnum
6.nu c                   else
6.30sc                   eval      w_orda = %subst(b1refr:3:8)
6.30 c                   eval      w_lina = %subst(b1refr:14:4)
6.30 c                   eval      w_ordn = %dec(w_orda:8:0)
6.30 c                   endif
      *
      * Finn ordre eller faktura
      *
     c                   eval      fohel1_numm = w_ordn
     c     fohel1_key    chain     fohel1r
     c                   if        %found
     c                   eval      c1inf1 = 'Ordr: ' + w_orda + ' Lin: ' +
     c                                       w_lina
     c                   eval      c1inf2 = 'Kunde: ' + fokuna
     c                   eval      c1inf3 = 'Odat: ' + %char(fobdat) +
     c                                      ' Bekr: ' + %char(fopdat)
     c                   eval      c1inf4 = 'Vår ref: '  + fovref
     c                   else
     c                   eval      sohel3_numm = w_ordn
     c     sohel3_key    chain     sohel3r
     c                   if        %found
     c                   eval      c1inf1 = 'Ordr: ' + w_orda + ' Lin: ' +
     c                                       w_lina  + ' Fakt:' +
     c                                       %char(sobinr)
     c                   eval      c1inf2 = 'Kunde: ' + sokuna
     c                   eval      c1inf3 = 'Fakt: ' + %char(sofkda)
     c                   eval      c1inf4 = 'Vår ref: '  + sovref
     c                   else
     c                   eval      c1inf2 = 'Ordre ikke funnet!'
     c                   eval      c1inf3 = *blank
     c                   eval      c1inf4 = *blank
     c                   endif
     c                   endif
      *
     c     end_ordre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vhisl3                                 15
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
      * Styrer brudd i lesing avhengig av bla-alternativer
      *
     c                   select
     c                   when      w_valg = 1
     c                   if        vhvare <> w_vare or vhlage <> w_lage
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
     c                   if        vhdato < w_dato
     c                   goto      les_neste
     c                   endif
     c                   when      w_valg = 2
     c                   if        vhvare <> w_vare or vhlage <> w_lage
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
     c                   when      w_valg = 3
     c                   if        vhvare <> w_vare
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
     c                   if        vhdato < w_dato
     c                   goto      les_neste
     c                   endif
     c                   when      w_valg = 4
     c                   if        vhvare <> w_vare
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
     c                   endsl
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1lage = vhlage
      *
     c                   if        vhdato <> *loval
     c     *dmy          move      vhdato        b1dato
     c                   else
     c                   eval      b1dato = 0
     c                   endif
      *
     c                   if        vhtime <> *loval
     c     *hms          move      vhtime        w_time
     c                   movel     w_time        b1time
     c                   else
     c                   eval      b1time = 0
     c                   endif
      *
     c                   eval      b1syst = vhsyst
     c                   eval      b1kode = vhkode
     c                   eval      b1type = vhtype
6.31 c                   if        %abs(vhanta) > 9999999,999
6.31 c                   eval      b1anta = 9999999,999
6.31 c                   else
     c                   eval(h)   b1anta = vhanta
6.31 c                   endif
     c                   eval(h)   b1behl = vhbehl
     c                   eval      b1refr = vhrefr
     c                   if        vhtype = 'U'
     c                   eval      b1anta = b1anta * -1
     c                   endif
     c                   eval      b1datk = vhdato
     c                   eval      b1timk = vhtime
     c                   eval      b1sekv = vhsekv
     c                   eval      b1user = vhuser
     c                   eval      b1vare = vhvare
8.01 c                   eval      b1onum = vhonum
      *
     c                   write     b1sfl
      *
     c     les_neste     tag
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_lager     begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på lager
      *
     c                   if        w_actf = 'B2LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    b2vltk
     c                   move      w_lage        b2vlag
     c                   move      w_lage        b2lage
     c                   goto      end_sp
     c                   endif
      *
     c     end_sp        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av historisk lagerbok
      *
     c     vhisl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl3_vare
     c                   kfld                    vhisl3_lage
     c                   kfld                    vhisl3_dato
      *
      *  Key for oppslag på historisk lagerbok
      *
     c     vhisl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl1_lage
     c                   kfld                    vhisl1_vare
     c                   kfld                    vhisl1_dato
     c                   kfld                    vhisl1_time
     c                   kfld                    vhisl1_sekv
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      * Key for les av BATCH
      *
     c     lbchlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchlr_batc
      *
      * Key for les på lagertransaksjonskode
      *
     c     ra24lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra24lr_xrko
      *
      * Key for oppslag på bongnummer
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohel1_aarr
     c                   kfld                    bohel1_wsid
     c                   kfld                    bohel1_bong
3.31  *
3.31  * Key for bestillings-hode
3.31  *
3.31 c     lohel1_key    klist
3.31 c                   kfld                    w_firm
3.31 c                   kfld                    lohel1_numm
      *
      * Key for inngående faktura
      *
     c     sihel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sihel3_numm
3.31  *
3.31  * Key for ordre-hode
3.31  *
3.31 c     fohel1_key    klist
3.31 c                   kfld                    w_firm
3.31 c                   kfld                    fohel1_numm
      *
      * Key for utgående faktura
      *
     c     sohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sohel3_numm
8.00  *
8.00  * Key for opplysninger fra MalProff
8.00  *
8.00 c     rmpil1_key    klist
8.00 c                   kfld                    rmpil1_fir
8.00 c                   kfld                    rmpil1_lag
8.00 c                   kfld                    rmpil1_var
8.00 c                   kfld                    rmpil1_dat
8.00 c                   kfld                    rmpil1_tim
8.00 c                   kfld                    rmpil1_sek
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_dato
     c                   parm                    p_valg
      *
      * Legger inn startverdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
     c                   move      p_lage        w_lage
     c                   eval      w_dato = p_dato
     c                   eval      w_valg = p_valg
      *
     c                   exsr      les_vare
     c                   eval      b2vvar = w_vare
     c                   eval      b2vtek = vvtek1
     c                   eval      b2enhl = vvenhl
      *
     c                   if        p_lage <> *blank
     c                   exsr      les_lager
     c                   move      w_lage        b2vlag
     c                   eval      b2vltk = w_latx
     c                   else
     c                   eval      b2vlag = *blank
     c                   eval      b2vltk = 'Alle lager'
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vhisl3_vare = w_vare
     c                   eval      vhisl3_lage = w_lage
     c                   eval      vhisl3_dato = w_dato
      *
     c     vhisl3_key    setll     vhisl3
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
8.00  *
8.00  *  sjekk om firmaet kjører MalProff
8.00  *
8.00 c                   read      rmpil1
8.00 c                   if        not %eof(rmpil1)
8.00 c                   eval      w_mp = 'J'
8.00 c                   endif
      *
     c                   endsr
      *
