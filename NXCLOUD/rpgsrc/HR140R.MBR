     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HR140R
      * Beskrivelse . . : Radioterminal trans, lokasjonsregistrering - param
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.11  * R6.10 090512 AMD  Det er lagt inn mulighet for å kunne legge
6.11  * R6.10             inn lokasjon på alternativ 1 og 2
6.12  * R6.10 290812 AMD  Liten justering er lagt inn i tillegg
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fmdlolr    if   e           k disk    rename(mdlopfr:mdlolrr)
     fhr140d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d mdlolr_lage     s                   like(mdlage)
     d mdlolr_loka     s                   like(mdloka)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_kode          s              1
     d w_tims          s               z
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_text          s             30
6.11 d w_test          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      b1lage = 0
     c                   eval      b1vars = 'J'
     c     b1taga        tag
     c                   eval      b1loka = *blank
6.11 c                   eval      b1pla1 = *blank
6.11 c                   eval      b1pla2 = *blank
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tagb
     c                   endsl
      *
      * Sjekker input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
      * Finner unik nøkkel
      *
     c                   eval      w_kode = 'L'
     c                   time                    w_tims
      *
      * Kaller registeringsprogram
      *
     c                   call      'HR141R'
     c                   parm                    w_firm
     c                   parm                    w_kode
     c                   parm                    w_tims
     c                   parm                    b1lage
     c                   parm                    b1loka
6.11 c                   parm                    b1pla1
6.11 c                   parm                    b1pla2
     c                   parm                    b1vars
      *
     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Lager
      *
     c                   if        wactfe = 'B1LAGE'
     c                   call      'RA580R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1latx
     c                   goto      end_spØrr
     c                   endif
      *
      * Lokasjon
      *
     c                   if        wactfe = 'B1LOKA'
     c                   call      'HR142R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1loka
     c                   parm                    b1latx
6.11 c                   eval      b1pla1 = *blank
6.11 c                   eval      b1pla2 = *blank
     c                   goto      end_spØrr
     c                   endif
6.11  *
6.11  * Lokasjon alternativ 1
6.11  *
6.11 c                   if        wactfe = 'B1PLA1'
6.11 c                   call      'HR142R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    b1lage
6.11 c                   parm                    b1loka
6.11 c                   parm                    b1latx
6.11 c                   eval      b1pla1 = b1loka
6.11 c                   eval      b1loka = *blank
6.11 c                   eval      b1pla2 = *blank
6.11 c                   goto      end_spØrr
6.11 c                   endif
6.11  *
6.11  * Lokasjon alternativ 2
6.11  *
6.11 c                   if        wactfe = 'B1PLA2'
6.11 c                   call      'HR142R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    b1lage
6.11 c                   parm                    b1loka
6.11 c                   parm                    b1latx
6.11 c                   eval      b1pla2 = b1loka
6.11 c                   eval      b1loka = *blank
6.11 c                   eval      b1pla1 = *blank
6.11 c                   goto      end_spØrr
6.11 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
     c                   eval      b_feil = *off
      *
      * Lager
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_sjekk
     c                   endif
6.11  *
6.11  * Det kan bare oppgis en lokasjon om gangen
6.11  *
6.11 c                   eval      w_test = 0
6.11  *
6.11 c                   if        b1loka <> ' '
6.11 c                   add       1             w_test
6.11 c                   endif
6.11  *
6.11 c                   if        b1pla1 <> ' '
6.11 c                   add       1             w_test
6.11 c                   endif
6.11  *
6.11 c                   if        b1pla2 <> ' '
6.11 c                   add       1             w_test
6.11 c                   endif
6.11  *
6.11 c                   if        w_test > 1
6.11 c                   eval      b_feil = *on
6.11 c                   eval      *in36 = *on
6.11 c                   goto      end_sjekk
6.11 c                   endif
6.12  *
6.12  * Varsle også dersom ingen lokasjon er oppgitt
6.12  *
6.12 c                   if        w_test = 0
6.12 c                   eval      b_feil = *on
6.12 c                   eval      *in32 = *on
6.12 c                   goto      end_sjekk
6.12 c                   endif
      *
      * Lokasjon
      *
5.20 c                   eval      mdlolr_lage = b1lage
     c                   eval      mdlolr_loka = b1loka
6.12 c                   if        b1loka <> *blank
     c     mdlolr_key    chain     mdlolr
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      end_sjekk
     c                   endif
6.12 c                   endif
6.11  *
6.11  * Lokasjon alternativ 1
6.11  *
6.11 c                   if        b1pla1 <> *blank
6.11 c                   eval      mdlolr_lage = b1lage
6.11 c                   eval      mdlolr_loka = b1pla1
6.11 c     mdlolr_key    chain     mdlolr
6.11 c                   if        not %found
6.11 c                   eval      b_feil = *on
6.11 c                   eval      *in34 = *on
6.11 c                   goto      end_sjekk
6.11 c                   endif
6.11 c                   endif
6.11  *
6.11  * Lokasjon alternativ 2
6.11  *
6.11 c                   if        b1pla2 <> *blank
6.11 c                   eval      mdlolr_lage = b1lage
6.11 c                   eval      mdlolr_loka = b1pla2
6.11 c     mdlolr_key    chain     mdlolr
6.11 c                   if        not %found
6.11 c                   eval      b_feil = *on
6.11 c                   eval      *in35 = *on
6.11 c                   goto      end_sjekk
6.11 c                   endif
6.11 c                   endif
      *
      * Overstyringsvalg
      *
     c                   if        b1vars <> 'J' and
     c                             b1vars <> 'N'
     c                   eval      b_feil = *on
     c                   eval      *in33 = *on
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på lagerlokasjon
      *
     c     mdlolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlolr_lage
     c                   kfld                    mdlolr_loka
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      *  Hent opplysninger fra LAGER-KODE-REGISTER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
