     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK601R
      * Beskrivelse . . : Kunde bonus, skriver ut grunnlag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190911 bsa  Nytt program
6.10  * 6.10  140312 bsa  Sjekker ikke mot år
6.30  * 6.30  080617 bsa  Tar ikke med ikke bonusbyggende elementer i grunnlaget
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekbti3    if   e           k disk    rename(ekbtst:ekbti3r)
     fekbti1    if   e           k disk    rename(ekbtst:ekbti1r)
     fekboir    if   e           k disk    rename(ekbost:ekboirr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
      *
     fek601p    o    e             printer oflind(*in30)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_paar          s              4
     d p_tbot          s              2
     d b_bonu          s              1    inz(*off)
      *
      * Definering av variabler i nøkler
      *
     d ekbti3_tkka     s                   like(ektkka)
     d ekbti1_tbku     s                   like(ektbku)
     d ekboir_bkun     s                   like(ekbkun)
     d rkunl1_kund     s                   like(rkkund)
     d vbotl1_tbot     s                   like(vatbot)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ektfir)
     d w_tbot          s              2
     d w_kund          s              6  0
     d w_bgrl          s             11  2
     d w_aar           s              4  0
      *
      * Parameter overføringer
      *
     d                uds
     d paarf                 300    303  0                                      Pr. år
     d paart                 304    307  0                                      Pr. år
     d pkatf                 308    311  0                                      Kategori f.o.m.
     d pkatt                 312    315  0                                      Kategori t.o.m.
     d pknrf                 316    321  0                                      Knr. f.o.m.
     d pknrt                 322    327  0                                      Knr. t.o.m.
     d ptbot                 328    329                                         Bonustype
      *
      *  Hva skal skrives på liste                                Valg: J/N=Ja/N
     d pabon                 330    330                                         Alle m/avtale
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980                                         Navn
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_bonu = *off
      * Skriv ut heading
     c                   eval      a1aarr = paarf
     c                   write     a1hdr
      *
     c                   eval      t1subg = 0
     c                   eval      t1subn = 0
      * Hvis kundekategori er utfylt, leses på denne - alle kunder
     c                   if        pkatt <> 0
     c                   exsr      kal_kkat
     c                   else
     c                   exsr      kal_kund
     c                   endif
      *
     c                   if        b_bonu = *on
     c                   write     t1tot
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_kkat      begsr
      *
     c                   eval      w_tbot = *blank
     c                   eval      w_kund = 0
      *
     c                   eval      ekbti3_tkka  =  pkatf
     c     ekbti3_key    setll     ekbti3
     c     ekbti3_keyl   reade     ekbti3
      *
     c                   dow       not  %eof(ekbti3)
     c                   if        ektkka > pkatt
     c                   goto      end_kkat
     c                   endif
      *
     c                   if        ptbot <> *blank and
     c                             ektbot <> ptbot
     c                   goto      nyles
     c                   endif
      * Hent bonustypetekst
     c                   if        w_tbot <> ektbot
     c                   exsr      finn_btext
     c                   endif
      * Hent kundetekst
     c                   if        w_kund <> ektbku
     c                   exsr      finn_ktext
     c                   endif
      *
     c                   eval      w_bgrl = 0
     c                   exsr      kal_bgrl
     c                   eval      d1bgru = w_bgrl
     c                   eval      d1sbon = ektsbo
      *
     c                   if        pabon = '1'
     c                   goto      skriv_detalj
     c                   endif
      *
     c                   if        ektsbo = 0 and
     c                             w_bgrl = 0
     c                   goto      nyles
     c                   endif
      *
     c     skriv_detalj  tag
     c                   eval      t1subg = t1subg + w_bgrl
     c                   eval      t1subn = t1subn + ektsbo
     c                   write     d1det                                30
     c                   eval      b_bonu = *on
     c                   if        *in30 = *on
     c                   exsr      xovrfl
     c                   endif
      *
     c     nyles         tag
      *
     c     ekbti3_keyl   reade     ekbti3
     c                   enddo
      *
     c     end_kkat      endsr
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_kund      begsr
      *
     c                   eval      w_tbot = *blank
     c                   eval      w_kund = 0
      *
     c                   eval      ekbti1_tbku  =  pknrf
     c     ekbti1_key    setll     ekbti1
     c     ekbti1_keyl   reade     ekbti1
      *
     c                   dow       not  %eof(ekbti1)
     c                   if        ektbku > pknrt
     c                   goto      end_kund
     c                   endif
      *
     c                   if        ptbot <> *blank and
     c                             ektbot <> ptbot
     c                   goto      nyles2
     c                   endif
      * Hent bonustypetekst
     c                   if        w_tbot <> ektbot
     c                   exsr      finn_btext
     c                   endif
      * Hent kundetekst
     c                   if        w_kund <> ektbku
     c                   exsr      finn_ktext
     c                   endif
      *
     c                   eval      w_bgrl = 0
     c                   exsr      kal_bgrl
     c                   eval      d1bgru = w_bgrl
     c                   eval      d1sbon = ektsbo
      *
     c                   if        pabon = '1'
     c                   goto      skriv_detalj2
     c                   endif
      *
     c                   if        ektsbo = 0 and
     c                             w_bgrl = 0
     c                   goto      nyles2
     c                   endif
      *
     c     skriv_detalj2 tag
     c                   eval      t1subg = t1subg + w_bgrl
     c                   eval      t1subn = t1subn + ektsbo
     c                   write     d1det                                30
     c                   eval      b_bonu = *on
     c                   if        *in30 = *on
     c                   exsr      xovrfl
     c                   endif
      *
     c     nyles2        tag
      *
     c     ekbti1_keyl   reade     ekbti1
     c                   enddo
      *
     c     end_kund      endsr
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_bgrl      begsr
      *
     c                   eval      ekboir_bkun  =  ektbku
     c     ekboir_key    setll     ekboir
     c     ekboir_key    reade     ekboir
      *
     c                   dow       not  %eof(ekboir)
6.30  * Ikke bonusbyggende elementer skal ikke være med i grunnlaget
6.30 c                   if        ekebyg = 0
6.30  *
     c                   if        ekbdat <> *loval
     c                   extrct    ekbdat:*y     w_aar
     c                   else
     c                   eval      w_aar = paarf
     c                   endif
      *
6.10 c**                 if        w_aar = paarf and
6.10 c**                           ektbot = ekboty
6.10 c                   if        ektbot = ekboty
     c                   eval      w_bgrl = w_bgrl + ekbgru
     c                   endif
6.30  *
6.30 c                   endif
     c     ekboir_key    reade     ekboir
     c                   enddo
      *
     c                   endsr
      *--------------------------------------------------------------
      * Finn kundetekst
      *--------------------------------------------------------------
     c     finn_ktext    begsr
      *
     c                   eval      w_kund = ektbku
     c                   eval      d1kund = ektbku
     c                   eval      d1knvn = *blank
     c                   eval      rkunl1_kund = ektbku
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   movel     rknavn        d1knvn
     c                   eval      d1kkat = rkkatg
     c                   endif
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Finn tekst for bonustype
      *--------------------------------------------------------------
     c     finn_btext    begsr
      *
     c                   eval      w_tbot = ektbot
     c                   eval      d1btyp = ektbot
     c                   eval      d1btxt = *blank
     c                   eval      vbotl1_tbot = ektbot
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   movel     vatbes        d1btxt
     c                   endif
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Rutine for håndtering av ny side                            *
      *--------------------------------------------------------------
     c     xovrfl        begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for oppslag kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti1_tbku
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti1_keyl   klist
     c                   kfld                    w_firm
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti3_tkka
      *
      * Key for les av bonusgrunnlag
      *
     c     ekboir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboir_bkun
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti3_keyl   klist
     c                   kfld                    w_firm
      *
      * Henter parameter
      *
     c                   move      l_firm        w_firm
      *
     c                   endsr
