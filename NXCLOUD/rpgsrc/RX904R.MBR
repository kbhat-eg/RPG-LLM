     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : FIX
      * Program . . . . : RX904R
      * Beskrivelse . . : Oppdaterer kortregister med kundeinformasjon. Beregnet på
      *                   semikolonseparert fil.
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140510 bsa  Nytt program
6.10  * 6.10  180912 bsa  Feil lengde på kortnummer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk    rename(lexcpfr:lwexpfr)
     frukrlu    uf a e           k disk    rename(rukrpfr:rukrlur)
     frukrl1    if   e           k disk    rename(rukrpfr:rukrl1r)
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
      *
      *
     d                 ds
     d d_inpu                       256
     d  d_kunde                       6  0 overlay(d_inpu:1)
     d   d_kund                       6    overlay(d_kunde:1)
     d   d_kun5                       5    overlay(d_kunde:1)
     d   d_kun4                       4    overlay(d_kunde:1)
     d   d_kun3                       3    overlay(d_kunde:1)
     d   d_kun2                       2    overlay(d_kunde:1)
     d   d_kun1                       1    overlay(d_kunde:1)
6.10 d  d_bmknr                      11  0 overlay(d_inpu:7)
6.10 d   d_bmkn                      11    overlay(d_bmknr:1)
     d  d_knavn                      30    overlay(d_inpu:20)
     d  d_adres                      30    overlay(d_inpu:50)
     d  d_psted                      30    overlay(d_inpu:80)
     d  d_butnr                       7    overlay(d_inpu:110)
     d  d_bnavn                      30    overlay(d_inpu:117)
     d  d_fulm                        3    overlay(d_inpu:147)
      *
     d b_ok            s              1    inz(*off)
     d w_kunde         s              6
     d w_posi          s              4  0
     d digits          c                   ' 0123456789'
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t
      *
     d                uds
     d l_user                911    920
     d l_fgr                 931    933
     d l_firm                944    946  0
      *
     d w_fast          s             10
     d w_felt          s              6
     d w_kode          s              1
     d w_type          s              2
     d w_firm          s                   like(rkfirm)
     d w_ktsq          s              5  0
     d w_leng          s              2  0
      *
     d p_ofir          s              3  0
     d p_okun          s              6  0
     d p_opro          s              6  0
     d p_okpn          s              6  0
     d p_otyp          s              2  0
     d p_otsq          s              4  0
     d p_oper          s              1
      *
      * Definering av variable i nøkkler
      *
     d rkunlr_kund     s                   like(rkkund)
     d rukrlu_kkun     s                   like(rukkun)
     d rukrlu_kpro     s                   like(rukpro)
     d rukrlu_ktyp     s                   like(ruktyp)
     d rukrlu_ktsq     s                   like(ruktsq)
     d rukrl1_kkun     s                   like(rukkun)
     d rukrl1_kpro     s                   like(rukpro)
     d rukrl1_ktyp     s                   like(ruktyp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Leser inn input file, og splitter record fra regneark.
      *
     c                   read      lwexpf
     c                   dow       not %eof
      *
     c                   eval      b_ok = *off
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
      *   Legg ut ny post på kortregister
      *
     c                   if        b_ok = *on
      * Vi må hente siste sekvensnummer, siden vi skal opprette kort
     c                   exsr      finn_ktsq
      *
     c                   eval      rukrlu_kkun = d_kunde
     c                   eval      rukrlu_ktsq = w_ktsq
     c     rukrlu_key    chain     rukrlur
      *
     c                   if        not %found(rukrlu)
     c                   clear                   rukrlur
     c                   eval      rukfir = w_firm
     c                   eval      rukkun = d_kunde
     c                   eval      rukpro = 0
     c                   eval      ruktyp = 4
     c                   eval      ruktsq = w_ktsq
     c                   eval      rukous = l_user
     c                   eval      rukoti = w_time
     c                   eval      rukoda = w_date
     c                   endif
      *
     c                   move      d_bmknr       rukknr
     c                   move      d_bmknr       rukstr
     c                   eval      rukhld = d_knavn
     c                   eval      rueusr = l_user
     c                   eval      ruetim = w_time
     c                   eval      ruedat = w_date
     c                   if        %trim(d_fulm) = 'Ja'
     c                   eval      rukobl = 1
     c                   endif
      *
     c                   if        %found(rukrlu)
     c                   update    rukrlur
     c                   else
     c                   write     rukrlur
     c                   endif
      *
      * Må legge ut info på OFAK register
      *
     c                   eval      p_ofir = w_firm
     c                   eval      p_okun = d_kunde
     c                   eval      p_opro = 0
     c                   eval      p_okpn = 0
     c                   eval      p_otyp = 4
     c                   eval      p_otsq = w_ktsq
     c                   eval      p_oper = 'W'
      *
     c                   call      'RK112C'
     c                   parm                    p_ofir
     c                   parm                    p_okun
     c                   parm                    p_opro
     c                   parm                    p_okpn
     c                   parm                    p_otyp
     c                   parm                    p_otsq
     c                   parm                    p_oper
      *
     c                   endif
      *
     c                   read      lwexpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av innlest post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c**                 movel     exclin        d_inpu
     c     '"':' '       xlate     exclin        exclin
      * Kundenummer
     c                   eval      w_posi = 0
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_kund = %trim(%subst(exclin:1:w_posi-1))
      *
      * BM Kortnummer
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_bmkn = %trim(%subst(exclin:1:w_posi-1))
      *
      * Kortnavn
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_knavn = %trim(%subst(exclin:1:w_posi-1))
      *
      * Adresse
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_adres = %trim(%subst(exclin:1:w_posi-1))
      *
      * Postnr/sted
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_psted = %trim(%subst(exclin:1:w_posi-1))
      *
      *  Butikknummer
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_butnr = %trim(%subst(exclin:1:w_posi-1))
      *
      *  Butikknavn
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_bnavn = %trim(%subst(exclin:1:w_posi-1))
      *
      *  Fullmakt
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   if        w_posi = 0
     c                   eval      d_fulm = %trim(exclin)
     c                   else
     c                   eval      d_fulm = %trim(%subst(exclin:1:w_posi-1))
     c                   endif
      *
      * Sjekk gyldighet
      *  Kundenummer
      *
     c                   if        %check(digits:d_kund) <> 0
     c                   leavesr
     c                   endif
      *
6.10 c                   eval      w_kunde = *blanks
     c                   eval      w_leng = %len(%trim(d_kund))
     c                   if        w_leng = 1
     c                   move      d_kun1        w_kunde
     c                   elseif    w_leng = 2
     c                   move      d_kun2        w_kunde
     c                   elseif    w_leng = 3
     c                   move      d_kun3        w_kunde
     c                   elseif    w_leng = 4
     c                   move      d_kun4        w_kunde
     c                   elseif    w_leng = 5
     c                   move      d_kun5        w_kunde
     c                   elseif    w_leng = 6
     c                   move      d_kund        w_kunde
     c                   endif
      *
     c                   move      w_kunde       d_kunde
     c                   eval      rkunlr_kund = d_kunde
     c     rkunlr_key    chain     rkunlr
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne høyeste sekv.nr. på kunde/prosjekt/korttype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ktsq     begsr
      *
     c                   eval      rukrl1_kkun = d_kunde
     c                   eval      rukrl1_kpro = 0
     c                   eval      rukrl1_ktyp = 4
      *
     c     rukrl1_key2   setll     rukrl1
     c     rukrl1_key2   reade     rukrl1r
      *
     c                   dow       not %eof
     c     rukrl1_key2   reade     rukrl1r
     c                   enddo
      *
     c                   eval      w_ktsq = ruktsq + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av kunderegister
      *
     c     rukrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrlu_kkun
     c                   kfld                    rukrlu_kpro
     c                   kfld                    rukrlu_ktyp
     c                   kfld                    rukrlu_ktsq
      *
      * Key for les siste nr på kunde/prosjekt/korttype
      *
     c     rukrl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    rukrl1_kkun
     c                   kfld                    rukrl1_kpro
     c                   kfld                    rukrl1_ktyp
      *
      * Key for oppdatering av kunderegister
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
     c                   eval      w_firm = l_firm
     c                   eval      rukrlu_ktyp = 4
     c                   eval      rukrlu_kpro = 0
     c                   time                    w_time
     c                   time                    w_date
      *
     c                   endsr
