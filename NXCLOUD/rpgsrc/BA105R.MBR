     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSHOP
      * Program . . . . : BA105R
      * Beskrivelse . . : Vedlikehold av betalingskortkoder
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141014 ASK  Nytt program
7.xx  *       230119 bsa  Tilpasset ny GUI
7.01  *       131117 bsa  Legger inn reskontronummer og bilagsnummerserie som
7.01  *                   brukes ved dobbeltføring av bongbetaling og
7.01  *                   Santander.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbokoi1    if   e           k disk    rename(bokost:bokoi1r)
     fbokoir    if   e           k disk    rename(bokost:bokoirr)
     fbokoiu    uf a e           k disk    rename(bokost:bokoiur)
7.01 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.01 fanuml1    if   e           k disk    rename(anumpfr:anuml1r)
7.01 fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
      *
     fba105d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d bokoi1_ktyp     s                   like(byktyp)
     d bokoir_ktyp     s                   like(byktyp)
     d bokoiu_ktyp     s                   like(byktyp)
7.01 d rkunl1_kund     s                   like(rkkund)
7.01 d anuml1_fell     s                   like(anfell)
7.01 d anuml1_type     s                   like(antype)
7.01 d anuml1_fast     s                   like(anfast)
7.01 d ra01l1_akod     s                   like(raakod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
7.01 d u_sant          s              1    inz(*off)
      *
     d w_firm          s                   like(byfirm)
7.01 d w_bilk          s                   like(raakod)
7.01 d w_resk          s                   like(rkkund)
7.01 d w_navn          s             30
     d w_kont          s              5  0
     d w_spes          s              4  0
     d w_avde          s              4  0
     d w_text          s             30
7.01 d w_tx20          s             20
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(15)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   dcl-pr co402r extpgm('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   end-pr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      bokoi1_ktyp = b1ktyp
     c     bokoi1_key    setll     bokoi1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ktyp = b1ktyp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ktyp = b1ktyp
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ktyp = b1ktyp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ktyp = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      bokoir_ktyp = c1ktyp
     c     bokoir_key    chain     bokoir
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ktyp = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      bokoi1_ktyp = c1ktyp
     c     bokoi1_key    chain     bokoi1
      *
     c                   if        %found
     c                   eval      c2kttx = bykttx
     c                   eval      c2hkon = byhkon
     c                   eval      c2spes = byspes
7.01 c                   eval      c2resk = byresk
7.01 c                   eval      c2binu = bybinu
7.01 c                   eval      c2bilk = bybilk
     c                   eval      c2eusr = byeusr
     c     *dmy          move      byodat        c2odat
     c     *dmy          move      byedat        c2edat
     c                   else
     c                   eval      c2kttx = *blank
     c                   eval      c2hkon = *zeros
     c                   eval      c2spes = *zeros
7.01 c                   eval      c2resk = *zeros
7.01 c                   eval      c2binu = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   eval      c2odat = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      * Spørr på hovedbokskonto
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2HKON'
      *
     c                   call      'RH500R'
     c                   parm                    w_firm
     c                   parm                    w_kont
     c                   parm                    w_spes
     c                   parm                    w_avde
     c                   parm                    w_text
      *
     c                   if        w_kont <> *zero
     c                   eval      c2hkon = w_kont
     c                   eval      c2spes = w_spes
     c                   endif
     c                   goto      c2taga
     c                   endif
7.01  *
7.01  * Spørr på kundenummer
7.01  *
7.01 c                   if        *inkd = *on and
7.01 c                             w_afld = 'C2RESK'
7.01  *
7.01 c                   call      'RK500R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_resk
7.01 c                   parm                    w_navn
7.01  *
7.01 c                   if        w_resk <> *zero
7.01 c                   eval      c2resk = w_resk
7.01 c                   endif
7.01 c                   goto      c2taga
7.01 c                   endif
7.01  *
7.01  * Spørring på nummerserier
7.01  *
7.01 c                   if        *inkd = *on and
7.01 c                             w_afld = 'C2BINU'
7.01 c                   call      'RA531R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    c2binu
7.01  *
7.01 c                   goto      c2taga
7.01 c                   endif
7.01  *
7.01  * Spørr på bilagskoder
7.01  *
7.01 c                   if        *inkd = *on and
7.01 c                             w_afld = 'C2BILK'
7.01  *
7.01 c                   call      'RA501R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_bilk
7.01 c                   parm                    w_tx20
7.01  *
7.01 c                   if        w_bilk <> *zero
7.01 c                   eval      c2bilk = w_bilk
7.01 c                   endif
7.01 c                   goto      c2taga
7.01 c                   endif
7.01  *
7.01  * Skal kun skje hvis de kjører Santander løsning
7.01  *
7.01  * Sjekk på gyldig input av reskontronummer
7.01  *
7.01 c                   if        u_sant = *on
7.01  *
7.01 c                   if        c1ktyp = '98' and
7.01 c                             c2resk =  0
7.01 c                   eval      *in31 = *on
7.01 c                   goto      c2taga
7.01 c                   endif
7.01  *
9.01 c                   if        c2resk <> 0
7.01 c                   eval      rkunl1_kund = c2resk
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        not %found
7.01 c                   eval      *in31 = *on
7.01 c                   goto      c2taga
7.01 c                   endif
7.01 c                   endif
7.01  *
7.01 c                   if        c1ktyp = '98' and
7.01 c                             c2binu = *blank
7.01 c                   eval      *in32 = *on
7.01 c                   goto      c2taga
7.01 c                   endif
7.01  *
7.01  * Sjekk på bilagsnummerserie
7.01  *
7.01 c                   if        c2binu <>*blank
7.01 c                   eval      anuml1_fell = c2binu
7.01 c                   eval      anuml1_type = *blank
7.01 c                   eval      anuml1_fast = *blank
7.01 c     anuml1_key    chain     anuml1r
7.01 c                   if        not %found
7.01 c                   eval      *in32 = *on
7.01 c                   goto      c2taga
7.01 c                   endif
7.01 c                   endif
7.01  *
7.01  * Sjekk bilagskode
7.01  *
7.01 c                   if        c2bilk <> 0
7.01 c                   eval      ra01l1_akod = c2bilk
7.01 c     ra01l1_key    chain     ra01l1
7.01 c                   if        not %found
7.01 c                   eval      *in33 = *on
7.01 c                   goto      c2taga
7.01 c                   endif
7.01 c                   endif
7.01  *
7.01 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   bokoiur
      *
     c                   eval      bokoiu_ktyp = c1ktyp
     c     bokoiu_key    chain     bokoiur
      *
     c                   eval      bykttx = c2kttx
     c                   eval      byhkon = c2hkon
     c                   eval      byspes = c2spes
7.01 c                   eval      byresk = c2resk
7.01 c                   eval      bybinu = c2binu
7.01 c                   eval      bybilk = c2bilk
      *
     c                   time                    byodat
     c                   time                    byotim
     c                   time                    byedat
     c                   time                    byetim
     c                   eval      byeusr = l_user
      *
     c                   if        %found
     c                   update    bokoiur
     c                   else
     c                   eval      byfirm = w_firm
     c                   eval      byktyp = c1ktyp
     c                   write     bokoiur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ktyp = byktyp
     c                   eval      b1kttx = bykttx
     c                   eval      b1hkon = byhkon
     c                   eval      b1spes = byspes
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      bokoir_ktyp = d1ktyp
     c     bokoir_key    chain     bokoir
     c                   eval      d1kttx = bykttx
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      bokoiu_ktyp = d1ktyp
     c     bokoiu_key    chain     bokoiur
      *
     c                   delete    bokoiur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      bokoi1
      *
     c                   if        %eof or
     c                             byfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      bokoi1_ktyp = byktyp
      *
     c                   eval      b1valg = 0
     c                   eval      b1ktyp = byktyp
     c                   eval      b1kttx = bykttx
     c                   eval      b1hkon = byhkon
     c                   eval      b1spes = byspes
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     bokoi1_key    setgt     bokoi1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     bokoi1
      *
     c                   if        %eof or
     c                             byfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      bokoi1_ktyp = byktyp
      *
     c                   enddo
      *
     c                   if        *in15
     c     bokoi1_key    setll     bokoi1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på korttype
      *
     c     bokoi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokoi1_ktyp
      *
      * Key for les på korttype
      *
     c     bokoir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokoir_ktyp
      *
      * Key for oppdatering av korttype
      *
     c     bokoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokoiu_ktyp
7.01  *
7.01  * Key for oppslag på kunderegister
7.01  *
7.01 c     rkunl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rkunl1_kund
      *
      * Key for nummerteller
      *
7.01 c     anuml1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    anuml1_fell
7.01 c                   kfld                    anuml1_type
7.01 c                   kfld                    anuml1_fast
7.01  *
7.01  * Key for oppslag på bilagskode
7.01  *
7.01 c     ra01l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    ra01l1_akod
7.01  *
7.01  * Brukes egen kontering på dir.lev mot prosjekt
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'SANTANDER_KID':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_sant = *on;
7.01   endif;
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      bokoi1_ktyp = *blank
     c     bokoi1_key    setll     bokoi1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
