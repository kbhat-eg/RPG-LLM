      /title  Inntasting av parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AX000R
      * Beskrivelse . . : Parameter for bestilling av rapporter til Altin
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
Versj *Uts   Dato Usr Referanse Beskrivelse
      *____________________________________________________________________________________________
      *K00 - 200420 START FOR VERSJON K00
7.00  *K00 200420 SST NXS-4426  Utrulling av SAFT-Rapporter til alle kundene
7.01  *K00 270923 sst           Åpne for kjøring av rapporter lenger bakover i tid.
      *
      *
      *____________________________________________________________________________________________
      *
     fax000d    cf   e             workstn
      *
     fafirl1    if   e           k disk
      *
      *
      * Definering av local data area
      *
     d                uds
     d l_fper                  1      6
     d l_tper                  7     12
     d l_kom1                 13     30
     d l_kom2                 31     62
     d l_head                 63     63
     d l_ktop                 64     64
     d l_kund                 65     65
     d l_levr                 66     66
     d l_moms                 67     67
     d l_kodr                 68     68
     d l_hbok                 69     69
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_avde                947    950  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedlogikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   eval      p_fper = %char(umonth-1) + '20' +
     c                                      %char(uyear)
     c                   eval      p_tper = %char(umonth-1) + '20' +
     c                                      %char(uyear)
     c                   if        %subst(p_fper:6:1) = *blank
     c                   eval      %subst(p_fper:2:5) = %subst(p_fper:1:5)
     c                   eval      %subst(p_fper:1:1) = '0'
     c                   eval      %subst(p_tper:2:5) = %subst(p_tper:1:5)
     c                   eval      %subst(p_tper:1:1) = '0'
     c                   endif
      *
     c                   eval      p_kom1 = *blank
     c                   eval      p_kom2 = *blank
     c                   eval      p_head = *blank
     c                   eval      p_ktop = *blank
     c                   eval      p_kund = *blank
     c                   eval      p_levr = *blank
     c                   eval      p_moms = *blank
     c                   eval      p_kodr = *blank
     c                   eval      p_hbok = *blank
      *
     c     a2taga        tag
      *
      * Sender opp skjermbildet
      *
     c                   exfmt     a1win
      *
      * Annullere registrering.
      *
     c                   if        *inkc = *on
     c                   eval      p_fper = '*end  '
     c                   goto      xslutt
     c                   endif
      *
      *  Sjekk om fra periode er ok
      *
     c                   if        %subst(p_fper:1:2) < '01' or
     c                             %subst(p_fper:1:2) > '12' or
     c                             %subst(p_fper:3:2) <> '20' or
7.01 c                             %subst(p_fper:5:2) < %char(uyear - 10) or
     c                             %subst(p_fper:5:2) > %char(uyear)
     c                   goto      a2taga
     c                   endif
      *
      *  Sjekk om til periode er ok
      *
     c                   if        %subst(p_tper:1:2) < '01' or
     c                             %subst(p_tper:1:2) > '12' or
     c                             %subst(p_tper:3:2) <> '20' or
7.01 c                             %subst(p_tper:5:2) < %char(uyear - 10) or
     c                             %subst(p_tper:5:2) > %char(uyear)
     c                   goto      a2taga
     c                   endif
      *
      * Sjekk om noen valg er foretatt
      *
     c                   if        p_head = *blank and
     c                             p_ktop = *blank  and
     c                             p_kund = *blank  and
     c                             p_levr = *blank  and
     c                             p_moms = *blank  and
     c                             p_kodr = *blank  and
     c                             p_hbok = *blank
     c                   goto      a2taga
     c                   endif
      *
      *  Parameter er ok og vi kan avslutte
      *
     c                   eval      %subst(ax000par:01:06) = p_fper
     c                   eval      %subst(ax000par:07:06) = p_tper
     c                   eval      %subst(ax000par:13:18) = p_kom1
     c                   eval      %subst(ax000par:31:32) = p_kom2
     c                   eval      %subst(ax000par:63:01) = p_head
     c                   eval      %subst(ax000par:64:01) = p_ktop
     c                   eval      %subst(ax000par:65:01) = p_kund
     c                   eval      %subst(ax000par:66:01) = p_levr
     c                   eval      %subst(ax000par:67:01) = p_moms
     c                   eval      %subst(ax000par:68:01) = p_kodr
     c                   eval      %subst(ax000par:69:01) = p_hbok
      *
      *  Legg på foretaksnummer i parameter
      *
     c     l_firm        chain     afirl1
     c                   if        %found(afirl1)
     c                   eval      %subst(ax000par:90:9) = %char(aafftn)
     c                   endif
     c                   out       *dtaara
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define                  ax000par        100
     c     *lock         in        *dtaara
     c                   eval      w_alin = 3
     c                   eval      w_apos = 5
     c                   write     s1win
      *
     c                   endsr
