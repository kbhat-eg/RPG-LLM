     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RX000R
      * Beskrivelse . . : Henter file og bane til ovf-jobb
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       021020 sst  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FrscdL1    IF   E           K DISK    RENAME(rscdPFR:rscdL1R)
7.03 FaftpL1    IF   E           K DISK    RENAME(aftpPFR:aftpL1R)
      *
      *
     D                UDS
6.10 d l_user                911    920
6.10 d l_filg                931    933
     D  l_firm               944    946  0
     D  l_fnav               951    980
      *
     d w3stop          s              8
     d w_m50           s             50
     d stsfir          s                   like(l_firm)
     d aftpl1_firm     s                   like(l_firm)
     d aftpl1_navn     s                   like(afnavn)
     d p_file          s             20
     d p_bane          s             70
     d p_firm          s              3
     d p_job           s             10
      *
     c                   eval      stsfir = %dec(p_firm:3:0)
     C     STSKEY        CHAIN     rscdL1
     C                   IF        %found(rscdl1)
     C                   eval      aftpl1_firm = stsfir
     C                   eval      aftpl1_navn = rcftpa
     c     aftpl1_key    chain     aftpl1
     c                   if        not %found(aftpl1)
     c                             or  aflocd = ' '
     c                   eval      w_m50 = 'Lokal bane mangler i FTP-Prameter'
     c                   call      'AA005R'
     c                   parm                    w_m50
     c                   else
      *
     c                   eval      p_bane = aflocd
     c                   eval      p_file = affiln
     c                   eval      p_job  = rcjnav
     c                   endif
     c                   endif
      *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
     c     *entry        plist
     c                   parm                    p_file
     c                   parm                    p_bane
     c                   parm                    p_firm
     c                   parm                    p_job
      *
      *  Key for file : OFAK-KODE-REGISTER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
     C     aftpl1_key    KLIST
     C                   kfld                    aftpl1_firm
     C                   kfld                    aftpl1_navn
      *
     C                   ENDSR
