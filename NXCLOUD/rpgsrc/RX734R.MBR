     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RX734R
      * Beskrivelse . . : Fjerner (,) i navnefelt og oppdaterer alfafelt
      *                   dersom dette er lik første 12 pos i navn for å
      *                   få med inntil 18 pos fra navn inn i alfa
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110705 KOB  Nytt program
 5.42 * V5.42 260805 KOB  Editering av kundens tlfn. (nn nn nn nn)
 5.42 * V5.42 260805 KOB  Editering av kundens tlfx. (nn nn nn nn)
 5.42 * V5.42 260805 KOB  Editering av kundens mobn. (nn nn nn nn)
 5.42 * V5.42 300905 GRO  Korr. av subrut. for tlf.nr.
 5.42 * V5.42 011005 KOB  Rettet feil hvis , i første eller siste pos.
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 070510 BSA  Endret sporingsfelter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
      * Definering av variable
      *
     d w               s              2  0
     d w_firm          s                   like(rkfirm)
     d w_navn          s             12
     d w_navn1         s             30
     d w_navn2         s             30
5.42 d w_tlfi          s             11
5.42 d w_tlfu          s             11
5.42 d w_tlfw          s             11
     d w_tnum          s              2  0
     d w_tptr          s              2  0
      *
     d rkunlu_firm     s                   like(rkfirm)
      *
      * Definering av konstanter
      *
5.42 d c_digi          s             11    inz('0123456789')
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
      *
     d ux              c                   '²ÔÖ'
     d uy              c                   'ÆØÅ'
      *
      * Kunderegister
      *
     c                   eval      rkunlu_firm = l_firm
     c     rkunlu_key    setll     rkunlu                                 91
     c     rkunlu_key    reade     rkunlu                                 91
      *
     c                   dow       *in91 = *off
      *
      * Fjern (,) fra navn og alfafelt
      *
     c                   eval      w      = %scan(',':rknavn)
     c                   if        w > 1 and
     c                             w < 30
     c                   eval      w_navn1 = %subst(rknavn:1:w-1)
     c                   eval      w_navn2 = %subst(rknavn:w+1)
     c                   eval      rknavn  = %trim(w_navn1)+' '+%trim(w_navn2)
     c                   endif
      *
     c                   if        w = 1
     c                   eval      w_navn2 = %subst(rknavn:w+1)
     c                   eval      rknavn  = %trim(w_navn2)
     c                   endif
      *
     c                   if        w = 30
     c                   eval      w_navn1 = %subst(rknavn:1:w-1)
     c                   eval      rknavn  = %trim(w_navn1)
     c                   endif
      *
     c                   eval      w      = %scan(',':rkalfa)
     c                   if        w > 1 and
     c                             w < 18
     c                   eval      w_navn1 = %subst(rkalfa:1:w-1)
     c                   eval      w_navn2 = %subst(rkalfa:w+1)
     c                   eval      rkalfa  = %trim(w_navn1)+' '+%trim(w_navn2)
     c                   endif
      *
     c                   if        w = 1
     c                   eval      w_navn2 = %subst(rkalfa:w+1)
     c                   eval      rkalfa  = %trim(w_navn2)
     c                   endif
      *
     c                   if        w = 18
     c                   eval      w_navn1 = %subst(rkalfa:1:w-1)
     c                   eval      rkalfa  = %trim(w_navn1)
     c                   endif
      *
      * Korriger feil verdier i alfafelt
      *
     c     ux:uy         xlate     rkalfa        rkalfa
      *
      * Oppdater alfa-felt dersom 12 første pos i navn = gml. alfa
      *
     c     lo:up         xlate     rknavn        w_navn
      *
     c                   if        w_navn = rkalfa
     c     lo:up         xlate     rknavn        rkalfa
     c                   endif
5.42  *
5.42 c                   move      rktlfn        w_tlfi
5.42 c                   exsr      tlf_edit
5.42 c                   move      w_tlfu        rktlfn
5.42  *
5.42 c                   move      rkmobn        w_tlfi
5.42 c                   exsr      tlf_edit
5.42 c                   move      w_tlfu        rkmobn
5.42  *
5.42 c                   move      rktlfx        w_tlfi
5.42 c                   exsr      tlf_edit
5.42 c                   move      w_tlfu        rktlfx
      *
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RX734R    '
     c                   update    rkunlur
      *
     c     rkunlu_key    reade     rkunlu                                 91
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
5.42  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
5.42  * Editerer kundens tlf.nr. til 'nn nn nn nn' om åtte numeriske    *
5.42  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
5.42  *
5.42 c     tlf_edit      begsr
5.42  *
5.42 c                   eval      w_tnum = *zero
5.42 c                   eval      w_tptr = *zero
5.42 c                   eval      w_tlfw = *blank
5.42 c                   eval      w_tlfu = *blank
5.42  *
5.42 c                   if        %check(c_digi : %trim(w_tlfi)) <> 0 or
     c                             w_tlfi = *blank
5.42 c                   eval      w_tlfu = w_tlfi
5.42 c                   leavesr
5.42 c                   endif
5.42  *
5.42 c                   dow       w_tptr < %len(w_tlfi)
5.42 c                   eval      w_tptr = w_tptr + 1
5.42 c                   if        %check(c_digi : %subst(w_tlfi:w_tptr:1)) = 0
5.42 c                   if        %subst(w_tlfi:w_tptr:1) <> *blank
5.42 c                   eval      w_tnum = w_tnum + 1
5.42 c                   eval      %subst(w_tlfw:w_tnum:1) =
5.42 c                             %subst(w_tlfi:w_tptr:1)
5.42 c                   endif
5.42 c                   endif
5.42 c                   enddo
5.42  *
5.42 c                   if        w_tnum = 8
5.42 c                   eval      %subst(w_tlfu:1:2) =
5.42 c                             %subst(w_tlfw:1:2)
5.42 c                   eval      %subst(w_tlfu:4:2) =
5.42 c                             %subst(w_tlfw:3:2)
5.42 c                   eval      %subst(w_tlfu:7:2) =
5.42 c                             %subst(w_tlfw:5:2)
5.42 c                   eval      %subst(w_tlfu:10:2) =
5.42 c                             %subst(w_tlfw:7:2)
5.42 c                   else
5.42 c                   eval      w_tlfu = w_tlfi
5.42 c                   endif
5.42  *
5.42 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for lesing av kunderegister
      *
     c     rkunlu_key    klist
     c                   kfld                    rkunlu_firm
      *
     c                   endsr
