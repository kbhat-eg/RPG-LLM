     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSHOP
      * Program . . . . : BO313R
      * beskrivelse . . : Kasseoppgjør, rapport hode, vis
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050906 bø   Nytt program
5.60  *       180808 bø   Utvidet med nytt søkebegrep på diff (botri2)
6.20  *       130912 bø   Lagt på filter, posisjonering på opprettet dato
7.xx  * 7.00  260516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbotri1    if   e           k disk    rename(botrst:botri1r)
     fbotri2    if   e           k disk    rename(botrst:botri2r)
6.20 fbotri3    if   e           k disk    rename(botrst:botri3r)
     fbotrir    if   e           k disk    rename(botrst:botrirr)
6.20 fbotriu    uf   e           k disk    rename(botrst:botriur)
      *
     fbo313d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d botri2_diff     s                   like(brdiff)
6.20 d botri3_odat     s                   like(brodat)
     d botri1_tell     s                   like(brtell)
     d botrir_tell     s                   like(brtell)
     d botriu_tell     s                   like(brtell)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.20 d b_subf          s              1    inz(*off)
      *
     d w_firm          s                   like(brfirm)
6.20 d w_odat          s                   like(brodat)
      *
     d s_tell          s                   like(brtell)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2ctl  - br Subfile - control record
      * b2cmd  - br Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * b2ctl Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     brtaga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     brtagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
6.20 c                   when      *inka
6.20 c                   exsr      filter
6.20 c                   goto      brtagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      brtagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      brtagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      brtagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      brtaga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2tell <> 0 or
     c                             b2diff <> 0
     c                   exsr      posisjoner
     c                   goto      brtagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      brtaga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
6.20 c                   when      w_seqe = 'C'
6.20 c                   if        b1odat <> 0
6.20 c     *dmy          move      b1odat        w_odat
6.20 c                   else
6.20 c                   eval      w_odat = *loval
6.20 c                   endif
6.20 c                   eval      botri3_odat = w_odat
6.20 c     botri3_key    setll     botri3
     c                   when      w_seqe = 'B'
     c                   eval      botri2_diff = b1diff
     c     botri2_key    setll     botri2
     c                   other
     c                   eval      botri1_tell = b1tell
     c     botri1_key    setll     botri1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Teller
      *
     c                   if        b2tell <> 0
     c                   eval      w_seqe = *blank
     c                   eval      botri1_tell = b2tell
     c     botri1_key    setll     botri1
     c                   goto      end_pos
     c                   endif
      *
      * Differanse
      *
     c                   if        b2diff <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      botri2_diff = b2diff
     c     botri2_key    setll     botri2
     c                   goto      end_pos
     c                   endif
6.20  *
6.20  * Opprettet dato
6.20  *
6.20 c                   if        b2odat <> 0
6.20 c                   eval      w_seqe = 'C'
6.20 c                   if        b2odat <> 0
6.20 c     *dmy          move      b2odat        w_odat
6.20 c                   else
6.20 c                   eval      w_odat = *loval
6.20 c                   endif
6.20 c                   eval      botri3_odat = w_odat
6.20 c     botri3_key    setll     botri3
6.20 c                   goto      end_pos
6.20 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tell = 0
     c                   eval      b2diff = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1tell = b1tell
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      *
     c                   if        b1valg = 7
     c                   call      'BO314R  '
     c                   parm                    w_firm
     c                   parm                    b1tell
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      botrir_tell = c1tell
     c     botrir_key    chain     botrir
      *
     c                   if        %found
     c                   eval      c2strt = brstrt
     c                   eval      c2slut = brslut
     c                   eval      c2berg = brberg
     c                   eval      c2diff = brdiff
     c                   eval      c2eusr = breusr
     c     *dmy          move      brodat        c2odat
     c     *hms          move      brotim        c2otim
     c     *dmy          move      bredat        c2edat
     c     *hms          move      bretim        c2etim
     c                   eval      c2eusr = breusr
     c                   else
     c                   eval      c2strt = 0
     c                   eval      c2slut = 0
     c                   eval      c2berg = 0
     c                   eval      c2diff = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2otim = 0
     c                   eval      c2edat = 0
     c                   eval      c2etim = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
6.20  *
6.20 c                   if        *inkh
6.20 c                   eval      botriu_tell = c1tell
6.20 c                   exsr      godkjenn
6.30 c                   if        brgodk = 1
6.20 c                   eval      b1godk = 'J'
6.20 c                   else
6.20 c                   eval      b1godk = *blank
6.20 c                   endif
6.20 c                   endif
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
6.20 c                   when      w_seqe = 'C'
6.20 c                   read      botri3
     c                   when      w_seqe = 'B'
     c                   read      botri2
     c                   other
     c                   read      botri1
     c                   endsl
      *
     c                   if        %eof or
     c                             brfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
6.20  *
6.20  * sjekker om filter
6.20  *
6.20 c                   exsr      chk_subfile
6.20 c                   if        b_subf = *off
6.20 c                   iter
6.20 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
6.20 c                   when      w_seqe = 'C'
6.20 c                   eval      botri3_odat = brodat
     c                   when      w_seqe = 'B'
     c                   eval      botri2_diff = brdiff
     c                   other
     c                   eval      botri1_tell = brtell
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1tell = brtell
     c                   eval      b1strt = brstrt
     c                   eval      b1slut = brslut
     c                   eval      b1berg = brberg
     c                   eval      b1diff = brdiff
6.20 c                   if        brodat <> *loval
6.20 c     *dmy          move      brodat        b1odat
6.20 c                   else
6.20 c                   eval      b1odat = 0
6.20 c                   endif
6.30 c                   if        brgodk = 1
6.20 c                   eval      b1godk = 'J'
6.20 c                   else
6.20 c                   eval      b1godk = *blank
6.20 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
6.20 c                   when      w_seqe = 'C'
6.20 c     botri3_key    setgt     botri3
6.20 c                   readp     botri3
     c                   when      w_seqe = 'B'
     c     botri2_key    setgt     botri2
     c                   readp     botri2
     c                   other
     c     botri1_key    setgt     botri1
     c                   readp     botri1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
6.20 c                   when      w_seqe = 'C'
6.20 c                   readp     botri3
     c                   when      w_seqe = 'B'
     c                   readp     botri2
     c                   other
     c                   readp     botri1
     c                   endsl
      *
     c                   if        %eof or
     c                             brfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
6.20 c                   when      w_seqe = 'C'
6.20 c                   eval      botri3_odat = brodat
     c                   when      w_seqe = 'B'
     c                   eval      botri2_diff = brdiff
     c                   other
     c                   eval      botri1_tell = brtell
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
6.20 c                   when      w_seqe = 'C'
6.20 c     botri3_key    setll     botri3
     c                   when      w_seqe = 'B'
     c     botri2_key    setll     botri2
     c                   other
     c     botri1_key    setll     botri1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * F1 Behandle bilde for filter
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     filter        begsr
6.20  *
6.20  *  Send Alt
6.20  *
6.20 c     xf1tag        tag
6.20 c                   write     f1win
6.20 c                   exfmt     f1win
6.20  *
6.20  *  F12=Avslutt subrutinen
6.20  *
6.20 c                   if        *inkc or *inkl
6.20 c                   goto      xf1end
6.20 c                   endif
6.20  *
6.20  * Posisjonerer databasen, og fyll opp subfilen pånytt
6.20  *
6.20 c                   eval      w_seqe = *blank
6.20  *
6.20 c                   eval      botri1_tell = *hival
6.20 c     botri1_key    setll     botri1
6.20  *
6.20 c                   exsr      clr_subfile
6.20 c                   exsr      crt_subfile
6.20  *
6.20 c     xf1end        endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å sjekke om rad skal være med i subfile
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     chk_subfile   begsr
6.20  *
6.20 c                   eval      b_subf = *off
6.20  *
6.20  * sjekk om om diff > 0
6.20  *
6.20 c                   if        f1diff <> 0  and
6.20 c                             b1diff = 0
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20  * sjekk om godkjent
6.20  *
6.20 c                   if        f1gdok <> 0  and
6.20 c                             b1godk = 'J'
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20 c                   eval      b_subf = *on
6.20  *
6.20 c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å godkjenne et oppgjør
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     godkjenn      begsr
6.20  *
6.20 c     botriu_key    chain     botriu
6.20 c                   if        %found(botriu)
6.20 c                   eval      brgodk = 1
6.20 c                   time                    bredat
6.20 c                   time                    bretim
6.20 c                   eval      breusr = l_user
6.20 c                   update    botriur
6.20 c                   endif
6.20  *
6.20 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på kasseoppgjør hode
      *
     c     botri1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    botri1_tell
      *
      * Key for posisjonering på differanse
      *
     c     botri2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    botri2_diff
      *
6.20  * Key for posisjonering på dato
6.20  *
6.20 c     botri3_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    botri3_odat
      *
      * Key for les på kasseoppgjør hode
      *
     c     botrir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    botrir_tell
      *
      * Key for oppdatering av kasseoppgjør holde
      *
     c     botriu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    botriu_tell
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      botri1_tell = *hival
     c     botri1_key    setll     botri1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
