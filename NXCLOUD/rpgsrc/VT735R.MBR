     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : FW730R
      * Beskrivelse . . : Oppdaterer fra nobb integrasjonsfil
      *
      *                   Oppdaterer tilbud  fra Optimera
      *
      *
      * Spesialversjon. : Leser fil på INTEG.DAT format fra Optimera
      *                   Oppdaterer direkte i ADB - registre
      * Tilpasset for . : Optimera
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       310317 bof  Nytt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
      *
6.12 fvtillu    uf a e           k disk    rename(vtilpfr:vtillur)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
5.63 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
      *
     ffw727p    o    e             printer
      *
      * Array
      *
     d a_erec          s            297    dim(99)
     d a_prec          s            297    dim(99)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_kamp          s              5
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastrukturer
      *
      * Definering av varerecord
      *
     d                 ds
     d d_vrec                       297
     d  d_nobb                        8  0 overlay(d_vrec:3)
     d  d_modn                        8  0 overlay(d_vrec:12)
     d  d_vgrp                        7    overlay(d_vrec:21)
     d   d_ogrp                       2  0 overlay(d_vgrp)
     d   d_hgrp                       2  0 overlay(d_vgrp:3)
     d   d_ugrp                       3  0 overlay(d_vgrp:5)
     d  d_tek1                       35    overlay(d_vrec:30)
     d  d_tek2                       35    overlay(d_vrec:68)
     d  d_mte1                       35    overlay(d_vrec:106)
     d  d_mte2                       35    overlay(d_vrec:144)
     d  d_eann                       13  0 overlay(d_vrec:181)
     d  d_pvar                       15    overlay(d_vrec:196)
     d  d_lvar                       15    overlay(d_vrec:214)
     d  d_ldor                        6  0 overlay(d_vrec:231)
     d  d_prod                        6  0 overlay(d_vrec:238)
     d  d_adat                        6  0 overlay(d_vrec:247)
     d  d_udat                        6  0 overlay(d_vrec:256)
     d  d_merk                       30    overlay(d_vrec:264)
     d  d_leve_alf                    1    overlay(d_vrec:297)
     d   d_leve                       1  0 overlay(d_leve_alf:1)
      *
      * Definering av pakningsrecord
      *
     d                 ds
     d d_erec                       297
     d  d_pakn                        3    overlay(d_erec:4)
     d  d_enhe                        3    overlay(d_erec:10)
     d  d_bred                        6  0 overlay(d_erec:23)
     d  d_leng                        6  0 overlay(d_erec:30)
     d  d_hoyd                        6  0 overlay(d_erec:37)
     d  d_vekt                        9  0 overlay(d_erec:44)
     d  d_volu                        8  5 overlay(d_erec:54)
     d  d_anta                        8  2 overlay(d_erec:63)
     d  d_eean                       13  0 overlay(d_erec:72)
     d  d_ofak                       10  4 overlay(d_erec:86)
     d  d_edun                       14  0 overlay(d_erec:97)
     d  d_best                        1  0 overlay(d_erec:112)
     d  d_paks                        3    overlay(d_erec:115)
     d  d_antp_alf                    8    overlay(d_erec:120)
     d   d_antp                       8  0 overlay(d_antp_alf:1)
     d  d_psei_alf                    1    overlay(d_erec:129)
     d   d_psei                       1  0 overlay(d_psei_alf:1)
      *
      * Definering av prisrecord
      *
     d                 ds
     d d_prec                       297
     d  d_pris                        9  2 overlay(d_prec:3)
     d  d_gdat                        6  0 overlay(d_prec:15)
     d  d_pldo                        6  0 overlay(d_prec:22)
     d  d_plva                       15    overlay(d_prec:30)
     d  d_ipri                        9  2 overlay(d_prec:47)
     d  d_kpri                        9  2 overlay(d_prec:57)
     d  d_spri                        9  2 overlay(d_prec:67)
     d  d_tdat                        6  0 overlay(d_prec:79)
     d  d_valu                        3    overlay(d_prec:86)
     d  d_ppva                       15    overlay(d_prec:91)
5.67 d  d_pean_alf                   13    overlay(d_prec:108)
     d   d_pean                      13  0 overlay(d_pean_alf:1)
6.12  * kampanje-del
6.12 d  d_v_vkai                      9  2 overlay(d_prec:123)
6.12 d  d_v_fil3                      1    overlay(d_prec:132)
6.12 d  d_v_ifda                      8  0 overlay(d_prec:133)
6.12 d  d_v_fil4                      1    overlay(d_prec:141)
6.12 d  d_v_itda                      8  0 overlay(d_prec:142)
6.12 d  d_v_fil5                      1    overlay(d_prec:150)
6.12 d  d_v_kpri                      9  2 overlay(d_prec:151)
6.12 d  d_v_fil6                      1    overlay(d_prec:160)
6.12 d  d_v_kfda                      8  0 overlay(d_prec:161)
6.12 d  d_v_fil7                      1    overlay(d_prec:169)
6.12 d  d_v_ktda                      8  0 overlay(d_prec:170)
6.12 d  d_v_fil8                      1    overlay(d_prec:178)
6.12 d  d_v_vkaa                      9  2 overlay(d_prec:179)
6.12 d  d_v_fil9                      1    overlay(d_prec:188)
6.12 d  d_v_efda                      8  0 overlay(d_prec:189)
6.12 d  d_v_fil10                     1    overlay(d_prec:197)
6.12 d  d_v_etda                      8  0 overlay(d_prec:198)
6.12 d  d_v_fil11                     1    overlay(d_prec:206)
6.12 d  d_v_kamp                      5    overlay(d_prec:207)
      *
      * Definering av variable i nøkler
      *
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.21 d vtillu_ogrp     s                   like(vtogrp)
6.21 d vtillu_hgrp     s                   like(vthgrp)
6.21 d vtillu_ugrp     s                   like(vtugrp)
6.21 d vtillu_ldor     s                   like(vtldor)
6.12 d vtillu_vare     s                   like(vtvare)
6.12 d vtillu_prgr     s                   like(vtprgr)
6.12 d vtillu_lety     s                   like(vtlety)
6.12 d vtillu_fdat     s                   like(vtfdat)
6.12 d vtillu_ftim     s                   like(vtftim)
6.12 d vtillu_tdat     s                   like(vttdat)
6.12 d vtillu_ttim     s                   like(vtttim)
5.63 d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d b_inlr          s              1
     d b_ok            s              1
     d b_pris          s              1
     d b_inen          s              1    inz(*off)
6.12 d b_kamp          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_kamp          s              5
     d w_rect          s              1
     d x               s              2  0
     d w_etel          s              2  0
     d w_ptel          s              2  0
6.12 d w_dato_6        s              6  0
     d w_fdat          s                   like(vtfdat)
     d w_tdat          s                   like(vtfdat)
     d w_dato          s                   like(vtfdat)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_enh4          s                   like(veenhe)
     d w_enh5          s                   like(veenhe)
     d w_enhp          s                   like(veenhe)
     d w_enh_000       s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_omr4          s                   like(veomre)
     d w_omr5          s                   like(veomre)
     d w_omre          s                   like(veomre)
     d w_stel          s                   like(vesaen)
     d w_inen          s                   like(veinen)
      *
     d w_gdat          s                   like(vtfdat)
     d w_penh          s                   like(veenhe)
     d b_pdup          s              1
     d w_vare          s                   like(vvvare)
     d w_tip1          s                   like(vttip1)
     d w_kop1          s                   like(vttip1)
     d w_inp1          s                   like(vttip1)
     d w_tip2          s                   like(vttip1)
     d w_kop2          s                   like(vttip1)
     d w_inp2          s                   like(vttip1)
     d w_tip3          s                   like(vttip1)
     d w_kop3          s                   like(vttip1)
     d w_inp3          s                   like(vttip1)
     d w_stat          s              1
     d w_timestamp     s             12  0
     d w_pris          s             11  2
      *
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_frst = *on
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
     c                   eval      w_enh_000 = *blank
     c                   time                    w_dato
      *
      * Leser integrasjonsfil
      *
6.12 c                   eval      b_kamp = *off
      *
     c                   read      flvwpf
     c                   dow       not %eof
      *
     c                   eval      w_rect = %subst(fwirad:1:1)
      *
     c                   if        w_rect = '1' and
     c                             b_frst = *off
     c                   exsr      behandling
     c                   eval      w_enh_000 = *blank
     c                   endif
      *
     c                   select
     c                   when      w_rect = '1'
     c                   eval      d_vrec = fwirad
     c                   eval      b_frst = *off
     c                   when      w_rect = '2'
     c                   eval      w_etel = w_etel + 1
     c                   eval      a_erec(w_etel) = fwirad
     c                   if        %subst(fwirad:4:3) = '000'
     c                   eval      w_enh_000 = %subst(fwirad:10:3)
     c                   endif
     c                   when      w_rect = '3'
     c                   eval      w_ptel = w_ptel + 1
     c                   eval      a_prec(w_ptel) = fwirad
6.12 c                   if        %subst(fwirad:208:5) <> *blank
6.12 c                   eval      b_kamp = *on
6.12 c                   endif
     c                   endsl
      *
     c                   read      flvwpf
     c                   enddo
      *
      * Behandler siste vare-forsendelse
      *
     c                   if        b_frst = *off and
     c                             d_vrec <> *blank
     c                   exsr      behandling
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare-forsendelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
6.12  *
6.12  * kampanje ?
      *
5.63 c                   movel     d_nobb        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
6.12 c                   exsr      tilbud
     c                   endif
      *
     c     end_behandle  tag
      *
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av prisrecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tilbud        begsr
      *
     c                   exsr      finn_enhet
      *
     c                   eval      x = 0
      *
     c                   do        w_ptel
      *
     c                   eval      x = x + 1
     c                   eval      d_prec = a_prec(x)
      *
      * sjekker at prisen inneholder minst salgspris og kostpris
      *
     c                   if        d_spri > 0 and
     c                             d_kpri > 0
      *
      * pr. enhet
      *
      *
     c                   if        d_ppva = *blank
     c                   eval      d_ppva = d_plva
     c                   endif
      *
      * bruker fra dato og tildato fra filen
     c                   if        d_gdat <> 0
     c     *ymd          move      d_gdat        w_fdat
     c                   else
     c                   time                    w_fdat
     c                   endif
      *
     c                   if        d_tdat <> 0
     c     *ymd          move      d_tdat        w_tdat
     c                   else
     c                   time                    w_tdat
     c                   endif
      *
     c                   if        w_enh1 =  w_enh_000
     c                   eval      w_enhp = w_enh1
     c                   eval      w_omre = w_omr1
     c                   exsr      tilb_enhet
     c                   endif
      *
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle hver enhet i prisregisteret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     tilb_enhet    begsr
      *
     c                   eval      w_tip1 = 0
     c                   eval      w_kop1 = 0
     c                   eval      w_inp1 = 0
     c                   eval      w_tip2 = 0
     c                   eval      w_kop2 = 0
     c                   eval      w_inp2 = 0
     c                   eval      w_tip3 = 0
     c                   eval      w_kop3 = 0
     c                   eval      w_inp3 = 0
6.12  *
     c                   eval(h)   w_tip1 = d_spri * w_omr1
     c                   eval      w_kop1 = d_kpri * w_omr1
     c                   eval      w_inp1 = d_ipri * w_omr1
     c                   if        w_enh2 <> w_enh_000
     c                   eval(h)   w_tip2 = d_spri * w_omr2
     c                   eval      w_kop2 = d_kpri * w_omr2
     c                   eval      w_inp2 = d_ipri * w_omr2
     c                   endif
     c                   if        w_enh3 <> w_enh_000
     c                   eval(h)   w_tip3 = d_spri * w_omr3
     c                   eval      w_kop3 = d_kpri * w_omr3
     c                   eval      w_inp3 = d_ipri * w_omr3
     c                   endif
      *
6.12 c                   clear                   vtillur
6.21 c                   eval      vtillu_ogrp = 0
6.21 c                   eval      vtillu_hgrp = 0
6.21 c                   eval      vtillu_ugrp = 0
6.21 c                   eval      vtillu_ldor = 0
6.12 c                   eval      vtillu_vare = vvvare
6.12 c                   eval      vtillu_prgr = *blank
6.12 c                   eval      vtillu_lety = 0
6.12 c                   eval      vtillu_fdat = w_fdat
6.12 c                   eval      vtillu_ftim = *loval
6.12 c                   eval      vtillu_tdat = w_tdat
6.12 c                   eval      vtillu_ttim = *hival
6.12 c     vtillu_key    chain     vtillur
6.12  *
6.12 c                   eval      vtkamp = p_kamp
6.12 c                   eval      vtenh1 = w_enh_000
6.12 c                   eval      vtkop1 = w_kop1
6.12 c                   eval      vttip1 = w_tip1
6.12 c                   eval      vtenh2 = w_enh2
6.12 c                   eval      vtkop2 = w_kop2
6.12 c                   eval      vttip2 = w_tip2
6.12 c                   eval      vtenh3 = w_enh3
6.12 c                   eval      vtkop3 = w_kop3
6.12 c                   eval      vttip3 = w_tip3
6.12 c                   eval      vteusr = l_user
6.12 c                   time                    vtedat
6.12 c                   time                    vtetim
6.12  *
6.12 c                   if        not %found(vtillu)
6.12 c                   eval      vtfirm = w_firm
6.21 c                   eval      vtogrp = vtillu_ogrp
6.21 c                   eval      vthgrp = vtillu_hgrp
6.21 c                   eval      vtugrp = vtillu_ugrp
6.21 c                   eval      vtldor = vtillu_ldor
6.12 c                   eval      vtvare = vtillu_vare
6.12 c                   eval      vtprgr = vtillu_prgr
6.12 c                   eval      vtlety = vtillu_lety
6.12 c                   eval      vtfdat = vtillu_fdat
6.12 c                   eval      vtftim = vtillu_ftim
6.12 c                   eval      vttdat = vtillu_tdat
6.12 c                   eval      vtttim = vtillu_ttim
6.12 c                   eval      vtousr = l_user
6.12 c                   time                    vtodat
6.12 c                   time                    vtotim
6.12 c                   write     vtillur
6.12 c                   else
6.12 c                   update    vtillur
6.12 c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne inntil tre salgsenheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_enhet    begsr
      *
      * Finner tre salgsenheter på varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_enh4 = *blank
     c                   eval      w_enh5 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
     c                   eval      w_omr4 = 0
     c                   eval      w_omr5 = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
5.41 c     vvenl2_key2   reade     vvenl2
5.41 c                   dow       not %eof and
5.41 c                             vesaen > 0
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 = *blank
     c                   eval      w_enh4 = veenhe
     c                   eval      w_omr4 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 <> *blank and
     c                             w_enh5 = *blank
     c                   eval      w_enh5 = veenhe
     c                   eval      w_omr5 = veomre
     c                   leave
     c                   endsl
      *
5.41 c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kamp
6.12  *
6.12  * Key for oppdatering av tilbud
6.12  *
6.12 c     vtillu_key    klist
6.12 c                   kfld                    w_firm
6.21 c                   kfld                    vtillu_ogrp
6.21 c                   kfld                    vtillu_hgrp
6.21 c                   kfld                    vtillu_ugrp
6.21 c                   kfld                    vtillu_ldor
6.12 c                   kfld                    vtillu_vare
6.12 c                   kfld                    vtillu_prgr
6.12 c                   kfld                    vtillu_lety
6.12 c                   kfld                    vtillu_fdat
6.12 c                   kfld                    vtillu_ftim
6.12 c                   kfld                    vtillu_tdat
6.12 c                   kfld                    vtillu_ttim
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
5.41  *
5.41  * Key2 for les på vare-enhet
5.41  *
5.41 c     vvenl2_key2   klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    vvenl2_vare
5.63  *
5.63  * Key for les av vare
5.63  *
5.63 c     vvarl1_key    klist
5.63 c                   kfld                    w_firm
5.63 c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma fra LDA
     c                   eval      w_firm = p_firm
     c                   eval      w_kamp = p_kamp
      *
     c                   endsr
