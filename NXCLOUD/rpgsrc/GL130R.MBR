      /TITLE  ASOKON Vedlikehold av betalingsforslag.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASTELE                                        *
      * Program . . . . : GL130R                                        *
      * Beskrivelse . . : Forespørsel på utbetalinger pr. dag.          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           110796  Nytt program.                            RH   *
T5.00 * V5.00     100103  Ingen poster i GUBFL7 ga programfeil.    HFL
7.xx  * 7.xx      120618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *       38 = Varsler at forslaget ikke kan vedlikeholdes.         *
      *            Det er allerede overført til samlefile/remittering.  *
      *    60-62 = Fileindikatorer chain etc.                           *
      *    63-69 = Varsle v/feilmeldinger.                              *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgubfl7    if   e           k disk
     fgl130d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * w_arow - Actual line, angir startlinje for vindu.
      * w_acol - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      /eject
      *
      * Henter firmanavn.
      *
     d lda            uds
     d l_chgv                844    844
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for bet.dato ÅMD:
      *
     d                 ds
     d dsforf                         8  0
     d  dsforÅ                        4  0 overlay(dsforf)
     d  dsform                        2  0 overlay(dsforf:5)
     d  dsford                        2  0 overlay(dsforf:7)
      *
      * Datastruktur for bet.dato DMÅ:
      *
     d                 ds
     d dsfo2f                         8  0
     d  dsfo2d                        2  0 overlay(dsfo2f)
     d  dsfo2m                        2  0 overlay(dsfo2f:3)
     d  dsfo2Å                        4  0 overlay(dsfo2f:5)
      *
     d sfrn01          s                   like(srrn01)
     d spge01          s              3  0
     d srrn01          s              4  0
     d ssrn01          s                   like(srrn01)
      *
     d w_arow          s              2  0
     d w_acol          s              3  0
     d w_dato          s              8  0
     d w_firm          s              3  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Tøm subfile:
      *
     c                   exsr      xclr01
      *
      * Nullstiller tellere:
      *
     c                   eval      w_dato = 0
     c                   exsr      xnull
      *
      * Gjør klar til les:
      *
     c     gubfl7_key    setll     gubfl7
      *
      * Fyll opp subfile:
      *
     c                   exsr      xcrt01
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
     c     b2tagb        tag
      *
     c                   exsr      xdsp01
      *
      * Avslutt program ved F03.
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Roll
      *
     c                   if        *in10 = *on                                           .....
     c                             and *in60 = *off                                          .
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   goto      b2tagb
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   eval      spge01 = spge01 + 15                         SFL END
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01                              .....
     c                   exsr      xsum                                             .
T5.00c                   if        b1tota <> 0                                  ... .
     c                   eval      srrn01 = srrn01 + 1                              .
T5.00c                   endif                                                  ... .
      *                                                        .
      * Redigerer dato:                                        .
      *                                                        .
     c                   move      w_dato        dsfo2Å                             .
     c                   movel     dsfo2Å        dsfo2m                             .
     c                   move      w_dato        dsfo2d                             .
     c                   movel     w_dato        dsfo2Å                             .
     c                   eval      b1dato = dsfo2f                                  .
      *                                                        .
     c                   if        b1tota <> 0                                  ... .
     c                   write     b1sfl                                          . .
     c                   endif                                                  ... .
      *                                                        .
      * Nullstiller tellere:                                   .
      *                                                        .
     c                   exsr      xnull                                            .
     c     *in60         cabeq     *on           xend01                             .
      *                                                        .
      * Legger Betalingsdato for sist leste post over i        .
      * hjelpefelt:                                            .
      *                                                        .
     c                   eval      w_dato = dsforf                                  .
      *                                                        .
     c                   enddo                                                  .....
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01                                       .....
     c                   eval      sfrn01 = ssrn01 + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      w_virn = sfrn01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0                                           .....
     c                   move      *on           *in12                                       .
     c                   endif                                                           .....
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Nullstiller tellere:                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnull         begsr
      *
      * Nullstiller tellere:
      *
     c                   eval      b1dato = 0
     c                   eval      b1more = 0
     c                   eval      b1mare = 0
     c                   eval      b1ikov = 0
     c                   eval      b1tota = 0
     c                   eval      b1stat = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Summerer beløp til utbetaling:                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsum          begsr
      *
      * Leser neste post:
      *
     c     gubfl7_key    reade     gubfl7                                 60
      *
     c                   dow       *in60 = *off                                 .......
      *                                                          .
      * "Flagger" statusfelt dersom valuta:                      .
      *                                                          .
     c                   if        gavalk <> *blanks                                  .
     c                             and gavalk <> 'NOK'                                .
     c                   move      '*'           b1stat                               .
     c                   endif                                                        .
      *                                                          .
      * Bygger dato fra post til format ÅÅÅÅMMDD:                .
      *                                                          .
S5.00c*                  eval      dsforÅ = gaforÅ                                    .
S5.00c*                  eval      dsform = gaform                                    .
S5.00c*                  eval      dsford = gaford                                    .
T5.00c                   move      gafdat        dsforf                               .
      *                                                          .
      * Tester på om det er brudd på dato.  I såfall skal sum    .
      * pr. sist dato skrives ut i subfile.  Sist leste post     .
      * må derfor først legges "tilbake":                        .
      *                                                          .
     c                   if        w_dato <> 0                                  ...   .
     c                             and w_dato <> dsforf                           .   .
     c     gubfl7_key    readpe    gubfl7                                 60      .   .
     c                   goto      xend02                                         .   .
     c                   endif                                                  ...   .
      *                                                          .
      * Legger Betalingsdato for sist leste post over i          .
      * hjelpefelt:                                              .
      *                                                          .
     c                   eval      w_dato = dsforf                                    .
      *                                                          .
      * Totalt:                                                  .
      *                                                          .
     c                   eval      b1tota = b1tota + gabelØ                           .
      *                                                          .
      * 1.gangsretur er mottatt:                                 .
      *                                                          .
     c                   if        gareko = '1'                                 ...   .
     c                   eval      b1more = b1more + gabelØ                       .   .
     c                   endif                                                  ...   .
      *                                                          .
      * Sendt til bank, men 1.gangsretur er ikke mottatt:        .
      *                                                          .
     c                   if        gareko <> '1'                                ...   .
     c                             and gastat = 'G'                               .   .
     c                   eval      b1mare = b1mare + gabelØ                       .   .
     c                   endif                                                  ...   .
      *                                                          .
      * Ikke oversendt bank:                                     .
      *                                                          .
     c                   if        gareko <> '1'                                ...   .
     c                             and gastat <> 'G'                              .   .
     c                   eval      b1ikov = b1ikov + gabelØ                       .   .
     c                   endif                                                  ...   .
      *                                                          .
      * Leser neste post:                                        .
      *                                                          .
     c     gubfl7_key    reade     gubfl7                                 60          .
     c                   enddo                                                  .......
      *
     c     xend02        tag
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_dato = 0
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      w_arow = 4
     c                   eval      w_acol = 26
      *
     c                   eval      dsforf = 0
     c                   eval      dsfo2f = 0
      *
      *
     c     *dtaara       define    *lda          lda
      *
     c                   eval      w_firm = l_firm
      *
      * Key for file utbet.forslag; GUBFPF
      *
     c     gubfl7_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
      *
