      /TITLE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : AS???
      * Program . . . . :
      * Beskrivelse . . : Bygger FTP fil-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111001 MHA  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faflspf    o  a e             disk    rename(aflspf:aflspfr)
     faftpl1    if a e           k disk    rename(aftppfr:aftpl1r)
      *
      *
     dLinje1           ds
     d line1                   1     80
     d bruker                  1     36
     d passord                40     50
      *
     dLinje2           ds           200
     d line2                   1    200
      *
      * Definering av parametre
     d p_navn          s                   like(afnavn)
     d p_status        s              1
     d p_host          s                   like(afhost)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d aftpl1_navn     s                   like(afnavn)
      *
     d w_status        s              1
     d w_bruker        s             10a
     d w_passord       s             10a
      *
     d w_firm          s                   like(affirm)
      *
      * Definering av konstanter
      *
      /eject
      *
      *
     c     start         tag
      * Sjekke om FTP forespørsel finnes
     c     aftpl1_key    chain     aftpl1r
     c                   if        not %found
     c                   eval      p_status = 'N'
     c                   goto      avslutt
     c                   endif
      *
      * Hvis det er en biblioteksoverføring så skal vi ikke generere liste
     c                   if        aflibt = 'J'
     c                   eval      p_status = 'X'
     c                   goto      avslutt
     c                   endif
      *
      * Kan bare bruke denne metoden ved get....
     c                   if        %trim(aftype) <> 'GET'
     c                   eval      p_status = 'X'
     c                   goto      avslutt
     c                   endif
      *
      *
     c                   if        afqyps = 'J'
     c
     c                   endif
      *
      * Setter opp første linje med bruker og passord
     c                   eval      bruker = afuser
     c                   if        afprxu <> *blank
     c                   eval      bruker = %trim(bruker) + '@' +
     c                             %trim(afhost)
     c                   endif
     c                   if        afqyps = 'J'
     c                   eval      w_bruker = afuser
     c                   eval      w_passord = *blank
     c                   eval      w_status = *blank
     c                   call      'AF709R'
     c                   parm                    w_bruker
     c                   parm                    w_passord
     c                   parm                    w_status
     c                   if        w_status = *blank
     c                   eval      passord = w_passord
     c                   eval      bruker = w_bruker
     c                   else
     c                   eval      p_status = 'A'
     c                   goto      avslutt
     c                   endif
     c                   else
     c                   eval      passord = afpass
     c                   endif
     c                   eval      SrcDta = Linje1
     c                   write     aflspfr
      *
      * Setter riktig filformat
     c                   if        affils = 'A'
     c                   eval      line2 = 'namefmt 0'
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   else
     c                   eval      line2 = 'namefmt 1'
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   endif
      *
      * Setter riktig aktiv/passiv
     c                   if        afspas = 'A'
     c                   eval      line2 = 'sendpasv 0'
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   else
     c                   eval      line2 = 'sendpasv 1'
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   endif
      *
      * Vi skriver inn kommandoen
      * Setter bane
     c                   eval      line2 = 'cd ' + %trim(afchdr)
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   if        afxcm1 <> *blank
     c                   eval      line2 = %trim(afxcm1)
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   endif
     c                   if        afxcm2 <> *blank
     c                   eval      line2 = %trim(afxcm2)
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   endif
     c                   if        afxcm3 <> *blank
     c                   eval      line2 = %trim(afxcm3)
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
     c                   endif
     c                   eval      line2 = 'ls ' + %trim(affiln) +
     c                             ' (disk'
     c                   eval      SrcDta = Linje2
     c                   write     aflspfr
      *
      * Vi legger inn slutt kommandoen
     c                   eval      line2 = 'quit'
     c                   eval      SrcDta = linje2
     c                   write     aflspfr
      *
      * Vi gir tilbake parametre ang. host etc...
     c                   if        afprxu <> *blank
     c                   eval      p_host = afprxu
     c                   else
     c                   eval      p_host = afhost
     c                   endif
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     aftpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftpl1_navn
      *
     c     *entry        plist
     c                   parm                    p_navn
     c                   parm                    p_status
     c                   parm                    p_host
      *
     c                   eval      aftpl1_navn = p_navn
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
