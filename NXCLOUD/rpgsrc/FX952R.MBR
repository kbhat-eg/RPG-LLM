      /title  ASOFAK FIX: Sletting av varer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX952R
      * Beskrivelse . . : FIX: Sletter aller varer som fyller disse krit:
      *                   - har utg.dato som er passert
      *                   - ikke har ordre annet enn tilbud  (samme test
      *                   - ikke har beholdning               som slett
      *                                                       vare)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050811 bof  Nytt program
6.nu  *       160614 bof  Gjennomgått mtp. nummerutvidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lodtlv_vare     s                   like(ldvare)
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
      *
     d vlagl1_vare     s                   like(vlvare)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
     d b_ordr          s              1    inz(*off)
     d b_lagr          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_udat          s                   like(vvudat)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c                   time                    w_udat
      *
      * Leser vareregister, og kaller sletteprogram
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1                                 90
     c                   dow       *in90 = *off
      *
      * sjekker om utg.dato og om den er passert
      *
     c                   if        vvudat <> *loval and
     c                             vvudat <  w_udat
      *
     c                   call      'VV490R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    b_inlr
     c                   parm                    b_ordr
     c                   parm                    b_lagr
      *
     c                   exsr      sjekk_lager
      *
     c                   if        b_ordr = *off  and
     c                             b_lagr = *off
      *
     c                   call      'VV400R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    b_inlr
      *
     c                   endif
      *
     c                   endif
      *
     c     vvarl1_key    reade     vvarl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke aktive poster på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_lagr = *off
      *
     c                   eval      vlagl1_vare = vvvare
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1
     c                   dow       not %eof
      *
     c                   if        vlbehl <> 0
     c                   eval      b_lagr = *on
     c                   leavesr
      *
     c                   endif
      *
     c     vlagl1_key    reade     vlagl1
     c                   enddo
      *
      * Test på at vare ikke finnes i bestilling
      * Tilbud identifiseres som ordretype med behandlingskode lik 0
      *
     c                   eval      lodtlv_vare = vvvare
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
     c                   dow       not %eof
     c                   eval      lohel1_numm = ldnumm
     c                   eval      lohel1_suff = ldsuff
     c     lohel1_key    chain     lohel1
     c                   if        %found(lohel1)
     c                   eval      b_lagr = *on
     c                   endif
     c     lodtlv_key    reade     lodtlv
     c                   enddo
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på best.-linje
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      * Key for les på best.hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
5.60  *
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
