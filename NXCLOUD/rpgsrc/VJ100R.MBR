     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VJ100R
      * Beskrivelse . . : Påslag-linje, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091100 HKO  Nytt program
      *       270803 HKO  Utvidet påslagstabell med salgspris
      *       040504 HKO  Åpnet for negative påslag og påslagbeløp
      *       100504 HKO  Lagt inn dekningsgrad
      *                   Fjernet test på at varegruppe kun kan fylles ut
      *                   sammen med leverandør
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
7.xx  * 7.00  240216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-99 = Arbeidsindikatorer ordinære
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvpdtlr    if   e           k disk    rename(vpdtpfr:vpdtlrr)
     fvpdtlu    uf a e           k disk    rename(vpdtpfr:vpdtlur)
     fvpdtl1    if   e           k disk    rename(vpdtpfr:vpdtl1r)
     fvpdtl2    if   e           k disk    rename(vpdtpfr:vpdtl2r)
     fvpdtl3    if   e           k disk    rename(vpdtpfr:vpdtl3r)
     fvpdtl4    if   e           k disk    rename(vpdtpfr:vpdtl4r)
     fvpdtl5    if   e           k disk    rename(vpdtpfr:vpdtl5r)
     fvpdtl6    if   e           k disk    rename(vpdtpfr:vpdtl6r)
     fvpdtl7    if   e           k disk    rename(vpdtpfr:vpdtl7r)
     fvpdtl8    if   e           k disk    rename(vpdtpfr:vpdtl8r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
      *
     fvj100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vjfirm)
     d p_pasl          s                   like(vjpasl)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d vpdtlr_ldor     s                   like(vjldor)
     d vpdtlr_ogrp     s                   like(vjogrp)
     d vpdtlr_hgrp     s                   like(vjhgrp)
     d vpdtlr_ugrp     s                   like(vjugrp)
     d vpdtlr_kpri     s                   like(vjkpri)
     d vpdtlr_vare     s                   like(vjvare)
     d vpdtlr_lety     s                   like(vjlety)
     d vpdtlu_ldor     s                   like(vjldor)
     d vpdtlu_ogrp     s                   like(vjogrp)
     d vpdtlu_hgrp     s                   like(vjhgrp)
     d vpdtlu_ugrp     s                   like(vjugrp)
     d vpdtlu_kpri     s                   like(vjkpri)
     d vpdtlu_vare     s                   like(vjvare)
     d vpdtlu_lety     s                   like(vjlety)
     d vpdtl1_ldor     s                   like(vjldor)
     d vpdtl1_ogrp     s                   like(vjogrp)
     d vpdtl1_hgrp     s                   like(vjhgrp)
     d vpdtl1_ugrp     s                   like(vjugrp)
     d vpdtl1_kpri     s                   like(vjkpri)
     d vpdtl1_vare     s                   like(vjvare)
     d vpdtl1_lety     s                   like(vjlety)
     d vpdtl2_ogrp     s                   like(vjogrp)
     d vpdtl3_hgrp     s                   like(vjhgrp)
     d vpdtl4_ugrp     s                   like(vjugrp)
     d vpdtl5_kpri     s                   like(vjkpri)
     d vpdtl6_vare     s                   like(vjvare)
     d vpdtl7_lety     s                   like(vjlety)
     d vpdtl8_vare     s                   like(vjvare)
     d vpdtl8_lety     s                   like(vjlety)
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
     d vltpl1_lety     s                   like(vylety)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
      *
     d w_firm          s                   like(vjfirm)
     d w_pasl          s                   like(vjpasl)
     d w_ogrp          s                   like(vjogrp)
     d w_hgrp          s                   like(vjhgrp)
     d w_ugrp          s                   like(vjugrp)
     d w_kpri          s                   like(vjkpri)
     d w_ldor          s                   like(vjldor)
     d w_vare          s                   like(vjvare)
     d w_lety          s                   like(vjlety)
      *
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
     d w_tek1          s                   like(vvtek1)
     d w_lbes          s                   like(vybesk)
      *
     d s_ldor          s                   like(vjldor)
     d s_ogrp          s                   like(vjogrp)
     d s_hgrp          s                   like(vjhgrp)
     d s_ugrp          s                   like(vjugrp)
     d s_kpri          s                   like(vjkpri)
     d s_vare          s                   like(vjvare)
     d s_lety          s                   like(vjlety)
      *
     d s_saim          s                   like(vjsapr)
     d s_sapr          s                   like(vjsapr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for å opprette ny rad
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0 or
     c                             b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2kpri <> *blank or
     c                             b2vare <> *blank or
     c                             b2lety <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      vpdtl2_ogrp = b1ogrp
     c     vpdtl2_key    setll     vpdtl2
     c                   when      w_seqe = 'H'
     c                   eval      vpdtl3_hgrp = b1hgrp
     c     vpdtl3_key    setll     vpdtl3
     c                   when      w_seqe = 'U'
     c                   eval      vpdtl4_ugrp = b1ugrp
     c     vpdtl4_key    setll     vpdtl4
     c                   when      w_seqe = 'P'
     c                   eval      vpdtl5_kpri = b1kpri
     c     vpdtl5_key    setll     vpdtl5
     c                   when      w_seqe = 'V'
     c                   eval      vpdtl6_vare = b1vare
     c     vpdtl6_key    setll     vpdtl6
     c                   when      w_seqe = 'T'
     c                   eval      vpdtl7_lety = b1lety
     c     vpdtl7_key    setll     vpdtl7
     c                   other
     c                   eval      vpdtl1_ldor = b1ldor
     c                   eval      vpdtl1_ogrp = b1ogrp
     c                   eval      vpdtl1_hgrp = b1hgrp
     c                   eval      vpdtl1_ugrp = b1ugrp
     c                   eval      vpdtl1_kpri = b1kpri
     c                   eval      vpdtl1_vare = b1vare
     c                   eval      vpdtl1_lety = b1lety
     c     vpdtl1_key    setll     vpdtl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vpdtl1_ldor = b2ldor
     c                   eval      vpdtl1_ogrp = b2ogrp
     c                   eval      vpdtl1_hgrp = b2hgrp
     c                   eval      vpdtl1_ugrp = b2ugrp
     c                   eval      vpdtl1_kpri = b2kpri
     c                   eval      vpdtl1_vare = b2vare
     c                   eval      vpdtl1_lety = b2lety
     c     vpdtl1_key    setll     vpdtl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      vpdtl2_ogrp = b2ogrp
     c     vpdtl2_key    setll     vpdtl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      vpdtl3_hgrp = b2hgrp
     c     vpdtl3_key    setll     vpdtl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vpdtl4_ugrp = b2ugrp
     c     vpdtl4_key    setll     vpdtl4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på priskode
      *
     c                   if        b2kpri <> *blank
     c                   eval      w_seqe = 'P'
     c                   eval      vpdtl5_kpri = b2kpri
     c     vpdtl5_key    setll     vpdtl5
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      vpdtl6_vare = b2vare
     c     vpdtl6_key    setll     vpdtl6
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'T'
     c                   eval      vpdtl7_lety = b2lety
     c     vpdtl7_key    setll     vpdtl7
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2kpri = *blank
     c                   eval      b2vare = *blank
     c                   eval      b2lety = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b1ldor
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1kpri = b1kpri
     c                   eval      c1vare = b1vare
     c                   eval      c1lety = b1lety
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ldor = b1ldor
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1kpri = b1kpri
     c                   eval      k1vare = b1vare
     c                   eval      k1lety = b1lety
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ldor = b1ldor
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1kpri = b1kpri
     c                   eval      d1vare = b1vare
     c                   eval      d1lety = b1lety
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b1ldor
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1kpri = b1kpri
     c                   eval      c1vare = b1vare
     c                   eval      c1lety = b1lety
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ldor = b2ldor
     c                   eval      c1ogrp = b2ogrp
     c                   eval      c1hgrp = b2hgrp
     c                   eval      c1ugrp = b2ugrp
     c                   eval      c1kpri = b2kpri
     c                   eval      c1vare = b2vare
     c                   eval      c1lety = b2lety
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Sjekker input
      *
     c                   eval      w_ldor = c1ldor
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_kpri = c1kpri
     c                   eval      w_vare = c1vare
     c                   eval      w_lety = c1lety
     c                   exsr      sjekk_input
     c                   eval      c1ldor = w_ldor
     c                   eval      c1ogrp = w_ogrp
     c                   eval      c1hgrp = w_hgrp
     c                   eval      c1ugrp = w_ugrp
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
     c                   if        c1vare = *blank
     c                   eval      vpdtlr_ldor = c1ldor
     c                   eval      vpdtlr_ogrp = c1ogrp
     c                   eval      vpdtlr_hgrp = c1hgrp
     c                   eval      vpdtlr_ugrp = c1ugrp
     c                   eval      vpdtlr_kpri = c1kpri
     c                   eval      vpdtlr_vare = c1vare
     c                   eval      vpdtlr_lety = c1lety
     c     vpdtlr_key    chain     vpdtlr                             90
     c                   else
     c                   eval      vpdtl8_vare = c1vare
     c                   eval      vpdtl8_lety = c1lety
     c     vpdtl8_key    chain     vpdtl8                             90
     c                   if        *in90 = *off
     c                   eval      c1ldor = vjldor
     c                   eval      c1ogrp = vjogrp
     c                   eval      c1hgrp = vjhgrp
     c                   eval      c1ugrp = vjugrp
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ldor = 0
     c                   eval      c1lnav = *blank
     c                   eval      c1ogrp = 0
     c                   eval      c1otxt = *blank
     c                   eval      c1hgrp = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1ugrp = 0
     c                   eval      c1utxt = *blank
     c                   eval      c1kpri = *blank
     c                   eval      c1vare = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1lety = 0
     c                   eval      c1lbes = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ldor = b2ldor
     c                   eval      b2ogrp = b2ogrp
     c                   eval      b2hgrp = b2hgrp
     c                   eval      b2ugrp = b2ugrp
     c                   eval      b2kpri = b2kpri
     c                   eval      b2vare = b2vare
     c                   eval      b2lety = b2lety
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vpdtlr_ldor = c1ldor
     c                   eval      vpdtlr_ogrp = c1ogrp
     c                   eval      vpdtlr_hgrp = c1hgrp
     c                   eval      vpdtlr_ugrp = c1ugrp
     c                   eval      vpdtlr_kpri = c1kpri
     c                   eval      vpdtlr_vare = c1vare
     c                   eval      vpdtlr_lety = c1lety
     c     vpdtlr_key    chain     vpdtlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2inpa = vjinpa
     c                   eval      c2inbe = vjinbe
     c                   eval      c2kopa = vjkopa
     c                   eval      c2kobe = vjkobe
     c                   eval      c2sapa = vjsapa
     c                   eval      c2sabe = vjsabe
     c                   eval      c2saim = vjsapr * w_mvat
     c                   eval      c2sapr = vjsapr
     c                   eval      c2dekn = vjdekn
     c                   eval      c2eusr = vjeusr
     c     *dmy          move      vjodat        c2odat
     c     *dmy          move      vjedat        c2edat
     c                   else
     c                   eval      c2inpa = 0
     c                   eval      c2inbe = 0
     c                   eval      c2kopa = 0
     c                   eval      c2kobe = 0
     c                   eval      c2sapa = 0
     c                   eval      c2sabe = 0
     c                   eval      c2saim = 0
     c                   eval      c2sapr = 0
     c                   eval      c2dekn = 0
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ldor = k1ldor
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1kpri = k1kpri
     c                   eval      c1vare = k1vare
     c                   eval      c1lety = k1lety
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
     c                   eval      s_saim = c2saim
     c                   eval      s_sapr = c2sapr
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2inpa = 0 and                               Betingelse 1
     c                             c2inbe = 0 and                               Betingelse 1
     c                             c2kopa = 0 and                               Betingelse 1
     c                             c2kobe = 0 and                               Betingelse 1
     c                             c2sapa = 0 and                               Betingelse 1
     c                             c2sabe = 0 and                               Betingelse 1
     c                             c2saim = 0 and                               Betingelse 1
     c                             c2sapr = 0 and                               Betingelse 1
     c                             c2dekn = 0                                   Betingelse 1
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2sapa <> 0 or                               Betingelse 1
     c                             c2sabe <> 0 or                               Betingelse 1
     c                             c2saim <> 0 or                               Betingelse 1
     c                             c2sapr <> 0                                  Betingelse 1
     c                   if        c2dekn <> 0                                  Betingelse 1
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2saim <> s_saim and
     c                             c2sapr <> s_sapr
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2saim <> 0 or
     c                             c2sapr <> 0
     c                   if        c2sapa <> 0 or
     c                             c2sabe <> 0
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2saim <> 0 and
     c                             c2saim <> s_saim
     c                   eval(h)   c2sapr = c2saim * w_mvaf
     c                   endif
      *
     c                   eval      s_saim = c2saim
     c                   eval      s_sapr = c2sapr
      *
      * Oppdatering
      *
     c                   clear                   vpdtlur
      *
     c                   eval      vpdtlu_ldor = c1ldor
     c                   eval      vpdtlu_ogrp = c1ogrp
     c                   eval      vpdtlu_hgrp = c1hgrp
     c                   eval      vpdtlu_ugrp = c1ugrp
     c                   eval      vpdtlu_kpri = c1kpri
     c                   eval      vpdtlu_vare = c1vare
     c                   eval      vpdtlu_lety = c1lety
     c     vpdtlu_key    chain     vpdtlur                            90
      *
     c                   if        c1vare <> *blank
     c                   eval      vvarl1_vare = c1vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c1ldor = vvldor
     c                   eval      c1ogrp = vvogrp
     c                   eval      c1hgrp = vvhgrp
     c                   eval      c1ugrp = vvugrp
     c                   endif
     c                   endif
      *
     c                   eval      vjldor = c1ldor
     c                   eval      vjogrp = c1ogrp
     c                   eval      vjhgrp = c1hgrp
     c                   eval      vjugrp = c1ugrp
     c                   eval      vjinpa = c2inpa
     c                   eval      vjinbe = c2inbe
     c                   eval      vjkopa = c2kopa
     c                   eval      vjkobe = c2kobe
     c                   eval      vjsapa = c2sapa
     c                   eval      vjsabe = c2sabe
     c                   eval      vjsapr = c2sapr
     c                   eval      vjdekn = c2dekn
     c                   eval      vjeusr = l_user
      *
     c                   time                    vjedat
     c                   time                    vjetim
      *
     c                   if        *in90 = *off
     c                   update    vpdtlur
     c                   else
     c                   eval      vjfirm = w_firm
     c                   eval      vjpasl = w_pasl
     c                   eval      vjkpri = c1kpri
     c                   eval      vjvare = c1vare
     c                   eval      vjlety = c1lety
     c                   time                    vjodat
6.10 c                   time                    vjotim
6.10 c                   eval      vjousr = l_user
     c                   write     vpdtlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1kopa = c2kopa
     c                   eval      b1kobe = c2kobe
     c                   eval      b1sapa = c2sapa
     c                   eval      b1sabe = c2sabe
     c                   eval      b1sapr = c2sapr
     c***                eval      b1lnav = c1lnav
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vpdtlr_ldor = d1ldor
     c                   eval      vpdtlr_ogrp = d1ogrp
     c                   eval      vpdtlr_hgrp = d1hgrp
     c                   eval      vpdtlr_ugrp = d1ugrp
     c                   eval      vpdtlr_kpri = d1kpri
     c                   eval      vpdtlr_vare = d1vare
     c                   eval      vpdtlr_lety = d1lety
     c     vpdtlr_key    chain     vpdtlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vpdtlu_ldor = d1ldor
     c                   eval      vpdtlu_ogrp = d1ogrp
     c                   eval      vpdtlu_hgrp = d1hgrp
     c                   eval      vpdtlu_ugrp = d1ugrp
     c                   eval      vpdtlu_kpri = d1kpri
     c                   eval      vpdtlu_vare = d1vare
     c                   eval      vpdtlu_lety = d1lety
     c     vpdtlu_key    chain     vpdtlur                            90
     c                   delete    vpdtlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ldor = k1ldor
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_kpri = k1kpri
     c                   eval      s_vare = k1vare
     c                   eval      s_lety = k1lety
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vpdtlr_ldor = k1ldor
     c                   eval      vpdtlr_ogrp = k1ogrp
     c                   eval      vpdtlr_hgrp = k1hgrp
     c                   eval      vpdtlr_ugrp = k1ugrp
     c                   eval      vpdtlr_kpri = k1kpri
     c                   eval      vpdtlr_vare = k1vare
     c                   eval      vpdtlr_lety = k1lety
     c     vpdtlr_key    chain     vpdtlr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ldor = k1ldor
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_kpri = k1kpri
     c                   eval      w_vare = k1vare
     c                   eval      w_lety = k1lety
     c                   exsr      sjekk_input
     c                   eval      k1ldor = w_ldor
     c                   eval      k1ogrp = w_ogrp
     c                   eval      k1hgrp = w_hgrp
     c                   eval      k1ugrp = w_ugrp
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   if        k1vare = *blank
     c                   eval      vpdtlr_ldor = k1ldor
     c                   eval      vpdtlr_ogrp = k1ogrp
     c                   eval      vpdtlr_hgrp = k1hgrp
     c                   eval      vpdtlr_ugrp = k1ugrp
     c                   eval      vpdtlr_kpri = k1kpri
     c                   eval      vpdtlr_vare = k1vare
     c                   eval      vpdtlr_lety = k1lety
     c     vpdtlr_key    chain     vpdtlr                             90
     c                   else
     c                   eval      vpdtl8_vare = k1vare
     c                   eval      vpdtl8_lety = k1lety
     c     vpdtl8_key    chain     vpdtl8                             90
     c                   endif
      *
     c                   if        *in90 = *off
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = s_ldor
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1kpri = s_kpri
     c                   eval      c1vare = s_vare
     c                   eval      c1lety = s_lety
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   read      vpdtl2
     c                   when      w_seqe = 'H'
     c                   read      vpdtl3
     c                   when      w_seqe = 'U'
     c                   read      vpdtl4
     c                   when      w_seqe = 'P'
     c                   read      vpdtl5
     c                   when      w_seqe = 'V'
     c                   read      vpdtl6
     c                   when      w_seqe = 'T'
     c                   read      vpdtl7
     c                   other
     c                   read      vpdtl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vjfirm <> p_firm or
     c                             vjpasl <> p_pasl
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      vpdtl2_ogrp = vjogrp
     c                   when      w_seqe = 'H'
     c                   eval      vpdtl3_hgrp = vjhgrp
     c                   when      w_seqe = 'U'
     c                   eval      vpdtl4_ugrp = vjugrp
     c                   when      w_seqe = 'P'
     c                   eval      vpdtl5_kpri = vjkpri
     c                   when      w_seqe = 'V'
     c                   eval      vpdtl6_vare = vjvare
     c                   when      w_seqe = 'T'
     c                   eval      vpdtl7_lety = vjlety
     c                   other
     c                   eval      vpdtl1_ldor = vjldor
     c                   eval      vpdtl1_ogrp = vjogrp
     c                   eval      vpdtl1_hgrp = vjhgrp
     c                   eval      vpdtl1_ugrp = vjugrp
     c                   eval      vpdtl1_kpri = vjkpri
     c                   eval      vpdtl1_vare = vjvare
     c                   eval      vpdtl1_lety = vjlety
     c                   endsl
      *
     c                   eval      b1ldor = vjldor
     c                   eval      b1ogrp = vjogrp
     c                   eval      b1hgrp = vjhgrp
     c                   eval      b1ugrp = vjugrp
     c                   eval      b1kpri = vjkpri
     c***                eval      b1lnav = *blank
     c                   eval      b1vare = vjvare
     c                   eval      b1lety = vjlety
     c                   eval      b1kopa = vjkopa
     c                   eval      b1kobe = vjkobe
     c                   eval      b1sapa = vjsapa
     c                   eval      b1sabe = vjsabe
     c                   eval      b1sapr = vjsapr
      *
      * Henter leverandørnavn
      *
     c***                if        b1ldor <> 0
     c***                eval      rlevl1_levr = b1ldor
     c***  rlevl1_key    chain     rlevl1                             90
     c***                if        *in90 = *off
     c***                eval      b1lnav = rlnavn
     c***                endif
     c***                endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'O'
     c     vpdtl2_key    setgt     vpdtl2
     c                   readp     vpdtl2
     c                   when      w_seqe = 'H'
     c     vpdtl3_key    setgt     vpdtl3
     c                   readp     vpdtl3
     c                   when      w_seqe = 'U'
     c     vpdtl4_key    setgt     vpdtl4
     c                   readp     vpdtl4
     c                   when      w_seqe = 'P'
     c     vpdtl5_key    setgt     vpdtl5
     c                   readp     vpdtl5
     c                   when      w_seqe = 'V'
     c     vpdtl6_key    setgt     vpdtl6
     c                   readp     vpdtl6
     c                   when      w_seqe = 'T'
     c     vpdtl7_key    setgt     vpdtl7
     c                   readp     vpdtl7
     c                   other
     c     vpdtl1_key    setgt     vpdtl1
     c                   readp     vpdtl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   readp     vpdtl2
     c                   when      w_seqe = 'H'
     c                   readp     vpdtl3
     c                   when      w_seqe = 'U'
     c                   readp     vpdtl4
     c                   when      w_seqe = 'P'
     c                   readp     vpdtl5
     c                   when      w_seqe = 'V'
     c                   readp     vpdtl6
     c                   when      w_seqe = 'T'
     c                   readp     vpdtl7
     c                   other
     c                   readp     vpdtl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vjfirm <> p_firm or
     c                             vjpasl <> p_pasl
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      vpdtl2_ogrp = vjogrp
     c                   when      w_seqe = 'H'
     c                   eval      vpdtl3_hgrp = vjhgrp
     c                   when      w_seqe = 'U'
     c                   eval      vpdtl4_ugrp = vjugrp
     c                   when      w_seqe = 'P'
     c                   eval      vpdtl5_kpri = vjkpri
     c                   when      w_seqe = 'V'
     c                   eval      vpdtl6_vare = vjvare
     c                   when      w_seqe = 'T'
     c                   eval      vpdtl7_lety = vjlety
     c                   other
     c                   eval      vpdtl1_ldor = vjldor
     c                   eval      vpdtl1_ogrp = vjogrp
     c                   eval      vpdtl1_hgrp = vjhgrp
     c                   eval      vpdtl1_ugrp = vjugrp
     c                   eval      vpdtl1_kpri = vjkpri
     c                   eval      vpdtl1_vare = vjvare
     c                   eval      vpdtl1_lety = vjlety
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'O'
     c     vpdtl2_key    setll     vpdtl2
     c                   when      w_seqe = 'H'
     c     vpdtl3_key    setll     vpdtl3
     c                   when      w_seqe = 'U'
     c     vpdtl4_key    setll     vpdtl4
     c                   when      w_seqe = 'P'
     c     vpdtl5_key    setll     vpdtl5
     c                   when      w_seqe = 'V'
     c     vpdtl6_key    setll     vpdtl6
     c                   when      w_seqe = 'T'
     c     vpdtl7_key    setll     vpdtl7
     c                   other
     c     vpdtl1_key    setll     vpdtl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1otxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   eval      k1hgrp = 0
     c                   eval      k1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   eval      c1ugrp = 0
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   eval      k1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   eval      c1utxt = *blank
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1tek1
     c                   eval      c1vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      k1vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_lbes
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    c1lbes
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1lety
     c                   parm                    w_lbes
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_lnav = *blank
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_lbes = *blank
      *
      * Sjekk nivå
      *
     c***                if        w_ldor = 0
     c***                if        w_ogrp <> 0 or
     c***                          w_hgrp <> 0 or
     c***                          w_ugrp <> 0
     c***                eval      *in31  = *on
     c***                eval      b_feil = *on
     c***                goto      end_sjekk
     c***                endif
     c***                endif
      *
     c                   if        w_kpri <> *blank and
     c                             w_vare <> *blank
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Vare
      *
     c                   if        w_vare <> *blank
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1                             45
     c                   if        *in45 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_ldor = vvldor
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_tek1 = vvtek1
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_ldor <> 0
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_lnav = rlnavn
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0 and
     c                             w_ugrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
      * Priskode
      *
     c                   if        w_kpri <> *blank and
     c                             w_kpri <> 'B' and
     c                             w_kpri <> 'N'
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_lety <> 0
     c                   eval      vltpl1_lety = w_lety
     c     vltpl1_key    chain     vltpl1                             41
     c                   if        *in41 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_lbes = vybesk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1lnav = *blank
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1lbes = *blank
     c                   eval      c2enhe = *blank
      *
      * Leverandør
      *
     c                   if        c1ldor <> 0
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c1lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Overgruppe
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1otxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1htxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Varenummer
      *
     c                   if        c1vare <> *blank
     c                   eval      vvarl1_vare = c1vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c1tek1 = vvtek1
     c                   eval      c2enhe = vvenhe
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_lety <> 0
     c                   eval      vltpl1_lety = w_lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lbes = vybesk
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_pasl
      *
      * Key for oppslag på påslag-linje
      *
     c     vpdtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtlr_ldor
     c                   kfld                    vpdtlr_ogrp
     c                   kfld                    vpdtlr_hgrp
     c                   kfld                    vpdtlr_ugrp
     c                   kfld                    vpdtlr_kpri
     c                   kfld                    vpdtlr_vare
     c                   kfld                    vpdtlr_lety
      *
      * Key for oppdatering av påslag-linje
      *
     c     vpdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtlu_ldor
     c                   kfld                    vpdtlu_ogrp
     c                   kfld                    vpdtlu_hgrp
     c                   kfld                    vpdtlu_ugrp
     c                   kfld                    vpdtlu_kpri
     c                   kfld                    vpdtlu_vare
     c                   kfld                    vpdtlu_lety
      *
      * Key for posisjonering på overgruppe
      *
     c     vpdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl1_ldor
     c                   kfld                    vpdtl1_ogrp
     c                   kfld                    vpdtl1_hgrp
     c                   kfld                    vpdtl1_ugrp
     c                   kfld                    vpdtl1_kpri
     c                   kfld                    vpdtl1_vare
     c                   kfld                    vpdtl1_lety
      *
      * Key for posisjonering på overgruppe
      *
     c     vpdtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl2_ogrp
      *
      * Key for posisjonering på hovedgruppe
      *
     c     vpdtl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl3_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     vpdtl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl4_ugrp
      *
      * Key for posisjonering på priskode
      *
     c     vpdtl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl5_kpri
      *
      * Key for posisjonering på varenummer
      *
     c     vpdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl6_vare
      *
      * Key for posisjonering på leveringstype
      *
     c     vpdtl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl7_lety
      *
      * Key for oppslag på påslag-linje m/varenummer
      *
     c     vpdtl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_pasl
     c                   kfld                    vpdtl8_vare
     c                   kfld                    vpdtl8_lety
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      *
      * Henter firma og påsalg fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_pasl = p_pasl
      *
      * Henter mva-sats
      *
     c                   time                    w_dato
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2pasl = p_pasl
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vpdtl1_key    setll     vpdtl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
