     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP147R
      * Beskrivelse . . : Prosjekt, Hente budsjett til Ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       181019 sst  Nytt program
7.00  * 7.00  051219 sst  Justering vedr priser og endring av ordretype OP/HP
7.01  * 7.00  070120 sst  Div justeringer ifølge mail fra MBE pr 03.01.2020
7.02  * 7.00  220120 sst  Hente pris også ved overføring av hele lasset
7.03  * 7.00  170320 sst  Rettet feil vedr dup Key i fodtlu
7.04  * 7.00  240320 sst  Alltid ordretype 'HP' på ordre som henter vare m/F17
      *____________________________________________________________________________________________
Versj *Uts   Dato Usr Referanse Beskrivelse
7.05  *K00 240420 SST NXS-3898  Justering av varelinjer m/info fra vvar m.m.
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frpval1    if   e           k disk    rename(rpvapfr:rpval1r)
     frpval2    if   e           k disk    rename(rpvapfr:rpval2r)
     frpval3    if   e           k disk    rename(rpvapfr:rpval3r)
     frpvalr    if   e           k disk    rename(rpvapfr:rpvalrr)
     frpval4    if   e           k disk    rename(rpvapfr:rpval4r)
     frpval5    if   e           k disk    rename(rpvapfr:rpval5r)
     frpvalu    uf a e           k disk    rename(rpvapfr:rpvalur)
     frpkol1    if   e           k disk    rename(rpkopfr:rpkol1r)
     frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
     frp2ul1    if   e           k disk    rename(rp2upfr:rp2ul1r)
     frp3al1    if   e           k disk    rename(rp3apfr:rp3al1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
7.01 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
7.05 fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
7.00 ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
      *
     frp147d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * Definering av Local data area
      *
     d                udS
     d l_user                911    920
     d l_fgr                 931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rpval1_vare     s                   like(rpvvar)
     d rpval1_lass     s                   like(rpvlas)
     d rpval2_vtxt     s                   like(rpvtxt)
     d rpval2_vlas     s                   like(rpvlas)
     d rpvalr_vare     s                   like(rpvvar)
     d rpvalr_lass     s                   like(rpvlas)
     d rpval4_vare     s                   like(rpvvar)
     d rpval4_lass     s                   like(rpvlas)
     d rpval5_vare     s                   like(rpvvar)
     d rpval5_lass     s                   like(rpvlas)
     d rpvalu_vare     s                   like(rpvvar)
     d rpvalu_lass     s                   like(rpvlas)
     d rpvalu3_firm    s                   like(rpvfir)
     d rpvalu3_pros    s                   like(rpvpro)
     d rpvalu3_upro    s                   like(rpvupr)
     d rpvalu3_akti    s                   like(rpvakt)
     d rpvalu3_vare    s                   like(rpvvar)
     d rpvalu3_lass    s                   like(rpvlas)
     d rpkol1_firm     s                   like(rpkfir)
     d rpkol1_type     s                   like(rpktyp)
     d rpkol1_kode     s                   like(rpkkod)
     d rp1pl1_pros     s                   like(rp1pro)
     d rp2ul1_pros     s                   like(rp2pro)
     d rp2ul1_upro     s                   like(rp2upr)
     d rp3al1_pros     s                   like(rp3pro)
     d rp3al1_upro     s                   like(rp3upr)
     d rp3al1_akti     s                   like(rp3akt)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
     d vvarl1_vare     s                   like(vvvare)
7.01 d vlagl1_vare     s                   like(vlvare)
7.01 d vlagl1_lage     s                   like(vllage)
7.05 d vvepl1_vare     s                   like(vvvare)
7.05 d vvepl1_enhe     s                   like(vvenhe)
7.05 d vvepl1_ldor     s                   like(vvldor)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_firm          s              3  0
     d s_vare          s                   like(rpvvar)
      *
     d w_dato          s             10
     d w_datd          s                   like(rpvlda)
     d w_1alf          s                   like(rpvtxt)
     d w_datx          s             10
     d w_datn          s              6  0
     d p_tx30          s             30
     d p_rapx          s              4
     d p_fdat          s              6  0
     d p_tdat          s              6  0
     d p_knav          s             30
     d p_selg          s              4  0
     d p_kunf          s              6  0
     d p_pros          s              6
     d p_upro          s              6
     d p_akti          s              6
     d p_kont          s              5  0
     d p_vare          s              8
     d p_text          s             30
     d p_kpro          s              6  0
     d w_fper          s              6  0
     d p_rapt          s             30
     d p_numx          s              6
     d p_sufx          s              3
     d p_numm          s                    like(fdnumm)
     d p_suff          s                    like(fdsuff)
7.00 d p_firm          s                    like(w_firm)
8.01 d p_prgr          s              2
7.00 d p_enhe          s                   like(rpvenh)
7.00 d p_lety          s                   like(fdlety)
7.00 d p_dato          s                   like(fdldat)
7.00 d p_time          s                   like(fdtime)
7.00 d p_sapr          s                   like(fdsapr)
7.00 d p_bupr          s                   like(fdsapr)
7.00 d p_tipr          s                   like(fdsapr)
7.00 d p_kopr          s                   like(fdkopr)
7.00 d p_inlr          s              1
     d w3avde          s              4
     d w3prol          s              5
     d w_sumi          s              8  0
     d w_sumix         s             11  0
     d w_sumk          s              8  0
     d w_sumkx         s             11  0
     d w_buo           s              8  0
     d w_but           s              8  0
     d w_rest          s              8  0
     d w_pros          s              8  0
     d w_mld1          s             50
     d w_stat          s              1
     d w_spor          s              1    inz('5')
     d w_endr          s              1    inz('2')
     d w_a51           s              1  0 inz(0)
     d w_tilgko        s             10
     d w_rutine        s             10
     d w_adv           s              3
     d w_status        s             10
     d w_upro          s                   like(rpvupr)
     d                                      inz(' ')
     d w_m1            s             50
     d w_m2            s             50
     d w_sv            s              1
     d w_pkod          s              4
     d w_ptxt          s             30
     d w_kost          s              9  0
     d w_hellas        s              1
      *
     d w_pluant        s             11  3
     d w_levant        s             11  3
     d w_fakant        s             11  3
     d w_besant        s             11  3
     d w_pluantx       s              9  0
     d w_levantx       s              9  0
     d w_fakantx       s              9  0
     d w_besantx       s              9  0
     d w_sql           s           1000
     d w_map           s             20     inz('Prosjekt/')
     d w_tildok        s             25
     d w_typ           s              4     inz('xlsx')
     d w_ms1           s             50
     d w_ms2           s             50
     d w_svar          s              1
     d w_in12          s              1  0
     d w_nextlass      s                   like(rpvlas)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkj
     c                   exsr      ovf_lass
     c                   exsr      forny
     c                   goto      b2tagb
      *
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Utvalg
      *
      *
      * Posisjonering
      *
     c                   if        b2slas <> *zero  or
     c                             b2svar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Retur til første bilde
      *
     c                   goto      b2tagb
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'A'
     c     rpval4_key    setll     rpval4
     c                   other
     c     rpval5_key    setll     rpval5
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på prosjekt
      *
     c                   if        b2slas <> *zero
     c                   eval      w_seqe = 'A'
     c                   eval      rpval4_lass = b2slas
     c                   eval      rpval4_vare = *blank
     c     rpval4_key    setll     rpval4
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2svar <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      rpval5_vare = b2svar
     c                   eval      rpval5_lass = *zero
     c     rpval5_key    setll     rpval5
     c                   endif
      *
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2slas = *zero
     c                   eval      b2svar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
     c                   eval      w_hellas = *blank
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Velge lass
      *
     c                   if        b1valg = '1'
     c                   exsr      Ovf_Ordre
     c                   eval      b1valg = *blank
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *    debug
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      rpval4                                 15
     c                   other
     c                   read      rpval5                                 15
     c                   endsl
      *
     c                   if        *in15
     c                             or rpvfir <> w_firm
     c                             or rpvpro <> p_pros
     c                   leave
     c                   endif
      * Underprosjekt
     c                   if        p_upro <>  *blank
     c                             and rpvupr <> p_upro
     c                   iter
     c                   endif
      * Aktivitet
     c                   if        p_akti <>  *blank
     c                             and rpvakt <> p_akti
     c                   iter
     c                   endif
      * Konto
     c                   if        p_kont <>  *zero
     c                             and rpvkto <> p_kont
     c                   iter
     c                   endif
      * Plukke ferdig
     c                   if        rpvsts = 'P'
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   eval      b1vare = rpvvar
     c                   eval      b1vtxt = rpvtxt
     c                   eval      b1lass = rpvlas
     c**                 eval      b1lassx = %char(rpvlas)
     c                   eval      b1enhe  = rpvenh
     c**                 eval      b1budant = %char(rpvabu)
     c                   eval      b1buds   = rpvabj - rpvapl
     c                   eval      b1anta   = *zero
     c                   if        rpvkpr <> 0
     c                   eval      b1kpri   = rpvkpr
     c                   else
     c                   eval      b1kpri   = *zero
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overføre valgte varelinjer til ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     Ovf_Ordre     begsr
      *
7.05  *    Ordre heading er hentet inn i *INZSR
      *
      *   Bygge ordrelinje
      *
     c                   eval      fodtlu_numm = p_numm
     c                   eval      fodtlu_suff = p_suff
     c                   eval      fodtlu_line = fodtlu_line + 10
     c     fodtlu_key    chain     fodtlu
     c                   dow       %found(fodtlu)
     c                   eval      fodtlu_line = fodtlu_line + 10
     c     fodtlu_key    chain     fodtlu
     c                   enddo
      *  Hent rpva
     c                   eval      rpval1_vare = b1vare
     c                   eval      rpval1_lass = b1lass
     c     rpval1_key    chain     rpval1
      *
     c                   clear                   fodtlur
     c                   eval      fdnumm = p_numm
     c                   eval      fdsuff = p_suff
     c                   eval      fdline = fodtlu_line
7.04 c                   eval      fdotyp = 'HP'
     c                   eval      fdltyp = *blank
     c                   eval      fdlety = folety
     c                   eval      fdvare = b1vare
     c                   eval      fdenh1 = b1enhe
     c                   eval      fdlage = folage
     c                   eval      fdlass = b1lass
     c                   eval      fdkund = fokund
     c                   eval      fdldat = rpvlda
     c                   eval      fdavde = foavde
     c                   eval      fdproj = p_pros
     c                   eval      fdkont = rpvkto
     c                   eval      fdtek1 = rpvtxt
     c                   if        b1anta <> 0
     c                   eval      fdanta = b1anta
     c                   else
     c                   eval      fdanta = rpvabj
     c                   endif
7.02  *
7.02 c                   exsr      HentPris
7.02  *
7.05 c                   exsr      GetVarLagVep
7.05  *
7.05  *
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
7.00 c     fohel1_key    chain     fohelu
7.04 c**                 if        footyp = 'OP'
7.00 c                   eval      footyp = 'HP'
7.00 c                   update    fohelur
7.04 c**                 endif
      *  oppdatere rpva
     c                   eval      rpvalu_vare = b1vare
     c                   eval      rpvalu_lass = b1lass
     c     rpvalu_key    chain     rpvalu
     c                   if        %found(rpvalu)
     c                   if        b1anta <> 0
     c                   eval      rpvapl = b1anta
     c                   eval      rpvsts = 'P'
     c                   else
     c                   eval      rpvapl = rpvabj
     c                   eval      rpvsts = 'P'
     c                   endif
     c                   update    rpvalur
     c                   if        rpvabj - rpvapl > 0
     c                   eval      rpvlas = w_nextlass
     c                   eval      rpvsts = ' '
     c                   write     rpvalur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overføre valgt lass        til ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     Ovf_Lass      begsr
      *   Bekreft at dette lass skal overføres
     c                   eval      w_ms1 = 'Du har bedt om overføring av hele +
     c                                      lass nr. ' + %char(b2slas)
     c                   eval      w_ms2 = 'Kan du bekrefte at dette er riktig'
     c                   eval      w_svar = 'J'
      *
     c                   call      'AA007R'
     c                   parm                    w_ms1
     c                   parm                    w_ms2
     c                   parm                    w_svar
     c                   parm                    w_in12
     c                   if        w_svar <> 'J'
     c                             or  w_in12 = 1
     c                   goto      end_lass
     c                   endif
      *
      *   Bygge ordrelinje
      *
     c                   eval      fodtlu_numm = p_numm
     c                   eval      fodtlu_suff = p_suff
     c                   eval      fodtlu_line = fodtlu_line + 10
     c     fodtlu_key    chain     fodtlu
     c                   dow       %found(fodtlu)
     c                   eval      fodtlu_line = fodtlu_line + 10
     c     fodtlu_key    chain     fodtlu
     c                   enddo
      *  Hent rpva
     c     rpval4l_key   setll     rpval4
     c     rpval4l_key   reade     rpval4
     c                   dow       not %eof(rpval4)
      *    debug
     c                   if        rpvsts <> 'P'
     c                             and (b2slas = 0
     c                                or rpvlas = b2slas)
     c                   clear                   fodtlur
     c                   eval      fdnumm = p_numm
     c                   eval      fdsuff = p_suff
     c                   eval      fdline = fodtlu_line
7.04 c                   eval      fdotyp = 'HP'
     c                   eval      fdltyp = *blank
7.01 c                   eval      fdlety = folety
     c                   eval      fdvare = rpvvar
     c                   eval      fdenh1 = rpvenh
7.01 c                   eval      fdlage = folage
     c                   eval      fdlass = rpvlas
     c                   eval      fdkund = fokund
7.01 c**                 eval      fdldat = rpldat
7.01 c**                 eval      fdldat = foldat
7.01 c                   eval      fdldat = *loval
     c                   eval      fdavde = foavde
     c                   eval      fdproj = p_pros
     c                   eval      fdkont = rpvkto
     c                   eval      fdtek1 = rpvtxt
     c                   eval      fdanta = rpvabj
7.02  *
7.02 c                   exsr      HentPris
7.02  *
7.05 c                   exsr      GetVarLagVep
7.05  *
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
7.03 c                   eval      fodtlu_line = fodtlu_line + 10
      *  oppdatere rpva
     c                   eval      rpvalu_vare = rpvvar
     c                   eval      rpvalu_lass = rpvlas
     c     rpvalu_key    chain     rpvalu
     c                   if        %found(rpvalu)
     c                   eval      rpvapl = rpvabj
     c                   eval      rpvsts = 'P'
     c                   update    rpvalur
     c                   endif
      *
     c                   endif
      *
     c     rpval4l_key   reade     rpval4
     c                   enddo
7.04  *
7.04  *   Oppdatere ordren til HP
7.04 c                   eval      fohel1_numm = p_numm
7.04 c                   eval      fohel1_suff = p_suff
7.04 c     fohel1_key    chain     fohelu
7.04 c                   eval      footyp = 'HP'
7.04 c                   update    fohelur
      *
     c     end_lass      endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for henting av prise           til ordre
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     hentpris      begsr
7.02 c                   eval      p_firm = w_firm
7.02 c                   eval      p_vare = fdvare
7.02 c                   eval      p_prgr = *blank
7.02 c                   eval      p_enhe = fdenh1
7.02 c                   eval      p_lety = fdlety
7.02 c                   time                    p_dato
7.02 c                   time                    p_time
7.02 c                   call      'VP700R'
7.02 c                   parm                    p_firm
7.02 c                   parm                    p_vare
7.02 c                   parm                    p_prgr
7.02 c                   parm                    p_enhe
7.02 c                   parm                    p_lety
7.02 c                   parm                    p_dato
7.02 c                   parm                    p_time
7.02 c                   parm                    p_inlr
7.02 c                   parm                    p_sapr
7.02 c                   parm                    p_bupr
7.02 c                   parm                    p_tipr
7.02 c                   parm                    p_kopr
7.02 c                   eval      fdsapr = p_sapr
7.02 c                   eval      fdkopr = p_kopr
7.02 c                   if        fdkopr = 0
7.02 c                   eval      fdkopr = rpvkpr
7.02 c                   endif
7.02 c                   if        fdsapr = 0
7.02 c                   eval      fdsapr = fdkopr
7.02 c                   endif
7.02  *
7.02 c                   endsr
7.05  *
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Subrutine for henting fra Vvar, Vlag og  Vvep til ordrelinje
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     GetVarLagVep  begsr
     c                   eval      vvarl1_vare = rpvvar
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
7.05 c                   eval      fdlvar = vvlvar
     c                   else
     c                   eval      fdogrp = *zero
     c                   eval      fdhgrp = *zero
     c                   eval      fdugrp = *zero
7.05 c                   eval      fdlvar = *blank
     c                   endif
      *   Hent leverandør fra lager
7.05 c                   eval      vlagl1_vare = fdvare
7.05 c                   eval      vlagl1_lage = folage
7.05 c     vlagl1_key    chain     vlagl1
7.05 c                   if        not %found(vvarl1) or
7.05 c                              (  %found(vlagl1) and
7.05 c                                 vlvtyp = 'S')
7.05 c*                  eval      fdlvre = 1   Skal nå ikke gjøres oifølge MBE se NXS-3898
7.05 c                   endif
7.05 c                   if        %found(vlagl1)
7.05 c                             and vlbldo <> 0
7.05 c                   eval      fdldor = vlbldo
7.05 c                   else
7.05 c                   if        %found(vvarl1)
7.05 c                   eval      fdldor = vvldor
7.05 c                   else
7.05 c                   eval      fdldor = *zero
7.05 c                   endif
7.05 c                   endif
7.05 c                   if        fdldor > 0
7.05 c                   eval      vvepl1_vare = fdvare
7.05 c                   eval      vvepl1_enhe = fdenh1
7.05 c                   eval      vvepl1_ldor = fdldor
7.05 c     vvepl1_key    chain     vvepl1
7.05 c                   if        %found(vvepl1)
7.05 c                   eval      fdeann = vepgti
7.05 c                   else
7.05 c                   eval      fdeann = *zero
7.05 c                   endif
7.05 c                   endif
7.05  *
7.05 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_pros
     c                   parm                    p_upro
     c                   parm                    p_akti
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for posisjonering på prosjekt
      *
     c     rpval1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c                   kfld                    p_upro
     c                   kfld                    p_akti
     c                   kfld                    rpval1_vare
     c                   kfld                    rpval1_lass
      *
      * Key for posisjonering på tekst
      *
     c     rpval2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c                   kfld                    p_upro
     c                   kfld                    p_akti
     c                   kfld                    rpval2_vtxt
     c                   kfld                    rpval2_vlas
      *
      * Key for posisjonering på avdeling
      *
     c     rpval3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c                   kfld                    p_upro
     c                   kfld                    p_akti
     c*                  kfld                    rpval3_vvar
      *
      * Key for les på prosjekt
      *
     c     rpvalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c                   kfld                    p_upro
     c                   kfld                    p_akti
     c                   kfld                    rpvalr_vare
     c                   kfld                    rpvalr_lass
      *
     c     rpvalr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
      *
      * Key for oppdatering av prosjket
      *
     c     rpvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c                   kfld                    p_upro
     c                   kfld                    p_akti
     c                   kfld                    rpvalu_vare
     c                   kfld                    rpvalu_lass
      *
     c     rpvalu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
      *
     c     rpvalu3_key   klist
     c                   kfld                    rpvalu3_firm
     c                   kfld                    rpvalu3_pros
     c                   kfld                    rpvalu3_upro
     c                   kfld                    rpvalu3_akti
     c                   kfld                    rpvalu3_vare
     c                   kfld                    rpvalu3_lass
      *
     c     rpval4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c                   kfld                    rpval4_lass
     c                   kfld                    rpval4_vare
      *
     c     rpval4l_key   klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c**                 kfld                    rpval4_lass
      *
     c     rpval5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_pros
     c                   kfld                    rpval5_vare
     c                   kfld                    rpval5_lass
      *
      * Key for les av prosjekt
      *
     c     rp1pl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl1_pros
      *
      * Key for les av underprosjekt
      *
     c     rp2ul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp2ul1_pros
     c                   kfld                    rp2ul1_upro
      *
      * Key for les av prosjekt
      *
     c     rp3al1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp3al1_pros
     c                   kfld                    rp3al1_upro
     c                   kfld                    rp3al1_akti
      *
      * Key for les av Ordre
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for les av Ordre-linjer
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      * Key for les av Varer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
7.01  *
7.01  * Key for les av vare på lager
7.01  *
7.01 c     vlagl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    vlagl1_vare
7.01 c                   kfld                    vlagl1_lage
7.05  *
7.05  * Key for les av vare på vvep
7.05  *
7.05 c     vvepl1_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    vvepl1_vare
7.05 c                   kfld                    vvepl1_enhe
7.05 c                   kfld                    vvepl1_ldor
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *  Finn neste ledige lassnr
     c                   eval      rpval4_lass = 99
     c     rpval4l_key   setgt     rpval4
     c                   readp     rpval4
     c                   if        rpvpro <> p_pros
     c                             or %eof(rpval4)
     c                   eval      w_nextlass = 1
     c                   else
     c                   eval      w_nextlass = rpvlas + 1
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe      = *blank
     c                   eval      rpval5_vare = *blank
     c                   eval      rpval5_lass = *zero
     c     rpval5_key    setll     rpval5
      *
     c                   eval      b2pros = p_pros
     c                   eval      rp1pl1_pros = p_pros
     c     rp1pl1_key    chain     rp1pl1
     c                   if        %found(rp1pl1)
     c                   eval      b2ptxt = rp1txt
     c                   endif
     c                   if        p_upro <> *blank
     c                   eval      b2pros = %trim(b2pros) + '\' + p_upro
     c                   eval      rp2ul1_pros = p_pros
     c                   eval      rp2ul1_upro = p_upro
     c     rp2ul1_key    chain     rp2ul1
     c                   if        %found(rp2ul1)
     c                   eval      b2ptxt = %trim(b2ptxt) + '\' + rp2txt
     c                   endif
     c                   endif
     c                   if        p_akti <> *blank
     c                   eval      b2pros = %trim(b2pros) + '\' + p_akti
     c                   eval      rp3al1_pros = p_pros
     c                   eval      rp3al1_upro = p_upro
     c                   eval      rp3al1_akti = p_akti
     c     rp3al1_key    chain     rp3al1
     c                   if        %found(rp3al1)
     c                   eval      b2ptxt = %trim(b2ptxt) + '\' + rp3txt
     c                   endif
     c                   endif
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
