      /TITLE  ASOFAK Konvertering, bytter varegruppe på ordre/faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX751R
      * Beskrivelse . . : Konvertering, bytter varegruppe på ordre/faktura
      *
      *                   Leser gjennom ordre-linje og faktura-linje, og
      *                   bytter varegruppe i.h.t. vareregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090699 HKO  Nytt program
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtpf    uf a e           k disk    rename(fodtpfr:fodtpfr)
     fsodtlu    uf a e           k disk    rename(sodtpfr:sodtlur)
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
6.10  * Definering av local data area
6.10  *
6.10 d lda            uds
6.10 d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser hele ordre-linje-registeret for oppdatering
      *
     c                   read      fodtpfr                                90
     c                   dow       *in90 = *off
      *
     c                   if        fdvare <> *blank
     c                   eval      vvarl1_firm = fdfirm
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c     vvarl1_key    chain     vvasl1                             91
     c                   endif
     c                   if        *in91 = *off
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
     c                   endif
     c                   endif
      *
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtpfr
      *
     c                   read      fodtpfr                                90
     c                   enddo
      *
      * Leser hele faktura-linje-registeret for oppdatering
      *
     c                   read      sodtlur                                90
     c                   dow       *in90 = *off
      *
     c                   if        sdvare <> *blank
     c                   eval      vvarl1_firm = sdfirm
     c                   eval      vvarl1_vare = sdvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c     vvarl1_key    chain     vvasl1                             91
     c                   endif
     c                   if        *in91 = *off
     c                   eval      sdogrp = vvogrp
     c                   eval      sdhgrp = vvhgrp
     c                   eval      sdugrp = vvugrp
     c                   endif
     c                   endif
      *
6.10 c                   time                    sdedat
6.10 c                   time                    sdetim
6.10 c                   eval      sdeusr = l_user
     c                   update    sodtlur
      *
     c                   read      sodtlur                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
     c                   endsr
