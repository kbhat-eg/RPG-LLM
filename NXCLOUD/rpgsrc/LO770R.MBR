     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO770R
      *  Beskrivelse . . : Leverandørbestilling i FARVERINGEN - format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
6.11  * 6.10  130709 bof  Utvidet eannr til 14 og brukt std. oppslag
6.nu  * 6.30  120614 bof  Gjennomgått mtp. nummerutvidelse
6.31  * 6.30  101018 bof  Utvidet med trelast sortiment
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flwfrpf    o    e           k disk
      *
      *  Definering av datastrukturer for FARVERINGEN
      *
      *  Heading-record
     d                 ds
     d d_hrec                        80
     d  d_knum                        5    overlay(d_hrec)
     d  d_kref                       14    overlay(d_hrec:6)
      *
      * Detail-record
     d                 ds
     d d_drec                        80
     d  d_vnum                       13    overlay(d_drec)
     d  d_anta                        6  0 overlay(d_drec:14)
      *
      * Slutt-record
     d                 ds
     d d_srec                        80
     d  d_fslu                        3    overlay(d_srec)     inz('END')
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d vvarl1_vare     s                   like(vvvare)
      *
      *  Definering av variable i parameter
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_line          s             80
     d w_ant_alf       s              6
     d w_numm_alf      s              6
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
6.31 d w_lv_nobb       s                   like(vvnobb)
      *
     d b_feil          s              1    inz(*off)
      *
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lohel1_key    chain     lohel1r                            90
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr                             92
     c                   if        *in92
     c                   eval      lldilr_lage = *blank
     c     lldilr_key    chain     lldilr                             92
     c                   if        *in92
     c                   goto      end_pgm
     c                   endif
     c                   endif
      *
     c                   eval      d_knum = %subst(lplknr:1:5)
     c                   move      p_numm        w_numm_alf
     c                   eval      d_kref = '"' + w_numm_alf + '"'
     c                   eval      w_line = %trim(d_knum) + ',' +
     c                                      %trim(d_kref)
      *
     c                   eval      frirec = w_line
     c                   write     lwfrpfr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av detalj-linjer, utelat tekstlinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r                                91
      *
     c                   dow       *in91 = *off
      *
     c                   if        ldltyp = laatty
     c                   goto      neste_linje
     c                   endif
      *
     c                   exsr      finn_vnr
     c                   if        b_feil = *on
     c                   goto      neste_linje
     c                   endif
      *
     c                   eval      d_anta = %inth(ldanta)
     c                   move      d_anta        w_ant_alf
     c                   eval      w_line = %trim(d_vnum) + ',' + w_ant_alf
     c                   eval      frirec = w_line
     c                   write     lwfrpfr
      *
     c     neste_linje   tag
     c     lodtl1_key    reade     lodtl1r                                91
     c                   enddo
      *
     c                   eval      frirec = d_srec
     c                   write     lwfrpfr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne varenummer (NOBB el EAN el LEV.VNR)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vnr      begsr
      *
     c                   eval      d_vnum = *blank
     c                   eval      b_feil = *on
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1                             94
     c                   if        *in94 = *on
     c                   goto      end_vnr
     c                   endif
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_nobb = 0
6.31 c/exec sql
6.31 c+ select vvlnob
6.31 c+ into   :w_lv_nobb
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :ldvare and
6.31 c+        vvlldo = :ldldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_nobb <> 0
6.31 c                   movel     w_lv_nobb     d_vnum
6.31 c                   eval      b_feil = *off
6.31 c                   goto      end_vnr
6.31 c                   endif
      *
     c                   if        vvnobb <> 0
     c                   movel     vvnobb        d_vnum
     c                   eval      b_feil = *off
     c                   goto      end_vnr
     c                   endif
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    ldvare
6.11 c                   parm                    ldenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
     c                   if        w_east = '1' and
     c                             w_eann <> 0
     c                   movel     w_eann        d_vnum
     c                   eval      b_feil = *off
     c                   goto      end_vnr
     c                   endif
      *
     c                   if        vvlvar <> *blank
     c                   movel     vvlvar        d_vnum
     c                   eval      b_feil = *off
     c                   goto      end_vnr
     c                   endif
      *
     c     end_vnr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file les FIRMA-OPPLYSNINGER
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      *  Key for file les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
      *
      *  Hent opplysninger fra FIRMA og LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c     afirl1_key    chain     afirl1r                            90
      *
     c                   endsr
