     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . :
      * Program . . . . : VG810R
      * Beskrivelse . . : Oppdaterer faktor på gj.kostpriser                gnet på
      *                   semikolonseparert fil.
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020219 nho  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fgjkipf    if   e             disk
     fvgkfiu    uf a e           k disk    rename(vgkfst:vgkfiur)
      *
     fvgkti1    if   e           k disk    rename(vgktst:vgkti1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Input post
     d                 ds
     d d_inpu                       256
     d  d_firmnr                      3  0 overlay(d_inpu:1)
     d   d_firm                       3    overlay(d_firmnr:1)
     d  d_text                       20    overlay(d_inpu:4)
     d  d_levnnr                      6  0 overlay(d_inpu:24)
     d   d_levn                       6    overlay(d_levnnr:1)
     d  d_overgr                      2  0 overlay(d_inpu:30)
     d   d_over                       2    overlay(d_overgr:1)
     d  d_hovegr                      2  0 overlay(d_inpu:32)
     d   d_hove                       2    overlay(d_hovegr:1)
     d  d_undegr                      3  0 overlay(d_inpu:34)
     d   d_unde                       3    overlay(d_undegr:1)
     d  d_varenr                     15    overlay(d_inpu:37)
     d   d_vare                       8    overlay(d_varenr:1)
     d  d_faktor                      8  6 overlay(d_inpu:52)
     d   d_fakt                       8    overlay(d_faktor:1)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_lagr          s              2  0
      *
      * Definering av variable i nøkler
      *
     d vgkfiu_firm     s                   like(vgfirm)
     d vgkfiu_lage     s                   like(vgflag)
     d vgkfiu_flev     s                   like(vgflev)
     d vgkfiu_ftyp     s                   like(vgftyp)
     d vgkfiu_fogr     s                   like(vgfogr)
     d vgkfiu_fhgr     s                   like(vgfhgr)
     d vgkfiu_fugr     s                   like(vgfugr)
     d vgkfiu_fvar     s                   like(vgfvar)
     d vgkti1_ftyp     s                   like(vtftyp)
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av øvrige variable
      *
     d exclin          s           1024
     d x               s              3  0
     d w_varnr         s              8  0
     d w_firm          s                   like(vgfirm)
     d w_posi          s              4  0
     d b_ok            s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandler ikke første post - overskrift regneark
      *
     c                   read      gjkipf                                 15
      *
      *  Leser inn input file, og splitter record fra regneark.
      *
     c                   dow       not %eof
     c                   read      gjkipf
     c                   movel     rec1          exclin
      *
     c                   eval      b_ok = *off
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
      * Sjekk om post finnes / Legg verdier inn i nøkler
     c                   if        b_ok = *on
     c                   eval      vgkfiu_firm = w_firm
     c                   eval      vgkfiu_lage = p_lagr
     c                   movel     d_text        vgkfiu_ftyp
     c                   eval      vgkfiu_flev = d_levnnr
     c                   eval      vgkfiu_fogr = d_overgr
     c                   eval      vgkfiu_fhgr = d_hovegr
     c                   eval      vgkfiu_fugr = d_undegr
     c                   eval      vgkfiu_fvar = d_varenr
      *
     c     vgkfiu_key    chain     vgkfiu
      *
     c                   if        %found
     c                   eval      vgffak = d_faktor
     c                   time                    vgfdat
     c                   eval      vgfusr = l_user
     c                   update    vgkfiur
     c                   else
     c                   eval      vgffak = d_faktor
     c                   eval      vgfirm = w_firm
     c                   eval      vgftyp = vgkfiu_ftyp
     c                   eval      vgflev = vgkfiu_flev
     c                   eval      vgfogr = vgkfiu_fogr
     c                   eval      vgfhgr = vgkfiu_fhgr
     c                   eval      vgfugr = vgkfiu_fugr
     c                   eval      vgfvar = vgkfiu_fvar
     c                   eval      vgflag = vgkfiu_lage
     c                   time                    vgfdat
     c                   eval      vgfusr = l_user
     c                   write     vgkfiur
     c                   endif
      *
     c                   endif
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av innlest post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     exclin        exclin
      * Æ,Ø,Å
     c                   eval      exclin = %xlate(x'77':'Ø':exclin)
     c                   eval      exclin = %xlate(x'2A':'Å':exclin)
     c                   eval      exclin = %xlate(x'46':'Æ':exclin)
     c                   eval      exclin = %xlate(x'EF':'å':exclin)
     c                   eval      exclin = %xlate(x'90':'ø':exclin)
     c                   eval      exclin = %xlate(x'A0':'æ':exclin)
      *
      * Firma
     c                   eval      w_posi = 0
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_firm = %trim(%subst(exclin:1:w_posi-1))
     c                   movel     w_firm        d_firm
     c                   eval      d_firmnr = w_firm
     c                   if        d_firm = *blanks
     c                   eval      d_firmnr = *zero
     c                   else
     c                   eval      d_firmnr = %dec(d_firm:3:0)
     c                   endif
      *
      * Text, faktortype
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_text = %trim(%subst(exclin:1:w_posi-1))
      *
      * Leverandørnr
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_levn = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_levn = *blanks
     c                   eval      d_levnnr = *zero
     c                   else
     c                   eval      d_levnnr = %dec(d_levn:6:0)
     c                   endif
      *
      * Overgr
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_over = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_over = *blanks
     c                   eval      d_overgr = *zero
     c                   else
     c                   eval      d_overgr = %dec(d_over:2:0)
     c                   endif
      *
      * Hovedgr
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_hove = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_hove = *blanks
     c                   eval      d_hovegr = *zero
     c                   else
     c                   eval      d_hovegr = %dec(d_hove:2:0)
     c                   endif
      *
      * Undergr
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_unde = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_unde = *blanks
     c                   eval      d_undegr = *zero
     c                   else
     c                   eval      d_undegr = %dec(d_unde:3:0)
     c                   endif
      *
      * Varenr
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_vare = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_vare = *blank
     c                   movel     *blanks       d_varenr
     c                   eval      w_varnr = 0
     c                   else
     c                   eval      w_varnr = %dec(d_vare:8:0)
     c                   movel     w_varnr       d_varenr
     c                   endif
      *
      * Faktor
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(' ':exclin)
     c                   eval      d_fakt = %trim(%subst(exclin:1:w_posi-1))
     c                   eval      d_faktor = %dec(d_fakt:8:6)
      *
      * Sjekk Faktortype
      *
     c                   if        d_text <> *blanks
     c                   eval      vgkti1_ftyp = d_text
     c     vgkti1_key    chain     vgkti1r
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      *
      * Sjekk Leverandør
      *
     c                   if        d_levnnr <> 0
     c                   eval      rlevl1_levr = d_levnnr
      *
     c     rlevl1_key    chain     rlevl1r
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk overgruppe
      *
     c                   if        d_overgr <> 0 and
     c                             d_hovegr = 0
     c                   eval      vogrl1_oogr = d_overgr
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk hovedgruppe
      *
     c                   if        d_hovegr <> 0 and
     c                             d_undegr = 0
     c                   eval      vhgrl1_hogr = d_overgr
     c                   eval      vhgrl1_hhgr = d_hovegr
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk undergruppe
      *
     c                   if        d_undegr <> 0
     c                   eval      vugrl1_uogr = d_overgr
     c                   eval      vugrl1_uhgr = d_hovegr
     c                   eval      vugrl1_uugr = d_undegr
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk varenummer
      *
     c                   if        w_varnr <> *zero
     c                   movel     w_varnr       vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk faktor
      * må være utfylt
     c                   if        d_faktor = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_lagr
      *
      * Key for posisjonering på faktortype / update file
      *
     c     vgkfiu_key    klist
     c                   kfld                    vgkfiu_firm
     c                   kfld                    vgkfiu_lage
     c                   kfld                    vgkfiu_flev
     c                   kfld                    vgkfiu_ftyp
     c                   kfld                    vgkfiu_fogr
     c                   kfld                    vgkfiu_fhgr
     c                   kfld                    vgkfiu_fugr
     c                   kfld                    vgkfiu_fvar
      *
      * Key for posisjonering på faktyratype
      *
     c     vgkti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkti1_ftyp
      *
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Setter startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      x = 0
      *
     c                   endsr
      *
