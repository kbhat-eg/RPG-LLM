      /TITLE  Spesial program - lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System  . . . . : ASLAGR                                       *
      *  Program . . . . : LX892R                                       *
      *  Beskrivelse . . : Oppdatering av telle-liste utfra telle-bunt  *
      *                    Oppdaterer hele batchen som angis.           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                               *
      *  Tilpasset for . :                                               *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      * --------- ------  ----------------------------------------------*
      *   Hardkodet:  Batch-nr                                          *
      *               Dette må legges inn og programmet rekompileres    *
5.70  *  PRGR 151208 bof  Utvidet med prisgruppe
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
6.21  * 6.20  190511 bof  Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     flbhel1    if   e           k disk    rename(lbhepfr:lbhel1r)
     flbdtl1    if   e           k disk    rename(lbdtpfr:lbdtl1r)
     fltellu    uf   e           k disk    rename(ltelpfr:ltellur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      *  UDS Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      *  Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d lbdtl1_batc     s                   like(lvbatc)
     d lbhel1_batc     s                   like(lubatc)
     d lbhel1_numm     s                   like(lunumm)
     d ltellu_batc     s                   like(lebatc)
     d ltellu_numm     s                   like(lenumm)
     d ltellu_suff     s                   like(lesuff)
     d ltellu_line     s                   like(leline)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
      *
      *  Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Lese bunt-telle-linjer og oppdatere hovedtelle-liste           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Lese file
      *
     c     lbdtl1_key    setll     lbdtl1
     c     lbdtl1_key    reade     lbdtl1                                 15
      *
     c                   dow       *in15 = *off
      *
      *  Ikke oppdatering dersom antall er null
      *
     c                   if        lvantt <> 0
     c                   exsr      opptel
     c                   endif
      *
     c     lbdtl1_key    reade     lbdtl1                                 15
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdatering av LAGER-TELLE-REGISTER                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opptel        begsr
      *
      *  Hent innformasjon fra LAGER-TELLE-VARE-REGISTER
      *
     c                   eval      *in90 = *off
     c                   eval      ltellu_line = lvtlin
     c     ltellu_key    setll     ltellu
     c     ltellu_key    reade     ltellu                                 90
      *
     c                   dow       *in90 = *off
     c                   if        levare = lvvare
      *
      *  Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = lvvare
     c     vvarl1_key    chain     vvarl1r                            96
      *
      *  Lagerenhet i vare-register skal benyttes.
      *  Telle-listen må alltid ha alle varer i samme enhet
      *
     c                   exsr      omr_antall
      *
      *  Legger inn antallsfelter i TELLE-REGISTER
      *
     c                   eval      leantt = leantt + lvantt
     c                   eval      leantb = leantb + lvantt
      *
      *  Oppdaterer register
      *
     c                   if        *in90 = *off
6.10 c                   time                    leedat
6.10 c                   time                    leetim
6.10 c                   eval      leeusr = l_user
     c                   update    ltellur
     c                   endif
     c                   endif
      *
     c     ltellu_key    reade     ltellu                                 90
     c                   enddo
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall fra opptalt til lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Dersom opptalt enhet er lik lagerenhet eller diversevare
      * beholdes antall som registrert
      *
     c                   if        lvenh1 = vvenhl
     c                   goto      end_omr
     c                   endif
      *
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    lvlage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
     *
5.70 c                   if        w_vtyp = 'D'
     c                   goto      end_omr
     c                   endif
      *
      * Finner omregningsfaktor volum for lagerenhet og for opptalt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = lvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 91
     c                   dow       *in91 = *off
     c                   if        lvenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 91
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl <> 0
     c                   eval      lvantt = lvantt * w_omrl / w_omrs
     c                   else
     c                   eval      lvantt = 0
     c                   endif
     c                   eval      leenh1 = vvenhl
      *
     c     end_omr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      *  Key for BATCH-REGISTER
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Key for LAGERTELLEBUNT-HODE-REGISTER
      *
     c     lbhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhel1_batc
     c                   kfld                    lbhel1_numm
      *
      *  Key for LAGERTELLELINJE-REGISTER
      *
     c     lbdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl1_batc
      *
      *  Key for LAGER-TELLE-REGISTER
      *
     c     ltellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltellu_batc
     c                   kfld                    ltellu_numm
     c                   kfld                    ltellu_suff
     c                   kfld                    ltellu_line
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      lbchl1_batc = 'ASK01     '
     c                   eval      lbhel1_batc = 'ASK01     '
     c                   eval      lbdtl1_batc = 'ASK01     '
     c                   eval      ltellu_batc = 'ASK01     '
      *
      *  Hent opplysninger fra BATCH-REGISTER
      *
     c     lbchl1_key    chain     lbchl1r                            90
     c                   eval      ltellu_numm = lcnumm
     c                   eval      ltellu_suff = 0
      *
     c                   endsr
