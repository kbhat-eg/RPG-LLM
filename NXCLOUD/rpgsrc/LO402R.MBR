     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO402R
      *  Beskrivelse . . : Sletting av utplukk og merking av best.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  ------------------------------------------------
      *   6.10     180609  Oppdatert med sporingsinformasjon        BSA
6.nu  *   6.30     110614  Sjekket mtp. nummerutvidelse             bof
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flplolu    uf   e           k disk    rename(lplopfr:lplolur)
     flplll1    if   e           k disk    rename(lpllpfr:lplll1r)
     flplllu    uf   e           k disk    rename(lpllpfr:lplllur)
      *
      * Local Data Area
      *
     d                uds
6.10 d  l_user               911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_depr          s              1
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av variable i nøkler
      *
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lplll1_numm     s                   like(lklonr)
     d lplll1_suff     s                   like(lklsuf)
     d lplllu_numm     s                   like(lklonr)
     d lplllu_suff     s                   like(lklsuf)
     d lplllu_line     s                   like(lkllin)
     d lplolu_numm     s                   like(lkponr)
     d lplolu_suff     s                   like(lkpsuf)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(lofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av utplukk    UTPLUKK-LINJE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lplll1_key    setll     lplll1r
     c     lplll1_key    reade     lplll1r
      *
     c                   dow       not %eof
     c                   eval      lplllu_numm = lklonr
     c                   eval      lplllu_suff = lklsuf
     c                   eval      lplllu_line = lkllin
      *
     c     lplllu_key    chain     lplllur
     c                   if        %found
     c                   delete    lplllur
     c                   endif
      *
     c     lplll1_key    reade     lplll1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av utplukk    UTPLUKK-PARAM
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lplolu_key    chain     lplolur
     c                   if        %found
     c                   delete    lplolur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Blanker ut merkingen i recorden i BESTILLINGS-HODE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lokode = 0
     c                   move      *loval        lokoti
     c                   move      *loval        lokoda
     c                   eval      lokous = *blank
     c                   eval      lokows = *blank
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_depr
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppdatering av BESTILLINGS-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for oppdatering av UTPLUKK-PARAM
      *
     c     lplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplolu_numm
     c                   kfld                    lplolu_suff
      *
      * Key for les av UTPLUKK-LINJE
      *
     c     lplll1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplll1_numm
     c                   kfld                    lplll1_suff
      *
      * Key for oppdatering av UTPLUKK-LINJE
      *
     c     lplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplllu_numm
     c                   kfld                    lplllu_suff
     c                   kfld                    lplllu_line
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
      *
      *  Legger inn bestillingsnummer og suffix i nøkklene
      *
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_suff
      *
     c                   eval      lplolu_numm = p_numm
     c                   eval      lplolu_suff = p_suff
      *
     c                   eval      lplll1_numm = p_numm
     c                   eval      lplll1_suff = p_suff
      *
     c                   eval      lplllu_numm = p_numm
     c                   eval      lplllu_suff = p_suff
     c                   eval      lplllu_line = 0
      *
     c                   endsr
