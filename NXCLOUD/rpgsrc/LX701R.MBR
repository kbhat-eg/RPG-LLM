      /TITLE  Spesial program - lageroppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LX701R
      *  Beskrivelse . . : Oppdatering av informasjon på lagerregister
      *                    fra ekstern fil.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. : Excel-fil med lagerinfo
      *  Tilpasset for . : Johnsen
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           300800  Nytt program                             ASK
      *   6.10    260609  Oppdatert med sporingsinformasjon        BSA
6.20  *           310812  Nytt felt, endringsdato for faste data som vises
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      *  Datastruktur - excel-fil
      *
     d                 ds
     d d_ex                         512
     d  d_ldor                        6    overlay(d_ex:3)
     d  d_vare                        8    overlay(d_ex:9)
     d  d_bmen                        7    overlay(d_ex:66)
     d  d_bdes                        2    overlay(d_ex:74)
     d  d_bpun                        7    overlay(d_ex:80)
     d  d_enhl                        3    overlay(d_ex:87)
     d  d_lage                        2    overlay(d_ex:103)
      *
      *  UDS Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      *  Definering av variable i nøkler
      *
     d vvarlu_vare     s                   like(vvvare)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
      *
      *  Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_lage          s                   like(vllage)
     d w_vare          s                   like(vvvare)
     d w_ldor          s                   like(vvldor)
     d w_bpun          s              7  0
     d w_bmen          s              9  2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese ekstern fil for å hente neste vare for behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Lese file
      *
     c                   read      lwexpf                                 90
     c                   dow       *in90 = *off
     c                   eval      d_ex = exclin
      *
     c                   exsr      behand
      *
     c                   read      lwexpf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behand        begsr
     c                   movel     d_vare        w_vare
     c                   eval      vvarlu_vare = w_vare
     c     vvarlu_key    chain     vvarlur                            92
     c                   if        *in92 = *off
      *
     c                   move      d_ldor        w_ldor
     c                   eval      vvldor = w_ldor
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarlur
      *
     c                   eval      vlaglu_vare = w_vare
     c                   move      d_lage        w_lage
     c                   eval      vlaglu_lage = w_lage
     c     vlaglu_key    chain     vlaglur                            93
      *
     c                   move      d_bpun        w_bpun
     c                   movel     d_bmen        w_bmen
     c                   move      d_bdes        w_bmen
     c                   eval      vlbpun = w_bpun
     c                   eval      vlbmen = w_bmen
     c                   eval      vlenhl = d_enhl
      *
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.20 c                   time                    vlfeda
6.20 c                   time                    vlfeti
6.20 c                   eval      vlfeus = l_user
     c                   if        *in93 = *off
     c                   update    vlaglur
     c                   else
     c                   eval      vlfirm = w_firm
     c                   eval      vlvare = w_vare
     c                   eval      vllage = w_lage
6.10 c                   time                    vlodat
6.10 c                   time                    vlotim
6.10 c                   eval      vlousr = l_user
     c                   write     vlaglur
     c                   endif
      *
     c                   endif
     c     behand_ut     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for oppdatering av VARE
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
