     H/TITLE  ASSHOP: Endring/Oppdatering av transer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BOENOR
      * Beskrivelse . . : Endring/Oppdatering av transer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * R3.11 060700 AMD  Ved valg av kunde (F8) ble varelinjer priset etter
3.11  *                   leveringstype 0 igjen, dersom den var <> 0.
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
5.70  * PRGR  121208 bof  Utvidet med prisgruppe
6.31  * 6.30  210815 bof  feilretting i parameter til VP900R
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FVLTYL1    IF   E           K DISK    RENAME(VLTYPFR:VLTYL1R)
     FBMHEL1    IF   E           K DISK    RENAME(BMHEPFR:BMHEL1R)
     FBMDTLU    UF   E           K DISK    RENAME(BMDTPFR:BMDTLUR)
     FBMDTL1    IF   E           K DISK    RENAME(BMDTPFR:BMDTL1R)
      *
      *
8.01 d w_prgr          s              2
     D/EJECT
      * - - - - - - - - - - - - - - - - - - *
      *  Priser      :  Overføringsfelter   *
      * - - - - - - - - - - - - - - - - - - *
      *
      *                                                     Record, Sender
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.31 d  hinumm                        8  0 overlay(hirec:56)
6.31 d  hikode                        1    overlay(hirec:64)
6.31 d  hidekn                        5  2 overlay(hirec:65)
6.31 d  hiavgf                        1  0 overlay(hirec:70)
6.31 d  hipriv                        1  0 overlay(hirec:71)
6.31 d  hitilb                        1    overlay(hirec:72)
6.31 d  hiskaf                        1    overlay(hirec:73)
6.31 d  hildor                        6  0 overlay(hirec:74)
6.31 d  hisapr                        9  2 overlay(hirec:80)
6.31 d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      *                                                     Record, Mottar
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdatering/Beregning av bong                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Nullstiller totalfelter for denne bong
      *
     C                   MOVE      *ZERO         WWTOTS
      *
      *  Hent inn opplysninger fra BONG-HODE-REGISTER
      *
     C     HEDKEY        CHAIN     BMHEL1R                            90
      *
      *  Start med BONG-LINJE-REGISTER
      *
     C                   MOVE      *ZERO         LINLIN
     C     LINKEY        SETLL     BMDTL1R
      *
     C     STARTL        TAG
     C                   SETOFF                                           90
     C                   READ      BMDTL1R                                90
      *
     C     *IN90         IFEQ      *OFF
     C     LINAAR        IFEQ      BMAARR
     C     LINWSD        IFEQ      BMWSID
     C     LINNUM        IFEQ      BMNUMM
      *
      *  Hent opplysninger fra LINJE-TYPE-REGISTER
      *
     C                   MOVE      BMLTYP        LTYTYP
     C     LTYKEY        CHAIN     VLTYL1R                            92
      *
      *  Beregner/Henter nye priser kun for beløpslinjer
      *
     C     VALKTX        IFEQ      0
      *
      *  Legger over feltvariable over i hjelpevariable
      *
     C                   Z-ADD     BMoppr        WZoppr
     C                   Z-ADD     BMsapr        WZsapr
     C                   Z-ADD     BMkopr        WZkopr
     C                   MOVE      BMkpri        WZkpri
     C                   Z-ADD     BMRAB1        WZRAB1
     C                   Z-ADD     BMRAB2        WZRAB2
      *
      *  Hent inn nye priser og rabatter dersom statuskode = blank
      *
     C     WPSTAT        IFEQ      *BLANK
     C     BMkpri        IFNE      'F'
      *
      *  Dersom fast rabatt er utfylt skal den benyttes
      *
     C     WPRABF        IFNE      *ZERO
     C                   Z-ADD     WPRABF        WZRAB1
     C                   Z-ADD     0             WZRAB2
      *
      *  Henter inn priser og rabatter
      *
     C                   ELSE
     C                   EXSR      HNTPRI
     C                   END
      *
     C                   END
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Beregner totaler for den enkelte varelinje                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Beregning av antallsfelt         +/-
      *
     C                   Z-ADD     BMANTA        WZANTA
     C     VALKRE        IFEQ      '-'
     C                   MULT      -1            WZANTA
     C                   END
      *
      *  Beregning av salg
      *
     C     WZsapr        MULT      WZANTA        WWSUMS
     C                   Z-ADD     WWSUMS        WLISUM
      *
      *  Beregning av rabatter
      *
     C     WLISUM        MULT      BNRABP        WWRAB1
     C                   DIV       100           WWRAB1
     C                   Z-ADD     WWRAB1        WWSUMR
     C                   SUB       WWRAB1        WLISUM
      *
     C     WLISUM        MULT      WZRAB1        WWRAB1
     C                   DIV       100           WWRAB1
     C                   ADD       WWRAB1        WWSUMR
     C                   SUB       WWRAB1        WLISUM
      *
     C     WLISUM        MULT      WZRAB2        WWRAB1
     C                   DIV       100           WWRAB1
     C                   ADD       WWRAB1        WWSUMR
     C                   SUB       WWRAB1        WLISUM
      *
      *  Oppdatering av BONG-LINJE-REGISTER med nye opplysninger
      *
     C                   MOVE      BMLINE        LINLIN
     C     LINKEY        CHAIN     BMDTLUR                            92
     C     *IN92         IFEQ      *OFF
     C                   Z-ADD     WZoppr        BMoppr
     C                   Z-ADD     WZsapr        BMsapr
     C                   Z-ADD     WZkopr        BMkopr
     C                   MOVE      WZkpri        BMkpri
     C                   Z-ADD     WZRAB1        BMRAB1
     C                   Z-ADD     WZRAB2        BMRAB2
      *
     C     BMkopr        IFEQ      *ZERO
     C     BMsapr        MULT      WZPROS        BMkopr
     C                   END
      *
     C                   Z-ADD     WLISUM        BMSUMS
      *
     C                   UPDATE    BMDTLUR
     C                   END
      *
      *  Akkumulerer totaler
      *
     C                   ADD       BMSUMS        WWTOTS
      *
      *  Legger inn sist brukte linjenummer i overføringsfelt
      *
     C                   MOVE      BMLINE        WPLINE
      *
     C                   END
      *
     C                   GOTO      STARTL
     C                   END
     C                   END
     C                   END
     C                   END
      *
      *  Legger inn totalsum overføringsfelt
      *
     C                   Z-ADD     WWTOTS        WPTOTS
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter opplysninger om varen fra pris/vare subrutinen          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     HNTPRI        BEGSR
      *
     C                   MOVE      HEDFIR        HIFIRM
     C                   MOVE      BNRKAT        HIRKAT
      *
     C                   MOVE      BNKUND        HIKUND
     C     BNLKUN        IFNE      *ZERO
     C                   MOVE      BNLKUN        HIKUND
     C                   END
      *
     C                   MOVE      BNkpro        HIkpro
3.11 c                   move      bmlety        hilety
     C                   MOVE      BMENH1        hienhe
     C                   MOVE      BMVARE        HIVARE
     C                   MOVE      BNBDAT        HIDATO
     c                   move      *loval        hitime
     c                   eval      hinumm = 0
     C                   MOVE      *BLANK        hikode
     C                   MOVE      *ZERO         hidekn
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
5.70 c                   eval      hiprgr = w_prgr
      *
     C                   CALL      'VP900R'
     C                   PARM                    HIREC
     C                   PARM                    HOREC
      *
     C     HOoppr        IFGT      0.01
     C                   Z-ADD     HOoppr        WZoppr
     C                   END
      *
     C     HOsapr        IFGT      0.01
     C                   Z-ADD     HOsapr        WZsapr
     C                   END
      *
     C     HOkopr        IFGT      0.01
     C                   Z-ADD     HOkopr        WZkopr
     C                   END
      *
     C                   MOVE      HOkpri        WZkpri
     C                   Z-ADD     HORAB1        WZRAB1
     C                   Z-ADD     HORAB2        WZRAB2
     C                   Z-ADD     HOPROS        WZPROS
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     CSR   *INZSR        BEGSR
      *
     C                   MOVE      *ZERO         WWTOTS           11 2
     C                   MOVE      *ZERO         WWSUMR           11 2
     C                   move      *blank        w_alf1            1
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    BMANTA        WZANTA
     C     *LIKE         DEFINE    BMoppr        WZoppr
     C     *LIKE         DEFINE    BMsapr        WZsapr
     C     *LIKE         DEFINE    BMkopr        WZkopr
     C     *LIKE         DEFINE    BMkpri        WZkpri
     C     *LIKE         DEFINE    BMRAB1        WZRAB1
     C     *LIKE         DEFINE    BMRAB2        WZRAB2
     C     *LIKE         DEFINE    BMRAB2        WZPROS
      *
     C     *LIKE         DEFINE    BMSUMS        WWRAB1
     C     *LIKE         DEFINE    BMSUMS        WLISUM
      *
     C     *LIKE         DEFINE    BMSUMS        WWSUMS
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    w_alf1        WPSTAT
     C     *LIKE         DEFINE    BMFIRM        WPFIRM
     C     *LIKE         DEFINE    BMAARR        WPAARR
     C     *LIKE         DEFINE    BMWSID        WPWSID
     C     *LIKE         DEFINE    BMLINE        WPLINE
     C     *LIKE         DEFINE    BNRABP        WPRABF
     C     *LIKE         DEFINE    BNTOTS        WPTOTS
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VALFIR        LTYFIR
     C     *LIKE         DEFINE    VALTYP        LTYTYP
      *
     C     *LIKE         DEFINE    BNFIRM        HEDFIR
     C     *LIKE         DEFINE    BNAARR        HEDAAR
     C     *LIKE         DEFINE    BNWSID        HEDWSD
     C     *LIKE         DEFINE    BNNUMM        HEDNUM
      *
     C     *LIKE         DEFINE    BMFIRM        LINFIR
     C     *LIKE         DEFINE    BMAARR        LINAAR
     C     *LIKE         DEFINE    BMWSID        LINWSD
     C     *LIKE         DEFINE    BMNUMM        LINNUM
     C     *LIKE         DEFINE    BMLINE        LINLIN
      *
      *  Key for file : LINJE-TYPE-REGISTER
      *
     C     LTYKEY        KLIST
     C                   KFLD                    LTYFIR
     C                   KFLD                    LTYTYP
      *
      *  Key for file : BONG-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDAAR
     C                   KFLD                    HEDWSD
     C                   KFLD                    HEDNUM
      *
      *  Key for file : BONG-LINJE-REGISTER
      *
     C     LINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINAAR
     C                   KFLD                    LINWSD
     C                   KFLD                    LINNUM
     C                   KFLD                    LINLIN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPSTAT
     C                   PARM                    WPFIRM
     C                   PARM                    WPAARR
     C                   PARM                    WPWSID
     C                   PARM                    WPLINE
     C                   PARM                    WPRABF
     C                   PARM                    WPTOTS
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      WPFIRM        LTYFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        LINFIR
      *
      *  Legger inn årstall og skjerm/kasse i nøkklene
      *
     C                   MOVE      WPAARR        HEDAAR
     C                   MOVE      WPWSID        HEDWSD
     C                   MOVE      *ZERO         HEDNUM
      *
     C                   MOVE      WPAARR        LINAAR
     C                   MOVE      WPWSID        LINWSD
     C                   MOVE      *ZERO         LINNUM
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     CSR                 ENDSR
