     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP110R
      * Beskrivelse . . : Vare-pris, vedlikehold for vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190599 HKO  Nytt program
      *       251103 HKO  Rettet feil ved behandling av subfile
5.70  * PRGR  051108 BOF  Utvidet med prisgruppe
6.11  *       051108 BOF  Utvidet VVEPPF
6.20  *       300311 BOF  Utvidet vvprpf med leverandør
6.21  *       040112 BOF  utvidet med felter
6.22  *       110113 BOF  Lov til å slette priser på underleverandører
6.23  *       110113 BOF  Hvis siste pris er slettet på underlev.,så fjern
6.23  *                   leverandør fra lager.
6.24  *       030913 BOF  Litt strengere test på slett av priser på hovedl.
6.25  *       170214 bsa  Kan ikke slette enheten hvis det finnes bestillinger
6.25  *                   på denne enheten.
6.30  *       270314 bof  feilretting + test om varepris VA er utgått
6.31  *       200814 bof  utvidet med innkjøpspris 2
6.32  *       281015 bof  Hvis prisgr.blanks, tillatt å slette en med prgr.
7.xx  * 7.00  220216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  271119 bof  Skal ikke vise prisgruppe blank pris hvis pris med prisgr.(Gunvald)
7.02  * 7.00  260821 bof  Filter på visning
7.03  * 7.00  051121 bof  Hvis pris er i kopi av Master pris, så sett farge blå
8.01  *       070524 bof  Ved sletting av pris og sjekk bestill, ta med leverandør
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprlr    if   e           k disk    rename(vvprpfr:vvprlrr)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvprlu    uf   e           k disk    rename(vvprpfr:vvprlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvvenlu    uf a e           k disk    rename(vvenpfr:vvenlur)
6.11 fvveplu    uf   e           k disk    rename(vveppfr:vveplur)
6.23 fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.20 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.21 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
6.21 fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
6.25 flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
      *
     fvp110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
6.20 d p_ldor          s                   like(vpldor)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
5.70 d vvprlr_prgr     s                   like(vpprgr)
     d vvprlr_enhe     s                   like(vpenhe)
     d vvprlr_lety     s                   like(vplety)
     d vvprlr_pdat     s                   like(vppdat)
5.70 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
     d vvenlu_enhe     s                   like(veenhe)
6.11 d vveplu_penh     s                   like(vepenh)
     d vltpl1_lety     s                   like(vylety)
5.70 d ra30l1_dkod     s                   like(redkod)
     d venhl1_eenh     s                   like(vaeenh)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
6.20 d rlevl1_levr     s                   like(rllevr)
6.21 d jlevl3_enum     s                   like(jlenum)
6.21 d jvprl1_vare     s                   like(jxvare)
6.21 d jvprl1_ldor     s                   like(jxldor)
6.23 d vlaglu_vare     s                   like(vlvare)
6.25 d lodtlv_vare     s                   like(ldvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_enhs          s              1    inz(*off)
     d b_enhl          s              1    inz(*off)
     d b_enhe          s              1    inz(*off)
6.23 d b_pris          s              1    inz(*off)
6.25 d b_best          s              1    inz(*off)
6.32 d b_pgbl          s              1    inz(*off)
7.01 d b_prgr          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_pdat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vpfirm)
     d w_vare          s                   like(vpvare)
6.20 d w_ldor          s                   like(vpldor)
7.01 d w_prgr          s                   like(vpprgr)
7.02 d w_h1da1         s                   like(vppdat)
7.02 d w_h1da2         s                   like(vppdat)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
7.02 c                   when      *inka
7.02 c                   exsr      xh1win
7.02 c                   exsr      forny
7.02 c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
6.31 c                   when      *inkx
6.31 c                   if        *in80 = *on
6.31 c                   eval      *in80 = *off
6.31 c                   else
6.31 c                   eval      *in80 = *on
6.31 c                   endif
6.31 c                   exsr      forny
6.31 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     vvprl1_key    setll     vvprl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Slett
      *
     c                   if        b1valg = 4
5.70 c                   eval      d1prgr = b1prgr
     c                   eval      d1lety = b1lety
     c                   eval      d1enhe = b1enhe
     c                   eval      d1pdat = b1pdat
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
5.70 c                   eval      c2prgr = b1prgr
     c                   eval      c2lety = b1lety
     c                   eval      c2enhe = b1enhe
     c                   eval      c2pdat = b1pdat
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
5.70 c                   eval      vvprlr_prgr = c2prgr
     c                   eval      vvprlr_enhe = c2enhe
     c                   eval      vvprlr_lety = c2lety
     c     *dmy          move      c2pdat        vvprlr_pdat
     c     vvprlr_key    chain     vvprlr                             90
     c                   if        *in90 = *on
     c                   goto      end_c2bld
     c                   endif
      *
     c                   eval      c2sapr = vpsapr
     c                   eval      c2kopr = vpkopr
     c                   eval      c2inpr = vpinpr
6.31 c                   eval      c2inp2 = vpinp2
     c                   eval      c2bupr = vpbupr
6.21 c                   eval      c2nopr = vpnopr
     c                   eval      c2eusr = vpeusr
      *
     c     *dmy          move      vpedat        c2edat
6.21  *
6.21  * Henter nobb prisgiver og prisgivers varenr
6.21  *
6.21 c                   eval      jlevl3_enum = vpldor
6.21 c     jlevl3_key    chain     jlevl3
6.21 c                   if        %found(jlevl3)
6.21 c                   eval      c2nlev = jlldor
6.21 c                   endif
6.21  *
6.21 c                   eval      jvprl1_vare = vpvare
6.30 c                   eval      jvprl1_ldor = jlldor
6.21 c     jvprl1_key    chain     jvprl1
6.21 c                   if        %found(jvprl1)
6.30 c                   if        (jxtdat =  *loval) or
6.30 c                             (jxtdat <> *loval and
6.30 c                             jxtdat > w_udat)
6.21 c                   eval      c2lvar = jxlvar
6.30 c                   endif
6.21 c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Behandler bildet
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1vare = b2vare
     c     *dmy          move      d1pdat        w_pdat
6.32  *
6.32 c                   exsr      sjekk_pgbl
      *
5.70 c                   eval      vvprlr_prgr = d1prgr
     c                   eval      vvprlr_enhe = d1enhe
     c                   eval      vvprlr_lety = d1lety
     c                   eval      vvprlr_pdat = w_pdat
     c     vvprlr_key    chain     vvprlr                             90
      *
     c     d1tagb        tag
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
6.22  *
6.22  * hvis underleverandør, kan man alltid få slette prisene
6.24  * ellers må det sjekkes at det ikke slettes på basisenheter.
6.22  *
6.22 c                   if        vpldor =  vvldor
      *
      * Dersom prisen som slettes er pris mot statistikkenhet, lagerenhet
      * eller basisenhet, sjekkes det at det fortsatt vil finnes igjen
      * priser mot disse enhetene før prisen slettes
      *
     c                   if        d1enhe = vvenhs or
     c                             d1enhe = vvenhl or
     c                             d1enhe = vvenhe
      *
     c                   eval      b_enhs = *off
     c                   eval      b_enhl = *off
     c                   eval      b_enhe = *off
      *
6.24 c     vvprlr_key4   setll     vvprlr
6.24 c     vvprlr_key4   reade     vvprlr
6.22 c                   dow       not %eof(vvprlr)
      *
6.22 c                   if        d1enhe = vpenhe
      *
     c                   if        vplety <> d1lety or
     c                             vppdat <> w_pdat
     c                   if        vpenhe = vvenhs
     c                   eval      b_enhs = *on
     c                   endif
     c                   if        vpenhe = vvenhl
     c                   eval      b_enhl = *on
     c                   endif
     c                   if        vplety = 0 and
     c                             vpenhe = vvenhe
     c                   eval      b_enhe = *on
     c                   endif
     c                   endif
      *
6.22 c                   endif
      *
6.24 c     vvprlr_key4   reade     vvprlr                                 90
     c                   enddo
      *
6.32 c                   if        d1prgr = *blank or
6.32 c                             (d1prgr <> *blank and
6.32 c                              b_pgbl = *off)
     c                   if        d1enhe = vvenhs and
     c                             b_enhs = *off
     c                   eval      *in31 = *on
     c                   goto      d1tagb
     c                   endif
6.32 c                   endif
      *
6.32 c                   if        d1prgr = *blank or
6.32 c                             (d1prgr <> *blank and
6.32 c                              b_pgbl = *off)
     c                   if        d1enhe = vvenhl and
     c                             b_enhl = *off
     c                   eval      *in32 = *on
     c                   goto      d1tagb
     c                   endif
6.32 c                   endif
      *
6.32 c                   if        d1prgr = *blank or
6.32 c                             (d1prgr <> *blank and
6.32 c                              b_pgbl = *off)
     c                   if        d1enhe = vvenhe and
     c                             b_enhe = *off
     c                   eval      *in33 = *on
     c                   goto      d1tagb
     c                   endif
6.32 c                   endif
      *
     c                   endif
6.22  *
6.22 c                   endif
6.25  *
6.25  * Kan ikke slette enhet hvis den ligger på bestilling.
6.25  *
6.25 c                   exsr      sjk_best
6.25 c                   if        b_best = *on
6.25 c                   eval      *in34 = *on
6.25 c                   goto      d1tagb
6.25 c                   endif
6.25 c
      *
      * Sletter rad
      *
     c                   eval      b_forn = *on
      *
5.70 c                   eval      vvprlu_prgr = d1prgr
     c                   eval      vvprlu_enhe = d1enhe
     c                   eval      vvprlu_lety = d1lety
     c     *dmy          move      d1pdat        vvprlu_pdat
     c     vvprlu_key    chain     vvprlu                             90
      *
     c                   delete    vvprlur
6.22  *
6.22  * her sjekkes :
6.22  *  - det kun slettes enhet hvis det ikke er en eneste pris på enheten
6.22  *  - hvis det ikk er fler priser på underlev., så fjern fra lager
6.22  *
6.22 c                   eval      b_enhe = *off
6.22 c                   eval      b_pris = *off
6.22 c     vvprlr_key3   setll     vvprlr
6.22 c     vvprlr_key3   reade     vvprlr
6.22 c                   dow       not %eof(vvprlr)
6.22 c                   if        vpenhe = d1enhe
6.22 c                   eval      b_enhe = *on
6.22 c                   endif
6.23 c                   if        w_ldor = vpldor
6.23 c                   eval      b_pris = *on
6.23 c                   endif
6.22 c     vvprlr_key3   reade     vvprlr
6.22 c                   enddo
      *
      * Sletter enheter som er fjernet fra vare-pris
      *
6.22 c                   if        b_enhe = *off
      *
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 90
     c                   dow       *in90 = *off
6.22  *
6.22  * fjerner kun en og en enhet - den aktuelle
6.22  *
6.22 c                   if        veenhe = d1enhe
      *
     c                   eval      vvenlu_enhe = veenhe
     c     vvenlu_key    chain     vvenlur
     c                   if        %found(vvenlu)
     c                   delete    vvenlur
6.11 c                   eval      vveplu_penh = veenhe
6.11 c     vveplu_key    setll     vveplu
6.22 c     vveplu_key    reade     vveplu
6.11 c                   dow       not %eof(vveplu)
6.11 c                   delete    vveplur
6.22 c     vveplu_key    reade     vveplu
6.11 c                   enddo
6.23 c                   endif
6.23 c                   endif
      *
     c     vvenl1_key    reade     vvenl1                                 90
     c                   enddo
6.22  *
6.22 c                   endif
6.23  *
6.23  * Fjerner evt. lev.nr fra lager hvis det ikke finnes priser lenger
6.23  * på leverandør
6.23 c                   if        b_pris = *off
6.23  *
6.23 c     vlaglu_key    setll     vlaglu
6.23 c     vlaglu_key    reade     vlaglu
6.23 c                   dow       not %eof(vlaglu)
     c                   if        vlbldo = w_ldor
     c                   eval      vlbldo = 0
6.23 c                   time                    vledat
6.23 c                   time                    vletim
6.23 c                   eval      vleusr = l_user
6.23 c                   update    vlaglur
6.23 c                   endif
6.23 c     vlaglu_key    reade     vlaglu
6.23 c                   enddo
6.23  *
6.23 c                   endif
      *
     c     end_d1win     endsr
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * xh1win  Bilde for behandling av bla-alternativer
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     xh1win        begsr
7.02  *
7.02 c                   eval      w_h1da1 = *loval
7.02 c                   eval      w_h1da2 = *loval
7.02 c     h1taga        tag
7.02  *
7.02 c                   write     h1win
7.02 c                   exfmt     h1win
7.02  *
7.02  * Behandling av funksjonstaster
7.02  *
7.02 c                   select
7.02 c                   when      *inkc
7.02 c                   goto      end_h1win
7.02 c                   endsl
7.02  *
7.02  * validering
7.02  *
7.02  *
7.02  * sjekker dato 1
7.02 c                   if        h1dat1 <> 0
7.02 c     *dmy          test(d)                 h1dat1                 41
7.02 c                   if        *in41 = *on
7.02 c                   goto      h1taga
7.02 c                   endif
7.02 c                   endif
7.02  * sjekker dato 2
7.02 c                   if        h1dat2 <> 0
7.02 c     *dmy          test(d)                 h1dat2                 42
7.02 c                   if        *in42 = *on
7.02 c                   goto      h1taga
7.02 c                   endif
7.02 c                   endif
7.02 c                   if        h1dat1 <> 0
7.02 c     *dmy          move      h1dat1        w_h1da1
7.02 c                   endif
7.02 c                   if        h1dat2 <> 0
7.02 c     *dmy          move      h1dat2        w_h1da2
7.02 c                   endif
7.02  *
7.02 c     end_h1win     endsr
7.02  *
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  * Subrutine for sjekke om enhet ligger på bestilling
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  *
6.25 c     sjk_best      begsr
6.25  * "b_best" brukes litt "knølete", men enklest slik i de testene
6.25  * som brukes.
6.25 c                   eval      b_best = *on
6.25  *
6.25  * Sjekk om det vil ligge igjen priser på denne enhet etterpå
6.25  * Hvis ja, kan prisen bare slettes
6.25  *
6.25 c     vvprlr_key4   setll     vvprlr
6.25 c     vvprlr_key4   reade     vvprlr
6.25 c                   dow       not %eof(vvprlr)
6.25  *
6.25 c                   if        d1enhe = vpenhe
6.25 c                   if        vplety <> d1lety or
6.25 c                             vppdat <> w_pdat
6.25 c                   eval      b_best = *off
6.25 c                   leavesr
6.25 c                   endif
6.25 c                   endif
6.25  *
6.25 c     vvprlr_key4   reade     vvprlr                                 90
6.25 c                   enddo
6.25  *
6.25  * Sjekk om det ligger på en bestilling
6.25  *
6.25 c                   eval      b_best = *off
6.25 c                   eval      lodtlv_vare = p_vare
6.25 c     lodtlv_key    setll     lodtlv
6.25 c     lodtlv_key    reade     lodtlv
6.25 c                   dow       not %eof
8.01 c                   if        ldldor = p_ldor
6.25 c                   if        ldenh1 = d1enhe
6.25 c                   eval      b_best = *on
6.25 c                   leavesr
6.25 c                   endif
8.01 c                   endif
6.25 c     lodtlv_key    reade     lodtlv
6.25 c                   enddo
6.25  *
6.25 c                   endsr
6.25  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     vvprl1_key    reade     vvprl1
      *
     c                   if        %eof or
     c                             vpfirm <> w_firm or
     c                             vpvare <> w_vare or
     c                             vpldor <> w_ldor
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
7.01 c                   if        u_701 = *on
7.01 c                   if        vpprgr = *blank
7.01 c                   exsr      sjekk_prisgr
7.01 c                   if        b_prgr = *on
7.01 c                   iter
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
7.02  * sjekk filter
7.02 c                   if        h1prg1 <> *blank or
7.02 c                             h1prg2 <> *Blank or
7.02 c                             h1prg3 <> *Blank or
7.02 c                             h1prg4 <> *Blank or
7.02 c                             h1prg5 <> *Blank or
7.02 c                             h1prg6 <> *Blank or
7.02 c                             h1prg7 <> *Blank or
7.02 c                             h1prg8 <> *Blank or
7.02 c                             h1prg9 <> *Blank or
7.02 c                             h1pr10 <> *Blank
7.02 c                   if        vpprgr <> *blank and
7.02 c                             vpprgr <> h1prg1 and
7.02 c                             vpprgr <> h1prg2 and
7.02 c                             vpprgr <> h1prg3 and
7.02 c                             vpprgr <> h1prg4 and
7.02 c                             vpprgr <> h1prg5 and
7.02 c                             vpprgr <> h1prg6 and
7.02 c                             vpprgr <> h1prg7 and
7.02 c                             vpprgr <> h1prg8 and
7.02 c                             vpprgr <> h1prg9 and
7.02 c                             vpprgr <> h1pr10
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02 c                   if        w_h1da1 <> *loval or
7.02 c                             w_h1da2 <> *loval
7.02 c                   if        w_h1da1 <> *loval and
7.02 c                             w_h1da2 <> *loval
7.02 c                   if        vppdat <> w_h1da1 and
7.02 c                             vppdat <> w_h1da2
7.02 c                   iter
7.02 c                   endif
7.02 c                   else
7.02 c                   if        w_h1da1 <> *loval  and
7.02 c                             vppdat <> w_h1da1
7.02 c                   iter
7.02 c                   endif
7.02 c                   if        w_h1da2 <> *loval  and
7.02 c                             vppdat <> w_h1da2
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02 c                   endif
7.02 c
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1lety = vplety
     c                   eval      b1prgr = vpprgr
     c                   eval      b1enhe = vpenhe
     c                   eval      b1pdat = 0
     c                   eval      b1sapr = vpsapr
     c                   eval      b1kopr = vpkopr
     c                   eval      b1inpr = vpinpr
6.31 c                   if        *in80  = *on
6.31 c                   eval      b1pris = vpinp2
6.31 c                   else
     c                   eval      b1pris = vpbupr
6.31 c                   endif
     c                   eval      b1dekn = 0
      *
     c     *dmy          move      vppdat        b1pdat
      *
     c                   exsr      kalk_dekn
      *
7.03 c                   eval      *in50 = *off
7.03 c                   if        vpousr = 'MASTER'
7.03 c                   eval      *in50 = *on
7.03 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c2vare = p_vare
     c                   eval      c2tek1 = b2tek1
     c                   eval      c2ltxt = *blank
     c                   eval      c2etxt = *blank
     c                   eval      c2ptxt = *blank
     c                   eval      c2dekn = b1dekn
      *
      * Leveringstype
      *
     c                   if        c2lety <> 0
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *off
     c                   eval      c2ltxt = vybesk
     c                   endif
     c                   endif
      *
      * prisgruppe
      *
5.70 c                   if        c2prgr <> *blank
5.70 c                   eval      ra30l1_dkod = c2prgr
5.70 c     ra30l1_key    chain     ra30l1                             91
5.70 c                   if        *in91 = *off
5.70 c                   eval      c2ptxt = redtxt
5.70 c                   endif
5.70 c                   endif
      *
      * Enhet
      *
     c                   if        c2enhe <> *blank
     c                   eval      venhl1_eenh = c2enhe
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *off
     c                   eval      c2etxt = vaetxt
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulering av dekningsgrad
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_dekn     begsr
      *
      * Dersom kostpris = 0 hentes DG fra varen eller undergruppe
      *
     c                   if        b1sapr <> 0
      *
     c                   if        b1kopr <> 0
     c                   select
     c                   when      (100 - (b1kopr * 100 / b1sapr)) < -999,99
     c                   eval      b1dekn = -999,99
     c                   when      (100 - (b1kopr * 100 / b1sapr)) > 999,99
     c                   eval      b1dekn = 999,99
     c                   other
     c                   eval(h)   b1dekn = 100 - (b1kopr * 100 / b1sapr)
     c                   endsl
     c                   endif
      *
     c                   if        b1kopr = 0
     c                   eval      b1dekn = vvdekn
     c                   if        b1dekn = 0
     c                   eval      b1dekn = vgudek
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekk om prisgruppe blank på enhet finnes
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pgbl    begsr
6.32  *
6.32 c                   eval      b_pgbl = *off
6.32  *
6.32 c                   eval      vvprlr_prgr = *blank
6.32 c                   eval      vvprlr_enhe = d1enhe
6.32 c                   eval      vvprlr_lety = d1lety
6.32 c                   eval      vvprlr_pdat = w_pdat
6.32 c     vvprlr_key    chain     vvprlr                             90
6.32 c                   if        %found(vvprlr)
6.32 c                   eval      b_pgbl = *on
6.32 c                   endif
6.32  *
6.32 c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å sjekke om prisgruppe finnes
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     sjekk_prisgr  begsr
7.01  *
7.01 c                   eval      b_prgr = *off
7.01 c                   eval      vvprlr_prgr = w_prgr
7.01 c                   eval      vvprlr_enhe = vpenhe
7.01 c     vvprlr_key2   chain     vvprlr
7.01 c                   if        %found(vvprlr)
7.01 c                   eval      b_prgr = *on
7.01 c                   endif
7.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
6.20 c                   parm                    p_ldor
      *
      * Key for oppslag på vare-pris
      *
     c     vvprlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    w_ldor
5.70 c                   kfld                    vvprlr_prgr
     c                   kfld                    vvprlr_enhe
     c                   kfld                    vvprlr_lety
     c                   kfld                    vvprlr_pdat
      *
     c     vvprlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    w_ldor
5.70 c                   kfld                    vvprlr_prgr
     c                   kfld                    vvprlr_enhe
6.22  *
6.22 c     vvprlr_key3   klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    w_vare
6.24  *
6.24 c     vvprlr_key4   klist
6.24 c                   kfld                    w_firm
6.24 c                   kfld                    w_vare
6.24 c                   kfld                    w_ldor
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    w_ldor
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    w_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av vare-enhet
      *
     c     vvenlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenlu_enhe
6.11  *
6.11  * Key for oppdatering av vare-enhet-prisgiver
6.11  *
6.11 c     vveplu_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    w_vare
6.11 c                   kfld                    vveplu_penh
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
6.20  *
6.20  * Key for oppslag på leverandør
6.20  *
6.20 c     rlevl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rlevl1_levr
6.21  *
6.21  * Key for oppslag på nobb-leverandør
6.21  *
6.21 c     jlevl3_key    klist
6.21 c                   kfld                    jlevl3_enum
6.21  *
6.21  * Key for oppslag på nobb-vare-pris-lev
6.21  *
6.21 c     jvprl1_key    klist
6.21 c                   kfld                    jvprl1_vare
6.21 c                   kfld                    jvprl1_ldor
6.23  *
6.23  * Key for oppdatering av leveranør på lagerregister
6.23  *
6.23 c     vlaglu_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vlaglu_vare
6.25  *
6.25  * Key for oppdatering av leveranør på lagerregister
6.25  *
6.25 c     lodtlv_key    klist
6.25 c                   kfld                    w_firm
6.25 c                   kfld                    lodtlv_vare
      *
      *
      * Henter firma og vare fra parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
6.20 c                   eval      w_ldor = p_ldor
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'IKKE_VIS_PG_BLANK':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
      * Initierer skjermbilde
      *
     c     vvarl1_key    chain     vvarl1                             90
     c                   eval      b2vare = vvvare
     c                   eval      b2tek1 = vvtek1
6.20 c                   eval      rlevl1_levr = w_ldor
6.20 c     rlevl1_key    chain     rlevl1                             91
6.20 c                   eval      b2ldor = w_ldor
6.20 c                   movel     rlnavn        b2lnav
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             90
      *
      * Finner dagens dato
      *
     c                   time                    w_udat
7.01  *
7.01 c                   if        u_701 = *on
7.01 c
7.01 c                   call      'VL711R   '
7.01 c                   parm                    w_prgr
7.01 c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vvprl1_key    setll     vvprl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
