     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RC653R                                              *
      * Beskrivelse . . : Inkasso - utskrift av nye saker                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.30     030205  Skriver sist purret dato i stedet for ink.dato HFL  *
      *           070519  For comp mot nytt rca0pf                            *
      * ___________________________________________________________________________________________
Korr- * Referanse  Dato   Endringsbeskrivelse                                                  Sign
versj.* --------- ------  -------------------------------------------------------------------- ----
7.00  * Nxs-1870  280520  Ny løsning Skattum m/excelark ut med inkasso forslag                 sst
      *
      * ___________________________________________________________________________________________
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbpf    if   e           k disk    rename(rkwbpfr:rkwbpfr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frca0l1    if   e           k disk    rename(rca0pfr:rca0l1r)
     frc653p    o    e             printer oflind(*in30)
      *
     d sub             s              3  0 dim(13)
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  ppros                461    463  1
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pparam               300    490
      *
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_fgrp               931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
7.00  *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
     d w_bdat          s              6  0
     d w_fdat          s              6  0
     d w_idat          s              6  0
     d wkjdat          s              6  0
     d w_firm          s                   like(rnfirm)
     d w_kund_gml      s                   like(rnkund)
     d rkunl1_kund     s                   like(rnkund)
     d wnbelØ          s                   like(rnbelØ)
      *
     d wbforp          s             11  2
     d wbfort          s             11  2
     d n               s              3  0
7.00  *   Felter for fil ut i regeark
7.00  *
7.00 d Fil_Til_Excel   s              1    inz(' ')
7.00 d w_sql           s          10000
7.00 d w_map           s             20    inz('Inkasso\Forslag\')
7.00 d w_tildok        s             50
7.00 d w_typ           s              4    inz('xlsx')
7.00 d w_dat           s               d   datfmt(*iso)
7.00 d w_tim           s               t   timfmt(*iso)
7.00 d w_ms1           s             50
7.00 d w_ms2           s             50
7.00 d w_svar          s              1
7.00 d w_in12          s              1  0
      *
7.00 c     *dmy          move      udate         w_dat
7.00 c                   time                    w_tim
     c     dummy         tag
      *
     c                   read      rkwbpfr                                61
      *
     c                   dow       *in61 = *off
      *
      * Brudd på kundenummer
      *
     c                   if        rnkund <> w_kund_gml
      *
     c                   if        w_kund_gml > *zeros
      *
     c                   if        wbforp > rksald
     c                   eval      w_komm = 'Saldo < Inkassosaldo'
     c                   endif
      *
     c                   if        wbforp <> *zeros
     c                   write     t1tot
     c                   endif
      *
     c                   endif
      *
     c                   eval      wbforp = 0
     c                   eval      w_komm = *blanks
      *
      * Les kunderegister
      *
     c                   move      rnkund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1                             10
      *
      * Sjekk saldo og merking
      *
     c                   if        rksald <= 0 or
     c                             rkpneg = 'N'
     c                   goto      neste
     c                   endif
      *
      * Sjekk om reklamasjon eller 0 i restbeløp
      *
     c                   if        rnbilk = 89 or
     c                             rnbelØ = 0
     c                   goto      neste
     c                   endif
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     l1kun
      *
     c                   eval      w_kund_gml = rnkund
     c                   endif
      *
      * Behandle bilagsdato, forfallsdato og inkassodato
      *
     c                   eval      w_bdat = *zeros
     c                   eval      w_fdat = *zeros
     c                   eval      w_idat = *zeros
      *
     c     *dmy          move      rnbdat        w_bdat
      *
     c                   if        rnbelØ > 0
     c     *dmy          move      rnfdat        w_fdat
     c                   endif
      *
T5.30c                   if        rnpdat <> *loval
T5.30c     *dmy          move      rnpdat        w_idat
     c                   endif
      *
      * Akkumuler totalt forfalt
      *
     c                   eval      wbforp = wbforp + rnrest
     c                   eval      wbfort = wbfort + rnrest
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     d1det
      *
     c     neste         tag
      *
     c                   read      rkwbpfr                                61
     c                   enddo
      *
     c                   move      *on           *inlr
      *
     c                   write     t1tot
     c                   write     t2tot
      *
7.00 c                   if        Fil_Til_Excel = 'J'
7.00 c                   eval      w_sql = 'select +
7.00 c                                     rnavde as "Avdeling" +
7.00 c                                   , rnkund as "Kunde"        +
7.00 c                                   , rknavn as "Kunde-navn"   +
7.00 c                                   , rkgate as "Adresse"      +
7.00 c                                   , rkadr2 as "Adresse-2 "   +
7.00 c                                   , rkponr as "Postnr"       +
7.00 c                                   , rksted as "Poststed"     +
7.00 c                                   , rnbdat as "Bilags-dato"  +
7.00 c                                   , rnbiln as "Bilagsnummer" +
7.00 c                                   , rntext as "Bilagstekst"  +
7.00 c                                   , rnbelØ as "Beløp"        +
7.00 c                                   , rnrest as "Restbeløp"    +
7.00 c                                   , rnantp as "Ant Purr"     +
7.00 c                                   , rnpdat as "Siste Varsel" +
7.00 c                                   from rkwbpf                +
7.00 c                                   left outer join rkunpf on  +
7.00 c                                         rnfirm = rkfirm and  +
7.00 c                                         rnkund = rkkund'
7.00 c                   eval      w_tildok = %trim(L_user) + '_' +
7.00 c                                        %char(w_dat)  + '_' +
7.00 c                                        %char(w_tim)
7.00 c                   call      'ASPTOXLSX'
7.00 c                   parm                    w_sql
7.00 c                   parm                    w_map
7.00 c                   parm                    w_tildok
7.00 c                   parm                    w_typ
7.00  *
7.00 c                   eval      w_ms1 = 'Inkasso-forslag er lagt +
7.00 c                                    i excel i IFS-området som:'
7.00 c                   eval      w_ms2 = '\asp\' + l_fgrp +
7.00 c                                     '\' + %trim(w_map) +
7.00 c                                      %trim(w_tildok)
7.00 c                   eval      w_svar = 'J'
7.00  *
7.00 c                   call      'AA007R'
7.00 c                   parm                    w_ms1
7.00 c                   parm                    w_ms2
7.00 c                   parm                    w_svar
7.00 c                   parm                    w_in12
7.00 c                   endif
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   eval      n = *zeros
     c                   move      l_firm        w_firm
      *
      *  Hent sist brukte inkassonummer
      *
     c     w_firm        chain     rca0l1                             10
      *
     c                   if        *in10 = *on
     c                   eval      rc0inr = 1
     c                   else
     c                   eval      rc0inr = rc0inr + 1
     c                   endif
      *
7.00  *
7.00  *   Sjekk diverse spesialiteter
7.00  *
7.00    CallP CO402R(l_fgrp:l_firm:0:'INKASSOFORSLAGTILEXCEL':
7.00                     co402_verdi1:co402_verdi2);
7.00    if co402_verdi1 = '1';
7.00      eval  Fil_Til_Excel = 'J';
7.00    endif;
      *
     c                   if        ppkod = 'I'
     c                   eval      *in31 = *on
     c                   else
     c                   eval      *in31 = *off
     c                   endif
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
