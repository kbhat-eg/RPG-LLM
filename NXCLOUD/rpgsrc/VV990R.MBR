     h option(*nodebugio) datedit(*dmy) decedit('.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV990R
      * Beskrivelse . . : Vare, hent informasjon til WAP
      *
      *                   Programmet setter ikke på *inLR
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       301003 HKO  Nytt program
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       180304 HKO  Tilpasset nye feltlenger
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
5.70  * PRGR  041108 bof  Utvidet med prisgruppe
6.nu  *       160614 bof  Endret ihht. nummerutvidelse
      *
8.01  *PRG2   140622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_outp          s            280
      *
      * Definering av datastrukturer
      *
      * Parameterliste - ut
      *
     d                 ds
     d d_outp                       281
     d  d_vare                       16    overlay(d_outp:  1)
     d  d_nobb                        8  0 overlay(d_outp: 17)
     d  d_tek1                       35    overlay(d_outp: 25)
     d  d_tek2                       35    overlay(d_outp: 60)
     d  d_kpri                       10    overlay(d_outp: 95)
     d  d_enh1                        3    overlay(d_outp:105)
     d  d_enh2                        3    overlay(d_outp:108)
     d  d_enh3                        3    overlay(d_outp:111)
     d  d_sap1                       10    overlay(d_outp:114)
     d  d_sap2                       10    overlay(d_outp:124)
     d  d_sap3                       10    overlay(d_outp:134)
     d  d_rab1                        6    overlay(d_outp:144)
     d  d_rab2                        6    overlay(d_outp:150)
     d  d_rab3                        6    overlay(d_outp:156)
     d  d_ntp1                       10    overlay(d_outp:162)
     d  d_ntp2                       10    overlay(d_outp:172)
     d  d_ntp3                       10    overlay(d_outp:182)
     d  d_lag1                       12    overlay(d_outp:192)
     d  d_lag2                       12    overlay(d_outp:204)
     d  d_lag3                       12    overlay(d_outp:216)
     d  d_omr1                       13    overlay(d_outp:228)
     d  d_omr2                       13    overlay(d_outp:241)
     d  d_omr3                       13    overlay(d_outp:254)
     d  d_enhe                        3    overlay(d_outp:267)
     d  d_anta                       12    overlay(d_outp:270)
      *
      * Datastruktur - parametre inn til VP900R
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Datastruktur - parametre ut fra VP900R
      *
     d                 ds
     d horec                         80
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vpkol1_pkod     s                   like(vapkod)
      *
      * Definering av variable
      *
     d w_udat          s               d   datfmt(*iso)
     d x               s              1  0
      *
     d w_kpri          s              1
     d w_sap1          s              9  2
     d w_sap2          s              9  2
     d w_sap3          s              9  2
     d w_rab1          s              5  2
     d w_rab2          s              5  2
     d w_rab3          s              5  2
     d w_ntp1          s              9  2
     d w_ntp2          s              9  2
     d w_ntp3          s              9  2
      *
     d w_firm          s                   like(vvfirm)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
      *
8.01 d w_prgr          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initiering
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
     c                   eval      w_kpri = *blank
     c                   eval      w_sap1 = 0
     c                   eval      w_sap2 = 0
     c                   eval      w_sap3 = 0
     c                   eval      w_rab1 = 0
     c                   eval      w_rab2 = 0
     c                   eval      w_rab3 = 0
     c                   eval      w_ntp1 = 0
     c                   eval      w_ntp2 = 0
     c                   eval      w_ntp3 = 0
      *
     c                   time                    w_udat
      *
     c                   eval      p_outp = *blank
      *
      * Avbryter dersom vare ikke finnes
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter informasjon
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av forespørsel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Henter enheter fra vare
      *
     c                   exsr      enheter
      *
      * Kaller prisberegningsprogram for enhet-1 og finner salgspriser og
      * rabatt
      *
     c                   eval      hifirm = p_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = p_kund
     c                   eval      hikpro = p_kpro
     c                   eval      hivare = vvvare
     c                   eval      hilety = 0
     c                   eval      hidato = w_udat
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hitilb = *on
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hiinlr = *on
      *
     c                   eval      hienhe = w_enh1
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   eval      w_kpri = hokpri
     c                   eval      w_sap1 = hosapr
     c                   eval      w_ntp1 = hosapr
      *
     c                   if        horab1 > 0
     c                   eval(h)   w_ntp1 = w_ntp1 - (w_ntp1 * horab1 / 100)
     c                   endif
     c                   if        horab2 > 0
     c                   eval(h)   w_ntp1 = w_ntp1 - (w_ntp1 * horab2 / 100)
     c                   endif
      *
     c                   eval      w_rab1 = horab1
      *
     c                   if        horab2 <> 0 and
     c                             w_sap1 <> 0
     c                   eval(h)   w_rab1 = (w_sap1 - w_ntp1) * 100 / w_sap1
     c                   endif
      *
      * Kaller prisberegningsprogram for enhet-2 og finner salgspris
      *
     c                   if        w_enh2 <> *blank
      *
     c                   eval      hienhe = w_enh2
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   eval      w_sap2 = hosapr
     c                   eval      w_ntp2 = hosapr
      *
     c                   if        horab1 > 0
     c                   eval(h)   w_ntp2 = w_ntp2 - (w_ntp2 * horab1 / 100)
     c                   endif
     c                   if        horab2 > 0
     c                   eval(h)   w_ntp2 = w_ntp2 - (w_ntp2 * horab2 / 100)
     c                   endif
      *
     c                   eval      w_rab2 = horab1
      *
     c                   if        horab2 <> 0 and
     c                             w_sap2 <> 0
     c                   eval(h)   w_rab2 = (w_sap2 - w_ntp2) * 100 / w_sap2
     c                   endif
      *
     c                   endif
      *
      * Kaller prisberegningsprogram for enhet-3 og finner salgspris
      *
     c                   if        w_enh3 <> *blank
      *
     c                   eval      hienhe = w_enh3
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   eval      w_sap3 = hosapr
     c                   eval      w_ntp3 = hosapr
      *
     c                   if        horab1 > 0
     c                   eval(h)   w_ntp3 = w_ntp3 - (w_ntp3 * horab1 / 100)
     c                   endif
     c                   if        horab2 > 0
     c                   eval(h)   w_ntp3 = w_ntp3 - (w_ntp3 * horab2 / 100)
     c                   endif
      *
     c                   eval      w_rab3 = horab1
      *
     c                   if        horab2 <> 0 and
     c                             w_sap3 <> 0
     c                   eval(h)   w_rab3 = (w_sap3 - w_ntp3) * 100 / w_sap3
     c                   endif
      *
     c                   endif
      *
      * Flytter verdier til datastruktur
      *
     c                   eval      d_vare = vvvare
     c                   eval      d_nobb = vvnobb
     c                   eval      d_tek1 = vvtek1
     c                   eval      d_tek2 = vvtek2
     c                   eval      d_kpri = vvkpri
     c                   eval      d_enh1 = w_enh1
     c                   eval      d_enh2 = w_enh2
     c                   eval      d_enh3 = w_enh3
     c                   eval      d_sap1 = %editc(w_sap1:'4')
     c                   eval      d_sap2 = %editc(w_sap2:'4')
     c                   eval      d_sap3 = %editc(w_sap3:'4')
     c                   eval      d_rab1 = %editc(w_rab1:'4')
     c                   eval      d_rab2 = %editc(w_rab2:'4')
     c                   eval      d_rab3 = %editc(w_rab3:'4')
     c                   eval      d_ntp1 = %editc(w_ntp1:'4')
     c                   eval      d_ntp2 = %editc(w_ntp2:'4')
     c                   eval      d_ntp3 = %editc(w_ntp3:'4')
     c                   eval      d_lag1 = '00000000.000'
     c                   eval      d_lag2 = '00000000.000'
     c                   eval      d_lag3 = '00000000.000'
     c                   eval      d_omr1 = %editc(w_omr1:'4')
     c                   eval      d_omr2 = %editc(w_omr2:'4')
     c                   eval      d_omr3 = %editc(w_omr3:'4')
     c                   eval      d_enhe = *blank
     c                   eval      d_anta = '00000000.000'
      *
     c                   if        w_kpri <> *blank
     c                   eval      vpkol1_pkod = w_kpri
     c     vpkol1_key    chain     vpkol1
     c                   if        %found
     c                   eval      d_kpri = vaptxt
     c                   endif
     c                   endif
      *
      * Returnerer datastruktur
      *
     c                   eval      p_outp = d_outp
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     enheter       begsr
      *
     c                   eval      x = 0
      *
      * Henter salgsenheter
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
      * Finner omregningsfaktorer
      *
     c                   if        w_omr1 <> 0
     c                   eval      w_omr2 = w_omr2 / w_omr1
     c                   eval      w_omr3 = w_omr3 / w_omr1
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_outp
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare-enhet m/salgsenhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på priskode
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
      *
     c                   endsr
