     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB001R
      * Beskrivelse . . : Leser linjer fra FOVF til kommaseparert til
      *                   (Business)
9.01  * Spesialversjon. : Skal kjøre denne versjonen av summering (6.20
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
      *
6.20  * V6.20 130213 nho  Sjekk av kid
6.21  * V6.20 051113 nho  Avstand mellom felter
6.22  * V6.20 141014 nho  Test på firma
6.23  * V6.20 141014 nho  Blanker kid etter hver write
6.24  * V6.20 120515 nho  Val.dato skal være lik bilagsdato
6.nu  * V6.30 230613 bof  Gjennomgått mtp. nummerutvidelse
6.nu  * R6.20     280515  Utvidelse av kort kid                          NHO  *
6.30  * V6.30 131218 nho  Legger ut avdeling
7.00  * V7.00 260419 nho  Henter inn konvertering av bilagskode
7.01  * V7.00 240619 nho  Henter inn firm_begin
6.30  * V7.00 190820 sst  Hentet inn igjen utlegg av avdeling fra 630 versjonen
7.02  * V7.00 190820 sst  XLedger format
7.03  *       120521 mst  Henter avvikende momskode, hvis den finnes
7.04  *       280521 sst  Legge ut noen felter bare når kundenr i record
7.05  *       200921 sst  Justert 7.04 vedr test på kundenr 99999/10000
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffovupf    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
6.20 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.20 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
7.00 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
7.03 frb00l1    if   e           k disk    rename(rb00pfr:rb00l1r)
      *
      *
     frv01pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d valdat          s                   like(fffdat)
     d nval08          s              8  0
     d nval04          s              4  0
     d nfor08          s              8  0
     d nfor04          s              4  0
     d nbil08          s              8  0
     d nbil04          s              4  0
     d tell            s              1  0
7.00 d xxbilk          s              5  0
     d f1momd          s                   like(ffdmom)
     d f1momc          s                   like(ffcmom)
6.30 d f1avde          s                   like(ffcavd)
     d zfbelp          s             12  4
     d w_binr          s                   like(sobinr)
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
7.02 d x10             s             10
7.02 d x8              s              8
      *
6.20  * Variabler for nøkler
6.20  *
6.20 d votyl1_otyp     s                   like(vaotyp)
6.20 d aforl1_ruti     s                   like(afruti)
7.00 d rb10l1_gkod     s                   like(raakod)
      *
6.nu d*w_kidr          s              6  0
6.nu d w_kidr          s              8  0
6.20 d w_kidd          s                   like(sokidd)
6.20 d w_kid2          s                   like(sokidd)
6.20 d t1kkid          s             23
      *
6.21 d b_kkid          s              1    inz(*off)
7.03 d rb00l1_gkod     s                   like(r00kod)
7.03 d w_type          s             10
7.04 d w_biln          s             10
7.04 d w_for08         s             10
      * Datastruktur for valuteringsdato
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
      * Nøkkel dato
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
      *
      * Datastruktur for forfallsdato
     d                 ds
     d f_fdati                 1     10
     d  f_fa                   1      4
     d  f_f1                   5      5
     d  f_fn                   6      7
     d  f_f2                   8      8
     d  f_fd                   9     10
     d*
      *
      * Datastruktur for bilagsdato
     d                 ds
     d b_fdati                 1     10
     d  b_fa                   1      4
     d  b_f1                   5      5
     d  b_fn                   6      7
     d  b_f2                   8      8
     d  b_fd                   9     10
     d*
      *
7.00  * Key for posisjonering på bilagskoder
7.00  *
7.00 c     rb10l1_key    klist
7.00 c                   kfld                    w_firm
7.00 c                   kfld                    rb10l1_gkod
7.00  *
      * Key for oppslag på faktura-hode-register
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
      *
6.20  * Key for les av ordretype
6.20  *
6.20 c     votyl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    votyl1_otyp
6.20  *
6.20  * Key for les av rutine-register
6.20  *
6.20 c     aforl1_key    klist
6.20 c                   kfld                    aforl1_ruti
6.20  *
7.03  * Key for posisjonering på koder (moms)
7.03  *
7.03 c     rb00l1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    w_type
7.03 c                   kfld                    rb00l1_gkod
6.22  *
6.22 c                   if        l_firm <> fffirm
6.22 c                   goto      slutt
6.22 c                   endif
      *
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
7.01 c                   read      rb10l1                                 90
7.02  *   Linje 1
7.02 c                   eval      rvdata = *blank
7.02 c                   eval      rvdata = %trim('@WaBnd(Descr, ValDt, SrcTp)')
7.02 c                   write     rv01pfr
7.02  *
7.02  *   Linje 2
7.02 c                   eval      rvdata = *blank
7.02 c                   write     rv01pfr
7.02  *
7.02  *   Linje 3
7.02 c                   eval      rvdata = *blank
7.02 c                   time                    valdat
7.02 c                   movel     valdat        x10
7.02 c                   eval      x8 = %subst(x10:1:4) +
7.02 c                                  %subst(x10:6:2) +
7.02 c                                  %subst(x10:9:2)
7.02 c                   eval      rvdata = %trim('Fakt.fil;' +
7.02 c                                      x8 + ';12')
7.02 c                   write     rv01pfr
7.02  *
7.02  *   Linje 4
7.02 c                   eval      rvdata = *blank
7.02 c                   write     rv01pfr
7.02  *
7.02  *   Linje 5
7.02 c                   eval      rvdata = *blank
7.02 c                   eval      rvdata = %trim('@WaVo(VoNo,VoDt,txt,VoTp,Am,+
7.02 c                                             DbAcNo,CrAcNo,+
7.02 c                                             DbTrnC1,CrTrnC1,DbTxCd,+
7.02 c                                             CrTxCd,DbAcC1,CrAcC1,+
7.02 c                                             R1,R2,InvoNo,AgRef,DueDt,+
7.02 c                                             Cid')
7.02 c                   write     rv01pfr
7.02  *
7.02  *   Linje 6
7.02 c                   eval      rvdata = *blank
7.02 c                   write     rv01pfr
7.02  *
7.02 c                   eval      rvdata = *blank
7.02  *
7.02 c                   endif
7.02  *
      ***********************************************************
     c
     c* Blanker felt
     c                   eval      d_dati  = *blanks
      *
     c                   move      ffbdat        d_dati
      *
      * Forfallsdato
     c                   movel     fffdat        f_fdati
     c                   movel     f_fa          nfor08
     c                   movel     f_fn          nfor04
     c                   move      f_fd          nfor04
     c                   move      nfor04        nfor08
      *
      * Bilagsdato
     c                   movel     ffbdat        b_fdati
     c                   movel     b_fa          nbil08
     c                   movel     b_fn          nbil04
     c                   move      b_fd          nbil04
     c                   move      nbil04        nbil08
     c*
      * Beløp skal legges ut med 4 desimaler
     c                   z-add     ffbelp        zfbelp
      * Legger ut detaljer fra FOVF
      * Nullstiller
     c                   eval      f1momd = '0'
     c                   eval      f1momc = '0'
      *
     c                   exsr      control_init
      *
7.00  * Sjekker om bilagstype skal konverteres
7.00 c                   eval      xxbilk = ffbilk
7.00 c                   eval      rb10l1_gkod = ffbilk
7.00 c     rb10l1_key    chain     rb10l1
7.00 c                   if        %found
7.00 c                   eval      xxbilk = rankod
7.00 c                   endif
7.00  *
      *
     c                   if        ffdkto <> *zeros and
     c                             ffdkto >=  1000 and
     c                             ffdkto <=  9999
     c                   eval      f1momd = ffdmom
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momd        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c                   movel     r00nko        f1momd
7.03 c                   endif
     c                   endif
      *
     c                   if        ffckto <> *zeros and
     c                             ffckto >=  1000 and
     c                             ffckto <=  9999
     c                   eval      f1momc = ffcmom
     c                   eval      f1momd = '0'
7.03  * Sjekker om momskode  skal konverteres
7.03 c                   eval      w_type      = 'MVAKODE'
7.03 c                   move      f1momc        rb00l1_gkod
7.03 c     rb00l1_key    chain     rb00l1
7.03 c                   if        %found
7.03 c                   movel     r00nko        f1momc
7.03 c                   endif
     c                   endif
      *
6.30  * Legger ut avdeling
6.30 c                   eval      f1avde = *zeros
6.30  *
6.30 c                   if        ffcavd <> *zeros
6.30 c                   eval      f1avde = ffcavd
6.30 c                   endif
6.30  *
6.30 c                   if        ffdavd <> *zeros
6.30 c                   eval      f1avde = ffdavd
6.30 c                   endif
7.04  *
7.04 c                   eval      w_biln  = %char(ffbiln)
7.04 c                   eval      w_for08 = %char(nfor08)
6.30  *
7.04  *  Enklete felter skal kun ut dersom det er kundenr i record
7.05 c                   if        ffckto < 10000
7.05 c                             and ffdkto < 10000
7.04 c                   eval      t1kkid  = *blank
7.04 c                   eval      w_for08 = *blank
7.04 c                   eval      w_biln  = *blank
7.04 c                   endif
7.04  *
     c**********
     c                   eval      rvdata = *blank
      * Start
     c*                  eval      rvdata =  +
      * Bilagsnr.
     c                   eval      rvdata = %char(ffbiln)     + ';' +
      * Bil.dato
     c                                      %char(nbil08)     + ';' +
      * Val.dat fra heading
6.24 c*                                    %char(nval08)    + ';' +
6.24 c                                     %char(nbil08)     + ';' +
      * Bil.type
7.00 c**                                   %char(ffbilk)     + ';' +
7.00 c                                     %char(xxbilk)     + ';' +
7.02  * Bil.tekst
7.02 c***                                  %trim(fftext)     + ';' +
7.02  * Beløp på linje
7.02 c                                     %char(zfbelp)     + ';' +
7.02  * Deb.konto
7.02 c                                     %char(ffdkto)     + ';' +
7.02  * Kredkonto
7.02 c                                     %char(ffckto)     + ';' +
7.02  * DBTmC1
7.02 c                                     ''                + ';' +
7.02  * CrTmC1
7.02 c                                     ''                + ';' +
7.02  * Deb.mva kode
7.02 c                                     %trim(f1momd)     + ';' +
7.02  *
7.02  * Kredit mva kode
7.02 c                                     %trim(f1momc)     + ';' +
7.02  * DBAcC1
7.02 c                                     ''                + ';' +
7.02  * CrAcC1
7.02 c                                     ''                + ';' +
7.02  * R1  Avdeling??
7.02 c                                     %trim(%char(f1avde)) + ';' +
7.02  * R2  Prosjekt??
7.02 c                                     %trim(ffproj)     + ';' +
7.02  * Fakturanr (Bilagsnummer)
7.02 c                                     %trim(w_biln)     + ';' +
7.02  *
7.02  * AgRef  ??
7.02 c                                     ''                + ';' +
7.02  * Forfallsdato
7.02 c                                     %trim(w_for08)    + ';' +
7.02  * Kid nr ???????
7.02 c                                     %trim(t1kkid)
      *
     c                   write     rv01pfr
6.23  *
6.23 c                   eval      t1kkid = *blanks
      *
6.22 c     slutt         tag
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = fffirm
     c                   eval      w_binr = ffbiln
     c                   move      d_aa          w_aarr
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
6.20  *
6.20  * Henter rutine fra ordretype
6.20  *
6.20 c                   eval      b_kkid = *off
6.20 c                   eval      votyl1_otyp = sootyp
6.20 c     votyl1_key    chain     votyl1
6.20 c                   if        %found(votyl1)   and
6.20 c                             vaorut <> *blank
6.20 c                   eval      aforl1_ruti = vaorut
6.20 c     aforl1_key    chain     aforl1
6.20 c                   if        %found(aforl1) and
6.20 c                             afkidk = 1
6.20 c                   movel     sokidd        t1kkid
6.20 c                   else
6.20 c                   if        afkidk = 2
6.20 c                   movel     sokidr        t1kkid
6.20 c                   else
6.20 c                   if        afkidk = 3
6.20 c                   movel     sokidr        w_kidr
6.20 c                   movel     sokidr        w_kidr
6.20 c                   call      'AK710R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    w_kidr
6.20 c                   parm                    w_kidd
6.20 c                   parm                    w_kid2
6.20 c                   movel     w_kid2        t1kkid
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
