     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV772R
      * Beskrivelse . . : Vare, sett varetype til S etter telleresultatr
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030622 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fltell2    if   e           k disk    rename(ltelpfr:ltell2r)
      *
     fvv772d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d b_first         s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_numm          s                   like(lenumm)
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
     c                   if        *inkd = *on
     c                   call      'LC501R'
     c                   parm                    w_firm
     c                   parm                    b1batc
     c                   goto      b1taga
     c                   endif
      *
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
     c                   if        b_first = *off
     c                   eval      b_first = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller oppdateringsprogram
      *
     c                   call      'VV773C'
     c                   parm                    w_firm
     c                   parm                    b1batc
     c                   parm                    b1numm
     c                   parm                    b1oppd
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1batc = *blank
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_sjekk
     c                   endif
      * finn tellenummer
9.01 c/exec sql
9.01 c+                  SELECT lenumm
9.01 c+                  into :w_numm
9.01 c+                  FROM ltelpf
9.01 c+                  where lefirm = :w_firm and
9.01 c+                        lebatc = :b1batc
9.01 c+                  fetch first 1 rows only
9.01 c/end-exec
      *
     c                   if        w_numm = 0
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1numm = w_numm
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
