     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT025R
      * Beskrivelse . . : Leser prosjekter og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250816 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d rp1pl1_proj     s                   like(rp1pro)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kode                        6
     d  d_besk                       30
     d  d_kund                        6
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle poster på rp1pPF
      *
     c                   eval      rp1pl1_proj = *blank
     c     rp1pl1_key    setll     rp1pl1
      *
     c                   read      rp1pl1
     c                   dow       not %eof and
     c                             rp1frm = w_firm
      *
     c                   eval      d_kode = rp1pro
     c                   eval      d_besk = rp1txt
     c                   eval      d_kund = %triml(%editw(rp1knr : '     0'))
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      rp1pl1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     rp1pl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl1_proj
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportProsjekt'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT025R'
      *
     c                   endsr
