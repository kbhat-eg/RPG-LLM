     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LS100R
      * Beskrivelse . . : Registrering av lagertransaksjonslinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.50 221106 KOB  Lagt inn årsakskode ved registrering av transer
      * V5.50 290307 KOB  Lagt inn opphenting av kostpris
5.70  * PRGR 021208  bof  Utvidet med prisgruppe
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10 291110  bof  Sjekker om varetype = L
6.12  * 6.10 110211  bof  Hvis justering, godta 0 i antall
6.13  * 6.10 100812  bof  vedr. 6.12, to enter for å godta 0 i antall
6.21  * 6.20 190511  bof  Utvidet vl710r med div. info
6.nu  * 6.30 040614  bof  Sjekket mtp. nummerutvidelse
6.31  * 6.30 190417  bof  Cursor skal plassere seg i varenr etter enter
6.32  * 6.30 200617  bsa  Justert cursonhåndtering pga avvik i jacada
6.33  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   160622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fllhelr    if   e           k disk    rename(llhepfr:llhelrr)
     flldtlr    if   e           k disk    rename(lldtpfr:lldtlrr)
     flldtl1    if   e           k disk    rename(lldtpfr:lldtl1r)
     flldtl2    if   e           k disk    rename(lldtpfr:lldtl2r)
     flldtlu    uf a e           k disk    rename(lldtpfr:lldtlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
6.33 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fls100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av variabler i parametre
      *
     d p_numm          s                   like(lsnumm)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d llhelr_numm     s                   like(lqnumm)
     d lldtlr_numm     s                   like(lsnumm)
     d lldtlr_line     s                   like(lsline)
     d lldtl1_numm     s                   like(lsnumm)
     d lldtl1_line     s                   like(lsline)
     d lldtl2_numm     s                   like(lsnumm)
     d lldtl2_vare     s                   like(lsvare)
     d lldtlu_numm     s                   like(lsnumm)
     d lldtlu_line     s                   like(lsline)
     d vvarl1_vare     s                   like(vvvare)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(lsfirm)
     d w_numm          s                   like(lsnumm)
     d w_slin          s                   like(lsline)
     d w_vare          s                   like(lsvare)
     d w_vtxt          s                   like(lstek1)
     d w_lage          s                   like(lslage)
     d w_enh1          s                   like(lsenh1)
     d w_etxt          s             35
     d w_ltxt          s             20
     d w_stat          s              1
     d w_xrko          s              2
     d w_xtxt          s             20
      *
5.50 d w_lety          s              1  0
5.50 d w_dato          s               d   datfmt(*iso)
5.50 d w_time          s               t   timfmt(*iso)
5.50 d w_inlr          s              1
5.50 d w_enhe          s                   like(lsenh1)
5.50 d w_sapr          s              9  2
5.50 d w_bupr          s              9  2
5.50 d w_tipr          s              9  2
5.50 d w_kopr          s              9  2
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
6.11 d w_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.33 d w_lv_nobb       s                   like(vvlnob)
6.33 d w_lv_modn       s                   like(vvlmod)
6.33 d w_lv_lldo       s                   like(vvlnle)
6.33 d w_lv_llva       s                   like(vvllva)
6.13 d b_fors          s              1    inz(*off)
      *
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2WIN  - C2 Vindu for registrering/endring av linjer
      * B4WIN  - B4 Vindu for slette
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oppr = *on
     c                   exsr      xc2win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2line <> 0 or
     c                             b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = ' '
     c     lldtl1_key    setll     lldtl1
     c                   when      w_seqe = 'V'
     c     lldtl2_key    setll     lldtl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på linje
      *
     c                   if        b2line <> 0
     c                   eval      w_seqe = ' '
     c                   eval      lldtl1_line = b2line
     c     lldtl1_key    setll     lldtl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      lldtl2_vare = b2vare
     c     lldtl2_key    setll     lldtl2
     c                   goto      end_pos
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c     end_pos       tag
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2line = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      b_oppr = *off
     c                   exsr      xc2win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      b4vare = b1vare
     c                   eval      b4tek1 = b1tek1
     c                   eval      lldtlu_line = b1line
     c                   exsr      xb4win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endring/opprett
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
     c     c2taga        tag
      *
      * Klargjør bilde for ny linje
      *
     c                   if        b_oppr = *on
     c                   eval      c2vare = *blank
     c                   eval      c2tek1 = *blank
     c                   eval      c2anta = 0
     c                   eval      c2enh1 = *blank
     c                   eval      c2type = lqtype
     c                   move      lqlage        c2lage
     c                   move      lqlast        c2last
     c                   eval      c2xrko = lqxrko
     c                   eval      c2ltek = *blank
     c                   eval      c2latk = *blank
     c                   eval      c2xtxt = *blank
      *
      * Vedlikehold eksisterende linje
      *
     c                   else
      *
     c                   eval      lldtlr_line = b1line
     c     lldtlr_key    chain     lldtlr
     c                   eval      c2vare = lsvare
     c                   eval      c2tek1 = lstek1
     c                   eval      c2anta = lsanta
     c                   eval      c2enh1 = lsenh1
     c                   eval      c2type = lstype
      *
     c                   move      lslage        c2lage
     c                   move      lslast        c2last
     c                   move      lslage        w_lage
     c                   exsr      sjekk_lager
     c                   eval      c2ltek = w_ltxt
     c                   move      lslast        w_lage
     c                   exsr      sjekk_lager
     c                   eval      c2latk = w_ltxt
      *
     c                   eval      w_xrko = lsxrko
     c                   exsr      sjekk_Årsak
     c                   eval      c2xrko = w_xrko
     c                   eval      c2xtxt = w_xtxt
     c                   endif
      *
     c                   eval      *in30 = *on
     c                   eval      *in32 = *off
6.31 c                   eval      *in50 = *on
     c                   eval      *in33 = *off
     c                   eval      *in38 = *off
     c                   eval      *in39 = *off
6-13 c                   eval      b_fors = *off
      *
     c                   if        lqtype <> *blank
     c                   eval      *in38 = *on
     c                   endif
      *
     c                   if        lqtype <> 'O' and
     c                             lqtype <> *blank
     c                   eval      *in39 = *on
     c                   endif
      *
     c     c2tagb        tag
     c                   eval      b_feil = *off
     c                   exfmt     c2win
     c                   eval      *in30 = *off
     c                   eval      *in32 = *off
6.31 c                   eval      *in50 = *off
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   goto      c2tagb
     c                   endif
      *
      * Avslutt
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on or
     c                             c2vare = *blank
     c                   goto      end_c2win
     c                   endif
      *
      * Hent vareinformasjon
      *
     c                   movel     c2vare        w_vare
     c                   exsr      les_vare
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Rediger input på lager fra alfa til numerisk
      *
     c                   if        %len(%trim(c2lage)) = 1
     c                   eval      c2lage = '0' + %trim(c2lage)
     c                   endif
     c                   if        %len(%trim(c2last)) = 1
     c                   eval      c2last = '0' + %trim(c2last)
     c                   endif
      *
      * Er gyldige lager angitt?
      *
     c                   move      c2lage        w_lage
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in36 = *on
     c                   goto      c2tagb
     c                   endif
6.11  *
6.11 c                   call      'VL710R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_vare
6.11 c                   parm                    w_lage
6.11 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.33 c                   parm                    w_lv_nobb
6.33 c                   parm                    w_lv_modn
6.33 c                   parm                    w_lv_lldo
6.33 c                   parm                    w_lv_llva
6.11 c                   if        w_vtyp <> 'L'
6.11 c                   eval      *in40 = *on
6.11 c                   goto      c2tagb
6.11 c                   endif
      *
     c                   eval      c2ltek = w_ltxt
      *
     c                   if        c2type = 'O '
     c                   move      c2last        w_lage
     c                   exsr      sjekk_lager
      *
     c                   if        b_feil = *on
     c                   eval      *in37 = *on
     c                   goto      c2tagb
     c                   endif
6.11  *
6.11 c                   call      'VL710R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_vare
6.11 c                   parm                    w_lage
6.11 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.33 c                   parm                    w_lv_nobb
6.33 c                   parm                    w_lv_modn
6.33 c                   parm                    w_lv_lldo
6.33 c                   parm                    w_lv_llva
6.11 c                   if        w_vtyp <> 'L'
6.11 c                   eval      *in41 = *on
6.11 c                   goto      c2tagb
6.11 c                   endif
      *
     c                   eval      c2latk = w_ltxt
     c                   else
     c                   eval      c2latk = *blank
     c                   endif
      *
      * Er antall utfylt?
      *
6.12 c                   if        lqtype <> 'J'
6.32 c                   if        c2anta = 0 and
6.32 c                             c2vare <> *blanks
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
6.12 c                   endif
      *
6.13 c                   if        lqtype =  'J'
6.13 c                   if        c2anta = 0 and
6.32 c                             c2vare <> *blanks and
6.13 c                             b_fors = *off
6.13 c                   eval      *in32 = *on
6.13 c                   eval      b_fors = *on
6.13 c                   goto      c2tagb
6.13 c                   endif
6.13 c                   endif
      *
      * Er det gyldig enhet?
      *
     c                   exsr      les_vare_enh
     c                   if        b_feil = *on
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Er det riktig behandlingskode?
      *
     c                   if        c2type <> 'I ' and
     c                             c2type <> 'U ' and
     c                             c2type <> 'J ' and
     c                             c2type <> 'O '
     c                   eval      *in34 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Er det gyldig til lager?
      *
     c                   if        c2type = 'O '
     c                   if        c2last = *blank or
     c                             c2last = c2lage
     c                   eval      *in35 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Er gyldige årsakskode angitt?
      *
     c                   if        c2xrko <> ' '
     c                   eval      w_xrko = c2xrko
     c                   exsr      sjekk_Årsak
      *
     c                   if        b_feil = *on
     c                   eval      *in40 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      c2xtxt = w_xtxt
     c                   else
     c                   eval      w_xrko = lqxrko
     c                   exsr      sjekk_Årsak
     c                   eval      c2xrko = w_xrko
     c                   eval      c2xtxt = w_xtxt
     c                   endif
5.50  *
5.50  * Hent priser
5.50  *
5.50 c                   eval      w_sapr = 0
5.50 c                   eval      w_bupr = 0
5.50 c                   eval      w_tipr = 0
5.50 c                   eval      w_kopr = 0
5.50 c                   eval      w_lety = 0
5.50 c                   eval      w_inlr = '1'
5.50 c                   time                    w_dato
5.50 c                   time                    w_time
5.50  *
5.50 c                   eval      w_enhe = c2enh1
5.70 c                   exsr      hent_prisgr
5.50  *
5.50 c                   call      'VP700R'
5.50 c                   parm                    w_firm
5.50 c                   parm                    w_vare
5.50 c                   parm                    w_prgr
5.50 c                   parm                    w_enhe
5.50 c                   parm                    w_lety
5.50 c                   parm                    w_dato
5.50 c                   parm                    w_time
5.50 c                   parm                    w_inlr
5.50 c                   parm                    w_sapr
5.50 c                   parm                    w_bupr
5.50 c                   parm                    w_tipr
5.50 c                   parm                    w_kopr
      *
      *  Legger inn ny linje i bunt
      *
     c                   if        b_oppr = *on
     c                   eval      lldtlu_line = w_slin + 1
     c                   else
     c                   eval      lldtlu_line = b1line
     c                   endif
      *
     c     lldtlu_key    chain     lldtlur
      *
     c                   eval      lstype = c2type
     c                   movel     c2vare        lsvare
     c                   move      c2lage        lslage
     c                   move      c2last        lslast
     c                   eval      lsanta = c2anta
     c                   eval      lsenh1 = c2enh1
     c                   eval      lstek1 = vvtek1
5.50  *
5.50 c                   eval      lskpny = w_kopr
      *
     c                   eval      lsxrko = w_xrko
      *
     c                   if        not %found
     c                   eval      w_slin = w_slin + 1
     c                   eval      lsfirm = w_firm
     c                   eval      lsnumm = w_numm
     c                   eval      lsline = w_slin
6.10 c                   time                    lsodat
6.10 c                   time                    lsotim
6.10 c                   eval      lsousr = l_user
6.10 c                   time                    lsedat
6.10 c                   time                    lsetim
6.10 c                   eval      lseusr = l_user
     c                   write     lldtlur
     c                   else
6.10 c                   time                    lsedat
6.10 c                   time                    lsetim
6.10 c                   eval      lseusr = l_user
     c                   update    lldtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   if        b_oppr = *on
6.31 c                   eval      *in32  = *off
6.31 c                   eval      *in50  = *on
     c                   goto      c2taga
     c                   endif
      *
     c     end_c2win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb4win        begsr
      *
     c                   exfmt     b4win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_b4win
     c                   endif
      *
      *  Sletter valgt linje i bunt
      *
     c     lldtlu_key    chain     lldtlur
     c                   if        %found
     c                   delete    lldtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_b4win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      b2bunt = w_numm
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   read      lldtl1                                 15
     c                   when      w_seqe = 'V'
     c                   read      lldtl2                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        lsfirm <> w_firm or
     c                             lsnumm <> w_numm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = lsline
     c                   movel     lsvare        b1vare
     c                   eval      b1type = lstype
     c                   eval      b1lage = lslage
     c                   eval      b1last = lslast
     c                   eval(h)   b1anta = lsanta
     c                   eval      b1enh1 = lsenh1
     c                   eval      b1tek1 = lstek1
     c                   eval      b1xrko = lsxrko
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   if        c2tek1 = *blank
     c                   eval      c2tek1 = vvtek1
     c                   endif
      *
     c                   if        c2enh1 = *blank
     c                   eval      c2enh1 = vvenhl
     c                   endif
     c                   else
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring i vareregister
      *
     c                   if        w_actf = 'B2VARE'
     c                   eval      w_vare = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b2vare
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'C2VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
      *
     c                   eval      c2vare = w_vare
     c                   eval      c2tek1 = *blank
     c                   eval      c2enh1 = *blank
      *
     c                   exsr      les_vare
     c                   eval      *in32 = *on
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring på enhet
      *
     c                   if        w_actf = 'C2ENH1'
     c                   movel     c2vare        w_vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c2enh1
     c                   parm                    w_etxt
     c                   goto      end_sp
     c                   endif
      *
      * Spørring på lager
      *
     c                   if        w_actf = 'C2LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    c2lage
     c                   parm                    c2ltek
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'C2LAST'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    c2last
     c                   parm                    c2latk
     c                   goto      end_sp
     c                   endif
      *
      * Spørring på årsakskode
      *
     c                   if        w_actf = 'C2XRKO'
     c                   call      'RA524R'
     c                   parm                    w_firm
     c                   parm                    c2xrko
     c                   parm                    c2xtxt
     c                   goto      end_sp
     c                   endif
      *
     c     end_sp        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      w_ltxt = *blank
      *
     c                   call      'RS710R'
     c                   parm                    w_stat
     c                   parm                    w_lage
     c                   parm                    w_ltxt
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av årsakskode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_Årsak   begsr
      *
     c                   eval      w_xtxt = *blank
      *
     c                   call      'RS724R'
     c                   parm                    w_stat
     c                   parm                    w_xrko
     c                   parm                    w_xtxt
      *
     c                   if        w_stat <> *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av vare/enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare_enh  begsr
      *
     c                   eval      b_feil = *off
     c                   movel     c2vare        vvenlr_vare
     c                   eval      vvenlr_enhe = c2enh1
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Parameter
      *
     c     *entry        plist
     c                   parm                    p_numm
      *
      *  Key for les av BUNT-HODE
      *
     c     llhelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llhelr_numm
      *
      *  Key for les av TRANSAKSJONSLINJE
      *
     c     lldtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtlr_numm
     c                   kfld                    lldtlr_line
      *
      *  Key for les av TRANSAKSJONSLINJE - linjenummersortert
      *
     c     lldtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtl1_numm
     c                   kfld                    lldtl1_line
      *
      *  Key for les av TRANSAKSJONSLINJE - varenummersortert
      *
     c     lldtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtl2_numm
     c                   kfld                    lldtl2_vare
      *
      *  Key for oppdatering av TRANSAKSJONSLINJE
      *
     c     lldtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtlu_numm
     c                   kfld                    lldtlu_line
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_numm = p_numm
      *
      * Les bunthode
      *
     c                   eval      llhelr_numm = w_numm
     c     llhelr_key    chain     llhelr
      *
      * Finn høyest brukte linjenummer
      *
     c                   eval      lldtl1_numm = w_numm
     c                   eval      lldtl1_line = 9999
     c     lldtl1_key    setll     lldtl1
     c                   readp     lldtl1
     c                   if        %eof or
     c                             lsnumm <> w_numm
     c                   eval      w_slin = 0
     c                   else
     c                   eval      w_slin = lsline
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lldtlr_numm = w_numm
     c                   eval      lldtl1_numm = w_numm
     c                   eval      lldtl2_numm = w_numm
     c                   eval      lldtlu_numm = w_numm
     c                   eval      lldtl1_line = 0
     c                   eval      lldtl2_vare = *blank
      *
     c     lldtl1_key    setll     lldtl1
      *
     c                   eval      w_seqe = ' '
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
