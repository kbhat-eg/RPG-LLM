     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR701R
      * Beskrivelse . . : Håndholdt trans, danne bestillingsforslag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220108 bof  Nytt program
6.10  * 6.10  141010 bsa  Må ta høyde for prisgruppe
6.20  * 6.20  151113 bsa  Bruker innkjøpspris og ikke kostpris ved ut-
6.20  *                   legg av verdi på bestillingsforslag.
6.23  * 6.20  151113 bsa  Bruker innkjøpspris og ikke kostpris ved ut-
6.23  *                   legg av verdi på bestillingsforslag.
6.24  * 6.20  130614 bkv  Innkjøpspris: bruker VP753R i stedet for VP750R
6.31  * 6.30  021216 bof  Sjekk om overst. leverandør på lager.
6.32  * 6.30  121018 bof  Utvidet med trelast-sortiment
6.33  * 6.30  251119 bof  Må ha med omregningsfaktor ved beregning stat 5
      *
7.01  * 7.00  141212 nho  Legger inn saksbehandler fra leverandøren
7.01  *                   i stedet for fra ordre
      *
8.01  * 800   080621 ask  Lagt inn fjerning av statuskode 6 om bestilling er til ordre
8.01  *                   Styrt av switch 'BESTF_STATUS6'
8.02  * 800   070721 bof  feilretting vedr. vekt
8.03  * 800   090222 ask  Ny opprinnelseskode lagt inn i LWHBPF, oppdaterer bestillingsforslag
8.04  * 800   250222 ask  Henter ordretype fra LLDIPF dersom det er intern bestilling.
8.05  * 800   100322 ask  Endrer statuskode fra " " til "0" på 8.01
8.06  * 800   150622 bsa  Endring 7.01 er speial fra Skattum. Ikke alle kunder ønsker dette,
8.06  * 800               derfor gjøres denne switchstyrt.
8.07  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwhbl1    if   e           k disk    rename(lwhbpfr:lwhbl1r)
     flwhblu    uf a e           k disk    rename(lwhbpfr:lwhblur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.33 fvvenl4    if   e           k disk    rename(vvenpfr:vvenl4r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flfhelu    uf a e           k disk    rename(lfhepfr:lfhelur)
     flfdtlu    uf a e           k disk    rename(lfdtpfr:lfdtlur)
      *
      *
      * Definering av parametre
      *
     d p_time          s                   like(mytime)
     d p_stat          s              1
     d p_numm          s                   like(lhnumm)
     d p_fnum          s                   like(lhnumm)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
8.01 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
     d  d_fast                        3  0 overlay(d_e128)
     d  d_eann                       13  0 overlay(d_e128:4)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
6.33 d vvenl4_vare     s                   like(vevare)
6.33 d vvenl4_inen     s                   like(veinen)
6.33 d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d lodtlv_vare     s                   like(ldvare)
     d lfhelu_numm     s                   like(lhnumm)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d rlevl1_levr     s                   like(rllevr)
     d votylr_otyp     s                   like(vaotyp)
     d lwhbl1_kode     s                   like(mykode)
     d lwhblu_kode     s                   like(mykode)
     d lwhblu_line     s                   like(myline)
      *
      * Definering av variable
     d b_forst         s              1    inz(*off)
      *
      *
     d w_bmen          s                   like(vlbmen)
     d w_bmin          s                   like(vlbmin)
     d w_stat          s                   like(lfstat)
     d w_hnum          s                   like(lhnumm)
     d w_112           s             11  2
     d w_tbel          s                   like(lhtbel)
     d w_tvkt          s             11  3
     d w_xenhe         s                   like(myenhe)
6.24 d w_enh2          s                   like(myenhe)
6.nu d w_numm          s              8  0
     d w_firm          s                   like(myfirm)
     d w_dato          s                   like(mybdat)
     d w_time          s                   like(mytime)
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_kod2          s              1
     d w_bdat          s                   like(mybdat)
     d w_timL          s               t   timfmt(*iso)
8.02 d w_vekt          s                   like(vevekt)
8.02 d w_vek1          s                   like(vevekt)
8.02 d w_vek2          s                   like(vevekt)
8.02 d w_vek3          s                   like(vevekt)
      *
     d w_lety          s              1  0
     d w_kopr          s              9  2
     d w_inpr          s              9  2
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_inlr          s              1    inz(*off)
     d w_ldor          s                   like(lhldor)
     d w_lsor          s                   like(lhlsor)
8.07 d w_prgr          s              2
6.20 d w_kost          s              9  2
6.20 d w_kamp          s              5
6.32 d w_lv_lvar       s                   like(vvlvar)
6.33 d w_enhe          s                   like(veenhe)
6.33 d w_enh1x         s                   like(veenhe)
6.33 d w_enh2x         s                   like(veenhe)
6.33 d w_enh3x         s                   like(veenhe)
6.33 d w_omr1          s                   like(veomre)
6.33 d w_omr2          s                   like(veomre)
6.33 d w_omr3          s                   like(veomre)
6.33 d w_anta          s                   like(myanta)
8.04 d w_otyp          s              2
8.04 d w_form          s             10
6.33  *
8.01 d u_801           s              1    inz(*off)
8.06 d u_selg          s              1    inz(*off)
8.01  * Definering av eksterne prg
8.01  *
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   DCL-PR CO402R EXTPGM('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
8.01     p_verdi2            char(2048);
8.01   END-PR;

6.32 C/Exec SQL
6.32 C+  Set Option DatFmt=*ISO
6.32 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = ' '
      *
     c                   exsr      opprett
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      p_stat = '1'
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av bestilling i bestillingsforlag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c     lwhbl1_key    setll     lwhbl1
     c     lwhbl1_key    reade     lwhbl1
     c                   dow       not %eof(lwhbl1)
      *
      *
      * Oppretter bestillingsforslag-hode
      *
     c                   if        b_forst = *off
      *
      * Hent opplysninger fra lager status-koder
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   exsr      hent_bnumm
     c                   eval      p_fnum = w_numm
      *
     c                   eval      lhfirm = myfirm
     c                   eval      lhnumm = w_numm
8.04 c*                  eval      lhotyp = 'B '
8.04 c                   exsr      finn_otyp
     c                   eval      lhlage = mylage
     c                   eval      lhavde = myavde
V5.60c                   eval      lhselg = myselg
     c                   eval      lhbdat = mybdat
     c                   eval      lhldat = myldat
     c                   eval      lhwsid = myeusr
     c                   eval      lhuser = myeusr
8.03 c                   if        myoppr <> ' '
8.03 c                   eval      lhoppr = myoppr
8.03 c                   else
     c                   eval      lhoppr = 'R'
8.03 c                   endif
     c                   if        myldor <> 0
     c                   eval      lhldor = myldor
     c                   eval      rlevl1_levr = myldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found(rlevl1)
     c                   movel     rlalfa        lhlsor
8.06 c                   if        u_selg = *off
7.01 c                   eval      lhselg = rlsaks
8.06 c                   endif
     c                   endif
7.01 c                   else
7.01 c                   eval      lhselg = myselg
     c                   endif
      *
     c                   time                    lhdate
     c                   time                    lhtime
      *
     c                   if        lhbdat = *loval
     c                   time                    lhbdat
     c                   endif
      *
     c                   write     lfhelur
      *
     c                   eval      b_forst = *on
      *
     c                   endif
      *
      * Flytter bestilling fra radioterminal til bestillingsforslag-linje
      *
     c                   eval      vvarl1_vare = *blank
     c                   movel     myvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
      *
6.32  * sjekker om logistikk-vare
6.32 c                   eval      w_lv_lvar = *blank
6.32 c/exec sql
6.32 c+ select vvllva
6.32 c+ into   :w_lv_lvar
6.32 c+ from   vvlepf
6.32 c+ where  vvlvnr = :myvare and
6.32 c+        vvlldo = :myldor
6.32 c+ fetch first 1 rows only
6.32 c/end-exec

     c                   eval      vvenl1_vare = *blank
     c                   movel     myvare        vvenl1_vare
     c                   eval      vvenl1_enhe = myenhe
     c     vvenl1_key    chain     vvenl1
      *
6.33 c                   exsr      finn_enhet
      *
     c                   exsr      finn_pris
      *
     c                   eval      lffirm = myfirm
     c                   eval      lfnumm = w_numm
     c                   eval      lfline = myline
     c                   eval      lfltyp = *blank
     c                   eval      lfldor = 0
     c                   eval      lflage = mylage
     c                   eval      lfogrp = vvogrp
     c                   eval      lfhgrp = vvhgrp
     c                   eval      lfugrp = vvugrp
     c                   eval      lflvar = *blank
     c                   eval      lfvare = vvvare
     c                   eval      lfenh1 = myenhe
     c                   eval      lftek1 = vvtek1
     c                   eval      lftek2 = vvtek2
     c                   eval      lfanta = myanta
8.02 c*                  eval      lfvekt = vevekt
8.02 c                   eval      lfvekt = w_vekt
     c                   eval      lflbel = w_kopr
     c                   exsr      finn_varelag
     c                   exsr      sjekk_status
     c                   if        laabst = 1
     c                   eval      lfstat = w_stat
     c                   else
     c                   eval      lfstat = '0'
     c                   endif
      *
6.32 c                   if        w_lv_lvar <> *blank
6.32 c                   eval      lflvar = w_lv_lvar
6.32 c                   else
6.31 c                   eval      lflvar = vvlvar
6.32 c                   endif
6.31 c                   if        myldor = 0
6.31 c                   if        vlbldo <> 0
6.31 c                   eval      lfldor = vlbldo
6.31 c                   eval      lfvlev = vlbldo
6.31 c                   else
     c                   eval      lfldor = vvldor
6.31 c                   eval      lfvlev = vvldor
6.31 c                   endif
     c                   else
     c                   eval      lfldor = myldor
6.31 c                   eval      lfvlev = myldor
     c                   endif
      *
     c                   write     lfdtlur
      *
     c                   eval      lwhblu_line = myline
     c     lwhblu_key    chain     lwhblu
     c                   if        %found(lwhblu)
     c                   delete    lwhblur
     c                   endif
      *
     c     lwhbl1_key    reade     lwhbl1r                                90
     c                   enddo
      *
      * finner status for heading - oppdaterer
      *
     c                   eval      lfhelu_numm = w_numm
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
     c                   eval      w_hnum = w_numm
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   eval      w_stat = *blank
     c                   call      'LH714R'
     c                   parm                    w_hnum
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhtbel = w_tbel
     c                   eval      lhstat = w_stat
     c                   update    lfhelur
     c                   endif
      *
      * Hvis leverandør ikke er bestemt fra rad.term.,så sjekkes det om
      * bestillingsforslag må splittes på flere leverandører.
      *
     c                   if        myldor = 0
     c                   eval      p_numm = w_numm
     c                   call      'HR112R'
     c                   parm                    myfirm
     c                   parm                    p_numm
     c                   endif
      *
      * Viser avslutingsbildet
      *
     c                   eval      p_stat = '1'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finne prisen på en vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      *
     c                   eval      w_lety = 0
6.24 c                   eval      w_enh2 = myenhe
     c                   move      w_time        w_dato
     c                   move      w_time        w_timL
     c*                  time                    w_dato
     c*                  time                    w_timL
     c                   eval      w_inlr = *off
      *
     c                   call      'VP700R'
     c                   parm                    myfirm
     c                   parm                    vvvare
6.10 c                   parm                    w_prgr
6.24 c                   parm                    w_enh2
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_timL
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
6.23  *
6.23  * Henter innkjøpspris, slik at den benyttes i verdiberegning
6.23  *
6.23 c                   eval      w_kost = *zeros
6.23 c                   eval      w_inpr = *zeros
6.23 c                   eval      w_kamp = *blanks
6.24 c                   eval      w_enh2 = myenhe
6.23  *
6.23 c                   call      'VP753R'
6.23 c                   parm                    w_firm
6.23 c                   parm                    vvvare
6.23 c                   parm                    w_prgr
6.24 c                   parm                    w_enh2
6.24 c                   parm                    w_lety
6.23 c                   parm                    w_dato
6.23 c                   parm                    w_kost
6.23 c                   parm                    w_inpr
6.23 c                   parm                    w_kamp
      * Hvis innkjøpspris finnes brukes denne, hvis ikke kost
6.23 c                   if        w_inpr <> 0
6.23 c                   eval      w_kopr = w_inpr
6.23 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av vare/lager informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_varelag  begsr
      *
     c                   eval      w_bmin = 0
     c                   eval      w_bmen = 0
      *
     c                   eval      vlagl1_vare = lfvare
     c                   eval      vlagl1_lage = lflage
      *
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found(vlagl1)
     c                   leavesr
     c                   endif
      *
      * Verdier fra lager eller vare?
      * fom. v 5.50 tas dette kun fra lager
      *
     c                   eval      w_bmen = vlbmen
     c                   eval      w_bmin = vlbmin
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker mulige statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_status  begsr
      *
      * Her sjekkes det for status 4 - 6.
      *
     c                   eval      w_stat = '0'
     c                   if        w_bmen <> 0 and
     c                             myanta <> w_bmen
     c                   eval      w_stat = '4'
     c                   endif
      *
      * beregner antall i forhold til lagerenhet
     c                   if        w_bmin <> 0
6.33 c                   eval      w_112  = w_anta / w_bmin
     c                   move      w_112         w_20              2 0
     c                   if        w_20 > 0
     c                   eval      w_stat = '5'
     c                   endif
     c                   endif
      *
      * Sjekk mot bestilling /restleveranse
      *
     c                   exsr      sjk_best
      *
      * Sjekk om det er en cross-docking vare
      *
     c                   if        vvcrdo = 1
     c                   eval      w_stat = '7'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_best      begsr
      *
readec                   eval      lodtlv_vare = vvvare
readec     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
     c                   dow       not %eof(lodtlv)
     c                   if        ldanta > 0 and
     c                             ldlage = lhlage
      *
     c                   eval      lohelr_numm = ldnumm
     c                   eval      lohelr_suff = ldsuff
     c     lohelr_key    chain     lohelr
     c                   if        %found(lohelr)
     c                   eval      votylr_otyp = lootyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaolag = 1  and
     c                             vaoakk = 1
     c                   eval      w_stat = '6'
8.01 c                   goto      sjk_u801
     c                   endif
     c                   endif
     c                   endif
     c     lodtlv_key    reade     lodtlv
     c                   enddo
8.01  *
8.01  * Sjekk om switch for overstyring av kode 6 er satt
8.01  *
8.01 c     sjk_u801      tag
8.01 c                   if        u_801 = *on and
8.01 c                             ldornu <> 0
8.05 c                   eval      w_stat = '0'
8.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente neste best.forslagsnr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_bnumm    begsr
      *
      * Henter neste bestillingsforslag-nummer
      *
     c                   eval      w_kod2 = '0'
     c                   eval      w_fell = laafor
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
     c                   call      'AS100R'
     c                   parm                    w_kod2
     c                   parm                    myfirm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      *
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Rutine for finne enheter og omregningsfaktorer
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33
6.33 c     finn_enhet    begsr
6.33  *
6.33 c                   eval      w_anta = 0
8.02 c                   eval      w_vek1 = 0
8.02 c                   eval      w_vek2 = 0
8.02 c                   eval      w_vek3 = 0
8.02 c                   eval      w_vekt = 0
6.33  * Henter tre salgsenheter fra varen
6.33  *
6.33 c                   eval      w_enh1x = *blank
6.33 c                   eval      w_enh2x = *blank
6.33 c                   eval      w_enh3x = *blank
6.33 c                   eval      w_omr1 = 0
6.33 c                   eval      w_omr2 = 0
6.33 c                   eval      w_omr3 = 0
6.33  *
6.33 c                   eval      vvenl4_vare = vvvare
6.33 c                   eval      vvenl4_inen = 1
6.33 c     vvenl4_key    setll     vvenl4
6.33 c     vvenl4_key2   reade     vvenl4
6.33 c                   dow       not %eof
6.33  *
6.33 c                   select
6.33 c                   when      w_enh1x = *blank
6.33 c                   eval      w_enh1x = veenhe
6.33 c                   eval      w_omr1 = veomre
8.02 c                   eval      w_vek1 = vevekt
6.33 c                   when      w_enh1x <> *blank and
6.33 c                             w_enh2x = *blank
6.33 c                   eval      w_enh2x = veenhe
6.33 c                   eval      w_omr2 = veomre
8.02 c                   eval      w_vek2 = vevekt
6.33 c                   when      w_enh1x <> *blank and
6.33 c                             w_enh2x <> *blank and
6.33 c                             w_enh3x = *blank
6.33 c                   eval      w_enh3x = veenhe
6.33 c                   eval      w_omr3 = veomre
8.02 c                   eval      w_vek3 = vevekt
6.33 c                   leave
6.33 c                   endsl
6.33  *
6.33 c     vvenl4_key2   reade     vvenl4
6.33 c                   enddo
6.33  *
6.33 c                   if        w_enh1x <> myenhe and
6.33 c                             w_enh2x <> myenhe and
6.33 c                             w_enh3x <> myenhe
6.33 c                   eval      w_anta = myanta
6.33 c                   leavesr
6.33 c                   endif
6.33 c                   select
6.33 c                   when      myenhe = w_enh1x
6.33 c                   eval      w_anta = myanta
8.02 c                   eval      w_vekt = w_vek1
6.33 c                   when      myenhe = w_Enh2x
6.33 c                   eval      w_anta = myanta * w_omr2/ w_omr1
8.02 c                   eval      w_vekt = w_vek2
6.33 c                   when      myenhe = w_Enh3x
6.33 c                   eval      w_anta = myanta * w_omr3/ w_omr1
8.02 c                   eval      w_vekt = w_vek3
6.33 c                   endsl
6.33 c                   if        w_anta = 0
6.33 c                   eval      w_anta = myanta
6.33 c                   endif
6.33 c                   endsr
8.04  *
8.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  * Finner ordretype på internbestilling
8.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  *
8.04    begsr finn_otyp;
8.04       exec sql
8.04        select lpform, lpotyp
8.04         into   :w_form, :w_otyp
8.04        from   lldipf
8.04         where  lpfirm = :w_firm and
8.04                lpldor = :myldor and
8.04                lplage = :mylage;
8.04
8.04       if sqlcod = 0 and
8.04          w_form = 'INTERN';
8.04           lhotyp = w_otyp;
8.04         else;
8.04           lhotyp = 'B';
8.04       endif;
8.04    endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
610  c                   move      *on           *inlr
610  c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_time
     c                   parm                    p_fnum
     c                   parm                    p_stat
      *
      * Key for oppslag på radioterminal-trans
      *
     c     lwhbl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwhbl1_kode
     c                   kfld                    w_time
      *
      * Key for oppdatering av radioterminal-trans
      *
     c     lwhblu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwhblu_kode
     c                   kfld                    w_time
     c                   kfld                    lwhblu_line
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
6.33  *
6.33  * Key for oppslag på vare-enhet i innkjøpsenhet rekkefølge
6.33  *
6.33 c     vvenl4_key    klist
6.33 c                   kfld                    w_firm
6.33 c                   kfld                    vvenl4_vare
6.33 c                   kfld                    vvenl4_inen
6.33  *
6.33 c     vvenl4_key2   klist
6.33 c                   kfld                    w_firm
6.33 c                   kfld                    vvenl4_vare
      *
      * Key for oppslag på lager status-koder
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for les av bestillinger
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key for les av bestilling hode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      *  Key for les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *  Key for oppdatering av FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      * Henter firma og timestamp fra parameter
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_time = p_time
     c                   eval      lwhbl1_kode = 'B'
     c                   eval      lwhblu_kode = 'B'
      *
6.10  * Finner prisgruppe
6.10  *
6.10 c                   call      'VL711R  '
6.10 c                   parm                    w_prgr
      *
8.01  *
8.01  * Endring 8.01
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'BESTF_STATUS6':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_801 = *on;
8.01   endif;
8.06  *
8.06  * Endring 8.06
8.06  *
8.06   CallP CO402R(l_filg:w_firm:0:'HR701_806':
8.06                    co402_verdi1:co402_verdi2);
8.06   if co402_verdi1 = '1';
8.06     u_selg = *on;
8.06   endif;
     c                   endsr
