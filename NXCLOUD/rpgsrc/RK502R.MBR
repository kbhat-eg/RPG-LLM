     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK502R                                              *
      * Beskrivelse . . : Kunder, spørring                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.00 * V5.00     131202  Flere param. i call RS718R.                    HFL  *
T5.00 *                   Henter tekst til feltene i forsendelse-vindu.  HFL  *
T5.00 * V5.00     180803  Unngår decimat-data error på RKEDIT.           HFL  *
T5.30 * V5.30     040205  Lagt inn merke når notatblokk på kunden        HFL
T5.30 * V5.40     240405  Lagt inn vising av avtalespesifikasjon         KOB
T5.33 * V5.40     250505  Lagt inn visning av besøksadresse              KOB
T5.33 * V5.40     260505  Fjernet ubrukte koder i F2bildet               KOB
T5.33 * V5.40     260505  Fjernet ubrukte koder i H2bildet               KOB
T5.40 * V5.40     150605  Leser RKLAL2 i stedet for RKLALU.              HFL
T5.40 * V5.40     250705  Lagt inn tabell 2 vedr service kunder          HFL
T5.40 * V5.40     030805  Utvidet parameterliste for RF020R              HFL
T5.40 * V5.50     270606  Lagt ut opprettelsesinformasjon i F2-bildet    KOB
T5.50 * V5.50     050906  F-taster for å vise kontaktpersoner og kort.   HFL
T5.40 * V5.50     070307  Utvidet med utenlandsk postnr.                 KOB
T5.40 * V5.50     070307  Utvidet med kode for låst kreditgrense         KOB
6.20  * V6.20     250211  Avdelingstekst utvidet til 30 posisjoner(DDS)  BSA
6.21  * R6.20     030113  Notatblokk ble ikke vist                       nho  *
7.xx  * 7.00  051017 bkv  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fapospf    if   e           k disk
E5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
     frksal1    if   e           k disk    rename(rksapfr:rksal1r)
     frk502d    cf   e             workstn
      *
     d wpf             s              1    dim(30)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
      * LOCAL DATA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d apospf_ponr     s                   like(apponr)
E5.40d rklal2_syst     s                   like(rxsyst)
E5.40d rklal2_pros     s                   like(rxpros)
E5.40d rklal2_akti     s                   like(rxakti)
E5.40d rklal2_kund     s                   like(rxkont)
     d rksal1_kund     s                   like(rkkund)
      *
      * Parametre
      *
     d p_stat          s              1
     d p_bkod          s                   like(rkbetm)
     d p_ckod          s                   like(rkbetb)
     d p_fkod          s                   like(rkrabk)
     d p_gkod          s                   like(rkavde)
     d p_hkod          s                   like(rkdist)
     d p_ikod          s                   like(rkslgr)
     d p_jkod          s                   like(rklagr)
     d p_kkod          s                   like(rkkatg)
     d p_lkod          s                   like(rkland)
     d p_mkod          s                   like(rkmobo)
     d p_mtxt          s             30
     d p_pkod          s                   like(rkvalk)
     d p_qkod          s                   like(rkform)
T5.00d p_qtxt          s             20
     d p_rkod          s                   like(rklbet)
T5.00d p_rtxt          s             25
T5.00d p_tx2           s             25
T5.00d p_tx3           s             25
     d p_vkod          s                   like(rksped)
T5.00d p_vtxt          s             20
     d p_ukod          s                   like(rkkjØr)
     d p_yrut          s                   like(rkkjØr)
T5.00d p_ytxt          s             30
     d p_spav          s                   like(rkspav)
     d p_tx20          s             20
     d p_tx25          s             25
     d p_tx30          s             30
6.NU d p_biln          s              8  0
     d p_rest          s             11  2
5.41 d p_kall          s              1
5.41 d p_faty          s              2  0
5.41 d p_tbes          s             20
      *
      * Parameter - program RF020R
6.20 i/COPY QCPYLESRC,RF020PAR
6.20 d*                ds
6.20 d*rxulda                       175
6.20 d* rxstat                        1    overlay(rxulda)
6.20 d* rxfirm                        3  0 overlay(rxulda:2)
6.20 d* rxsyst                        1    overlay(rxulda:5)
6.20 d* rxpros                        6  0 overlay(rxulda:6)
6.20 d* rxakti                        3    overlay(rxulda:12)
6.20 d* rxf001                        3    overlay(rxulda:15)
6.20 d* rxkont                        6  0 overlay(rxulda:18)
6.20 d* rxspes                        4  0 overlay(rxulda:24)
6.20 d* rxavde                        4  0 overlay(rxulda:28)
6.20 d* rxlinj                        4  0 overlay(rxulda:32)
6.20 d* rxtgrp                        3    overlay(rxulda:36)
6.20 d* rxtext                       78    overlay(rxulda:39)
6.20 d* rxf002                       13    overlay(rxulda:117)
6.20 d* rxvalg                        1  0 overlay(rxulda:130)
6.20 d* rxtist                         z   overlay(rxulda:131)
6.20 d* rxbiln                        6  0 overlay(rxulda:158)
6.20 d* rxrper                        2  0 overlay(rxulda:164)
6.20 d* rxraar                        4  0 overlay(rxulda:166)
6.20 d* rxonr                         6  0 overlay(rxulda:170)
     d rxupar          s                   like(rxulda)
      *
     d wactli          s              2  0
     d wactpo          s              3  0
     d werror          s              1  0
     d whalfa          s                   like(rkalfa)
6.21 d wpakti          s              6
     d wpantp          s              2  0
     d wpavde          s              4  0
     d wpfunk          s              1  0
     d wpf001          s              3
     d wpkund          s                   like(rkkund)
     d wpponr          s                   like(rkponr)
6.21 d wppros          s              6
     d wpretk          s              1
E5.30d wpspes          s              4  0
     d wpsyst          s              1
     d wpvalg          s              1  0
     d wp4knr          s              6  0
     d wseqen          s              1
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
5.50 d w_levr          s              6  0 inz(0)
     d ww1bek          s                   like(ra1bek)
     d ww1bkk          s                   like(ra1bkk)
      *
     c     dummy         tag
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * C2BLD  - C2 BILDE for vise
      * F2WIN  - F2 Vindu for å vise OFAK-koder
      * E2WIN  - E2 Vindu for å vise besøkadresse
      * G2WIN  - G2 Vindu for å vise Fakturakunde/EAN-kunde
      * K2WIN  - H2 Vindu for å vise Autogiroinformasjon
      * I2WIN  - I2 Vindu for å vise Forsendelsesinformasjon
      * J2WIN  - J2 Vindu for å vise Avtalespesifikasjon
      * ASUME  -    Record for å styre at vindu legges over
      *             eksisterende data.
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * HOVEDRUTINE                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2BLD Behandle bilde for vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Hent sist brukte kundenummer
      *
     c                   move      w_kund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1                             60
      *
     c                   if        *in60 = *off
     c                   move      rkkund        c2kund
     c                   move      rkalfa        c2alfa
     c                   move      rknavn        c2navn
     c                   move      rkgate        c2gate
     c                   move      rkadr2        c2adr2
     c                   move      rksted        c2sted
     c                   move      rkponr        c2ponr
     c                   move      rkbetb        c2betb
     c                   move      rkbetm        c2betm
     c                   move      rkkatg        c2katg
     c                   move      rkslgr        c2slgr
     c                   move      rkland        c2land
T5.50c                   move      rkupnr        c2upnr
S5.50c*                  move      rkpers        c2pers
     c                   move      rksprk        c2sprk
     c                   move      rkpass        c2pass
     c                   move      rktlfn        c2tlfn
     c                   move      rkmobn        c2mobn
     c                   move      rktlfx        c2tlfx
     c                   move      rkbgir        c2bgir
     c                   move      rkftnr        c2ftnr
     c                   move      rkrenk        c2renk
     c                   move      rkmkod        c2mkod
     c                   move      rkvalk        c2valk
     c                   move      rkgrns        c2grns
T5.50c                   move      rkkgrk        c2kgrk
     c                   move      rkfrie        c2frie
     c                   move      rkavde        c2avde
     c                   move      rkkres        c2kres
     c                   move      rkrabk        c2rabk
     c                   move      rkorab        c2orab
     c                   move      rkdist        c2dist
     c                   move      rklagr        c2lagr
     c                   move      rkspfa        c2spfa
     c                   move      rkspl1        c2spl1
     c                   move      rkspl2        c2spl2
     c                   move      rkspl3        c2spl3
     c                   move      rkspl4        c2spl4
     c                   move      rkspl5        c2spl5
     c                   move      rkptab        c2ptab
T5.40c                   move      rkpta2        c2pta2
     c                   move      rkemal        c2emal
T5.00c                   if        rkedat = *loval
T5.00c                   move      *zeros        c2edat
T5.00c                   else
     c     *dmy          move      rkedat        c2edat
T5.00c                   endif
     c                   move      rkesig        c2esig
     c                   move      rkspav        c2spav
      *
     c                   move      rkkund        e2kund
     c                   move      rknavn        e2navn
     c                   move      rkbgat        e2bgat
     c                   move      rkbadr        e2badr
     c                   move      rkbpnr        e2bpnr
     c                   move      rkweba        e2weba
     c                   move      rkmobo        e2mobo
      *
      * Hent poststed
      *
     c                   move      rkbpnr        apospf_ponr
     c     apospf_key    chain     apospfr
      *                                                                 .
     c                   if        %found
     c                   move      apsted        e2bste
     c                   endif
      *
     c                   move      rkfagb        f2fagb
     c                   move      rkfrgb        f2frgb
     c                   move      rkekgb        f2ekgb
     c                   move      rkordb        f2ordb
     c                   move      rkprpa        f2prpa
     c                   move      rksamf        f2samf
     c                   move      rkrekv        f2rekv
     c                   move      rksekv        f2sekv
     c                   move      rkfagr        f2fagr
     c                   move      rkfcop        f2fcop
     c                   move      rkfrsk        f2frsk
     c                   move      rkrasp        f2rasp
     c                   move      rkneto        f2neto
     c                   move      rkebiz        f2ebiz
     c                   move      rklety        f2lety
     c                   move      rkpbok        f2pbok
      *
T5.00c                   if        rkndat = *loval
T5.00c                   move      *zeros        f2ndat
T5.00c                   else
     c     *dmy          move      rkndat        f2ndat
T5.00c                   endif
     c                   move      rknsig        f2nsig
      *                                                                 .
     c                   move      rkedik        g2edik
     c                   move      rkeank        g2eank
     c                   move      rkfaku        g2faku
     c                   move      rknavn        g2navn
     c                   move      rkfacn        g2facn
5.32 c                   move      rkfaty        g2faty
      *                                                                 .
5.41 c                   if        rkfaty <> *zero
5.41 c                   eval      p_faty = rkfaty
5.41 c                   eval      p_tbes = *blank
5.41 c                   eval      p_kall = '1'
5.41 c                   call      'RK111C'
5.41 c                   parm                    w_firm
5.41 c                   parm                    p_kall
5.41 c                   parm                    p_faty
5.41 c                   parm                    p_tbes
5.41  *
5.41 c                   if        p_faty <> *zero
5.41 c                   eval      g2tbes = p_tbes
5.41 c                   else
5.41 c                   eval      g2tbes = *blank
5.41 c                   endif
5.41 c                   endif
5.41  *
     c                   if        rkafda = *loval
     c                   eval      h2afda = *zeros
     c                   else
     c     *dmy          move      rkafda        h2afda
     c                   endif
      *                                                                 .
     c                   if        rkatda = *loval
     c                   eval      h2atda = *zeros
     c                   else
     c     *dmy          move      rkatda        h2atda
     c                   endif
      *                                                                 .
     c                   move      rksent        h2sent
      *                                                                 .
     c                   move      rkform        i2form
     c                   move      rksped        i2sped
     c                   move      rklbet        i2lbet
     c                   move      rkladr        i2ladr
     c                   move      rkkjØr        i2kjor
     c                   move      rkkjnr        i2kjnr
      *                                                                 .
      * Henter tekster
      *                                                                 .
     c                   exsr      xsr020
      *                                                                 .
     c                   endif
      *
     c     c2tagb        tag
      *
T5.30 * Finnes notatblokk på kunden?
      *
E5.40c                   eval      rklal2_syst = 'K'
E5.40c***                eval      rklal2_pros = *zeros
6.21 c                   eval      rklal2_pros = *blanks
E5.40c                   eval      rklal2_akti = *blanks
E5.40c                   eval      rklal2_kund = c2kund
E5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.30c                   if        *in94 = *on
T5.30c                   eval      C2notb = 'NOTATARK'
T5.30c                   endif
      *
     c                   exfmt     c2bld
      *
     c     *inkc         cabeq     *on           c2tag1
     c     *inkl         cabeq     *on           c2tag1
      *
      * Call program for posteringer
      *
     c                   if        *inke = *on
     c                   exsr      xe2bld
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for OFAK-KODER
      *
     c                   if        *inkf = *on
     c                   exsr      xf2win
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for EAN-KODER
      *
     c                   if        *inkg = *on
     c                   exsr      xg2win
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for autogiro
      *
     c                   if        *inkh = *on
     c                   exsr      xh2win
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for forsendelse
      *
     c                   if        *inki = *on
     c                   exsr      xi2win
     c                   goto      c2tagb
     c                   endif
5.50  *
5.50  * Call program for kontaktperson
5.50  *
5.50 c                   if        *inkj = *on
      *
5.50 c                   call      'RF210R'
5.50 c                   parm                    w_firm
5.50 c                   parm                    w_kund
5.50 c                   parm                    w_levr
      *
5.50 c                   goto      c2tagb
5.50 c                   endif
      *
      * Call program for besøkadresse
      *
     c                   if        *inkk = *on
     c                   exsr      xe2win
     c                   goto      c2tagb
     c                   endif
5.50  *
5.50  * Call program for kort
5.50  *
5.50 c                   if        *inkm = *on
      *
5.50 c                   call      'RF220R'
5.50 c                   parm                    w_firm
5.50 c                   parm                    w_kund
      *
5.50 c                   goto      c2tagb
5.50 c                   endif
      *
      * Oppkall av notatark. hvis kunden sperret
      * eller F2
      *
     c                   if        c2sprk = 'J' or
     c                             *inkb = *on
     c                   move      w_firm        rxfirm
     c                   move      wpsyst        rxsyst
E5.40c                   move      wppros        rxpros
     c                   move      wpakti        rxakti
     c                   move      w_kund        rxkont
     c                   move      wpspes        rxspes
     c                   move      wpavde        rxavde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   move      wpvalg        rxvalg
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
     c                   move      rxulda        rxupar
     c                   call      'RF020R'
     c                   parm                    rxupar
     c                   move      rxupar        rxulda
     c                   eval      *inkb = *off
     c                   goto      b2taga
     c                   endif
      *
      * Vis bilde for avtalespesifikasjon
      *
     c                   if        c2spav = 1
     c                   exsr      xj2win
     c                   endif
      *
     c     c2tag1        tag
      *
     c                   goto      xslutt
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2BLD Vise posteringer                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2bld        begsr
      *
     c                   call      'RK512R'
     c                   parm                    wpvalg
     c                   parm                    w_kund
     c                   parm                    p_biln
     c                   parm                    p_rest
      *
     c                   move      *on           *in31
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2WIN Behandle bilde for vedlikehold av besøksadresse           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2win        begsr
      *
     c     e2tagb        tag
      *
     c                   exfmt     e2win
      *
     c     *inkc         cabeq     *on           e2tag1
     c     *inkl         cabeq     *on           e2tag1
      *
     c     e2tag1        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * F2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xf2win        begsr
      *
     c                   eval      wactli = 4
     c                   eval      wactpo = 22
      *
     c                   exfmt     f2win
      *
     c     *inkc         cabeq     *on           f2tag1
     c     *inkl         cabeq     *on           f2tag1
      *
      *
      *
     c     f2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * G2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xg2win        begsr
      *
     c                   eval      wactli = 6
     c                   eval      wactpo = 15
      *
     c                   exfmt     g2win
      *
     c     *inkc         cabeq     *on           g2tag1
     c     *inkl         cabeq     *on           g2tag1
      *
      *
      *
     c     g2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * H2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xh2win        begsr
      *
     c                   eval      wactli = 6
     c                   eval      wactpo = 25
      *
     c                   exfmt     h2win
      *
     c     *inkc         cabeq     *on           h2tag1
     c     *inkl         cabeq     *on           h2tag1
      *
     c     h2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * I2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xi2win        begsr
      *
T5.00 * Hent FORSENDELSESMÅTE
      *
T5.00c                   if        i2form <> *zero
T5.00c                   move      i2form        p_qkod
T5.00c                   call      'RS717R'
T5.00c                   parm                    p_stat
T5.00c                   parm                    p_qkod
T5.00c                   parm                    p_qtxt
      *                                                                 .
T5.00c                   move      p_qtxt        i2qtxt
T5.00c                   endif
      *
T5.00 * Hent LEVERINGSBETINGELSE
      *
T5.00c                   if        i2lbet <> *zero
T5.00c                   move      i2lbet        p_rkod
T5.00c                   call      'RS718R'
T5.00c                   parm                    p_stat
T5.00c                   parm                    p_rkod
T5.00c                   parm                    p_rtxt
T5.00c                   parm                    p_tx2
T5.00c                   parm                    p_tx3
      *                                                                 .
T5.00c                   movel     p_rtxt        i2rtx1
T5.00c                   endif
      *
T5.00 * Hent SPEDITØR
      *
T5.00c                   if        i2sped <> *zero
T5.00c                   move      i2sped        p_vkod
T5.00c                   call      'RS722R'
T5.00c                   parm                    p_stat
T5.00c                   parm                    p_vkod
T5.00c                   parm                    p_vtxt
      *                                                                 .
T5.00c                   movel     p_vtxt        i2vtxt
T5.00c                   endif
      *
T5.00 * Hent KJØRERUTE
      *
T5.00c                   if        i2kjor <> *zero
T5.00c                   move      i2kjor        p_yrut
T5.00c                   call      'RS725R'
T5.00c                   parm                    p_stat
T5.00c                   parm                    p_yrut
T5.00c                   parm                    p_ytxt
      *                                                                 .
T5.00c                   move      p_ytxt        i2ytxt
T5.00c                   endif
      *
     c                   eval      wactli = 6
     c                   eval      wactpo = 25
      *
     c     i2tagb        tag
      *
     c                   exfmt     i2win
      *
     c     *inkc         cabeq     *on           i2tag1
     c     *inkl         cabeq     *on           i2tag1
      *
     c     i2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * J2WIN Behandle bilde for å vise avtalespesifikasjon             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xj2win        begsr
      *
     c                   eval      w_alin = 6
     c                   eval      w_acol = 25
      *
     c                   eval      rksal1_kund = w_kund
     c     rksal1_key    chain     rksal1                             93
      *
     c                   if        *in93 = *off
     c                   eval      j2sat1 = rksat1
     c                   eval      j2sat2 = rksat2
     c                   eval      j2sat3 = rksat3
     c                   else
     c                   eval      j2sat1 = *blanks
     c                   eval      j2sat2 = *blanks
     c                   eval      j2sat3 = *blanks
     c                   endif
      *
     c                   exfmt     j2win
      *
     c     *inkc         cabeq     *on           j2tag1
     c     *inkl         cabeq     *on           j2tag1
      *
     c     j2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Henter tekster                                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsr020        begsr
      *
      * Sjekke om postnummer er gyldig
      *
     c                   if        c2ponr <> *zero
     c                   move      c2ponr        apospf_ponr
     c     apospf_key    chain     apospfr                            62
      *                                                                 .
     c                   if        *in62 = *on
     c                   move      *on           *in32
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   movel     apponr        c2sted
     c                   move      apsted        c2sted
     c                   endif
     c                   endif
      *
      * Hent betalingsbetingelse
      *
     c                   if        c2betb <> *zero
     c                   move      c2betb        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2ctxt
     c                   endif
      *
      * Hent valuta-kode
      *
     c                   if        c2valk <> *blank
     c                   move      c2valk        p_pkod
     c                   call      'RS716R'
     c                   parm                    p_stat
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2pmna
     c                   endif
      *
      * Hent avdeling
      *
     c                   if        c2avde <> *zero
     c                   move      c2avde        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2gtxt
     c                   endif
      *
      * Hent rabattkategori
      *
     c                   if        c2rabk <> *zero
     c                   move      c2rabk        p_fkod
     c                   call      'RS706R'
     c                   parm                    p_stat
     c                   parm                    p_fkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2ftxt
     c                   endif
      *
      * Hent betalingsmåte
      *
     c                   if        c2betm <> *zero
     c                   move      c2betm        p_bkod
     c                   call      'RS702R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2btxt
     c                   endif
      *
      * Hent kundekategori
      *
     c                   if        c2katg <> *zero
     c                   move      c2katg        p_kkod
     c                   call      'RS711R'
     c                   parm                    p_stat
     c                   parm                    p_kkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2ktxt
     c                   endif
      *
      * Hent distrikt
      *
     c                   if        c2dist <> *zero
     c                   move      c2dist        p_hkod
     c                   call      'RS708R'
     c                   parm                    p_stat
     c                   parm                    p_hkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2htxt
     c                   endif
      *
      * Hent selger
      *
     c                   if        c2slgr <> *zero
     c                   move      c2slgr        p_ikod
     c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_ikod
     c                   parm                    p_tx20
      *
E5.00c                   movel     p_tx20        c2itxt
     c                   endif
      *
      * Hent lager
      *
     c                   if        c2lagr <> *zero
     c                   move      c2lagr        p_jkod
     c                   call      'RS710R'
     c                   parm                    p_stat
     c                   parm                    p_jkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2jtxt
     c                   endif
      *
      * Hent Mobiloperatør
      *
     c                   if        e2mobo <> *blank
     c                   move      e2mobo        p_mkod
     c                   call      'RS729R'
     c                   parm                    p_stat
     c                   parm                    p_mkod
     c                   parm                    p_mtxt
      *
     c                   move      p_mtxt        e2mtxt
     c                   endif
      *
     c     xa2010        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wactli = 0
     c                   eval      wactpo = 0
     c                   eval      werror = *zero
     c                   eval      wpfunk = *zero
     c                   eval      wpantp = *zero
     c                   eval      wseqen = *blank
     c                   eval      p_rest = 0
      *
      * Parametre til Notatark.
      *
     c                   move      'K'           wpsyst
     c                   eval      wpspes = 0
     c                   eval      wpavde = 0
6.21 c                   eval      wppros = *blanks
     c                   eval      wpakti = *blanks
     c                   eval      wpf001 = *blanks
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_kund
     c                   parm                    wpvalg
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
     c                   move      w_kund        c2kund
      *
      * Hent kunde
      *
     c     rkunl1_key    chain     rkunl1                             61
      *
      * Hent statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             61
      *
     c                   if        *in61 = *on
     c                   move      15            ww1bek
     c                   move      0             ww1bkk
     c                   endif
      *
     c                   if        *in61 = *off
     c                   move      ra1bek        ww1bek
     c                   move      ra1bkk        ww1bkk
     c                   endif
      *
      * Key for kunderegister via kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
T5.30 * Key for notatblokk
      *
E5.40c     rklal2_key    klist
T5.30c                   kfld                    w_firm
E5.40c                   kfld                    rklal2_syst
E5.40c                   kfld                    rklal2_pros
E5.40c                   kfld                    rklal2_akti
E5.40c                   kfld                    rklal2_kund
      *
      * Key for avtalespesifikasjon
      *
     c     rksal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksal1_kund
      *
      * Key for postnummer-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
     c                   endsr
