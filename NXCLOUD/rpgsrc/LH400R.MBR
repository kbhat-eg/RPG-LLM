     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LH400R
      *  Beskrivelse . . : Sletting av bestillingsforslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            210100  Nytt program                            ASK
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flfhelu    uf   e           k disk    rename(lfhepfr:lfhelur)
     flfdtl1    if   e           k disk    rename(lfdtpfr:lfdtl1r)
     flfdtlu    uf   e           k disk    rename(lfdtpfr:lfdtlur)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lfhelu_numm     s                   like(lhnumm)
     d lfdtl1_numm     s                   like(lfnumm)
     d lfdtlu_numm     s                   like(lfnumm)
     d lfdtlu_line     s                   like(lfline)
      *
      * Definering av variable i parametre
      *
     d p_firm          s                   like(lhfirm)
     d p_numm          s                   like(lhnumm)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lhfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestillingsforslag : Linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lfdtl1_key    setll     lfdtl1r
      *
     c     lfdtl1_key    reade     lfdtl1r                                90
      *
     c                   dow       *in90 = *off
     c                   eval      lfdtlu_line = lfline
     c     lfdtlu_key    chain     lfdtlur                            92
     c                   if        *in92 = *off
     c                   delete    lfdtlur
     c                   endif
      *
     c     lfdtl1_key    reade     lfdtl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestillingsforslag : Hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     lfhelu_key    chain     lfhelur                            92
     c                   if        *in92 = *off
     c                   delete    lfhelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      *  Key for oppdatering av BESTILLING-FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      *  Key for les av BESTILLING-FORSLAG-LINJE
      *
     c     lfdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtl1_numm
      *
      *  Key for oppdatering av BESTILLING-FORSLAG-LINJE
      *
     c     lfdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlu_numm
     c                   kfld                    lfdtlu_line
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lfhelu_numm = p_numm
     c                   eval      lfdtl1_numm = p_numm
     c                   eval      lfdtlu_numm = p_numm
      *
     c                   endsr
