      /TITLE  ASOFAK: Hvem som holder ordre klargjøres i parametere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO709R
      * Beskrivelse . . : Hvem som holder ordre klargjøres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R4.00 230102 AMD  Skrevet om til ny standard
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
      *
      * Definering av parametere
      *
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
      *
      * Definering av variable
      *
     d w_koda          s                   like(fokoda)
     d w_koti          s                   like(fokoti)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legge inn opplysninger som skal oppdatere ordrehode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hente inn dagens dato og
      * klokkeslett
      *
     c                   time                    w_koda
     c                   time                    w_koti
      *
      * Oppdater ORDRE-HODE-REGISTER
      *
     c     fohelu_key    chain     fohelur                            90
      *
     c                   if        *in90 = *off
     c                   eval      fokous = l_user
     c                   eval      fokows = l_wsid
     c                   eval      fokoda = w_koda
     c                   eval      fokoti = w_koti
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Legger inn firma ordrenummer og suffix i nøkklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      fohelu_numm = p_numm
     c                   eval      fohelu_suff = p_suff
      *
     c                   endsr
