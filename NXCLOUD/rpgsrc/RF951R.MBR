     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF951R                                              *
      * Beskrivelse . . : Fjerner ett journal-nr. innenfor bestemt år         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
6.30  * sst       130217  Fjernet RPTRPF,  Ikke i bruk lenger                 *
6.31  * sst       151019  Sette tilbake restbeløp på ev. kryssede transer     *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhtrlu    upe  e           k disk    rename(rhtrpfr:rhtrlur)
     frktrlu    use  e           k disk    rename(rktrpfr:rktrlur)
     frltrlu    use  e           k disk    rename(rltrpfr:rltrlur)
6.31 frktrl8    uf   e           k disk    rename(rktrpfr:rktrl8r)
6.31 f                                     prefix(rx:2)
6.30 f**rptrlu    use  e           k disk    rename(rptrpfr:rptrlur)
      *
6.31 d rktrl8_firm     s                   like(rnfirm)
6.31 d rktrl8_kund     s                   like(rnkund)
6.31 d rktrl8_biln     s                   like(rnbiln)
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpjof                         5  0
     d rhpjot                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
     d rhpsps                         1
     d rhpktu                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *  Posteringer - hovedbok
     irhtrlur       01
      *
      *  Posteringer - kunder
     irktrlur       02
      *
      *  Posteringer - leverandører
     irltrlur       03
      *
      *  Posteringer - prosjekt
6.30 i**rptrlur       04
      *
B001 c   01              if        l_firm = rbfirm and
B002 c                             rhpjof = rbjour and
B003 c                             rhprÅr = rbraar
 003 c                   delete    rhtrlur
E003 c                   endif
      *
B001 c   02              if        l_firm = rnfirm and
B002 c                             rhpjof = rnjour and
B003 c                             rhprÅr = rnraar
6.31  *  Dersom innb. og fakt oppdatert med restb. skal dette settes tilbake
6.31 c                   if        rnbilk = 87
6.31 c                             and rnrefn <> 0
6.31 c                   exsr      SettRest
6.31 c                   endif
6.31  *
 003 c                   delete    rktrlur
E003 c                   endif
      *
B001 c   03              if        l_firm = rofirm and
B002 c                             rhpjof = rojour and
B003 c                             rhprÅr = roraar
 003 c                   delete    rltrlur
E001 c                   endif
      *
6.30 c*  04              if        l_firm = rdfirm and
6.30 c*                            rhpjof = rdjour and
6.30 c*                            rhprÅr = rdraar
6.30 c*                  delete    rptrlur
6.30 c*                  endif
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.31  * Subrutine for å sette tilbake restbeløp                         *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.31  *
6.31 c     SettRest      begsr
6.31 c                   eval      rktrl8_firm = rnfirm
6.31 c                   eval      rktrl8_kund = rnkund
6.31 c                   eval      rktrl8_biln = rnrefn
6.31 c     rktrl8_key    setll     rktrl8
6.31 c     rktrl8_key    reade     rktrl8
6.31 c                   dow       not %eof(rktrl8)
6.31 c                   if        rxkund = rnkund
6.31 c                             and rxbiln = rnrefn
6.31 c                             and rxrefn = rnbiln
6.31 c                   eval      rxrest = rxrest + rnbelØ * -1
6.31 c                   eval      rxrefn = *zero
6.31 c                   update    rktrl8r
6.31 c                   leave
6.31 c                   endif
6.31 c     rktrl8_key    reade     rktrl8
6.31 c                   enddo
6.31  *
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
6.31  *
6.31  * Key for kundetranser pr kunde og bilagsnr
6.31  *
6.31 c     rktrl8_key    klist
6.31 c                   kfld                    rktrl8_firm
6.31 c                   kfld                    rktrl8_kund
6.31 c                   kfld                    rktrl8_biln
6.31  *
     c                   endsr
