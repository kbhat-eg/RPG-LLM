     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP500R
      * Beskrivelse . . : Vise og aktiviser betingelse for betingelsesnivå
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080798 HKO  Nytt program
      *       120804 HKO  Leter ikke etter betingelse mot vareeier
      *       130605 HKO  Utvidet nivå med varetype og leveringstype
6.30  * 6.30  251114 Bsa  Innfører F tast for visning av NOBB info. Erstatter
6.30  *                   koding i jacada.
6.3x  * 6.30  310515 bof  Utvidet med prisgruppe
6.31  * 6.30  040816 bof  For å kunne akt. på rab.elem.nivå, kalles nytt program
6.32  * 6.30  050618 nho  Prisgruppe ble ikke med inn i JRAELR         t program
6.33  * 6.30  211218 bkv  Utvidet med logistikk-vare behandling.
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
     fjbetlr    if   e           k disk    rename(jbetpfr:jbetlrr)
     fjbetlu    uf   e           k disk    rename(jbetpfr:jbetlur)
     fjbetlb    if   e           k disk    rename(jbetpfr:jbetlbr)
     fjbetlc    if   e           k disk    rename(jbetpfr:jbetlcr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.3x fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjraelr    if   e           k disk    rename(jraepfr:jraelrr)
      *
     fjp500d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jpfirm)
     d p_vare          s                   like(jpvare)
     d p_ldor          s                   like(jpldor)
6.3x d p_prgr          s                   like(jpeprg)
     d p_lety          s                   like(jplety)
6.30 d p_nobb          s              8
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jbetl1_ldor     s                   like(jpldor)
6.3x d jbetl1_prgr     s                   like(jpprgr)
     d jbetl1_ogrp     s                   like(jpogrp)
     d jbetl1_hgrp     s                   like(jphgrp)
     d jbetl1_ugrp     s                   like(jpugrp)
     d jbetl1_modn     s                   like(jpmodn)
     d jbetl1_vare     s                   like(jpvare)
     d jbetl1_vtyp     s                   like(jpvtyp)
     d jbetl1_lety     s                   like(jplety)
     d jbetlu_ldor     s                   like(jpldor)
6.3x d jbetlu_prgr     s                   like(jpprgr)
     d jbetlu_ogrp     s                   like(jpogrp)
     d jbetlu_hgrp     s                   like(jphgrp)
     d jbetlu_ugrp     s                   like(jpugrp)
     d jbetlu_modn     s                   like(jpmodn)
     d jbetlu_vtyp     s                   like(jpvtyp)
     d jbetlu_lety     s                   like(jplety)
     d jbetlu_vare     s                   like(jpvare)
     d jbetlu_rsts     s                   like(jprsts)
     d jbetlu_line     s                   like(jpline)
     d jbetlr_ldor     s                   like(jpldor)
6.3x d jbetlr_prgr     s                   like(jpprgr)
     d jbetlr_ogrp     s                   like(jpogrp)
     d jbetlr_hgrp     s                   like(jphgrp)
     d jbetlr_ugrp     s                   like(jpugrp)
     d jbetlr_modn     s                   like(jpmodn)
     d jbetlr_vare     s                   like(jpvare)
     d jbetlr_vtyp     s                   like(jpvtyp)
     d jbetlr_lety     s                   like(jplety)
     d jbetlr_rsts     s                   like(jprsts)
     d jbetlr_line     s                   like(jpline)
     d jbetlb_ldor     s                   like(jpldor)
     d jbetlb_vare     s                   like(jpvare)
6.3x d jbetlb_prgr     s                   like(jpprgr)
     d jbetlb_vtyp     s                   like(jpvtyp)
     d jbetlb_lety     s                   like(jplety)
     d jbetlc_ldor     s                   like(jpldor)
     d jbetlc_modn     s                   like(jpmodn)
     d jbetlc_vare     s                   like(jpvare)
6.3x d jbetlc_prgr     s                   like(jpprgr)
     d jbetlc_vtyp     s                   like(jpvtyp)
     d jbetlc_lety     s                   like(jplety)
     d jvarl1_vare     s                   like(jvvare)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
6.3x d ra30l1_ekod     s                   like(redkod)
     d jmodl1_modn     s                   like(jumodn)
     d jraelr_eldo     s                   like(jpeldo)
6.3x d jraelr_eprg     s                   like(jpeprg)
     d jraelr_eogr     s                   like(jpeogr)
     d jraelr_ehgr     s                   like(jpehgr)
     d jraelr_eugr     s                   like(jpeugr)
     d jraelr_emod     s                   like(jpemod)
     d jraelr_evar     s                   like(jpevar)
     d jraelr_evty     s                   like(jpevty)
     d jraelr_elet     s                   like(jpelet)
     d jraelr_ebrs     s                   like(jpebrs)
     d jraelr_ebli     s                   like(jpebli)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d b_feil          s              1    inz(*off)
     d b_visn          s              1    inz(*off)
      *
     d w_firm          s                   like(jpfirm)
     d w_ldor          s                   like(jpldor)
6.3x d w_prgr          s                   like(jpeprg)
     d w_vare          s                   like(jpvare)
     d w_vtyp          s                   like(jpvtyp)
      *
6.33 d w_lv_nobb       s                   like(jvnobb)
6.33 d w_lv_modn       s                   like(jvmodn)
6.33 d w_lv_ldor       s                   like(jvldor)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(6)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.33 C/Exec SQL
6.33 C+  Set Option DatFmt=*ISO , commit=*none
6.33 C/End-Exec
6.33  *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     jbetl1_key    setll     jbetl1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Rabattelement
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b2vare
6.31 c                   call      'JP511R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
6.3x c                   parm                    b2prgr
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    b2modn
     c                   parm                    w_vare
     c                   parm                    b2vtyp
     c                   parm                    b2lety
     c                   parm                    b1rsts
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   exsr      finn_fant
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis vareavtale
      *
     c                   if        b1valg = 8
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Aktiviser/passiviser betingelse
      *
     c                   if        b1valg = 9
     c                   exsr      aktiviser_bet
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for å vise betingelsesinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      jbetlr_ldor = b2ldor
6.3x c                   eval      jbetlr_prgr = b2prgr
     c                   eval      jbetlr_ogrp = b2ogrp
     c                   eval      jbetlr_hgrp = b2hgrp
     c                   eval      jbetlr_ugrp = b2ugrp
     c                   eval      jbetlr_modn = b2modn
     c                   eval      jbetlr_vare = b2vare
     c                   eval      jbetlr_vtyp = b2vtyp
     c                   eval      jbetlr_lety = b2lety
     c                   eval      jbetlr_rsts = b1rsts
     c                   eval      jbetlr_line = b1line
     c     jbetlr_key    chain     jbetlr
      *
     c                   eval      c2ldor = jpldor
6.2X c                   eval      c2prgr = jpprgr
     c                   eval      c2ogrp = jpogrp
     c                   eval      c2hgrp = jphgrp
     c                   eval      c2ugrp = jpugrp
     c                   eval      c2modn = jpmodn
     c                   eval      c2vare = jpvare
     c                   eval      c2rsts = jprsts
     c                   eval      c2line = jpline
      *
     c                   eval      c2beti = jpbeti
     c                   eval      c2eusr = jpeusr
     c                   eval      c2hgrp = 0
     c                   eval      c2ugrp = 0
     c                   eval      c2utxt = *blank
     c                   eval      c2nobb = 0
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2dato = 0
     c                   eval      c2edat = 0
     c                   if        jpfdat <> *loval
     c     *dmy          move      jpfdat        c2fdat
     c                   endif
     c                   if        jptdat <> *loval
     c     *dmy          move      jptdat        c2tdat
     c                   endif
     c                   if        jpdato <> *loval
     c     *dmy          move      jpdato        c2dato
     c                   endif
     c                   if        jpedat <> *loval
     c     *dmy          move      jpedat        c2edat
     c                   endif
      *
      * Henter beskrivelse
      *
     c                   eval      c2lnav = b2lnav
6.3x c                   eval      c2pnav = b2ptxt
     c                   eval      c2mte1 = b2mte1
     c                   eval      c2nobb = b2nobb
      *
     c                   eval      c2otxt = *blank
     c                   eval      c2htxt = *blank
     c                   eval      c2utxt = *blank
     c                   eval      c2vtek = *blank
      *
     c                   if        c2ogrp <> 0
     c                   eval      vogrl1_oogr = c2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   eval      c2otxt = vgotxt
     c                   endif
      *
     c                   if        c2hgrp <> 0
     c                   eval      vhgrl1_hogr = c2ogrp
     c                   eval      vhgrl1_hhgr = c2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      c2htxt = vghtxt
     c                   endif
      *
     c                   if        c2ugrp <> 0
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   eval      c2utxt = vgutxt
     c                   endif
      *
     c                   if        c2vare <> *blank
     c                   eval      c2vtek = b2vte2
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
6.30  *
6.30  * F18=NOBB informasjon
6.30  *
6.30 c                   if        *inks = *on
6.30 c                   exsr      vis_nobb
6.30 c                   goto      c2tagb
6.30 c                   endif
      *
      * Vise rabattelement
      *
     c                   if        *inkm
     c                   eval      w_vare = c2vare
6.31 c                   call      'JP511R'
     c                   parm                    w_firm
     c                   parm                    c2ldor
6.3x c                   parm                    c2prgr
     c                   parm                    c2ogrp
     c                   parm                    c2hgrp
     c                   parm                    c2ugrp
     c                   parm                    c2modn
     c                   parm                    w_vare
     c                   parm                    c2vtyp
     c                   parm                    c2lety
     c                   parm                    c2rsts
     c                   parm                    c2line
     c                   goto      c2tagb
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     jbetl1_key    reade     jbetl1                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1rsts = jprsts
     c                   eval      b1line = jpline
     c                   eval      b1beti = jpbeti
     c                   eval      b1akti = *blank
     c                   eval      b1rkod = *blank
      *
     c                   if        jpakti = 1
     c                   eval      b1akti = 'A'
     c                   endif
      *
     c                   if        jprsts = 1
     c                   eval      b1rkod = 'L'
     c                   endif
      *
      * Finner nettopris eller rabatt fra rabattelementene
      *
     c                   exsr      finn_fant
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente betingelsesnivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nivÅ     begsr
      *
      * Finner betingelser på varenivå
      *
     c                   eval      jbetlb_ldor = w_ldor
6.33 c                   if        w_lv_nobb <> 0
6.33 c                   movel     w_lv_nobb     jbetlb_vare
6.33 c                   else
     c                   eval      jbetlb_vare = p_vare
6.33 c                   endif
6.3x c                   eval      jbetlb_prgr = p_prgr
     c                   eval      jbetlb_vtyp = w_vtyp
     c                   eval      jbetlb_lety = p_lety
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlb_lety = 0
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        w_vtyp <> *blank
      *
     c                   eval      jbetlb_vtyp = w_vtyp
     c                   eval      jbetlb_lety = p_lety
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlb_lety = 0
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Finner betingelser på modulnummer-nivå
      *
     c                   eval      jbetlc_ldor = w_ldor
6.33 c                   if        w_lv_modn <> 0
6.33 c                   eval      jbetlc_modn = w_lv_modn
6.33 c                   else
     c                   eval      jbetlc_modn = jvmodn
6.33 c                   endif
     c                   eval      jbetlc_vare = *blank
6.3x c                   eval      jbetlc_prgr = p_prgr
     c                   eval      jbetlc_vtyp = w_vtyp
     c                   eval      jbetlc_lety = p_lety
      *
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlc_lety = 0
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        w_vtyp <> *blank
      *
     c                   eval      jbetlc_vtyp = w_vtyp
     c                   eval      jbetlc_lety = p_lety
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlc_lety = 0
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Finner betingelser på undergruppe-nivå
      *
     c                   eval      jbetl1_ldor = w_ldor
     c                   eval      jbetl1_ogrp = jvogrp
     c                   eval      jbetl1_hgrp = jvhgrp
     c                   eval      jbetl1_ugrp = jvugrp
     c                   eval      jbetl1_modn = 0
     c                   eval      jbetl1_vare = *blank
     c                   eval      jbetl1_vtyp = w_vtyp
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        w_vtyp <> *blank
      *
     c                   eval      jbetl1_vtyp = w_vtyp
     c                   eval      jbetl1_lety = p_lety
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Finner betingelser på hovedgruppe-nivå
      *
     c                   eval      jbetl1_ugrp = 0
     c                   eval      jbetl1_vtyp = w_vtyp
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
      * Finner betingelser på overgruppe-nivå
      *
     c                   eval      jbetl1_hgrp = 0
     c                   eval      jbetl1_vtyp = w_vtyp
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        w_vtyp <> *blank
      *
     c                   eval      jbetl1_vtyp = w_vtyp
     c                   eval      jbetl1_lety = p_lety
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Finner betingelser på leverandør-nivå
      *
     c                   eval      jbetl1_ogrp = 0
     c                   eval      jbetl1_vtyp = w_vtyp
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        w_vtyp <> *blank
      *
     c                   eval      jbetl1_vtyp = w_vtyp
     c                   eval      jbetl1_lety = p_lety
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne nettopris eller rabatt fra rabattelementene
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_fant     begsr
      *
     c                   eval      b1fakt = 1
     c                   eval      b1ntpr = 0
     c                   eval      b1raba = 0
      *
     c                   eval      jraelr_eldo = b2ldor
6.32 c                   eval      jraelr_eprg = p_prgr
     c                   eval      jraelr_eogr = b2ogrp
     c                   eval      jraelr_ehgr = b2hgrp
     c                   eval      jraelr_eugr = b2ugrp
     c                   eval      jraelr_emod = b2modn
     c                   eval      jraelr_evar = b2vare
     c                   eval      jraelr_evty = b2vtyp
     c                   eval      jraelr_elet = b2lety
     c                   eval      jraelr_ebrs = b1rsts
     c                   eval      jraelr_ebli = b1line
     c     jraelr_key    setll     jraelr
     c     jraelr_key    reade     jraelr
     c                   dow       not %eof
     c                   select
     c                   when      jpereg = '1'
     c                   eval      b1fakt = b1fakt - (jpever / 100 * b1fakt)
     c                   when      jpereg = '2'
     c                   eval      b1fakt = b1fakt - (jpever / 100)
     c                   when      jpereg = 'N'
     c                   eval      b1ntpr = b1ntpr + jpever
     c                   endsl
     c     jraelr_key    reade     jraelr
     c                   enddo
      *
     c                   eval      b1raba = (1 - b1fakt) * 100
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for aktivisering/passivisering av betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     aktiviser_bet begsr
      *
     c                   eval      jbetlu_ldor = b2ldor
     c                   eval      jbetlu_ogrp = b2ogrp
     c                   eval      jbetlu_hgrp = b2hgrp
     c                   eval      jbetlu_ugrp = b2ugrp
     c                   eval      jbetlu_modn = b2modn
     c                   eval      jbetlu_vare = b2vare
     c                   eval      jbetlu_vtyp = b2vtyp
     c                   eval      jbetlu_lety = b2lety
     c                   eval      jbetlu_rsts = b1rsts
     c                   eval      jbetlu_line = b1line
     c     jbetlu_key    chain     jbetlur
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * Oppdaterer betingelse med valg
      *
     c                   if        jpakti = 1
     c                   eval      jpakti = 0
     c                   else
     c                   eval      jpakti = 1
     c                   endif
      *
     c                   update    jbetlur
      *
      * Oppdater subfile
      *
     c                   if        b1valg <> 0
     c                   if        jpakti = 1
     c                   eval      b1akti = 'A'
     c                   else
     c                   eval      b1akti = *blank
     c                   endif
     c                   endif
      *
     c                   endsr
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     vis_nobb      begsr
6.30 c
6.30 c                   eval      p_nobb = %char(c2nobb)
6.30 c                   call      'AP633R'
6.30 c                   parm                    p_nobb
6.30  *
6.30 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_ldor
6.3x c                   parm                    p_prgr
     c                   parm                    p_lety
      *
      * Key for les på betingelse
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetl1_ldor
6.3X c                   kfld                    jbetl1_prgr
     c                   kfld                    jbetl1_ogrp
     c                   kfld                    jbetl1_hgrp
     c                   kfld                    jbetl1_ugrp
     c                   kfld                    jbetl1_modn
     c                   kfld                    jbetl1_vare
     c                   kfld                    jbetl1_vtyp
     c                   kfld                    jbetl1_lety
      *
      * Key for oppslag på betingelse
      *
     c     jbetlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlr_ldor
6.3x c                   kfld                    jbetlr_prgr
     c                   kfld                    jbetlr_ogrp
     c                   kfld                    jbetlr_hgrp
     c                   kfld                    jbetlr_ugrp
     c                   kfld                    jbetlr_modn
     c                   kfld                    jbetlr_vare
     c                   kfld                    jbetlr_vtyp
     c                   kfld                    jbetlr_lety
     c                   kfld                    jbetlr_rsts
     c                   kfld                    jbetlr_line
      *
      * Key for oppdatering av betingelse
      *
     c     jbetlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlu_ldor
6.3x c                   kfld                    jbetlu_prgr
     c                   kfld                    jbetlu_ogrp
     c                   kfld                    jbetlu_hgrp
     c                   kfld                    jbetlu_ugrp
     c                   kfld                    jbetlu_modn
     c                   kfld                    jbetlu_vare
     c                   kfld                    jbetlu_vtyp
     c                   kfld                    jbetlu_lety
     c                   kfld                    jbetlu_rsts
     c                   kfld                    jbetlu_line
      *
      * Key for oppslag på betingelse med leverandør/varenummer
      *
     c     jbetlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlb_ldor
     c                   kfld                    jbetlb_vare
6.3x c                   kfld                    jbetlb_prgr
     c                   kfld                    jbetlb_vtyp
     c                   kfld                    jbetlb_lety
      *
      * Key for oppslag på betingelse med leverandør/modulnummer
      *
     c     jbetlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlc_ldor
     c                   kfld                    jbetlc_modn
     c                   kfld                    jbetlc_vare
6.3x c                   kfld                    jbetlc_prgr
     c                   kfld                    jbetlc_vtyp
     c                   kfld                    jbetlc_lety
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
6.3x  *
6.3x  * Key for oppslag på prisgruppe
6.3x  *
6.3x c     ra30l1_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    ra30l1_ekod
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for les på rabattelementer
      *
     c     jraelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jraelr_eldo
6.3x c                   kfld                    jraelr_eprg
     c                   kfld                    jraelr_eogr
     c                   kfld                    jraelr_ehgr
     c                   kfld                    jraelr_eugr
     c                   kfld                    jraelr_emod
     c                   kfld                    jraelr_evar
     c                   kfld                    jraelr_evty
     c                   kfld                    jraelr_elet
     c                   kfld                    jraelr_ebrs
     c                   kfld                    jraelr_ebli
      *
      *
      * Henter informasjon fra parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      b2var2 = p_vare
     c                   eval      b2lety = p_lety
     c                   eval      b2prgr = p_prgr
      *
      * Henter vareinformasjon
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   eval      b2vte2 = jvtek1
      *
      * Henter varetype fra vare-detaljer
      *
     c                   eval      jvdtl1_dvar = p_vare
     c     jvdtl1_key    chain     jvdtl1
     c                   if        %found and
     c                             jvdsko = 1
     c                   eval      b2vtyp = 'S'
     c                   else
     c                   eval      b2vtyp = *blank
     c                   endif
      *
      * Henter prisgivende leverandør
      *
     c                   if        p_ldor <> 0
     c                   eval      w_ldor = p_ldor
     c                   else
     c                   eval      w_ldor = jvldor
      * sjekk om logistikk-vare, hent evt. hovedlev.
     c                   eval      w_lv_ldor = 0
6.33 c/exec sql
6.33 c+ select jvkldo
6.33 c+ into   :w_lv_ldor
6.33 c+ from   jvkhpf
6.33 c+ where  jvkfir = :w_firm and
6.33 c+        jvkvnr = :p_vare
6.33 c/end-exec
6.33 c                   if        w_lv_ldor <> 0
6.33 c                   eval      w_ldor = w_lv_ldor
6.33 c                   endif
     c                   endif
6.33 c                   eval      w_lv_nobb = 0
6.33 c                   eval      w_lv_modn = 0
6.33 c/exec sql
6.33 c+ select jvquno,
6.33 c+        jvmodn
6.33 c+ into   :w_lv_nobb,
6.33 c+        :w_lv_modn
6.33 c+ from   jvklpf
6.33 c+ inner join jvarpf on
6.33 c+       jvvare = jvquno
6.33 c+ where  jvqvnr = :p_vare and
6.33 c+        jvqldo = :w_ldor
6.33 c/end-exec
      *
      * Henter betingelsesnivå
      *
     c                   exsr      hent_nivÅ
      *
     c                   eval      b2ldor = jpldor
     c                   eval      b2ogrp = jpogrp
     c                   eval      b2hgrp = jphgrp
     c                   eval      b2ugrp = jpugrp
     c                   eval      b2modn = jpmodn
     c                   eval      b2vare = jpvare
     c                   eval      b2vtyp = jpvtyp
     c                   eval      b2lety = jplety
      *
      * Dersom betingelsesnivå ble funnet hentes beskrivelse av nivået
      *
     c                   if        jpldor <> 0
      *
     c                   eval      jlevl1_ldor = b2ldor
     c     jlevl1_key    chain     jlevl1
     c                   eval      b2lnav = jlnavn
      *
     c                   if        b2ogrp <> 0
     c                   eval      vogrl1_oogr = b2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   eval      b2utxt = vgotxt
     c                   endif
      *
     c                   if        b2hgrp <> 0
     c                   eval      vhgrl1_hogr = b2ogrp
     c                   eval      vhgrl1_hhgr = b2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      b2utxt = vghtxt
     c                   endif
      *
     c                   if        b2ugrp <> 0
     c                   eval      vugrl1_uogr = b2ogrp
     c                   eval      vugrl1_uhgr = b2hgrp
     c                   eval      vugrl1_uugr = b2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   eval      b2utxt = vgutxt
     c                   endif
6.3x  *
6.3x c                   if        b2prgr <> *blank
6.3x c                   eval      ra30l1_ekod = b2prgr
6.3x c     ra30l1_key    chain     ra30l1
6.3x c                   eval      b2ptxt = redtxt
6.3x c                   endif
      *
     c                   if        b2modn <> 0
     c                   eval      jmodl1_modn = c2modn
     c     jmodl1_key    chain     jmodl1
     c                   eval      b2mte1 = jumte1
     c                   endif
      *
     c                   if        b2vare <> *blank
6.33 c                   if        w_lv_nobb <> 0
6.33 c                   eval      b2nobb = w_lv_nobb
6.33 c                   else
     c                   eval      b2nobb = jvnobb
6.33 c                   endif
     c                   endif
      *
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jbetl1_ldor = b2ldor
6.3x c                   eval      jbetl1_prgr = b2prgr
     c                   eval      jbetl1_ogrp = b2ogrp
     c                   eval      jbetl1_hgrp = b2hgrp
     c                   eval      jbetl1_ugrp = b2ugrp
     c                   eval      jbetl1_modn = b2modn
     c                   eval      jbetl1_vare = b2vare
     c                   eval      jbetl1_vtyp = b2vtyp
     c                   eval      jbetl1_lety = b2lety
     c     jbetl1_key    setll     jbetl1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
