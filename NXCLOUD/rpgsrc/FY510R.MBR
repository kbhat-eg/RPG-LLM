      /TITLE  ASOFAK Slettede ordre, vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FY510R
      * Beskrivelse . . : Slettede ordre, vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080501 AMD  Nytt program
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffdell1    if   e           k disk    rename(fdelpfr:fdell1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffy510d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fyfirm)
     d p_aarr          s                   like(fyaarr)
6.nu d p_numm          s                   like(fynumm)
     d p_suff          s                   like(fysuff)
      *
      * Definering av variabler i nøkler
      *
     d fdell1_aarr     s                   like(fyaarr)
     d fdell1_numm     s                   like(fynumm)
     d fdell1_suff     s                   like(fysuff)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fyfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Vise detaljer for slettede ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      a1kund = 0
     c                   eval      a1dumy = *blank
     c                   eval      a1navn = *blank
     c                   eval      a1adr1 = *blank
     c                   eval      a1adr2 = *blank
     c                   eval      a1sted = *blank
     c                   eval      a1bdat = 0
      *
      *  Henter inn opplysninger fra Ordre-Slettet-tabell
      *
     c                   eval      fdell1_aarr = p_aarr
     c                   eval      fdell1_numm = p_numm
     c                   eval      fdell1_suff = p_suff
     c     fdell1_key    chain     fdell1r                            90
      *
     c                   if        *in90  = *off
     c                   eval      a1date = 0
     c                   eval      a1time = 0
     c                   eval      a1kund = fykund
     c                   movel     fydref        a1dref
     c                   eval      a1salg = fytots - fytotr
      *
     c                   if        fydate <> *loval
     c     *dmy          move      fydate        a1date
     c                   endif
      *
     c                   if        fytime <> *loval
     c     *hms          move      fytime        a1time
     c                   endif
      *
     c                   if        fybdat <> *loval
     c     *dmy          move      fybdat        a1bdat
     c                   endif
     c                   endif
      *
      *  Henter inn opplysninger fra Kunde-tabell
      *
     c                   eval      rkunl1_kund = fykund
     c     rkunl1_key    chain     rkunl1r                            91
     c                   if        *in91  = *off
     c                   eval      a1navn = rknavn
     c                   eval      a1adr1 = rkgate
     c                   eval      a1adr2 = rkadr2
     c                   eval      a1sted = rksted
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *    Behandler skjembilde om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     e1tabg        tag
     c                   exfmt     a1bld
      *
      * Gå tilbake til forrige bilde
      *
     c                   if        *inkc
     c                   goto      xslutt
     c                   endif
      *
      * Avslutt program
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for Ordre-Slettet-tabell
      *
     c     fdell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fdell1_aarr
     c                   kfld                    fdell1_numm
     c                   kfld                    fdell1_suff
      *
      * Key for kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm      = l_firm
      *
     c                   endsr
      *
