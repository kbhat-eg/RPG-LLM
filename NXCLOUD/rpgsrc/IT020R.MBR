     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT020R
      * Beskrivelse . . : Leser alle kunder og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090714 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     fit020d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kund                        6
     d  d_navn                             like(rknavn)
     d  d_gate                             like(rkgate)
     d  d_adr2                             like(rkadr2)
     d  d_ponr                        4
     d  d_sted                             like(rksted)
     d  d_bgat                             like(rkbgat)
     d  d_badr                             like(rkbadr)
     d  d_bpnr                        4
     d  d_land                             like(rkland)
     d  d_upnr                             like(rkupnr)
     d  d_ftnr                        9
     d  d_alfa                             like(rkalfa)
     d  d_penr                       11
     d  d_emal                             like(rkemal)
     d  d_weba                             like(rkweba)
     d  d_tlfn                             like(rktlfn)
     d  d_tlfx                             like(rktlfx)
     d  d_mobn                             like(rkmobn)
     d  d_bgir                       11
     d  d_eank                       13
     d  d_avde                        4
     d  d_lagr                        2
     d  d_slgr                        4
     d  d_dist                        4
     d  d_betb                        2
     d  d_valk                             like(rkvalk)
     d  d_lbet                        2
     d  d_sped                        3
     d  d_sper                        1
     d  d_pass                        1
     d  d_rent                        1
     d  d_kgre                        9
     d  d_kgsp                        1
     d  d_ktyp                        1
     d  d_avgf                        1
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
     d w_kgre          s              8  0
     d w_kgra          s              8
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekker om initiell overføring eller oppdatering
      *
     c                   eval      a1vlg1 = 'O'
     c                   exfmt     a1bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      slutt
     c                   endif
      *
     c                   if        a1vlg1 = 'F'
     c                   eval      w_comd = 'Insert'
     c                   else
     c                   eval      w_comd = 'Update'
     c                   endif
      *
      * Leser alle poster på rkunl1
      *
     c                   eval      rkunl1_kund = 0
     c     rkunl1_key    setll     rkunl1
      *
     c                   read      rkunl1
     c                   dow       not %eof and
     c                             rkfirm = w_firm
      *
     c                   eval      d_kund = %triml(%editw(rkkund : '    0 '))
     c                   eval      d_navn = rknavn
     c                   eval      d_gate = rkgate
     c                   eval      d_adr2 = rkadr2
     c                   eval      d_ponr = %editw(rkponr : '0   ')
     c     ' ':'0'       xlate     d_ponr        d_ponr
     c                   eval      d_sted = %subst(rksted : 6 : 25)
     c                   eval      d_bgat = rkbgat
     c                   eval      d_badr = rkbadr
     c                   eval      d_bpnr = %editw(rkbpnr : '0   ')
     c     ' ':'0'       xlate     d_bpnr        d_bpnr
     c                   eval      d_land = rkland
     c                   eval      d_upnr = rkupnr
     c                   eval      d_ftnr = %editw(rkftnr : '        0')
     c                   eval      d_alfa = rkalfa
     c                   eval      d_penr = %editw(rkpenr : '          0')
     c                   eval      d_emal = rkemal
     c                   eval      d_weba = rkweba
     c                   eval      d_tlfn = rktlfn
     c                   eval      d_tlfx = rktlfx
     c                   eval      d_mobn = rkmobn
     c                   eval      d_bgir = %editw(rkbgir : '          0')
     c                   eval      d_eank = %editw(rkeank : '            0')
     c                   eval      d_avde = %triml(%editw(rkavde : '   0'))
     c                   eval      d_lagr = %triml(%editw(rklagr : ' 0'))
     c                   eval      d_slgr = %triml(%editw(rkslgr : '   0'))
     c                   eval      d_dist = %triml(%editw(rkdist : '   0'))
     c                   eval      d_betb = %triml(%editw(rkbetb : ' 0'))
     c                   eval      d_valk = rkvalk
     c                   eval      d_lbet = %triml(%editw(rklbet : ' 0'))
     c                   eval      d_sped = %triml(%editw(rksped : '  0'))
      *
     c                   eval      d_sper = rksprk
     c                   eval      d_pass = rkpass
     c                   eval      d_rent = %editw(rkrenk : '0')
     c     ' ':'0'       xlate     d_rent        d_rent
     c                   eval      w_kgre = rkgrns * 1000
     c                   eval      w_kgra = %editc(w_kgre : 'X')
     c                   eval      d_kgre = '+' + w_kgra
     c                   eval      d_kgsp = rkkgrk
     c                   eval      d_ktyp = %editw(rkkres : '0')
     c     ' ':'0'       xlate     d_ktyp        d_ktyp
     c                   eval      d_avgf = %editw(rkmkod : '0')
     c     ' ':'0'       xlate     d_avgf        d_avgf
      *
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      rkunl1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportKunder'
     c                   eval      w_rpgp = 'IT020R'
      *
     c                   endsr
