     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : VR743R
      * Beskrivelse . . : Varer linket opp mot vargr. for transport
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       103011 NHO  Nytt program
6.20  * 6.20  040512 bsa  Manglet ledetekst for velg
6.21  * 6.20  031014 bsa  Slår ikke opp med varetekst, da denne kan endres på
6.21  *                   ordren.
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  220916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * K000  140121 bsa  Hvis kode er satt, kan vi ikke avslutte med F3.
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.21 f*varl2    if   e           k disk    rename(vvarpfr:vvarl2r)
6.21 fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fvR743d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
7.01  *
7.01  * Definering av Local data area
7.01  *
7.01 d                uds
7.01 d l_filg                931    933
      *
      * Definering av parametre
      *
     d p_indi          s              1
     d p_lmat          s              2  0
     d p_suff          s                   like(fdsuff)
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_varenr        s                   like(vvvare)
     d p_tek1          s                   like(vvtek1)
6.NU d p_numm          s                   like(fdnumm)
     d p_ogrp          s                   like(vguogr)
     d p_hgrp          s                   like(vguhgr)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fodtl1_line     s                   like(fdline)
     d vvarl1_vare     s                   like(vvvare)
6.21 d vvarlr_vare     s                   like(vvvare)
6.21 d*vvarl2_tek1     s                   like(vvtek1)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(9)
      *
7.01 d u_701           s              1    inz(*off)
7.01  *
7.01  * Eksternt program
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
      * Avslutter dersom ingen poster på undergtruppe har 1
     c                   if        w_srrn = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
     c                   eval      p_indi = '0'
      *
     c                   select
     c                   when      *inkc
7.01 c                   if        u_701 = *off
     c                   eval      p_indi = '0'
     c***                eval      p_indi = '1'
     c                   goto      avslutt
7.01 c                   else
7.01 c                   goto      b2taga
7.01 c                   endif
     c                   when      *inkl
7.01 c                   if        u_701 = *off
     c                   eval      p_indi = '0'
     c                   goto      avslutt
7.01 c                   else
7.01 c                   goto      b2taga
7.01 c                   endif
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     fodtl1_key    setll     fodtl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_indi = '1'
     c                   eval      p_varenr = b1vare
     c                   eval      p_tek1 = b1tek1
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tes_subfile   begsr
      *
      * Leser ordrelinjer på gjeldende ordrenr
     c     nyles1        tag
      *
     c                   read      fodtl1
     c                   if        %eof or
     c                             fdfirm <> p_firm or
     c                             fdnumm <> p_numm
     c                   goto      slutt1
     c                   endif
     c                   eval      vvarl1_vare = fdvare
6.21 c**                 eval      vvarl2_tek1 = fdtek1
6.21 c                   eval      vvarlr_vare = fdvare
      *
6.21 c**   vvarl2_key    chain     vvarl2
6.21 c     vvarlr_key    chain     vvarlr
      *
      * Sjekk om vare har 'J' i feltet VGUTRA
      * ellers gå til ny les av ordrelinje
      *
     c                   eval      p_ogrp = vvogrp
     c                   eval      p_hgrp = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off and
     c                             vgutra = 1
     c                   eval      p_indi = 'N'
     c                   eval      *inlr = '1'
     c                   goto      slutt1
     c                   else
     c                   goto      nyles1
     c                   endif
      *
     c     slutt1        tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
      * Leser varer
     c     nyles         tag
      *
     c                   read      vvarl1
      *
     c                   if        %eof or
     c                             vvfirm <> p_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c
      *
      * Sjekk om vare har 'J' i feltet VGUTRA
      * ellers gå til ny les av ordrelinje
      *
     c                   eval      p_ogrp = vvogrp
     c                   eval      p_hgrp = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             90
      *
     c                   if        *in90 = *on
     c                   goto      nyles
     c                   endif
      *
     c                   if        *in90 = *off and
     c                             vgutra <> 1
     c                   goto      nyles
     c                   endif
      *
      *
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      vvarl1_vare = vvvare
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = vvvare
     c                   eval      b1tek1 = vvtek1
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_lmat
     c                   parm                    p_indi
     c                   parm                    p_varenr
      *
      * Key for posisjonering på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    p_ogrp
     c                   kfld                    p_hgrp
     c                   kfld                    vugrl1_uugr
      *
      *
      * Key for posisjonering på varenr
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på beskrivelse
      *
6.21 c*    vvarl2_key    klist
6.21 c*                  kfld                    w_firm
6.21 c*                  kfld                    vvarl2_tek1
6.21  *
6.21  * Key for posisjonering på varenr
6.21  *
6.21 c     vvarlr_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vvarlr_vare
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
     c                   kfld                    fodtl1_line
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     fodtl1_key    setll     fodtl1
      *
     c                   exsr      tes_subfile
      *
     c                   if        *inlr = '0'
     c                   eval      vvarl1_vare = *zeros
     c     vvarl1_key    setll     vvarl1
     c                   exsr      crt_subfile
     c                   endif
7.01  *
7.01  * Endring 7.01 Hvis koden er satt kan vi ikke avslutte uten å velge transportlinje
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'VR743R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
     c                   endsr
