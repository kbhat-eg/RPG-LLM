     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : asserv
      * Program . . . . : il110r
      * beskrivelse . . : Lønns-kalender, Vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsfdatrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020804 ss   Nytt program
5.30  *       170204 bø   lagt inn datotester
5.31  *       190406 ss   Lagt inn Regnskapsperiode pr interval
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     filkal1    if   e           k disk    rename(ilkapfr:ilkal1r)
     filkal2    if   e           k disk    rename(ilkapfr:ilkal2r)
     filkalr    if   e           k disk    rename(ilkapfr:ilkalrr)
     filkalu    uf a e           k disk    rename(ilkapfr:ilkalur)
      *
     fil110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ilkal1_lper     s                   like(ilkper)
     d ilkal2_fdat     s                   like(ilkfda)
     d ilkalr_lper     s                   like(ilkper)
     d ilkalu_lper     s                   like(ilkper)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_fraiso        s               d   datfmt(*iso)
     d w_tiliso        s               d   datfmt(*iso)
     d w_dager         s              5  0
     d w_aar           s              2  0
     d w_saar          s              2  0
     d w_dag           s              2  0
     d w_mnd           s              2  0
     d w_just          s              2  0
      *
     d s_lper          s                   like(ilkper)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lper <> 0 or
     c                             b2fdat <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     *dmy          test (d)                b1fdat                 33
      *
     c                   if        *in33 = *off
     c     *dmy          move      b1fdat        ilkal2_fdat
     c     ilkal2_key    setll     ilkal2
     c                   endif
     c                   other
     c                   eval      ilkal1_lper = b1lper
     c     ilkal1_key    setll     ilkal1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * lønnsperiode
      *
     c                   if        b2lper <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ilkal1_lper = b2lper
     c     ilkal1_key    setll     ilkal1
     c                   goto      end_pos
     c                   endif
      *
      * fra dato
      *
     c                   if        b2fdat <> 0
     c                   eval      w_seqe = 'B'
     c     *dmy          move      b2fdat        ilkal2_fdat
     c     ilkal2_key    setll     ilkal2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lper = 0
     c                   eval      b2fdat = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1lper = b1lper
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1lper = b1lper
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1lper = b1lper
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1lper = b1lper
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Legg ut perioder ut året med samme intervall i dager
      *
     c                   if        b1valg = 9
     c     *dmy          move      b1fdat        w_fraiso
     c     *dmy          move      b1tdat        w_tiliso
     c     w_tiliso      subdur    w_fraiso      w_Dager:*D
     c                   move      b1fdat        w_aar
     c                   move      b1fdat        w_saar
     c                   dow       w_saar = w_aar
     c                   if        w_dager > 26 and
     c                             w_dager < 32
     c     w_tiliso      adddur    01:*days      w_fraiso
     c     w_tiliso      adddur    01:*months    w_tiliso
     c                   eval      w_dag = %subdt(w_tiliso:*d)
     c                   eval      w_mnd = %subdt(w_tiliso:*m)
     c                   if        w_dag > 27
     c                   if        w_mnd = 02
     c                   eval      w_just = 28 - w_dag
     c                   adddur    w_just:*d     w_tiliso
     c                   else
     c                   if        w_mnd = 01 or
     c                             w_mnd = 03 or
     c                             w_mnd = 05 or
     c                             w_mnd = 07 or
     c                             w_mnd = 08 or
     c                             w_mnd = 10 or
     c                             w_mnd = 12
     c                   eval      w_just = 31 - w_dag
     c                   adddur    w_just:*d     w_tiliso
     c                   else
     c                   if        w_mnd = 04 or
     c                             w_mnd = 06 or
     c                             w_mnd = 09 or
     c                             w_mnd = 11
     c                   eval      w_just = 30 - w_dag
     c                   adddur    w_just:*d     w_tiliso
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   else
     c     w_tiliso      adddur    01:*days      w_fraiso
     c     w_fraiso      adddur    w_Dager:*days w_tiliso
     c                   endif
     c                   eval      b1lper = b1lper + 1
     c                   eval      ilkalu_lper = b1lper
     c     ilkalu_key    chain     ilkalu
     c                   eval      ilkper = b1lper
     c                   eval      ilkfda = w_fraiso
     c                   eval      ilktda = w_tiliso
s.ss c                   eval      ilkokp = b1lper
s.ss c                   move      w_mnd         ilkokp
     c     *dmy          move      w_tiliso      wdat              6 0
     c                   move      wdat          wdatx             6
     c                   move      *zero         wmm               2
     c                   eval      wmm = %subst(wdatx:3:2)
     c                   move      wmm           ilkokp
     c     *dmy          move      w_fraiso      b1fdat
     c                   move      b1fdat        w_saar
     c                   time                    ilkeda
     c                   time                    ilketi
     c                   eval      ilkusr = l_user
     c                   if        %found(ilkalu)
     c                   update    ilkalur
     c                   else
     c                   time                    ilkoda
     c                   write     ilkalur
     c                   endif
     c                   eval      b_forn = *on
     c                   enddo
      *
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1lper = b2lper
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1lper = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1lper = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ilkalr_lper = c1lper
     c     ilkalr_key    chain     ilkalr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1lper = 0
     c                   eval      c1oper = 0
     c                   eval      c1sper = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2lper = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ilkalr_lper = c1lper
     c     ilkalr_key    chain     ilkalr
      *
     c                   if        %found
     c     *dmy          move      ilkfda        c2fdat
     c     *dmy          move      ilktda        c2tdat
     c                   eval      c1oper = ilkokp
     c                   eval      c1sper = ilkpsp
     c                   eval      c2eusr = ilkusr
     c     *dmy          move      ilkoda        c2odat
     c     *dmy          move      ilkeda        c2edat
     c                   else
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c1oper = 0
     c                   eval      c1sper = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1lper = k1lper
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2fdat = 0
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          test (d)                c2fdat                 31
     c                   if        *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2tdat = 0
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          test (d)                c2tdat                 34
     c                   if        *in34 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ilkalur
      *
     c                   eval      ilkalu_lper = c1lper
     c     ilkalu_key    chain     ilkalur
      *
     c     *dmy          move      c2fdat        ilkfda
     c     *dmy          move      c2tdat        ilktda
s.ss c                   eval      ilkokp = c1oper
s.ss c                   eval      ilkpsp = c1sper
      *
     c                   time                    ilkeda
     c                   time                    ilketi
     c                   eval      ilkusr = l_user
      *
     c                   if        %found
     c                   update    ilkalur
     c                   else
     c                   eval      ilkper = c1lper
     c                   time                    ilkoda
     c                   write     ilkalur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c     *dmy          move      ilkfda        b1fdat
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ilkalr_lper = d1lper
     c     ilkalr_key    chain     ilkalr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ilkalu_lper = d1lper
     c     ilkalu_key    chain     ilkalur
      *
     c                   delete    ilkalur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_lper = k1lper
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ilkalr_lper = k1lper
     c     ilkalr_key    chain     ilkalr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1lper = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ilkalr_lper = k1lper
     c     ilkalr_key    chain     ilkalr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1lper = s_lper
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ilkal2
     c                   other
     c                   read      ilkal1
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ilkal2_fdat = ilkfda
     c                   other
     c                   eval      ilkal1_lper = ilkper
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1lper = ilkper
s.ss c                   eval      b1oper = ilkokp
s.ss c                   eval      b1sper = ilkpsp
     c     *dmy          move      ilkfda        b1fdat
     c     *dmy          move      ilktda        b1tdat
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ilkal2_key    setgt     ilkal2
     c                   readp     ilkal2
     c                   other
     c     ilkal1_key    setgt     ilkal1
     c                   readp     ilkal1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ilkal2
     c                   other
     c                   readp     ilkal1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ilkal2_fdat = ilkfda
     c                   other
     c                   eval      ilkal1_lper = ilkper
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ilkal2_key    setll     ilkal2
     c                   other
     c     ilkal1_key    setll     ilkal1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på lønnsperiode
      *
     c     ilkal1_key    klist
     c                   kfld                    ilkal1_lper
      *
      * Key for posisjonering på fdatrivelse
      *
     c     ilkal2_key    klist
     c                   kfld                    ilkal2_fdat
      *
      * Key for les på lønnsperiode
      *
     c     ilkalr_key    klist
     c                   kfld                    ilkalr_lper
      *
      * Key for oppdatering av lønnsperiode
      *
     c     ilkalu_key    klist
     c                   kfld                    ilkalu_lper
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ilkal1_lper = 0
     c     ilkal1_key    setll     ilkal1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
