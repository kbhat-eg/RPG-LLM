      /TITLE  ASOKON Flytting av kunder og posteringer
     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RK108R
      * Beskrivelse . . : Flytting av kunder og posteringer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160400 KOB  Nytt program
T5.30 * V5.30 140704 HFL  Firmanavn i skjermbildet
T5.30 * V5.30 020205 HFL  Sjekker om spesialavtale og notatblokk.
T5.30 * V5.40 140705 KOB  Endret slik av subfile viser kunder i alfarekkefølge  og med mulighet
      *                   til å vise navn/gateadresse og poststed/telefon
T5.30 * V5.40 140705 KOB  Lagt inn muligheter til å vis bare dubletter
T5.30 * V5.40 270106 GRo  Korr. feil v/ flytt til kundenr=0 eller "samme nr"
5.60  * R5.60 271107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      * R5.60 010409 KOB  Lagt inn flytting av KID-info
6.10  * V6.10 220909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 060510 BSA  Utvidet med sporingsfelter
6.20  * V6.20 140611 BSA  Memosaldo er flyttet ut på eget register
6.21  * V6.20 270616 BKV  Tok vekk if, som gjorde at div tester ikke kjørte
6.30  * R6.30 110914 bsa  Felt flyttet fra RKUN til RKME (rkkjal)
6.31  * V6.30 010616 nho  Kopiere notatblokk
7.00  *K00    220321 sst  Sperre denne rutina via NX
7.01  *       150622 mst  Kun kompilert på nytt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
6.20 frkmelu    uf a e           k disk    rename(rkmepfr:rkmelur)
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
     frkunlu    uf a e           k disk    rename(rkunpfr:rkunlur)
     frksolu    uf   e           k disk    rename(rksopfr:rksolur)
     frktrlu    uf a e           k disk    rename(rktrpfr:rktrlur)
T5.30frksal1    if   e           k disk    rename(rksapfr:rksal1r)
T5.30frksalu    uf   e           k disk    rename(rksapfr:rksalur)
T5.30frklalu    uf   e           k disk    rename(rklapfr:rklalur)
     fakidlu    uf   e           k disk    rename(akidpfr:akidlur)
      *
     frk108d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
T5.30d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d rkunl2_alfa     s                   like(rkalfa)
     d rkunlu_kund     s                   like(rkkund)
     d rksolu_skun     s                   like(rkskun)
     d rktrlu_kund     s                   like(rnkund)
     d rktrlu_bdat     s                   like(rnbdat)
     d rktrlu_tist     s                   like(rntist)
T5.30d rksal1_kund     s                   like(rkkund)
T5.30d rksalu_skun     s                   like(rkkund)
T5.30d rklalu_syst     s                   like(rxsyst)
T5.30d rklalu_pros     s                   like(rxpros)
T5.30d rklalu_akti     s                   like(rxakti)
T5.30d rklalu_kund     s                   like(rxkont)
6.31 d fØrste          s              1  0
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rkfirm)
     d w_kund_gml      s                   like(rkkund)
     d w_sald_gml      s                   like(rksald)
     d w_kjop_gml      s                   like(rkkjØp)
     d w_kjof_gml      s                   like(rkkjØf)
6.30 d w_kjoa_gml      s                   like(rkkjal)
     d w_mems_gml      s                   like(rkmems)
5.60 d w_mema_gml      s                   like(rkmema)
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
     d ant             s              2  0
     d w_ikke          s              1
T5.00d w_navk          s              1
T5.00d w_gatk          s              1
T5.00d w_ponk          s              1
T5.00d w_tlfk          s              1
T5.00d w_mobk          s              1
T5.00d w_ftnk          s              1
T5.00d xsrobk          s              1
      *
     d w_kund          s                   like(rkkund)
     d w_alfa          s                   like(rkalfa)
     d w_navn          s                   like(rknavn)
     d w_gate          s                   like(rkgate)
     d w_sted          s                   like(rksted)
     d w_ponr          s                   like(rkponr)
     d w_tlfn          s                   like(rktlfn)
     d w_mobn          s                   like(rkmobn)
     d w_ftnr          s                   like(rkftnr)
     d w_kkto          s                   like(rkkund)
7.00 d w_sp            s              1
7.00 d w_rut           s             50
7.00 d w_l             s              3  0
7.00 d w_p             s              3  0
7.00 d w_m1            s             30
7.00 d w_m2            s             30
      *
     d p_valg          s              1  0
     d p_kund          s                   like(rkkund)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Datastruktur for kid-feltet
      *
     d                 ds
     d d_kkid                        21
     d  d_kkus                       20    overlay(d_kkid)
     d   d_kfir                       3  0 overlay(d_kkus)
     d   d_kkod                       1  0 overlay(d_kkus:4)
     d   d_kled                       4  0 overlay(d_kkus:5)
     d   d_kkto                       6  0 overlay(d_kkus:9)
     d   d_kbil                       6  0 overlay(d_kkus:15)
     d  d_kmod                        1  0 overlay(d_kkid:21)
      *
      /eject
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bilde K1WIN
      *             er referert til dette.
      * V1WIN  - V1 Vindu for utvalg av dubletter
      *
7.00  * ___________________________________________________________________
7.00  *  Sjekk om filgruppa er sperret for bruk av dette programmet
7.00  *
7.00 c                   eval      w_rut = 'SPERRE_FLYTTING_KUND/LEV'
7.00 c                   call      'TESTSPERRE'
7.00 c                   parm                    w_sp
7.00 c                   parm                    w_rut
7.00 c                   if        w_sp = '1'
7.00 c                   eval      w_m1 = 'Denne rutina er sperret for'
7.00 c                   eval      w_m2 = 'denne filgruppa, Trykk Ok'
7.00 c                   eval      w_l  = 5
7.00 c                   eval      w_p  = 30
7.00 c                   call      'AA011C'
7.00 c                   parm                    w_l
7.00 c                   parm                    w_p
7.00 c                   parm                    w_m1
7.00 c                   parm                    w_m2
7.00 c                   goto      avslutt
7.00 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F11=Bytter på å vise informasjon i subfien
      * skifter mellom navn og gateadresse
      *
     c                   if        *inkk = *on
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
T5.30c                   else
     c                   eval      *in80 = *on
T5.30c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * F7=Bytter på å vise alle eller bare kunder med dubletter
      *
     c                   if        *inkg = *on
      *
     c                   write     s1win
      *
     c                   exfmt     v1win
     c                   eval      w_navk = v1navn
     c                   eval      w_gatk = v1gate
     c                   eval      w_ponk = v1ponr
     c                   eval      w_tlfk = v1tlfn
     c                   eval      w_mobk = v1mobn
     c                   eval      w_ftnk = v1ftnr
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
T5.30c                   else
     c                   eval      *in81 = *on
T5.30c                   endif
      *
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kund <> 0 or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     rkunl2_key    setll     rkunl2
     c                   other
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på kundenummer
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = ' '
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    setll     rkunl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundenavn
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rkunl2_alfa = b2alfa
     c     rkunl2_key    setll     rkunl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kund = 0
     c                   eval      b2alfa = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      p_valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      p_valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Flytte post
      *
     c                   if        b1valg = 7
     c                   eval      k1kund = b1kund
T5.30c                   exsr      sjekk_sp
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
T5.30 * Subrutine for sjekk om det finnes spesialavtaler eller
T5.30 * notatblokk på kunden.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
T5.30c     sjekk_sp      begsr
      *
     c                   eval      b_anul = *off
     c                   eval      *in78 = *off
     c                   eval      *in79 = *off
      *
      * Finnes spesialavtale på kunden?
      *
     c                   eval      rksal1_kund = k1kund
     c     rksal1_key    chain     rksal1                             93
      *
     c                   if        *in93 = *off
     c                   eval      *in78 = *on                                               .
     c                   endif                                                           .....
      *
      * Finnes notatblokk på kunden?
      *
     c                   eval      rklalu_syst = 'K'                                         .
6.31 c***                eval      rklalu_pros = *zeros                                      .
6.31 c                   eval      rklalu_pros = *blanks                                     .
     c                   eval      rklalu_akti = *blanks                                     .
     c                   eval      rklalu_kund = k1kund                                      .
     c     rklalu_key    setll     rklalu                                 94
      *
     c                   if        *in94 = *on
     c                   eval      *in79 = *on                                               .
     c                   endif                                                           .....
      *
     c                   if        *in78 = *on or
     c                             *in79 = *on
     c                   exfmt     c2msg
     c                   endif
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_sjekksp
     c                   endif
      *
     c     end_sjekksp   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
      * Vedlikehold av kunderegisteret
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
      *
     c                   eval      p_kund = b1kund
     c                   call      'RK101R'
     c                   parm                    p_kund
     c                   parm                    p_valg
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 7
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_kund_gml = k1kund
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rkunl1_kund = k1kund
     c     rkunl1_key    chain     rkunl1                             90
6.20  * Henter inn memosaldo informasjon
6.20 c     rkunl1_key    chain     rkmel1                             91
      *
      * Ta vare på saldi m.v. fra gammel kunde
      *
     c                   eval      w_sald_gml = rksald
     c                   eval      w_kjop_gml = rkkjØp
     c                   eval      w_kjof_gml = rkkjØf
     c                   eval      w_mems_gml = rkmems
5.60 c                   eval      w_mema_gml = rkmema
6.30 c                   eval      w_kjoa_gml = rkkjal
      *
     c                   write     s1win
      *
     c     k1tagb        tag
      *
6.31  *
6.31 c                   if        fØrste = 0
6.31 c                   add       1             fØrste
6.31 c                   eval      rknota = 'N'
6.31 c                   endif
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
6.21 c*                  if        *inke
6.21 c*                  goto      k1tagc
6.21 c*                  endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1kund = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Adder til eksisterende kunde
      *
     c                   if        *inke
      *
      * Sjekker om fra og til kundenr er likt
      *
     c                   if        k1kund = w_kund_gml
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker at kunde finnes
      *
     c                   eval      rkunl1_kund = k1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found(rkunl1)
     c                   eval      *in34 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   goto      k1tagc
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rkunl1_kund = k1kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kopier til nytt kundenummer
      *
     c                   eval      rkkund = k1kund
6.10 c                   time                    rkndat
6.10 c                   time                    rkntim
6.10 c                   eval      rknsig = l_user
6.10 c                   time                    rkedat
6.10 c                   time                    rketim
6.10 c                   eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK108R    '
     c                   write     rkunlur
6.20  * Lag memosaldo
6.20 c                   eval      rkfirm = l_firm
6.20 c                   eval      rkkund = k1kund
6.20 c                   eval      rkmems = *zeros
6.20 c                   eval      rkmema = *zeros
6.30 c                   eval      rkkjal = *zeros
6.20 c                   time                    rkmeda
6.20 c                   time                    rkmeti
6.20 c                   eval      rkmeus = l_user
6.20 c                   time                    rkmoda
6.20 c                   time                    rkmoti
6.20 c                   write     rkmelur
      *
      * Oppdater søketekst
      *
     c                   call      'RK750R'
     c                   parm                    w_firm
     c                   parm                    k1kund
      *
     c                   goto      k1tagd
      *
      * Kopiering til eksisterende kunde
      *
     c     k1tagc        tag
      *
     c                   eval      rkunlu_kund = k1kund
     c     rkunlu_key    chain     rkunlu                             90
     c                   if        *in90 = *off
     c                   eval      rksald = rksald + w_sald_gml
     c                   eval      rkkjØp = rkkjØp + w_kjop_gml
     c                   eval      rkkjØf = rkkjØf + w_kjof_gml
6.20 c**                 eval      rkmems = rkmems + w_mems_gml
6.20 c**                 eval      rkmema = rkmema + w_mema_gml
6.10 c                   time                    rkedat
6.10 c                   time                    rketim
6.10 c                   eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK108R    '
     c                   update    rkunlur
6.20  * Oppdaterer memosaldo
6.20 c                   eval      rkunlu_kund = k1kund
6.20 c     rkunlu_key    chain     rkmelu                             91
6.20 c                   if        *in91 = *off
6.20 c                   eval      rkmems = rkmems + w_mems_gml
6.20 c                   eval      rkmema = rkmema + w_mema_gml
6.30 c                   eval      rkkjal = rkkjal + w_kjoa_gml
6.20 c                   update    rkmelur
6.20 c                   endif
     c                   endif
      *
     c     k1tagd        tag
      *
      * Flytt poster i andre delsystemer
      *
     c                   call      'RK790C'
     c                   parm                    w_firm
     c                   parm                    w_kund_gml
     c                   parm                    k1kund
      *
      * Flytt transaksjoner
      *
     c                   exsr      xk2tra
      *
      * Flytt kid-poster
      *
     c                   exsr      xk2kid
      *
      * Slett gammel kunde med søketekst
      *
     c                   eval      rkunlu_kund = w_kund_gml
     c     rkunlu_key    chain     rkunlu                             90
     c                   if        *in90 = *off
      * Søketekst
     c                   eval      rksolu_skun = w_kund_gml
     c     rksolu_key    chain     rksolu                             91
     c                   if        *in91 = *off
     c                   delete    rksolur
     c                   endif
      *
T5.30 * Spesialavtaler
T5.30c                   eval      rksalu_skun = w_kund_gml
T5.30c     rksalu_key    chain     rksalu                             91
T5.30c                   if        *in91 = *off
T5.30c                   delete    rksalur
T5.30c                   endif
      *
T5.30 * Notatblokk
6.31  *
6.31 c                   if        rknota = 'J'
6.31 c                   exsr      nota
6.31 c                   goto      etnot
6.31 c                   endif
6.31  *
T5.30c                   eval      rklalu_syst = 'K'                                         .
6.31 c                   eval      rklalu_pros = *blanks                                     .
T5.30c                   eval      rklalu_akti = *blanks                                     .
T5.30c                   eval      rklalu_kund = w_kund_gml                                  .
T5.30c     rklalu_key    setll     rklalu                                 94
      *
T5.30c                   if        *in94 = *on
T5.30c     rklalu_key    reade     rklalu                                 95
T5.30c                   dow       *in95 = *off
T5.30c                   delete    rklalur
T5.30c     rklalu_key    reade     rklalu                                 95
T5.30c                   enddo                                                           .....
     c                   endif
6.31  *
6.31 c     etnot         tag
6.31  *
6.20  * Sletter memosaldo
6.20 c     rkunlu_key    chain     rkmelu                             91
6.20 c                   if        *in91 = *off
6.20 c                   delete    rkmelur
6.20 c                   endif
      * Sletter kunden
     c                   delete    rkunlur
     c                   endif
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle posteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk2tra        begsr
      *
      * Leser kundeposteringer
      *
     c                   eval      rktrlu_kund = w_kund_gml
     c     rktrlu_key    setll     rktrlu
     c     rktrlu_key    reade     rktrlu                                 91
     c                   eval      w_tell = *zeros
      *
     c                   dow       *in91 = *off
      *
      * Teller/Timestamp
      *
     c                   if        w_tell = 999999
     c                   eval      w_tell = 0
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rntist
     c     *iso0         move      rntist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rntist
      *
     c                   eval      rnkund = k1kund
      *
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
      *
     c     rktrlu_key    reade     rktrlu                                 91
      *
     c                   enddo
      *
     c     end_k2tra     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kid-poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk2kid        begsr
      *
      * Leser kid-poster
      *
     c     akidlu_key    setll     akidlu
     c     akidlu_key    reade     akidlu                                 92
      *
     c                   dow       *in92 = *off
      *
     c                   movel     akkidd        d_kkid
      *
     c                   if        d_kkid <> ' '
     c                   if        d_kkto = w_kund_gml
     c                   eval      d_kkto = k1kund
     c                   eval      akkidd = d_kkid
     c                   update    akidlur
     c                   endif
     c                   endif
      *
     c     akidlu_key    reade     akidlu                                 92
      *
     c                   enddo
      *
     c     end_k2kid     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rkunl2                                 15
     c                   other
     c                   read      rkunl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        rkfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c   81              do
      *
      * Konverter til store bokstaver
      *
     c     lo:up         xlate     rknavn        rknavn
     c     lo:up         xlate     rkgate        rkgate
      *
     c                   exsr      xsrkont
      *
     c                   if        w_ikke = ' '
     c                   eval      ant = ant + 1
     c                   else
     c                   eval      ant = 1
     c                   endif
      *
     c                   select
     c                   when      ant = 1
     c                   eval      w_kund = rkkund
     c                   eval      w_navn = rknavn
     c                   eval      w_gate = rkgate
     c                   eval      w_sted = rksted
     c                   eval      w_ponr = rkponr
     c                   eval      w_tlfn = rktlfn
     c                   eval      w_mobn = rkmobn
     c                   eval      w_ftnr = rkftnr
      *
     c                   when      ant = 2
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1alfa = w_alfa
     c                   eval      b1kund = w_kund
     c   80              eval      b1navn = w_navn
     c   80              eval      b1navn = w_gate
     c   80              eval      b1sted = w_sted
     c   82              eval      b1sted = %trim(w_tlfn)+'  '+%trim(w_mobn)
     c   83              eval      b1sted = ' '
     c   83              movel     w_ftnr        b1sted
     c                   write     b1sfl
      *
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1alfa = rkalfa
     c                   eval      b1kund = rkkund
     c   80              eval      b1navn = rknavn
     c   80              eval      b1navn = rkgate
     c   80              eval      b1sted = rksted
     c   82              eval      b1sted = %trim(rktlfn)+'  '+%trim(rkmobn)
     c   83              eval      b1sted = ' '
     c   83              movel     rkftnr        b1sted
     c                   write     b1sfl
      *
     c                   when      ant > 2
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1alfa = rkalfa
     c                   eval      b1kund = rkkund
     c  n80              eval      b1navn = rknavn
     c   80              eval      b1navn = rkgate
     c  n80              eval      b1sted = rksted
     c   82              eval      b1sted = %trim(rktlfn)+'  '+%trim(rkmobn)
     c   83              eval      b1sted = ' '
     c   83              movel     rkftnr        b1sted
     c                   write     b1sfl
     c                   endsl
      *
     c                   eval      w_alfa = rkalfa
     c                   enddo
      *
     c  n81              do
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1valg = 0
     c                   eval      b1alfa = rkalfa
     c                   eval      b1kund = rkkund
     c  n80              eval      b1navn = rknavn
     c   80              eval      b1navn = rkgate
     c  n80              eval      b1sted = rksted
     c   80              eval      b1sted = %trim(rktlfn)+'  '+%trim(rkmobn)
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setgt     rkunl2
     c                   readp     rkunl2
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setgt     rkunl1
     c                   readp     rkunl1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     rkunl2
     c                   other
     c                   readp     rkunl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rkfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rkunl2_alfa = rkalfa
     c                   other
     c                   eval      rkunl1_kund = rkkund
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setll     rkunl2
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsrkont       begsr
      *
     c                   eval      w_ikke = ' '
      *
      * Sjekk navn
      *
     c                   if        w_navk = 'J'
     c                   eval      *in80 = *on
     c                   if        rknavn <> w_navn
     c                   eval      w_ikke = 'J'
     c                   endif
     c                   endif
      *
      * Sjekk gateadresse
      *
     c                   if        w_gatk = 'J'
     c                   eval      *in80 = *on
     c                   if        rkgate <> w_gate and
     c                             rkgate <> *blank
     c                   eval      w_ikke = 'J'
     c                   endif
     c                   endif
      *
      * Sjekk postnummer
      *
     c                   if        w_ponk = 'J'
     c                   eval      *in80 = *on
     c                   if        rkponr <> w_ponr
     c                   eval      w_ikke = 'J'
     c                   endif
     c                   endif
      *
      * Sjekk telefonnummer
      *
     c                   if        w_tlfk = 'J'
     c                   eval      *in82 = *on
     c                   if        rktlfn <> w_tlfn and
     c                             rktlfn <> *blank
     c                   eval      w_ikke = 'J'
     c                   endif
     c                   endif
      *
      * Sjekk mobilnummer
      *
     c                   if        w_mobk = 'J'
     c                   eval      *in82 = *on
     c                   if        rkmobn <> w_mobn and
     c                             rkmobn <> *blank
     c                   eval      w_ikke = ' '
     c                   endif
     c                   endif
      *
      * Sjekk foretaksnummer
      *
     c                   if        w_ftnk = 'J'
     c                   eval      *in83 = *on
     c                   if        rkftnr = w_ftnr and
     c                             rkftnr <> 0
     c                   eval      w_ikke = ' '
     c                   endif
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på alfa
      *
     c     rkunl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl2_alfa
      *
      * Key for oppdatering av kundenummer
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Key for oppdatering av kundesøketekstregister
      *
     c     rksolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksolu_skun
      *
      * Key for oppdatering av kundeposter
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
      *
T5.30 * Key for avtalespesifikasjon
      *
T5.30c     rksal1_key    klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    rksal1_kund
      *
T5.30 * Key for avtalespesifikasjon
      *
T5.30c     rksalu_key    klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    rksalu_skun
      *
T5.30 * Key for notatblokk
      *
T5.30c     rklalu_key    klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    rklalu_syst
T5.30c                   kfld                    rklalu_pros
T5.30c                   kfld                    rklalu_akti
T5.30c                   kfld                    rklalu_kund
      *
      * Key for kid
      *
     c     akidlu_key    klist
     c                   kfld                    w_firm
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_seqe = 'B'
      *
     c                   eval      *in22 = *on
      *
     c                   eval      w_alfa = ' '
     c                   eval      w_kund = 0
     c                   eval      ant = 0
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rkunl1_kund = 0
     c     rkunl1_key    setll     rkunl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
6.31 c     nota          begsr
6.31 c                   eval      rklalu_syst = 'K'                                         .
6.31 c                   eval      rklalu_pros = *blanks                                     .
6.31 c                   eval      rklalu_akti = *blanks                                     .
6.31 c                   eval      rklalu_kund = w_kund_gml                                  .
6.31 c     rklalu_key    setll     rklalu                                 94
6.31  *
6.31 c                   if        *in94 = *on
6.31 c     rklalu_key    reade     rklalu                                 95
6.31 c                   dow       *in95 = *off
6.31 c                   eval      rxkont = k1kund
6.31 c                   update    rklalur
6.31 c     rklalu_key    reade     rklalu                                 95
6.31 c                   enddo                                                           .....
6.31 c                   endif
6.31 c                   endsr
**
  Ny post                  001
  Endring                  002
  Vise                     003
