     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : SO700R
      * Beskrivelse . . : Generere ordre fra historisk faktura register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180601 AMD  Nytt program.
5.01  * R5.01 241002 AMD  Lagt om rutiner for oppdatering av lager
5.31  * R5.31 191203 AMD  Utvidet med funksjoner for almenning
5.32  * R5.32 100104 ASK  Lagt inn endringer for lager på linjenivå.
5.33  * R5.33 080904 AMD  Lagt inn ordretype for nummerserie (se dok)
5.40  * R5.40 090605 AMD  Negative varelinjer ble lagt ut igjen med +
      *                   i ordreregister. Dette er korrigert.
      *       080805 HKO  Fjernet nettovekt og bruttovekt fra ordre-
      *                   hode og faktura-hode
5.41  * R5.41 170106 AMD  Innhenting fra kreditnota ble feil, her ble
      *                   fortegn snudd motsatt. (5.41 erstatter 5.40)
5.50  * R5.50 130907 BOF  Tar med fakturatype til ordre
5.60  * R5.60 091107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.11  110610 bof  Justert i utvidelse av parm VL001R
6.20  * 6.20  151210 nho  Underprosjekt må legges ut
6.21  * 6.20  180612 bsa  Henter inn NOBB og lev. varenummer. Ligger ikke
6.21  *                   på historisk lagerregister. Kan sansynligvis
6.21  *                   fjernes på versjon 6.30.
6.22  * 6.20  090413 amd  Koder for undertykking av gebyrer blir ikke
6.22  *                   overført til historisk fakturaregister.
6.22  *                   Legger derfor inn sjekk mot kunderegister
6.22  *                   for å få satt disse koder i genererte ordre.
6.23  * 6.20  071113 bsa  Skal ikke kopiere med seg EDI referansen.
6.24  * 6.20  100714 bof  Legge riktig refr. til historikk
6.25  * 6.20  271114 bkv  Sjekk på om er skaffevare
6.26  * 6.20  110215 bof  Bet.bet. skal tas fra kunde
6.27  * 6.20  280915 BOF  Lagt inn ryddeprogram ved oppr.av ordre
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  061014 BKV  Henter overstyrt info fra kundeprosjekt
6.31  * 6.30  040615 bkv  Ordre økt fra 6 til 8
      *
7.01  * R6.20 080813 bsa  Skal ikke kopiere med referansen til EDI meldinga
      *
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  * R5.42 191107 ASK  Legger ned påslagsinformasjon ved gjenoppretting
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsodtl1    if   e           k disk    rename(sodtpfr:sodtl1r)
5.41 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
5.50 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
6.25 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.30 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av datastruktur
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(sofirm)
     d p_aarr          s                   like(soaarr)
     d p_binr          s                   like(sobinr)
     d p_otyp          s                   like(sootyp)
6.21 d p_vare          s                   like(sdvare)
6.21 d p_nobb          s                   like(fdnobb)
6.21 d p_lvar          s                   like(fdlvar)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(sofirm)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d sohel1_suff     s                   like(sosuff)
     d sodtl1_aarr     s                   like(sdaarr)
     d sodtl1_binr     s                   like(sdbinr)
     d fohelu_numm     s                   like(sonumm)
     d fohelu_suff     s                   like(sosuff)
     d fodtlu_numm     s                   like(sdnumm)
     d fodtlu_suff     s                   like(sdsuff)
     d fodtlu_line     s                   like(sdline)
5.41 d votyl1_otyp     s                   like(vaotyp)
5.50 d rkunl1_kund     s                   like(rkkund)
6.25 d vvarl1_vare     s                   like(vvvare)
6.30 d fkprl1_kund     s                   like(flkund)
6.30 d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_kode          s              1
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
     d w_kund          s                   like(sokund)
     d w_numm          s                   like(sonumm)
     d w_line          s                   like(sdline)
5.33 d w_onum          s              2
6.nu d w_lrec          s            128
     d wv001r          s              1
5.01 d w_oakk          s              1  0
6.31 d w_numa          s              8
6.24 d w_lina          s              4
6.25 d b_skaf          s              1    inz(*off)
6.30 d w_ekgb          s                   like(rkekgb)
6.30 d w_fagb          s                   like(rkfagb)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra historisk faktura-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Henter faktura
      *
     c     sohel1_key    setll     sohel1
     c                   read      sohel1                                 90
     c                   eval      w_kund = sokund
     c                   eval      fdkund = sokund
5.41  *
5.41  *  Finner ordretypen
5.41  *
5.41 c                   eval      votyl1_otyp = sootyp
5.41 c     votyl1_key    chain     votyl1r
5.50  *
5.50  *  Finner kunde - fakturatype
5.50  *
5.50 c                   eval      rkunl1_kund = sokund
5.50 c     rkunl1_key    chain     rkunl1r
6.30 c                   eval      w_ekgb = rkekgb
6.30 c                   eval      w_fagb = rkfagb
6.30  * Slå opp på kundeprosjekt for å finne om prosjekt er
6.30  * overstyrer verdi på kunde
6.30 c                   if        sokpro > 0
6.30 c                   eval      fkprl1_kund = sokund
6.30 c                   eval      fkprl1_kpro = sokpro
6.30 c     fkprl1_key    chain     fkprl1
6.30 c                   if        %found(fkprl1)
6.30 c                   eval      w_ekgb = flekgb
6.30 c                   eval      w_fagb = flfagb
6.30 c                   endif
6.30 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Generering av ordre     ORDRE-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_line = *zero
     c     sodtl1_key    setll     sodtl1r
     c     sodtl1_key    reade     sodtl1r                                90
      *
     c                   dow       *in90 = *off
      *
6.25 c                   eval      b_skaf = *off
6.25 c                   eval      vvarl1_vare = sdvare
6.25 c     vvarl1_key    chain     vvarl1
6.25 c                   if        not %found
6.25 c                   eval      b_skaf = *on
6.25 c                   endif
      *
     c                   eval      w_line      = w_line + 10
     c                   eval      fodtlu_line = w_line
     c     fodtlu_key    chain     fodtlur                            92
     c                   if        *in92 = *on
      *
      * Flytter over  opplysninger for ordrelinje
      *
     c                   exsr      flytt_line
      *
      * Oppdater ordrelinje-register
      *
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = fodtlu_numm
     c                   eval      fdsuff = fodtlu_suff
     c                   eval      fdline = fodtlu_line
     c                   eval      fdotyp = p_otyp
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
     c                   exsr      opplag
     c                   endif
      *
     c                   endif
      *
     c     sodtl1_key    reade     sodtl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Generering av ordre     ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger ut igjen ordren med nytt ordrenummer
      *
     c     fohelu_key    chain     fohelur                            92
      *
     c                   if        *in92 = *on
      *
      * Flytter over opplysninger for ordrehode
      *
     c                   exsr      flytt_hode
      *
      * Legger inn dagens dato
      *
     c                   time                    fodate
     c                   time                    fotime
     c                   time                    fobdat
      *
      * Oppdater ordre-hode-register
      *
     c                   eval      fofirm = w_firm
     c                   eval      fonumm = fohelu_numm
     c                   eval      fosuff = fohelu_suff
     c                   eval      foline = *zero
     c                   eval      footyp = p_otyp
6.10 c                   time                    fodate
6.10 c                   time                    fotime
6.10 c                   eval      fooous = l_user
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   write     fohelur
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sluttbehandling av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Rekalkulering for oppdatering av ordrehode
      *
     c                   unlock    fohelu
     c                   unlock    fodtlu
      *
      * Kaller opp subprogram
      *
     c                   call      'FO700R'
     c                   parm                    w_firm
6.nu c                   parm                    fohelu_numm
     c                   parm                    fohelu_suff
      *
      * Nytt oppslag mot ordre-hode for å få med
      * ajourførte tall. (Etter oppdatering i FO700R)
      *
     c     fohelu_key    chain     fohelur                            92
     c                   unlock    fohelu
      *
      * Oppdatering av memo-saldo
      *
     c                   eval      w_sald = fotots - fototr
5.60 c                   eval      w_sal2 = fobrra
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    p_otyp
     c                   parm                    w_kund
     c                   parm                    w_sald
5.60 c                   parm                    w_sal2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   move      fdvare        hlvare
5.32 c                   eval      hllage = fdlage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = p_otyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
6.24 c                   eval      hlrefr = *blank
6.24 c                   move      fdnumm        w_numa
6.24 c                   move      fdline        w_lina
6.24 c                   eval      hlrefr = 'O:' + w_numa +
6.24 c                                      ' L:' + w_lina
6.24 c*                  eval      hlrefr = 'O:' + %char(fdnumm) +
6.24 c*                                     'L:' + %char(fdline)
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    wv001r
6.nu c                   parm                    w_lrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = faaork
     c                   eval      w_type = p_otyp
     c                   eval      w_fast = l_wsid
5.33  *
5.33  * Hente inn eventuell overstyrt
5.33  * ordretype for nummerserie
5.33  *
5.33 c                   call      'VA752R'
5.33 c                   parm                    w_firm
5.33 c                   parm                    p_otyp
5.33 c                   parm                    w_onum
5.33  *
5.33 c                   if        w_onum <> *blank
5.33 c                   eval      w_type = w_onum
5.33 c                   endif
      *
      * Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_numm
6.27  *
6.27  * sjekker at ordrenr ikke har tidligere relasjoner liggende
6.27  *
6.27 c                   call      'FO415R '
6.27 c                   parm                    w_firm
6.27 c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Flytter over felter for Ordre-linje-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_line    begsr
      *
5.41  * Snu fortegn dersom det hentes inn fra kreditnota
      *
5.41 c                   if        vaokre = '-'
5.41 c                   eval      sdanta = sdanta * -1
5.41 c                   eval      sdsalg = sdsalg * -1
5.41 c                   eval      sdkost = sdkost * -1
5.41 c                   eval      sdrkr1 = sdrkr1 * -1
5.41 c                   eval      sdrkr2 = sdrkr2 * -1
5.41 c*** kun for        eval      sdbrk1 = sdbrk1 * -1
5.41 c*** dok.sjon       eval      sdbrk2 = sdbrk2 * -1
5.41 c                   eval      sdrkro = sdrkro * -1
5.41 c                   endif
      *
     c                   eval      fdltyp = sdltyp
     c                   eval      fdlety = sdlety
     c                   eval      fdvare = sdvare
     c                   eval      fdenh1 = sdenh1
     c                   eval      fdlage = sdlage
     c                   eval      fdkpri = sdkpri
     c                   eval      fdavde = sdavde
     c                   eval      fdkont = sdkont
     c                   eval      fdspes = sdspes
     c                   eval      fdproj = sdproj
     c                   eval      fdakti = sdakti
5.31 c                   eval      fdalme = sdalme
5.31 c                   eval      fdbrty = sdbrty
5.31 c                   eval      fdpriv = sdpriv
     c                   eval      fdtek1 = sdtek1
     c                   eval      fdtek2 = sdtek2
     c                   eval      fdanta = sdanta
     c                   eval      fdoppr = sdoppr
     c                   eval      fdsapr = sdsapr
     c                   eval      fdkopr = sdkopr
     c                   eval      fdrab1 = sdrab1
     c                   eval      fdrab2 = sdrab2
5.31 c                   eval      fdbrr1 = sdbrr1
5.31 c                   eval      fdbrr2 = sdbrr2
     c                   eval      fdpasl = sdpasl
     c                   eval      fdrkr1 = sdrkr1
     c                   eval      fdrkr2 = sdrkr2
     c                   eval      fdsums = sdsalg
     c                   eval      fdsumr = sdrkr1 + sdrkr2 + sdrkro
     c                   eval      fdsumk = sdkost
5.31 c                   eval      fdomva = sdomva
6.25 c*                  eval      fdlvre = sdlvre
6.25 c                   if        b_skaf = *on
6.25 c                   eval      fdlvre = 1
6.25 c                   else
6.25 c                   eval      fdlvre = 0
6.25 c                   end
     c                   eval      fdogrp = sdogrp
     c                   eval      fdhgrp = sdhgrp
     c                   eval      fdugrp = sdugrp
     c                   eval      fdedih = sdedih
7.01 c**                 eval      fdedil = sdedil
7.01 c                   eval      fdedih = 0
8.02 c                   eval      fdpslm = sdpslm
6.21  * Hent inn nobb og lev.varenummer
6.21 c                   eval      p_vare = sdvare
6.21 c                   eval      p_nobb = *zeros
6.21 c                   eval      p_lvar = *blanks
6.21 c                   call      'VV710R'
6.21 c                   parm                    p_vare
6.21 c                   parm                    p_nobb
6.21 c                   parm                    p_lvar
6.21  *
6.21 c                   eval      fdnobb = p_nobb
6.21 c                   eval      fdlvar = p_lvar
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Flytter over felter for Ordre-hode-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_hode    begsr
      *
     c                   eval      folety = solety
     c                   eval      folage = solage
6.26 c                   eval      fobetb = rkbetb
6.26 c**                 eval      fobetb = sobetb
     c                   eval      foselg = soselg
     c                   eval      fodist = sodist
     c                   eval      folmat = solmat
     c                   eval      folbet = solbet
     c                   eval      fovalk = sovalk
     c                   eval      forkat = sorkat
     c                   eval      fosped = sosped
     c                   eval      fokjor = sokjor
     c                   eval      fomkod = somkod
     c                   eval      folkun = solkun
     c                   eval      fokund = sokund
     c                   eval      fokuna = sokuna
     c                   eval      fonavn = sonavn
     c                   eval      foadr1 = soadr1
     c                   eval      foadr2 = soadr2
     c                   eval      fosted = sosted
     c                   eval      foavde = soavde
     c                   eval      fokont = sokont
     c                   eval      fospes = sospes
     c                   eval      foproj = soproj
6.20 c                   eval      foupro = soupro
     c                   eval      foakti = soakti
     c                   eval      fovref = sovref
     c                   eval      fodref = sodref
     c                   eval      forekv = sorekv
     c                   eval      folmtx = solmtx
     c                   eval      folbtx = solbtx
     c                   eval      forabp = sorabp
     c                   eval      fopasl = sopasl
     c                   eval      fopask = sopask
     c                   eval      fokoli = sokoli
5.50 c                   eval      fofaty = rkfaty
6.22  *
6.22  * Sjekker koder for gebyrer i kunderegister
6.22  * og oppdaterer ordrehode med dette
6.22  *
6.30 c                   eval      fokgfa = w_fagb
6.30 c                   eval      fokgek = w_ekgb
6.22 c                   eval      fokgfr = rkfrgb

      *
      * Totalsummer blir rekalkulert
      * utfra varelinjer
      *
     c                   eval      fototx = 0
     c                   eval      fotots = 0
     c                   eval      fototr = 0
     c                   eval      fototk = 0
      *
     c                   eval      fouskr = souskr
     c                   eval      fowsid = sowsid
     c                   eval      fouser = souser
6.23 c**                 eval      foedih = soedih
6.23 c                   eval      foedih = 0
     c                   eval      fokpro = sokpro
      *
      * Nullstiller diverse felter
      *
     c                   eval      foldat = *loval
     c                   eval      fopdat = *loval
     c                   eval      fofkda = *loval
     c                   eval      foffda = *loval
     c                   eval      fotdat = *loval
     c                   eval      foadat = *loval
     c                   eval      fobinr = *zero
     c                   eval      fokref = *zero
     c                   eval      fokfak = *zero
     c                   eval      fokspr = *zero
5.01  *
5.01  * Dersom behandlingskode 2 eller 3,
5.01  * skal det settes kode for at lager
5.01  * er oppdatert, i motsatt fall, ikke.
5.01  *
5.01 c                   call      'VA750R'
5.01 c                   parm                    w_firm
5.01 c                   parm                    p_otyp
5.01 c                   parm                    w_oakk
5.01  *
5.01 c                   if        w_oakk = 2 or
5.01 c                             w_oakk = 3
5.01 c                   eval      foolag = 1
5.01 c                   else
5.01 c                   eval      foolag = 0
5.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_sald = *zero
5.60 c                   eval      w_sal2 = *zero
      *
      * Key for file : STATUS-KODER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : HISTORISK-FAKTURA-REGISTER HODE
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
     c                   kfld                    sohel1_suff
      *
      * Key for file : HISTORISK-FAKTURA-REGISTER LINJE
      *
     c     sodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtl1_aarr
     c                   kfld                    sodtl1_binr
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
5.41  *
5.41  * Key for file : ORDRE-TYPE-REGISTER
5.41  *
5.41 c     votyl1_key    klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    votyl1_otyp
5.50  *
5.50  * Key for file : Kunde-REGISTER
5.50  *
5.50 c     rkunl1_key    klist
5.50 c                   kfld                    w_firm
5.50 c                   kfld                    rkunl1_kund
6.25  *
6.25  * Key for oppslag på vare
6.25  *
6.25 c     vvarl1_key    klist
6.25 c                   kfld                    w_firm
6.25 c                   kfld                    vvarl1_vare
6.30  *
6.30  *
6.30  * Key for oppslag på kundeprosjekt
6.30  *
6.30 c     fkprl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    fkprl1_kund
6.30 c                   kfld                    fkprl1_kpro
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
6.nu c                   parm                    p_binr
     c                   parm                    p_otyp
      *
      * Legger inn type for oppdatering av lager  R=Registrering
      *
5.01 c                   eval      hltype = ' '
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn år og fakturanummer i nøkklene
      *
     c                   eval      sohel1_aarr = p_aarr
     c                   eval      sohel1_binr = p_binr
     c                   eval      sohel1_numm = *zero
     c                   eval      sohel1_suff = *zero
      *
     c                   eval      sodtl1_aarr = p_aarr
     c                   eval      sodtl1_binr = p_binr
      *
      * Hent opplysninger fra OFAK-KODE-REGISTER
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * Henter inn neste ledige ordrenummer
      *
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
      *
      * Legger inn ordrenummer og suffix for ny ordre
      *
     c                   eval      fohelu_numm = w_numm
     c                   eval      fohelu_suff = *zero
      *
     c                   eval      fodtlu_numm = w_numm
     c                   eval      fodtlu_suff = *zero
     c                   eval      fodtlu_line = *zero
      *
     c                   endsr
