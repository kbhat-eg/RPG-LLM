     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY845R
      * Beskrivelse . . : VGRP: Oppdaterer varegruppe og modul på påslag
      *
      *                   Leser gjennom påslag, og bytter varegruppe og
      *                   modul i.h.t. vareregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251000 HKO  Nytt program
      *       280605 HKO  Oppdaterer også med modulnummer
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjpfalu    uf   e           k disk    rename(jpfapfr:jpfalur)
      *
      * Definering av LDA (Local Data Area)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jcfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Påslag
      *
     c     key01         setll     jpfalu
     c     key01         reade     jpfalur
     c                   dow       not %eof
      *
     c                   if        jcvare <> *blank
     c                   eval      jvarl1_vare = jcvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      jcogrp = jvogrp
     c                   eval      jchgrp = jvhgrp
     c                   eval      jcugrp = jvugrp
     c                   eval      jcmodn = jvmodn
     c                   endif
     c                   endif
      *
     c                   update    jpfalur
      *
     c     key01         reade     jpfalur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les på firma
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
