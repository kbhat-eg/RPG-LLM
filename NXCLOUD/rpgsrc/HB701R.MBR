     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HB701R
      * Beskrivelse . . : Bestilling - håndterminal METO-format
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030100 ASK  Nytt program
      *       210604 bø   Tilpasset mot nye bestillingsforsl.-rutiner
      *       240805 bø   Kode for å styre status på plass
      *       290506 bø   Tar med firma over til HR112R
      *       010307 bø   Justert reade
      *       150307 bø   Tar aldri best.pkt,min,mm. fra varereg. kun lager
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
      *  6.10 170609 BSA  Oppdatert med sporingsinformasjon
6.20  *  6.10 101110 bof  Utvidet med leverandør på lager
6.21  *  6.20 120112 bsa  Utvidet med kampanje på vp750r
6.22  *  6.20 151113 bsa  Bruker innkjøpspris og ikke kostpris ved ut-
6.22  *                   legg av verdi på bestillingsforslag.
6.nu  *  6.30 060614 bof  Endringer ihht. nummerutvidelse
6.31  * 6.30  121018 bof  Utvidet med trelast-sortiment
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhbbepf    if   e           k disk
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flfhelu    uf a e           k disk    rename(lfhepfr:lfhelur)
     flfdtlu    uf a e           k disk    rename(lfdtpfr:lfdtlur)
      *
      *  Parameter record
      *
     d                 ds
     d d_prec                        32
     d  d_otyp                        2    overlay(d_prec)
     d  d_ldor                        6  0 overlay(d_prec:3)
     d  d_lage                        2  0 overlay(d_prec:9)
     d  d_bdat                        6  0 overlay(d_prec:11)
     d  d_ldat                        6  0 overlay(d_prec:17)
     d  d_numm                        6  0 overlay(d_prec:23)
     d  d_avde                        4  0 overlay(d_prec:29)
      *
      *  Datastruktur - håndterminalfil
      *
     d                 ds
     d d_ht                          22
     d  d_ht_vare                     8    overlay(d_ht)
     d  d_ht_enhe                     3    overlay(d_ht:9)
     d  d_ht_anta                    11    overlay(d_ht:12)
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d lfhelu_numm     s                   like(lhnumm)
     d lfdtlu_numm     s                   like(lfnumm)
     d lfdtlu_line     s                   like(lfline)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d lodtlv_vare     s                   like(ldvare)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d votylr_otyp     s                   like(vaotyp)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_parm          s             32
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_firm          s                   like(vvfirm)
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
6.nu d w_numm          s              8  0
     d w_type          s              2
     d w_anta_alf      s             11
     d w_bmen          s                   like(vlbmen)
     d w_bmin          s                   like(vlbmin)
     d w_stat          s                   like(lfstat)
     d w_hnum          s                   like(lhnumm)
     d p_numm          s                   like(lhnumm)
     d w_112           s             11  2
     d w_tbel          s                   like(lhtbel)
     d w_tvkt          s             11  3
      *
     d w_lety          s              1  0
     d w_kopr          s              9  2
     d w_inpr          s              9  2
     d w_ldor          s                   like(lhldor)
     d w_lsor          s                   like(lhlsor)
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
5.70 d w_lage          s                   like(vllage)
6.20 d b_inlr          s              1    inz(*off)
6.21 d w_kamp          s              5
6.31 d w_lv_lvar       s                   like(vvlvar)
      *
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Henter neste ledige FORSLAGS-nummer fra Nummer-register
      *
     c                   exsr      hntnum
     c                   eval      lfhelu_numm = w_numm
     c                   eval      lfdtlu_numm = w_numm
     c                   eval      d_numm = w_numm
     c                   eval      lfdtlu_line = 0
      *
      * Leser alle rader i håndterminalfilen
      *
     c                   read      hbbepf                                 91
     c                   dow       *in91 = *off
      *
     c                   exsr      behandling
      *
     c                   read      hbbepf                                 91
     c                   enddo
      *
      *
      *  Legger ut best.forslag-hode dersom linjer er lagt ut.
      *
     c                   if        lfdtlu_line > 0
     c                   exsr      forsl_hode
     c                   endif
      *
      * finner status for heading - oppdaterer
      *
     c                   eval      lfhelu_numm = d_numm
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
     c                   eval      w_hnum = d_numm
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   eval      w_stat = *blank
     c                   call      'LH714R '
     c                   parm                    w_hnum
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhtbel = w_tbel
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      *
      * hvis leverandør ikke er bestemt fra rad.term.,så sjekkes det om
      * bestillingsforslag må splittes på flere leverandører.
     c                   if        d_ldor = 0
     c                   eval      p_numm = d_numm
     c                   call      'HR112R '
     c                   parm                    w_firm
     c                   parm                    p_numm
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      w_parm = d_prec
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av innlest rad
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_ht = hbinfo
      *
     c                   if        d_ht_vare = *blank
     c                   goto      end_behand
     c                   endif
      *
      * Finner vare
      *
     c                   eval      vvarl1_vare = d_ht_vare
     c     vvarl1_key    chain     vvarl1                             94
     c                   if        *in94 = *on
     c                   goto      end_behand
     c                   endif
      *
     c                   eval      vvenl1_vare = d_ht_vare
     c                   eval      vvenl1_enhe = d_ht_enhe
     c     vvenl1_key    chain     vvenl1
6.20  *
6.20  * henter leverandør fra lager
6.20  *
6.20 c                   call      'VL721R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_lvar = *blank
6.31 c/exec sql
6.31 c+ select vvllva
6.31 c+ into   :w_lv_lvar
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :vvvare and
6.31 c+        vvlldo = :w_ldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
      *
      * Oppretter vare i bestillingsforslag
      *
     c                   eval      lfdtlu_line = lfdtlu_line + 10
     c     lfdtlu_key    chain     lfdtlur                            92
      *
     c                   eval      lfltyp = *blank
     c                   if        d_ldor <> 0
     c                   eval      lfldor = d_ldor
     c                   else
6.20 c                   eval      lfldor = w_ldor
     c                   endif
     c                   eval      lflage = d_lage
     c                   eval      lfogrp = vvogrp
     c                   eval      lfhgrp = vvhgrp
     c                   eval      lfugrp = vvugrp
6.31 c                   if        w_lv_lvar <> *blank
6.31 c                   eval      lflvar = w_lv_lvar
6.31 c                   else
     c                   eval      lflvar = vvlvar
6.31 c                   endif
     c                   eval      lfvare = vvvare
     c                   eval      lfenh1 = d_ht_enhe
     c                   eval      lftek1 = vvtek1
     c                   eval      lftek2 = vvtek2
      *
     c                   eval      w_anta_alf = %subst(d_ht_anta:1:8) +
     c                                          %subst(d_ht_anta:10:2) + '0'
     c                   move      w_anta_alf    lfanta
      *
     c                   exsr      finn_pris
     c                   eval      lfvekt = vevekt / 1000
6.22  * Bruk innkjøpspris, hvis ikke finnes bruk kost
6.22 c**                 eval      lflbel = w_kopr
6.22 c                   eval      lflbel = w_inpr
6.22 c                   if        lflbel = 0
6.22 c                   eval      lflbel = w_kopr
6.22 c                   endif
     c                   exsr      finn_varelag
     c                   exsr      sjk_stat
     c                   if        laabst = 1
     c                   eval      lfstat = w_stat
     c                   else
     c                   eval      lfstat = '0'
     c                   endif
6.20 c                   eval      lfvlev = w_ldor
      *
     c                   if        *in92 = *on
     c                   eval      lffirm = w_firm
     c                   eval      lfnumm = lfdtlu_numm
     c                   eval      lfline = lfdtlu_line
6.10 c                   time                    lfodat
6.10 c                   time                    lfotim
6.10 c                   eval      lfousr = l_user
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   write     lfdtlur
     c                   endif
      *
     c     end_behand    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer bestillingsforslag - hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forsl_hode    begsr
      *
     c     lfhelu_key    chain     lfhelur                            93
      *
     c                   eval      lhotyp = d_otyp
      *
     c                   if        d_bdat > 0
     c     *dmy          move      d_bdat        lhbdat
     c                   else
     c                   move      udate         lhbdat
     c                   endif
     c                   if        d_ldat > 0
     c     *dmy          move      d_ldat        lhldat
     c                   endif
      *
     c                   eval      lhwsid = l_wsid
     c                   eval      lhuser = l_user
     c                   eval      lhdate = w_date
     c                   eval      lhtime = w_time
     c                   eval      lhlage = d_lage
     c                   eval      lhavde = d_avde
     c                   eval      lhoppr = 'R'
     c                   if        d_ldor <> 0
     c                   eval      lhldor = d_ldor
     c                   eval      rlevl1_levr = d_ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found(rlevl1)
     c                   movel     rlalfa        lhlsor
     c                   endif
     c                   endif
      *
     c                   if        *in93 = *on
     c                   eval      lhfirm = w_firm
     c                   eval      lhnumm = lfhelu_numm
6.10 c                   time                    lhdate
6.10 c                   time                    lhtime
6.10 c                   eval      lhousr = l_user
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   write     lfhelur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = laafor
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finne prisen på en vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      *
     c                   eval      w_lety = 0
6.21 c                   eval      w_kamp = *blank
     c                   time                    w_date
      *
     c                   call      'VP750R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_date
     c                   parm                    lfenh1
     c                   parm                    w_kopr
     c                   parm                    w_inpr
6.21 c                   parm                    w_kamp
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av vare/lager informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_varelag  begsr
      *
     c                   eval      w_bmin = 0
     c                   eval      w_bmen = 0
      *
     c                   eval      vlagl1_vare = lfvare
     c                   eval      vlagl1_lage = lflage
      *
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found(vlagl1)
     c                   goto      end_finn
     c                   endif
      *
      * Verdier fra lager eller vare?
      * fom v 5.50 tas verdi kun fra lager !
      *
     c                   eval      w_bmen = vlbmen
     c                   eval      w_bmin = vlbmin
      *
     c     end_finn      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker mulige statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_stat      begsr
      *
      * Her sjekkes det for status 4 - 6.
      *
     c                   eval      w_stat = '0'
     c                   if        w_bmen <> 0 and
     c                             lfanta <> w_bmen
     c                   eval      w_stat = '4'
     c                   endif
      *
     c                   if        w_bmin <> 0
     c                   eval      w_112  = lfanta / w_bmin
     c                   move      w_112         w_20              2 0
     c                   if        w_20 > 0
     c                   eval      w_stat = '5'
     c                   endif
     c                   endif
      *
      * sjekk mot bestilling /restleveranse
     c                   exsr      sjk_best
      *
      * sjekk om det er en cross-docking vare
      *
     c                   if        vvcrdo = 1
     c                   eval      w_stat = '7'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om varen ligger på bestilling.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_best      begsr
readec                   eval      lodtlv_vare = vvvare
readec     lodtlv_key    setll     lodtlv
readec     lodtlv_key    reade     lodtlv
readec                   if        not %eof(lodtlv)
     c                   if        ldanta > 0
      *
     c                   eval      lohelr_numm = ldnumm
     c                   eval      lohelr_suff = ldsuff
     c     lohelr_key    chain     lohelr
     c                   if        %found(lohelr)
     c                   eval      votylr_otyp = lootyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaolag = 1  and
     c                             vaoakk = 1
     c                   eval      w_stat = '6'
     c                   goto      end_sjk
readec                   endif
readec                   endif
     c                   endif
     c                   endif
      *
     c     end_sjk       endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for les STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for oppdatering BESTILLINGS-FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      *  Key for oppdatering BESTILLINGS-FORSLAG-LINJE
      *
     c     lfdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlu_numm
     c                   kfld                    lfdtlu_line
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for les av bestillinger
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key for les av bestilling hode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Henter inn firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      *  Legger inn dato og klokkeslett (timestamp)
      *
     c                   time                    w_date
     c                   time                    w_time
      *
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
      * Hent prisgruppe
      *
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   eval      w_lage = d_lage
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
      *
     c                   endsr
