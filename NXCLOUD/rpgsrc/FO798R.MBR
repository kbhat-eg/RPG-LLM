     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO798R
      * Beskrivelse . . : Sende utskrift som e-mail
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       181002 AMD  Nytt program
5.60  * R5.60 250308 AMD  Lagt inn mulighet for å skrive inn 4 meldings
5.60  *                   linjer til mottaker.
5.61  * R5.61 200608 AMD  Dersom epost-adresse ligger på kundeprosjekt,
      *                   overstyrer dette det som ligger på kunden
pdf   * R6.10 250610 BSA  Legger ut og viser kopi e-post adresse.
6.10  * R6.10 130910 BSA  Sender med e-mail adresse i parameterlista
6.11  * R6.10 310311 bof  Kall til feil program med kontaktpersoner
6.20  * R6.20 040511 BSA  Hvis e-mail adresse er en del av parametrene inn,
6.20  *                   benyttes denne. Samme med tekstlinjer.
6.21  * R6.21 220113 AMD  Fikk ikke opp kontaktpersoner for kunder, her
6.21  *                   er det to forskjellige programmer som skal
6.21  *                   benyttes, RK503R for kunde og RL503R for lev.
6.22  * R6.20 200314 BSA  Forts av 6.20. Bruker også evnt emailadr 2.
6.nu  * 6.30  050614 bof  endring i forb. nummerutvidelse
6.30  * 6.30  150814 bsa  Slår opp for å finne default epostadresser via
6.30  *                   rutineregisteret.
6.31  * 6.30  090914 bsa  Hvis epostadresser funnet fra rutinereg, skal
6.31  *                   disse benyttes.
6.32  * 6.30  110914 bsa  Henter mailadresser fra kundekort, prosjekt og
6.32  *                   kontaktperson. Legges i separate felt.
6.33  * 6.30  110915 bsa  Utvider bredden på skjermbildet da det er problemer
6.33  *                   i jacada.
6.34  * 6.30  251115 nho  Mangler parameter inn i RL503R
6.35  * 6.30  181215 bsa  Hvis emne kommer utfylt fra kallende program,
6.35  *                   brukes denne.
6.36  * 6.30  060117 bsa  Legger mobilnummer ut på vår referanse
6.37  * 6.30  250717 bsa  Legger suffix inn i emnedelen av mailen.
7.00  * 7.00  180220 bkv  Gjort om til full screen
7.02  * K000  031116 bof  Lagt ut kundeprosjektnavn i emnefeltet
7.02  * K000              Spesial fra Gipling.
7.03  * K000  290616 bof  Bestilling:hvis kontaktperson, så sjekkes spes.rolle
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunpf    if   e           k disk
     frlevpf    if   e           k disk
5.61 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.30 fafeml1    if   e           k disk    rename(afempfr:afeml1r)
6.32 flohel1    if   e           k disk    rename(lohepfr:lohel1r)
6.32 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.32 frukpl1    if   e           k disk    rename(rukppfr:rukpl1r)
     ffo798d    cf   e             workstn
      *
      * Definering av parametere
      *
     d p_kule          s              1
     d p_kund          s                   like(rkkund)
5.61 d p_kpro          s                   like(flkpro)
     d p_navn          s                   like(rknavn)
     d p_emal          s                   like(rkemal)
6.32 d p_ema2          s                   like(rkemal)
6.32 d p_ema3          s                   like(rkemal)
6,32 d p_kopi          s                   like(rkemal)
6.10 d p_kema          s             50
6.10 d p_knvn          s             30
6.32 d p_kpnr          s                   like(fokpnr)
     d p_emne          s             50
5.60 d p_txt1          s             50
5.60 d p_txt2          s             50
5.60 d p_txt3          s             50
5.60 d p_txt4          s             50
     d p_pers          s             50
     d p_inif          s             40
     d p_in03          s              1
6.nu d p_numm          s                   like(fonumm)
6.32 d p_suff          s                   like(fosuff)
     d p_selg          s              4  0
      *
      * Definering av LDA
      *
     d                uds
     d l_ruti                800    809
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d rkunpf_kund     s                   like(rkkund)
5.61 d fkprl1_kund     s                   like(flkund)
5.61 d fkprl1_kpro     s                   like(flkpro)
     d rlevpf_levr     s                   like(rllevr)
     d ra09l1_ikod     s                   like(raikod)
     d aforl1_ruti     s                   like(afruti)
     d afirl1_ffir     s                   like(aaffir)
6.30 d afeml1_mrut     s                   like(aemrut)
6.32 d fohel1_numm     s                   like(fonumm)
6.32 d fohel1_suff     s                   like(fosuff)
6.32 d rukpl1_kund     s                   like(rukund)
6.32 d rukpl1_levr     s                   like(rulevr)
6.32 d rukpl1_kpnr     s                   like(rukpnr)
6.32 d lohel1_numm     s                   like(lonumm)
6.32 d lohel1_suff     s                   like(losuff)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_firm          s                   like(rkfirm)
6.nu d w_numm          s              8
7.02 d w_knav          s                   like(flnavn)
7.02 d w_krol          s                   like(rukrol)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c                   eval      b1kund = *zero
     c                   eval      b1navn = *blank
5.60 c                   eval      b1txt1 = *blank
5.60 c                   eval      b1txt2 = *blank
5.60 c                   eval      b1txt3 = *blank
5.60 c                   eval      b1txt4 = *blank
     c                   eval      b1pers = *blank
     c                   eval      b1emal = *blank
6.32 c                   eval      b1ema2 = *blank
6.32 c                   eval      b1ema3 = *blank
6.22 c                   eval      b1kopi = *blank
6.22 c                   if        p_kule <> *blank
6.32 c                   eval      p_kopi = *blank
6.22 c                   endif
      * Finn epostadresser fra kunde
      * - kundekort
     c                   if        p_kule = 'K'
     c                   eval      rkunpf_kund = p_kund
     c     rkunpf_key    chain     rkunpfr
      *
     c                   if        %found
     c                   eval      b1kund = rkkund
     c                   eval      b1navn = rknavn
     c                   eval      b1emal = rkemal
     c                   endif
6.32 c**                 endif
5.61  *
5.61  * - kundeprosjekt
5.61  *
7.02 c                   eval      w_knav = *blank
6.32 c**                 if        p_kule = 'K'
5.61 c                   if        p_kpro <> 0
5.61 c                   eval      fkprl1_kund = p_kund
5.61 c                   eval      fkprl1_kpro = p_kpro
5.61 c     fkprl1_key    chain     fkprl1r
5.61  *
5.61 c                   if        %found
7.02 c                   eval      w_knav = flnavn
5.61 c                   if        flmail <> ' '
6.32 c                   eval      b1ema2 = flmail
5.61 c                   endif
5.61 c                   endif
5.61 c                   endif
6.32  *
6.32  * - kontaktperson
6.32  *
6.32 c                   eval      fohel1_numm = p_numm
6.32 c                   eval      fohel1_suff = p_suff
6.32 c     fohel1_key    chain     fohel1r
6.32 c                   if        %found
6.32 c                   eval      rukpl1_kund = p_kund
6.32 c                   eval      rukpl1_levr = 0
6.32 c                   eval      rukpl1_kpnr = fokpnr
6.32 c     rukpl1_key    chain     rukpl1r
6.32 c                   if        %found and
6.32 c                             rumail <> *blanks
6.32 c                   eval      b1ema3 = rumail
6.32 c                   endif
6.32 c                   endif
6.32  *
5.61 c                   endif
      *
     c                   if        p_kule = 'L'
     c                   eval      rlevpf_levr = p_kund
     c     rlevpf_key    chain     rlevpfr
      *
     c                   if        %found
     c                   eval      b1kund = rllevr
     c                   eval      b1navn = rlnavn
     c                   eval      b1emal = rlemal
     c                   endif
6.32  *
6.32  * - kontaktperson
6.32  *
6.32 c                   eval      lohel1_numm = p_numm
6.32 c                   eval      lohel1_suff = p_suff
6.32 c     lohel1_key    chain     lohel1r
6.32 c                   if        %found
7.02 c                   eval      w_krol = 'MAILBEST_' +
7.02 c                                      %char(lolage)
7.02 c                   eval      rukpl1_kund = 0
7.02 c                   eval      rukpl1_levr = p_kund
7.02 c     rukpl1_key2   setll     rukpl1r
7.02 c     rukpl1_key2   reade     rukpl1r
7.02 c                   dow       not %eof(rukpl1)
7.02 c                   if        rukrol = w_krol  and
7.02 c                             rumail <> *blank
7.02 c                   eval      b1emal = rumail
7.02 c                   endif
7.02 c     rukpl1_key2   reade     rukpl1r
7.02 c                   enddo
7.02 c                   endif
6.32  *
     c                   endif
      *
      * Finne e-mail-adresse for avsender
      *
     c                   eval      ra09l1_ikod = p_selg
     c     ra09l1_key    chain     ra09l1r
     c                   if        %found
6.32 c                   eval      p_kopi =  raiema
pdf  c                   eval      b1kopi =  raiema
6.36 c                   if        raimob <> *blanks
6.36 c                   eval      b1pers = 'Vennlig hilsen: ' + %trim(raitxt) +
6.36 c                             ', ' + raimob
6.36 c                   else
     c                   eval      b1pers = 'Vennlig hilsen: ' + %trim(raitxt)
6.36 c                   endif
     c                   endif
      *
      * Finne informasjon fra rutineregister
      * og legge dette inn i parametere
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        %found
     c                   eval      p_inif = afhex1 + afhex2
     c                   endif
      *
      *  Henter inn firmaopplysninger
      *  og klargjør variabel for emne
      *  i skjermbildet
      *
     c                   eval      afirl1_ffir = w_firm
     c     afirl1_key    chain     afirl1r
     c                   eval      b1emne = %trim(aftxt1) + ' ' +
6.37 c                                           (w_numm) + '/' +
6.37 c                                           %char(p_suff) + ' ' +
     c                                           ('fra')  + ' ' +
7.02 c                                      %trim(aafnav) + ' '+
7.02 c                                      %trim(w_knav)
6.35 c                   if        p_emne <> *blanks
6.35 c                   eval      b1emne = p_emne
6.35 c                   endif
6.20  * Hvis kode er ' ' brukes e-mailadressen fra parametrene inn.
6.20 c                   if        p_kule = ' '
6.20 c                   eval      b1emal = p_emal
6.32 c                   eval      b1ema2 = p_ema2
6.32 c                   eval      b1ema3 = p_ema3
6.20 c                   eval      b1emne = p_emne
6.20 c                   eval      b1txt1 = p_txt1
6.20 c                   eval      b1txt2 = p_txt2
6.20 c                   eval      b1txt3 = p_txt3
6.20 c                   eval      b1txt4 = p_txt4
6.32 c                   eval      b1kopi = p_kopi
6.20 c                   endif
6.30  *
6.31  * Sjekk om det finnes avsender/mottaker på rutinereg.
6.30  *
6.31 c**                 if        b1emal = *blanks or
6.31 c**                           b1kopi = *blanks
6.30 c                   eval      afeml1_mrut = l_ruti
6.30 c     afeml1_key    chain     afeml1r
6.30 c                   if        %found
6.31 c                   if        aemtil <> *blanks
6.30 c                   eval      b1emal = aemtil
6.30 c                   endif
6.31 c                   if        aemfra <> *blanks
6.30 c                   eval      b1kopi = aemfra
6.30 c                   endif
6.30 c                   endif
6.31 c**                 endif
6.30  *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   eval      p_in03 = *on
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
6.10 c                   if        *inkd = *on
6.10 c                   exsr      spØrring
6.10 c                   goto      b1taga
6.10 c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kontaktperson
      *
6.10 c                   if        w_afld = 'B1EMAL'
6.10 c                   eval      p_kema = *blanks
6.21  *
6.21 c                   if        p_kule = 'K'
6.21 c                   call      'RK503R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    b1kund
6.21 c                   parm                    p_knvn
6.32 c                   parm                    p_kpnr
6.21 c                   parm                    p_kema
6.21 c                   else
6.21  *
6.11 c                   call      'RL503R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    b1kund
6.10 c                   parm                    p_knvn
6.34 c                   parm                    p_kpnr
6.10 c                   parm                    p_kema
6.21 c                   endif
6.10  *
6.32 c                   eval      b1ema3 =  p_kema
6.10 c                   goto      end_spØrr
6.10 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Kontroller at det er
      * oppgitt e-mail-adresse
      *
6.32 c                   if        b1emal = *blank and
6.32 c                             b1ema2 = *blank and
6.32 c                             b1ema3 = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      p_navn = b1navn
     c                   eval      p_pers = b1pers
     c                   eval      p_emne = b1emne
     c                   eval      p_emal = b1emal
6.32 c                   eval      p_ema2 = b1ema2
6.32 c                   eval      p_ema3 = b1ema3
5.60 c                   eval      p_txt1 = b1txt1
5.60 c                   eval      p_txt2 = b1txt2
5.60 c                   eval      p_txt3 = b1txt3
5.60 c                   eval      p_txt4 = b1txt4
pdf  c                   if        b1kopi <> *blank
6.32 c                   eval      p_kopi = b1kopi
pdf  c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter-liste
      *
     c     *entry        plist
     c                   parm                    p_kule
     c                   parm                    p_kund
5.61 c                   parm                    p_kpro
     c                   parm                    p_numm
6.32 c                   parm                    p_suff
     c                   parm                    p_selg
     c                   parm                    p_navn
     c                   parm                    p_emal
6.32 c                   parm                    p_ema2
6.32 c                   parm                    p_ema3
     c                   parm                    p_emne
5.60 c                   parm                    p_txt1
5.60 c                   parm                    p_txt2
5.60 c                   parm                    p_txt3
5.60 c                   parm                    p_txt4
     c                   parm                    p_pers
6.32 c                   parm                    p_kopi
     c                   parm                    p_inif
     c                   parm                    p_in03
      *
      * Key for oppslag på kunde
      *
     c     rkunpf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunpf_kund
5.61  *
5.61  * Key for oppslag på kunde-prosjekt
5.61  *
5.61 c     fkprl1_key    klist
5.61 c                   kfld                    w_firm
5.61 c                   kfld                    fkprl1_kund
5.61 c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på leverandør
      *
     c     rlevpf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevpf_levr
      *
      * Key for oppslag på selgerregister
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på rutineregister
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
6.30  *
6.30  * Key for oppslag på rutineregister epostadresser
6.30  *
6.30 c     afeml1_key    klist
6.30 c                   kfld                    afeml1_mrut
      *
      * Key for oppslag på firma-register
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
6.32  *
6.32  * Key for oppslag på ordrehodet
6.32  *
6.32 c     fohel1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    fohel1_numm
6.32 c                   kfld                    fohel1_suff
6.32  *
6.32  * Key for oppslag på ordrehodet
6.32  *
6.32 c     lohel1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    lohel1_numm
6.32 c                   kfld                    lohel1_suff
6.32  *
6.32  * Key for oppslag på kontaktpersonregister
6.32  *
6.32 c     rukpl1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    rukpl1_kund
6.32 c                   kfld                    rukpl1_levr
6.32 c                   kfld                    rukpl1_kpnr
7.02  *
7.02  * Key for oppslag på kontaktpersonregister
7.02  *
7.02 c     rukpl1_key2   klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    rukpl1_kund
7.02 c                   kfld                    rukpl1_levr
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Flytt over ordrenummer til alfafelt
      *
     c                   move      p_numm        w_numm
      *
     c                   endsr
      *
