     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE716R
      * Beskrivelse . . : Henter gtinnr ut fra gitt varenr på salgsenh1
      *                   og nobb-lev.nr på vare
      *                   Hvis gtinnr ikke finnes på hovedleverandør, sjekkes om det
      *                   finnes GTIN nummer på enhet uavhengig av leverandør.
      *                   Hvis gtinnr ikke finnes på vveppf, brukes veanpf
      *
      *                   Input : firma, varenr.
      *                   Output: ean.nr  status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       291010 bsa  Nytt program, basert på VE712
6.10  *       011210 bsa  Lagt inn enda en sjekk på ean kode uten enhetskontroll
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
     d vvenl2_firm     s                   like(vefirm)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d veanl2_firm     s                   like(vxfirm)
     d veanl2_vare     s                   like(vxvare)
     d vveplr_pfir     s                   like(vepfir)
     d vveplr_pvar     s                   like(vepvar)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pldo     s                   like(vepldo)
     d vvepl1_pfir     s                   like(vepfir)
     d vvepl1_pvar     s                   like(vepvar)
     d vvepl1_penh     s                   like(vepenh)
     d vvepl2_pfir     s                   like(vepfir)
     d vvepl2_pvar     s                   like(vepvar)
      *
     d w_sae1          s                   like(vxenhe)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vxfirm)
     d p_eann          s                   like(vxeann)
     d p_vare          s                   like(vxvare)
     d p_enhe          s                   like(vxenhe)
     d p_stat          s              1
      *
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Initierer output-parametre
      *
     c                   eval      p_eann = 0
     c                   eval      p_stat = *blank
      *
     c                   eval      vvarlr_firm = p_firm
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   goto      avslutt
     c                   endif
      *
      * Henter eannr salgsenhet 1
      *
     c                   eval      vvenl2_firm = p_firm
     c                   eval      vvenl2_vare = p_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    chain     vvenl2
     c                   if        %found(vvenl2)
     c                   eval      w_sae1 = veenhe
     c                   eval      vveplr_pfir = p_firm
     c                   eval      vveplr_pvar = p_vare
     c                   eval      vveplr_penh = veenhe
     c                   eval      vveplr_pldo = vvldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
      * Sjekk en gang til på VVEP uavhengig av leverandør, men med salgsenhet 1
     c                   eval      vvepl1_pfir = p_firm
     c                   eval      vvepl1_pvar = p_vare
     c                   eval      vvepl1_penh = veenhe
     c     vvepl1_key    setll     vveplr
     c     vvepl1_key    reade     vveplr                                 91
     c                   dow       *in91 = *off
      * Har vi match på enhet ? (Først enhet 1)
     c                   if        w_sae1 = vepenh and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c     vvepl1_key    reade     vveplr                                 91
     c                   enddo
      * Sjekk en gang til på VVEP uavhengig av leverandør og enhet
     c                   eval      vvepl2_pfir = p_firm
     c                   eval      vvepl2_pvar = p_vare
     c     vvepl2_key    setll     vveplr
     c     vvepl2_key    reade     vveplr                                 91
     c                   dow       *in91 = *off
      * Har vi match på vare
     c                   if        vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c     vvepl2_key    reade     vveplr                                 91
     c                   enddo
      *
     c                   endif
      *
      * hvis gtin ikke fantes på vare-enhet-prisgiver - prøv veanpf
      *
     c                   if        p_stat = *blank
     *
     c                   eval      veanl2_firm = p_firm
     c                   eval      veanl2_vare = p_vare
     c     veanl2_key    setll     veanl2
     c     veanl2_key    reade     veanl2                                 91
     c                   dow       *in91 = *off
      * Har vi match på enhet ? (Først enhet 1)
     c                   if        w_sae1 = vxenhe
     c                   eval      p_eann = vxeann
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
     c     veanl2_key    reade     veanl2                                 91
     c                   enddo
      *
     c                   endif
6.10  *
6.10  * Hvis det ikke finnes for salgsenhet 1, hent 'en eller annen'
6.10  *
6.10 c                   if        p_stat = *blank
6.10 *
6.10 c                   eval      veanl2_firm = p_firm
6.10 c                   eval      veanl2_vare = p_vare
6.10 c     veanl2_key    setll     veanl2
6.10 c     veanl2_key    reade     veanl2                                 91
6.10 c                   dow       *in91 = *off
6.10  *
6.10 c                   eval      p_eann = vxeann
6.10 c                   eval      p_stat = '1'
6.10 c                   goto      avslutt
6.10  *
6.10 c     veanl2_key    reade     veanl2                                 91
6.10 c                   enddo
6.10  *
6.10 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_eann
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vvenl2_key    klist
     c                   kfld                    vvenl2_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
      * Key for les på ean-register
      *
     c     veanl2_key    klist
     c                   kfld                    veanl2_firm
     c                   kfld                    veanl2_vare
      *
      *
      * Key for les på vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for les på vareenhets-register
      *
     c     vveplr_key    klist
     c                   kfld                    vveplr_pfir
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
      *
      * Key for les på VVEP-register
      *
     c     vvepl1_key    klist
     c                   kfld                    vvepl1_pfir
     c                   kfld                    vvepl1_pvar
     c                   kfld                    vvepl1_penh
      *
      * Key for les på VVEP-register
      *
     c     vvepl2_key    klist
     c                   kfld                    vvepl2_pfir
     c                   kfld                    vvepl2_pvar
      *
     c                   endsr
