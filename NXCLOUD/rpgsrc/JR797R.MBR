     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR797R
      * Beskrivelse . . : Leser fil på recordformat OPTIMERA (Optimera),
      *                   og oppretter/oppdaterer priser på eksisterende
      *                   varer i VA.
      *
      *                   Dersom en ny vare matcher mot Nobbnr i VA,
      *                   opprettes prisen som prisgivende leverandør på
      *                   varen.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231105 HKO  Nytt program
6.30  * 6.30  280113 bof  Omskriving i forb. endring pakning VA
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - OPTIMERA
      *
     d                 ds
     d d_inpu                       256
     d  d_vare                       12    overlay(d_inpu:1)                    Varenr/Nobbnr
     d  d_fill1                       1    overlay(d_inpu:13)                   Filler
     d  d_vgrp                        4    overlay(d_inpu:14)                   Varegruppe
     d  d_fill2                       1    overlay(d_inpu:18)                   Filler
     d  d_tek1                       30    overlay(d_inpu:19)                   Varetekst-1
     d  d_fill3                       1    overlay(d_inpu:49)                   Filler
     d  d_enhe                        3    overlay(d_inpu:50)                   Enhet
     d  d_fill4                       1    overlay(d_inpu:53)                   Filler
     d  d_tek2                       30    overlay(d_inpu:54)                   Varetekst-2
     d  d_fill5                       1    overlay(d_inpu:84)                   Filler
     d  d_antp                        5    overlay(d_inpu:85)                   Antall i pakning
     d  d_fill8                       1    overlay(d_inpu:90)                   Filler
     d  d_sapr                       10    overlay(d_inpu:91)                   Salgspris
     d   d_sapr_kr                    7  0 overlay(d_sapr:1)                    Salgspris - kr
     d   d_sapr_Øre                   2  2 overlay(d_sapr:9)                    Salgspris - øre
     d  d_fill9                       1    overlay(d_inpu:101)                  Filler
     d  d_inpr                       10    overlay(d_inpu:102)                  Innkjøpspris
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)                    Innkjøpspris - kr
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)                    Innkjøpspris - øre
     d  d_fill10                      1    overlay(d_inpu:112)                  Filler
     d  d_eann                       13  0 overlay(d_inpu:124)                  Ean-nummer
      *
      * Definering av variable i nøkkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvpkl2_vare     s                   like(jwvare)
6.30 d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
     d jlevl1_ldor     s                   like(jlldor)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d b_feil          s              3    inz(*off)
     d b_inlr          s              3    inz(*off)
      *
     d w_firm          s              3  0
      *
     d w_nobb          s                   like(jvnobb)
     d w_inpr          s                   like(jxpris)
     d w_sapr          s                   like(jxpris)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found or
     c                             jrldor = 0
     c                   goto      avslutt
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      jlnavn = %trim(jlnavn)
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
      * Behandling
      *
     c                   read      jrvapf
     c                   dow       not %eof
     c                   exsr      behandling
     c                   read      jrvapf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   exsr      sjekk_vare
     c                   if        b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_inpr = d_inpr_kr + d_inpr_Øre
     c                   eval      w_sapr = d_sapr_kr + d_sapr_Øre
      *
     c                   if        jwpakn <> '000' and
     c                             jwofak <> 0
     c                   eval      w_inpr = w_inpr * jwofak
     c                   eval      w_sapr = w_sapr * jwofak
     c                   endif
      *
     c                   eval      jvprl1_vare = jvvare
     c                   eval      jvprl1_ldor = jrldor
     c     jvprl1_key    chain     jvprl1
     c                   if        %found
     c                   exsr      endr_pris
     c                   else
     c                   exsr      oppr_pris
     c                   endif
      *
      * Oppdaterer søketekst
      *
     c                   eval      b_inlr = *on
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jxvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om vare og enhet finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vare    begsr
      *
     c                   eval      b_feil = *on
      *
     c                   if        %len(%trim(d_vare)) <> 8 or
     c                             d_enhe = *blank
     c                   leavesr
     c                   endif
      *
     c                   movel     d_vare        w_nobb
     c                   if        w_nobb < 10000000
     c                   leavesr
     c                   endif
      *
     c                   eval      jvarl3_nobb = w_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      jvpkl2_vare = jvvare
6.30 c                   eval      jvpkl2_ldor = jrldor
     c                   eval      jvpkl2_enhe = d_enhe
     c     jvpkl2_key    chain     jvpkl2
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      b_feil = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av nytt prisgiver-forhold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
     c                   eval      jxlvar = d_vare
     c                   eval      jxpris = w_inpr
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_pris     begsr
      *
     c                   if        w_inpr = jxpris
     c                   leavesr
     c                   endif
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    chain     jvprlur
      *
     c                   eval      jxlvar = d_vare
     c                   eval      jxpris = w_inpr
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   update    jvprlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
6.30 c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
