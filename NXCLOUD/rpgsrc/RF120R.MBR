      /TITLE  Samler bank- og postinnbetalinger
     h dftname(rf120r) datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                        *     *
      * Program . . . . : RF120R                                        *
      * Beskrivelse . . : Innlesing av innbetalinger OCR                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INT   * Spesialversjon. : Beløp har minustegn foran når negativt        *
INT   * Tilpasset for . : Inter-Sun                                     *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * R5.00     140103  Rettet feil i RJRAAR.                    HFL  *
T5.00 * R5.00     120203  Rettet feil ved bruk av kortkid.         HFL  *
T5.00 * R5.40     081005  Lagt inn behandling av negative poster   KOB  *
T5.00 * R5.41     241006  Lagt inn behandling av negative beløp    KOB  *
T5.00 * R5.42     011106  Lagt inn endr. ved manglende kort kid    KOB  *
T5.50 * R5.50     230407  Lagt inn kontroll mot leverandørregister KOB  *
T5.51 * R5.51     070911  Flyttet blank til rjpros                 NHO  *
6.nu  * R6.3      150814  Endring vedr utvidelse av felter         NHO        *
6.nu  * R6.20     280515  Utvidelse av kort kid                    nho   NHO  *
6.30  *           100615  Testfelt for å finne lang kid er minsket nho  *
6.30  *                   med 2 posisjoner pga 8 langt fakt.nr     nho
6.31  *           140918  Bruker annet felt til bilagsdato         nho
7.01  *K000       050218  Kid = 18 lang / Santander                nho
8.01  * 800       160521  Håndterer KID fra PayEx                  ask
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * RJOUPF - Benyttes som arbeidsfile
      *          Bruddbegrep legges ut i Sort-feltet
      *
     focrin     up   f   83        disk
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
     fakidl1    if   e           k disk    rename(akidpfr:akidl1r)
7.01 fakidl2    if   e           k disk    rename(akidpfr:akidl2r)
7.01 f                                     prefix(xa:2)
T5.50frlevl6    if   e           k disk    rename(rlevpfr:rlevl6r)
     frjoupf    o    e           k disk
      *
     d per             s              2  0 dim(13) ctdata perrcd(13)            PERIODER
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d                 ds
     d wdato                          6  0
     d  wdag                          2  0 overlay(wdato)
     d  wmnd                          2  0 overlay(wdato:3)
     d  waar                          2  0 overlay(wdato:5)
      *
     d                 ds
     d wref                          10  0
     d  wseid                         2  0 overlay(wref)
     d  wdagk                         2  0 overlay(wref:3)
     d  wdela                         1  0 overlay(wref:5)
     d  wlØpn                         5  0 overlay(wref:6)
      *
      *       KEY-REKKEFØLGE PÅ RJOUPF (POSTER UT)
      *
     d                 ds
     d wssort                  2     37
     d  wsfirm                        3  0 overlay(wssort)
     d  wsbref                       12    overlay(wssort:4)
     d  wsbaar                        4  0 overlay(wssort:16)
     d  wsbmnd                        2  0 overlay(wssort:20)
     d  wsbdag                        2  0 overlay(wssort:22)
      *
T5.00 *       Lang kid fra AKIDPF (Kort-kid-register)
      *
T5.00d                 ds
T5.00d w_kidd                        25
T5.00d  w_kid21                      21    overlay(w_kidd)
T5.00d  w_nfir                        3  0 overlay(w_kidd)
T5.00d  w_kode                        1  0 overlay(w_kidd:4)
T5.00d  w_f001                        1  0 overlay(w_kidd:5)
T5.00d  w_mvak                        1    overlay(w_kidd:6)
6.nu d* w_betb                        2  0 overlay(w_kidd:7)
6.nu d* w_kund                        6  0 overlay(w_kidd:9)
6.nu d  w_kund                        6  0 overlay(w_kidd:7)
6.nu d* w_fakt                        6  0 overlay(w_kidd:15)
6.nu d  w_fakt                        8  0 overlay(w_kidd:13)
8.01 d  w_aarr                        8  0 overlay(w_kidd:13)
      *
7.01 d                 ds
7.01 d x_dumy                        18
7.01 d  x_kkus                       17    overlay(x_dumy)
7.01 d   x_kkli                       3  0 overlay(x_kkus:1)
7.01 d   x_kbil                       8  0 overlay(x_kkus:4)
7.01 d   x_kund                       6  0 overlay(x_kkus:12)
7.01 d  x_kmod                        1  0 overlay(x_dumy:18)
      *
     d w_firm          s                   like(ra3fir)
610  d w_memb          s                   like(rjmemb)
      *
     d akidl1_kidr     s                   like(akkidr)
7.01 d akidl2_firm     s                   like(xafirm)
7.01 d akidl2_kid2     s                   like(xakid2)
7.01 d akidl2_kaar     s                   like(xaaarr)
7.01 d akidl2_kidr     s                   like(xakidr)
T5.50d rlevl6_bgir     s                   like(rlbgir)
      *
     iocrin     ns  01    1 cN    7 c3    8 c0
     i                                  3    4 0btjty
     i                                  5    6 0btrty
     i                                  7    8 0brety
     i                                  9   15 0btrnr
     i                                 16   21 0bdato
8.01 i                                 20   21 0baarr
     i                                 22   23 0bseid
     i                                 24   25 0bdagk
     i                                 26   26 0bdela
     i                                 27   31 0blØpn
     i                                 32   32  bfort
     i                                 33   49 2bbelØ
INT  i                                 33   49  bbelØx
     i                                 50   74  bkid
      * Kort kid
6.30 i*                                50   67  btest
6.30 i                                 50   65  btest
7.01 i                                 57   57 0bsant
7.01 i                                 57   60 0bsaar
8.01 i                                 57   61 0bpayx
6.nu i*                                68   73 0bkidr
6.nu i                                 66   73 0bkidr
     i                                 74   74 0bksif
      * Lang kid
     i                                 54   56 0bnfir
     i                                 57   57 0bkode
     i                                 58   58 0bf001
     i                                 59   59  bmvak
6.nu i*                                60   61 0bbetb
6.nu i*                                62   67 0bkund
6.nu i                                 60   65 0bkund
6.nu i*                                68   73 0bfakt
6.nu i                                 66   73 0bfakt
     i                                 75   80 0bf002
     i                                 81   83 0gdqofi
      *
      *  Felt fra beløpspost 2
      *
     i          ns  02    1 cN    7 c3    8 c1
     i                                 16   25  bblnr
     i                                 26   34  bavar
T5.50i                                 48   58 0bgiro
      *
      *  Felt fra autogiropost
      *
     i                                 46   50  autkid
     i                                 70   75 0nyadto
      *
     i                                 42   47 0nybdto
      *
      *  Firma fra avtale-register
      *
     i                                 81   83 0gdqofi
      *
     i          ns  09
      *
     c     dummy         tag
      *
     c                   if        l_firm <> gdqofi
     c                   goto      slutt
     c                   endif
      *                                                                 .
      /eject
      *
      * Behandle transaksjonskode  - 30
      *
     c   01              do
      *
7.01  * Blanker felter for test om '111' kid
7.01 c                   eval      x_kkli = *zeros
7.01 c                   eval      x_kbil = *zeros
7.01 c                   eval      x_kund = *zeros
      *
7.01  * Sjekk om kid fra Santander
7.01 c                   if        bsant = 2
7.01 c                   exsr      santand
7.01 c                   goto      ettest
7.01 c                   endif
8.01  *
8.01  * Sjekk om kid fra PayEx
8.01  *
8.01 c                   if        bpayx = 55555
8.01 c                   exsr      payex
8.01 c                   goto      ettest
8.01 c                   endif
      *
      * Hvis kort kid, hent lang kid fra kid-register
      *
     c                   if        btest = *blank
     c                   eval      akidl1_kidr = bkidr
     c     akidl1_key    chain     akidl1
      *
     c                   if        %found
7.01  * Er dette en kid som starter med  111 ?
7.01 c                   eval      x_dumy = akkidd
7.01 c                   if        x_kkli = 111
7.01 c                   eval      bkode  = *zeros
7.01 c                   eval      bmvak  = *blank
7.01 c                   eval      bkund  = *zeros
7.01 c                   eval      bfakt  = *zeros
7.01 c                   eval      bkid   = *blanks
7.01 c                   move      akkidd        bkid
7.01 c                   goto      etdumy
7.01 c                   endif
7.01  *
T5.00c                   eval      w_kidd = akkidd
      *
T5.00 * Flytter over verdier når kort-kid
      *
T5.00c                   eval      bkode  = w_kode
T5.00c                   eval      bmvak  = w_mvak
6.nu c*                  eval      bbetb  = w_betb
T5.00c                   eval      bkund  = w_kund
T5.00c                   eval      bfakt  = w_fakt
T5.00c                   eval      bkid   = *blanks
T5.00c                   move      w_kid21       bkid
     c                   endif
      *
      * Hvis  kort kid ikke finnes - blank alle felt
      * dette medfører at posteringen stoppes i kontroll
      *
7.01 c     etdumy        tag
      *
     c                   if        not%found
T5.00c                   eval      bkode  = *zeros
T5.00c                   eval      bmvak  = *blank
6.nu c**                 eval      bbetb  = *zeros
T5.00c                   eval      bkund  = *zeros
T5.00c                   eval      bfakt  = *zeros
T5.00c                   eval      bkid   = *blanks
     c                   endif
      *
     c                   endif
7.01  *
7.01 c     ettest        tag
      *
     c                   eval      rjreid = 'H'                                 Rec.-id
     c                   eval      rjfirm = l_firm
     c                   eval      rjkont = bkund
     c                   eval      rjspes = *zero                               Spes.
     c                   eval      rjavde = *zero                               Avd.
     c                   eval      rjmvap = *zero                               Avd.
     c                   eval      rjmmrk = *blank                              Aktivitet
     c     *dmy          move      bdato         rjbdat
     c                   eval      rjbiln = bfakt
     c                   eval      rjbilk = 87                                  Bilagskode
     c                   eval      rjtext = *blank                              Bilagstekst
7.01 c                   if        x_kkli = 111
7.01 c                   eval      rjkont = x_kund
7.01 c                   eval      rjbiln = x_kbil
7.01 c                   endif
      *
      * Behandle negative beløp
      *
INT   *  Håndterer beløp (evt minustegn foran)
      *
INT  c                   if        %scan('-':bbelØx) > 0
INT  c                   eval      bbelØx = %xlate('-':'0':bbelØx)
INT  c                   eval      bfort = '-'
INT  c                   endif
      *
INT  c                   move      bbelØx        bbelØ
     c                   z-add     bbelØ         rjneto                         Beløp
      *
INT  c*                  if        bbelØ < 0
INT  c*                  eval      rjneto = bbelØ * -1                          Beløp
INT  c*                  eval      bfort = '-'
INT  c*                  endif
      *
      * Behandle poster med '-' i fortegnsfeltet
      *
     c                   if        bfort = '-'
     c                   eval      rjdbkr = 'K'                                 Kredit
     c                   else
     c                   eval      rjdbkr = 'D'                                 Debet
     c                   endif
      *
     c                   eval      rjmvab = *zero                               MVA-beløp
     c                   eval      rjiavg = *zero                               Investeringsavgift
     c                   eval      rjstar = *blank                              Overstyrt mva
     c                   eval      rjmvak = bmvak                               MVA-kode
     c                   eval      rjfeik = *blank                              Saldering
     c                   eval      rjatte = *blank                              Attestert
     c                   eval      rjtype = btjty                               Vanlig/Automatisk
     c                   eval      rjbunt = *zero                               Bunt-nr.
     c                   eval      rjlinj = *zero                               Linjenr.
     c                   eval      rjrefn = bfakt                               Referansenummer
7.01 c                   if        x_kkli = 111
7.01 c                   eval      rjrefn = x_kbil
7.01 c                   endif
     c                   eval      rjfdat = *loval                              Forfall
     c                   eval      rjopdt = *blank                              Oppdatert
T5.51c*                  eval      rjpros = *zero                               Prosjekt
T5.51c                   eval      rjpros = *blank                              Prosjekt
     c                   eval      rjvalk = *blank                              Valutakode
     c                   eval      rjvalb = *zero                               Valuta-beløp
     c                   eval      rjmngd = *zero                               Mengde
     c                   eval      rjmngk = *zero                               Mengde-kode
     c                   eval      rjresn = bkund                               Reskontronummer
7.01 c                   if        x_kkli = 111
7.01 c                   eval      rjresn = x_kund
7.01 c                   endif
     c                   eval      rjress = 0                                   Kostnadsbærer
     c                   eval      rjresa = 0                                   Avdeling
     c                   eval      rjresm = *blank                              MVA-kode
     c                   eval      rjvirk = *blank                              Virksomhet
     c                   eval      rjmvag = 0                                   MVA-grunnlag
     c                   eval      rjkrys = *blank                              Avkryssing
     c                   eval      rjrstb = 0                                   Restbeløp
     c                   eval      rjrstv = 0                                   Restbeløp valuta
     c                   eval      rjakti = *blank                              Aktivitet
6.nu c**                 eval      rjbetb = bbetb                               Betalingsbetingelse
     c                   eval      rjbref = *blank                              Bank-referanse
     c                   eval      rjlfak = *blank                              Lev. fakturanummer
     c                   eval      rjdeck = *blank                              Deklarasjonskode
     c                   eval      rjjour = 0                                   Journal
     c                   eval      rjblde = *blank                              Registreringsbilde
     c                   eval      rjovfn = 0                                   Overføringsnummer
     c                   eval      rjflgr = *blank                              Filgruppe
     c                   eval      rjsort = *blank                              Sorteringsfelt
     c                   eval      rjkidi = bkid                                KID-referanse
      *
      * Sjekk om samlefaktura, legg ut kode for saldering/avkryssing
      *
     c                   if        bkode = 1
     c                   move      'S'           rjkrys
     c                   endif
      *
      * Sorteringsbegrep
      *
     c                   move      bdato         wdato
     c                   eval      wsfirm = gdqofi
     c                   move      bseid         wseid
     c                   move      bdagk         wdagk
     c                   move      bdela         wdela
     c                   move      blØpn         wlØpn
     c                   move      wref          rjbref
     c                   move      wref          wsbref
      *
     c                   enddo
      *                                                                 .
      /eject
      *
      * Behandle transaksjonskode  - 31
      *
     c   02              do
      *
      * Hvis ny bilagsdato er utfylt, benytt denne
      *
6.31 c**                 if        nybdto <> *zero
6.31 c**   *dmy          move      nybdto        rjbdat
6.31 c**                 move      nybdto        wdato
6.31 c**                 endif
      *
      * Autogiro - hent ny dato
      *
     c                   if        nyadto <> *zero and
     c                             btjty = 01
     c     *dmy          move      nyadto        rjbdat
     c                   move      nyadto        wdato
     c                   endif
      *
     c                   move      per(wmnd)     rjrper
T5.00c                   move      2000          rjraar
     c                   move      waar          rjraar
      *
     c                   if        wmnd < ra3stp
     c                   eval      rjraar = rjraar - 1
     c                   endif
      *
     c                   move      2000          wsbaar
     c                   move      waar          wsbaar
     c                   move      wmnd          wsbmnd
     c                   move      wdag          wsbdag
      *
      * Autogiro - hent nytt kid
      *
     c                   if        btjty = 01
     c                   move      autkid        rjkidi
     c                   endif
T5.50 *
T5.50 * Hvis  konto ikke er utfylt - sjekk on leverandørpost
T5.50 *
T5.50c                   if        rjkont = 0
T5.50c                   eval      rlevl6_bgir = bgiro
T5.50c     rlevl6_key    chain     rlevl6
T5.50c                   if        %found(rlevl6)
T5.50c                   eval      rjkont = rllevr
T5.50c                   eval      rjresn = rllevr
     c                   eval      rjkrys = 'X'                                 Avkryssing
T5.50c                   endif
T5.50c                   endif
      *
     c                   move      wssort        rjsort
      *
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
     c                   enddo
      *
      /space 3
      *
      * Sletter innput-rec. fortløpende
      *
     c                   delete    ocrin
      *
      * Ved evnt. stopp; kan rutinen kjøres på nytt hvor
      * ubehandlede poster vil bli tatt med (RJOUPF blir ikke tømt).
      *
     c     slutt         tag
      *
      /eject
7.01  *************************************************************
7.01 c     santand       begsr
7.01 c                   eval      akidl2_firm = l_firm
7.01 c                   eval      akidl2_kid2 = bkid
7.01 c                   eval      akidl2_kaar = bsaar
7.01 c                   eval      akidl2_kidr = *zero
7.01  *
7.01 c     akidl2_key    setll     akidl2
7.01  *
7.01 c     nyles         tag
7.01 c                   read      akidl2                                 15
7.01  * EOF eller akid2 har ikke slått til
7.01 c                   if        *in15 = *on or
7.01 c                             xafirm <> akidl2_firm or
7.01 c                             xaaarr <> akidl2_kaar
7.01 c                   eval      bkode  = *zeros
7.01 c                   eval      bmvak  = *blank
7.01 c                   eval      bkund  = *zeros
7.01 c                   eval      bfakt  = *zeros
7.01 c                   eval      bkid   = *blanks
7.01 c                   goto      ikles
7.01 c                   endif
7.01  *
7.01 c                   if        akidl2_kid2 = xakid2
7.01 c                   eval      w_kidd = xakidd
7.01  *
7.01 c                   eval      bkode  = w_kode
7.01 c                   eval      bmvak  = w_mvak
7.01 c                   eval      bkund  = w_kund
7.01 c                   eval      bfakt  = w_fakt
7.01 c                   eval      bkid   = *blanks
7.01 c                   move      w_kid21       bkid
7.01 c                   else
7.01 c                   goto      nyles
7.01 c                   endif
7.01 c
7.01 c     ikles         tag
7.01  *
7.01 c                   endsr
8.01  *
8.01  * Subrutine for spesialbehandling av PayEx OCR
8.01  *
8.01 c     payex         begsr
8.01 c                   eval      w_aarr = 2000 + baarr
8.01 c                   eval      akidl2_firm = l_firm
8.01 c                   eval      akidl2_kid2 = bkid
8.01 c                   eval      akidl2_kaar = w_aarr
8.01 c                   eval      akidl2_kidr = *zero
8.01  *
8.01 c     akidl2_key    setll     akidl2
8.01  *
8.01 c     nyles1        tag
8.01 c                   read      akidl2                                 15
8.01  * EOF eller akid2 har ikke slått til
8.01 c                   if        *in15 = *on or
8.01 c                             xafirm <> akidl2_firm or
8.01 c                             xaaarr <> akidl2_kaar
8.01 c                   eval      bkode  = *zeros
8.01 c                   eval      bmvak  = *blank
8.01 c                   eval      bkund  = *zeros
8.01 c                   eval      bfakt  = *zeros
8.01 c                   eval      bkid   = *blanks
8.01 c                   goto      ikles1
8.01 c                   endif
8.01  *
8.01 c                   if        akidl2_kid2 = xakid2
8.01 c                   eval      w_kidd = xakidd
8.01  *
8.01 c                   eval      bkode  = w_kode
8.01 c                   eval      bmvak  = w_mvak
8.01 c                   eval      bkund  = w_kund
8.01 c                   eval      bfakt  = w_fakt
8.01 c                   eval      bkid   = *blanks
8.01 c                   move      w_kid21       bkid
8.01 c                   else
8.01 c                   goto      nyles1
8.01 c                   endif
8.01 c
8.01 c     ikles1        tag
8.01  *
8.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
610  c     *entry        plist
610  c                   parm                    w_memb                         user
      * Legge inn firma
      *
     c                   move      l_firm        w_firm
      *
      * Hent statuskode 3
      *
     c     raa3l1_key    chain     raa3l1                             61
      *
     c                   if        *in61 = *off
     c                   move      ra3p01        per(01)
     c                   move      ra3p02        per(02)
     c                   move      ra3p03        per(03)
     c                   move      ra3p04        per(04)
     c                   move      ra3p05        per(05)
     c                   move      ra3p06        per(06)
     c                   move      ra3p07        per(07)
     c                   move      ra3p08        per(08)
     c                   move      ra3p09        per(09)
     c                   move      ra3p10        per(10)
     c                   move      ra3p11        per(11)
     c                   move      ra3p12        per(12)
     c                   move      ra3p13        per(13)
     c                   endif
      *
     c                   if        *in61 = *on
     c                   move      01            ra3stp
     c                   endif
      *
      * Key for statuskode 3
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kid-register
      *
     c     akidl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    akidl1_kidr
7.01  * Key for kid-register
7.01  *
7.01 c     akidl2_key    klist
7.01 c                   kfld                    akidl2_firm
7.01 c                   kfld                    akidl2_kid2
7.01 c                   kfld                    akidl2_kaar
7.01 c                   kfld                    akidl2_kidr
7.01  *
T5.50 * Key for leverandørregister
T5.50 *
T5.50c     rlevl6_key    klist
T5.50c                   kfld                    w_firm
T5.50c                   kfld                    rlevl6_bgir
      *
     c                   endsr
      *
** TAB - NORMALT REGNSKAPSÅR
01020304050607080910111213
