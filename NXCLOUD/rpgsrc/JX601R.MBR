     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JX601R
      * Beskrivelse . . : Utskrift av priser for prisgivende leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280398 HKO  Nytt program
      *       280506 HKO  Endret parameter ved kall av JV750R
      *       280508 bof  Utvidet parm. til JV750R
5.70  *       201108 bof  Utvidet med prisgruppe
6.10  *       260809 bof  Endret statuskode fra num. til alfa.
os71  * os71  161210 bsa  Lagt inn korreksjoner på SQL pga. OS/400 feil
os71  *                   Det er også lagt inn endring på SQL,
os71  *                   som er nødvendig ved overgang til v6.1
6.30  *       160714 bof  Lev. varenr hentes fra JVLEPF
6.31  *       060814 bof  Utvidelse med fraktbetingelse
6.3q  *       260515 bof  Datoformat sql
6.3x  * 6.30  040615 bof  utvidet parm i forb. med prgr. på betingelse
6.3y  * 6.30  050815 bof  utv. parm i forb. med prgr. på frakt + oppdat.
6.34  * 6.30  010416 bof  rettelse i parm til jv750r                   .
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvprpf    if   e           k disk
     fjvarpf    if   e           k disk
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.30 fjvlelr    if   e           k disk    rename(jvlepfr:jvlelrr)
      *
     fjx601p    o    e             printer
      *
      * Definering av parametre
      *
     d p_list          s             64
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                        10
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        64
     D  d_firm                        3  0 overlay(d_list)
     D  d_rtyp                        1    overlay(d_list:4)
     D  d_pldo                        6  0 overlay(d_list:5)
     D  d_dato                         d   overlay(d_list:11) datfmt(*iso)
     D  d_oper                        2    overlay(d_list:21)
     D  d_ldor                        6  0 overlay(d_list:23)
     D  d_ogrp                        2  0 overlay(d_list:29)
     D  d_hgrp                        2  0 overlay(d_list:31)
     D  d_ugrp                        3  0 overlay(d_list:33)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
6.30 d jvlelr_lldo     s                   like(jvlldo)
6.30 d jvlelr_lvnr     s                   like(jvlvnr)
      *
      * Definering av variable
      *
     d b_EOF           s              1    inz(*off)
     d b_vare_evr      s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_timestamp     s             12  0
     d w_ldor_alf      s              6
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
8.01 d w_pgbe          s              2
8.01 d w_pgfr          s              2
6.3y d w_oppd          s              1
6.34 d w_utgp          s              1  0
      *
     d w_midl          s              1  0
     d w_bvty          s              1
     d w_blet          s              1  0
     d w_bbet          s             35
     d w_enh2          s              3
     d w_enh3          s              3
     d w_eni1          s              3
     d w_eni2          s              3
     d w_eni3          s              3
     d w_enp1          s              3
     d w_enp2          s              3
     d w_enp3          s              3
     d w_gdat          s               d   datfmt(*iso)
     d w_grpr          s              9  2
     d w_ifak          s              8  6
     d w_inpr          s              9  2
     d w_kbel          s              7  2
     d w_kfak          s              8  6
     d w_ntpr          s              8  2
     d w_ofp1          s             12  6
     d w_ofp2          s             12  6
     d w_ofp3          s             12  6
     d w_ofv2          s             12  6
     d w_ofv3          s             12  6
     d w_ofi1          s             12  6
     d w_ofi2          s             12  6
     d w_ofi3          s             12  6
     d w_ppko          s              1
     d w_pvty          s              1
     d w_plet          s              1  0
     d w_sfak          s              8  6
     d w_s2im          s              9  2
     d w_s2pr          s              9  2
     d w_s3im          s              9  2
     d w_s3pr          s              9  2
     d w_lety          s              1  0
     d w_inlr          s              1
     d w_bugm          s              9  2
     d w_ingm          s              9  2
     d w_gprk          s              1  0
6.31 d w_ffak          s              8  6
6.31 d w_inp2          s              9  2
      *
     d w_firm          s              3  0
     d w_lnav          s                   like(jlnavn)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
      *
     d w_bldo          s                   like(jvldor)
     d w_bogr          s                   like(jvogrp)
     d w_bhgr          s                   like(jvhgrp)
     d w_bugr          s                   like(jvugrp)
     d w_bmod          s                   like(jvmodn)
     d w_bvar          s                   like(jvvare)
     d w_eldo          s                   like(jvldor)
     d w_ldor          s                   like(jvldor)
     d w_lvar          s                   like(jvlvar)
     d w_pogr          s                   like(jvogrp)
     d w_phgr          s                   like(jvhgrp)
     d w_pugr          s                   like(jvugrp)
     d w_pldo          s                   like(jvldor)
     d w_pmod          s                   like(jvmodn)
     d w_pvar          s                   like(jvvare)
8.01 d w_prgr          s              2
      *
     d s_ldor          s                   like(jvldor)
     d s_ogrp          s                   like(jvogrp)
     d s_hgrp          s                   like(jvhgrp)
     d s_ugrp          s                   like(jvugrp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Behandling av utskrift
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2pldo = d_pldo
     c                   eval      a2ldor = d_ldor
     c                   eval      a2oper = d_oper
     c                   eval      a2ogrp = d_ogrp
     c                   eval      a2hgrp = d_hgrp
     c                   eval      a2ugrp = d_ugrp
      *
     c                   select
     c                   when      d_rtyp = '1'
     c                   eval      a2rtyp = 'Kun varer i EVR'
     c                   when      d_rtyp = '2'
     c                   eval      a2rtyp = 'Ikke varer i EVR'
     c                   other
     c                   eval      a2rtyp = 'Alle varer'
     c                   endsl
      *
     c                   eval      jlevl1_ldor = d_pldo
     c     jlevl1_key    chain     jlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2navn = jlnavn
     c                   endif
      *
     c                   if        d_dato <> *loval
     c     *dmy          move      d_dato        a2dato
     c                   endif
      *
     c                   if        d_ldor <> 0
     c                   eval      jlevl1_ldor = d_ldor
     c     jlevl1_key    chain     jlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   select
     c                   when      d_ugrp <> 0
     c                   eval      vugrl1_uogr = d_ogrp
     c                   eval      vugrl1_uhgr = d_hgrp
     c                   eval      vugrl1_uugr = d_ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   eval      a2utxt = vgutxt
     c                   when      d_hgrp <> 0
     c                   eval      vhgrl1_hogr = d_ogrp
     c                   eval      vhgrl1_hhgr = d_hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   eval      a2utxt = vghtxt
     c                   when      d_ogrp <> 0
     c                   eval      vogrl1_oogr = d_ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   eval      a2utxt = vgotxt
     c                   endsl
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster som matcher utplukkskriteriene
      * Behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c/exec sql
     c+ declare priser cursor for
os71 c+ select v.jvldor,
os71 c+        v.jvogrp,
os71 c+        v.jvhgrp,
os71 c+        v.jvugrp,
os71 c+        v.jvtek1,
os71 c+        v.jvvare,
os71 c+        v.jvnobb,
os71 c+        p.jxvare,
os71 c+        p.jxldor,
os71 c+        p.jxgdat,
os71 c+        p.jxpris,
os71 c+        p.jxrsts
os71 c+ from   jvprpf as p inner join
os71 c+        jvarpf as v on
os71 c+        p.jxvare  = v.jvvare
os71 c+ where  p.jxldor  = :d_pldo and
os71 c+       (v.jvldor  = :d_ldor or
     c+        :d_ldor = 0) and
os71 c+       (v.jvogrp  = :d_ogrp or
     c+        :d_ogrp = 0) and
os71 c+       (v.jvhgrp  = :d_hgrp or
     c+        :d_hgrp = 0) and
os71 c+       (v.jvugrp  = :d_ugrp or
     c+        :d_ugrp = 0)
os71 c+ order by v.jvldor, v.jvogrp, v.jvhgrp, v.jvugrp, v.jvtek1, p.jxvare
     c+ for read only
     c/end-exec
      *
     c/exec sql
     c+ open priser
     c/end-exec
      *
      * Leser cursor priser
      *
     c                   dow       b_EOF = *off
      *
     c/exec sql
     c+ fetch priser
     c+ into  :jvldor,
     c+       :jvogrp,
     c+       :jvhgrp,
     c+       :jvugrp,
     c+       :jvtek1,
     c+       :jvvare,
     c+       :jvnobb,
     c+       :jxvare,
     c+       :jxldor,
     c+       :jxgdat,
     c+       :jxpris,
     c+       :jxrsts
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   leave
     c                   endif
      *
      * Henter vareinformasjon fra EVR
      *
     c                   eval      b_vare_evr = *off
     c                   eval      vvarl1_vare = jvvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      b_vare_evr = *on
     c                   endif
      *
      * Utplukk
      *
     c                   if        d_rtyp = '1' and
     c                             b_vare_evr = *off
     c                   iter
     c                   endif
      *
     c                   if        d_rtyp = '2' and
     c                             b_vare_evr = *on
     c                   iter
     c                   endif
      *
     c                   if        d_dato <> *loval
     c                   select
     c                   when      (d_oper = *blank or
     c                              d_oper = 'EQ') and
     c                             jxgdat <> d_dato
     c                   iter
     c                   when      d_oper = 'GT' and
     c                             jxgdat <= d_dato
     c                   iter
     c                   when      d_oper = 'LT' and
     c                             jxgdat >= d_dato
     c                   iter
     c                   endsl
     c                   endif
      *
      * Behandler brudd
      *
     c                   if        jvldor <> s_ldor
     c                   exsr      hode_ldor
     c                   endif
      *
     c                   if        jvogrp <> s_ogrp
     c                   exsr      hode_ogrp
     c                   endif
      *
     c                   if        jvogrp <> s_ogrp or
     c                             jvhgrp <> s_hgrp
     c                   exsr      hode_hgrp
     c                   endif
      *
     c                   if        jvogrp <> s_ogrp or
     c                             jvhgrp <> s_hgrp or
     c                             jvugrp <> s_ugrp
     c                   exsr      hode_ugrp
     c                   endif
      *
6.30 c                   eval      jvlelr_lvnr = jxvare
6.30 c                   eval      jvlelr_lldo = jxldor
6.30 c     jvlelr_key    chain     jvlelr                             90
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Tar vare på brudd-variable
      *
     c                   eval      s_ogrp = jvogrp
     c                   eval      s_hgrp = jvhgrp
     c                   eval      s_ugrp = jvugrp
     c                   eval      s_ldor = jvldor
      *
     c                   enddo
      *
      * Lukker cursor priser
      *
     c/exec sql
     c+ close priser
     c/end-exec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på leverandøre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ldor     begsr
      *
      * Dersom leverandør er utelatt eller mangler på vare, skrives blank
      * linje
      *
     c                   if        jvldor = 0
     c                   write     space                                30
     c                   exsr      overflow
     c                   goto      end_hode_ldor
     c                   endif
      *
      * Dersom leverandør ikke er utelatt, skrives leverandørnavn
      *
     c                   eval      w_lnav = *blank
      *
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lnav = jlnavn
     c                   endif
      *
     c                   move      jvldor        w_ldor_alf
      *
     c                   eval      a4txt1 = %trim( %trim(w_lnav) + ' (' +
     c                                      w_ldor_alf + ')')
      *
     c                   eval      a4txt2 = a4txt1
      *
     c                   write     a4hdr                                30
     c                   exsr      overflow
      *
     c     end_hode_ldor endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ogrp     begsr
      *
     c                   eval      w_otxt = *blank
      *
     c                   eval      vogrl1_oogr = jvogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_hgrp     begsr
      *
     c                   eval      w_htxt = *blank
      *
     c                   eval      vhgrl1_hogr = jvogrp
     c                   eval      vhgrl1_hhgr = jvhgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ugrp     begsr
      *
     c                   eval      w_utxt = *blank
      *
     c                   eval      vugrl1_uogr = jvogrp
     c                   eval      vugrl1_uhgr = jvhgrp
     c                   eval      vugrl1_uugr = jvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c                   move      jvogrp        w_ogrp_alf
     c                   move      jvhgrp        w_hgrp_alf
     c                   move      jvugrp        w_ugrp_alf
      *
     c                   eval      a5txt1 = %trim( %trim(w_otxt) + ' - ' +
     c                                      %trim(w_htxt) + ' - ' +
     c                                      %trim(w_utxt) + ' (' +
     c                                      w_ogrp_alf + ' ' +
     c                                      w_hgrp_alf + ' ' +
     c                                      w_ugrp_alf + ')')
      *
     c                   eval      a5txt2 = a5txt1
      *
     c                   write     a5hdr                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
      * Nullstilling
      *
     c                   eval      d1gdat = 0
     c                   eval      d1rsts = *blank
     c                   eval      d1kogm = 0
     c                   eval      d1s1gm = 0
     c                   eval      d1endr = 0
      *
      * Henter verdier til detaljlinje
      *
     c                   eval      d1tek1 = jvtek1
     c                   eval      d1vare = jvvare
     c                   eval      d1nobb = jvnobb
6.30 c                   eval      d1lvar = jvllva
     c                   eval      d1pris = jxpris
      *
     c                   if        jxgdat <> *loval
     c     *dmy          move      jxgdat        d1gdat
     c                   endif
      *
     c                   if        jxrsts = 1
     c                   eval      d1rsts = 'L'
     c                   endif
      *
      * Henter vare-detalj informasjon
      *
     c                   eval      jvdtl1_dvar = jvvare
     c     jvdtl1_key    chain     jvdtl1
     c                   if        not %found
     c                   eval      jvdsko = 0
     c                   eval      jvdpko = *blank
     c                   endif
      *
      * Kaller sub-program for å hente kalkulerte verdier for
      * prisgivende leverandør
      *
     c                   eval      w_gprk = 0
6.3y c                   eval      w_oppd = *blank
6.34 c                   eval      w_utgp = 1
      *
     c                   call      'JV750R'
      *
     c                   parm                    w_firm
     c                   parm                    jvvare
5.70 c                   parm                    w_prgr
     c                   parm                    jxldor
     c                   parm                    jvdpko
     c                   parm                    jvdsko
     c                   parm                    w_lety
     c                   parm                    w_gprk
6.34 c                   parm                    w_udat
6.34 c                   parm                    w_utgp
      *
     c                   parm                    d1osts
      *
     c                   parm                    w_ldor
     c                   parm                    w_lvar
     c                   parm                    w_eldo
     c                   parm                    w_grpr
     c                   parm                    w_gdat
      *
     c                   parm                    d1ekod
     c                   parm                    d1enh1
     c                   parm                    w_enh2
     c                   parm                    w_enh3
     c                   parm                    w_eni1
     c                   parm                    w_eni2
     c                   parm                    w_eni3
     c                   parm                    w_enp1
     c                   parm                    w_enp2
     c                   parm                    w_enp3
     c                   parm                    w_ofp1
     c                   parm                    w_ofp2
     c                   parm                    w_ofp3
     c                   parm                    w_ofv2
     c                   parm                    w_ofv3
     c                   parm                    w_ofi1
     c                   parm                    w_ofi2
     c                   parm                    w_ofi3
      *
     c                   parm                    w_bldo
     c                   parm                    w_bogr
     c                   parm                    w_bhgr
     c                   parm                    w_bugr
     c                   parm                    w_bmod
     c                   parm                    w_bvar
     c                   parm                    w_bvty
     c                   parm                    w_blet
     c                   parm                    w_bbet
     c                   parm                    w_ntpr
     c                   parm                    w_ifak
      *
     c                   parm                    w_pogr
     c                   parm                    w_phgr
     c                   parm                    w_pugr
     c                   parm                    w_pldo
     c                   parm                    w_pmod
     c                   parm                    w_pvar
     c                   parm                    w_ppko
     c                   parm                    w_pvty
     c                   parm                    w_plet
     c                   parm                    w_kfak
     c                   parm                    w_kbel
     c                   parm                    w_sfak
      *
     c                   parm                    w_midl
     c                   parm                    w_inpr
     c                   parm                    d1kopr
     c                   parm                    d1s1pr
     c                   parm                    d1s1im
     c                   parm                    w_s2pr
     c                   parm                    w_s2im
     c                   parm                    w_s3pr
     c                   parm                    w_s3im
     c                   parm                    d1dekn
5.70 c                   parm                    d1prut
6.31 c                   parm                    w_ffak
6.31 c                   parm                    w_inp2
6.3x c                   parm                    w_pgbe
6.3y c                   parm                    w_pgfr
6.3y c                   parm                    w_oppd
      *
6.31 c                   eval(h)   w_inpr = w_inpr * w_ffak
      *
      * Henter priser fra EVR, og kalkulerer %-endring i salgspris dersom
      * varen finnes i EVR.
      *
     c                   if        b_vare_evr = *on
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    jvvare
5.70 c                   parm                    w_prgr
     c                   parm                    d1enh1
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    w_inlr
     c                   parm                    d1s1gm
     c                   parm                    w_bugm
     c                   parm                    d1kogm
     c                   parm                    w_ingm
      *
     c                   if        d1s1gm <> 0
     c                   if        (100 * d1s1pr / d1s1gm) - 100 > 9999,99
     c                   eval      d1endr = 9999,99
     c                   else
     c                   eval(h)   d1endr = (100 * d1s1pr / d1s1gm) - 100
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for opplag på EVR-vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key les på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
6.30  *
6.30  * Key for les på vare-leverandør
6.30  *
6.30 c     jvlelr_key    klist
6.30 c                   kfld                    jvlelr_lvnr
6.30 c                   kfld                    jvlelr_lldo
      *
      *
      * Kopierer parameter-liste til datastruktur, og variable
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = d_firm
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c                   endsr
