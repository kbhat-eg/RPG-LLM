     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV780R
      * Beskrivelse . . : Vare, kopiering til annen filgruppe - param.
      *
      * NB! d_dumm har som oppgave å forhindre "forsøpling" av datastruktur
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       171199 HKO  Nytt program
      *       100402 HKO  Lagt inn parametre for å beholde priskode, aut.
      *                   oppretting av varesortiment og kopiering av
      *                   leverandørinfo og rabattmatrise/spesialpriser
      *       040703 HKO  Endret header på bilde
      *       240107 HKO  Utvidet valg med "Varer opprettet dato' og
      *                   firma innenfor filgruppe
      *       120407 HKO  Lagt inn overstyring av varetype
6.31  *       040718 bof  Utvidet med les varer fra csv-fil
7.xx  * 7.00  010416 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvv780d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                       260
     d  d_fgrp                        3    overlay(d_list:1)
     d  d_firm                        3  0 overlay(d_list:4)
     d  d_ldor                        6  0 overlay(d_list:7)
     d  d_ogrp                        2  0 overlay(d_list:13)
     d  d_hgrp                        2  0 overlay(d_list:15)
     d  d_ugrp                        3  0 overlay(d_list:17)
     d  d_var0                       15    overlay(d_list:20)
     d  d_var1                       15    overlay(d_list:35)
     d  d_var2                       15    overlay(d_list:50)
     d  d_var3                       15    overlay(d_list:65)
     d  d_var4                       15    overlay(d_list:80)
     d  d_var5                       15    overlay(d_list:95)
     d  d_var6                       15    overlay(d_list:110)
     d  d_var7                       15    overlay(d_list:125)
     d  d_var8                       15    overlay(d_list:140)
     d  d_var9                       15    overlay(d_list:155)
     d  d_odat                         d   overlay(d_list:170) datfmt(*iso)
     d  d_vtyp                        1    overlay(d_list:180)
     d  d_fgt0                        3    overlay(d_list:181)
     d  d_fit0                        3  0 overlay(d_list:184)
     d  d_fgt1                        3    overlay(d_list:187)
     d  d_fit1                        3  0 overlay(d_list:190)
     d  d_fgt2                        3    overlay(d_list:193)
     d  d_fit2                        3  0 overlay(d_list:196)
     d  d_fgt3                        3    overlay(d_list:199)
     d  d_fit3                        3  0 overlay(d_list:202)
     d  d_fgt4                        3    overlay(d_list:205)
     d  d_fit4                        3  0 overlay(d_list:208)
     d  d_fgt5                        3    overlay(d_list:211)
     d  d_fit5                        3  0 overlay(d_list:214)
     d  d_fgt6                        3    overlay(d_list:217)
     d  d_fit6                        3  0 overlay(d_list:220)
     d  d_fgt7                        3    overlay(d_list:223)
     d  d_fit7                        3  0 overlay(d_list:226)
     d  d_fgt8                        3    overlay(d_list:229)
     d  d_fit8                        3  0 overlay(d_list:232)
     d  d_fgt9                        3    overlay(d_list:235)
     d  d_fit9                        3  0 overlay(d_list:238)
6.31 d  d_file                        1    overlay(d_list:241)
     d  d_dumm                        1    overlay(d_list:260)
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_tek1          s                   like(vvtek1)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Flytter skjermfelter til datastruktur -ut
      *
     c                   eval      d_fgrp = l_fgrp
     c                   eval      d_firm = l_firm
     c                   eval      d_ldor = b1ldor
     c                   eval      d_ogrp = b1ogrp
     c                   eval      d_hgrp = b1hgrp
     c                   eval      d_ugrp = b1ugrp
     c                   eval      d_var0 = b1var0
     c                   eval      d_var1 = b1var1
     c                   eval      d_var2 = b1var2
     c                   eval      d_var3 = b1var3
     c                   eval      d_var4 = b1var4
     c                   eval      d_var5 = b1var5
     c                   eval      d_var6 = b1var6
     c                   eval      d_var7 = b1var7
     c                   eval      d_var8 = b1var8
     c                   eval      d_var9 = b1var9
     c                   eval      d_odat = *loval
     c                   eval      d_vtyp = b1vtyp
     c                   eval      d_fgt0 = b1fgt0
     c                   eval      d_fit0 = b1fit0
     c                   eval      d_fgt1 = b1fgt1
     c                   eval      d_fit1 = b1fit1
     c                   eval      d_fgt2 = b1fgt2
     c                   eval      d_fit2 = b1fit2
     c                   eval      d_fgt3 = b1fgt3
     c                   eval      d_fit3 = b1fit3
     c                   eval      d_fgt4 = b1fgt4
     c                   eval      d_fit4 = b1fit4
     c                   eval      d_fgt5 = b1fgt5
     c                   eval      d_fit5 = b1fit5
     c                   eval      d_fgt6 = b1fgt6
     c                   eval      d_fit6 = b1fit6
     c                   eval      d_fgt7 = b1fgt7
     c                   eval      d_fit7 = b1fit7
     c                   eval      d_fgt8 = b1fgt8
     c                   eval      d_fit8 = b1fit8
     c                   eval      d_fgt9 = b1fgt9
     c                   eval      d_fit9 = b1fit9
6.31 c                   eval      d_file = *blank
     c                   eval      d_dumm = '*'
      *
     c                   if        b1odat <> 0
     c     *dmy          move      b1odat        d_odat
     c                   endif
      *
      * Kaller overføringsprogram
      *
     c                   call      'VV780C'
     c                   parm                    d_list
      *
     c                   exfmt     b1sbm
      *
     c                   eval      b1ldor = 0
     c                   eval      b1ogrp = 0
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   eval      b1var0 = *blank
     c                   eval      b1var1 = *blank
     c                   eval      b1var2 = *blank
     c                   eval      b1var3 = *blank
     c                   eval      b1var4 = *blank
     c                   eval      b1var5 = *blank
     c                   eval      b1var6 = *blank
     c                   eval      b1var7 = *blank
     c                   eval      b1var8 = *blank
     c                   eval      b1var9 = *blank
     c                   eval      b1odat = 0
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   move      *on           *inlr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (fd)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        wactfe = 'B1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1lnav
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        wactfe = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   eval      b1utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        wactfe = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   eval      b1utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        wactfe = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        wactfe = 'B1VAR0'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var0
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR1'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var1
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR2'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var2
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR3'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var3
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR4'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var4
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR5'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var5
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR6'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var6
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR7'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var7
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR8'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var8
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   if        wactfe = 'B1VAR9'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1var9
     c                   parm                    w_tek1
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1lnav = *blank
     c                   eval      b1utxt = *blank
      *
      * Leverandør eller vare må oppgis
      *
     c                   if        b1ldor = 0 and
     c                             b1var0 = *blank and
     c                             b1var1 = *blank and
     c                             b1var2 = *blank and
     c                             b1var3 = *blank and
     c                             b1var4 = *blank and
     c                             b1var5 = *blank and
     c                             b1var6 = *blank and
     c                             b1var7 = *blank and
     c                             b1var8 = *blank and
     c                             b1var9 = *blank and
     c                             b1odat = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldor <> 0
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        b1var0 <> *blank
     c                   eval      vvarl1_vare = b1var0
     c     vvarl1_key    chain     vvarl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var1 <> *blank
     c                   eval      vvarl1_vare = b1var1
     c     vvarl1_key    chain     vvarl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var2 <> *blank
     c                   eval      vvarl1_vare = b1var2
     c     vvarl1_key    chain     vvarl1                             38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var3 <> *blank
     c                   eval      vvarl1_vare = b1var3
     c     vvarl1_key    chain     vvarl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var4 <> *blank
     c                   eval      vvarl1_vare = b1var4
     c     vvarl1_key    chain     vvarl1                             40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var5 <> *blank
     c                   eval      vvarl1_vare = b1var5
     c     vvarl1_key    chain     vvarl1                             41
     c                   if        *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var6 <> *blank
     c                   eval      vvarl1_vare = b1var6
     c     vvarl1_key    chain     vvarl1                             42
     c                   if        *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var7 <> *blank
     c                   eval      vvarl1_vare = b1var7
     c     vvarl1_key    chain     vvarl1                             43
     c                   if        *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var8 <> *blank
     c                   eval      vvarl1_vare = b1var8
     c     vvarl1_key    chain     vvarl1                             44
     c                   if        *in44 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1var9 <> *blank
     c                   eval      vvarl1_vare = b1var9
     c     vvarl1_key    chain     vvarl1                             45
     c                   if        *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1odat <> 0
      *
     c     *dmy          test(d)                 b1odat                 46
     c                   if        *in46 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1var0 <> *blank or
     c                             b1var1 <> *blank or
     c                             b1var2 <> *blank or
     c                             b1var3 <> *blank or
     c                             b1var4 <> *blank or
     c                             b1var5 <> *blank or
     c                             b1var6 <> *blank or
     c                             b1var7 <> *blank or
     c                             b1var8 <> *blank or
     c                             b1var9 <> *blank
     c                   eval      *in47 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
     c                   if        b1vtyp <> *blank and
     c                             b1vtyp <> 'L' and
     c                             b1vtyp <> 'S'
     c                   eval      *in48 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   move      l_firm        w_firm
      *
     c                   endsr
