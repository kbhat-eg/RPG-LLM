      /TITLE  Beregning og oppdatering av nummerserier
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AS100R
      * Beskrivelse . . : Beregning og oppdatering av nummerserier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.11 300800 AMD  Lagt om til ILE-RPG
3.30  * R3.30 120901 AMD  Justering i tildeling av nummer når en ordre
3.30  *                   avsluttes uten at varelinjer er registrert.
6.nu  * R6.30 190614 bof  Endring ihht. nummerutvidelse
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *
     fanuml1    if   e           k disk
     f                                     rename(anumpfr:anuml1r)
     fanumlu    uf   e           k disk
     f                                     rename(anumpfr:anumlur)
      *
      * Definering av parametere
      *
     d p_kode          s              1
     d p_firm          s              3  0
     d p_fell          s              6
     d p_type          s              2
     d p_fast          s             10
6.nu d p_numm          s              8  0
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(anfirm)
     d anuml1_fell     s                   like(anfell)
     d anuml1_type     s                   like(antype)
     d anuml1_fast     s                   like(anfast)
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter opplysninger fra nummer-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Forklaring på p_kode som sendes til/fra oppkallende program
      *
      *    Kode sendt fra program :
      *                                 0 = Hent standard nummer
      *                                 1 = Legg tilbake ett nummer
      *                                 2 = Sjekk manuellt innslått nummer
      *
      *    Kode sendt tilbake     :
      *                                 0 = Ok, nummer er hentet
      *                                 7 = Manuellt nummer er utenfor gr.
      *                                 8 = Alle nummer er benyttet
      *                                 9 = Nummerserie finnes ikke
      *
      *
      * Sjekk, Identifikasjon + Type + Fast begrep
      *
     c                   eval      anuml1_type = p_type
     c                   eval      anuml1_fast = p_fast
     c     anuml1_key    chain     anuml1r                            66
     c                   if        *in66 = *off
     c                   if        p_kode = '2'
     c                   if        anauto = 'M'
     c                   goto      xstart
     c                   endif
     c                   else
     c                   if        anauto = 'A' or
     c                             anauto = *blank
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
      *
      * Sjekk, Identifikasjon + Type
      *
     c                   eval      anuml1_fast = *blank
     c     anuml1_key    chain     anuml1r                            66
     c                   if        *in66 = *off
     c                   if        p_kode = '2'
     c                   if        anauto = 'M'
     c                   goto      xstart
     c                   endif
     c                   else
     c                   if        anauto = 'A' or
     c                             anauto = *blank
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
      *
      * Sjekk, Identifikasjon
      *
     c                   eval      anuml1_type = *blank
     c     anuml1_key    chain     anuml1r                            66
     c                   if        *in66 = *off
     c                   if        p_kode = '2'
     c                   if        anauto = 'M'
     c                   goto      xstart
     c                   endif
     c                   else
     c                   if        anauto = 'A' or
     c                             anauto = *blank
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
      *
      * Det ble ikke funnet noen nummer-serie
      *
     c                   eval      p_kode = '9'
     c                   eval      p_numm = *zero
     c                   goto      xslutt
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Velger hva som skal utføres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xstart        tag
      *
      * KODE=2, Sjekk om nummer er i Manuell serie
      *
     c                   if        p_kode = '2'
     c                   exsr      mannnr
     c                   goto      xslutt
     c                   endif
      *
      * Chain NUMMER-REGISTER, for binding av record
      *
     c     anuml1_key    chain     anumlur                            66
      *
      * KODE=0, Hent neste ledige nummer i Automatisk serie
      *
     c                   if        p_kode = '0'
     c                   exsr      nyttnr
     c                   goto      xoppda
     c                   endif
      *
      * KODE=1, Legg tilbake sist brukte nummer i Automatisk serie
      *
     c                   if        p_kode = '1'
     c                   exsr      gammnr
     c                   goto      xoppda
     c                   endif
      *
      *
     c                   goto      xslutt
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xoppda        tag
      *
     c                   if        p_kode = '0'
     c                   if        *in66 = *off
     c                   eval      ansist = p_numm
     c                   update    anumlur
     c                   endif
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beregner nytt nummer som skal benyttes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     nyttnr        begsr
      *
     c                   eval      p_kode = '0'
     c                   eval      p_numm = ansist + 1
      *
     c                   if        p_numm > anntom
     c                   if        anwrap = '1'
     c                   eval      p_numm = annfom
     c                   eval      p_numm = p_numm + 1
     c                   else
     c                   eval      p_numm = *zero
     c                   eval      p_kode = '8'
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beregner nummer som skal legges tilbake
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     gammnr        begsr
      *
     c                   eval      p_kode = '0'
      *
      * Legger tilbake nummeret bare dersom det er sist brukte
      *
     c                   if        p_numm = ansist
     c                   eval      p_numm = ansist - 1
      *
3.30 c***                if        p_numm <= annfom
3.30 c***                if        anwrap = '1'
3.30 c***                eval      p_numm = anntom
3.30 c***                endif
3.30 c***                endif
3.30  *
3.30 c                   else
3.30 c                   eval      p_numm = ansist
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om manuellt nummer er innenfor serien
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     mannnr        begsr
      *
     c                   if        p_numm < annfom or
     c                             p_numm > anntom
     c                   eval      p_kode = '7'
     c                   eval      p_numm = *zero
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      * Key for file : NUMMER-REGISTER
      *
     c     anuml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anuml1_fell
     c                   kfld                    anuml1_type
     c                   kfld                    anuml1_fast
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_fell
     c                   parm                    p_type
     c                   parm                    p_fast
     c                   parm                    p_numm
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm      = p_firm
     c                   eval      anuml1_fell = p_fell
      *
     c                   endsr
