     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RF228R
      * Beskrivelse . . : Tilganger, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110117 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fruktlr    if   e           k disk    rename(ruktpfr:ruktlrr)
     fruktlu    uf a e           k disk    rename(ruktpfr:ruktlur)
     fruktl1    if   e           k disk    rename(ruktpfr:ruktl1r)
     frukflr    if   e           k disk    rename(rukfpfr:rukflrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frukpl2    if   e           k disk    rename(rukppfr:rukpl2r)
      *
     frf228d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rkfirm)
     d p_kund          s                   like(rkkund)
     d p_kpnr          s                   like(rutpnr)
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ruktlr_tpnr     s                   like(rutpnr)
     d ruktlr_ttil     s                   like(ruttil)
     d ruktlu_tpnr     s                   like(rutpnr)
     d ruktlu_ttil     s                   like(ruttil)
     d ruktl1_tpnr     s                   like(rutpnr)
     d rukflr_ftil     s                   like(ruftil)
     d rkunl1_kund     s                   like(rkkund)
     d rukpl2_kpnr     s                   like(rukpnr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_knav          s                   like(rknavn)
     d w_kpna          s                   like(rufnav)
     d w_ttil          s                   like(ruttil)
      *
     d b_forn          s              1    inz(*off)
     d b_tgok          s              1    inz(*off)
      *
     d w_firm          s                   like(rutfir)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
     d c_digi          c                   '0123456789 '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * F1WIN  - F1 Vindu for spørring på valgalternativer (Hardkodet)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      b2navn = w_knav
     c                   eval      b2kper = w_kpna
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkf
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      ruktl1_tpnr = p_kpnr
     c     ruktl1_key    setll     ruktl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      b_forn = *off
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ruktl1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        rutfir <> w_firm or
     c                             rutpnr <> p_kpnr
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      ruktl1_tpnr = rutpnr
      *
     c                   eval      b1valg = 0
     c                   eval      b1ttil = ruttil
      *
     c                   eval      w_ttil = b1ttil
     c                   exsr      sjekk_tilgang
     c                   if        b_tgok = *on
     c                   eval      b1ttek = ruftek
     c                   else
     c                   eval      b1ttek = 'Ukjent tilgangstype'
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   endif
     c                   write     b2cmd
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      c2navn = w_knav
     c                   eval      c2kper = w_kpna
     c                   eval      c2ttil = *blank
     c                   eval      c2ttek = *blank
      *
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   call      'RA534R'
     c                   parm                    w_firm
     c                   parm                    c2ttil
     c                   parm                    c2ttek
     c                   goto      c2tagb
     c                   endif
      *
      *
      * Validering
      *
     c                   if        c2ttil = *blank
     c                   exsr      xc2msg1
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      w_ttil = c2ttil
     c                   exsr      sjekk_tilgang
     c                   if        b_tgok = *off
     c                   exsr      xc2msg1
     c                   eval      c2ttil = *blank
     c                   eval      c2ttek = *blank
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdaterer tabell
      *
     c                   eval      ruktlu_tpnr = p_kpnr
     c                   eval      ruktlu_ttil = c2ttil
     c     ruktlu_key    chain     ruktlur
     c                   if        not %found(ruktlu)
     c                   eval      rutfir = w_firm
     c                   eval      rutpnr = p_kpnr
     c                   eval      ruttil = c2ttil
6.10 c                   eval      rutous = l_user
6.10 c                   time                    rutoti
6.10 c                   time                    rutoda
     c                   write     ruktlur
     c                   exsr      forny
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke gyldig tilgang
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_tilgang begsr
      *
     c                   eval      b_tgok = *off
     c                   eval      rukflr_ftil = w_ttil
     c     rukflr_key    chain     rukflr
     c                   if        %found(rukflr)
     c                   eval      b_tgok = *on
     c                   endif
     c                   eval      w_ttil = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad på
      * en ugyldig tilgang.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg1       begsr
      *
     c                   exfmt     c2msg1
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1navn = w_knav
     c                   eval      d1kper = w_kpna
     c                   eval      d1ttil = b1ttil
     c                   eval      d1ttek = b1ttek
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      ruktlu_tpnr = p_kpnr
     c                   eval      ruktlu_ttil = b1ttil
     c     ruktlu_key    chain     ruktlur
      *
     c                   if        %found(ruktlu)
     c                   delete    ruktlur
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpnr
      *
      * Key for oppslag på tilgangs-register
      *
     c     ruktlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ruktlr_tpnr
     c                   kfld                    ruktlr_ttil
      *
      * Key for oppdatering av tilgangs-register
      *
     c     ruktlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ruktlu_tpnr
     c                   kfld                    ruktlu_ttil
      *
      * Key for posisjonering på tilgang
      *
     c     ruktl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ruktl1_tpnr
      *
      * Key for oppslag på tilgangskode
      *
     c     rukflr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukflr_ftil
      *
      * Key for oppslag på kontaktperson
      *
     c     rukpl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl2_kpnr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
      * Henter kundenavn og kontaktperson
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      w_knav = rknavn
     c                   else
     c                   eval      w_knav = 'Ukjent kunde'
     c                   endif
      *
     c                   eval      rukpl2_kpnr = p_kpnr
     c     rukpl2_key    chain     rukpl2
     c                   if        %found(rukpl2)
     c                   eval      w_kpna = ruenav
     c                   else
     c                   eval      w_knav = 'Ukjent kontaktperson'
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ruktl1_tpnr = p_kpnr
     c     ruktl1_key    setll     ruktl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
