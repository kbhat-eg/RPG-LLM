     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA110R
      * Beskrivelse . . : Lagere, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
      * V5.40 060505 KOB  Fjernet EAN-kode
7.xx  * 7.00  020316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       110615 ASK  Lagt inn visning av regionkode på lager og
7.01  *                   overgang til region/koblingsregister
7.02  *       200917 ASK  Rekompilert på grunn av tabellendring regionskobling
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fra10l2    if   e           k disk    rename(ra10pfr:ra10l2r)
     fra10lr    if   e           k disk    rename(ra10pfr:ra10lrr)
     fra10lu    uf a e           k disk    rename(ra10pfr:ra10lur)
7.01 fra91l2    if   e           k disk    rename(ra91pfr:ra91l2r)
      *
     fra110d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra10l1_jkod     s                   like(rajkod)
     d ra10l2_jtxt     s                   like(rajtxt)
     d ra10lr_jkod     s                   like(rajkod)
     d ra10lu_jkod     s                   like(rajkod)
7.01 d ra91l2_lage     s                   like(rrlage)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rajfir)
      *
     d s_jkod          s                   like(rajkod)
      *
     d p_stat          s              1
     d p_ponr          s                   like(rajpnr)
     d p_sted          s                   like(rajste)
     d p_avde          s                   like(rajavd)
     d p_tx20          s             20
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2jkod <> 0 or
     c                             b2jtxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra10l2_jtxt = b1jtxt
     c     ra10l2_key    setll     ra10l2
     c                   other
     c                   eval      ra10l1_jkod = b1jkod
     c     ra10l1_key    setll     ra10l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Lager
      *
     c                   if        b2jkod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ra10l1_jkod = b2jkod
     c     ra10l1_key    setll     ra10l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2jtxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra10l2_jtxt = b2jtxt
     c     ra10l2_key    setll     ra10l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2jkod = 0
     c                   eval      b2jtxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1jkod = b1jkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1jkod = b1jkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1jkod = b1jkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1jkod = b1jkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1jkod = b2jkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1jkod = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1jkod = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra10lr_jkod = c1jkod
     c     ra10lr_key    chain     ra10lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1jkod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2jkod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra10lr_jkod = c1jkod
     c     ra10lr_key    chain     ra10lr
      *
     c                   if        %found
     c                   eval      c2jtxt = rajtxt
     c                   eval      c2jadr = rajadr
     c                   eval      c2jad2 = rajad2
     c                   eval      c2jpnr = rajpnr
     c                   eval      c2jste = rajste
     c                   eval      c2jtlf = rajtlf
     c                   eval      c2jfax = rajfax
     c                   eval      c2javd = rajavd
     c                   eval      c2eusr = rajusr
     c     *dmy          move      rajdat        c2edat
      *
      * Hent avdeling
      *
     c                   if        c2javd <> *zero                                       .....
     c                   move      c2javd        p_avde                                      .
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat                           kode
     c                   parm                    p_avde                           kode
     c                   parm                    p_tx20                           kode
      *
     c                   eval      c2jatx = p_tx20                                           .
     c                   endif
     c                   else
     c                   eval      c2jtxt = *blank
     c                   eval      c2jadr = *blank
     c                   eval      c2jad2 = *blank
     c                   eval      c2jpnr = *zeros
     c                   eval      c2jste = *blank
     c                   eval      c2jtlf = *blank
     c                   eval      c2jfax = *blank
     c                   eval      c2javd = *zeros
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
7.01  *
7.01  *  Henter regionskode dersom denne er lagt inn
7.01  *
7.01 c                   eval      ra91l2_lage = c1jkod
7.01 c     ra91l2_key    chain     ra91l2
7.01 c                   if        not %found  or
7.01 c                             rrregi = 0
7.01 c                   eval      c2regt = 'Ikke koblet til region'
7.01 c                   else
7.01 c                   eval      c2regi = rrregi
7.01 c                   eval      c2regt = rrrtek
7.01 c                   endif
7.01  *
      *
     c                   if        b1valg = 3
     c                   eval      c1jkod = k1jkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      *
      * Call program for post-sted
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2JPNR'                                         .
      *                                                                 .
     c                   call      'AF010R'                                                  .
     c                   parm                    p_ponr                                      .
      *                                                                 .
      * Hent poststed                                                   .
      *                                                                 .
     c                   call      'RS730R'                                                  .
     c                   parm                    p_stat                                      .
     c                   parm                    p_ponr                                      .
     c                   parm                    p_sted                                      .
      *                                                                 .
     c                   eval      c2jpnr = p_ponr                                           .
     c                   eval      c2jste = p_sted                                           .
      *                                                                 .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
      * Call program for avdeling
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2JAVD'                                         .
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_avde                                      .
     c                   parm                    p_tx20                                      .
      *                                                                 .
     c                   if        p_avde <> *zero                                           .
     c                   eval      c2javd = p_avde                                           .
     c                   eval      c2jatx = p_tx20                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   move      *on           *in36                                       .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
7.01  *
7.01  * Kall av program for regionskobling
7.01  *
7.01 c                   if        *inkh = *on
7.01  *                                                                 .
7.01 c                   call      'RA910R'                                                  .
7.01 c                   goto      c2taga
7.01 c                   endif
      *
      * Validering av input
      *
     c                   if        c2jtxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *                                                                 .
      * Sjekk poststed                                                  .
      *                                                                 .
     c                   if        c2jpnr <> *zeros
     c                   eval      p_ponr = c2jpnr                                           .
     c                   call      'RS730R'                                                  .
     c                   parm                    p_stat                                      .
     c                   parm                    p_ponr                                      .
     c                   parm                    p_sted                                      .
      *                                                                 .
     c                   eval      c2jste = p_sted
      *                                                                 .
     c                   if        p_stat <> *blank                                          .
     c                   move      *on           *in32                                       .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
     c                   else
      *
     c                   eval      c2jste = *blank
     c                   endif                                                           .....
      *
      * Sjekke avdeling
      *
     c                   if        c2javd <> *zero                                       .....
     c                   move      c2javd        p_avde                                      .
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat                           kode
     c                   parm                    p_avde                           kode
     c                   parm                    p_tx20                           kode
      *
     c                   eval      c2jatx = p_tx20                                           .
      *
     c                   if        p_stat <> *blank                                          .
     c                   move      *on           *in33                                       .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Oppdatering
      *
     c                   clear                   ra10lur
      *
     c                   eval      ra10lu_jkod = c1jkod
     c     ra10lu_key    chain     ra10lur
      *
     c                   eval      rajtxt = c2jtxt
     c                   eval      rajadr = c2jadr
     c                   eval      rajad2 = c2jad2
     c                   eval      rajpnr = c2jpnr
     c                   eval      rajste = c2jste
     c                   eval      rajtlf = c2jtlf
     c                   eval      rajfax = c2jfax
     c                   eval      rajavd = c2javd
      *
     c                   time                    rajdat
     c                   time                    rajtim
     c                   eval      rajusr = l_user
      *
     c                   if        %found
     c                   update    ra10lur
     c                   else
     c                   eval      rajfir = w_firm
     c                   eval      rajkod = c1jkod
     c                   write     ra10lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1jtxt = rajtxt
     c                   eval      b1javd = rajavd
     c                   eval      b1jadr = rajadr
     c                   eval      b1jpnr = rajpnr
     c                   eval      b1jste = rajste
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra10lr_jkod = d1jkod
     c     ra10lr_key    chain     ra10lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra10lu_jkod = d1jkod
     c     ra10lu_key    chain     ra10lur
      *
     c                   delete    ra10lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_jkod = k1jkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra10lr_jkod = k1jkod
     c     ra10lr_key    chain     ra10lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1jkod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra10lr_jkod = k1jkod
     c     ra10lr_key    chain     ra10lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1jkod = s_jkod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra10l2
     c                   other
     c                   read      ra10l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rajfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra10l2_jtxt = rajtxt
     c                   other
     c                   eval      ra10l1_jkod = rajkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1jkod = rajkod
     c                   eval      b1jtxt = rajtxt
     c                   eval      b1jadr = rajadr
     c                   eval      b1jpnr = rajpnr
     c                   eval      b1jste = rajste
     c                   eval      b1javd = rajavd
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra10l2_key    setgt     ra10l2
     c                   readp     ra10l2
     c                   other
     c     ra10l1_key    setgt     ra10l1
     c                   readp     ra10l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra10l2
     c                   other
     c                   readp     ra10l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rajfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra10l2_jtxt = rajtxt
     c                   other
     c                   eval      ra10l1_jkod = rajkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra10l2_key    setll     ra10l2
     c                   other
     c     ra10l1_key    setll     ra10l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra10l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l2_jtxt
      *
      * Key for les på lager
      *
     c     ra10lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10lr_jkod
      *
      * Key for oppdatering av lager
      *
     c     ra10lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10lu_jkod
7.01  *
7.01  * Key for oppslag på regionskobling
7.01  *
7.01 c     ra91l2_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    ra91l2_lage
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra10l1_jkod = 0
     c     ra10l1_key    setll     ra10l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
