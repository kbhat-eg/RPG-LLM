     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT017R
      * Beskrivelse . . : Leser speditører og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250614 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra22l1    if   e           k disk    rename(ra22pfr:ra22l1r)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ra22l1_vkod     s                   like(ravkod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kode                        3
     d  d_besk                       20
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle poster på ra22PF
      *
     c                   eval      ra22l1_vkod = 0
     c     ra22l1_key    setll     ra22l1
      *
     c                   read      ra22l1
     c                   dow       not %eof and
     c                             ravfir = w_firm
      *
     c                   eval      d_kode = %triml(%editw(ravkod : '  0'))
     c                   eval      d_besk = ravtxt
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      ra22l1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     ra22l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra22l1_vkod
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportSpeditorer'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT017R'
      *
     c                   endsr
