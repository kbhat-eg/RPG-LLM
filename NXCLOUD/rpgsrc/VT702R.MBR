     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : vt702R
      * Beskrivelse . . : Tilbudspris, innlesing fra fil - SDV
      *
      *
      * Programmet leser valgt fil, og oppretter tilbudspriser
      * er på Nobb prisformat.
      *
      * Spesialversjon. : Skattum
      * Tilpasset for . : Kopiert fra C28 av SVKKA
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270608 BOF  Nytt program
      *       031008 BOF  Justert måten å finne omr. faktor
5.70  *       191008 BOF  Utivdet med prisgruppe
6.20  *       060312 AMD  Nøkkel i VTILLU - filen er utvidet
6.21  *       190214 bof  Utvidet innlesing med innkjøpspris
6.31  * spes  310519 bof  Utvidet med innkjøpsperiode fra-til og prisgr.
6.32  *       111119 bof  Skal takle både 2 desimaler og 0 desimaler
6.33  *       191119 bof  feilretting vedr. semikolon siste kolonne
6.34  *       080120 bkv  Manglet prisgruppe
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    uf   e           k disk    rename(flvwpfr:flvwpfr)
      *
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvtillu    uf a e           k disk    rename(vtilpfr:vtillur)
6.31 fvti2lu    uf a e           k disk    rename(vti2pfr:vti2lur)
6.31 f                                     usropn

      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
6.31 d l_filg                931    933
      *
      * Definering av Nobb prisformat
      *
     d                 ds
6.31 d d_list                       100
     d  d_vare                       15    overlay(d_list:1)
     d  d_enhe                        3    overlay(d_list:16)
     d  d_kost                        9  2 overlay(d_list:19)
     d  d_tilb                        9  2 overlay(d_list:28)
     d  d_fdat                        6  0 overlay(d_list:37)
     d  d_tdat                        6  0 overlay(d_list:45)
     d  d_kamp                        5    overlay(d_list:53)
6.21 d  d_inpr                        9  2 overlay(d_list:58)
8.01 d  d_prgr                        2    overlay(d_list:67)
8.01 d  d_vidf                        6  0 overlay(d_list:69)
8.01 d  d_vidt                        6  0 overlay(d_list:75)
      *
     d w_kr            s              7
     d w_ore           s              2
     d w_pris          s              9
     d w_enhe          s                   like(veenhe)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_tip1          s                   like(vttip1)
     d w_tip2          s                   like(vttip1)
     d w_tip3          s                   like(vttip1)
     d w_kop1          s                   like(vttip1)
     d w_kop2          s                   like(vttip1)
     d w_kop3          s                   like(vttip1)
6.21 d w_inp1          s                   like(vttip1)
6.21 d w_inp2          s                   like(vttip1)
6.21 d w_inp3          s                   like(vttip1)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vtfirm)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
6.20 d vtillu_ogrp     s                   like(vtogrp)
6.20 d vtillu_hgrp     s                   like(vthgrp)
6.20 d vtillu_ugrp     s                   like(vtugrp)
6.20 d vtillu_ldor     s                   like(vtldor)
     d vtillu_lety     s                   like(vtlety)
     d vtillu_vare     s                   like(vtvare)
5.70 d vtillu_prgr     s                   like(vtprgr)
     d vtillu_fdat     s                   like(vtfdat)
     d vtillu_ftim     s                   like(vtftim)
     d vtillu_tdat     s                   like(vttdat)
     d vtillu_ttim     s                   like(vtttim)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
      *
      * Definering av variable
      *
     d                 ds
     d d_dato                        10
     d  d_dd                          2    overlay(d_dato:1)
     d  d_mm                          2    overlay(d_dato:4)
     d  d_aa                          4    overlay(d_dato:7)
     d                 ds
     d w_dat6                         6
     d  w_dd                          2    overlay(w_dat6:1)
     d  w_mm                          2    overlay(w_dat6:3)
     d  w_aa                          2    overlay(w_dat6:5)
      *
     d w_firm          s                   like(vtfirm)
     d w_posi          s              2  0
     d w_posp          s              2  0
     d st              s              2  0
     d w_nobx          s              8
     d w_kpri          s              9
     d w_tilb          s              9
     d w_kost          s              9
6.32 d* w_kost          s              9
6.32 d* w_inpr          s              9
6.32 d w_numb          s             15p 4
     d w_dato          s               d   datfmt(*iso)
      *
     d b_feil          s              1    inz(*off)
      *
6.31 d u_631           s              1    inz(*off)                            Endring 7.00
      *
      * Konstanter
      *
     d c_form          s              1    inz('1')
     d c_digi          s             15    inz('0123456789 ')
      *
      * Definering av konstanter
6.31  * Definering av eksterne prg
6.31  *
6.31   dcl-s co402_verdi1  char(1);
6.31   dcl-s co402_verdi2  char(2048);
6.31   DCL-PR CO402R EXTPGM('CO402R');
6.31     p_filgrp            char(3)     const;
6.31     p_firm              packed(3:0) const;
6.31     p_lager             packed(2:0) const;
6.31     p_nokkel            char(50)    const;
6.31     p_verdi1            char(1);
6.31     p_verdi2            char(2048);
6.31   END-PR;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 c                   if        u_631 = *on
6.31 c                   open      vti2lu
6.31 c                   endif
      * Dersom sletting er valgt, slettes alle eksisterende spesialpriser
      * for rabattkategori eller kunde/kundeprosjekt
      *
      *
      * Leser og oppretter/oppdaterer spesialpriser
      *
     c                   read      flvwpfr
     c                   dow       not %eof
     c                   exsr      utled_sdv
     c                   if        b_feil = *off
     c                   exsr      oppdater
     c                   endif
     c                   read      flvwpfr
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
6.31 c                   if        u_631 = *on
6.31 c                   close     vti2lu
6.31 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_sdv     begsr
      *
     c                   eval      d_list = *blank
     c                   eval      b_feil = *off
      *
     c     '"':' '       xlate     fwirad        fwirad
      *
      * Varenr.
      *
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_vare = %trim(%subst(fwirad:1:w_posi-1))
     c     c_digi        check     d_vare                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
6.31 c                   if        u_631 = *on
6.31  * prisgr.
6.31 c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
6.31 c                   eval      w_posi = %scan(';':fwirad)
6.31 c                   eval      d_prgr = %trim(%subst(fwirad:1:w_posi-1))
6.31 c                   endif
6.31  * enhet
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_enhe = %trim(%subst(fwirad:1:w_posi-1))
6.31 c     X'46':'Æ'     xlate     d_enhe        d_enhe
6.31 c     X'77':'Ø'     xlate     d_enhe        d_enhe
6.31 c     X'2A':'Å'     xlate     d_enhe        d_enhe
6.31 c     X'EF':'Å'     xlate     d_enhe        d_enhe
6.31 c     X'04':X'FC'   xlate     d_enhe        d_enhe
6.31 c     X'90':'Ø'     xlate     d_enhe        d_enhe
6.31  *
6.21  * Innkjøpspris
6.21  *
6.21 c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
6.21 c                   eval      w_posi = %scan(';':fwirad)
6.21 c                   eval      w_pris = %trim(%subst(fwirad:1:w_posi-1))
6.32 c*                   eval      w_posp = %scan(',':w_pris)
6.32 c*                   if        w_posp > 0
6.32 c*                   eval      st     = 8 - (w_posp - 1)
6.32 c*                   eval      w_kr   = *blank
6.32 c*                   eval      %subst(w_kr:st:w_posp-1) =
6.32 c*                                      %trim(%subst(w_pris:1:w_posp-1))
6.32 c*                   eval      w_ore  = %trim(%subst(w_pris:w_posp+1:2))
6.32 c*                   move      w_ore         w_inpr
6.32 c*                   movel     w_kr          w_inpr
6.32 c*                   move      w_inpr        d_inpr
6.32 c*                   else
6.32 c*                   eval      d_inpr = 0
6.32 c*                   endif
6.32 c                   if        w_pris = *blank
6.32 c                   eval      d_inpr = 0
6.32 c                   else
6.32 c                   eval      w_numb = %dec(w_pris:15:4)
6.32 c                   eval(h)   d_inpr = w_numb
6.32 c                   endif
6.32 c
      *
      * Kostpris
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      w_pris = %trim(%subst(fwirad:1:w_posi-1))
6.32 c*                   eval      w_posp = %scan(',':w_pris)
6.32 c*                   if        w_posp > 0
6.32 c*                   eval      st     = 8 - (w_posp - 1)
6.32 c*                   eval      w_kr   = *blank
6.32 c*                   eval      %subst(w_kr:st:w_posp-1) =
6.32 c*                                      %trim(%subst(w_pris:1:w_posp-1))
6.32 c*                   eval      w_ore  = %trim(%subst(w_pris:w_posp+1:2))
6.32 c*                   move      w_ore         w_kost
6.32 c*                   movel     w_kr          w_kost
6.32 c*                   move      w_kost        d_kost
6.32 c*                   else
6.32 c*                   eval      d_kost = 0
6.32 c*                   endif
6.32 c                   if        w_pris = *blank
6.32 c                   eval      d_kost = 0
6.32 c                   else
6.32 c                   eval      w_numb = %dec(w_pris:15:4)
6.32 c                   eval(h)   d_kost = w_numb
6.32 c                   endif
6.32  *
      *
      * Tilbudspris
      *
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      w_pris = %trim(%subst(fwirad:1:w_posi-1))
6.32 c*                   eval      w_posp = %scan(',':w_pris)
6.32 c*                   if        w_posp > 0
6.32 c*                   eval      st     = 8 - (w_posp - 1)
6.32 c*                   eval      w_kr   = *blank
6.32 c*                   eval      %subst(w_kr:st:w_posp-1) =
6.32 c*                                      %trim(%subst(w_pris:1:w_posp-1))
6.32 c*                   eval      w_ore  = %trim(%subst(w_pris:w_posp+1:2))
6.32 c*                   move      w_ore         w_kost
6.32 c*                   movel     w_kr          w_kost
6.32 c*                   move      w_kost        d_tilb
6.32 c*                   else
6.32 c*                   eval      d_tilb = 0
6.32 c*                   endif
6.32 c                   if        w_pris = *blank
6.32 c                   eval      d_tilb = 0
6.32 c                   else
6.32 c                   eval      w_numb = %dec(w_pris:15:4)
6.32 c                   eval(h)   d_tilb = w_numb
6.32 c                   endif
      * fra dato
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
     c                   exsr      konv_dato
     c                   move      w_dat6        d_fdat
      * til dato
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
     c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
     c                   exsr      konv_dato
     c                   move      w_dat6        d_tdat
      * kampanje
     c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':fwirad)
6.33 c                   if        w_posi > 0
     c                   eval      d_kamp = %trim(%subst(fwirad:1:w_posi-1))
6.33 c                   else
6.33 c                   eval      d_kamp = %trim(%subst(fwirad:1:5))
6.33 c                   endif
     c     X'46':'Æ'     xlate     d_kamp        d_kamp
     c     X'A0':'æ'     xlate     d_kamp        d_kamp
     c     X'2A':'Å'     xlate     d_kamp        d_kamp
     c     X'EF':'å'     xlate     d_kamp        d_kamp
     c     X'77':'Ø'     xlate     d_kamp        d_kamp
     c     X'90':'ø'     xlate     d_kamp        d_kamp
      *
6.31 c                   if        u_631 = *on
6.31  * fra innkjøpsdato
6.31 c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
6.31 c                   eval      w_posi = %scan(';':fwirad)
6.31 c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
6.31 c                   if        d_dato <> *blank
6.31 c                   exsr      konv_dato
6.31 c                   move      w_dat6        d_vidf
6.31 c                   else
6.31 c                   eval      d_vidf = 0
6.31 c                   endif
6.31  * til innkjøpsdat
6.31 c                   eval      fwirad = %subst(fwirad:w_posi+1:512-w_posi)
6.31 c                   eval      w_posi = %scan(';':fwirad)
6.31 c                   if        w_posi > 0
6.31 c                   eval      d_dato = %trim(%subst(fwirad:1:w_posi-1))
6.31 c                   else
6.31 c                   eval      d_dato = %trim(%subst(fwirad:1:10))
6.31 c                   endif
6.31 c                   if        d_dato <> *blank
6.31 c                   exsr      konv_dato
6.31 c                   move      w_dat6        d_vidt
6.31 c                   else
6.31 c                   eval      d_vidt = 0
6.31 c                   endif
6.31  *
6.31 c                   endif
      *
     c     end_utled     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konvertere dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_dato     begsr
      *
     c                   eval      w_dd = d_dd
     c                   eval      w_mm = d_mm
     c                   move      d_aa          w_aa
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Avbryter dersom nobb-nummer eller pris ikke er utfylt
      *
     c                   if        d_vare = *blank or
     c                             d_tilb = 0
     c                   leavesr
     c                   endif
      *
      * Avbryter dersom vare ikke finnes
      *
     c                   eval      vvarlr_vare = d_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * Avbryter dersom datoer ikke er gyldig
      *
     c     *dmy          test(d)                 d_fdat                 32
     c                   if        *in32 = *on
     c                   leavesr
     c                   endif
      *
     c     *dmy          test(d)                 d_tdat                 32
     c                   if        *in32 = *on
     c                   leavesr
     c                   endif
      *
      * Henter varenes vareenhet (pakningskode '000' i LVR)
      *
     c*                  eval      w_enhe = *blank
      *
     c*                  call      'FO712C'
     c*                  parm                    w_firm
     c*                  parm                    vvvare
     c*                  parm                    w_enhe
      *
     c*                  if        w_enhe = *blank
     c*                  call      'FO713C'
     c*                  parm                    w_firm
     c*                  parm                    vvnobb
     c*                  parm                    w_enhe
     c*                  endif
      *
     c                   if        d_enhe = *blank
     c                   leavesr
     c                   endif
      *
      * Henter tre salgsenheter fra varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   leave
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
      * Avbryter dersom enhet ikke finnes på varen
      *
     c                   if        w_enh1 <> d_enhe and
     c                             w_enh2 <> d_enhe and
     c                             w_enh3 <> d_enhe
     c                   leavesr
     c                   endif
      *
      * Finner salgspris for alle salgsenheter
      *
     c                   eval      w_tip1 = 0
     c                   eval      w_tip2 = 0
     c                   eval      w_tip3 = 0
      *
     c                   select
     c                   when      d_enhe = w_enh1
     c                   eval      w_tip1 = d_tilb
     c                   eval      w_tip2 = w_tip1 * w_omr2 / w_omr1
     c                   eval      w_tip3 = w_tip1 * w_omr3 / w_omr1
     c                   eval      w_kop1 = d_kost
     c                   eval      w_kop2 = w_kop1 * w_omr2 / w_omr1
     c                   eval      w_kop3 = w_kop1 * w_omr3 / w_omr1
6.21 c                   eval      w_inp1 = d_inpr
6.21 c                   eval      w_inp2 = w_inp1 * w_omr2 / w_omr1
6.21 c                   eval      w_inp3 = w_inp1 * w_omr3 / w_omr1
     c                   when      d_enhe = w_enh2
     c                   eval      w_tip2 = d_tilb
     c                   eval      w_tip1 = w_tip2 * w_omr1 / w_omr2
     c                   eval      w_tip3 = w_tip2 * w_omr3 / w_omr2
     c                   eval      w_kop2 = d_kost
     c                   eval      w_kop1 = w_kop2 * w_omr1 / w_omr2
     c                   eval      w_kop3 = w_kop2 * w_omr3 / w_omr2
6.21 c                   eval      w_inp2 = d_inpr
6.21 c                   eval      w_inp1 = w_inp2 * w_omr1 / w_omr2
6.21 c                   eval      w_inp3 = w_inp2 * w_omr3 / w_omr2
     c                   when      d_enhe = w_enh3
     c                   eval      w_tip3 = d_tilb
     c                   eval      w_tip1 = w_tip3 * w_omr1 / w_omr3
     c                   eval      w_tip2 = w_tip3 * w_omr2 / w_omr3
     c                   eval      w_kop3 = d_kost
     c                   eval      w_kop1 = w_kop3 * w_omr1 / w_omr3
     c                   eval      w_kop2 = w_kop3 * w_omr2 / w_omr3
6.21 c                   eval      w_inp3 = d_inpr
6.21 c                   eval      w_inp1 = w_inp3 * w_omr1 / w_omr3
6.21 c                   eval      w_inp2 = w_inp3 * w_omr2 / w_omr3
     c                   endsl
      *
     c                   clear                   vtillur
      *
     c                   eval      vtillu_vare = d_vare
6.34 c                   eval      vtillu_prgr = d_prgr
6.20 c                   eval      vtillu_ogrp = 0
6.20 c                   eval      vtillu_hgrp = 0
6.20 c                   eval      vtillu_ugrp = 0
6.20 c                   eval      vtillu_ldor = 0
     c                   eval      vtillu_lety = 0
     c     *dmy          move      d_fdat        vtillu_fdat
     c                   eval      vtillu_ftim = *loval
     c     *dmy          move      d_tdat        vtillu_tdat
     c                   eval      vtillu_ttim = *loval
     c     vtillu_key    chain     vtillur
      *
     c                   eval      vtfirm = w_firm
     c                   eval      vtvare = d_vare
6.34 c                   eval      vtprgr = d_prgr
     c     *dmy          move      d_fdat        vtfdat
     c                   eval      vtftim = *loval
     c     *dmy          move      d_tdat        vttdat
     c                   eval      vtttim = *loval
     c                   eval      vtkamp = d_kamp
     c                   eval      vtenh1 = w_enh1
     c                   eval      vttip1 = w_tip1
     c                   eval      vtkop1 = w_kop1
6.21 c                   eval      vtinp1 = w_inp1
     c                   eval      vtenh2 = w_enh2
     c                   eval      vttip2 = w_tip2
     c                   eval      vtkop2 = w_kop2
6.21 c                   eval      vtinp2 = w_inp2
     c                   eval      vtenh3 = w_enh3
     c                   eval      vttip3 = w_tip3
6.21 c                   eval      vtinp3 = w_inp3
     c                   eval      vtkop3 = w_kop3
      *
     c                   eval      vteusr = l_user
     c                   time                    vtedat
     c                   time                    vtetim
      *
     c                   if        %found(vtillu)
     c                   update    vtillur
     c                   else
     c                   eval      vtousr = l_user
     c                   time                    vtodat
     c                   time                    vtotim
     c                   write     vtillur
     c                   endif
6.31  *
6.31 c                   if        u_631 = *on
6.31  * oppdaterer evt. innkjøpsdato
6.31 c                   if        d_vidf <> 0  and
6.31 c                             d_vidt <> 0
6.31  *
6.31 c                   clear                   vtillur
6.31  *
6.31 c     vtillu_key    chain     vti2lu
6.31 c     *dmy          move      d_vidf        vtvidf
6.31 c     *dmy          move      d_vidt        vtvidt
6.31 c                   if        %found(vti2lu)
6.31 c                   update    vti2lur
6.31 c                   else
6.31 c                   eval      vtfirm = w_firm
6.31 c                   eval      vtvare = d_vare
6.31 c                   eval      vtprgr = d_prgr
6.31 c     *dmy          move      d_fdat        vtfdat
6.31 c                   eval      vtftim = *loval
6.31 c     *dmy          move      d_tdat        vttdat
6.31 c                   eval      vtttim = *loval
     c                   eval      vtousr = l_user
     c                   time                    vtodat
     c                   time                    vtotim
6.31 c                   write     vti2lur
6.31 c                   endif
6.31 c                   endif
6.31 c                   endif
      *
      * Sletter rad i arbeidsfil
      *
     c                   delete    flvwpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
     c     vtillu_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vtillu_ogrp
6.20 c                   kfld                    vtillu_hgrp
6.20 c                   kfld                    vtillu_ugrp
6.20 c                   kfld                    vtillu_ldor
     c                   kfld                    vtillu_vare
5.70 c                   kfld                    vtillu_prgr
     c                   kfld                    vtillu_lety
     c                   kfld                    vtillu_fdat
     c                   kfld                    vtillu_ftim
     c                   kfld                    vtillu_tdat
     c                   kfld                    vtillu_ttim
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.31  *
6.31  * Endring 6.31
6.31  *
6.31   CallP CO402R(l_filg:w_firm:0:'TILB_INNKJ_PERIOD':
6.31                    co402_verdi1:co402_verdi2);
6.31   if co402_verdi1 = '1';
6.31     u_631 = *on;
6.31     *in50 = *on;
6.31   endif;
      *
     c                   endsr
