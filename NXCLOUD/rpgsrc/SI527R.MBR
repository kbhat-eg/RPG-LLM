     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SI527R
      * Beskrivelse . . : Endring av innkjøpsstatistikk - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260305 KOB  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fsi527d    cf   e             workstn
      *
      * Datastruktur - parameter
      *
     d                 ds
     d d_parm                        96
     d  d_firm                       03  0 overlay(d_parm:01)
     d  d_paar                       04  0 overlay(d_parm:04)
     d  d_fper                       02  0 overlay(d_parm:08)
     d  d_tper                       02  0 overlay(d_parm:10)
     d  d_lkat                       01  0 overlay(d_parm:12)
     d  d_dist                       01  0 overlay(d_parm:13)
     d  d_selg                       01  0 overlay(d_parm:14)
     d  d_avde                       01  0 overlay(d_parm:15)
     d  d_lagr                       01  0 overlay(d_parm:16)
     d  d_nobb                       01  0 overlay(d_parm:17)
     d  d_nmod                       01  0 overlay(d_parm:18)
     d  d_ldor                       01  0 overlay(d_parm:19)
     d  d_lvar                       01  0 overlay(d_parm:20)
     d  d_vgrp                       01  0 overlay(d_parm:21)
     d  d_agrp                       01  0 overlay(d_parm:22)
      *
      * Definering av variable
      *
     d w_paar          s              4  0
     d w_jobq          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle valg av felt som skal oppdatere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     a1taga        tag
      *
     c                   exfmt     a1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Test om valg foretatt
      *
     c                   if        a1lkat = 0 and
     c                             a1dist = 0 and
     c                             a1selg = 0 and
     c                             a1avde = 0 and
     c                             a1lagr = 0 and
      *
     c                             a1nobb = 0 and
     c                             a1nmod = 0 and
     c                             a1ldor = 0 and
     c                             a1lvar = 0 and
     c                             a1vgrp = 0 and
     c                             a1agrp = 0
     c                   goto      a1taga
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1WIN Behandle valg av periode/år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b1taga        tag
      *
     c                   exfmt     b1win
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Test år
      *
     c                   if        b1paar < 1990
     c                   goto      b1taga
     c                   endif
      *
      * Test periode fra - til
      *
     c                   if        b1fper > b1tper
     c                   goto      b1taga
     c                   endif
      *
     c                   eval      w_paar = b1paar
      *
      * Legger inn verdier i parameter
      *
     c                   move      *zero         d_nobb
      *
     c                   move      l_firm        d_firm
     c                   move      b1paar        d_paar
     c                   move      b1fper        d_fper
     c                   move      b1tper        d_tper
      *
     c                   move      a1lkat        d_lkat
     c                   move      a1dist        d_dist
     c                   move      a1selg        d_selg
     c                   move      a1avde        d_avde
     c                   move      a1lagr        d_lagr
     c                   move      a1nobb        d_nobb
     c                   move      a1nmod        d_nmod
     c                   move      a1ldor        d_ldor
     c                   move      a1lvar        d_lvar
     c                   move      a1vgrp        d_vgrp
     c                   move      a1agrp        d_agrp
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Inntasting er ferdig !  Oppstart av utvalg/utskriftsprogrammer
      *  Kaller opp neste 'CL'-program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  F4=Legge jobb på jobbkø
      *
     c     *inkd         ifeq      *on
     c                   move      '1'           w_jobq
     c                   endif
      *
      *  Kaller opp subprogram
      *
     c                   call      'SI528C'
     c                   parm                    d_parm
     c                   parm                    w_jobq
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter verdi til skjermbilde
      *
     c                   eval      b1paar = *year
     c                   eval      b1fper = 01
     c                   eval      b1tper = 12
      *
     c                   endsr
