     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV751R
      * Beskrivelse . . : Søketekst, oppdaterer søketekst på alle varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090499 HKO  Nytt program
      *       261103 HKO  Lagt inn nyeste EAN-nummer som søketekst
6.11  *       021009 bof  Lagt inn GTINnummer fra VVEPPF som søketekst
6.31  *       211018 bkv  Lagt logistikkvare
6.31  *       131118 hko  Endret uthenting av nobbnr og lev.varenr fra
6.31  *                   logistikkvare. Endret hente-rekkefølge.
      *
8.01  *K0000  050523 bsa  Legger ut modulnummer, slik at det er mulig å søke på det.
8.01  *K0000              Legges ut helt sist, noe som betyr at det er mulig at det
8.01  *K0000              ikke "kommer med".
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl2    if   e           k disk    rename(vvarpfr:vvarl2r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
6.11 fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fvsokl1    if   e           k disk    rename(vsokpfr:vsokl1r)
     fvvsolu    uf a e           k disk    rename(vvsopfr:vvsolur)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d veanl2_vare     s                   like(vxvare)
6.11 d vvepl1_pvar     s                   like(vepvar)
6.31 d vvlel1_lvnr     s                   like(vvlvnr)
     d vsokl1_vare     s                   like(vsvare)
     d vvsolu_vare     s                   like(vvsvar)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vvarl2_key    setll     vvarl2
     c     vvarl2_key    reade     vvarl2
     c                   dow       not %eof
      *
     c                   exsr      oppdater
      *
     c     vvarl2_key    reade     vvarl2
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Sletter eksisterende søketekst på vare
      *
     c                   eval      vvsolu_vare = vvvare
     c     vvsolu_key    chain     vvsolu                             91
     c                   if        *in91 = *off
     c                   delete    vvsolur
     c                   endif
      *
      * Oppretter ny søketekst....
      *
     c                   eval      vvsfir = w_firm
     c                   eval      vvsvar = vvvare
     c                   eval      vvsogr = vvogrp
     c                   eval      vvshgr = vvhgrp
     c                   eval      vvsugr = vvugrp
     c                   eval      vvstek = *blank
      *
      * Varetekst-1
      *
     c                   eval      vvstek = vvtek1
      *
      * Varetekst-2
      *
     c                   if        vvtek2 <> *blank
     c                   eval      vvstek = %trimr(vvstek) + ' ' +
     c                                      %trim(vvtek2)
     c                   endif
      *
      * Leverandørnavn
      *
     c                   if        vvldor <> 0
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off and
     c                             rlnavn <> *blank
     c                   eval      rlnavn = %trim(rlnavn)
     c     Lo:Up         xlate     rlnavn        rlnavn
     c                   eval      vvstek = %trimr(vvstek) + ' ' +
     c                                      %trim(rlnavn)
     c                   endif
     c                   endif
      *
      * Undergruppetekst
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off and
     c                             vgutxt <> *blank
     c     Lo:Up         xlate     vgutxt        vgutxt
     c                   eval      vvstek = %trimr(vvstek) + ' ' +
     c                                      %trim(vgutxt)
     c                   endif
      *
      * Varenummer
      *
     c                   eval      vvstek = %trimr(vvstek) + ' ' + vvvare
      *
      * Lev. varenummer
      *
     c                   if        vvlvar <> *blank
     c                   eval      vvstek = %trimr(vvstek) + ' ' + vvlvar
     c                   endif
      *
      * Nobbnummer
      *
     c                   if        vvnobb <> 0
     c                   eval      vvstek = %trimr(vvstek) + ' ' +
     c                                      %trim(%editc(vvnobb:'Z'))
     c                   endif
      *
6.31  * Logistikkvare / vare-leverandør
6.31  *
6.31 c                   eval      vvlel1_lvnr = vvvare
6.31 c     vvlel1_key    setll     vvlel1
6.31 c     vvlel1_key    reade     vvlel1
6.31 c                   dow       not %eof(vvlel1)
6.31 c                   if        vvlnob <> 0 and
6.31 c                             vvlnob <> vvnobb
6.31 c                   eval      vvstek = %trimr(vvstek) + ' ' +
6.31 c                                      %char(vvlnob)
6.31 c                   endif
6.31 c                   if        vvllva <> *blank and
6.31 c                             vvllva <> vvlvar
6.31 c                   eval      vvstek = %trimr(vvstek) + ' ' +
6.31 c                                      %trim(vvllva)
6.31 c                   endif
6.31 c     vvlel1_key    reade     vvlel1
6.31 c                   enddo
      *
      * Egne søketekster
      *
     c                   eval      vsokl1_vare = vvvare
     c     vsokl1_key    setll     vsokl1
     c     vsokl1_key    reade     vsokl1                                 91
     c                   dow       *in91 = *off
     c                   eval      vvstek = %trimr(vvstek) + ' ' +
     c                                      %trim(vsstek)
     c     vsokl1_key    reade     vsokl1                                 91
     c                   enddo
      *
6.11  * EAN-nummer fra vare-enhet
6.11  *
6.11 c                   eval      vvepl1_pvar = vvvare
6.11 c     vvepl1_key    setll     vvepl1
6.11 c     vvepl1_key    reade     vvepl1
6.11 c                   dow       not %eof(vvepl1)
6.11 c                   if        vepgti <> 0
6.11 c                   eval      vvstek = %trimr(vvstek) + ' ' +
6.11 c                                      %trim(%editc(vepgti:'Z'))
6.11 c                   endif
6.11 c     vvepl1_key    reade     vvepl1
6.11 c                   enddo
      *
      * EAN-nummer
      *
     c                   eval      veanl2_vare = vvvare
     c     veanl2_key    chain     veanl2
     c                   if        %found
     c                   eval      vvstek = %trimr(vvstek) + ' ' +
     c                                      %trim(%editc(vxeann:'Z'))
     c                   endif
8.01  *
8.01  * Modulnummer
8.01  *
8.01 c                   if        vvnmod <> 0
8.01 c                   eval      vvstek = %trimr(vvstek) + ' ' +
8.01 c                                      %trim(%editc(vvnmod:'Z'))
8.01 c                   endif
      *
      * Oppretter søketekst
      *
     c                   write     vvsolur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vare
      *
     c     vvarl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på EAN
      *
     c     veanl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl2_vare
6.11  *
6.11  * Key for oppslag på vare-enhet
6.11  *
6.11 c     vvepl1_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    vvepl1_pvar
6.31  *
6.31  * Key for les av vare leverandør
6.31  *
6.31 c     vvlel1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vvlel1_lvnr
      *
      * Key for les på søketekst
      *
     c     vsokl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsokl1_vare
      *
      * Key for oppdatering av vare-søketekst
      *
     c     vvsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvsolu_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
