     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT021R
      * Beskrivelse . . : Leser alle leverandører skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100714 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fit021d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_levr                        6
     d  d_navn                             like(rlnavn)
     d  d_gate                             like(rlgate)
     d  d_adr2                             like(rladr2)
     d  d_ponr                        4
     d  d_sted                             like(rlsted)
     d  d_bgat                             like(rlbgat)
     d  d_badr                             like(rlbadr)
     d  d_bpnr                        4
     d  d_land                             like(rlland)
     d  d_alfa                             like(rlalfa)
     d  d_ftnr                        9
     d  d_emal                             like(rlemal)
     d  d_weba                             like(rlweba)
     d  d_tlfn                             like(rltlfn)
     d  d_tlfx                             like(rltlfx)
     d  d_mobn                             like(rlmobn)
     d  d_bgir                       11
     d  d_eanl                       13
     d  d_avde                        4
     d  d_lagr                        2
     d  d_saks                        4
     d  d_dist                        4
     d  d_betb                        2
     d  d_valk                             like(rlvalk)
     d  d_lbet                        2
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekker om initiell overføring eller oppdatering
      *
     c                   eval      a1vlg1 = 'O'
     c                   exfmt     a1bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      slutt
     c                   endif
      *
     c                   if        a1vlg1 = 'F'
     c                   eval      w_comd = 'Insert'
     c                   else
     c                   eval      w_comd = 'Update'
     c                   endif
      *
      * Leser alle poster på rlevl1
      *
     c                   eval      rlevl1_levr = 0
     c     rlevl1_key    setll     rlevl1
      *
     c                   read      rlevl1
     c                   dow       not %eof and
     c                             rlfirm = w_firm
      *
     c                   eval      d_levr = %triml(%editw(rllevr : '     0'))
     c                   eval      d_navn = rlnavn
     c                   eval      d_gate = rlgate
     c                   eval      d_adr2 = rladr2
     c                   eval      d_ponr = %editw(rlponr : '0   ')
      *
     c                   if        rlland = 'NO' or
     c                             rlland = '  '
     c     ' ':'0'       xlate     d_ponr        d_ponr
     c                   eval      d_sted = %subst(rlsted : 6 : 25)
     c                   else
     c                   eval      d_ponr = '0000'
     c                   eval      d_sted = rlsted
     c                   endif
      *
     c                   eval      d_bgat = rlbgat
     c                   eval      d_badr = rlbadr
     c                   eval      d_bpnr = %editw(rlbpnr : '0   ')
     c     ' ':'0'       xlate     d_bpnr        d_bpnr
     c                   eval      d_land = rlland
     c                   eval      d_ftnr = %editw(rlftnr : '        0')
     c                   eval      d_alfa = rlalfa
     c                   eval      d_emal = rlemal
     c                   eval      d_weba = rlweba
     c                   eval      d_tlfn = rltlfn
     c                   eval      d_tlfx = rltlfx
     c                   eval      d_mobn = rlmobn
     c                   eval      d_bgir = %editw(rlbgir : '          0')
     c                   eval      d_eanl = %editw(rleanl : '            0')
     c                   eval      d_avde = %triml(%editw(rlavde : '   0'))
     c                   eval      d_lagr = %triml(%editw(rllagr : ' 0'))
     c                   eval      d_saks = %triml(%editw(rlsaks : '   0'))
     c                   eval      d_dist = %triml(%editw(rldist : '   0'))
     c                   eval      d_betb = %triml(%editw(rlbetb : ' 0'))
     c                   eval      d_valk = rlvalk
     c                   eval      d_lbet = %triml(%editw(rllbet : ' 0'))
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      rlevl1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportLeverandorer'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT021R'
      *
     c                   endsr
