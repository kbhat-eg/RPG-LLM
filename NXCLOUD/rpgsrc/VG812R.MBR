      /title  ASLAGR Innlesing av gj.kospriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG812R
      * Beskrivelse . . : Innlesing av gj.kostpriser fra fil
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 800   300322 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgimi1    if   e           k disk    rename(vgimst:vgimi1r)
     fvgkpiu    uf a e           k disk    rename(vgkpst:vgkpiur)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvg812d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av nøkler
      *
     d vgimi1_vare     s                   like(vivare)
     d vvarlr_vare     s                   like(vvvare)
     d vlaglr_vare     s                   like(vlvare)
     d vlaglr_lage     s                   like(vllage)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(l_firm)
     d w_enhl          s                   like(vienhe)
     d w_gjkp          s                   like(vicost)
     d w_cost          s                   like(vicost)
     d b_vaok          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Viser bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Validering av input
      *
     c                   if        b1lagr = 0
     c                   exfmt     b1msg1
     c                   goto      b1taga
     c                   endif
      *
     c                   if        b1date = 0
     c                   exfmt     b1msg2
     c                   goto      b1taga
     c                   endif
      *
      * Leser inn kostpriser til valgt lager
      *
     c                   eval      vgimi1_vare = '  '
     c     vgimi1_key    setll     vgimi1r
      *
     c                   read      vgimi1r
     c                   dow       not %eof
     c                   exsr      sjekk_vare
     c                   if        b_vaok = *on
     c                   exsr      oppdater
     c                   endif
      *
     c                   read      vgimi1r
     c                   enddo
      *
      * Avslutt program
      *

     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk om gyldig vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vare    begsr
      *
      * Sjekker om varen er gyldig vare
      *
     c                   eval      b_vaok = *on
      *
     c                   eval      vvarlr_vare = vivare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   eval      b_vaok = *off
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekker om varen er lagervare
      *
     c                   eval      vlaglr_vare = vivare
     c                   eval      vlaglr_lage = b1lagr
     c     vlaglr_key    chain     vlaglr
     c                   if        not %found(vlaglr) or
     c                             vlvtyp <> 'L'
     c                   eval      b_vaok = *off
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekker om inn-enhet er lik lagerenhet
      *
     c                   if        vienhe = vvenhl
     c                   eval      w_enhl = vienhe
     c                   eval      w_gjkp = vicost
     c                   else
     c                   eval      w_cost = vicost
     c                   exsr      beregn_pris
     c                   eval      w_enhl = vvenhl
     c                   eval      w_gjkp = w_cost
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for beregning av kost- og innkjøps-pris i lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_pris   begsr
      *
      * Finn omregningsfaktor på innkjøpsenhet og beregn priser i forhold til basisenhet
      *
     c                   eval      vvenlr_vare = vivare
     c                   eval      vvenlr_enhe = vienhe
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_cost = w_cost / veomre
     c                   endif
      *
      * Finn omregningsfaktor på lagerenhet og beregn priser dersom lagerenhet er ulik basisenhet
      *
     c                   if        vvenhe <> vvenhl
     c                   eval      vvenlr_enhe = vvenhl
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_cost = w_cost * veomre
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av kostpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Oppdatering
      *
     c                   clear                   vgkpiur
     c                   eval      vgfirm = w_firm
     c                   eval      vghkod = ' '
     c                   eval      vgvare = vivare
     c                   eval      vglage = b1lagr
     c                   eval      vggdat = %date(b1date:*dmy)
     c                   eval      vggtim = %time(b1time:*hms)
     c                   eval      vgenhe = w_enhl
     c                   eval      vgtype = 'I'
     c                   eval      vgcost = w_gjkp
      *
     c                   eval      vgbest = 0
     c                   eval      vgsuff = 0
     c                   eval      vgline = 0
     c                   eval      vggmkp = w_gjkp
     c                   eval      vginkp = w_gjkp
     c                   eval      vgpkod = ' '
     c                   eval      vganta = 0
     c                   eval      vgbehl = 0
      *
     c                   time                    vgttim
     c                   time                    vgbdat
      *
     c                   write     vgkpiur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les på vare
      *
     c     vgimi1_key    klist
     c                   kfld                    vgimi1_vare
      *
      *  Key for oppdslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på vare via lagernummer
      *
     c     vlaglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglr_vare
     c                   kfld                    vlaglr_lage
      *
      *  Key for oppdslag på vare/enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
