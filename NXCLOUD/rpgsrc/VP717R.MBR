     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP717R
      * Beskrivelse . . : Vare-pris, henter priser på tre salgsenheter III
      *                   lik I, men man finner prisgruppe ut fra lager.
      *
      *                   Input:  Firma, vare, lager, lev.type og dato
      *                   Output: Enhet, salgspris og butikkpris for tre
      *                           salgsenheter
      *                           Omregningsfaktor volum for enhet-2 og
      *                           enhet-3 i forhold til enhet-1
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280616 bof  Nytt program (tatt utg.punkt i VP715R)
6.31  * 6.30  161116 ask  Lagt inn open/close funksjon for raskere kjøring
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
     d w_prgr          s                   like(vpprgr)
     d p_lage          s              2  0
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_enh1          s                   like(vpenhe)
     d p_sap1          s                   like(vpsapr)
     d p_bup1          s                   like(vpbupr)
     d p_enh2          s                   like(vpenhe)
     d p_sap2          s                   like(vpsapr)
     d p_bup2          s                   like(vpbupr)
     d p_omr2          s                   like(veomre)
     d p_enh3          s                   like(vpenhe)
     d p_sap3          s                   like(vpsapr)
     d p_bup3          s                   like(vpbupr)
     d p_omr3          s                   like(veomre)
6.31 d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
     d vvprl1_ldor     s                   like(vpldor)
     d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d x               s              1  0
     d b_pris          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_ldor          s                   like(vvldor)
     d w_avde          s              4  0
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31  * "Ekstrarunde" for å stenge åpne filer?
6.31  *
6.31 c                   if        p_inlr = *on
6.31 c                   goto      avslutt
6.31 c                   endif
      *
      * Henter prisgruppe
      *
     c                   eval      w_avde = 0
     c                   call      'VL712R   '
     c                   parm                    w_firm
     c                   parm                    p_lage
     c                   parm                    w_avde
     c                   parm                    w_prgr
      *
      *
      * Henter leverandør for vare og lager
      *
     c                   call      'VL721R  '
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    w_ldor
     c                   parm                    b_inlr
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
      *
      * Leser salgsenheter på vare, og henter prisinformasjon
      *
     c                   eval      x = 0
      *
     c                   eval      vvenl2_vare = p_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             x < 3
      *
      * 1. leverandør for lageret
     c                   eval      vvprl1_ldor = w_ldor
     c                   exsr      hent_pris
     c                   if        b_pris = *off  and
     c                             w_ldor <> vvldor
      * 2. hovedleverandør
     c                   eval      vvprl1_ldor = vvldor
     c                   exsr      hent_pris
     c                   if        b_pris = *off
      * siste mulighet...
     c                   eval      vvprl1_ldor = 0
     c                   exsr      hent_pris
     c                   endif
     c                   endif
      *
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
      *
      * Finner omregningsfaktor volum for enhet 2 og 3
      *
     c                   if        w_omr1 = 0
     c                   eval      w_omr1 = 1
     c                   endif
      *
     c                   eval(h)   p_omr2 = w_omr2 / w_omr1
     c                   eval(h)   p_omr3 = w_omr3 / w_omr1
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
6.31 c                   if        p_inlr = *on
     c                   eval      *inlr = *on
6.31 c                   endif
6.31  *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av prisinformasjon for salgsenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = p_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
      *
     c                   if        *in91 = *off
     c                   eval      x = x + 1
     c                   eval      b_pris = *on
     c                   endif
      *
     c                   dow       *in91 = *off
      *
     c                   select
     c                   when      x = 1
     c                   eval      p_enh1 = vpenhe
     c                   eval      p_sap1 = vpsapr
     c                   eval      p_bup1 = vpbupr
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      p_enh2 = vpenhe
     c                   eval      p_sap2 = vpsapr
     c                   eval      p_bup2 = vpbupr
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      p_enh3 = vpenhe
     c                   eval      p_sap3 = vpsapr
     c                   eval      p_bup3 = vpbupr
     c                   eval      w_omr3 = veomre
     c                   endsl
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
      * Dersom priser ikke finnes for oppgitt leveringstype, hentes priser
      * fra leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             p_lety <> 0
      *
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
      *
     c                   if        *in91 = *off
     c                   eval      x = x + 1
     c                   eval      b_pris = *on
     c                   endif
      *
     c                   dow       *in91 = *off
      *
     c                   select
     c                   when      x = 1
     c                   eval      p_enh1 = vpenhe
     c                   eval      p_sap1 = vpsapr
     c                   eval      p_bup1 = vpbupr
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      p_enh2 = vpenhe
     c                   eval      p_sap2 = vpsapr
     c                   eval      p_bup2 = vpbupr
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      p_enh3 = vpenhe
     c                   eval      p_sap3 = vpsapr
     c                   eval      p_bup3 = vpbupr
     c                   eval      w_omr3 = veomre
     c                   endsl
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
     c                   endif
      *
      * Dersom priser ikke finnes for oppgitt prisgruppe, hentes priser
      * fra prigruppe = blank
      *
     c                   if        b_pris = *off and
     c                             w_prgr <> *blank
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = *blank
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = p_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
      *
     c                   if        *in91 = *off
     c                   eval      x = x + 1
     c                   eval      b_pris = *on
     c                   endif
      *
     c                   dow       *in91 = *off
      *
     c                   select
     c                   when      x = 1
     c                   eval      p_enh1 = vpenhe
     c                   eval      p_sap1 = vpsapr
     c                   eval      p_bup1 = vpbupr
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      p_enh2 = vpenhe
     c                   eval      p_sap2 = vpsapr
     c                   eval      p_bup2 = vpbupr
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      p_enh3 = vpenhe
     c                   eval      p_sap3 = vpsapr
     c                   eval      p_bup3 = vpbupr
     c                   eval      w_omr3 = veomre
     c                   endsl
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
      * Dersom priser ikke finnes for oppgitt leveringstype, hentes priser
      * fra leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             p_lety <> 0
      *
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = *blank
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
      *
     c                   if        *in91 = *off
     c                   eval      x = x + 1
     c                   eval      b_pris = *on
     c                   endif
      *
     c                   dow       *in91 = *off
      *
     c                   select
     c                   when      x = 1
     c                   eval      p_enh1 = vpenhe
     c                   eval      p_sap1 = vpsapr
     c                   eval      p_bup1 = vpbupr
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      p_enh2 = vpenhe
     c                   eval      p_sap2 = vpsapr
     c                   eval      p_bup2 = vpbupr
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      p_enh3 = vpenhe
     c                   eval      p_sap3 = vpsapr
     c                   eval      p_bup3 = vpbupr
     c                   eval      w_omr3 = veomre
     c                   endsl
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
     c                   endif
      *
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enh1
     c                   parm                    p_sap1
     c                   parm                    p_bup1
     c                   parm                    p_enh2
     c                   parm                    p_sap2
     c                   parm                    p_bup2
     c                   parm                    p_omr2
     c                   parm                    p_enh3
     c                   parm                    p_sap3
     c                   parm                    p_bup3
     c                   parm                    p_omr3
6.31 c                   parm                    p_inlr
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
     c                   kfld                    vvprl1_ldor
     c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer dato
      *
     c                   eval      w_dato = p_dato
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_enh1 = *blank
     c                   eval      p_sap1 = 0
     c                   eval      p_bup1 = 0
     c                   eval      p_enh2 = *blank
     c                   eval      p_sap2 = 0
     c                   eval      p_bup2 = 0
     c                   eval      p_omr2 = 0
     c                   eval      p_enh3 = *blank
     c                   eval      p_sap3 = 0
     c                   eval      p_bup3 = 0
     c                   eval      p_omr3 = 0
      *
     c                   endsr
