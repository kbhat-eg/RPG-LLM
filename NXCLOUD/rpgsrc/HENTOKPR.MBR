     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Følgende kolonner kan/skal forekomme i regnearket
      *    Kontonummer
      *    Kontonavn
      *    År               Ikke utfyllt
      *    Periode          Ikke utfyllt
      *    Bevegelser
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *   02.08.2017 sst Fjernet oppdatering ved kontroll
7.00  *   09.02.2021 sst Blanke status i bunt
8.00  *   04.02.2022 sst Justeringer vedr postering på kunde/lev nr.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fokpost    uf   f  512        disk    usropn
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
     fra28l1    if   e           k disk    rename(ra28pfr:ra28l1r)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frtralu    uf a e           k disk    rename(rtrapfr:rtralur)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_feil                  1      1
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rtralu_nivo     s                   like(rtnivo)
     d rtralu_memb     s                   like(rtmemb)
     d rtralu_firm     s                   like(rtfirm)
     d rtralu_bunt     s                   like(rtbunt)
     d rtralu_linj     s                   like(rtlinj)
     d rbunlu_nivo     s                   like(rsnivo)
     d rbunlu_memb     s                   like(rsmemb)
     d rbunlu_firm     s                   like(rsfirm)
     d rbunlu_bunt     s                   like(rsbunt)
     d ra07l1_firm     s                   like(ragfir)
     d ra07l1_avde     s                   like(ragkod)
     d ra28l1_firm     s                   like(rebfir)
     d ra28l1_spes     s                   like(rebkod)
     d rhovl1_firm     s                   like(rhfirm)
     d rhovl1_kont     s                   like(rhkont)
     d rkunl1_firm     s                   like(rkfirm)
     d rkunl1_kund     s                   like(rkkund)
      *
     d c_digi          s             15    inz('0123456789 ')
     d c_dig2          s             15    inz('0123456789 ,-')
      *
      * Definering av variable
      *
     d i               s              3  0
     d j               s              3  0
     d w_amdpdat       s              6  0
     d w_amdfdat       s              6  0
     d w_amdidat       s              6  0
     d w_obel          s             11  2
     d w_rbel          s             11  2
     d w_deb           s             11  2
     d w_kre           s             11  2
      *
      * Definering av variable for timestamp
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
     d w_bunt          s                     like(rtbunt)
     d w_datx          s              6
     d w_dat           s              6  0
     d w_iso           s               d   datfmt(*iso)
     d w_isu           s               d   datfmt(*iso)
      *
     d w_stat          s             10
     d w_feil          s             15
     d w_feilMf        s              1    inz(' ')
     d w_tran          s            256
     d w_bdat          s              6  0
     d w_line          s              6  0
      *
     d w_ms1           s             50
     d w_ms2           s             50
     d w_svar          s              1
     d w_f3            s              1  0
      *
     d w_6             s              6
     d d_firm          s              3
     d d_lnav          s             30
     d d_bilk          s              2
     d d_aar           s              4
     d d_per           s              2
     d d_pdat          s              8
     d d_fdat          s              8
     d d_idat          s              8
     d d_bild          s             10
     d d_biln          s              8
     d d_kond          s              6
     d d_avdd          s              4
     d d_sped          s              4
     d d_mvad          s              1
     d d_konc          s              6
     d d_avdc          s              4
     d d_spec          s              4
     d d_mvac          s              1
     d d_jour          s              6
     d d_betb          s              2
     d d_belo          s             15
     d d_rbel          s             15
     d d_antp          s              5
     d d_ikod          s              1
     d d_valk          s              3
     d d_fakn          s             15
     d d_user          s             10
     d d_text          s             30
      *
     d s_aar           s              4
     d s_per           s              2
      *
     Iokpost    ns  01
     I                                  1  256  oktran
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   open      okpost
     c                   eval      w_stat = 'Kontroll'
     c                   eval      l_feil = *blank
      *
     c     start         tag
     c                   read      okpost
     c                   if        %eof(okpost)
     c                   if        w_stat = 'Oppdat'
     c                   exsr      getbunt
     c                   close     okpost
     c                   goto      end
     c                   else
      *
     c                   if        w_feilMf <> *blank
     c                   eval      w_ms1 = 'Regnearket inneholder +
     c                                      feil i posteringer'
     c                   eval      w_ms2 = 'Vil du allikevel danne posteringer?'
     c                   call      'AA007R'
     c                   parm                    w_ms1
     c                   parm                    w_ms2
     c                   parm                    w_svar
     c                   parm                    w_f3
     c                   if        w_svar = 'J'
     c                   eval      w_feilMf = *blank
     c                   endif
     c                   endif
      *
     c                   if        w_feilMf = *blank
     c                   close     okpost
     c                   open      okpost
     c                   exsr      getbunt
     c                   eval      w_stat = 'Oppdat'
     c                   eval      w_line = 09
     c                   goto      start
     c                   else
     c                   eval      l_feil = 'J'
     c                   goto      end
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      w_tran = oktran
      *
      * Periode kommer på egen linje først
8.00 c                   if        %subst(oktran:1:8) = 'Periode.'
8.00 c                             or %subst(oktran:1:8) = 'Periode:'
8.00 c                             or %subst(oktran:1:8) = 'PERIODE.'
8.00 c                             or %subst(oktran:1:8) = 'PERIODE:'
     c                   eval      d_aar = %subst(oktran:10:4)
     c                   eval      d_per = %subst(oktran:15:2)
     c                   if        %check(c_digi : d_aar) > 0
     c                             or %check(c_digi : d_per) > 0
     c                   eval      w_feil = 'Feil Periode'
     c                   goto      feil
     c                   endif
     c                   goto      start
     c                   endif
      *
     c                   if           ((%subst(oktran:1:1) < '0'
     c                             or  %subst(oktran:1:1) > '9')
     c                             and %subst(oktran:2:1) <> ';'
     c                             and %subst(oktran:3:1) <> ';')
     c                             or  %subst(oktran:1:3)  = ';;;'
     c                   goto      start
     c                   endif
     c                   eval      i = %scan('            ':oktran)
     c                   eval      %subst(oktran:i:1) = ';'
      *
8.00 c                   eval      d_firm = *zero
8.00 c                   eval      d_kond = *zero
8.00 c                   eval      d_avdd = *zero
8.00 c                   eval      d_sped = *zero
     c                   eval      d_mvad = *blank
8.00 c                   eval      d_konc = *zero
8.00 c                   eval      d_avdc = *zero
8.00 c                   eval      d_spec = *zero
     c                   eval      d_mvac = *blank
     c                   eval      d_bilk = *blank
     c                   eval      d_pdat = *blank
     c                   eval      d_fdat = *blank
     c                   eval      d_idat = *blank
     c                   eval      d_biln = *blank
     c                   eval      d_jour = *blank
     c                   eval      d_betb = *blank
     c                   eval      d_belo = *blank
     c                   eval      d_rbel = *blank
     c                   eval      d_antp = *blank
     c                   eval      d_ikod = *blank
     c                   eval      d_valk = *blank
     c                   eval      d_user = *blank
     c                   eval      d_text = *blank
      * Æ,Ø,Å
     c                   eval      oktran= %xlate(x'77':'Ø':oktran)
     c                   eval      oktran= %xlate(x'2A':'Å':oktran)
     c                   eval      oktran= %xlate(x'46':'Æ':oktran)
     c                   eval      oktran= %xlate(x'EF':'å':oktran)
     c                   eval      oktran= %xlate(x'90':'ø':oktran)
     c                   eval      oktran= %xlate(x'A0':'æ':oktran)
     c                   eval      oktran= %xlate(x'14':'Ø':oktran)
      *
      *  Bilagskode
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_bilk = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
     c                   dow       %subst(d_bilk:2:1) = ' ' and
     c                             d_bilk <> *blank
     c                   eval      %subst(d_bilk:2:1) = %subst(d_bilk:1:1)
     c                   eval      %subst(d_bilk:1:1) = '0'
     c                   enddo
     c                   if        %check(c_digi : d_bilk) > 0
     c                   eval      w_feil = 'Feil Bilagskode'
     c                   goto      feil
     c                   endif
      *
      *  Bilagsdato
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_bild = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
     c                   dow       %subst(d_bild:10:1) = ' ' and
     c                             d_bild <> *blank
     c                   eval      %subst(d_bild:2:9) = %subst(d_bild:1:9)
     c                   eval      %subst(d_bild:1:1) = '0'
     c                   enddo
      *
     c                   if            %subst(d_bild:3:1) = '.'
     c                             and %subst(d_bild:6:1) = '.'
     c                             and %subst(d_bild:9:2) <> ' '
     c                   eval      w_datx = %subst(d_bild:1:2)
     c                                    + %subst(d_bild:4:2)
     c                                    + %subst(d_bild:9:2)
     c                   move      w_datx        w_bdat
     c                   else
     c                   move      d_bild        w_bdat
     c                   endif
     c     *dmy          test (d)                w_bdat                 33
     c                   if        *in33 = *on
     c                   eval      w_feil = 'Feil bilagsdato'
     c                   goto      feil
     c                   endif
     c     *dmy          move      udate         w_isu
     c     *dmy          move      w_bdat        w_iso
     c                   if           %diff(w_iso:w_isu: *days) > 365
     c                             or %diff(w_iso:w_isu: *days) < -365
     c                   eval      w_feil = 'Feil bilagsdato'
     c                   goto      feil
     c                   endif
      *
      *  Bilagnummer
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_biln = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
     c                   dow       %subst(d_biln:8:1) = ' ' and
     c                             d_biln <> *blank
     c                   eval      %subst(d_biln:2:7) = %subst(d_biln:1:7)
     c                   eval      %subst(d_biln:1:1) = '0'
     c                   enddo
     c                   if        %check(c_digi : d_biln) > 0
     c                   eval      w_feil = 'Feil Bilagsdato'
     c                   goto      feil
     c                   endif
      *
      *  Avdeling  Debet
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_avdd = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
     c                   if        %check(c_digi : d_avdd) > 0
     c                   eval      w_feil = 'Feil Deb.Avdel.'
     c                   goto      feil
     c                   endif
     c                   if        d_avdd <> *blank
     c                             and d_avdd <> *zero
     c                   eval      ra07l1_firm = l_firm
     c                   eval      ra07l1_avde = %dec(d_avdd:4:0)
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found(ra07l1)
     c                   eval      w_feil = 'Ukjent Avdeling'
     c                   goto      feil
     c                   endif
8.00 c                   else
8.00 c                   eval      d_avdd = *zero
     c                   endif
      *
      *  kontonummer  Debet
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_kond = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
     c                   if        %check(c_digi : d_kond) > 0
     c                   eval      w_feil = 'Feil Deb.Konto.'
     c                   goto      feil
     c                   endif
     c                   if        d_kond <> *blank
     c                             and d_kond <> *zero
     c                   eval      rhovl1_firm = l_firm
8.00 c                   if        %dec(d_kond:6:0) < 100000
     c                   eval      rhovl1_kont = %dec(d_kond:6:0)
8.00 c                   else
8.00 c                   eval      rhovl1_kont = 1
8.00 c                   endif
     c     rhovl1_key    chain     rhovl1
     c                   if        not %found(rhovl1)
     c                   eval      rkunl1_firm = l_firm
     c                   eval      rkunl1_kund = %dec(d_kond:6:0)
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found(rkunl1)
     c     rkunl1_key    chain     rlevl1
     c                   if        not %found(rlevl1)
     c                   eval      w_feil = 'Ukjent Konto D.'
     c                   if        w_stat = 'Kontroll'
     c                   goto      feil
     c                   endif
     c                   endif
     c                   endif
     c                   endif
8.00 c                   else
8.00 c                   eval      d_kond = *zero
     c                   endif
      *
      *  Bærer / spes    Debet
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_sped = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
     c                   if        %check(c_digi : d_sped) > 0
     c                   eval      w_feil = 'Feil Deb.Spes. '
     c                   goto      feil
     c                   endif
     c                   if        d_sped <> *blank
     c                             and d_sped <> *zero
     c                   eval      ra28l1_firm = l_firm
8.00 c                   eval      ra28l1_spes = %dec(d_sped:4:0)
     c     ra28l1_key    chain     ra28l1
     c                   if        not %found(ra28l1)
     c                   eval      w_feil = 'Ukjent Spes. D.'
     c                   goto      feil
     c                   endif
8.00 c                   else
8.00 c                   eval      d_sped = *zero
     c                   endif
      *
      *  Momskode        Debet
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_mvad = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
      *  Beløp
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_belo = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
     c                   if        %check(c_dig2:d_belo) > 0
     c                   eval      w_feil = 'Feil Beløp  '
     c                   goto      feil
     c                   endif
      *
     c                   eval      w_obel = %dec(d_belo:14:2)
      *
      *  Avdeling        Credit
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_avdc = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
     c                   if        %check(c_digi : d_avdc) > 0
     c                   eval      w_feil = 'Feil Cre.Avdel.'
     c                   goto      feil
     c                   endif
     c                   if        d_avdc <> *blank
     c                             and d_avdc <> *zero
     c                   eval      ra07l1_firm = l_firm
     c                   eval      ra07l1_avde = %dec(d_avdc:4:0)
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found(ra07l1)
     c                   eval      w_feil = 'Ukjent Avdeling'
     c                   goto      feil
     c                   endif
8.00 c                   else
8.00 c                   eval      d_avdc = *zero
     c                   endif
      *
      *  kontonummer     Credit
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_konc = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
     c                   if        %check(c_digi : d_konc) > 0
     c                   eval      w_feil = 'Feil Cre.Konto.'
     c                   goto      feil
     c                   endif
     c                   if        d_konc <> *blank
     c                             and d_konc <> *zero
     c                   eval      rhovl1_firm = l_firm
8.00 c                   if        %dec(d_konc:6:0) < 100000
8.00 c                   eval      rhovl1_kont = %dec(d_konc:6:0)
8.00 c                   else
8.00 c                   eval      rhovl1_kont = 1
8.00 c                   endif
     c     rhovl1_key    chain     rhovl1
     c                   if        not %found(rhovl1)
     c                   eval      rkunl1_firm = l_firm
     c                   eval      rkunl1_kund = %dec(d_konc:6:0)
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found(rkunl1)
     c     rkunl1_key    chain     rlevl1
     c                   if        not %found(rlevl1)
     c                   eval      w_feil = 'Ukjent Konto C.'
     c                   if        w_stat = 'Kontroll'
     c                   goto      feil
     c                   endif
     c                   endif
     c                   endif
     c                   endif
8.00 c                   else
8.00 c                   eval      d_konc = *zero
     c                   endif
      *
      *  Bærer / spes    Credit
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_spec = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
     c                   if        %check(c_digi : d_spec) > 0
     c                   eval      w_feil = 'Feil Cre.Spes. '
     c                   goto      feil
     c                   endif
     c                   if        d_spec <> *blank
     c                             and d_spec <> *zero
     c                   eval      ra28l1_firm = l_firm
8.00 c                   eval      ra28l1_spes = %dec(d_spec:4:0)
     c     ra28l1_key    chain     ra28l1
     c                   if        not %found(ra28l1)
     c                   eval      w_feil = 'Ukjent Spes. C.'
     c                   if        w_stat = 'Kontroll'
     c                   goto      feil
     c                   endif
     c                   endif
8.00 c                   else
8.00 c                   eval      d_spec = *zero
     c                   endif
      *
      *  Momskode        Credit
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_mvac = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
      *  Tekst
     c                   eval      i = %scan(';':oktran)
     c                   eval      d_text = %trim(%subst(oktran:1:i-1))
     c                   eval      oktran = %subst(oktran:i+1:256-i)
      *
      *
     c     Ikke_Fler     tag
      *
6.31 c                   if        w_stat = 'Oppdat'
     c                   clear                   rtralur
     c                   eval      rtstat = *zero
     c                   eval      rtmemb = L_user
     c                   eval      rtfirm = *zero
     c                   if            d_aar  = s_aar
     c                             and d_per  = s_per
     c                   else
     c                   exsr      getbunt
     c                   eval      w_tell = 09
     c                   endif
     c                   eval      rtbunt = w_bunt
     c                   eval      w_tell = w_tell + 1
     c                   eval      rtlinj = w_tell
     c                   eval      rtstat = 'T'
     c                   move      d_aar         rtraar
     c                   move      d_per         rtrper
     c                   move      d_bilk        rtbilk
     c                   move      d_biln        rtbiln
8.00 c                   eval      rtdavd = %dec(d_avdd:4:0)
8.00 c                   eval      rtdkto = %dec(d_kond:6:0)
8.00 c                   eval      rtdsps = %dec(d_sped:4:0)
     c                   move      d_mvad        rtdmva
8.00 c                   eval      rtcavd = %dec(d_avdc:4:0)
8.00 c                   eval      rtckto = %dec(d_konc:6:0)
8.00 c                   eval      rtcsps = %dec(d_spec:4:0)
     c                   move      d_mvac        rtcmva
     c                   eval      rtbelØ = w_obel
     c                   if        rtdkto > 0
     c                   eval      w_deb = w_deb + w_obel
     c                   endif
     c                   if        rtckto > 0
     c                   eval      w_kre = w_kre + w_obel
     c                   endif
      *
     c                   if            %subst(d_bild:3:1) = '.'
     c                             and %subst(d_bild:6:1) = '.'
     c                             and %subst(d_bild:9:2) <> ' '
     c                   eval      w_datx = %subst(d_bild:1:2)
     c                                    + %subst(d_bild:4:2)
     c                                    + %subst(d_bild:9:2)
     c                   move      w_datx        w_dat
     c                   endif
     c     *dmy          move      w_dat         rtbdat
     c                   eval      rtfdat = *loval
     c                   eval      rttext = d_text
     c                   time                    rtedat
     c                   time                    rtetim
     c                   movel     l_user        rteusr
      *
      *  DEB_4
     c                   write     rtralur
6.31 c                   endif
      *
     c                   goto      start
      *
     c     feil          tag
     c                   eval      oktran = %trim(w_tran) + ';' +
     c                                      %trim(w_feil) + ';'
     c                   eval      w_feilmf = 'J'
     c                   except    okuppd
     c                   goto      start
      *
      *
     c     end           tag
     c                   eval      *inlr = *on
     c                   return
      *
      *  Ev. oppdaterer forrige bunt og henter nytt nummer
      *
     c     getbunt       begsr
      *  Ev. oppdaterer forrige bunt
     c                   if        s_aar <> *blank
     c                   eval      rbunlu_nivo = *blank
     c                   eval      rbunlu_memb = l_user
     c                   eval      rbunlu_firm = l_firm
     c                   eval      rbunlu_bunt = w_bunt
      * DEB_2
     c     rbunlu_key    chain     rbunlu
     c                   if        %found(rbunlu)
     c                   eval      rspdeb = w_deb
     c                   eval      rspkre = w_kre
      *  DEB_3
     c                   update    rbunlur
     c                   eval      w_deb  = *zero
     c                   eval      w_kre  = *zero
     c                   endif
     c                   endif
      *   Hent nytt buntnummer
     c                   if        not %eof(okpost)
     c                   eval      rbunlu_nivo = *blank
     c                   eval      rbunlu_memb = l_user
     c                   eval      rbunlu_firm = l_firm
     c                   eval      rbunlu_bunt = 9999
     c     rbunlu_key    setgt     rbunlu
      * DEB_1
     c                   readp     rbunlu
     c                   if        not %eof(rbunlu)
     c                   eval      w_bunt = rsbunt + 1
     c                   else
     c                   eval      w_bunt = 1
     c                   endif
      *
     c                   eval      rsnivo = *blank
     c                   eval      rsmemb = l_user
     c                   eval      rsfirm = l_firm
     c                   eval      rsbunt = w_bunt
7.00 c                   eval      rsstat = *blank
     c                   move      d_aar         rsraar
     c                   move      d_per         rsrper
     c                   move      d_aar         s_aar
     c                   move      d_per         s_per
     c                   time                    rspdat
     c                   time                    rsedat
     c                   time                    rsetim
     c                   eval      rseusr = l_user
      *  DEB_5
     c                   write     rbunlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på Avdelin
      *
     c     ra07l1_key    klist
     c                   kfld                    ra07l1_firm
     c                   kfld                    ra07l1_avde
      *
      * Key for posisjonering på Spes
      *
     c     ra28l1_key    klist
     c                   kfld                    ra28l1_firm
     c                   kfld                    ra28l1_spes
      *
      * Key for posisjonering på Konto
      *
     c     rhovl1_key    klist
     c                   kfld                    rhovl1_firm
     c                   kfld                    rhovl1_kont
      *
      * Key for posisjonering på Kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    rkunl1_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på bunts
      *
     c     rbunlu_key    klist
     c                   kfld                    rbunlu_nivo
     c                   kfld                    rbunlu_memb
     c                   kfld                    rbunlu_firm
     c                   kfld                    rbunlu_bunt
      *
      * Key for posisjonering på trans
      *
     c     rtralu_key    klist
     c                   kfld                    rtralu_nivo
     c                   kfld                    rtralu_memb
     c                   kfld                    rtralu_firm
     c                   kfld                    rtralu_bunt
     c                   kfld                    rtralu_linj
      *
      *
     c                   endsr
      *
     oOkPost    e            OkUppd
     o                       oktran
