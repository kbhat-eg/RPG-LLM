      /TITLE  ASOFAK Ordreinngang, hente ordre fra historisk bongregister
     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP804R
      * Beskrivelse . . : Ordreinngang, hente ordre fra historisk bongreg.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * R3.11 220900 AMD  Selgernr. utvidet fra 2 til 4 posisjoner
      * R3.31 020101 AMD  Utvidet med en ordretype til + lagt inn mulighet
      *                   for seleksjon også på historisk fakt.reg.
      *                   Endringer ikke anmerket på linjenivå
5.51  * R5.51 020107 AMD  Utvidet med mulighet til også å kunne
      *                   selektere på infokode
6.20  * 6.20  231110 NHO  Proj er nå alfa
6.nu  * 6.30  240614 bof  Gjennomgått mtp. nummerutvidelse
      *
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbohel9    if   e           k disk    rename(bohepfr:bohel9r)
     f                                     prefix(fo:2)
     ffdell3    if   e           k disk
     fwohepf    o  a e           k disk
      *
      *
      * Array
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av datastrukturer
      *
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
5.51 d d_list                       128
     d  d_firm                        3  0 overlay(d_list)
     d  d_hist                        1    overlay(d_list:4)
     d  d_oiar                        1    overlay(d_list:5)
     d  d_bong                        1    overlay(d_list:6)
3.11 d  d_selg                        4  0 overlay(d_list:7)
3.11 d  d_avde                        4  0 overlay(d_list:11)
3.11 d  d_hot1                        2    overlay(d_list:15)
3.11 d  d_hot2                        2    overlay(d_list:17)
3.11 d  d_hot3                        2    overlay(d_list:19)
3.11 d  d_hot4                        2    overlay(d_list:21)
3.11 d  d_hot5                        2    overlay(d_list:23)
3.11 d  d_hot6                        2    overlay(d_list:25)
3.11 d  d_hvlg                        1    overlay(d_list:27)
3.11 d  d_oot1                        2    overlay(d_list:28)
3.11 d  d_oot2                        2    overlay(d_list:30)
3.11 d  d_oot3                        2    overlay(d_list:32)
3.11 d  d_oot4                        2    overlay(d_list:34)
3.11 d  d_oot5                        2    overlay(d_list:36)
3.11 d  d_oot6                        2    overlay(d_list:38)
3.11 d  d_ovlg                        1    overlay(d_list:40)
3.11 d  d_bot1                        2    overlay(d_list:41)
3.11 d  d_bot2                        2    overlay(d_list:43)
3.11 d  d_bot3                        2    overlay(d_list:45)
3.11 d  d_bot4                        2    overlay(d_list:47)
3.11 d  d_bot5                        2    overlay(d_list:49)
3.11 d  d_bot6                        2    overlay(d_list:51)
3.11 d  d_bvlg                        1    overlay(d_list:53)
5.51 d  d_inf1                        2    overlay(d_list:54)
5.51 d  d_inf2                        2    overlay(d_list:56)
5.51 d  d_inf3                        2    overlay(d_list:58)
5.51 d  d_inf4                        2    overlay(d_list:60)
5.51 d  d_inf5                        2    overlay(d_list:62)
5.51 d  d_inf6                        2    overlay(d_list:64)
5.51 d  d_ivlg                        1    overlay(d_list:66)
5.51 d  d_fdat                         d   overlay(d_list:67) datfmt(*iso)
5.51 d  d_tdat                         d   overlay(d_list:77) datfmt(*iso)
5.51 d  d_dvlg                        1    overlay(d_list:87)
5.51 d  d_dumy                       15    overlay(d_list:88)
5.51 d  d_sort                        1    overlay(d_list:103)
5.51 d  d_ityp                        2    overlay(d_list:104)
      *
      * Definering av variabler i nøkler
      *
     dbohel9_firm      s                   like(fofirm)
     dbohel9_bdat      s                   like(fobdat)
     dbohel9_otyp      s                   like(footyp)
     dbohel9_selg      s                   like(foselg)
     dra09pf_selg      s                   like(foselg)
     drkunpf_kund      s                   like(fokund)
     dfdell3_firm      s                   like(fyfirm)
     dfdell3_numm      s                   like(fynumm)
     dfdell3_suff      s                   like(fysuff)
      *
      * Definering av variable
      *
      *
     d gml_selg        s                   like(foselg)
     d w_firm          s                   like(fofirm)
     d w_timestamp     s               d   datfmt(*iso)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler historikk-data (BOHEPF)
      *
     c                   if        d_bong = '1'
     c                   exsr      detalje_bohe
     c                   endif
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av i historiske bonger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalje_bohe  begsr
      *
     c                   read      bohel9                                 60
      *
     c                   if        *in60 = *off
     c                   eval      gml_selg = foselg
     c                   endif
      *
     c                   dow       *in60 = *off
      *
     c                   eval      fobolo = 'B'
      *
      * Velg ut riktig firma
      *
     c                   if        fofirm <> d_firm
     c                   goto      end_les
     c                   endif
      *
      *
      * Sjekk om Bong er kopiert fra Ordre
      * Hent original Ordredato, selger
      *
     c                   if        foornr <> 0
     c                   exsr      hent_ord
     c                   endif
      *
      * Skal leveringsdato benyttes ?
      *
     c                   if        d_dvlg = '1'
     c                   eval      fobdat = foldat
     c                   endif
      *
      * Velg ut riktig dato (enkelt eller fra/til)
      *
     c                   if        d_tdat =  *loval
      *
     c                   if        fobdat <> d_fdat
     c                   goto      ny_les
     c                   endif
     c                   else
     c                   if        fobdat > d_tdat or fobdat < d_fdat
     c                   goto      ny_les
     c                   endif
      *
     c                   endif
      *
      * Velg ut riktig selger (enkelt eller alle)
      *
     c                   if        d_selg <> 0
     c                   if        foselg <> d_selg
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
      * Velg ut riktig avdeling (enkelt eller alle)
      *
     c                   if        d_avde <> 0
     c                   if        foavde <> d_avde
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
      * Velg ut riktig ordretype
      *
     c                   if        d_bvlg <> *blanks
     c                   if        d_bvlg = '1'
     c                   if           footyp = d_bot1
     c                             or footyp = d_bot2
     c                             or footyp = d_bot3
     c                             or footyp = d_bot4
     c                             or footyp = d_bot5
     c                             or footyp = d_bot6
     c                   else
     c                   goto      ny_les
     c                   endif
     c                   endif
     c                   if        d_bvlg = '2'
     c                   if           footyp = d_bot1
     c                             or footyp = d_bot2
     c                             or footyp = d_bot3
     c                             or footyp = d_bot4
     c                             or footyp = d_bot5
     c                             or footyp = d_bot6
     c                   goto      ny_les
     c                   endif
     c                   endif
     c                   endif
      *
      * Skriv ut posten
      *
     c                   write     wohepfr
      *
     c     ny_les        tag
      *
     c                   read      bohel9                                 60
      *
      *
     c                   enddo
      *
     c     end_les       tag
      *
     c                   endsr
      *
      * HENT ORDREDATO OG SELGER FRA FOHEPF
      *
     c     hent_ord      begsr
      *
     c                   eval      fdell3_numm = foornr
     c                   eval      fdell3_suff = fosuff
      *
     c     fdell3_key    chain     fdell3                             61
      *
     c                   if        *in61 = *off
     c                   eval      fobdat = fybdat
     c                   eval      foldat = fybdat
     c                   eval      foselg = fyselg
     c                   move      '*'           fobolo
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
5.51 c                   parm                    w_list          128
      *
      * Nøkkel for BOHEL9
      *
     c     bohel9_key    klist
     c                   kfld                    bohel9_firm
     c                   kfld                    bohel9_selg
     c                   kfld                    bohel9_bdat
      *
      * Nøkkel for FDELL3
      *
     c     fdell3_key    klist
     c                   kfld                    fdell3_firm
     c                   kfld                    fdell3_numm
     c                   kfld                    fdell3_suff
      *
     c                   eval      d_list = w_list
      *
     c                   eval      bohel9_bdat = *loval
     c                   eval      bohel9_firm = d_firm
     c                   eval      bohel9_selg = d_selg
     c                   eval      fdell3_firm = d_firm
      *
     c     bohel9_key    setll     bohel9
      *
     c                   endsr
