     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LH500R
      * Beskrivelse . . : Spørring på historisk lagerbok - valgbilde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.xx  * 7.00  200516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     flh500d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_lage          s              2  0
     d w_dato          s                   like(vvedat)
     d w_vtxt          s             35
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_time          s              6  0
     d w_valg          s              1  0
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * A1BLD  - A1 Vindu for valg av varenummer,lager og dato
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle valgbilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1taga        tag
     c                   move      l_lage        a1lage
     c                   eval      a1dato = 0
     c                   eval      a1vare = *blank
     c                   eval      a1tek1 = *blank
     c                   eval      a1ltek = *blank
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      a1tagb
     c                   endsl
      *
      * Sjekk input
      *
     c                   eval      w_vare = *blank
     c                   movel     a1vare        w_vare
     c                   exsr      sj_input
     c                   if        b_feil = *on
     c                   goto      a1tagb
     c                   endif
      *
      * Finner bla-alternativer
      *
     c                   select
     c                   when      a1lage <> *blank and w_dato <> *loval
     c                   eval      w_valg = 1
     c                   when      a1lage <> *blank and w_dato = *loval
     c                   eval      w_valg = 2
     c                   when      a1lage = *blank and w_dato <> *loval
     c                   eval      w_valg = 3
     c                   when      a1lage = *blank and w_dato = *loval
     c                   eval      w_valg = 4
     c                   endsl
      *
     c                   call      'LH502R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    a1lage
     c                   parm                    w_dato
     c                   parm                    w_valg
      *
     c                   goto      a1taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sj_input      begsr
     c                   eval      b_feil = *off
      *
      *  Sjekk varenr
      *
     c                   eval      vvarl1_vare = a1vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_sj
     c                   endif
      *
      *  Sjekk lager
      *
     c                   if        a1lage <> *blank
     c                   move      a1lage        w_lage
     c                   exsr      les_lager
     c                   if        b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      end_sj
     c                   endif
     c                   else
     c                   eval      w_lage = 00
     c                   endif
      *
      *  Sjekk gyldig dato
      *
     c                   if        a1dato <> 0
     c     *dmy          test(d)                 a1dato                 33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sj
     c                   endif
     c     *dmy          move      a1dato        w_dato
     c                   else
     c                   eval      w_dato = *loval
     c                   endif
      *
     c     end_sj        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på varer
      *
     c                   if        w_actr = 'A1BLD '
     c                   if        w_actf = 'A1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        a1vare
     c                   movel     w_vtxt        a1tek1
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring på lager
      *
     c                   if        w_actf = 'A1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    a1ltek
     c                   move      w_lage        a1lage
     c                   goto      end_sp
     c                   endif
     c                   endif
      *
     c     end_sp        endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_lager     begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
