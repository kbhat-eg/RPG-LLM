     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LA965R
      *  Beskrivelse . . : Kalkulerer lagerbevegelser utfra lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *  6.30  240914 BSA   Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhislr    if   e           k disk    rename(vhispfr:vhislrr)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                       128
     d  d_lage                        2  0 overlay(d_prec)
     d  d_vare                       15    overlay(d_prec:3)
     d  d_fdat                       10d   datfmt(*iso)
     d                                     overlay(d_prec:18)
     d  d_tdat                       10d   datfmt(*iso)
     d                                     overlay(d_prec:28)
     d  d_beve                       11  3 overlay(d_prec:39)
     d  d_salg                       11  3 overlay(d_prec:50)
     d  d_utta                       11  3 overlay(d_prec:61)
     d  d_just                       11  3 overlay(d_prec:72)
     d  d_inng                       11  3 overlay(d_prec:83)
     d  d_behl                       11  3 overlay(d_prec:94)
      *
      * Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definere variable i nøkler
      *
     d vhislr_lage     s                   like(vhlage)
     d vhislr_vare     s                   like(vhvare)
     d vhislr_dato     s                   like(vhdato)
     d vhislr_time     s                   like(vhtime)
     d vhislr_sekv     s                   like(vhsekv)
      *
      * Definere øvrige variable
      *
     d w_firm          s                   like(vhfirm)
     d w_parm          s            128
     d w_anta          s                   like(vhanta)
     d w_beve          s             11  3
     d w_salg          s             11  3
     d w_utta          s             11  3
     d w_just          s             11  3
     d w_inng          s             11  3
     d w_behl          s             11  3
     d w_dato          s              6  0
     d w_time          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_beve = 0
     c                   eval      w_anta = 0
     c                   eval      w_utta = 0
     c                   eval      w_just = 0
     c                   eval      w_inng = 0
     c                   eval      w_behl = 0
      *
     c     vhislr_key    setll     vhislr
     c     vhislr_key2   reade     vhislr
     c                   dow       not %eof and
     c                             vhdato < d_tdat
      *
      *  Sjekk om vi har nådd tildato - da avslutter vi
      *
     c*                  if        vhdato > d_tdat
     c*                  iter
     c*                  endif
      *
      *  Sjekk om telletrans - skal ikke oppdatere
      *
     c                   if        vhsyst = 'L' and
     c                             vhkode = 'T '
     c                   goto      les_lagbok
     c                   endif
      *
     c                   eval      w_beve = w_beve + 1
      *
      *  Oppdaterer SALG    iår
      *
     c                   if        vhsyst = 'F'
     c                   if        vhtype = 'U'
     c                   eval      w_salg = w_salg + vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   eval      w_behl = w_behl - vhanta
     c                   else
     c                   eval      w_salg = w_salg - vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   eval      w_behl = w_behl + vhanta
     c                   endif
     c                   goto      les_lagbok
     c                   endif
      *
      *  Oppdaterer UTTAK iår
      *
     c                   if        vhsyst = 'L' and
     c                             vhkode = 'U '
     c                   if        vhtype = 'U'
     c                   eval      w_utta = w_utta + vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   eval      w_behl = w_behl - vhanta
     c                   else
     c                   eval      w_utta = w_utta - vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   eval      w_behl = w_behl + vhanta
     c                   endif
     c                   goto      les_lagbok
     c                   endif
      *
      *  Oppdaterer JUSTERING iår
      *
     c                   if        1=2
     c                   if        vhsyst = 'L' and
     c                             vhkode = 'J '
     c                   if        vhtype = 'U'
     c                   eval      w_just = w_just - vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   else
     c                   eval      w_just = w_just + vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   endif
     c                   goto      les_lagbok
     c                   endif
     c                   endif
      *
      *  Oppdaterer INNGANG iår
      *
     c                   if        vhsyst = 'L'
     c                   if        vhtype = 'U'
     c                   eval      w_inng = w_inng - vhanta
     c                   eval      w_anta = w_anta + vhanta
     c                   eval      w_behl = w_behl - vhanta
     c                   else
     c                   eval      w_inng = w_inng + vhanta
     c                   eval      w_anta = w_anta - vhanta
     c                   eval      w_behl = w_behl + vhanta
     c                   endif
     c                   endif
      *
     c     les_lagbok    tag
      *
     c     vhislr_key2   reade     vhislr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      d_beve = w_beve
     c                   eval      d_salg = w_salg
     c                   eval      d_utta = w_utta
     c                   eval      d_just = w_just
     c                   eval      d_inng = w_inng
     c                   eval      d_behl = w_behl
     c                   eval      w_parm = d_prec
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av HISTORISK-LAGERBOK-REGISTER
      *
     c     vhislr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhislr_lage
     c                   kfld                    vhislr_vare
     c                   kfld                    vhislr_dato
     c                   kfld                    vhislr_time
     c                   kfld                    vhislr_sekv
      *
      *  Key for les av HISTORISK-LAGERBOK-REGISTER
      *
     c     vhislr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vhislr_lage
     c                   kfld                    vhislr_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      vhislr_lage = d_lage
     c                   eval      vhislr_vare = d_vare
     c                   eval      vhislr_dato = d_fdat
     c                   eval      vhislr_time = *loval
     c                   eval      vhislr_sekv = *zero
      *
     c                   endsr
