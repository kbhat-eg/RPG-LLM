     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK981R                                              *
      * Beskrivelse . . : Kunder - reorganiserer spesialavtaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260506 HFL  Nytt program
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter for utvalgte OKO filer.
6.11  * V6.10 070510 BSA  Endret sporing
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frksalu    uf   e           k disk    rename(rksapfr:rksalur)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
      *
     d                uds
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d rkunlu_firm     s                   like(rkfirm)
     d rkunlu_kund     s                   like(rkkund)
      *
      * Les spesialavtaler
      *
     c     *loval        setll     rksalu                                 61
     c                   read      rksalu                                 61
      *
     c                   dow       *in61 = *off
      *
      * Sletter post hvis alle spesialavtaler er blanke
      *
     c                   if        rksat1 = *blank and
     c                             rksat2 = *blank and
     c                             rksat3 = *blank
     c                   delete    rksalur
     c                   else
      *
      * Gyldig kunde? Hvis ja, oppdateres koden
      *               Hvis nei, slettes posten
      *
     c                   eval      rkunlu_firm = rksafi
     c                   eval      rkunlu_kund = rksaku
     c     rkunlu_key    chain     rkunlu                             62
      *
     c                   if        %found
     c                   eval      rkspav = 1
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK981R    '
     c                   update    rkunlur
     c                   else
     c                   delete    rksalur
     c                   endif
      *
     c                   endif
      *
     c                   read      rksalu                                 61
      *
     c                   enddo
      *
     c                   seton                                        lr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for nullstilling av kode for spesialavtale            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     nulkun        begsr
      *
     c     *loval        setll     rkunlu                                 60
     c                   read      rkunlu                                 60
      *
     c                   dow       *in60 = *off
      *
      * Nullstill felt
      *
     c                   eval      rkspav = *zeros
      *
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK981R    '
     c                   update    rkunlur
      *
     c                   read      rkunlu                                 60
     c                   enddo
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Nullstill merke for spesialavtale i kunderegister
      *
     c                   exsr      nulkun
      *
      * Key for kunder
      *
     c     rkunlu_key    klist
     c                   kfld                    rkunlu_firm
     c                   kfld                    rkunlu_kund
      *
     c                   endsr
