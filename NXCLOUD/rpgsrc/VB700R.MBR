     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VB700R
      * Beskrivelse . . : Bruksrett-knytning, henter rabattinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221203 HKO  Nytt program
6.10  * V6.10 050511 AMD  Mvakode ble nullstillt dersom den ikke ble
      *                   satt på nytt fra bruksrettstypen.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvbrkl1    if   e           k disk    rename(vbrkpfr:vbrkl1r)
     fvbrkl5    if   e           k disk    rename(vbrkpfr:vbrkl5r)
     frmbtl1    if   e           k disk    rename(rmbtpfr:rmbtl1r)
      *
      * Definering parametre
      *
     d p_firm          s                   like(vbfirm)
     d p_alme          s                   like(vbalme)
     d p_vare          s                   like(vbvare)
     d p_brty          s                   like(vbbrty)
     d p_brr1          s                   like(rmtra1)
     d p_brr2          s                   like(rmtra2)
     d p_omva          s                   like(rmtmva)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(jvvare)
     d vbrkl1_ogrp     s                   like(vbogrp)
     d vbrkl1_hgrp     s                   like(vbhgrp)
     d vbrkl1_ugrp     s                   like(vbugrp)
     d vbrkl1_modn     s                   like(vbmodn)
     d vbrkl1_vare     s                   like(vbvare)
     d vbrkl5_vare     s                   like(vbvare)
     d rmbtl1_tbrt     s                   like(rmtbrt)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vbfirm)
     d w_alme          s                   like(vbalme)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom almenning eller vare ikke er valgt, eller dersom
      * vare ikke finnes
      *
     c                   if        p_alme = 0 or
     c                             p_vare = *blank
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      vvogrp = jvogrp
     c                   eval      vvhgrp = jvhgrp
     c                   eval      vvugrp = jvugrp
     c                   eval      vvnmod = jvmodn
     c                   else
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Leter først etter bruksrett på varenivå
      *
     c                   eval      vbrkl5_vare = p_vare
     c     vbrkl5_key    chain     vbrkl5
     c                   if        not %found
      *
      * Leter etter bruksrett på modul og varegruppe-nivå
      *
     c                   eval      vbrkl1_ogrp = vvogrp
     c                   eval      vbrkl1_hgrp = vvhgrp
     c                   eval      vbrkl1_ugrp = vvugrp
     c                   eval      vbrkl1_modn = vvnmod
     c                   eval      vbrkl1_vare = *blank
     c     vbrkl1_key    chain     vbrkl1
     c                   if        not %found
     c                   eval      vbrkl1_modn = 0
     c     vbrkl1_key    chain     vbrkl1
     c                   if        not %found
     c                   eval      vbrkl1_ugrp = 0
     c     vbrkl1_key    chain     vbrkl1
     c                   if        not %found
     c                   eval      vbrkl1_hgrp = 0
     c     vbrkl1_key    chain     vbrkl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      p_brty = vbbrty
      *
     c                   eval      rmbtl1_tbrt = vbbrty
     c     rmbtl1_key    chain     rmbtl1
     c                   if        %found
     c                   eval      p_brr1 = rmtra1
     c                   eval      p_brr2 = rmtra2
     c                   eval      p_omva = rmtmva
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_alme
     c                   parm                    p_vare
     c                   parm                    p_brty
     c                   parm                    p_brr1
     c                   parm                    p_brr2
     c                   parm                    p_omva
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på bruksrett-knytning
      *
     c     vbrkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrkl1_ogrp
     c                   kfld                    vbrkl1_hgrp
     c                   kfld                    vbrkl1_ugrp
     c                   kfld                    vbrkl1_modn
     c                   kfld                    vbrkl1_vare
      *
     c     vbrkl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    vbrkl5_vare
      *
      * Key for oppslag på bruksrett-type
      *
     c     rmbtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_alme
     c                   kfld                    rmbtl1_tbrt
      *
      *
      * Henter firma og almenning fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_alme = p_alme
      *
      * Initierer output-parametre
      *
     c                   eval      p_brty = 0
     c                   eval      p_brr1 = 0
     c                   eval      p_brr2 = 0
6.20 c***>               eval      p_omva = *blank
      *
     c                   endsr
