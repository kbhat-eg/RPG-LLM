     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG205R
      * Beskrivelse . . : Vedlikehold av parametre for gj.sn. kostpris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       101218 ASK  Nytt program
7.01  * 7.01  040220 ASK  Lagt inn bugfix og merking
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkkiu    uf a e           k disk    rename(vgkkst:vgkkiur)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fra01lr    if   e           k disk    rename(ra01pfr:ra01lrr)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
      *
     fvg205d    cf   e             workstn
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d ra01lr_akod     s                   like(raakod)
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_avde     s                   like(rhavde)
     d rhovlr_spes     s                   like(rhspes)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgkfrm)
     d w_kont          s              5  0
     d w_spes          s              4  0
     d w_avde          s              4  0
     d w_tx30          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Vedlikehold av parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     vgkkir_key    chain     vgkkir
      *
     c                   if        %found
     c                   eval      c2kprk = vgkprk
     c                   eval      c2kbik = vgkbik
     c                   eval      c2kbis = vgkbis
     c                   eval      c2krgn = vgkrgn
     c                   eval      c2oppd = vgoppd
     c                   eval      c2lagr = vgprlg
     c                   eval      c2ktok = vgkkos
     c                   eval      c2eusr = vgkusr
     c     *dmy          move      vgkdat        c2edat
     c     *hms          move      vgktim        c2etim
     c                   exsr      les_bilko
     c                   else
     c                   eval      c2kprk = *blank
     c                   eval      c2kbik = 0
     c                   eval      c2kbit = *blank
     c                   eval      c2kbis = *blank
     c                   eval      c2krgn = *blank
     c                   eval      c2oppd = *blank
     c                   eval      c2lagr = *blank
     c                   eval      c2ktok = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Vedlikehold av avviksprosenter
      *
     c                   if        *inkh
     c                   call      'VG230R'
     c                   goto      c2taga
     c                   endif
      *
      * Spørring
      *
7.01  * Bilagskode
     c                   if        *inkd
     c                   if        w_afld = 'C2KBIK'
     c                   call      'RA501R'
     c                   parm                    w_firm
     c                   parm                    c2kbik
     c                   parm                    c2kbit
     c                   goto      c2taga
     c                   endif
      *
7.01  * Eksternkost konto
      *
     c                   if        w_afld = 'C2KTOK'
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
     c                   eval      w_avde = 0
     c                   eval      w_tx30 = ' '
      *
     c                   call      'RH500R'
     c                   parm                    w_firm
     c                   parm                    w_kont
     c                   parm                    w_spes
     c                   parm                    w_avde
     c                   parm                    w_tx30
      *
     c                   if        w_kont <> 0
     c                   eval      c2ktok = w_kont
7.01 c                   eval      c2kktx = w_tx30
     c                   endif
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
7.01 c                   eval      *in46 = *off
7.01 c                   eval      *in47 = *off
7.01 c                   eval      *in48 = *off
      *
      * Sjekk priskode
      *
     c                   if        c2kprk = *blank
     c                   eval      *in41 = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk oppdateringskode
      *
     c                   if        c2oppd = *blank
     c                   eval      *in45 = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk lagerpris kode
      *
     c                   if        c2lagr = *blank
     c                   eval      *in46 = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk posteringskode
      *
     c                   if        c2krgn = *blank
     c                   eval      *in43 = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk bilagskode
      *
     c                   if        c2krgn = 'J'
     c                   if        c2kbik = 0
     c                   eval      *in42 = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk bilagsserie
      *
     c                   if        c2kbis = *blank
     c                   eval      *in44 = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk kostkonto ekstern salg
      *
     c                   if        c2ktok = 0
     c                   eval      *in47 = *on
     c                   exfmt     c1msg
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vgkkiur
      *
     c     vgkkiu_key    chain     vgkkiur
      *
     c                   eval      vgkprk = c2kprk
     c                   eval      vgkbik = c2kbik
     c                   eval      vgkbis = c2kbis
     c                   eval      vgkrgn = c2krgn
     c                   eval      vgoppd = c2oppd
     c                   eval      vgprlg = c2lagr
     c                   eval      vgkkos = c2ktok
7.01 c                   eval      vgkkoi = 0
      *
     c                   time                    vgkdat
     c                   time                    vgktim
     c                   eval      vgkusr = l_user
      *
     c                   if        %found
     c                   update    vgkkiur
     c                   else
     c                   eval      vgkfrm = w_firm
     c                   write     vgkkiur
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for feilmeldinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppslag mot bilagskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_bilko     begsr
      *
     c                   eval      ra01lr_akod = c2kbik
     c     ra01lr_key    chain     ra01lrr
     c                   eval      c2kbit = raatxt
      *
     c     end_bilko     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på kodetabell
      *
     c     vgkkiu_key    klist
     c                   kfld                    w_firm
      *
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Key for posisjonering på bilagskode
      *
     c     ra01lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01lr_akod
      *
      * Key for posisjonering på konto
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_avde
     c                   kfld                    rhovlr_spes
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
