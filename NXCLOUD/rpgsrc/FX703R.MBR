     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX703R
      * Beskrivelse . . : Konv530: Henter momskode på ordre- og fakturalinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       291004 HKO  Nytt program
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     fsodtlu    uf   e           k disk    rename(sodtpfr:sodtlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
6.10  * Definering av local data area
6.10  *
6.10 d lda            uds
6.10 d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Ordre-linje
      *
     c                   read      fodtlur
     c                   dow       not %eof
      *
     c                   if        fdvare <> *blank and
     c                             fdomva = *blank
      *
     c                   eval      fdomva = '3'
      *
     c                   eval      vvarl1_firm = fdfirm
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c     vvarl1_key    chain     vvasl1
     c                   endif
      *
     c                   if        %found and
     c                             vvkmva <> 0
     c                   if        vvkmva = 1
     c                   eval      fdomva = '4'
     c                   else
     c                   eval      fdomva = 'H'
     c                   endif
     c                   endif
      *
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   endif
      *
     c                   read      fodtlur
     c                   enddo
      *
      * Faktura-linje
      *
     c                   read      sodtlur
     c                   dow       not %eof
      *
     c                   if        sdvare <> *blank and
     c                             sdomva = *blank
     c                   select
     c                   when      sdkmva = 1
     c                   eval      sdomva = '4'
     c                   when      sdkmva = 2
     c                   eval      sdomva = 'H'
     c                   other
     c                   eval      sdomva = '3'
     c                   endsl
6.10 c                   time                    sdedat
6.10 c                   time                    sdetim
6.10 c                   eval      sdeusr = l_user
     c                   update    sodtlur
     c                   endif
      *
     c                   read      sodtlur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
     c                   endsr
