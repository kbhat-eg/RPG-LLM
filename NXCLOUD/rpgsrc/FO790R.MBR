     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO790R
      * Beskrivelse . . : Bytting av kundenummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030500 HKO  Nytt program
      *       110506 HKO  Dersom kundenr som skal byttes til allerede finnes
      *                   i kunde distribusjonsliste, rabattmatrise eller
      *                   spesialprisregister, slettes informasjonen knyttet
      *                   mot kundenr det byttes fra.
5.70  * PRGR  211008 bof  Utvidet med prisgruppe
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
7.01  *k00  030620 BOF NXS-1195  kompilert pga tab.utvidelse
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohele    uf   e           k disk    rename(fohepfr:foheler)
     ffohelf    uf   e           k disk    rename(fohepfr:fohelfr)
     ffodtlk    uf   e           k disk    rename(fodtpfr:fodtlkr)
     ffdelle    uf   e           k disk    rename(fdelpfr:fdeller)
     ffdellf    uf   e           k disk    rename(fdelpfr:fdellfr)
     ffkdil1    if   e           k disk    rename(fkdipfr:fkdil1r)
     ffkdilu    uf   e           k disk    rename(fkdipfr:fkdilur)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffkprlu    uf   e           k disk    rename(fkprpfr:fkprlur)
     ffkpslu    uf   e           k disk    rename(fkpspfr:fkpslur)
     ffrabl1    if   e           k disk    rename(frabpfr:frabl1r)
     ffrabl3    uf   e           k disk    rename(frabpfr:frabl3r)
     ffpril1    if   e           k disk    rename(fpripfr:fpril1r)
     ffpril3    uf   e           k disk    rename(fpripfr:fpril3r)
     ffstslu    uf   e           k disk    rename(fstspfr:fstslur)
      *
     fsohele    uf   e           k disk    rename(sohepfr:soheler)
     fsohelf    uf   e           k disk    rename(sohepfr:sohelfr)
6.10  *
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
     d p_gkun          s                   like(fokund)
     d p_kund          s                   like(fokund)
      *
      * Definering av variable i nøkler
      *
     d fkdil1_kund     s                   like(fhkund)
     d fkdil1_kpro     s                   like(fhkpro)
     d fkprlu_kund     s                   like(flkund)
     d fkprlu_kpro     s                   like(flkpro)
5.70 d frabl1_prgr     s                   like(frprgr)
     d frabl1_rkat     s                   like(frrkat)
     d frabl1_kkat     s                   like(frkkat)
     d frabl1_kund     s                   like(frkund)
     d frabl1_kpro     s                   like(frkpro)
     d frabl1_ogrp     s                   like(frogrp)
     d frabl1_hgrp     s                   like(frhgrp)
     d frabl1_ugrp     s                   like(frugrp)
     d frabl1_ldor     s                   like(frldor)
     d frabl1_modn     s                   like(frmodn)
     d frabl1_vare     s                   like(frvare)
     d frabl1_enhe     s                   like(frenhe)
     d frabl1_lety     s                   like(frlety)
5.70 d fpril1_prgr     s                   like(fpprgr)
     d fpril1_rkat     s                   like(fprkat)
     d fpril1_kkat     s                   like(fpkkat)
     d fpril1_kund     s                   like(fpkund)
     d fpril1_kpro     s                   like(fpkpro)
     d fpril1_vare     s                   like(fpvare)
     d fpril1_enhe     s                   like(fpenhe)
     d fpril1_lety     s                   like(fplety)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_gkun          s                   like(fokund)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma og gammelt kundenummer fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_gkun = p_gkun
      *
      * Ordre-hode - Fakturakunde
      *
     c     key02         setll     fohele
     c     key02         reade     fohele
     c                   dow       not %eof
     c                   eval      fokund = p_kund
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    foheler
     c     key02         reade     fohele
     c                   enddo
      *
      * Ordre-hode - Varekunde
      *
     c     key02         setll     fohelf
     c     key02         reade     fohelf
     c                   dow       not %eof
     c                   eval      folkun = p_kund
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelfr
     c     key02         reade     fohelf
     c                   enddo
      *
      * Ordre-linje
      *
     c     key02         setll     fodtlk
     c     key02         reade     fodtlkr
     c                   dow       not %eof
     c                   eval      fdkund = p_kund
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlkr
     c     key02         reade     fodtlkr
     c                   enddo
      *
      * Ordre-slettet - Fakturakunde
      *
     c     key02         setll     fdelle
     c     key02         reade     fdeller
     c                   dow       not %eof
     c                   eval      fykund = p_kund
6.10 c                   time                    fyedat
6.10 c                   time                    fyetim
6.10 c                   eval      fyeusr = l_user
     c                   update    fdeller
     c     key02         reade     fdeller
     c                   enddo
      *
      * Ordre-slettet - Varekunde
      *
     c     key02         setll     fdellf
     c     key02         reade     fdellfr
     c                   dow       not %eof
     c                   eval      fylkun = p_kund
6.10 c                   time                    fyedat
6.10 c                   time                    fyetim
6.10 c                   eval      fyeusr = l_user
     c                   update    fdellfr
     c     key02         reade     fdellfr
     c                   enddo
      *
      * Kunde-distibusjonsliste
      * Dersom det allerede finnes informasjon mot kundenr det byttes til,
      * slettes informasjon mot kundenr det byttes fra
      *
     c     key02         setll     fkdilu
     c     key02         reade     fkdilur
     c                   dow       not %eof
     c                   eval      fkdil1_kund = p_kund
     c                   eval      fkdil1_kpro = fhkpro
     c     fkdil1_key    chain     fkdil1
     c                   if        %found
     c                   delete    fkdilur
     c                   else
     c                   eval      fhkund = p_kund
6.10 c                   time                    fhedat
6.10 c                   time                    fhetim
6.10 c                   eval      fheusr = l_user
     c                   update    fkdilur
     c                   endif
     c     key02         reade     fkdilur
     c                   enddo
      *
      * Kundeprosjekt
      *
     c     key01         setll     fkprl1
     c     key01         reade     fkprl1
     c                   dow       not %eof
     c                   if        flkund = p_gkun or
     c                             flokun = p_gkun
     c                   eval      fkprlu_kund = flkund
     c                   eval      fkprlu_kpro = flkpro
     c     fkprlu_key    chain     fkprlur
     c                   if        %found
     c                   if        flkund = p_gkun
     c                   eval      flkund = p_kund
     c                   endif
     c                   if        flokun = p_gkun
     c                   eval      flokun = p_kund
     c                   endif
6.10 c                   time                    fledat
6.10 c                   time                    fletim
6.10 c                   eval      fleusr = l_user
     c                   update    fkprlur
     c                   endif
     c                   endif
     c     key01         reade     fkprl1
     c                   enddo
      *
      * Kundeprosjekt-søketekst
      *
     c     key02         setll     fkpslu
     c     key02         reade     fkpslur
     c                   dow       not %eof
     c                   eval      flskun = p_kund
6.10 c                   time                    flseda
6.10 c                   time                    flseti
6.10 c                   eval      flseus = l_user
     c                   update    fkpslur
     c     key02         reade     fkpslur
     c                   enddo
      *
      * Rabattmatrise
      * Dersom det allerede finnes informasjon mot kundenr det byttes til,
      * slettes informasjon mot kundenr det byttes fra
      *
     c     key02         setll     frabl3
     c     key02         reade     frabl3r
     c                   dow       not %eof
5.70 c                   eval      frabl1_prgr = frprgr
     c                   eval      frabl1_rkat = frrkat
     c                   eval      frabl1_kkat = frkkat
     c                   eval      frabl1_kund = p_kund
     c                   eval      frabl1_kpro = frkpro
     c                   eval      frabl1_ogrp = frogrp
     c                   eval      frabl1_hgrp = frhgrp
     c                   eval      frabl1_ugrp = frugrp
     c                   eval      frabl1_ldor = frldor
     c                   eval      frabl1_modn = frmodn
     c                   eval      frabl1_vare = frvare
     c                   eval      frabl1_enhe = frenhe
     c                   eval      frabl1_lety = frlety
     c     frabl1_key    chain     frabl1
     c                   if        %found
     c                   delete    frabl3r
     c                   else
     c                   eval      frkund = p_kund
6.10 c                   time                    fredat
6.10 c                   time                    fretim
6.10 c                   eval      freusr = l_user
     c                   update    frabl3r
     c                   endif
     c     key02         reade     frabl3r
     c                   enddo
      *
      * Spesialpriser
      * Dersom det allerede finnes informasjon mot kundenr det byttes til,
      * slettes informasjon mot kundenr det byttes fra
      *
     c     key02         setll     fpril3
     c     key02         reade     fpril3r
     c                   dow       not %eof
5.70 c                   eval      fpril1_prgr = fpprgr
     c                   eval      fpril1_rkat = fprkat
     c                   eval      fpril1_kkat = fpkkat
     c                   eval      fpril1_kund = p_kund
     c                   eval      fpril1_kpro = fpkpro
     c                   eval      fpril1_vare = fpvare
     c                   eval      fpril1_enhe = fpenhe
     c                   eval      fpril1_lety = fplety
     c     fpril1_key    chain     fpril1
     c                   if        %found
     c                   delete    fpril3r
     c                   else
     c                   eval      fpkund = p_kund
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fpril3r
     c                   endif
     c     key02         reade     fpril3r
     c                   enddo
      *
      * Status
      *
     c     key01         chain     fstslur
     c                   if        %found
     c                   if        faakun = p_gkun
     c                   eval      faakun = p_kund
     c                   endif
     c                   if        faaknr = p_gkun
     c                   eval      faaknr = p_kund
     c                   endif
6.10 c                   time                    faaeda
6.10 c                   time                    faaeti
6.10 c                   eval      faaeus = l_user
     c                   update    fstslur
     c                   endif
      *
      * Faktura-hode - fakturakunde
      *
     c     key02         setll     sohele
     c     key02         reade     soheler
     c                   dow       not %eof
     c                   eval      sokund = p_kund
6.10 c                   time                    soedat
6.10 c                   time                    soetim
6.10 c                   eval      soeusr = l_user
     c                   update    soheler
     c     key02         reade     soheler
     c                   enddo
      *
      * Faktura-hode - varekunde
      *
     c     key02         setll     sohelf
     c     key02         reade     sohelfr
     c                   dow       not %eof
     c                   eval      solkun = p_kund
6.10 c                   time                    soedat
6.10 c                   time                    soetim
6.10 c                   eval      soeusr = l_user
     c                   update    sohelfr
     c     key02         reade     sohelfr
     c                   enddo
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gkun
     c                   parm                    p_kund
      *
      * Key for les på firma
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
      * Key for les på firma og kunde
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    w_gkun
      *
      * Key for oppslag på kunde distribusjonsliste
      *
     c     fkdil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
     c                   kfld                    fkdil1_kpro
      *
      * Key for oppdatering av kundeprosjekt
      *
     c     fkprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlu_kund
     c                   kfld                    fkprlu_kpro
      *
      * Key for oppslag på rabattmatrise
      *
     c     frabl1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    frabl1_prgr
     c                   kfld                    frabl1_rkat
     c                   kfld                    frabl1_kkat
     c                   kfld                    frabl1_kund
     c                   kfld                    frabl1_kpro
     c                   kfld                    frabl1_ogrp
     c                   kfld                    frabl1_hgrp
     c                   kfld                    frabl1_ugrp
     c                   kfld                    frabl1_ldor
     c                   kfld                    frabl1_modn
     c                   kfld                    frabl1_vare
     c                   kfld                    frabl1_enhe
     c                   kfld                    frabl1_lety
      *
      * Key for oppslag på spesialpriser
      *
     c     fpril1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fpril1_prgr
     c                   kfld                    fpril1_rkat
     c                   kfld                    fpril1_kkat
     c                   kfld                    fpril1_kund
     c                   kfld                    fpril1_kpro
     c                   kfld                    fpril1_vare
     c                   kfld                    fpril1_enhe
     c                   kfld                    fpril1_lety
      *
     c                   endsr
