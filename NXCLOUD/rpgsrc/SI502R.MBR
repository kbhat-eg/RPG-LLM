      /TITLE  ASSTAT Spørring på kjøp, kjøp av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SI502R
      * Beskrivelse . . : Spørring på kjøp, kjøp av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150698 HKO  Nytt program
      *       271000 KOB  Fjernet referanse til leveringsadresse
6.nu  *       160614 bof  Gjennomgått mtp. nummerutvidelse
7.xx  * 7.00  041016 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 6.30  180517 bsa  Lagt inn oppslag mot arkiv.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsistl9    if   e           k disk    rename(sistpfr:sistl9r)
     f*istla    if   e           k disk    rename(sistpfr:sistlar)
     fsistlb    if   e           k disk    rename(sistpfr:sistlbr)
     fsistlc    if   e           k disk    rename(sistpfr:sistlcr)
     fsistld    if   e           k disk    rename(sistpfr:sistldr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     f*klal1    if   e           k disk    rename(lklapfr:lklal1r)
     fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
7.01 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
     fSI502d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sifirm)
     d p_paar          s                   like(sipaar)
     d p_vare          s                   like(sivare)
     d p_valn          s                   like(sivaln)
     d p_ladr          s                   like(siladr)
     d p_bida          s                   like(sibida)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sistl9_valn     s                   like(sivaln)
     d sistl9_ladr     s                   like(siladr)
     d sistl9_bida     s                   like(sibida)
     d*sistla_ladr     s                   like(siladr)
     d sistlb_bida     s                   like(sibida)
     d sistlc_binr     s                   like(sibinr)
     d sistld_ornr     s                   like(siornr)
     d vvarl1_vare     s                   like(vvvare)
     d vvasl1_vare     s                   like(vvvare)
     d rlevl1_levr     s                   like(rllevr)
     d*lklal1_levr     s                   like(llldor)
     d*lklal1_kode     s                   like(llkode)
     d sihel1_aarr     s                   like(shaarr)
     d sihel1_binr     s                   like(shbinr)
     d sihel1_numm     s                   like(shnumm)
7.01 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_spge          s              3  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_swtc          s              1  0
      *
     d w_firm          s                   like(sifirm)
     d w_paar          s                   like(sipaar)
     d w_vare          s                   like(sivare)
     d w_bida          s                   like(sibida)
     d w_knav          s                   like(rlnavn)
     d*w_lnav          s                   like(llnavn)
     d*w_adr1          s                   like(lladr1)
     d*w_adr2          s                   like(lladr2)
     d*w_sted          s                   like(lladr1)
     d*w_rkat          s                   like(llrkat)
     d*w_proj          s                   like(llproj)
7.01 d w_modu          s             10    inz('PROA    ')
7.01 d w_disp          s              1    inz('1')
7.01 d w_tilg          s              1
7.01 d w_dtyp          s             50
7.01 d w_refn          s             50
7.01 d w_aarr          s              4  0
7.01 d w_date          s               d   datfmt(*iso)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      /EJECT
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valn <> 0 or
     c***                          b2ladr <> 0 or
     c                             b2bida <> 0 or
     c                             b2binr <> 0 or
     c                             b2ornr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * levre
      *
     c                   if        wactfe = 'B2valn'
     c                   call      'FSPKUR'
     c                   parm                    w_firm
     c                   parm                    b2valn
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
      * Leveringsadresse-kode
      *
     c***                if        wactfe = 'B2LADR'
     c***                CALL      'FSPKLR'
     c***                parm                    w_firm
     c***                parm                    b2valn
     c***                parm                    b2ladr
     c***                parm                    w_lnav
     c***                parm                    w_adr1
     c***                parm                    w_adr2
     c***                parm                    w_sted
     c***                parm                    w_rkat
     c***                parm                    w_proj
     c***                parm                    w_swtc
     c***                goto      end_spØrr
     c***                endif
      *
      *
     c     end_spØrr     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c***                when      w_seqe = 'A'
     c***  sistla_key    setll     sistla
     c                   when      w_seqe = 'D'
     c     sistlb_key    setll     sistlb
     c                   when      w_seqe = 'F'
     c     sistlc_key    setll     sistlc
     c                   when      w_seqe = 'O'
     c     sistld_key    setll     sistld
     c                   other
     c     sistl9_key    setll     sistl9
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på varelevre
      *
     c                   if        b2valn <> 0
     c                   eval      w_seqe = ' '
     c                   eval      sistl9_valn = b2valn
     c***                eval      sistl9_ladr = b2ladr
     c                   eval      sistl9_ladr = *zeros
     c                   eval      sistl9_bida = *loval
     c                   if        b2bida <> 0
     c     *dmy          test(d)                 b2bida                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2bida        sistl9_bida
     c                   endif
     c                   endif
     c     sistl9_key    setll     sistl9
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leveringsadresse
      *
     c***                if        b2ladr <> 0
     c***                eval      w_seqe = 'A'
     c***                eval      sistla_ladr = b2ladr
     c***  sistla_key    setll     sistla
     c***                goto      end_pos
     c***                endif
      *
      * Posisjoner startverdi på fakturadato
      *
     c                   if        b2bida <> 0
     c                   eval      w_seqe = 'D'
     c                   eval      sistlb_bida = *loval
     c     *dmy          test(d)                 b2bida                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2bida        sistlb_bida
     c                   endif
     c     sistlb_key    setll     sistlb
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på fakturanummer
      *
     c                   if        b2binr <> 0
     c                   eval      w_seqe = 'F'
     c                   eval      sistlc_binr = b2binr
     c     sistlc_key    setll     sistlc
     c                   goto      end_pos
      *
     c                   endif
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2ornr <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      sistld_ornr = b2ornr
     c     sistld_key    setll     sistld
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2valn = 0
     c***                eval      b2ladr = 0
     c                   eval      b2bida = 0
     c                   eval      b2binr = 0
     c                   eval      b2ornr = 0
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn =  wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Vise salgsdetaljer
      *
     c                   if        b1valg = '5'
     c     *dmy          move      b1bida        w_bida
     c                   call      'SI510R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1binr
     c                   parm                    b1line
     c                   parm                    w_bida
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv fakturakopi
      *
     c                   if        b1valg = '6'
     c                   exsr      skriv
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise faktura
      *
     c                   if        b1valg = '7'
     c                   exsr      vise
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
7.01  *
7.01  * Slå opp i arkiv.
7.01  *
7.01 c                   if        b1valg = '9'
7.01  *
7.01  * Kontrollerer gyldig tilgang
7.01  *
7.01 c                   call      'AX700R'
7.01 c                   parm                    w_modu
7.01 c                   parm                    w_disp
7.01 c                   parm                    w_tilg
7.01  *
7.01 c                   if        w_tilg = '0'
7.01  *
7.01  * Hent hvilken kategori ordretype vi sjekker mot.
7.01  *
7.01 c                   eval      w_dtyp = *blanks
7.01 c                   eval      votyl1_otyp = b1otyp
7.01 c     votyl1_key    chain     votyl1
7.01 c                   if        not %found
7.01 c                   eval      vaoakk = 9
7.01 c                   endif
7.01  *
7.01 c                   if        vaosys = 1 and
7.01 c                             vaoakk = 3
7.01 c     *dmy          move      b1bida        w_date
7.01 c                   eval      w_aarr = %subdt(w_date:*years)
7.01 c                   eval      w_dtyp = 'incominginvoice'
7.01 c                   eval      w_refn = *blank
7.01  *
7.01 c**                 eval      w_refn = %trim(%char(b1binr))
7.01 c                   eval      w_refn = %char(w_aarr) + %trim(%char(b1binr))
7.01  *
7.01 c                   call      'AP622C'
7.01 c                   parm                    w_dtyp
7.01 c                   parm                    w_refn
7.01 c                   endif
7.01  *
7.01 c                   endif
7.01  *
7.01 c                   eval      b1valg = *blanks
7.01 c                   update    b1sfl
7.01 c                   iter
7.01 c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c***                when      w_seqe = 'A'
     c***                read      sistla                                 15
     c                   when      w_seqe = 'D'
     c                   read      sistlb                                 15
     c                   when      w_seqe = 'F'
     c                   read      sistlc                                 15
     c                   when      w_seqe = 'O'
     c                   read      sistld                                 15
     c                   other
     c                   read      sistl9                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        sifirm <> w_firm or
     c                             sipaar <> w_paar or
     c                             sivare <> w_vare
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   if        p_valn <> 0 and
     c                             sivaln <> p_valn
     c                   iter
     c                   endif
      *
     c                   if        p_ladr <> 0 and
     c                             siladr <> p_ladr
     c                   iter
     c                   endif
      *
     c                   if        p_bida <> *loval and
     c                             sibida <> p_bida
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   eval      b1line = siline
     c                   eval      b1wsid = siwsid
     c                   eval      b1valn = sivaln
     c***                eval      b1ladr = siladr
     c                   eval      b1binr = sibinr
     c                   eval      b1ornr = siornr
     c                   eval      b1otyp = siotyp
     c                   eval      b1anta = sianta
      *
     c                   eval      b1bida = 0
     c                   eval      b1lnav = *blank
     c***                eval      b1lana = *blank
      *
     c                   if        sibida <> *loval
     c     *dmy          move      sibida        b1bida
     c                   endif
      *
     c                   eval      rlevl1_levr = sivaln
     c     rlevl1_key    chain     rlevl1                             61
     c                   if        *in61 = *off
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
     c***                eval      lklal1_levr = sivaln
     c***                eval      lklal1_kode = siladr
     c***  lklal1_key    chain     lklal1                             61
     c***                if        *in61 = *off
     c***                eval      b1lnav = llnavn
     c***                endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vise          begsr
      *
      * Dersom rad er faktura, kalles program for å vise faktura
      *
     c                   eval      sihel1_aarr = w_paar
     c                   eval      sihel1_binr = b1binr
     c                   eval      sihel1_numm = b1ornr
     c     sihel1_key    chain     sihel1                             61
     c                   if        *in61 = *off
      *
     c***                call      'FO310R'
     c                   call      'SH410R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1valn
     c                   parm                    b1binr
      *
     c                   goto      end_vise
     c                   endif
      *
     c     end_vise      endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive fakturakopi
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv         begsr
      *
      * Dersom rad er faktura, kalles program for å skrive fakturakopi
      *
     c                   eval      sihel1_aarr = w_paar
     c                   eval      sihel1_binr = b1binr
     c                   eval      sihel1_numm = b1ornr
     c     sihel1_key    chain     sihel1                             61
     c                   if        *in61 = *off
      *
     c                   call      'FO681R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1binr
     c                   parm                    b1otyp
      *
     c                   goto      end_skriv
     c                   endif
      *
     c     end_skriv     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_paar
     c                   parm                    p_vare
     c                   parm                    p_valn
     c***                parm                    p_ladr
     c                   parm                    p_bida
      *
      * Key for posisjonering på levrenummer
      *
     c     sistl9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vare
     c                   kfld                    sistl9_valn
     c                   kfld                    sistl9_ladr
     c                   kfld                    sistl9_bida
      *
      * Key for posisjonering på leveringsadresse
      *
     c***  sistla_key    klist
     c***                kfld                    w_firm
     c***                kfld                    w_paar
     c***                kfld                    w_vare
     c***                kfld                    sistla_ladr
      *
      * Key for posisjonering på fakturadato
      *
     c     sistlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vare
     c                   kfld                    sistlb_bida
      *
      * Key for posisjonering på fakturanummer
      *
     c     sistlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vare
     c                   kfld                    sistlc_binr
      *
      * Key for posisjonering på ordrenummer
      *
     c     sistld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vare
     c                   kfld                    sistld_ornr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på slettet vare
      *
     c     vvasl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvasl1_vare
      *
      * Key for oppslag på levre
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på leveringsadresse
      *
     c***  lklal1_key    klist
     c***                kfld                    w_firm
     c***                kfld                    lklal1_levr
     c***                kfld                    lklal1_kode
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sihel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    sihel1_binr
     c                   kfld                    sihel1_numm
7.01  *
7.01  *  Key les av ordretype
7.01  *
7.01 c     votyl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    votyl1_otyp
      *
      * Henter firmakode, år og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_paar = p_paar
     c                   eval      w_vare = p_vare
      *
      * Henter verdi til skjermbilde
      *
     c                   eval      b2paar = w_paar
     c                   eval      b2vare = p_vare
     c                   eval      b2valn = p_valn
     c***                eval      b2lad1 = p_ladr
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             61
     c                   if        *in61 = *on
     c                   eval      vvasl1_vare = p_vare
     c     vvasl1_key    chain     vvasl1                             61
     c                   endif
      *
     c                   if        *in61 = *off
     c                   eval      b2tek1 = vvtek1
     c                   endif
      *
     c                   if        p_bida <> *loval
     c     *dmy          move      p_bida        b2bid1
     c                   endif
      *
      * Fyller subfile
      *
     c                   eval      *in22 = *on
      *
     c                   eval      sistl9_valn = p_valn
     c                   eval      sistl9_ladr = p_ladr
     c     sistl9_key    setll     sistl9
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
