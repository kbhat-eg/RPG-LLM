     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LI201R
      * Beskrivelse . . : Behandling av innkomne EDI-pakksedler - kolli informasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.00  *       260819 bsa  Nytt program
7.01  *       030220 bsa  Henter bestilt antal og enhet fra bestilling, hvis ikke
7.01  *                   bestillingen finnes, brukes info fra pakkseddel.
7.02  *       090320 bsa  Skiftet index for endre sortering
7.03  *K000   121120 bsa  Endret unik nøkkel på LPKLST
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flpkli1    if   e           k disk    rename(lpklst:lpkli1r)
7.02 flpkli3    if   e           k disk    rename(lpklst:lpkli3r)
     flphoir    if   e           k disk    rename(lphost:lphoirr)
     flppai1    if   e           k disk    rename(lppast:lppai1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     felogl3    uf   e           k disk    rename(elogpfr:elogl3r)
      *
     fli202d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fkey               369    369
     d  d_srow               370    370I 0
     d  d_scol               371    371I 0
     d  d_fcrn               378    379I 0
      *
      * Parameter-ut
      *
      *
      * Definering av variabler i nøkler
      *
     d lphoir_type     s                   like(phtype)
     d lphoir_mnum     s                   like(phmnum)
7.02 d lpkli3_type     s                   like(pltype)
7.02 d lpkli3_mnum     s                   like(plmnum)
7.02 d lpkli3_aarr     s                   like(plaarr)
7.02 d lpkli3_paid     s                   like(plpaid)
7.02 d lpkli3_pver     s                   like(plpver)
7.02 d lpkli3_numm     s                   like(plnumm)
7.02 d lpkli3_suff     s                   like(plsuff)
7.02 d lpkli3_blin     s                   like(plblin)
     d lpkli1_type     s                   like(pltype)
     d lpkli1_mnum     s                   like(plmnum)
     d lpkli1_aarr     s                   like(plaarr)
     d lpkli1_paid     s                   like(plpaid)
     d lpkli1_pver     s                   like(plpver)
     d lpkli1_fsni     s                   like(plfsni)
     d lpkli1_fniv     s                   like(plfniv)
7.03 d lpkli1_sscc     s                   like(plsscc)
     d lpkli1_linr     s                   like(pllinr)
     d lppai1_type     s                   like(pptype)
     d lppai1_mnum     s                   like(ppmnum)
     d lppai1_aarr     s                   like(ppaarr)
     d lppai1_paid     s                   like(pppaid)
     d lppai1_pver     s                   like(pppver)
     d lppai1_paty     s                   like(pppaty)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d elogl3_faar     s                   like(xlfaar)
     d elogl3_type     s                   like(xltype)
     d elogl3_mnum     s                   like(xlmnum)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(phfirm)
     d w_modu          s             10    inz('NED     ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
     d w_benr          s                   like(lonumm)
     d w_lpro          s              1
      *
     d p_type          s              4
     d p_mnum          s              8  0
     d p_aarr          s              4  0
     d p_paid          s             17
     d p_pver          s              3  0
     d p_fsni          s             18
     d p_fniv          s              6  0
     d p_sscc          s             18  0
      *
     d p_eann          s             14  0
     d p_vare          s             15
     d p_enhe          s              3
     d p_nobb          s              8  0
     d p_stat          s              1
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(17)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - -
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * B4WIN  - B4 Vindu for slette (Delete)
      * F1BLD  - F1 Søke på meldingstype
      * H1WIN  - H1 Vindu for angivelse av søkefilter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Sjekk om meldingstype er fast eller skal velges
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '1'
     c                   goto      end_pgm
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
7.02 c     lpkli3_key    setll     lpkli3
      *
     c                   exsr      crt_subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   eval      w_fcrn = *zero
     c                   exsr      forny
     c                   goto      b2tagb
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkj
     c                   exsr      xb10win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkm
     c                   exsr      xb13win
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   eval      lphoir_type = *blanks
     c                   eval      lphoir_mnum = b1mnum
     c                   else
     c                   eval      lphoir_mnum = 0
     c                   endif
      *
     c     lphoir_key    setll     lphoir
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b_forn = *off
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Slette melding
      *
     c                   if        b1valg = 4
     c**                 exsr      xb4win
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Se på detaljer i melding
      *
     c                   if        b1valg = 5
     c                   exsr      xb5bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på detaljer i melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb5bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      lpkli1_type = *blanks
     c                   eval      lpkli1_mnum = b1mnum
     c                   eval      lpkli1_aarr = b1aarr
     c                   eval      lpkli1_paid = b2paid
     c                   eval      lpkli1_pver = b1pver
     c                   eval      lpkli1_fsni = b1fsni
     c                   eval      lpkli1_fniv = b1fniv
7.03 c                   eval      lpkli1_sscc = b1sscc
     c                   eval      lpkli1_linr = b1linr
     c     lpkli1_key    chain     lpkli1
      *
     c                   if        %found
     c                   eval      c1paid = b2paid
     c                   eval      c1pver = plpver
     c                   eval      c1stat = plstat
     c                   eval      c1sbes = *blanks
     c                   eval      c1sscc = plsscc
     c                   eval      c1linr = pllinr
     c                   eval      c1gtin = plgtin
     c                   eval      c1nobb = plnobb
     c                   eval      c1lvar = pllvar
     c                   eval      c1navn = plnavn
     c                   eval      c1numm = plnumm
     c                   eval      c1suff = plsuff
     c                   eval      c1blin = plblin
     c                   eval      c1onum = 0
     c                   eval      c1osuf = 0
     c                   eval      c1olin = 0
     c                   eval      c1bant = 0
     c                   eval      c1benh = *blanks
     c                   eval      c1pant = plpant
     c                   eval      c1penh = plpenh
     c                   eval      c1rant = plrant
     c                   eval      c1renh = plrenh
     c                   eval      c1oant = 0
     c                   eval      c1oenh = *blanks
     c                   eval      c1vant = plvant
     c                   eval      c1venh = plvenh
      * Finnes det ordrelinjer tilknyttet ??
     c                   if        plnumm <> 0
     c                   eval      lodtl1_numm = plnumm
     c                   eval      lodtl1_suff = plsuff
     c                   eval      lodtl1_line = plblin
     c     lodtl1_key    chain     lodtl1
     c                   if        %found
     c                   eval      c1bant = ldanta
     c                   eval      c1benh = ldenh1
     c                   eval      fodtl1_numm = ldornu
     c                   eval      fodtl1_suff = ldorsu
     c                   eval      fodtl1_line = ldorli
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
     c                   eval      c1oant = fdanta
     c                   eval      c1oenh = fdenh1
     c                   eval      c1onum = fdnumm
     c                   eval      c1osuf = fdsuff
     c                   eval      c1olin = fdline
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        plrdat <> *loval
     c     *dmy          move      plrdat        c1rdat
     c                   else
     c                   eval      c1rdat = 0
     c                   endif
     c                   else
     c                   eval      c1paid = *blanks
     c                   eval      c1pver = 0
     c                   eval      c1stat = *blanks
     c                   eval      c1sbes = *blanks
     c                   eval      c1sscc = 0
     c                   eval      c1linr = 0
     c                   eval      c1gtin = *blanks
     c                   eval      c1nobb = 0
     c                   eval      c1lvar = *blanks
     c                   eval      c1navn = *blanks
     c                   eval      c1numm = 0
     c                   eval      c1suff = 0
     c                   eval      c1blin = 0
     c                   eval      c1onum = 0
     c                   eval      c1osuf = 0
     c                   eval      c1olin = 0
     c                   eval      c1bant = 0
     c                   eval      c1benh = *blanks
     c                   eval      c1pant = 0
     c                   eval      c1penh = *blanks
     c                   eval      c1rant = 0
     c                   eval      c1renh = *blanks
     c                   eval      c1oant = 0
     c                   eval      c1oenh = *blanks
     c                   eval      c1rdat = 0
     c                   endif
      *
     c     c5taga        tag
      *
     c                   exfmt     c5bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c5bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c5taga
     c                   endif
      *
     c     end_c5bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av filter-bildet (H1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c                   if        h1sldo = *zero
     c                   eval      h1lnav = *blank
     c                   endif
     c                   if        h1innk = *zero
     c                   eval      h1inav = *blank
     c                   endif
      *
     c                   if        h1lage = *zero
     c                   endif
      *
     c                   if        h2lage = *zero
     c                   endif
      *
     c                   if        h3lage = *zero
     c                   endif
      *
     c                   if        h4lage = *zero
     c                   endif
      *
     c                   if        h5lage = *zero
     c                   endif
      *
     c                   if        h1fakr = *blank
     c                   eval      h1fakr = '0'
     c                   endif
     c                   if        h1vmot = *blank
     c                   eval      h1vmot = '0'
     c                   endif
      *
     c     h1taga        tag
     c                   exfmt     h1win
      *
     c                   if        h1fakr = '0'
     c                   eval      h1fakr = *blank
     c                   endif
     c                   if        h1vmot = '0'
     c                   eval      h1vmot = *blank
     c                   endif
      *
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
      *
     c                   if        *inkc or *inkl
     c                   goto      end_h1win
     c                   endif
      *
     c                   if        *inkd
     c                   if        w_afld = 'H1SLDO'
     C                   call      'RL500R'
     C                   parm                    w_firm
     C                   parm                    h1sldo
     C                   parm                    h1lnav
     c                   endif
      *
     c                   if        w_afld = 'H1INNK'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    h1innk
     c                   parm                    h1inav
     c                   endif
     c                   goto      h1taga
     c                   endif
      *
     c                   if        h1snav <> *blank
     c                   eval      h1snav = %trim(%xlate(lo:up:h1snav))
     c                   endif
      *
     c                   if        h1smda <> 0
     c     *dmy          test(d)                 h1smda                 31
     c                   if        *in31  = *on
     c                   goto      h1taga
     c                   endif
     c                   endif
      *
     c                   if        h1sfak <> *blank
     c                   eval      h1sfak = %trim(%xlate(lo:up:h1sfak))
     c                   endif
      *
     c                   if        h1sbes <> *blank
     c                   eval      h1sbes = %trim(%xlate(lo:up:h1sbes))
     c                   endif
      *
     c                   eval      w_fcrn = *zero
      *
     c     end_h1win     endsr
      *
      * Subrutine for overgang til bestillingssystem
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb10win       begsr
      *
     c                   eval      w_xrow = d_srow
     c                   eval      w_xcol = d_scol
      *
     c                   eval      w_benr = *zero
     c                   eval      w_lpro = *blank
      *
     c                   call      'LO500R'
     c                   parm                    w_benr
     c                   parm                    w_lpro
      *
     c     end_xb10win   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb13win       begsr
      *
     c                   eval      w_xrow = d_srow
     c                   eval      w_xcol = d_scol
      *
     c                   exfmt     b13win
      *
     c     end_xb13win   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
7.02 c     lpkli3_key2   reade     lpkli3
      *
     c                   if        %eof or
     c                             plfirm <> l_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c*                  if        plstat <> *blanks
     c*                  iter
     c*                  endif
      *
     c                   if        p_fsni <> *blanks and
     c                             p_fsni <> plfsni
     c                   iter
     c                   endif
      *
     c                   if        p_fniv <> 0 and
     c                             p_fniv <> plfniv
     c                   iter
     c                   endif
      *
     c                   if        p_sscc <> 0  and
     c                             p_sscc <> plsscc
     c                   iter
     c                   endif
7.01  *
7.01 c                   eval      lodtl1_numm = plnumm
7.01 c                   eval      lodtl1_suff = plsuff
7.01 c                   eval      lodtl1_line = plblin
7.01 c     lodtl1_key    chain     lodtl1
7.01 c                   if        not %found
7.01 c                   eval      ldanta = 0
7.01 c                   eval      ldenh1 = *blanks
7.01 c                   endif
      *
     c                   eval      b1valg = 0
      *
     c                   eval      b1nobb = plnobb
      *
     c                   if        b1nobb = 0  and
     c                             %trim(plgtin) <> *blanks
     c                   move      plgtin        p_eann
     c                   eval      p_stat = *blank
     c                   eval      p_enhe = *blank
     c                   eval      p_nobb = *zero
     c                   eval      p_vare = *blank
      *
     c                   call      'VE717R'
     c                   parm                    w_firm
     c                   parm                    p_eann
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_nobb
     c                   parm                    p_stat
     c                   eval      b1nobb = p_nobb
     c                   endif
      *
     c                   eval      b1mnum = plmnum
     c                   eval      b1pver = plpver
     c                   eval      b1aarr = plaarr
     c                   eval      b1fsni = plfsni
     c                   eval      b1fniv = plfniv
     c                   eval      b1linr = pllinr
     c                   eval      b1sscc = plsscc
     c                   eval      b1navn = plnavn
     c                   if        phpsdt <> *loval
     c     *dmy          move      phpsdt        b1psdt
     c                   endif
7.09 c                   eval      b1bnum = plnumm
      * Finn eventuelt ordrenummer
     c*                  eval      b1onum = 0
7.09 c                   eval      lohel1_numm = plnumm
     c                   eval      lohel1_suff = 0
     c     lohel1_key    chain     lohel1
     c                   if        %found
     c*                  eval      b1onum = loornr
     c                   endif
      *
     c                   eval      b1blin = plblin
7.01 c**                 eval      b1bant = plbant
7.01 c**                 eval      b1benh = plbenh
7.01 c                   eval      b1bant = ldanta
7.01 c                   eval      b1benh = ldenh1
     c                   eval      b1pant = plpant
     c                   eval      b1penh = plpenh
     c                   eval      b1stat = plstat
      *
     c                   eval      w_srrn = w_srrn + 1
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
7.02 c     lpkli3_key2   readpe    lpkli3
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
7.02 c     lpkli3_key    setll     lpkli3
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutiner for kontroll av meldingsstatus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*     status_hode   begsr
      **
     c*                   select
     c*                   when      listat = ' '
     c*                   eval      b1tsta = 'Ok!            '
     c*                   when      listat = '1'
     c*                   eval      b1tsta = 'Feil i best.nr.'
     c*                   when      listat = '2'
     c*                   eval      b1tsta = 'Sjekk lever.   '
     c*                   when      listat = '3'
     c*                   eval      b1tsta = 'Avvik fra best.'
     c*                   when      listat = '4'
     c*                   eval      b1tsta = 'Feil ordretype.'
     c*                   when      listat = '5'
     c*                   eval      b1tsta = 'Avvik totalsum.'
     c*                   when      listat = '7'
     c*                   eval      b1tsta = 'Slettemarkert  '
     c*                   when      listat = '8'
     c*                   eval      b1tsta = 'Manuelt ok     '
     c*                   when      listat = '9'
     c*                   eval      b1tsta = 'Oppdatert      '
     c*                   endsl
      **
     c*                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_aarr
     c                   parm                    p_paid
     c                   parm                    p_pver
     c                   parm                    p_fsni
     c                   parm                    p_fniv
     c                   parm                    p_sscc
      *
      *  Key for posisjonering av pakkseddelinfo
      *
     c     lphoir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lphoir_type
     c                   kfld                    lphoir_mnum
7.02  *
7.02  *  Key for posisjonering av pakkseddelinfo - linjer
7.02  *
7.02 c     lpkli3_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    lpkli3_type
7.02 c                   kfld                    lpkli3_mnum
7.02 c                   kfld                    lpkli3_aarr
7.02 c                   kfld                    lpkli3_paid
7.02 c                   kfld                    lpkli3_pver
7.02 c                   kfld                    lpkli3_numm
7.02 c                   kfld                    lpkli3_suff
7.02 c                   kfld                    lpkli3_blin
7.02  *
7.02  *  Key for posisjonering av pakkseddelinfo - linjer
7.02  *
7.02 c     lpkli3_key2   klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    lpkli3_type
7.02 c                   kfld                    lpkli3_mnum
7.02 c                   kfld                    lpkli3_aarr
7.02 c                   kfld                    lpkli3_paid
7.02 c                   kfld                    lpkli3_pver
      *
      *  Key for posisjonering av pakkseddelinfo - linjer
      *
     c     lpkli1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkli1_type
     c                   kfld                    lpkli1_mnum
     c                   kfld                    lpkli1_aarr
     c                   kfld                    lpkli1_paid
     c                   kfld                    lpkli1_pver
     c                   kfld                    lpkli1_fsni
     c                   kfld                    lpkli1_fniv
7.03 c                   kfld                    lpkli1_sscc
     c                   kfld                    lpkli1_linr
      *
      *  Key for posisjonering av partner
      *
     c     lppai1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lppai1_type
     c                   kfld                    lppai1_mnum
     c                   kfld                    lppai1_aarr
     c                   kfld                    lppai1_paid
     c                   kfld                    lppai1_pver
     c                   kfld                    lppai1_paty
      *
      *  Key for posisjonering av bestilling
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for posisjonering av bestilling
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      *  Key for posisjonering av ordre
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      *  Key for behandling av loggfile
      *
     c     elogl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    elogl3_faar
     c                   kfld                    elogl3_type
     c                   kfld                    elogl3_mnum
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      lpkli1_type = p_type
     c                   eval      lpkli1_mnum = p_mnum
     c                   eval      lpkli1_aarr = p_aarr
     c                   eval      lpkli1_paid = p_paid
     c                   eval      lpkli1_pver = p_pver
     c                   eval      lpkli1_fsni = p_fsni
     c                   eval      lpkli1_fniv = p_fniv
7.02 c                   eval      lpkli3_type = p_type
7.02 c                   eval      lpkli3_mnum = p_mnum
7.02 c                   eval      lpkli3_aarr = p_aarr
7.02 c                   eval      lpkli3_paid = p_paid
7.02 c                   eval      lpkli3_pver = p_pver
7.02 c                   eval      lpkli3_numm = 0
7.02 c                   eval      lpkli3_suff = 0
7.02 c                   eval      lpkli3_blin = 0
      *
     c                   eval      lphoir_type = p_type
     c                   eval      lphoir_mnum = p_mnum
     c     lphoir_key    setll     lphoir
     c     lphoir_key    reade     lphoir
      *
     c                   eval      b2paid = phpaid
      * Finn leverandørnavn
     c                   eval      b2lnav = *blanks
     c                   eval      lppai1_type = phtype
     c                   eval      lppai1_mnum = phmnum
     c                   eval      lppai1_aarr = phaarr
     c                   eval      lppai1_paid = phpaid
     c                   eval      lppai1_pver = phpver
     c                   eval      lppai1_paty = 'SU'
     c     lppai1_key    chain     lppai1
     c                   if        %found
     c                   eval      b2lnav = ppnavn
     c                   endif
      *
     c                   endsr
      *
