     H/TITLE  Postering av lønn til Prosjekt
     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON61P                                     *
      * Program . . . . : IL200R                                        *
      * Beskrivelse . . : Uttrekk/oppdatering                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *                                                                 *
      *       Start på 18.10.2010                                       *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                   Nytt program Okt.- 10                         *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     filtrlb    uf   e           K DISK    rename(iltrpfr:iltrlbr)
     filonl1    if   e           k disk    rename(ilonpfr:ilonl1r)
     filarlu    if   e           k disk    rename(ilarpfr:ilarlur)
     filtrlc    if   e           k disk    rename(iltrpfr:iltrlcr)
     f                                     prefix(x:1)
      *
     frp4ol1    if   e           k disk    rename(rp4opfr:rp4ol1r)
      *
     fraa4l1    uf a e           k disk    rename(raa4pfr:raa4l1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frbunl2    if   e           k disk    rename(rbunpfr:rbunl2r)
     frtralu    uf a e           k disk    rename(rtrapfr:rtralur)
      *
     d wuserx          s             10
     d wperiod         s              4  0
     d  waaaamm        s              6  0
      *
     d wltper          s              6
     d ilonl1_kli      s                   like(ilrkli)
     d ilonl1_lnr      s                   like(ilrlnr)
     d ilarlu_kli      s                   like(ilakli)
     d ilarlu_art      s                   like(ilaart)
     d iltrlb_peri     s                   like(iltper)
     d iltrlb_arbo     s                   like(iltonr)
     d iltrlc_spr      s                   like(iltspr)
     d raa4l1_kli      s                   like(ra4fir)
     d raa4l1_aar      s                   like(ra4aar)
     d rp4ol1_frm      s                   like(rp4frm)
     d rp4ol1_ord      s                   like(rp4ord)
     d rbunl2_niv      s                   like(rsnivo)
     d rbunl2_mem      s                   like(rsmemb)
     d rbunl2_frm      s                   like(rsfirm)
     d rbunl2_aar      s                   like(rsraar)
     d rbunl2_per      s                   like(rsrper)
     d rbunl2_dat      s                   like(rspdat)
     d rbunlu_niv      s                   like(rsnivo)
     d rbunlu_mem      s                   like(rsmemb)
     d rbunlu_frm      s                   like(rsfirm)
     d rbunlu_bun      s                   like(rsbunt)
      *
     d trans2ordre     s              2
     d w_antl          s              9  0 inz(*zero)
     d w_anto          s              9  0 inz(*zero)
     d w_bdat          s               D
     d w_bilk          s              2  0
     d w_biln          s                   like(rtbiln)
     d w_linj          s              6  0
     d w_spr           s                   like(iltspr)
     d w_sum           s                   like(rtbelØ)
     d w_deb           s                   like(rtbelØ)
     d w_kre           s                   like(rtbelØ)
     d w_dat6          s              6  0
     d w_ar2           s              2  0
     d wsprlin         s                   like(iltspr)
      *
     d s_kli           s                   like(iltkli)
     d s_onr           s                   like(iltonr)
     d s_per           s                   like(iltper)
     d s_art           s                   like(iltart)
     d s_avd           s                   like(iltavd)
     d s_kto           s                   like(rtdkto)
     d s_bunt          s              9  0 inz(*zero)
     d s_aar           s              4  0 inz(*zero)
     d s_paar          s              4  0 inz(*zero)
      *
     d w_aar           s              2  0 inz(*zero)
      *
     d                 DS
     d  FRAKEY                 1     15
     d  FRAKLI                 1      6  0
     d  FRALNR                 7     11  0
     d  FRATEL                12     15  0
     d                 DS
     d  WFRA                   1      6  0
     d  WFaa                   1      2  0
     d  WFMM                   3      4  0
     d  WFdd                   5      6  0
     d                 DS
     d  WTIL                   1      6  0
     d  WTaa                   1      2  0
     d  WTMM                   3      4  0
     d  WTdd                   5      6  0
     d                 DS
     d  WFRA1                  1      8  0
     d  WFtt1                  1      2  0
     d  WFaa1                  3      4  0
     d  WFMM1                  5      6  0
     d  WFdd1                  7      8  0
     d                 DS
     d  WTIL1                  1      8  0
     d  WTtt1                  1      2  0
     d  WTaa1                  3      4  0
     d  WTMM1                  5      6  0
     d  WTdd1                  7      8  0
      *
     d                 DS
     d  WDAT                   1      8  0
     d  WDDD                   3      4  0
     d  WDMM                   5      6  0
     d  WDAA                   7      8  0
      *   Brukerkoder til bruk i oppdat av lønn mot ordre.
     d wbrkd           DS
     d  Wakkord                       7P 0
     d  Wxxxx                         5P 0
     d  Wfdat                         7P 0
     d  Wtdat                         7P 0
     d  wper                          4  0
     d  waavsl                        1
     d wbrkdo          DS
     d  Wfdato                 1      4p 0
     d  wpero                  5      8  0
     d  waavslo                9      9
     d  wakkordo              10     13p 0
     d  waktivo               14     18
     d  wprko                 19     20  0
      *   Til dato er fjernet da denne ligger i transdato??
     d wbrkd2          DS
     d  Wtdato                 1      4p 0
      *
     d wktostr         DS
     d  Wtyp                          1
     d  Wkto                          8  0
     d  Wavd                          4  0
     d  Wsps                          4  0
     d  Wnv1                          4  0
     d  Wnv2                          4  0
     d  Wmom                          1
     d  Wpro                          8  0
     d  Wakt                          3
     d  wrest                        13
      *
     d                UDS
     d  Uopdo                108    108
     d  Uopdl                111    111
     d  UPER                 113    116
     d  UAAR                 113    114  0
     d  UMND                 115    116  0
     d  l_user               911    920
     d  l_fgr                931    933
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Finne første
      * Lønnslinje
      *
     c     iltrlb_key    setll     iltrlb
     c                   read      iltrlb
      *
     c                   dow       not %eof(iltrlb)
      *
      *    Følgende transer er luket ut via OMIT i ILTRLB
      *
      *     OLTOVO <> ' '  Alle linjer uten blank i 'overført ordre'
      *     ILTATT  = ' '  Alle linjer som ikke er attestert
      *     ILTONR  = ' '  Alle linjer som ikke har arb.ordre utfyllt
      *
      *    Sjekk om arten har kostpris og konto i ILARPF
      *
     c                   eval      ilarlu_kli = iltkli
     c                   eval      ilarlu_art = iltart
     c     ilarlu_key    chain     ilarlu
     c                   if        not %found(ilarlu) or
     c                             ilakpr = 0         or
     c                             ilakto = 0
     c                   iter
     c                   endif
      *
      *    Sjekk om ny periode, ordre eller art
      *
     c                   if        (iltkli <> s_kli or
     c                              iltper <> s_per or
     c                              iltonr <> s_onr or
     c                              iltart <> s_art) and
     c                             w_sum   <> 0
     c                   exsr      poster
     c                   endif
      *
      *   Ta vare på periode, onr og art for senere postering
      *      og finn neste referansenr(sporingsnr).  Dette følger
      *      posteringen og legges i iltspr på lønnstrans.
      *
     c                   if        (iltkli <> s_kli or
     c                              iltper <> s_per or
     c                              iltonr <> s_onr or
     c                              iltart <> s_art)
     c                   eval      s_per = iltper
     c                   eval      s_onr = iltonr
     c                   eval      s_art = iltart
     c                   eval      s_kli = iltkli
     c                   eval      s_avd = iltavd
     c                   eval      s_kto = ilakto
     c     *ymd          move      iltfda        w_dat6
     c                   movel     w_dat6        w_ar2
     c                   eval      s_aar  = w_ar2 + 2000
     c                   eval      w_dat6 = w_dat6 - w_ar2*10000
     c                   eval      w_ar2  = w_dat6 / 100
     c                   eval      s_per  = w_ar2
     c                   eval      iltrlc_spr = w_aar * 100000 + 99999
     c     iltrlc_key    setll     iltrlc
     c                   readp     iltrlc
     c                   if        %eof(iltrlc)
     c                   eval      w_spr = w_aar * 100000 + 00001
     c                   else
     c                   if        iltrlc_spr > xltspr
     c                   eval      w_spr = w_aar * 100000 + 00001
     c                   else
     c                   eval      w_spr = xltspr + 1
     c                   endif
     c                   endif
     c                   endif
      *
      *
      *   Beregne posteringsbeløp for linja.
      *
     c                   eval      w_sum = w_sum + iltant * ilakpr
      *   Merk lønnslinje med at denne er overført til prosjekt
     c                   eval      iltovo = 'J'
     c                   eval      iltspr = w_spr
      *   Oppdatere lønnslinja i ILTRPF
     c                   update    iltrlbr
      *
      *   Les neste lønnstrans
      *
     c                   read      iltrlb
     c                   enddo
      *
      *   Desom w_sum inneholder noe må siste postering lages.
      *
     c                   if        w_sum <> 0
     c                   exsr      poster
     c                   endif
      *
      *   Slutt
     c                   eval      *inlr = *on
     c                   return
      *
     c
     c     poster        begsr
      *
     c                   eval      rbunl2_niv = *blank
     c                   eval      rbunl2_mem = l_user
     c                   eval      rbunl2_frm = s_kli
     c                   eval      rbunl2_aar = s_aar
     c                   eval      rbunl2_per = s_per
     c     *dmy          move      udate         rbunl2_dat
     c     rbunl2_key    chain     rbunl2
     c                   if        not %found(rbunl2)
     c                   eval      rbunlu_niv = *blank
     c                   eval      rbunlu_mem = l_user
     c                   eval      rbunlu_frm = s_kli
     c                   eval      rbunlu_bun = 1
     c     rbunlu_key    chain     rbunlu
     c                   dow       %found(rbunlu)
     c                   eval      rbunlu_bun = rbunlu_bun + 1
     c     rbunlu_key    chain     rbunlu
     c                   enddo
     c                   eval      rsnivo = *blank
     c                   eval      rsmemb = l_user
     c                   eval      rsfirm = s_kli
     c                   eval      rsbunt = rbunlu_bun
     c                   eval      rsreid = *blank
     c                   eval      rsstat = *blank
     c                   eval      rsraar = s_aar
     c                   eval      rsrper = s_per
     c                   eval      rsfØrs = *zero
     c                   eval      rssist = *zero
     c                   time                    RSPDAT
     c                   eval      rsbsum = *zero
     c                   eval      rspsum = *zero
     c                   eval      rspdeb = *zero
     c                   eval      rspkre = *zero
     c                   time                    RSEDAT
     c                   time                    RSETIM
     c                   eval      RSEUSR = l_user
     c                   write     rbunlur
     c                   endif
      *
      *   Finn bilagsnr
      *
     c                   if        s_aar  <> s_paar
     c                   eval      raa4l1_kli = s_kli
     c                   eval      raa4l1_aar = s_aar
     c     raa4l1_key    chain     raa4l1
     c                   if        %found(raa4l1)
     c                   eval      w_biln = ra4sgb + 1
     c                   else
     c                   eval      w_biln = 1
     c                   endif
     c                   if        %found(raa4l1)
     c                   eval      ra4sgb = w_biln
     c                   update    raa4l1r
     c                   else
     c                   eval      ra4fir = s_kli
     c                   eval      ra4aar = s_aar
     c                   write     raa4l1r
     c                   endif
     c                   eval      s_paar = s_aar
     c                   endif
      *
      *   Postering på prosjekt
      *
     c                   eval      rtbiln = w_biln
     c                   eval      rtnivo = ' '
     c                   eval      rtmemb = l_user
     c                   eval      rtfirm = s_kli
     c                   eval      rtbunt = rsbunt
     c                   eval      w_linj = w_linj + 1
     c                   eval      rtlinj = w_linj
     c                   eval      rtreid = ' '
     c                   eval      rtstat = ' '
     c                   eval      rtbdat = w_bdat
     c                   eval      rtbelØ = w_sum
     c                   eval      rtgmva = *zero
     c                   eval      rtraar = s_aar
     c                   eval      rtrper = s_per
     c                   eval      rtbill = *zero
     c                   if        w_sum > 0
     c                   eval      rtdavd = s_avd
     c                   eval      rtdkto = s_kto
     c                   eval      rtdsps = *zero
     c                   eval      rtcavd = *zero
     c                   eval      rtckto = *zero
     c                   eval      rtcsps = *zero
     c                   eval      w_deb  = w_deb + w_sum
     c                   else
     c                   eval      rtdavd = *zero
     c                   eval      rtdkto = *zero
     c                   eval      rtdsps = *zero
     c                   eval      rtcavd = s_avd
     c                   eval      rtckto = s_kto
     c                   eval      rtcsps = *zero
     c                   eval      w_kre  = w_kre - w_sum
     c                   endif
     c                   eval      rp4ol1_frm = s_kli
     c                   eval      rp4ol1_ord = s_onr
     c     rp4ol1_key    chain     rp4ol1
     c                   if        %found(rp4ol1)
     c                   eval      rtpros = rp4pro
     c                   eval      rtupro = rp4upr
     c                   eval      rtakti = rp4akt
     c                   endif
     c                   eval      rtdmva = '0'
     c                   eval      rtcmva = '0'
     c                   eval      rtarbo = iltonr
     c                   eval      rtrefn = *zero
     c                   eval      rtfdat = *loval
     c                   eval      rtattu = iltatt
     c                   eval      rtbilk = w_bilk
     c                   eval      rttext = 'Prosjektkost'
     c                   eval      rtedat = iltad1
     c                   eval      rtetim = *loval
     c                   eval      rteusr = iltatt
     c                   write     rtralur
      *
      *   Mot-Postering uten prosjekt
      *
     c                   eval      rtbiln = w_biln
     c                   eval      rtnivo = ' '
     c                   eval      rtmemb = l_user
     c                   eval      rtfirm = s_kli
     c                   eval      rtbunt = rsbunt
     c                   eval      w_linj = w_linj + 1
     c                   eval      rtlinj = w_linj
     c                   eval      rtreid = ' '
     c                   eval      rtstat = ' '
     c                   eval      rtbdat = w_bdat
     c                   eval      rtbelØ = w_sum
     c                   eval      rtgmva = *zero
     c                   eval      rtraar = s_aar
     c                   eval      rtrper = s_per
     c                   eval      rtbill = *zero
     c                   if        w_sum > 0
     c                   eval      rtdavd = *zero
     c                   eval      rtdkto = *zero
     c                   eval      rtdsps = *zero
     c                   eval      rtcavd = s_avd
     c                   eval      rtckto = s_kto
     c                   eval      rtcsps = *zero
     c                   eval      w_deb  = w_deb + w_sum
     c                   else
     c                   eval      rtdavd = s_avd
     c                   eval      rtdkto = s_kto
     c                   eval      rtdsps = *zero
     c                   eval      rtcavd = *zero
     c                   eval      rtckto = *zero
     c                   eval      rtcsps = *zero
     c                   eval      w_deb  = w_deb - w_sum
     c                   endif
     c*                  eval      rp4ol1_frm = s_kli
     c*                  eval      rp4ol1_ord = s_onr
     c*    rp4ol1_key    chain     rp4ol1
     c*                  if        %found(rp4ol1)
     c                   eval      rtpros = *blank
     c                   eval      rtupro = *blank
     c                   eval      rtakti = *blank
     c*                  endif
     c                   eval      rtdmva = '0'
     c                   eval      rtcmva = '0'
     c                   eval      rtarbo = *blank
     c                   eval      rtrefn = *zero
     c                   eval      rtfdat = *loval
     c                   eval      rtattu = iltatt
     c                   eval      rtbilk = w_bilk
     c                   eval      rttext = 'Prosjektkost'
     c                   eval      rtedat = iltad1
     c                   eval      rtetim = *loval
     c                   eval      rteusr = iltatt
     c                   write     rtralur
      *
      *   Oppdater bunt
      *
     c                   eval      rbunlu_niv = *blank
     c                   eval      rbunlu_mem = l_user
     c                   eval      rbunlu_frm = s_kli
     c                   eval      rbunlu_bun = rsbunt
     c     rbunlu_key    chain     rbunlu
     c                   eval      rspdeb = w_deb
     c                   eval      rspkre = w_kre
     c                   eval      w_deb = *zero
     c                   eval      w_kre = *zero
     c                   update    rbunlur
      *
     c                   endsr
      *
      *----------------------------------------------------------------
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *INZSR        BEGSR
      *
      * Key for file iltrlb nummerisk
      *
     c     iltrlb_key    KLIST
     c                   KFLD                    iltrlb_peri
     c                   KFLD                    iltrlb_arbo
      *
      * Key for og finne sist brukte sporingslinjenr.
      *
     c     iltrlc_key    KLIST
     c                   KFLD                    iltrlc_spr
      *
      * Key for oppslag på lønnstager i hovedfilgruppe
      *
     c     ilonl1_key    KLIST
     c                   KFLD                    ilonl1_kli
     c                   KFLD                    ilonl1_lnr
      *
      * Key for oppslag på lønnsart i hovedfilgruppe
      *
     c     ilarlu_key    KLIST
     c                   KFLD                    ilarlu_kli
     c                   KFLD                    ilarlu_art
      *
      * Key for oppslag på bilagsnr i Nexstep
      *
     c     raa4l1_key    KLIST
     c                   KFLD                    raa4l1_kli
     c                   KFLD                    raa4l1_aar
      *
      * Key for oppslag på arb.ordre for å finne prosjekt, underp. og aktiv.
      *
     c     rp4ol1_key    KLIST
     c                   KFLD                    rp4ol1_frm
     c                   KFLD                    rp4ol1_ord
      *
      * Key for å finne bunt på denne dato
      *
     c     rbunl2_key    KLIST
     c                   KFLD                    rbunl2_niv
     c                   KFLD                    rbunl2_mem
     c                   KFLD                    rbunl2_frm
     c                   KFLD                    rbunl2_aar
     c                   KFLD                    rbunl2_per
     c                   KFLD                    rbunl2_dat
      *
      * Key for å oppdater bunt
      *
     c     rbunlu_key    KLIST
     c                   KFLD                    rbunlu_niv
     c                   KFLD                    rbunlu_mem
     c                   KFLD                    rbunlu_frm
     c                   KFLD                    rbunlu_bun
      *
     c     *dmy          move      udate         w_bdat
     c                   eval      w_bilk = 11
      *
     c                   ENDSR
      *
