     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JR120R
      * Beskrivelse . . : Rapp. varegrupperef, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170498 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvglr    if   e           k disk    rename(jrvgpfr:jrvglrr)
     fjrvglu    uf a e           k disk    rename(jrvgpfr:jrvglur)
     fjrvgl1    if   e           k disk    rename(jrvgpfr:jrvglr1)
     fjrvgl2    if   e           k disk    rename(jrvgpfr:jrvgl2r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
      *
     fjr120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jrgfir)
     d p_rapp          s                   like(jrgrap)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jrvglu_gvgr     s                   like(jrgvgr)
     d jrvglr_gvgr     s                   like(jrgvgr)
     d jrvgl1_gvgr     s                   like(jrgvgr)
     d jrvgl2_gbes     s                   like(jrgbes)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jrapl1_rapp     s                   like(jrrapp)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(jrgfir)
     d w_rapp          s                   like(jrgrap)
     d w_uogr          s                   like(vguogr)
     d w_uhgr          s                   like(vguhgr)
     d w_uugr          s                   like(vguugr)
      *
     d s_gvgr          s                   like(jrgvgr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2gvgr <> *blank or
     c                             b2gbes <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      jrvgl2_gbes = b1gbes
     c     jrvgl2_key    setll     jrvgl2
     c                   other
     c                   eval      jrvgl1_gvgr = b1gvgr
     c     jrvgl1_key    setll     jrvgl1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varegruppe
      *
     c                   if        b2gvgr <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jrvgl1_gvgr = b2gvgr
     c     jrvgl1_key    setll     jrvgl1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2gbes <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      jrvgl2_gbes = b2gbes
     c     jrvgl2_key    setll     jrvgl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2gvgr = *blank
     c                   eval      b2gbes = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1grap = b2grap
     c                   eval      c1gvgr = b1gvgr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1grap = b2grap
     c                   eval      k1gvgr = b1gvgr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1grap = b2grap
     c                   eval      d1gvgr = b1gvgr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1grap = b2grap
     c                   eval      c1gvgr = b1gvgr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1grap = b2grap
     c                   eval      c1gvgr = b2gvgr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1gvgr = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jrvglr_gvgr = c1gvgr
     c     jrvglr_key    chain     jrvglr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1gvgr = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2gvgr = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jrvglr_gvgr = c1gvgr
     c     jrvglr_key    chain     jrvglr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2gbes = jrgbes
     c                   eval      c2gogr = jrgogr
     c                   eval      c2ghgr = jrghgr
     c                   eval      c2gugr = jrgugr
     c                   eval      c2gesi = jrgesi
     c                   eval      c2geda = 0
     c                   if        jrgeda <> *loval
     c     *dmy          move      jrgeda        c2geda
     c                   endif
     c                   else
     c                   eval      c2gbes = *blank
     c                   eval      c2gogr = 0
     c                   eval      c2ghgr = 0
     c                   eval      c2gugr = 0
     c                   eval      c2geda = 0
     c                   eval      c2gesi = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1gvgr = k1gvgr
     c                   eval      jrvglr_gvgr = k1gvgr
     c                   endif
      *
      * Henter beskrivelse av undergruppe
      *
     c                   eval      c2utxt = *blank
      *
     c                   if        c2gugr <> 0
     c                   eval      vugrl1_uogr = c2gogr
     c                   eval      vugrl1_uhgr = c2ghgr
     c                   eval      vugrl1_uugr = c2gugr
     c     vugrl1_key    chain     vugrl1                             91
     c                   eval      c2utxt = vgutxt
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2GOGR'
     c                   eval      c2ghgr = 0
     c                   eval      c2gugr = 0
     c                   eval      c2utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c2gogr
     c                   parm                    c2utxt
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2GHGR'
     c                   eval      c2gugr = 0
     c                   eval      c2utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c2gogr
     c                   parm                    c2ghgr
     c                   parm                    c2utxt
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2GUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c2gogr
     c                   parm                    c2ghgr
     c                   parm                    c2gugr
     c                   parm                    c2utxt
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2gbes = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2gogr = 0 or                                Betingelse 1
     c                             c2ghgr = 0 or                                Betingelse 1
     c                             c2gugr = 0                                   Betingelse 1
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      vugrl1_uogr = c2gogr
     c                   eval      vugrl1_uhgr = c2ghgr
     c                   eval      vugrl1_uugr = c2gugr
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   clear                   jrvglur
      *
     c                   eval      jrvglu_gvgr = c1gvgr
     c     jrvglu_key    chain     jrvglur                            90
      *
     c                   eval      jrgbes = c2gbes
     c                   eval      jrgogr = c2gogr
     c                   eval      jrghgr = c2ghgr
     c                   eval      jrgugr = c2gugr
     c                   eval      jrgesi = l_user
     c                   time                    jrgeda
     c                   time                    jrgeti
      *
     c                   if        *in90 = *off
     c                   update    jrvglur
     c                   else
     c                   eval      jrgfir = w_firm
     c                   eval      jrgrap = w_rapp
     c                   eval      jrgvgr = c1gvgr
     c                   write     jrvglur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1gbes = jrgbes
     c                   eval      b1gogr = jrgogr
     c                   eval      b1ghgr = jrghgr
     c                   eval      b1gugr = jrgugr
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jrvglr_gvgr = d1gvgr
     c     jrvglr_key    chain     jrvglr                             90
      *
     c                   eval      d1gbes = jrgbes
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      jrvglu_gvgr = d1gvgr
     c     jrvglu_key    chain     jrvglur                            90
     c                   delete    jrvglur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_gvgr = k1gvgr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jrvglr_gvgr = k1gvgr
     c     jrvglr_key    chain     jrvglr                             90
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1gvgr = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jrvglr_gvgr = k1gvgr
     c     jrvglr_key    chain     jrvglr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1gvgr = s_gvgr
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = 'B'
     c                   read      jrvgl2                                 15
     c                   else
     c                   read      jrvgl1                                 15
     c                   endif
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        jrgfir <> p_firm or
     c                             jrgrap <> p_rapp
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      jrvgl2_gbes = jrgbes
     c                   other
     c                   eval      jrvgl1_gvgr = jrgvgr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1gvgr = jrgvgr
     c                   eval      b1gbes = jrgbes
     c                   eval      b1gogr = jrgogr
     c                   eval      b1ghgr = jrghgr
     c                   eval      b1gugr = jrgugr
     c                   eval      b1gbe2 = *blank
      *
     c                   eval      vugrl1_uogr = jrgogr
     c                   eval      vugrl1_uhgr = jrghgr
     c                   eval      vugrl1_uugr = jrgugr
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b1gbe2 = vgutxt
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     jrvgl2_key    setgt     jrvgl2
     c                   readp     jrvgl2
     c                   other
     c     jrvgl1_key    setgt     jrvgl1
     c                   readp     jrvgl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     jrvgl2
     c                   other
     c                   readp     jrvgl1
     c                   endsl
      *
     c                   if        %eof or
     c                             jrgfir <> w_firm or
     c                             jrgrap <> w_rapp
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      jrvgl2_gbes = jrgbes
     c                   other
     c                   eval      jrvgl1_gvgr = jrgvgr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     jrvgl2_key    setll     jrvgl2
     c                   other
     c     jrvgl1_key    setll     jrvgl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_rapp
      *
      * Key for les på rapportør varegruppereferanse
      *
     c     jrvglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_rapp
     c                   kfld                    jrvglr_gvgr
      *
      * Key for oppdatering av rapportør varegruppereferanse
      *
     c     jrvglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_rapp
     c                   kfld                    jrvglu_gvgr
      *
      * Key for posisjonering på varegruppe
      *
     c     jrvgl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_rapp
     c                   kfld                    jrvgl1_gvgr
      *
      * Key for posisjonering på beskrivelse
      *
     c     jrvgl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_rapp
     c                   kfld                    jrvgl2_gbes
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      *
      * Henter firma og rapportør fra parameter, og fyller opp subfile
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_rapp = p_rapp
     c                   eval      b2grap = p_rapp
      *
     c                   eval      jrapl1_rapp = p_rapp
     c     jrapl1_key    chain     jrapl1                             90
     c                   eval      b2rnav = jrnavn
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jrvgl1_gvgr = *blank
     c     jrvgl1_key    setll     jrvgl1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
