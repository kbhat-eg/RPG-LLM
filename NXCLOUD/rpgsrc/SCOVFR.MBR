      /title  Overføring av budsjett til økonomi
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SCOVFR
      * Beskrivelse . . : Overføring av budsjett til økonomi
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.40 070405 KOB  Nytt program
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fslbul1    if   e           k disk    rename(slbupfr:slbul1r)
      *
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
      *
      * Definering av parameter (inn)
      *
     d                 ds
     d wsparm                        64
     d  wsfirm                        3  0 overlay(wsparm:01)
     d  wshhaa                        4  0 overlay(wsparm:04)
     d  wsvers                        2    overlay(wsparm:08)
     d  wsakod                       12    overlay(wsparm:10)
     d  wsaabb                        1    overlay(wsparm:22)
     d  wskode                       12    overlay(wsparm:23)
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
     d  d_hele                 1     88
     d  d_kav1                 1      4  0
     d  d_kko1                 5     10  0
     d  d_ksp1                11     12  0
     d  d_kav2                13     16  0
     d  d_kko2                17     22  0
     d  d_ksp2                23     24  0
     d  d_kav3                25     28  0
     d  d_kko3                29     34  0
     d  d_ksp3                35     36  0
     d  d_kav4                37     40  0
     d  d_kko4                41     46  0
     d  d_ksp4                47     48  0
     d  d_kav5                49     52  0
     d  d_kko5                53     58  0
     d  d_ksp5                59     60  0
     d  d_kav6                61     64  0
     d  d_kko6                65     70  0
     d  d_ksp6                71     72  0
     d  d_kav7                73     76  0
     d  d_kko7                77     82  0
     d  d_ksp7                83     84  0
     d  d_khnv                85     88
      *
      * Definering av variable i nøkler
      *
     d slbul1_hhaa     s                   like(schhaa)
     d slbul1_vers     s                   like(scvers)
     d slbul1_kode     s                   like(sckode)
     d slbul1_aabb     s                   like(scaabb)
      *
     d slbul1_avde     s                   like(scavde)
     d slbul1_lage     s                   like(sclage)
     d slbul1_otyp     s                   like(scotyp)
     d slbul1_land     s                   like(scland)
     d slbul1_dist     s                   like(scdist)
     d slbul1_selg     s                   like(scselg)
     d slbul1_kkat     s                   like(sclkat)
     d slbul1_kund     s                   like(scldor)
     d slbul1_ogrp     s                   like(scogrp)
     d slbul1_hgrp     s                   like(schgrp)
     d slbul1_ugrp     s                   like(scugrp)
     d slbul1_vare     s                   like(scvare)
      *
      *
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
      *
      * Definering av variable
      *
     d w_firm          s                   like(scfirm)
      *
     d                uds
6.10 d l_user                911    920
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XINIT  Initisiering av utskriften                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   eval      slbul1_avde = *zero
     c                   eval      slbul1_lage = *zero
     c                   eval      slbul1_otyp = *blank
     c                   eval      slbul1_land = *blank
     c                   eval      slbul1_dist = *zero
     c                   eval      slbul1_selg = *zero
     c                   eval      slbul1_kkat = *zero
     c                   eval      slbul1_kund = *zero
     c                   eval      slbul1_ogrp = *zero
     c                   eval      slbul1_hgrp = *zero
     c                   eval      slbul1_ugrp = *zero
     c                   eval      slbul1_vare = *blank
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XSLETT Slett budjsett i konto-register                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   eval      slbul1_kode = wskode
     c     slbul1_key    setll     slbul1r
      *
      * Les ...
      *
     c     xslett        tag
     c                   read      slbul1r                                10
     c     *in10         cabeq     *on           xgener
      *
      * Sjekk, Hopp ut dersom ikke riktig
      *
     c     scfirm        cabne     w_firm        xgener
     c     schhaa        cabne     slbul1_hhaa   xgener
     c     scvers        cabne     slbul1_vers   xgener
     c     sckode        cabne     slbul1_kode   xgener
     c     scaabb        cabne     slbul1_aabb   xgener
      *
      * Har funnet en record som tilfredstiller utvalget
      * Skal nå finne kontoen til denne recorden
      *
     c                   exsr      hntkon
      *
     c                   move      d_kko1        rhsalu_kont
     c                   move      d_ksp1        rhsalu_spes
     c                   move      d_kav1        rhsalu_avde
     c     rhsalu_key    chain     rhsalur                            12
      *
     c     *in12         ifeq      *off
     c                   delete    rhsalur
     c                   endif
      *
      * Returner til nytt les
      *
     c                   goto      xslett
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XGENER Generering av nye linjer                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xgener        tag
      *
     c                   eval      slbul1_kode = wskode
     c     slbul1_key    setll     slbul1r
      *
     c     xstart        tag
     c                   read      slbul1r                                10
     c     *in10         cabeq     *on           xslutt
      *
      * Sjekk, Hopp ut dersom ikke riktig
      *
     c     scfirm        cabne     w_firm        xslutt
     c     schhaa        cabne     slbul1_hhaa   xslutt
     c     scvers        cabne     slbul1_vers   xslutt
     c     sckode        cabne     slbul1_kode   xslutt
     c     scaabb        cabne     slbul1_aabb   xslutt
      *
      * Har funnet en record som tilfredstiller utvalget
      * Skal nå finne kontoen til denne recorden
      *
     c                   exsr      hntkon
      *
     c                   move      d_kko1        rhsalu_kont
     c                   move      d_ksp1        rhsalu_spes
     c                   move      d_kav1        rhsalu_avde
     c     rhsalu_key    chain     rhsalur                            12
      *
      * Dersom ny, nullstiller felter
      *
     c     *in12         ifeq      *on
     c                   move      *zero         risa01
     c                   move      *zero         risa02
     c                   move      *zero         risa03
     c                   move      *zero         risa04
     c                   move      *zero         risa05
     c                   move      *zero         risa06
     c                   move      *zero         risa07
     c                   move      *zero         risa08
     c                   move      *zero         risa09
     c                   move      *zero         risa10
     c                   move      *zero         risa11
     c                   move      *zero         risa12
     c                   move      *zero         risa13
     c                   move      *zero         risa00
     c                   endif
      *
      * Legger inn verdier fra statistikk-budsjett
      *
     c                   sub       scbu01        risa01
     c                   sub       scbu02        risa02
     c                   sub       scbu03        risa03
     c                   sub       scbu04        risa04
     c                   sub       scbu05        risa05
     c                   sub       scbu06        risa06
     c                   sub       scbu07        risa07
     c                   sub       scbu08        risa08
     c                   sub       scbu09        risa09
     c                   sub       scbu10        risa10
     c                   sub       scbu11        risa11
     c                   sub       scbu12        risa12
      *
6.10 c                   time                    riedat
6.10 c                   time                    rietim
6.10 c                   eval      rieusr = l_user
      *
     c     *in12         ifeq      *off
     c                   update    rhsalur
     c                   else
      *
     c                   move      w_firm        rifirm
     c                   move      rhsalu_kont   rikont
     c                   move      rhsalu_spes   rispes
     c                   move      rhsalu_avde   riavde
     c                   move      rhsalu_raar   riraar
     c                   move      rhsalu_type   ritype
      *
6.10 c                   time                    riodat
6.10 c                   time                    riotim
6.10 c                   eval      riousr = l_user
      *
     c                   write     rhsalur
     c                   endif
      *
      * Returner til nytt les
      *
     c                   goto      xstart
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter avdeling/konto/spesifikasjon                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     HNTKON        begsr
      *
      *  Legger ut parametere for konto-henvisnings-program
      *
     c                   move      *blank        w_stat
     c                   move      scogrp        w_ogrp
     c                   move      schgrp        w_hgrp
     c                   move      scugrp        w_ugrp
     c                   move      scvare        w_vare
     c                   move      *zero         w_hele
      *
      *  Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_stat
     c                   parm                    w_ltyp
     c                   parm                    w_otyp
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_vare
     c                   parm                    w_hele
     c                   move      w_hele        d_hele
      *
      *  Overstyrer avdeling dersom denne
      *  finnes på ordrehode
      *
     c     scavde        ifgt      *zero
     c                   z-add     scavde        d_kav1
     c                   z-add     scavde        d_kav2
     c                   z-add     scavde        d_kav3
     c                   z-add     scavde        d_kav4
     c                   z-add     scavde        d_kav5
     c                   z-add     scavde        d_kav6
     c                   z-add     scavde        d_kav7
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     csr   *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   move      *blank        w_stat            1
     c                   move      *zero         w_ogrp            2 0
     c                   move      *zero         w_hgrp            2 0
     c                   move      *zero         w_ugrp            3 0
     c                   move      *blank        w_vare           15
3.14 c                   move      *zero         w_hele           88
     c                   move      *blank        w_otyp            2
     c                   move      *zero         w_ltyp            1 0
      *
      * Key for saldomatrise via kontonummer - oppdatering
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for oppdatering av budsjett-register
      *
     c     slbul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    slbul1_hhaa
     c                   kfld                    slbul1_vers
     c                   kfld                    slbul1_kode
     c                   kfld                    slbul1_aabb
     c                   kfld                    slbul1_avde
     c                   kfld                    slbul1_lage
     c                   kfld                    slbul1_otyp
     c                   kfld                    slbul1_land
     c                   kfld                    slbul1_dist
     c                   kfld                    slbul1_selg
     c                   kfld                    slbul1_kkat
     c                   kfld                    slbul1_kund
     c                   kfld                    slbul1_ogrp
     c                   kfld                    slbul1_hgrp
     c                   kfld                    slbul1_ugrp
     c                   kfld                    slbul1_vare
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    dsparm           64
     c                   move      dsparm        wsparm
      *
      *  Legg inn verdier
      *
     c                   eval      w_firm      = wsfirm
     c                   eval      slbul1_hhaa = wshhaa
     c                   eval      slbul1_vers = wsvers
     c                   eval      slbul1_aabb = wsaabb
      *
     c                   eval      rhsalu_raar = wshhaa
     c                   eval      rhsalu_type = 3
      *
     csr                 endsr
