     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG230R
      * Beskrivelse . . : Avviks prosenter gj.kostpris, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240521 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkaiu    uf a e           k disk    rename(vgkast:vgkaiur)
     fvgkai1    if   e           k disk    rename(vgkast:vgkai1r)
     fvgkair    if   e           k disk    rename(vgkast:vgkairr)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvg230d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
      *
     d vgkaiu_aogr     s                   like(vgaogr)
     d vgkaiu_ahgr     s                   like(vgahgr)
     d vgkaiu_augr     s                   like(vgaugr)
     d vgkaiu_aldo     s                   like(vgaldo)
     d vgkaiu_amod     s                   like(vgamod)
     d vgkaiu_aart     s                   like(vgaart)
      *
     d vgkair_aogr     s                   like(vgaogr)
     d vgkair_ahgr     s                   like(vgahgr)
     d vgkair_augr     s                   like(vgaugr)
     d vgkair_aldo     s                   like(vgaldo)
     d vgkair_amod     s                   like(vgamod)
     d vgkair_aart     s                   like(vgaart)
      *
     d vgkai1_aogr     s                   like(vgaogr)
     d vgkai1_ahgr     s                   like(vgahgr)
     d vgkai1_augr     s                   like(vgaugr)
     d vgkai1_aldo     s                   like(vgaldo)
     d vgkai1_amod     s                   like(vgamod)
     d vgkai1_aart     s                   like(vgaart)
      *
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
     d rlevl1_levr     s                   like(rllevr)
      *
     d jmodl1_modn     s                   like(jumodn)
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d c2ltxt          s             30
     d w_tek1          s             35
     d w_gtxt          s             35
     d w_vare          s             15
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_inlr          s              1
     d b_forn          s              1    inz(*off)
     d b_fnul          s              1    inz(*off)
     d b_oprt          s              1    inz(*off)
      *
     d w_firm          s                   like(vgafrm)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_ffld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sok
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oprt = *on
     c                   exsr      xc2bld
     c                   eval      b_oprt = *off
     c                   exsr      forny
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c*                  if        b2ldor <> 0
     c*                  exsr      posisjoner
     c*                  goto      b2tagb
     c*                  endif
      *
     c*                  if        b2lage <> w_lage
     c*                  exsr      posisjoner
     c*                  goto      b2tagb
     c*                  endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      vgkai1_aogr = 0
     c                   eval      vgkai1_ahgr = 0
     c                   eval      vgkai1_augr = 0
     c                   eval      vgkai1_aldo = 0
     c                   eval      vgkai1_amod = 0
     c                   eval      vgkai1_aart = 0
     c     vgkai1_key    setll     vgkai1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på leverandør
      *
     c*                  if        b2ldor <> 0 or
     c*                            b2lage <> 0
     c*                  eval      w_seqe = *blank
     c*                  eval      vgkai1_flev = b2ldor
     c*                  eval      vgkai1_lage = b2lage
     c*                  eval      w_lage = b2lage
      *
     c*    vgkai1_key    setll     vgkai1
     c*                  goto      end_pos
     c*                  endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      *in31 = *on
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Test
      *
     c                   if        b1valg = 9
     c                   eval      *in31 = *on
     c                   eval      *in30 = *on
     c                   exsr      xb9win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   if        b_oprt = *on
     c                   eval      c2aogr = 0
     c                   eval      c2ahgr = 0
     c                   eval      c2augr = 0
     c                   eval      c2aldo = 0
     c                   eval      c2amod = 0
     c                   eval      c2aart = 0
     c                   eval      c2apro = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   eval      c2otxt = *blank
     c                   eval      c2htxt = *blank
     c                   eval      c2utxt = *blank
     c                   eval      c2ltxt = *blank
     c                   eval      c2mtxt = *blank
     c                   eval      c2vtxt = *blank
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      vgkair_aogr = b1aogr
     c                   eval      vgkair_ahgr = b1ahgr
     c                   eval      vgkair_augr = b1augr
     c                   eval      vgkair_aldo = b1aldo
     c                   eval      vgkair_amod = b1amod
     c                   eval      vgkair_aart = b1aart
     c     vgkair_key    chain     vgkair
      *
     c                   if        %found
      *
     c                   eval      c2aogr = vgaogr
     c                   eval      c2ahgr = vgahgr
     c                   eval      c2augr = vgaugr
     c                   eval      c2aldo = vgaldo
     c                   eval      c2amod = vgamod
     c                   eval      c2apro = vgapro
     c                   eval      c2aart = vgaart
     c                   eval      c2eusr = vgausr
     c     *dmy          move      vgadat        c2edat
     c                   exsr      hent_txt
     c                   else
     c                   eval      c2aogr = 0
     c                   eval      c2ahgr = 0
     c                   eval      c2augr = 0
     c                   eval      c2aldo = 0
     c                   eval      c2amod = 0
     c                   eval      c2aart = 0
     c                   eval      c2apro = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   eval      c2otxt = *blank
     c                   eval      c2htxt = *blank
     c                   eval      c2utxt = *blank
     c                   eval      c2ltxt = *blank
     c                   eval      c2mtxt = *blank
     c                   eval      c2vtxt = *blank
      *
     c                   endif
      *
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
      * Overgruppe
      *
     c                   if        w_afld = 'C2AOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c2aogr
     c                   parm                    w_gtxt
     c                   eval      c2otxt = w_gtxt
     c                   goto      c2taga
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'C2AHGR'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c2aogr
     c                   parm                    c2ahgr
     c                   parm                    w_gtxt
     c                   eval      c2htxt = w_gtxt
     c                   goto      c2taga
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'C2AUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c2aogr
     c                   parm                    c2ahgr
     c                   parm                    c2augr
     c                   parm                    w_gtxt
     c                   eval      c2utxt = w_gtxt
     c                   goto      c2taga
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'C2ALDO'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c2aldo
     c                   parm                    c2ltxt
     c                   goto      c2taga
     c                   endif
      *
      * Modul
      *
     c                   if        w_afld = 'C2AMOD'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    c2amod
     c                   parm                    c2mtxt
     c                   goto      c2taga
     c                   endif
      *
      * Varenummer
     c                   if        w_afld = 'C2AART'
     c                   movel     c2aart        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        c2aart
     c                   eval      c2vtxt = w_tek1
     c                   goto      c2taga
     c                   endif
      *
      *
     c                   endif
      *
      * Validering av input
      *
      * Feilindikatorer off
      *
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
     c                   eval      *in46 = *off
     c                   eval      *in47 = *off
      *
      * Test om overgruppe finnes
      *
     c                   if        c2aogr <> 0 and
     c                             c2ahgr = 0
     c                   eval      vogrl1_oogr = c2aogr
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   eval      *in41 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   eval      c2otxt = vgotxt
     c                   endif
      *
      * Test om hovedgruppe finnes
      *
     c                   if        c2ahgr <> 0 and
     c                             c2augr = 0
     c                   eval      vhgrl1_hogr = c2aogr
     c                   eval      vhgrl1_hhgr = c2ahgr
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   eval      *in42 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   eval      c2htxt = vghtxt
     c                   endif
      *
      * Test om undergruppe finnes
      *
     c                   if        c2augr <> 0
     c                   eval      vugrl1_uogr = c2aogr
     c                   eval      vugrl1_uhgr = c2ahgr
     c                   eval      vugrl1_uugr = c2augr
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   eval      *in43 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2utxt = vgutxt
     c                   endif
     c                   endif
      *
      *  Sjekk om leverandøren finnes
      *
     c                   if        c2aldo <> 0
     c                   eval      rlevl1_levr = c2aldo
      *
     c     rlevl1_key    chain     rlevl1r
     c                   if        not %found
     c                   eval      *in44 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2ltxt = rlnavn
     c                   endif
     c                   endif
      *
      * Test om modulnummer  finnes
      *
     c                   if        c2amod <> 0
     c                   eval      jmodl1_modn = c2amod
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   eval      *in45 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2mtxt = jumte1
     c                   endif
     c                   endif
      *
      * Test om varenummer  finnes
      *
     c                   if        c2aart <> 0
     c                   movel     c2aart        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in46 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2vtxt = vvtek1
     c                   endif
     c                   endif
      *
      * Prosent må fylles ut
      *
     c                   if        c2apro = 0
     c                   eval      *in47 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      vgkaiu_aogr = c2aogr
     c                   eval      vgkaiu_ahgr = c2ahgr
     c                   eval      vgkaiu_augr = c2augr
     c                   eval      vgkaiu_aldo = c2aldo
     c                   eval      vgkaiu_amod = c2amod
     c                   eval      vgkaiu_aart = c2aart
     c     vgkaiu_key    chain     vgkaiu
      *
     c                   if        %found
     c                   eval      vgaogr = c2aogr
     c                   eval      vgahgr = c2ahgr
     c                   eval      vgaugr = c2augr
     c                   eval      vgaldo = c2aldo
     c                   eval      vgamod = c2amod
     c                   eval      vgaart = c2aart
     c                   eval      vgapro = c2apro
     c                   time                    vgadat
     c                   eval      vgausr = l_user
     c                   update    vgkaiur
     c                   else
     c                   eval      vgafrm = w_firm
     c                   eval      vgaogr = c2aogr
     c                   eval      vgahgr = c2ahgr
     c                   eval      vgaugr = c2augr
     c                   eval      vgaldo = c2aldo
     c                   eval      vgamod = c2amod
     c                   eval      vgaart = c2aart
     c                   eval      vgapro = c2apro
     c                   time                    vgadat
     c                   eval      vgausr = l_user
     c                   write     vgkaiur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_fnul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_fnul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vgkaiu_aogr = b1aogr
     c                   eval      vgkaiu_ahgr = b1ahgr
     c                   eval      vgkaiu_augr = b1augr
     c                   eval      vgkaiu_aldo = b1aldo
     c                   eval      vgkaiu_amod = b1amod
     c                   eval      vgkaiu_aart = b1aart
     c     vgkaiu_key    chain     vgkaiu
      *
     c     d1tagb        tag
      *
     c                   eval      d1aogr = vgaogr
     c                   eval      d1ahgr = vgahgr
     c                   eval      d1augr = vgaugr
     c                   eval      d1aldo = vgaldo
     c                   eval      d1amod = vgamod
     c                   eval      d1aart = vgaart
     c                   eval      d1apro = vgapro
     c                   exsr      hent_txt
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      vgkaiu_aogr = b1aogr
     c                   eval      vgkaiu_ahgr = b1ahgr
     c                   eval      vgkaiu_augr = b1augr
     c                   eval      vgkaiu_aldo = b1aldo
     c                   eval      vgkaiu_amod = b1amod
     c                   eval      vgkaiu_aart = b1aart
      *
     c     vgkaiu_key    chain     vgkaiur
      *
     c                   delete    vgkaiur
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for testing av beregning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb9win        begsr
      *
     c                   eval      b9aogr = b1aogr
     c                   eval      b9ahgr = b1ahgr
     c                   eval      b9augr = b1augr
     c                   eval      b9aldo = b1aldo
     c                   eval      b9apro = 0
     c                   eval      w_inlr = ' '
      *
     c     b9taga        tag
     c                   exfmt     b9win
     c                   if        *inkc or *inkl
     c                   goto      end_xb9win
     c                   endif
      *
     c                   call      'VG707R'
     c                   parm                    b9aogr
     c                   parm                    b9ahgr
     c                   parm                    b9augr
     c                   parm                    b9aldo
     c                   parm                    b1amod
     c                   parm                    b1aart
     c                   parm                    b9apro
     c                   parm                    w_inlr
      *
     c                   goto      b9taga
      *
     c     end_xb9win    tag
     c                   eval      w_inlr = '1'
     c                   call      'VG707R'
     c                   parm                    b9aogr
     c                   parm                    b9ahgr
     c                   parm                    b9augr
     c                   parm                    b9aldo
     c                   parm                    b1amod
     c                   parm                    b1aart
     c                   parm                    b9apro
     c                   parm                    w_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vgkai1
      *
     c                   if        %eof or
     c                             vgafrm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
      *
     c                   eval      b1aogr = vgaogr
     c                   eval      b1ahgr = vgahgr
     c                   eval      b1augr = vgaugr
     c                   eval      b1aldo = vgaldo
     c                   eval      b1amod = vgamod
     c                   eval      b1aart = vgaart
     c                   eval      b1apro = vgapro
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     vgkai1_key    setgt     vgkai1
     c                   readp     vgkai1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vgkai1
      *
     c                   if        %eof or
     c                             vgafrm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     vgkai1_key    setll     vgkai1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke på leverandør eller lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok           begsr
      *
     c                   if        w_afld = 'B2AOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c2aogr
     c                   parm                    w_gtxt
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent tekster når linje skal vedlikeholdes/Se på
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_txt      begsr
      *
      * Overgruppe
      *
     c                   eval      vogrl1_oogr = b1aogr
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2otxt = vgotxt
     c                   eval      d1otxt = vgotxt
     c                   else
     c                   move      *blanks       c2otxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   eval      vhgrl1_hogr = b1aogr
     c                   eval      vhgrl1_hhgr = b1ahgr
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2htxt = vghtxt
     c                   eval      d1htxt = vghtxt
     c                   else
     c                   move      *blanks       c2htxt
     c                   endif
      *
      * Undergruppe
      *
     c                   eval      vugrl1_uogr = b1aogr
     c                   eval      vugrl1_uhgr = b1ahgr
     c                   eval      vugrl1_uugr = b1augr
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2utxt = vgutxt
     c                   eval      d1utxt = vgutxt
     c                   else
     c                   move      *blanks       c2utxt
     c                   endif
      *
      * Leverandørnavn
      *
     c                   eval      rlevl1_levr = b1aldo
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c2ltxt = rlnavn
     c                   eval      d1ltxt = rlnavn
     c                   else
     c                   move      *blanks       c2ltxt
     c                   endif
      *
      * Modulnummer
      *
     c                   eval      jmodl1_modn = b1amod
     c     jmodl1_key    chain     jmodl1
     c                   if        %found
     c                   eval      c2mtxt = jumte1
     c                   eval      d1mtxt = jumte1
     c                   else
     c                   eval      c2mtxt = *blanks
     c                   endif
      *
      * Varenummer
      *
     c                   movel     b1aart        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c2vtxt = vvtek1
     c                   eval      d1vtxt = vvtek1
     c                   else
     c                   move      *blanks       c2vtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på prosentsats
      *
     c     vgkaiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkaiu_aogr
     c                   kfld                    vgkaiu_ahgr
     c                   kfld                    vgkaiu_augr
     c                   kfld                    vgkaiu_aldo
     c                   kfld                    vgkaiu_amod
     c                   kfld                    vgkaiu_aart
      *
      * Key for les på på prosentsats
      *
     c     vgkair_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkair_aogr
     c                   kfld                    vgkair_ahgr
     c                   kfld                    vgkair_augr
     c                   kfld                    vgkair_aldo
     c                   kfld                    vgkair_amod
     c                   kfld                    vgkair_aart
      *
      * Key for posisjonering på faktortype / crt_sub
      *
     c     vgkai1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkai1_aogr
     c                   kfld                    vgkai1_ahgr
     c                   kfld                    vgkai1_augr
     c                   kfld                    vgkai1_aldo
     c                   kfld                    vgkai1_amod
     c                   kfld                    vgkai1_aart
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for posisjonering på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      * Ind. 50 og 51 styrer tilgang til modul og artikkel
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
     c                   eval      *in50 = *on
     c                   eval      *in51 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vgkai1_aogr = 0
     c                   eval      vgkai1_ahgr = 0
     c                   eval      vgkai1_augr = 0
     c                   eval      vgkai1_aldo = 0
     c                   eval      vgkai1_amod = 0
     c                   eval      vgkai1_aart = 0
     c     vgkai1_key    setll     vgkai1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
