     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB002R
      * Beskrivelse . . : Leser linjer fra FOVF til kommaseparert til
      *                   (Business, kundeinfo)
9.01  * Spesialversjon. : Skal kjøre denne versjonen av summering (6.20
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
6.21  *       141014 nho  Test på firma
6.20  *       040414 nho  Firma ble ikke flyttet til nøkkel
6.21  *       110319 nho  Flere linjer er fjernet, hentet fra I12
7.00  *       250619 nho  Henter inn firm_end
7.01  *       150321 sst  XLedger format  i egen fil
7.02  *       070921 sst  Nye felter bak på kunde,  Orgnr + email.adr
7.03  *       220422 mst  Orgnr skal sendes som blank, når det i NexStep er '0'
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffovfl1    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.00 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
      *
      *
     frvk1pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d w_kund          s                   like(rkkund)
     d w_biln          s                   like(ffbiln)
     d w_firm          s                   like(fffirm)
     d rkunl1_kund     s                   like(rkkund)
     d tell            s              1  0
7.03 d w_rkftnr        s              9
      *
6.20 c                   eval      w_firm = l_firm
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Leser post
     c     nyles         tag
      *
     c                   read      fovfl1                                 60
      *
610  c                   if        *in60 = '1'
610  c                   goto      slutt
610  c                   endif
      *
      *
6.21 c                   if        l_firm <> fffirm
6.21 c                   goto      nyles
6.21 c                   endif
6.21  *
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
      *
     c                   eval      rvdata = *blank
     c                   write     rvk1pfr
      *
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@IMPORT_METHOD(1)')
     c                   write     rvk1pfr
      *
     c                   eval      rvdata = %trim('@ActoR(=CustNO,') +
     c                                           ('Nm,Shrt,Ad1,Ad2,') +
     c                                           ('PNo,PArea,Phone,') +
     c                                           ('PrivPh,Fax,') +
7.02 c                                           ('InvoCust,FactNo,') +
7.02 c                                           ('Org.no,email)')
     c                   write     rvk1pfr
      *
      * Ta vare på første bilagsnr
     c                   eval      w_biln = ffbiln
      *
     c                   endif
      *
     c                   exsr      finnadr
     c                   goto      nyles
      *
      *
     c     slutt         tag
      *
      * Avslutnings tekster
      *
     c                   eval      rvdata = *blank
     c                   write     rvk1pfr
      *
      *
     c                   eval      rvdata = *blank
7.00 c                   read      rb10l1                                 90
7.00 c**                 eval      rvdata = %trim('@FIRM_END(22642)')
7.00 c**                 eval      rvdata = %trim('@FIRM_END(') +
7.00 c**                                    %trim(rafirb) +
7.00 c**                                    %trim(')')
     c**                 write     rvk1pfr
      *
     c                   eval      *inlr = *on
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner navn, adresse osv. / skriv post
      *
     c     finnadr       begsr
      *
     c                   if        ffckto <> 0
     c                   eval      w_kund = ffckto
     c                   else
     c                   eval      w_kund = ffdkto
     c                   endif
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             90
      *
     c                   if            %subst(rksted:1:1) >= '0'
     c                             and %subst(rksted:1:1) <= '9'
     c                             and %subst(rksted:2:1) >= '0'
     c                             and %subst(rksted:2:1) <= '9'
     c                             and %subst(rksted:3:1) >= '0'
     c                             and %subst(rksted:3:1) <= '9'
     c                             and %subst(rksted:4:1) >= '0'
     c                             and %subst(rksted:4:1) <= '9'
     c                   eval      rksted = %subst(rksted:6:25)
     c                   endif
7.03 c*
7.03 c                   if        rkftnr = 0
7.03 c                   movel     *blanks       w_rkftnr
7.03 c                   else
7.03 c                   movel     rkftnr        w_rkftnr
7.03 c                   endif
7.03 c*
     c                   if        *in90 = *off
     c                   eval      rvdata = *blank
     c*                  eval      rvdata =  +
      * Kundenr
     c                   eval      rvdata = %char(w_kund)     + ';' +
      * Navn
     c                                      %trim(rknavn)     + ';' +
      * Søkenavn
     c                                      %trim(rkalfa)     + ';' +
      * Adresse 1
     c                                      %trim(rkgate)     + ';' +
      * Adresse 2
     c                                      %trim(rkadr2)     + ';' +
      * Postnummer
     c                                      %char(rkponr)     + ';' +
      * Poststed
     c                                      %trim(rksted)     + ';' +
      * Telefon
     c                                      %trim(rkmobn)     + ';' +
      * Priv. telefon
     c                                      %trim(rktlfn)     + ';' +
      * Fax
     c                                      %trim(rktlfx)     + ';' +
      * Fakturaknr.
     c                                      %char(rkfaku)     + ';' +
      * Factoringnr.
7.02 c                                      %char(rkfacn)     + ';' +
7.02  * Org.nr
7.03 c*                                     %char(rkftnr)     + ';' +
7.03 c                                      %trim(w_rkftnr)     + ';' +
7.02  * Mail adresse
7.02 c                                      %trim(rkemal)
      *
     c                   write     rvk1pfr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
