     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM114R
      * Beskrivelse . . : Vedlikehold av kampanjepriser, gruppebehandling
      *                   av leverandørutplukk
      *
9.01  * Spesialversjon. : Utivdet med prisgruppe VA kamp
9.01  * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       011097 HKO  Nytt program
7.xx  * 7.00  171116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
      *
     fjm114d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_kamp          s              5
8.01 d p_prgr          s              2
     d p_user          s             10
     d p_firm          s              3  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jlevl1_ldor     s                   like(jlldor)
     d jvarl8_ldor     s                   like(jvldor)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_ldor_alf      s              6
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F04=Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * F13=Betingelser
      *
     c                   if        *inkm
     c                   exsr      betingelser
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_inp
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Oppdaterer kampanje-vare
      *
     c                   exsr      oppdater
      *
      * Viser bilde over antall varer i utplukket
      *
     c                   exfmt     m1win
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør (vareeier)
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'JL500R'
     c                   parm                    b1ldor
     c                   parm                    b1lnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Varegruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Prisgivende leverandør
      *
     c                   if        w_afld = 'B1PLDO'
     c                   call      'JL500R'
     c                   parm                    b1pldo
     c                   parm                    b1plna
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som kaller program for å vise betingelser for
      * leverandør eller prisgivende leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     betingelser   begsr
      *
     c                   if        b1pldo = 0
     c                   eval      b1pldo = b1ldor
     c                   endif
      *
     c                   if        b1pldo <> 0
      *
     c                   call      'JP102R'
     c                   parm                    w_firm
     c                   parm                    b1pldo
      *
     c                   else
      *
     c                   eval      w_ldor_alf = *blank
     c                   call      'JP101R'
     c                   parm                    w_ldor_alf
      *
     c                   endif
      *
     c     end_bet       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_inp     begsr
      *
     c                   eval      b_feil = *off
      *
      * Leverandør
      *
     c                   eval      b1lnav = *blank
     c                   if        b1ldor = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1                             32
     c                   if        *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1lnav = jlnavn
      *
      * Overgruppe
      *
     c                   eval      b1utxt = *blank
     c                   if        b1ogrp <> 0 and
     c                             b1hgrp =  0 and
     c                             b1ugrp =  0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0 and
     c                             b1ugrp =  0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Prisgivende leverandør
      *
     c                   eval      b1plna = *blank
     c                   if        b1pldo = 0
     c                   eval      b1pldo = b1ldor
     c                   eval      b1plna = b1lnav
     c                   else
     c                   eval      jlevl1_ldor = b1pldo
     c     jlevl1_key    chain     jlevl1                             36
     c                   if        *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1plna = jlnavn
     c                   endif
      *
      * Rabatter/påslag og priser kan ikke oppgis samtidig
      *
     c                   if        b1rab1 <> 0 or
     c                             b1rab2 <> 0 or
     c                             b1raba <> 0 or
     c                             b1pslk <> 0 or
     c                             b1psls <> 0
     c                   if        b1vkai <> 0 or
     c                             b1vkpr <> 0 or
     c                             b1vkaa <> 0
     c                   eval      *in39  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Rabatter/påslag -inn
      *
     c                   if        b1rab2 <> 0 and
     c                             b1rab1 = 0
     c                   eval      *in40  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Rabatter/påslag -ut
      *
     c                   if        b1raba <> 0 and
     c                             b1psls <> 0
     c                   eval      *in41  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Priser
      *
     c                   if        b1vkaa <> 0 and
     c                             b1vkai = 0                                   Betingelse 1
     c                   eval      *in42  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser vareregisteret, og kaller fellesprogram for oppretting/
      * oppdatering av kampanjevarer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Leser vareregister i.h.t. utplukk
      *
     c                   eval      jvarl8_ldor = b1ldor
     c     jvarl8_key    setll     jvarl8
     c     jvarl8_key    reade     jvarl8                                 90
     c                   dow       *in90 = *off
      *
      * Sjekker om vare skal være med på utplukket
      *
     c                   if        b1ogrp <> 0 and
     c                             b1ogrp <> jvogrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        b1hgrp <> 0 and
     c                             b1hgrp <> jvhgrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        b1ugrp <> 0 and
     c                             b1ugrp <> jvugrp
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      m1ant = m1ant + 1
      *
     c                   call      'JM700R'
     c                   parm                    p_firm
     c                   parm                    p_kamp
9.01 c                   parm                    p_prgr
     c                   parm                    p_user
     c                   parm                    b1ldor
     c                   parm                    b1pldo
     c                   parm                    b1rab1
     c                   parm                    b1rab2
     c                   parm                    b1pslk
     c                   parm                    b1raba
     c                   parm                    b1psls
     c                   parm                    b1vkai
     c                   parm                    b1vkpr
     c                   parm                    b1vkaa
     c                   parm                    jvvare
      *
     c     les_neste     tag
      *
     c     jvarl8_key    reade     jvarl8                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kamp
9.01 c                   parm                    p_prgr
     c                   parm                    p_user
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les på vare m/leverandør
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
