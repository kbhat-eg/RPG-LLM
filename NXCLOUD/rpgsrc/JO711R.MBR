     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JO711R
      * Beskrivelse . . : Matching, Maskinell på lev./lev.varenr
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       101196 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl3    if   e           k disk    rename(jmatpfr:jmatl3r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
      *
      * Definering av variable i nøkler
      *
     d jmatl3_vkod     s                   like(jovkod)
     d jmatlu_var1     s                   like(jovar1)
     d jlevl3_enum     s                   like(jlenum)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
      *
      * Definering av variable
      *
     d b_ldor_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(jofirm)
      *
     d s_ldor          s                   like(joldor)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      jmatl3_vkod = 0
     c     jmatl3_key    setll     jmatl3
     c     jmatl3_key    reade     jmatl3                                 90
     c                   dow       *in90 = *off
      *
     c                   if        joldor = 0 or
     c                             jolvar = *blank
     c                   goto      les_neste
     c                   endif
      *
     c                   if        joldor <> s_ldor
     c                   eval      jlevl3_enum = joldor
     c     jlevl1_key    chain     jlevl3                             91
     c                   if        *in91 = *off
     c                   eval      b_ldor_ok = *on
     c                   else
     c                   eval      b_ldor_ok = *off
     c                   endif
     c                   eval      s_ldor = joldor
     c                   endif
      *
     c                   if        b_ldor_ok = *off
     c                   goto      les_neste
     c                   endif
      *
     c                   if        jokode <> 0
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      jvarl8_ldor = jlldor
     c                   eval      jvarl8_lvar = jolvar
     c     jvarl8_key    chain     jvarl8                             92
     c                   if        *in92 = *on
     c                   goto      les_neste
     c                   endif
      *
      * Oppdaterer matcheregister med matchet vare
      *
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur                            93
     c                   if        *in93 = *off
     c                   eval      jovar2 = jvvare
     c                   eval      joldo2 = jvldor
     c                   eval      jokode = 2
     c                   update    jmatlur
     c                   endif
      *
     c     les_neste     tag
      *
     c     jmatl3_key    reade     jmatl3                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for les på matcheregister
      *
     c     jmatl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl3_vkod
      *
      * Key for oppdatering av matcheregister
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl3_enum
      *
      * Key for oppslag på vare
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
