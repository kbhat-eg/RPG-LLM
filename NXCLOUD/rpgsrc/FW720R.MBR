     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW720R
      * Beskrivelse . . : Danning av leverandør vareregister, Hov + Dokka
      *
      *                   Innlesing via MAL-regneark !
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070999 HKO  Nytt program
      *       070201 HKO  Tilpasset nytt recordformat (mal-regneark)
      *       020603 HKO  Tilpasset nytt recordformat (mal-regneark)
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Hov + Dokka
      *
     d                 ds
     d d_inpu                       256
     d  d_vare                       15    overlay(d_inpu:1)                    Varenr
     d  d_tek1                       40    overlay(d_inpu:16)                   Varetekst-1
     d  d_tek2                       40    overlay(d_inpu:56)                   Varetekst-2
     d  d_sapr                       10    overlay(d_inpu:106)                  Salgspris
     d   d_sapr_kr                    7  0 overlay(d_sapr:1)                    Salgspris
     d   d_sapr_Øre                   2  2 overlay(d_sapr:9)                    Salgspris
     d  d_inpr                       10    overlay(d_inpu:116)                  Innkjøpspris
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)                    Innkjøpspris
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)                    Innkjøpspris
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_leng          s              2  0
      *
      * Definering av konstanter
      *
     d tall            c                   '0123456789 '
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Venstrestiller varenummer
      *
     c                   eval      d_vare = %trim(d_vare)
      *
      * Oversetter til numeriske felt
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
     c     ' ':'0'       xlate     d_sapr        d_sapr
      *
      * Oversetter tekster til ascii og store bokstaver
      *
     c                   eval      d_tek1 = %trim(d_tek1)
     c                   eval      d_tek2 = %trim(d_tek2)
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
     c     X'46':'Æ'     xlate     d_tek2        d_tek2
     c     X'A0':'æ'     xlate     d_tek2        d_tek2
     c     X'77':'Ø'     xlate     d_tek2        d_tek2
     c     X'90':'ø'     xlate     d_tek2        d_tek2
     c     X'2A':'Å'     xlate     d_tek2        d_tek2
     c     X'EF':'å'     xlate     d_tek2        d_tek2
      *
     c     Lo:Up         xlate     d_tek1        d_tek1
     c     Lo:Up         xlate     d_tek2        d_tek2
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwtek1 = %trim(d_tek1)
     c                   eval      fwtek2 = %trim(d_tek2)
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = 0
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = 'STK'
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
     c                   eval      fwsapr = d_sapr_kr + d_sapr_Øre
     c                   eval      fwdato = *loval
     c                   eval      fwbmen = 0
     c                   eval      fwtvar = *blank
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   time                    fwdato
      *
     c                   if        d_vare <> *blank
     c                   eval      w_leng = %len(%trim(d_vare))
     c     tall          check     d_vare                                 91
     c                   if        *in91 = *off and
     c                             w_leng <= 8
     c                   eval      fwtvar = '00000000'
     c                   eval      fwtvar = %subst(fwtvar:1:8-w_leng) +
     c                                      %subst(d_vare:1:w_leng)
     c                   endif
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
