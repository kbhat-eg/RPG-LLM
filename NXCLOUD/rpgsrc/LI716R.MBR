     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LI716R
      *  Beskrivelse . . : Kontrollprog. EDI/ordrebekr. mot bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *       080305 GRo  Nytt program
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  220610 ASK  Korrigert rutine for linjekontroll
6.14  * R6.10 301111 ASK  Edringer ifbm. testing mot NeB
6.15  * R6.10 050213 bsa  Sjekker om differanser på GTIN nummer
6.16  * R6.10 120213 bsa  Sjekker flere linjestatuser
6.17  * R6.10 020413 bsa  Kan ikke slette status på linjer som ikke bekreftes på
6.17  *                   EDI melding.
6.20  * R6.20 140214 bsa  Feil i test på 6.17, gjorde at status ikke
6.20  *                   ble resatt riktig.
6.nu  * 6.30  050614 bof  Gjenomgått mtp. nummerutvidelse
6.30  * R6.20 300615 bsa  Sletter alle 55xxxx linjer før ny rekontroll starter.
6.30  *                   Bygges på nytt hvis det fortsatt er aktuelt.
6.31  * R6.30 220616 bsa  Skal ikke sjekke mot enhet, hvis lik GTIN nummer.
6.33  *  6.30 081018 bof  Utvidet med trelast sortiment
6.34  *  6.30 051218 bsa  Ta med omregning av enheter hvis de ikke
6.34  *                   er like.
6.35  * 6.30  191218 bsa  Kan ikke sjekke mot logistikkvare hvis vi
6.35  *                   ikke opererer med NOBB nummer.
      *
7.01  *K0000  080920 bsa  Skal ikke sjekke varer som ikke har kobling
7.01  *K0000              til linjer på bestillingen.
      *
8.01  *K0000  230621 BSA  Utvidet meldingsnummer fra 6-8 siffer
8.02  *K0000  240423 bsa  Innfører switch for å hoppe over sjekk av leveringsdato.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av linjenummersekvenser (2 første siffer i linjenr LIMLIN)
      *
      *    10 : Hodeinformasjon
      *    20 : Partsinformasjon
      *    30 : Referanser
      *    40 : Transportinformasjon
      *    50 : Linjer
      *    55 : Linjer fra best. (ikke match i EDI-melding)
      *    60 : Tillegg/fradrag hodenivå
      *    65 : Fritekst hodenivå
      *    90 : Totaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl5    if   e           k disk    rename(rlevpfr:rlevl5r)
     fledilu    uf a e           k disk    rename(ledipfr:ledilur)
     fledilr    if a e           k disk    rename(ledipfr:ledilrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fltoel1    if   e           k disk    rename(ltoepfr:ltoel1r)
      *
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lifirm)
     d p_mtyp          s                   like(litype)
     d p_mnum          s                   like(limnum)
     d p_stat          s              2  0
6.34 d p_antu          s             11  3
6.34 d p_anti          s             11  3
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
8.02 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlv_vare     s                   like(ldvare)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d rlevl1_levr     s                   like(rllevr)
     d rlevl5_eanl     s                   like(rleanl)
     d ledilu_type     s                   like(litype)
     d ledilu_mnum     s                   like(limnum)
     d ledilu_mlin     s                   like(limlin)
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
6.30 d ledil1_mlin     s                   like(limlin)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(ldltyp)
     d jvarl3_nobb     s                   like(jvnobb)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lifirm)
     d w_fexn_lin      s              6  0
     d w_bbest         s             15  2
     d w_bobkr         s             15  2
     d w_ldor          s                   like(lildor)
     d w_pris          s             15  2
     d w_rabb          s             15  2
6.30 d w_numm          s                   like(linumm)
6.30 d w_suff          s                   like(lisuff)
6.30 d w_mnum          s                   like(limnum)
6.30 d w_oexn_lin      s              6  0
6.33 d w_lv_vare       s                   like(vvvare)
6.34 d w_antb          s             13  2
6.34 d w_anto          s             13  2
      *
     d b_diff          s              1    inz(*off)
     d b_obkt          s              1    inz(*off)
     d b_obkh          s              1    inz(*off)
     d b_obkl          s              1    inz(*off)
6.30 d b_newl          s              1    inz(*off)
7.01 d b_line          s              1    inz(*off)
      *
      *  Def. av datastrukturer for EDI-obkr. (lagret i LEDIPF, felt LIMELD)
      *
      * ORDREBEKREFTELSE Hode
      *
     d d_obkh          ds
     d  d_oh_obnr                    35
     d  d_oh_olda                      d   datfmt(*iso)
     d  d_oh_llda                      d   datfmt(*iso)
     d  d_oh_sean                    13  0
     d  d_oh_bean                    13  0
     d  d_oh_fean                    13  0
     d  d_oh_kref                    30
     d  d_oh_aksp                     3
     d  d_oh_valk                     3
     d  d_oh_ttyp                     3
     d  d_oh_fmat                     3
     d  d_oh_lbet                     3
     d  d_oh_lste                    13  0
     d  d_oh_txt1                    70
     d  d_oh_txt2                    70
     d  d_oh_txt3                    70
      *
      * ORDREBEKREFTELSE Tillegg/fradragslinjer
      *
     d d_obkh2         ds
     d  d_oh_ftkv                     3
     d  d_oh_tsek                     3
     d  d_oh_ftty                     3
     d  d_oh_fttx                    30
     d  d_oh_tbel                    17  2
      *
      * Fritekst/hodelinje
      *
     d d_obkh3         ds
     d  d_oh_ftxq                     3
     d  d_oh_ftxt                    70
      *
      * Ordrebekr.referanser/hode
      *
     d d_obkh4         ds
     d  d_oh_bstn                    35
     d  d_oh_proj                    35
     d  d_oh_kunr                    35
     d  d_oh_konr                    35
      *
      * Ordrebekr. partsinfo./hode
      *
     d d_obkh5         ds
     d  d_oh_part                     3
     d  d_oh_pean                    13  0
     d  d_oh_pnav                    30
     d  d_oh_pad1                    30
     d  d_oh_pad2                    30
     d  d_oh_pste                    30
     d  d_oh_porg                    20
      *
      * Ordrebekr. transport/lev. - hode
      *
     d d_obkh6         ds
     d  d_oh_trmo                     3
     d  d_oh_trna                    35
     d  d_oh_lebe                     3
      *
      * ORDREBEKREFTELSE Linje
      *
     d d_obkl          ds
     d  d_ol_llin                     6  0
     d  d_ol_laks                     3
6.14 d  d_ol_vean                    14  0
     d  d_ol_vnob                     8  0
     d  d_ol_vlev                    15
     d  d_ol_bkva                    15  3
     d  d_ol_benh                     3
     d  d_ol_lkva                    15  3
     d  d_ol_lenh                     3
     d  d_ol_llda                      d   datfmt(*iso)
     d  d_ol_pris                    15  3
     d  d_ol_penh                     3
     d  d_ol_prpr                     9  0
     d  d_ol_prkv                     3
     d  d_ol_epri                    15  3
     d  d_ol_evar                     8  0
     d  d_ol_linr                     6  0
     d  d_ol_linb                    15  3
     d  d_ol_mvap                     4  2
     d  d_ol_tkv1                     3
     d  d_ol_tty1                     3
     d  d_ol_ttx1                    35
     d  d_ol_tse1                     3
     d  d_ol_tme1                    11  3
     d  d_ol_ten1                     3
     d  d_ol_tpr1                     5  2
     d  d_ol_tbe1                    15  2
     d  d_ol_tkv2                     3
     d  d_ol_tty2                     3
     d  d_ol_ttx2                    35
     d  d_ol_tse2                     3
     d  d_ol_tme2                    11  3
     d  d_ol_ten2                     3
     d  d_ol_tpr2                     5  2
     d  d_ol_tbe2                    15  2
     d  d_ol_txt1                    35
     d  d_ol_txt2                    35
      *
      * ORDREBEKREFTELSE Ordretotaler
      *
     d d_obks          ds
     d  d_os_suml                    17  2
     d  d_os_ntot                    17  2
     d  d_os_mvag                    17  2
     d  d_os_mvat                    17  2
     d  d_os_btot                    17  2
     d  d_os_tilt                    17  2
     d  d_os_frat                    17  2
      *
8.02 d u_802           s              1    inz(*off)
      *
6.14  * Definering av konstanter
6.14  *
6.14 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
6.14 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
8.02  *
8.02  * Definering av eksterne prg
8.02  *
8.02   dcl-s co402_verdi1  char(1);
8.02   dcl-s co402_verdi2  char(2048);
8.02   DCL-PR CO402R EXTPGM('CO402R');
8.02     p_filgrp            char(3)     const;
8.02     p_firm              packed(3:0) const;
8.02     p_lager             packed(2:0) const;
8.02     p_nokkel            char(50)    const;
8.02     p_verdi1            char(1);
8.02     p_verdi2            char(2048);
8.02   END-PR;
      *
6.33 C/Exec SQL
6.33 C+  Set Option DatFmt=*ISO
6.33 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hoved program styrerutine - les av innposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk gyldige meldingstype for dette kontr.prog.
      *
     c                   if        p_mtyp <> 'OBKR'
     c                   eval      p_stat = 1
     c                   goto      end_pgm
     c                   endif
      *
      * Nullstille statuser på EDI-records
      *
     c                   exsr      reset_stat
      *
      * Finn informasjon på meldingshode
      *
     c                   eval      ledilr_mlin = 100001
     c     ledilr_key    chain     ledilr
     c                   if        not %found
     c                   eval      p_stat = 2
     c                   goto      end_pgm
     c                   else
     c                   eval      d_obkh = limeld
     c                   exsr      sjk_hode
     c                   endif
      *
     c                   exsr      sjk_part
      *
     c                   eval      ledilr_mlin = 900001
     c     ledilr_key    chain     ledilr
     c                   if        not %found
     c                   eval      p_stat = 3
     c                   goto      end_pgm
     c                   else
     c                   eval      d_obks = limeld
     c                   exsr      sjk_tot
     c                   endif
      *
     c                   exsr      sjk_lin
6.30  * Sjekk bestilling mot EDI melding, og lag 55xxxx linjer hvis
6.30  * de ikke finnes.
6.30 c                   if        linumm > 0
6.30 c                   eval      w_numm = linumm
6.30 c                   eval      w_suff = lisuff
6.30 c                   eval      w_mnum = limnum
6.30 c                   eval      w_oexn_lin = 550000
6.30 c                   exsr      kryss_sjekk
6.30 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *  Subrutine for å kryssjekke EDI-meldinger mot bestilling
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     kryss_sjekk   begsr
6.30  *
6.30  * Legg til linjer som ikke finnes fra før
6.30  *
6.30 c                   eval      lodtl1_numm = w_numm
6.30 c                   eval      lodtl1_suff = w_suff
6.30 c                   eval      lodtl1_line = 0
6.30 c     lodtl1_key2   setll     lodtl1
6.30 c     lodtl1_key    reade     lodtl1
6.30  *
6.30 c                   dow       not %eof(lodtl1)
6.30 c                   eval      b_newl = *on
6.30 c                   eval      ledil1_type = 'OBKR'
6.30 c                   eval      ledil1_mnum = w_mnum
6.30 c                   eval      ledil1_mlin = 500001
6.30 c     ledil1_key    setll     ledil1
6.30 c     ledil1_key2   reade     ledil1
6.30 c                   dow       not %eof(ledil1)
6.30 c                   if        limlin < 550000 and
6.30 c                             ldline = liline
6.30 c                   eval      b_newl = *off
6.30 c                   endif
6.30 c     ledil1_key2   reade     ledil1
6.30 c                   enddo
6.30  *
6.30 c                   if        b_newl = *on
6.30 c                   eval      w_oexn_lin = w_oexn_lin + 1
6.30 c                   exsr      bygg_xedil
6.30 c                   endif
6.30  *
6.30 c     lodtl1_key    reade     lodtl1
6.30 c                   enddo
6.30  *
6.30 c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *  Subrutine for å skrive ordrebekreftelse linje  (55 linje)
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     bygg_xedil    begsr
6.30  *
6.30 c                   eval      ledilu_type = 'OBKR'
6.30 c                   eval      ledilu_mnum = w_mnum
6.30 c                   eval      ledilu_mlin = w_oexn_lin
6.30 c     ledilu_key    chain     ledilur
6.30 c                   if        not %found
6.30 c                   eval      lifirm = w_firm
6.30 c                   eval      limnum = w_mnum
6.30 c                   eval      litype = 'OBKR'
6.30 c                   eval      limlin = w_oexn_lin
6.30 c                   eval      liline = ldline
6.30 c                   eval      limeld = *blank
6.30  *
6.30 c                   clear                   d_obkl
6.30 c                   eval      d_ol_txt1 = ldtek1
6.30 c                   eval      d_ol_vean = ldeann
6.30 c                   eval      d_ol_laks = 'U'
6.30 c                   eval      limeld = d_obkl
6.30  *
6.30 c                   eval      listat = '8'
6.30  *
6.30 c                   time                    liodat
6.30 c                   time                    liotim
6.30 c                   eval      liousr = l_user
6.30 c                   time                    liedat
6.30 c                   time                    lietim
6.30 c                   eval      lieusr = l_user
6.30 c                   write     ledilur
6.30 c                   else
6.30 c                   time                    liedat
6.30 c                   time                    lietim
6.30 c                   eval      lieusr = l_user
6.30 c                   update    ledilur
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av hodeinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_hode      begsr
      *
     c                   eval      b_obkh = *off
     c                   eval      lohel1_numm = linumm
     c                   eval      lohel1_suff = lisuff
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
      *
      * Sjekk at bestilling finnes
      *
     c     lohel1_key    chain     lohel1
     c                   if        %found
     c                   eval      b_obkh = *on
     c                   else
     c     ledilu_key    chain     ledilur
     c                   if        listat = *blank
     c                   eval      listat = '1'
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk ordretype
      *
     c                   if        lootyp <> *blank
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             vaosys <> 1 or
     c                             vaoakk <> 1
     c     ledilu_key    chain     ledilur
     c                   if        listat = *blank
     c                   eval      listat = '4'
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av partsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_part      begsr
      *
     c                   eval      w_ldor = *zero
      *
     c                   eval      ledilr_mlin = 200000
     c     ledilr_key    setll     ledilr
     c                   read      ledilr
      *
     c                   dow       limlin < 300000 and
     c                             not %eof
      *
     c                   eval      d_obkh5 = limeld
      *
     c                   if        d_oh_part = 'SU '
     c                   if        loldor <> 0
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   if        rleanl <> d_oh_pean
     c     ledilu_key    chain     ledilur
     c                   if        listat = *blank
     c                   eval      listat = '2'
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   else
     c                   eval      w_ldor = loldor
     c                   endif
     c                   endif
      *
     c                   else
      *
     c                   eval      rlevl5_eanl = d_oh_pean
     c     rlevl5_key    chain     rlevl5
     c                   if        not %found
     c     ledilu_key    chain     ledilur
     c                   eval      listat = '2'
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   else
     c                   eval      w_ldor = rllevr
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c                   read      ledilr
     c                   enddo
      *
     c                   if        listat = *blank
     c                   if        w_ldor <> *zero
     c                   eval      ledilu_mlin = 100001
     c     ledilu_key    chain     ledilur
     c                   if        %found
     c                   eval      lildor = w_ldor
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av totalinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_tot       begsr
      *
     c                   eval      b_obkt = *off
     c                   eval      lohel1_numm = linumm
     c                   eval      lohel1_suff = lisuff
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
      *
     c     lohel1_key    chain     lohel1
     c                   if        %found
     c                   eval      b_obkt = *on
     c                   endif
      *
      * Sjekk ordretotal
      *
     c     ledilu_key    chain     ledilur
     c                   if        %found
     c                   if        listat = *blank
     c                   if        b_obkt = *off
     c                   eval      listat = '1'
     c                   else
     c                   if        lototi <> d_os_suml
     c                   eval      w_bbest = lototi
     c                   eval      w_bobkr = d_os_suml
     c                   exsr      sjk_toleranse
     c                   if        b_diff = *on
     c                   eval      listat = '5'
     c                   endif
     c                   endif
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
      *
     c                   eval      ledilu_mlin = 100001
     c     ledilu_key    chain     ledilur
     c                   if        %found and
     c                             lototi <> d_os_suml
     c                   if        listat = *blank
     c                   eval      w_bbest = lototi
     c                   eval      w_bobkr = d_os_suml
     c                   exsr      sjk_toleranse
     c                   if        b_diff = *on
     c                   eval      listat = '5'
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av linjeinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_lin       begsr
      *
     c                   eval      b_obkh = *off
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = 500000
      *
     c     ledilu_key    setll     ledilu
     c                   read      ledilu
      *
     c                   dow       limlin > 500000 and
     c                             limlin < 600000 and
     c                             not %eof
6.17  * Linja kommer fra bestilling, og ikke fra EDI. Hopp over alle kontroller
6.17 c                   if        listat = '8'
6.17 c                   goto      ny_linje
6.17 c                   endif
      *
     c                   eval      d_obkl = limeld
     c                   eval      listat = *blank
6.11 c                   eval      b_obkl = *off
      *
     c                   if        d_ol_llin <> 0
     c                   exsr      finn_vlin
      *
7.01 c                   if        b_line = *off
7.01 c                   eval      listat = '3'
7.01 c                   goto      ny_linje
7.01 c                   endif
      *
     c                   if        b_obkl = *on
     c                   exsr      lin_ktrl
     c                   endif
     c                   endif
      *
     c                   if        b_obkl = *off
     c                   if        d_ol_vnob <> 0
     c                   exsr      finn_nobb
     c                   if        b_obkl = *on
     c                   exsr      lin_ktrl
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_obkl = *off
     c                   eval      listat = '4'
     c                   endif
      *
6.17 c     ny_linje      tag
      *
     c                   if        listat <> *blank
     c                   eval      b_obkh = *on
     c                   endif
      *
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
      *
     c                   read      ledilu
     c                   enddo
      *
     c                   if        b_obkh = *on
     c                   exsr      oppd_hode
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for nullstilling av meldingsstatus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reset_stat    begsr
      *
     c                   eval      ledilu_type = p_mtyp
     c                   eval      ledilu_mnum = p_mnum
     c                   eval      ledilu_mlin = *zero
      *
     c     ledilu_key    setll     ledilu
     c                   read      ledilu
      *
     c                   dow       not %eof and
     c                             limnum = p_mnum
6.30 c**                 if        limlin < 550000 or
6.30 c**                           limlin > 559999
     c                   eval      listat = *blank
6.30 c**                 endif
6.30 c                   if        limlin > 550000 and
6.30 c                             limlin < 559999
6.30 c                   delete    ledilur
6.30 c                   else
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
6.30 c                   endif
      *
     c                   read      ledilu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å lese bestillingslinje fra melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vlin     begsr
      *
7.01 c                   eval      b_line = *on
7.01 c                   eval      b_obkl = *off
     c                   eval      lodtlr_numm = linumm
     c                   eval      lodtlr_suff = lisuff
     c                   eval      lodtlr_line = liline
     c     lodtlr_key    chain     lodtlrr
7.01 c                   if        not %found
7.01 c**                 eval      b_obkl = *on
7.01 c                   eval      b_line = *off
7.01 c                   goto      end_vlin
     c                   endif
      *
7.01 c                   eval      b_obkl = *on
6.14 c                   if        d_ol_vnob <> ldnobb
6.14 c                   eval      b_obkl = *off
6.14 c                   endif
      *
     c     end_vlin      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne bestillingslinje fra NOBB-nr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_nobb     begsr
6.33  *
6.35 c                   if        d_ol_vnob <> 0
6.33  * sjekker om logistikk-vare
6.33 c                   eval      w_lv_vare = *blank
6.33 c/exec sql
6.33 c+ select jvqvnr
6.33 c+ into   :w_lv_vare
6.33 c+ from   jvklpf
6.33 c+ where  jvquno = :d_ol_vnob and jvqfir = :w_firm
6.33 c+ fetch first 1 rows only
6.33 c/end-exec
6.35  *
6.35 c                   else
6.35 c                   eval      w_lv_vare = *blank
6.35 c                   endif
6.35  *
6.33 c                   if        w_lv_vare <> *blank
6.33 c                   eval      lodtlv_vare = w_lv_vare
6.33 c                   else
      *
      * Finn varenummer via Nobb-nr
      *
     c                   eval      vvarl3_nobb = d_ol_vnob
     c     vvarl3_key    chain     vvarl3r
     c                   if        %found
     c                   eval      lodtlv_vare = vvvare
     c                   else
     c                   eval      jvarl3_nobb = d_ol_vnob
     c     jvarl3_key    chain     jvarl3r
     c                   if        %found
     c                   eval      lodtlv_vare = jvvare
     c                   else
     c                   goto      end_nobb
     c                   endif
     c                   endif
6.33 c                   endif
      *
      * Finn bestillingslinje på eget varenummer
      *
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
      *
     c                   dow       not %eof
      *
     c                   if        ldnumm = linumm
     c                   if        ldsuff = lisuff
     c                   move      ldline        liline
     c                   eval      b_obkl = *on
     c                   goto      end_nobb
     c                   endif
     c                   endif
      *
     c     lodtlv_key    reade     lodtlv
     c                   enddo
      *
     c     end_nobb      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lin_ktrl      begsr
      *
     c                   eval      w_rabb = 0
     c*                  if        d_ol_tty1 = 'DI '
     c                   if        d_ol_prkv = 'AAB'
     c                   if        d_ol_tpr1 <> 0
     c                   eval      w_rabb = (d_ol_pris * d_ol_tpr1) / 100
     c                   else
     c                   eval      w_rabb = d_ol_tbe1
     c                   endif
     c                   endif
     c*                  endif
6.15  * Sjekk differanse på GTIN nummer
6.31  *  Sjekken er flyttet
6.15 c                   if        d_ol_vean <> 0 and
6.15 c                             ldeann <> 0    and
6.15 c                             ldeann <> d_ol_vean
6.15 c                   eval      listat = '9'
6.15 c                   leavesr
6.15 c                   endif
6.15 c*                  if        d_ol_vean <> 0 and
6.15 c*                            ldeann = 0
6.15 c*                  eval      listat = '9'
6.15 c*                  leavesr
6.15 c*                  endif
      *
     c                   if        d_ol_lkva <> ldanta
6.34 c                   if        d_ol_lenh <> ldenh1
6.34 c                   exsr      omr_enhet
6.34 c                   else
     c                   eval      listat = '5'
6.34 c**                 leavesr
6.34 c                   endif
     c                   endif
      *
6.34 c                   if        listat <> *blank
6.34 c                   leavesr
6.34 c                   endif
      *
6.31  * Sjekk enheter hvis GTIN koden er 0
6.31 c                   if        d_ol_vean = 0
     c     lo:up         xlate     d_ol_lenh     d_ol_lenh
     c                   if        d_ol_lenh <> ldenh1
     c                   eval      listat = '6'
     c                   leavesr
     c                   endif
6.31 c                   endif
      *
     c*                  eval      w_pris = d_ol_pris - w_rabb
     c                   if        d_ol_lkva <> *zero
     c                   eval(h)   w_pris = d_ol_linb / d_ol_lkva
     c                   else
     c                   eval(h)   w_pris = d_ol_linb / 1
     c                   endif
      *
     c                   if        w_pris <> 0 and
     c                             w_pris <> ldipny
     c                   eval      w_bbest = ldipny
     c*                  eval(h)   w_bbest = ldipny * ldanta
     c                   eval      w_bobkr = w_pris
     c*                  eval(h)   w_bobkr = d_ol_linb
     c                   exsr      sjk_toleranse
     c                   if        b_diff = *on
     c                   eval      listat = '7'
     c                   leavesr
     c                   endif
     c                   endif
6.16  * Sjekk leveringstidspunkt
8.02 c                   if        u_802 = *off
6.16 c                   if        d_ol_llda <> *loval and
6.16 c                             ldldat <> *loval  and
6.16 c                             ldldat <> d_ol_llda
6.16 c                   eval      listat = 'D'
6.16 c                   leavesr
6.16 c                   endif
8.02 c                   endif
      *
     c     end_lin       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for oppdat. hodepost m/generell feilkode (linjefeil)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_hode     begsr
      *
     c                   eval      ledilu_mlin = 100001
      *
     c     ledilu_key    chain     ledilur
     c                   if        %found
     c                   if        listat = *blank
     c                   eval      listat = '3'
6.10 c                   time                    liedat
6.10 c                   time                    lietim
6.10 c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
     c                   endif
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk om akseptabel prisdiff. ift. toleranse/EDI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_toleranse begsr
      *
     c                   eval      b_diff = *off
      *
      * Leser toleransetabell
      *
     c     ltoel1_key    chain     ltoel1
     c                   if        not %found
     c                   eval      b_diff = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk om obkr.beløp lavere enn beløp som ikke skal avrundes
      *
     c                   if        w_bobkr < laetg0
     c                   eval      b_diff = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk om obkr.beløp lavere enn avviksgrense 1
      *
     c                   if        w_bobkr < laetg1
     c                   if        %abs(w_bobkr - w_bbest) > laeav1
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om obkr.beløp lavere enn avviksgrense 2
      *
     c                   if        w_bobkr < laetg2
     c                   if        %abs(w_bobkr - w_bbest) > laeav2
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om obkr.beløp lavere enn avviksgrense 3
      *
     c                   if        w_bobkr < laetg3
     c                   if        %abs(w_bobkr - w_bbest) > laeav3
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om obkr.beløp lavere enn avviksgrense 4
      *
     c                   if        w_bobkr < laetg4
     c                   if        %abs(w_bobkr - w_bbest) > laeav4
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om obkr.beløp høyere enn avviksgrense 4
      *
     c                   if        w_bobkr >= laetg4
     c                   if        %abs(w_bobkr - w_bbest) > laeav5
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
     c     end_sjk       endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Subrutine for omregning av enheter
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     omr_enhet     begsr
6.34  *
6.34  * Regn om til bestilt enhet hvis det avvikende enheter
6.34  *
6.34 c                   eval      p_antu = 0
6.34 c                   eval      p_anti = d_ol_lkva
6.34 c                   call      'VA770R'
6.34 c                   parm                    w_firm
6.34 c                   parm                    ldvare
6.34 c                   parm                    d_ol_lenh
6.34 c                   parm                    p_anti
6.34 c                   parm                    ldenh1
6.34 c                   parm                    p_antu
6.34  *
6.34  * Tillater litt mer slingringsmonn ved omregning
6.34  *
6.34 c                   eval(h)   w_antb = ldanta
6.34 c                   eval(h)   w_anto = p_antu
6.34  *
6.34 c                   if        w_antb <> w_anto
6.34 c                   eval      listat = '5'
6.34 c                   else
6.34 c                   eval      listat = *blank
6.34 c                   endif
6.34  *
6.34 c     end_enhet     endsr
6.34  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_mtyp
     c                   parm                    p_mnum
     c                   parm                    p_stat
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
6.30  *
6.30  *  Key for les BESTILLING-LINJE
6.30  *
6.30 c     lodtl1_key2   klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    lodtl1_numm
6.30 c                   kfld                    lodtl1_suff
6.30 c                   kfld                    lodtl1_line
      *
      *  Key for les BESTILLING-LINJE på varenummer
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key for direkte les BESTILLING-LINJE
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av leverandør på EAN - lokasjonsnummer
      *
     c     rlevl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl5_eanl
      *
      * Key for oppdatering av EDI-mottaksfil
      *
     c     ledilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilu_type
     c                   kfld                    ledilu_mnum
     c                   kfld                    ledilu_mlin
      *
      * Key for les av EDI-mottaksfil
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      * Key for les av EDI-mottaksfil
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
6.30 c                   kfld                    ledil1_mlin
6.30  *
6.30  * Key for oppdatering av EDI-mottaksfil
6.30  *
6.30 c     ledil1_key2   klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ledil1_type
6.30 c                   kfld                    ledil1_mnum
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av vare på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for les av vare på nobb-nummer (Vareadm.)
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for les av ordretype-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for linjetyper
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på toleranse/EDI
      *
     c     ltoel1_key    klist
     c                   kfld                    w_firm
      *
      *  Legger inn firmanummer
      *
     c                   eval      w_firm = p_firm
     c                   eval      ledil1_type = p_mtyp
     c                   eval      ledil1_mnum = p_mnum
     c                   eval      ledilr_type = p_mtyp
     c                   eval      ledilr_mnum = p_mnum
      *
     c                   eval      p_stat = *zero
8.02  *
8.02  * Endring 8.02 - Switch for styring av test på leveringsdato
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'SJEKK_LEVERINGSDATO':
8.02                    co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_802 = *on;
8.02   endif;
8.02  *
     c                   endsr
