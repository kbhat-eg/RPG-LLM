      /TITLE  Utskrift av åpne poster  - leverandører
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL636R                                              *
      * Beskrivelse . . : Utskrift av åpne poster  - leverandører             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.30 * V5.60 210308 KOB  Nytt program
      *                                                                       *
6.20  *       250815 NHO  Lagt inn ROREST ved summering av Saldo              *
      *                                                                       *
8.01  *K0000  040425 bsa  Omskrevet til PDF og PROA
      *
      *                                                                       *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl9    if   e           k disk    rename(rltrpfr:rltrl9r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     frl636p    o    e             printer oflind(*in30)
8.01 f                                     usropn
      *
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d w_firm          s                   like(rlfirm)
      *
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_avde          s              4  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_tbnk          s             10
     d w_fbgi          s             13
      *
     d w_pper          s              6  0
     d w_kper          s              6  0
      *
     d p_levr          s                   like(rllevr)
      *
     d rlevl1_levr     s                   like(rllevr)
     d rltrl9_levr     s                   like(rolevr)
     d aforl1_ruti     s                   like(afruti)
      *
8.01 d p_btyp          s            100
      *
8.01 d splfname2       s             10a
8.01 d jobname2        s             10a
8.01 d username2       s             10a
8.01 d jobnbr2         s              6a
8.01 d splfnbr2        s             10i 0
8.01 d splfname3       s             10a
8.01 d jobname3        s             10a
8.01 d username3       s             10a
8.01 d jobnbr3         s              6a
8.01 d splfnbr3        s             10i 0
8.01 d p_tagg0         s             20
8.01 d p_tagg0val      s             50
8.01 d p_tagg1         s             20
8.01 d p_tagg1val      s             50
8.01 d p_tagg2         s             20
8.01 d p_tagg2val      s             50
8.01 d p_tagg3         s             20
8.01 d p_tagg3val      s             50
8.01 d p_tagg4         s             20
8.01 d p_tagg4val      s             50
8.01 d p_tagg5         s             20
8.01 d p_tagg5val      s             50
8.01 d p_tagg6         s             20
8.01 d p_tagg6val      s             50
8.01 d p_tagg7         s             20
8.01 d p_tagg7val      s             50
8.01 d p_tagg8         s             20
8.01 d p_tagg8val      s             50
8.01 d p_tagg9         s             20
8.01 d p_tagg9val      s             50
8.01 d p_refn          s             50
8.01 d p_dspl          s            100
8.01  *
8.01 d qsprilsp        pr                  extpgm('QSPRILSP')
8.01 d rcvvar                     32767a   options(*varsize)
8.01 d rcvvarlen                     10i 0 const
8.01 d format                         8a   const
8.01 d errorcode                  32767a   options(*varsize)
8.01  *
8.01 d sprl0100        ds
8.01 d  bytesrtn                     10i 0
8.01 d  bytesavl                     10i 0
8.01 d  splfname                     10a
8.01 d  jobname                      10a
8.01 d  username                     10a
8.01 d  jobnbr                        6a
8.01 d  splfnbr                      10i 0
8.01 d  systemname                    8a
8.01 d  createdate                    7a
8.01 d                                1a
8.01 d  createtime                    6a
8.01  *
8.01 d errorcode       ds
8.01 d  bytesprov                    10i 0 inz(0)
8.01 d  byteavail                    10i 0 inz(0)
      *
      * Local data
      *
     d                uds
     d l_dato                300    305  0
     d l_aarf                         4  0
     d l_aart                         4  0
     d l_perf                         2  0
     d l_pert                         2  0
8.01 d l_poff                584    584
8.01 d l_bach                595    635
8.01 d l_skje                637    637  0
     d l_ruti                800    809
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      /eject
      *
      * Leser leverandør
      *
     c                   eval      rlevl1_levr = p_levr
     c     rlevl1_key    chain     rlevl1                             61
      *
     c                   eval      t1kjØp = rlkjØp
     c                   eval      t1kjØf = rlkjØf
      *
      * Skriv ut faste data
      *
     c                   write     head
      *
      * Leser kundeposteringer
      *
     c                   exsr      les_levtr
      *
      * Skriv ut totaler
      *
     c                   write     total
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on                                  ..............
8.01  * Generer xml for videre utskrift
8.01 clr                 if        l_poff = '1'
8.01 clr                 exsr      spoolnbr
8.01 clr                 close     rl636p
8.01 clr                 exsr      xml_create
8.01 clr                 exsr      pdf_preview
8.01  * Avslutt jobbiden
8.01 clr                 call      'AB710R'
8.01 c                   parm                    l_bach
8.01 clr                 endif
      *
      /eject
      *
      * Leser leverandørtransaksjoner
      *
     c     les_levtr     begsr
      *
     c                   eval      rltrl9_levr = p_levr
     c     rltrl9_key    setll     rltrl9
     c     rltrl9_key    reade     rltrl9                                 62
      *
     c                   dow       *in62 = *off
      *
      * Sjekk mot periode
      *
     c                   movel     roraar        w_kper
     c                   move      rorper        w_kper
      *
     c                   if        w_kper <= w_pper
      *
      * Ta vare på siste transaksjonsdato
      *
     c     *dmy          move      robdat        t1sdat
      *
      * Beregn saldo
      *
6.20 c***                eval      t1sald = t1sald + robelØ                     Saldo
6.20 c                   eval      t1sald = t1sald + rorest                     Saldo
      *
      * Skriver detalj-linje
      *
     c     *dmy          move      robdat        d1bdat
     c     *dmy          move      rofdat        d1fdat
      *
     c                   write     linje
      *
      * Overflow ?
      *
     c                   if        *in30 = *on
     c                   write     head
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endif
      *
     c     rltrl9_key    reade     rltrl9                                 62
     c                   enddo                                                     fordeling
      *
     c                   endsr
      *
      /eject
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01  * Finner spool som er generert av jobben
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01 c     spoolnbr      begsr
8.01  /Free
8.01
8.01           QSPRILSP( SPRL0100
8.01                   : %size(SPRL0100)
8.01                   : 'SPRL0100'
8.01                   : errorcode );
8.01  /End-free
8.01 c*
8.00  *
8.01 c                   endsr
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01  * Kaller eget program for generering av xml fil. Eget pgm         *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01 c     xml_create    begsr
8.01  *
8.01 c                   eval      splfname2 = *blanks
8.01 c                   eval      splfname3 = *blanks
8.01 c                   eval      jobname2 = *blanks
8.01 c                   eval      jobname3 = *blanks
8.01 c                   eval      username2 = *blanks
8.01 c                   eval      username3 = *blanks
8.01 c                   eval      jobnbr2 = *blanks
8.01 c                   eval      jobnbr3 = *blanks
8.01 c                   eval      splfnbr2 = *zeros
8.01 c                   eval      splfnbr3 = *zeros
8.01 c                   eval      p_btyp = 'AAPEN POST  '
8.01 c                   eval      p_dspl = 'AApen post liste   , '+
8.01 c                                      ' kjørt dato: '+
8.01 c                                      %char(udate)
8.01 c                   eval      p_refn = 'KKU-' + %char(udate)
8.01 c                   eval      p_tagg0 = *blanks
8.01 c                   eval      p_tagg0val = *blanks
8.01 c                   eval      p_tagg1 = *blanks
8.01 c                   eval      p_tagg1val = *blanks
8.01 c                   eval      p_tagg2 = *blanks
8.01 c                   eval      p_tagg2val = *blanks
8.01 c                   eval      p_tagg3 = *blanks
8.01 c                   eval      p_tagg3val = *blanks
8.01 c                   eval      p_tagg4 = *blanks
8.01 c                   eval      p_tagg4val = *blanks
8.01 c                   eval      p_tagg5 = *blanks
8.01 c                   eval      p_tagg5val = *blanks
8.01 c                   eval      p_tagg6 = *blanks
8.01 c                   eval      p_tagg6val = *blanks
8.01 c                   eval      p_tagg7 = *blanks
8.01 c                   eval      p_tagg7val = *blanks
8.01 c                   eval      p_tagg8 = *blanks
8.01 c                   eval      p_tagg8val = *blanks
8.01 c                   eval      p_tagg9 = *blanks
8.01 c                   eval      p_tagg9val = *blanks
8.01  *
8.01 c                   call      'AP627R'
8.01 c                   parm                    splfname
8.01 c                   parm                    jobname
8.01 c                   parm                    username
8.01 c                   parm                    jobnbr
8.01 c                   parm                    splfnbr
8.01 c                   parm                    splfname2
8.01 c                   parm                    jobname2
8.01 c                   parm                    username2
8.01 c                   parm                    jobnbr2
8.01 c                   parm                    splfnbr2
8.01 c                   parm                    splfname3
8.01 c                   parm                    jobname3
8.01 c                   parm                    username3
8.01 c                   parm                    jobnbr3
8.01 c                   parm                    splfnbr3
8.01 c                   parm                    p_tagg0
8.01 c                   parm                    p_tagg0val
8.01 c                   parm                    p_tagg1
8.01 c                   parm                    p_tagg1val
8.01 c                   parm                    p_tagg2
8.01 c                   parm                    p_tagg2val
8.01 c                   parm                    p_tagg3
8.01 c                   parm                    p_tagg3val
8.01 c                   parm                    p_tagg4
8.01 c                   parm                    p_tagg4val
8.01 c                   parm                    p_tagg5
8.01 c                   parm                    p_tagg5val
8.00 c                   parm                    p_tagg6
8.01 c                   parm                    p_tagg6val
8.01 c                   parm                    p_tagg7
8.01 c                   parm                    p_tagg7val
8.01 c                   parm                    p_tagg8
8.01 c                   parm                    p_tagg8val
8.01 c                   parm                    p_tagg9
8.01 c                   parm                    p_tagg9val
8.01 c                   parm                    l_bach
8.01 c                   PARM                    p_btyp
8.01 c                   PARM                    p_refn
8.01 c                   PARM                    p_dspl
8.01  *
8.01 c                   endsr
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01 c     pdf_preview   begsr
8.01 c                   if        l_skje = 1
8.01  * Vi kaller program for launch av PDF i browser
8.01 c                   call      'AP621R'
8.01 c                   parm                    l_bach
8.01  *
8.01 c                   endif
8.01  *
8.01 c                   endsr
8.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_levr
      *
8.01 c                   open      rl636p
     c                   eval      w_firm = l_firm
      *
     c                   movel     l_aart        w_pper
     c                   move      l_pert        w_pper
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
     c     rltrl9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl9_levr
      *
      * Key for rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Hente inn formular-register
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        not%found
     c                   eval      afprfi = 1
     c                   endif
      *
      *  Hent inn firma opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   if        afprfi = 1
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = 0
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
      *
      * Skal firmanavn droppes ?
      *
     c                   if        w_prfi = 0
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
