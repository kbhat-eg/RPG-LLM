     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LA900R
      *  Beskrivelse . . : Oppretter standardverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *  6.10      180609  Oppdatert med sporingsinformasjon       BSA
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fausrlu    uf a e           k disk    rename(ausrpfr:ausrlur)
     faliblu    uf a e           k disk    rename(alibpfr:aliblur)
     faforlu    uf a e           k disk    rename(aforpfr:aforlur)
     fanumlu    uf a e           k disk    rename(anumpfr:anumlur)
     flstslu    uf a e           k disk    rename(lstspfr:lstslur)
     fvltylu    uf a e           k disk    rename(vltypfr:vltylur)
     fvotylu    uf a e           k disk    rename(votypfr:votylur)
     fla900d    cf   e             workstn
      *
      * Definering av arrays
      *
     d a_usr1          s             80    dim(1) ctdata perrcd(1)
     d a_lib1          s             80    dim(3) ctdata perrcd(1)
     d a_rut1          s             80    dim(7) ctdata perrcd(1)
     d a_rut2          s             80    dim(7) ctdata perrcd(1)
     d a_num1          s             80    dim(9) ctdata perrcd(1)
      *
     d a_sts1          s             80    dim(1) ctdata perrcd(1)
     d a_sts2          s             80    dim(1) ctdata perrcd(1)
     d a_lty1          s             80    dim(3) ctdata perrcd(1)
     d a_oty1          s             80    dim(6) ctdata perrcd(1)
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_wsid                901    910
6.10 d l_user                911    920
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
      *  Definering av datastrukturer for input
      *
     d                 ds
     d d_rec0                        80
     d  d_usr1                       10    overlay(d_rec0)
     d  d_navn                       30    overlay(d_rec0:11)
     d  d_meny                       10    overlay(d_rec0:41)
      *
     d                 ds
     d d_rec1                        80
     d  d_usr2                       10    overlay(d_rec1)
     d  d_plas                        3  0 overlay(d_rec1:11)
     d  d_bibl                       10    overlay(d_rec1:14)
      *
     d                 ds
     d d_rec2                        80
     d  d_ruti                       10    overlay(d_rec2)
     d  d_prog                       10    overlay(d_rec2:11)
     d  d_tex1                       40    overlay(d_rec2:21)
     d  d_form                       10    overlay(d_rec2:61)
     d  d_outq                       10    overlay(d_rec2:71)
      *
     d                 ds
     d d_rec3                        80
     d  d_acpi                        2  0 overlay(d_rec3)
     d  d_alin                        3  0 overlay(d_rec3:3)
     d  d_oflw                        3  0 overlay(d_rec3:6)
     d  d_akop                        3  0 overlay(d_rec3:9)
     d  d_hldq                        1  0 overlay(d_rec3:12)
     d  d_prty                        1    overlay(d_rec3:13)
     d  d_inst                        1  0 overlay(d_rec3:14)
     d  d_prfi                        1  0 overlay(d_rec3:15)
     d  d_bgir                        1  0 overlay(d_rec3:16)
     d  d_pgir                        1  0 overlay(d_rec3:17)
     d  d_jour                        1  0 overlay(d_rec3:18)
     d  d_mvak                        1  0 overlay(d_rec3:19)
     d  d_ocrk                        1  0 overlay(d_rec3:20)
      *
     d                 ds
     d d_rec4                        80
     d  d_fell                        6    overlay(d_rec4)
     d  d_type                        2    overlay(d_rec4:7)
     d  d_fast                       10    overlay(d_rec4:9)
     d  d_txt1                       30    overlay(d_rec4:19)
     d  d_wrap                        1    overlay(d_rec4:49)
     d  d_arnu                        1    overlay(d_rec4:50)
     d  d_auto                        1    overlay(d_rec4:51)
     d  d_nfom                        6  0 overlay(d_rec4:52)
     d  d_ntom                        6  0 overlay(d_rec4:58)
     d  d_sist                        6  0 overlay(d_rec4:64)
     d  d_txt2                       11    overlay(d_rec4:70)
      *
      *
     d                 ds
     d d_rec5                        80
     d  d_abrk                        6    overlay(d_rec5)
     d  d_afor                        6    overlay(d_rec5:7)
     d  d_abin                        6    overlay(d_rec5:13)
     d  d_adel                        6    overlay(d_rec5:19)
     d  d_alsm                        6    overlay(d_rec5:25)
     d  d_abnd                        1    overlay(d_rec5:31)
     d  d_aoty                        2    overlay(d_rec5:32)
     d  d_alty                        2    overlay(d_rec5:33)
     d  d_atty                        2    overlay(d_rec5:35)
     d  d_amva                        1  0 overlay(d_rec5:37)
     d  d_aenl                        1  0 overlay(d_rec5:38)
     d  d_aens                        1  0 overlay(d_rec5:39)
     d  d_alag                        1  0 overlay(d_rec5:40)
     d  d_alev                        6    overlay(d_rec5:41)
     d  d_alnr                        6    overlay(d_rec5:47)
     d  d_avnr                       15    overlay(d_rec5:53)
     d  d_abil                        2  0 overlay(d_rec5:68)
     d  d_ades                        1  0 overlay(d_rec5:70)
     d  d_asak                        1  0 overlay(d_rec5:78)
     d  d_asa1                        1  0 overlay(d_rec5:79)
     d  d_asa2                        1  0 overlay(d_rec5:80)
      *
     d                 ds
     d d_rec6                        80
     d  d_akhv                        4    overlay(d_rec6)
     d  d_aot1                        2    overlay(d_rec6:5)
     d  d_aot2                        2    overlay(d_rec6:7)
     d  d_aot3                        2    overlay(d_rec6:9)
     d  d_aot4                        2    overlay(d_rec6:11)
     d  d_aot5                        2    overlay(d_rec6:13)
     d  d_ark1                        1  0 overlay(d_rec6:19)
     d  d_ark2                        1  0 overlay(d_rec6:20)
     d  d_ark3                        1  0 overlay(d_rec6:21)
     d  d_ark4                        1  0 overlay(d_rec6:22)
     d  d_ark5                        1  0 overlay(d_rec6:23)
     d  d_ark6                        1  0 overlay(d_rec6:24)
     d  d_ark7                        1  0 overlay(d_rec6:25)
     d  d_ark8                        1  0 overlay(d_rec6:26)
      *
     d                 ds
     d d_rec7                        80
     d  d_ltyp                        2    overlay(d_rec7)
     d  d_ltxt                       30    overlay(d_rec7:3)
     d  d_lktx                        1  0 overlay(d_rec7:33)
     d  d_lpbb                        1  0 overlay(d_rec7:34)
     d  d_llag                        1  0 overlay(d_rec7:35)
     d  d_lØko                        1  0 overlay(d_rec7:36)
     d  d_lsta                        1  0 overlay(d_rec7:37)
     d  d_lkre                        1    overlay(d_rec7:38)
      *
     d                 ds
     d d_rec8                        80
     d  d_otyp                        2    overlay(d_rec8)
     d  d_otxt                       30    overlay(d_rec8:3)
     d  d_oakk                        1  0 overlay(d_rec8:33)
     d  d_osys                        1  0 overlay(d_rec8:34)
     d  d_oboo                        1  0 overlay(d_rec8:35)
     d  d_oska                        1  0 overlay(d_rec8:36)
     d  d_osam                        1  0 overlay(d_rec8:37)
     d  d_olag                        1  0 overlay(d_rec8:38)
     d  d_oØko                        1  0 overlay(d_rec8:39)
     d  d_osta                        1  0 overlay(d_rec8:40)
     d  d_opro                        1  0 overlay(d_rec8:41)
     d  d_obut                        1  0 overlay(d_rec8:42)
     d  d_okre                        1    overlay(d_rec8:43)
     d  d_obil                        2  0 overlay(d_rec8:44)
     d  d_onxt                        2    overlay(d_rec8:46)
     d  d_orut                       10    overlay(d_rec8:48)
     d  d_orub                       10    overlay(d_rec8:58)
      *
      * Definering av variable i nøkler
      *
     d aliblu_plas     s                   like(alplas)
     d aliblu_user     s                   like(aluser)
     d vltylu_ltyp     s                   like(valtyp)
     d amumlu_fast     s                   like(anfast)
     d amumlu_fell     s                   like(anfell)
     d amumlu_type     s                   like(antype)
     d vltylu_otyp     s                   like(vaotyp)
     d aforlu_ruti     s                   like(afruti)
     d ausrlu_user     s                   like(abuser)
      *
      * Definering av variable
      *
     d w_firm          s                   like(valfir)
     d w_levd          s                   like(laalev)
     d w_indx          s              2  0
     d w_valg          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller opp parameterbilde for oppstart av rutinen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      a1valg = *blank
     c                   eval      a1levr = 0
      *
     c                   exfmt     a1bld
      *
      * F3/F12=Avslutt
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      w_valg = a1valg
     c                   eval      w_levd = a1levr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppretter standard BRUKER     BESTILLING/LAGER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      d_rec0 = a_usr1(01)
      *
     c                   eval      ausrlu_user = d_usr1
     c     ausrlu_key    chain     ausrlur                            90
      *
     c                   eval      abnavn = d_navn
     c                   eval      ablevl = 0
     c                   eval      abtlfn = *blank
     c                   eval      abmeny = d_meny
     c                   eval      absfil = *blank
     c                   eval      absbib = *blank
     c                   eval      absfir = 0
     c                   eval      absavd = 0
     c                   eval      abfnav = *blank
     c                   eval      abbfil = *blank
     c                   eval      abbfir = *blank
     c                   eval      abbavd = *blank
     c                   eval      aba4s3 = *blank
     c                   eval      abrlib = *blank
      *
     c                   if        w_valg = '1'
     c                   if        *in90 = *off
     c                   update    ausrlur
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      abuser = ausrlu_user
     c                   write     ausrlur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppretter standard BRUKER/BIBILOTEKS-LISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_indx = 0
     c                   dou       w_indx = 3
     c                   eval      w_indx = w_indx + 1
     c                   eval      d_rec1 = a_lib1(w_indx)
      *
     c                   eval      aliblu_user = d_usr2
     c                   eval      aliblu_plas = d_plas
     c     aliblu_key    chain     aliblur                            90
      *
     c                   eval      albibl = d_bibl
      *
     c                   if        w_valg = '1'
     c                   if        *in90 = *off
     c                   update    aliblur
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      aluser = aliblu_user
     c                   eval      alplas = aliblu_plas
     c                   write     aliblur
     c                   endif
      *
     c                   enddo
      *
      * Genererer Ip-program og bruker-profil
      *
     c                   call      'AA030C'
     c                   parm                    abuser
      *
     c                   call      'AA034C'
     c                   parm                    abuser
     c                   parm                    abnavn
     c                   parm                    abmeny
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppretter standard RUTINER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_indx = 0
     c                   dou       w_indx = 7
     c                   eval      w_indx = w_indx + 1
     c                   eval      d_rec2 = a_rut1(w_indx)
     c                   eval      d_rec3 = a_rut2(w_indx)
      *
     c                   eval      aforlu_ruti = d_ruti
     c     aforlu_key    chain     aforlur                            90
      *
     c                   eval      aftxt1 = d_tex1
     c                   eval      aftxt2 = *blank
     c                   eval      afmld1 = *blank
     c                   eval      afmld2 = *blank
     c                   eval      afmld3 = *blank
     c                   eval      afform = d_form
     c                   eval      afoutq = d_outq
     c                   eval      afacpi = d_acpi
     c                   eval      afalin = d_alin
     c                   eval      afoflw = d_oflw
     c                   eval      afakop = d_akop
     c                   eval      afhldq = d_hldq
     c                   eval      afprty = d_prty
     c                   eval      afinst = d_inst
     c                   eval      afprfi = d_prfi
     c                   eval      afbgir = d_bgir
      *                  MOVE      d_pgir        AFPGIR
      *                  MOVE      d_jour        AFJOUR
     c                   eval      afmvak = d_mvak
     c                   eval      afocrk = d_ocrk
     c                   eval      afprog = d_prog
      *
     c                   if        w_valg = '1'
     c                   if        *in90 = *off
     c                   update    aforlur
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      afruti = aforlu_ruti
     c                   write     aforlur
     c                   endif
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppretter standard NUMMER-SERIER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_indx = 0
     c                   dou       w_indx = 9
     c                   eval      w_indx = w_indx + 1
     c                   eval      d_rec4 = a_num1(w_indx)
      *
     c                   eval      amumlu_fell = d_fell
     c                   eval      amumlu_type = d_type
     c                   eval      amumlu_fast = d_fast
     c     anumlu_key    chain     anumlur                            90
      *
     c                   eval      antxt1 = d_txt1
     c                   movel     d_txt2        antxt2
     c                   eval      anwrap = d_wrap
     c                   eval      anÅrnu = d_arnu
     c                   eval      anauto = d_auto
     c                   if        *in90 = *on
     c                   eval      annfom = d_nfom
     c                   eval      anntom = d_ntom
     c                   eval      ansist = d_sist
     c                   endif
      *
     c                   if        w_valg = '1'
     c                   if        *in90 = *off
     c                   update    anumlur
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      anfirm = w_firm
     c                   eval      anfell = amumlu_fell
     c                   eval      antype = amumlu_type
     c                   eval      anfast = amumlu_fast
     c                   write     anumlur
     c                   endif
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppretter standard LINJETYPER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_indx = 0
     c                   dou       w_indx = 3
     c                   eval      w_indx = w_indx + 1
     c                   eval      d_rec7 = a_lty1(w_indx)
      *
     c                   eval      vltylu_ltyp = d_ltyp
     c     vltylu_key    chain     vltylur                            90
      *
     c                   eval      valtxt = d_ltxt
     c                   eval      valktx = d_lktx
     c                   eval      valpbb = d_lpbb
     c                   eval      vallag = d_llag
     c                   eval      valoko = d_lØko
     c                   eval      valsta = d_lsta
     c                   eval      valkre = d_lkre
     c                   eval      valo01 = *blank
     c                   eval      valo02 = *blank
     c                   eval      valo03 = *blank
     c                   eval      valo04 = *blank
      *
     c                   if        w_valg = '1'
     c                   if        *in90 = *off
6.10 c                   time                    valeda
6.10 c                   time                    valeti
6.10 c                   eval      valeus = l_user
     c                   update    vltylur
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      valfir = w_firm
     c                   eval      valtyp = vltylu_ltyp
6.10 c                   time                    valoda
6.10 c                   time                    valoti
6.10 c                   eval      valous = l_user
6.10 c                   time                    valeda
6.10 c                   time                    valeti
6.10 c                   eval      valeus = l_user
     c                   write     vltylur
     c                   endif
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppretter standard ORDRETYPER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_indx = 0
     c                   dou       w_indx = 6
     c                   eval      w_indx = w_indx + 1
     c                   eval      d_rec8 = a_oty1(w_indx)
      *
     c                   eval      vltylu_otyp = d_otyp
     c     votylu_key    chain     votylur                            90
      *
     c                   eval      vaotxt = d_otxt
     c                   eval      vaosys = d_osys
     c                   eval      vaoakk = d_oakk
     c                   eval      vaoboo = d_oboo
     c                   eval      vaoska = d_oska
     c                   eval      vaosam = d_osam
     c                   eval      vaolag = d_olag
     c                   eval      vaooko = d_oØko
     c                   eval      vaosta = d_osta
     c                   eval      vaopro = d_opro
     c                   eval      vaobut = d_obut
     c                   eval      vaokre = d_okre
     c                   eval      vaobil = d_obil
     c                   eval      vaonxt = d_onxt
     c                   eval      vaorut = d_orut
     c                   eval      vaorub = d_orub
      *
     c                   if        w_valg = '1'
     c                   if        *in90 = *off
6.10 c                   time                    vaoeda
6.10 c                   time                    vaoeti
6.10 c                   eval      vaoeus = l_user
     c                   update    votylur
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      vaofir = w_firm
     c                   eval      vaotyp = vltylu_otyp
6.10 c                   time                    vaooda
6.10 c                   time                    vaooti
6.10 c                   eval      vaoous = l_user
6.10 c                   time                    vaoeda
6.10 c                   time                    vaoeti
6.10 c                   eval      vaoeus = l_user
     c                   write     votylur
     c                   endif
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppretter standard BESTILLING/LAGER-KODER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      d_rec5 = a_sts1(01)
     c                   eval      d_rec6 = a_sts2(01)
      *
     c     lstslu_key    chain     lstslur                            90
      *
     c                   eval      laabrk = d_abrk
     c                   eval      laabin = d_abin
     c                   eval      laafor = d_afor
     c                   eval      laadel = d_adel
     c                   eval      laalsm = d_alsm
     c                   eval      laabnd = d_abnd
     c                   eval      laaoty = d_aoty
     c                   eval      laalty = d_alty
     c                   eval      laatty = d_atty
     c                   eval      laamva = d_amva
     c                   eval      laaenl = d_aenl
     c                   eval      laaens = d_aens
     c                   eval      laalag = d_alag
      *
     c                   eval      laavnr = d_avnr
     c                   eval      laabil = d_abil
     c                   eval      laades = d_ades
     c                   eval      laasak = d_asak
     c                   eval      laasa1 = d_asa1
     c                   eval      laasa2 = d_asa2
      *
     c                   eval      laakhv = d_akhv
     c                   eval      laaot1 = d_aot1
     c                   eval      laaot2 = d_aot2
     c                   eval      laaot3 = d_aot3
     c                   eval      laaot4 = d_aot4
     c                   eval      laaot5 = d_aot5
     c                   eval      laark1 = d_ark1
     c                   eval      laark2 = d_ark2
     c                   eval      laark3 = d_ark3
     c                   eval      laark4 = d_ark4
     c                   eval      laark5 = d_ark5
     c                   eval      laark6 = d_ark6
     c                   eval      laark7 = d_ark7
     c                   eval      laark8 = d_ark8
      *
     c                   eval      laalev = w_levd
     c                   eval      laalnr = w_levd
      *
     c                   if        w_valg = '1'
     c                   if        *in90 = *off
6.10 c                   time                    laaeda
6.10 c                   time                    laaeti
6.10 c                   eval      laaeus = l_user
     c                   update    lstslur
     c                   endif
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      laafir = w_firm
6.10 c                   time                    laaoda
6.10 c                   time                    laaoti
6.10 c                   eval      laaous = l_user
6.10 c                   time                    laaeda
6.10 c                   time                    laaeti
6.10 c                   eval      laaeus = l_user
     c                   write     lstslur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for BRUKER
      *
     c     ausrlu_key    klist
     c                   kfld                    ausrlu_user
      *
      *  Key for BRUKER/BIBLIOTEKSLISTE
      *
     c     aliblu_key    klist
     c                   kfld                    aliblu_user
     c                   kfld                    aliblu_plas
      *
      *  Key for FORMULAR-RUTINE
      *
     c     aforlu_key    klist
     c                   kfld                    aforlu_ruti
      *
      *  Key for NUMMER
      *
     c     anumlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    amumlu_fell
     c                   kfld                    amumlu_type
     c                   kfld                    amumlu_fast
      *
      *  Key for LAGER-KODE
      *
     c     lstslu_key    klist
     c                   kfld                    w_firm
      *
      *  Key for LINJETYPE
      *
     c     vltylu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltylu_ltyp
      *
      *  Key for ORDRETYPE
      *
     c     votylu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltylu_otyp
      *
      *  Legger inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
**     B R U K E R
ASLAGR    Standard Best/Innkjøp  Bruker LO00
**     B R U K E R - B I B L I O T E K E R
ASLAGR    110ASLAGR
ASLAGR    120ASOFAK
ASLAGR    130MDOFAK
**     R U T I N E R  (1)
LFORS     LO910C    Bestillingsforslag                      FORS      *JOB
LBEST     LO920C    Bestilling                              BEST      *JOB
LIFAX     LO920C    Fax bestilling                          LFAX      *JOB
LPAKK     LO930C    Innkommet Pakkseddel                    INPA      *JOB
LPAKKNEG  LO930C    Korrigering Pakkseddel, retur           INPA      *JOB
LFAKT     LO940C    Inngående Faktura                       INFA      *JOB
LKRED     LO940C    Inngående Kreditnota                    INFA      *JOB
**     R U T I N E R  (2)
10072068001000000000
10072068001000000000
10072068001000000000
10072068001000000000
10072068001000000000
10072068001000010100
10072068001000010100
**     N U M M E R - K O D E R
BEST              Best.nummer, fellesserie      10A000000999999000000Best/Innkj.
FBILAG            Bilagsnummer,fellesserie      10A000000999999000000Ordre/fakt.
LFORSL            Bestillingsforslag            10A000000999999000000Best/Innkj.
LSLETT            Slettenummer,fellesserie      10A000000999999000000Best/Innkj.
LLISTE            Listenummer, fellesserie      10A000000999999000000Best/Innkj.
LBATCH            Batchnummer, fellesserie      10A000000999999000000Best/Innkj.
LLBUNT            Buntnummer, fellesserie       10A000000999999000000Best/Innkj.
LTELLE            Telleliste, fellesserie       10A000000999999000000Best/Innkj.
LTBUNT            Buntnummer, fellesserie       10A000000999999000000Best/Innkj.
**   L A G E R   S T A T U S   K O D E R  (1)
BEST  LBILAGLFORSLLSLETTLL                                                   000
**   L A G E R   S T A T U S   K O D E R  (2)
OFAKFOB IPIFIK    11110000
**     L I N J E - T Y P E R
  Vanlig varelinje              00111
- Negativ varelinje             00111-
TXTekstlinje                    10000
**     O R D R E - T Y P E R
FOBestillingsforslag            1000000000 00B LFORS
B Bestilling                    1100010000 00IPLBEST
IPInnkommet Pakkseddel          1200010001 00IPLPAKK
INKorrigering Pakkseddel,retur  1200010001-00IKLPAKKNEG
IFInngående faktura             1300011100 01  LFAKT
IKInngående kreditnota          1300011100-03  LKRED
