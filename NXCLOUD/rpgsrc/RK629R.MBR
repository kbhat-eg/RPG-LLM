      /TITLE  Utskrift av kunderegister Notatblokk
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK629R                                        *
      * Beskrivelse . . : Utskrift av kunderegister Notatblokk          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * R5.00     011103  Nytt program.                                  HFL
T5.40 * R5.40     150605  Leser RKLAL2 i stedet for RKLALU.              HFL
T5.40 * R5.40     310806  Distrikt 4 pos.                                HFL
6.20  * R6.20     080113  Prosjekt er nå alfa                            HFL
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
E5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
     frk629p    o    e             printer oflind(*in30)
      *
      *************************  T A B E L L E R  ************************
      *
     d f               s              1    dim(30)
      *
     d                 ds
     d w_tdat                         8  0
     d  w_taar                        4  0 overlay(w_tdat)
     d  w_tmnd                        2  0 overlay(w_tdat:5)
     d  w_tdag                        2  0 overlay(w_tdat:7)
      *
     d w_xdat          s             10d   datfmt(*iso)
      *
      *RESKONTRO - Datastruktur for parameter
     d                uds
     d pdato                 300    305  0                                      Pr. dato
     d paarf                          4  0                                      Pr. år
     d paart                          4  0                                      Pr. år
     d pperf                          2  0                                      Periode f.o.m.
     d ppert                          2  0                                      Periode t.o.m.
     d pavdf                          4  0                                      Avdeling f.o.m.
     d pavdt                          4  0                                      Avdeling t.o.m.
     d pkatf                          4  0                                      Kategori f.o.m.
     d pkatt                          4  0                                      Kategori t.o.m.
     d pself                          4  0                                      Selger f.o.m.
     d pselt                          4  0                                      Selger t.o.m.
     d pknrf                          6  0                                      Nr. f.o.m.
     d pknrt                          6  0                                      Nr. t.o.m.
     d kun                            6  0 dim(10)                              Tabell-enkeltkunder
E5.40d pdisf                          4  0                                      Distrikt f.o.m.
E5.40d pdist                          4  0                                      Distrikt t.o.m.
     d plagf                          2  0                                      Lager f.o.m
     d plagt                          2  0                                      Lager t.o.m
     d psagr                          1                                         Saldotest(J/N)
     d psabe                          7  0                                      Saldogrense i kroner
     d pktid                          1                                         Kredittest(J/N)
     d pktdg                          5  0                                      Kreditgrense i dager
     d pkgrn                          1                                         Kredit overskredet
     d pedat                         10d   datfmt(*iso)                         Endringsdato
      *
      *  Hva skal skrives på liste                                Valg: J/N=Ja/N
EAS01d pnota                          1                                         Valutabeløp
     d pkper                          1                                         K.person
     d ptlf                           1                                         Tlf.nr
     d pkomp                          1                                         Komprimert
     d psald                          1                                         Kun saldo
     d pkjop                          1                                         Kun saldo
      *
      *  Hvordan skal liste sorteres                              Valg: J/N=Ja/N
     d pkats                          1                                         Kategori
     d pavds                          1                                         Avdeling
     d psels                          1                                         Selger
     d psort                          1                                         Alfa/Numerisk
     d petik                          1  0                                      Etikettype
     d ptype                          1                                         Listetype
     d prabk                          1                                         Rabattkategori
     d ppsla                          1                                         Påslagstabell
     d pssal                          1                                         Stigende saldo
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920                                         Bruker
     d l_firm                944    946  0                                      Firma
     d l_fnav                951    980                                         Navn
      *
     d antpos          s              3  0
     d wakjli          s              5  0
     d wakjre          s              5  0
     d wantli          s              5  0
     d wantre          s              5  0
     d wdebli          s              5  0
     d wdebre          s              5  0
     d wdsali          s             13  2
     d wdsare          s             13  2
     d wkjoli          s             13  2
     d wkjore          s             13  2
     d wkreli          s              5  0
     d wkrere          s              5  0
     d wksali          s             13  2
     d wksare          s             13  2
     d wsalli          s             13  2
     d wsalre          s             13  2
     d w_alfa          s                   like(rkalfa)
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
      *
     d w_syst          s                   like(rxsyst)
     d w_pros          s                   like(rxpros)
     d w_akti          s                   like(rxakti)
     d w_kont          s                   like(rxkont)
      *
      * Les kunderegister
      * avhengig av valg num./alfa
      *
     c     lesk          tag
      *
     c                   if        psort = ' '                                           .....
     c                   read      rkunl1                                 10                 .
     c                   else                                                                .
     c                   read      rkunl2                                 10                 .
     c                   endif                                                           .....
      *
     c                   if        rkfirm > l_firm                                       .....
     c                   eval      *in10 = *on                                               .
     c                   endif                                                           .....
      *
     c                   if        *in10 = *on
     c                   goto      slutt                                        FERDIG
     c                   endif
      *
      * Sjekk kundenr. fra/til
      *
     c                   if        rkkund < pknrf or                                     .....
     c                             rkkund > pknrt                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        rkavde < pavdf or                                     .....
     c                             rkavde > pavdt                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk kundekategori fra/til
      *
     c                   if        rkkatg < pkatf or                                     .....
     c                             rkkatg > pkatt                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk selger fra/til
      *
     c                   if        rkslgr < pself or                                     .....
     c                             rkslgr > pselt                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk distrikt fra/til
      *
     c                   if        rkdist < pdisf or                                     .....
     c                             rkdist > pdist                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk siste trans.dato
      *
     c                   if        rktdat > pedat                                            .
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Selekterte kunder -
      * summer for siste side og skriv
      *
     c                   eval      wantli = wantli + 1
      *
     c                   if        rksald > *zero                                        .....
     c                   eval      wdebli = wdebli + 1                                       .
     c                   eval      wdsali = wdsali + rksald                                  .
     c                   endif                                                           .....
      *
     c                   if        rksald < *zero                                        .....
     c                   eval      wkreli = wkreli + 1                                       .
     c                   eval      wksali = wksali + rksald                                  .
     c                   endif                                                           .....
      *
     c                   if        rkkjØp <> *zero                                       .....
     c                   eval      wakjli = wakjli + 1                                       .
     c                   eval      wkjoli = wkjoli + rkkjØp                                  .
     c                   endif                                                           .....
      *
     c                   eval      wsalli = wsalli + rksald
      *
     c                   eval      antpos = 12
     c                   movea     rkalfa        f
     c                   movea     f             rkalfa
      *
      * Finnes notatblokk på kunden?
      *
     c                   eval      w_syst = 'K'                                              .
6.20 c*                  eval      w_pros = *zeros                                           .
6.20 c                   eval      w_pros = *blanks                                          .
     c                   eval      w_akti = *blanks                                          .
     c                   eval      w_kont = rkkund                                           .
E5.40c     keynot        setll     rklal2                                 60
      * Hvis bare utskrift av kunder med notatblokk
     c                   if        *in60 = *on and
     c                             *in12 = *on
     c                   exsr      skrive                                                    .
     c                   else                                                                .
     c                   if        *in12 = *off
      * Hvis utskrift av alle kunder
     c                   exsr      skrive                                                    .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
E5.40c     keynot        reade     rklal2                                 61
     c                   dow       *in61 = *off
     c                   eval      wnotat = rxtext
     c                   write     notatb                                                    .
E5.40c     keynot        reade     rklal2                                 61
     c                   enddo                                                           .....
      *
     c     alle          tag
      *
      * Alle kunder -
      * summer for siste side
      *
     c                   if        rksald > *zero                                        .....
     c                   eval      wdebre = wdebre + 1                                       .
     c                   eval      wdsare = wdsare + rksald                                  .
     c                   endif                                                           .....
      *
     c                   if        rksald < *zero                                        .....
     c                   eval      wkrere = wkrere + 1                                       .
     c                   eval      wksare = wksare + rksald                                  .
     c                   endif                                                           .....
      *
     c                   if        rkkjØp <> *zero                                       .....
     c                   eval      wakjre = wakjre + 1                                       .
     c                   eval      wkjore = wkjore + rkkjØp                                  .
     c                   endif                                                           .....
      *
     c                   eval      wantre = wantre + 1
     c                   eval      wsalre = wsalre + rksald
      *
     c                   goto      lesk
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
     clr                 write     total
      *
      /eject
      ****************************************************************
      *   SUBRUTINE FOR Å SKRIVE KUNDE - HEADING (ENKEL LISTE)       *
      ****************************************************************
     c     skrive        begsr
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                        .....  .
     c                   do                                                                  .
     c                   write     head1                                                     .
     c                   write     head2                                                     .
     c                   write     head2e                                                    .
     c                   eval      *in30 = *off                                              .
     c                   enddo                                                           .....
     c                   endif                                                           .....
      *
     c                   write     kund1
      *
     c*                  if        rkadr2 <> *blank                                      .....
     c*                  write     kund1a                                                    .
     c*                  endif                                                           .....
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      * Behandle parameter
      *
     c                   if        pnota = 'J'                                           .....
     c                   eval      *in12 = *on                                               .
     c                   endif                                                           .....
      *
      * Sette startverdier
      *
     c                   move      l_firm        w_firm
      *
     c                   if        psort = ' '                                           .....
     c     key01         setll     rkunl1                                                    .
     c                   else                                                                .
     c     key02         setll     rkunl2                                                    .
     c                   endif                                                           .....
      *
      * Skriv heading på første side
      *
     c                   write     head1
     c                   write     headku
      *
     c                   if        pavdt > *zero                                         .....
     c                   write     headav                                                    .
     c                   endif                                                           .....
      *
     c                   if        pdist > *zero                                         .....
     c                   write     headdi                                                    .
     c                   endif                                                           .....
      *
     c                   if        pselt > *zero                                         .....
     c                   write     headse                                                    .
     c                   endif                                                           .....
      *
     c                   if        pkatt > *zero                                         .....
     c                   write     headkk                                                    .
     c                   endif                                                           .....
      *
     c                   if        pedat > *loval
     c     *dmy          move      pedat         wpedat
     c                   move      pnota         wpnota
     c                   write     headda
     c                   endif
      *
     c                   write     head1
     c                   write     head2
      * Sjekk om enkel heading
     c                   eval      *in11 = *on                                               .
     c                   if        *in11 = *on                                               .
     c                   write     head2e
     c                   endif
      *
      * Key for kunderegister
      * via kundenummer
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_kund
      *
      * Key for kunderegister
      * via alfafeltet
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    w_alfa
     c                   kfld                    w_kund
      *
      * Key for notatblokk
      *
     c     keynot        klist
     c                   kfld                    w_firm
     c                   kfld                    w_syst
     c                   kfld                    W_pros
     c                   kfld                    W_akti
     c                   kfld                    W_kont
     c*                  kfld                    w_spes
     c*                  kfld                    w_avde
     c*                  kfld                    w_tist
     c*                  kfld                    w_linj
     c                   endsr
