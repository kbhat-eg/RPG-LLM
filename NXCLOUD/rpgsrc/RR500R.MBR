     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPMAL
      * Program . . . . : RR500R
      * Beskrivelse . . : Resultatrapport, spørring rapport
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210902 KOB  Nytt program
7.xx  * 7.xx  040516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frrf0l1    if   e           k disk    rename(rrf0pfr:rrf0l1r)
     frrf0l2    if   e           k disk    rename(rrf0pfr:rrf0l2r)
      *
     frr500d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_0rap          s                   like(rr0rap)
     d p_0txt          s                   like(rr0txt)
     d p_rper          s              2  0
     d p_arow          s              3  0
     d p_acol          s              3  0
     d p_mld1          s             30
     d p_mld2          s             30
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rrf0l1_0rap     s                   like(rr0rap)
     d rrf0l2_0txt     s                   like(rr0txt)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rr0fir)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b20rap <> 0 or
     c                             b20txt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = b10txt
     c     rrf0l2_key    setll     rrf0l2
     c                   other
     c                   eval      rrf0l1_0rap = b10rap
     c     rrf0l1_key    setll     rrf0l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rabattgruppe
      *
     c                   if        b20rap <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rrf0l1_0rap = b20rap
     c     rrf0l1_key    setll     rrf0l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b20txt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = b20txt
     c     rrf0l2_key    setll     rrf0l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b20rap = 0
     c                   eval      b20txt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      *
      * Skriv rapport (vanlig)
      *
     c                   if        b1valg = 1                                          ..... .
     c                   eval      p_0rap = b10rap                                         . .
      *
     c                   call      'RR621C'                                                . .
     c                   parm                    p_0rap                                    . .
      *
     c                   eval      b1valg = 0                                              . .
     c                   update    b1sfl
     c                   endif                                                         ..... .
      *
      * Skriv rapport (13 perioder)
      *
     c                   if        b1valg = 2                                          ..... .
     c                   eval      p_0rap = b10rap                                         . .
      *
     c                   call      'RR623C'                                                . .
     c                   parm                    p_0rap
      *
     c                   eval      b1valg = 0                                              . .
     c                   update    b1sfl
     c                   endif                                                         ..... .
      *
      * Skriv rapport med forventet resultat
      *
     c                   if        b1valg = 3                                          ..... .
     c                   eval      p_0rap = b10rap                                         . .
      *
     c                   call      'RR627C'                                                . .
     c                   parm                    b10rap                                    . .
      *
     c                   eval      b1valg = 0                                              . .
     c                   update    b1sfl
     c                   endif                                                         ..... .
      *
      * Beregning av rapport
      *
     c                   if        b1valg = 8                                          ..... .
      *
     c                   move      8             p_arow
     c                   move      38            p_acol
     c                   eval      p_mld1 = 'Vennligst vent ......'
     c                   eval      p_mld2 = 'Beregning pågår .....'
      *
     c                   call      'AA006C'
     c                   parm                    p_arow
     c                   parm                    p_acol
     c                   parm                    p_mld1
     c                   parm                    p_mld2
      *
     c                   eval      p_0rap = b10rap                                         . .
      *
     c                   call      'RR109R'
     c                   parm                    p_0rap
     c                   parm                    p_rper
      *
     c                   call      'RR510R'
     c                   parm                    p_0rap
      *
     c                   eval      b1valg = 0                                                .
     c                   update    b1sfl
     c                   endif                                                         ..... .
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rrf0l2
     c                   other
     c                   read      rrf0l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr0fir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = rr0txt
     c                   other
     c                   eval      rrf0l1_0rap = rr0rap
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b10rap = rr0rap
     c                   eval      b10txt = rr0txt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     rrf0l2_key    setgt     rrf0l2
     c                   readp     rrf0l2
     c                   other
     c     rrf0l1_key    setgt     rrf0l1
     c                   readp     rrf0l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rrf0l2
     c                   other
     c                   readp     rrf0l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr0fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = rr0txt
     c                   other
     c                   eval      rrf0l1_0rap = rr0rap
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rrf0l2_key    setll     rrf0l2
     c                   other
     c     rrf0l1_key    setll     rrf0l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på rapportnummer
      *
     c     rrf0l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0l1_0rap
      *
      * Key for posisjonering på beskrivelse
      *
     c     rrf0l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0l2_0txt
      *
     c                   move      *month        p_rper
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rrf0l1_0rap = 0
     c     rrf0l1_key    setll     rrf0l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
