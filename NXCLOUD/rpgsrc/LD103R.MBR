     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LD103R
      * Beskrivelse . . : Bestilling-linje, spørring/registr. av varelinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  *       010611 bsa  Nytt program
6.21  * 6.20  110112 bsa  Kalkulering av innkjøp/kost priser må ta hensyn
6.21  *                   til kampanjer.
6.nu  * 6.30  260514 bof  sjekket mht. nummerutvidelse
6.30  * 6.30  130515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.32  * 6.30  121018 bof  justert, bruker leverandør fra bestilling.
7.01  * K00   120520 bsa  Takler brede skjermbilder
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvtill5    if   e           k disk    rename(vtilpfr:vtill5r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fld103d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      * Definering av array
      *
     d a_meld          s             50    dim(1) ctdata perrcd(1)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_line          s                   like(ldline)
     d p_kost          s              1
     d p_orde          s              1
     d p_kamp          s              5
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d lusrl1_user     s                   like(lbuser)
     d rlevl1_levr     s                   like(rllevr)
     d vtill5_kamp     s                   like(vtkamp)
     d vtill5_vare     s                   like(vtvare)
     d lodtlv_vare     s                   like(ldvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
     d b_sess          s              1    inz(*off)
     d b_sent          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_ddat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
      *
     d s_stek          s             35
      *
     d w_firm          s                   like(vvfirm)
     d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_line          s                   like(ldline)
6.21 d w_kamp          s                   like(p_kamp)
      *
     d w_vare          s                   like(vvvare)
     d w_anta          s                   like(ldanta)
     d w_kopr          s                   like(ldkpny)
     d w_toti          s                   like(ldsumi)
     d w_totr          s                   like(ldsumr)
     d w_ldat          s                   like(ldldat)
     d w_lety          s                   like(ldlety)
     d w_vtyp          s                   like(vvvtyp)
     d w_avde          s              4  0
8.01 d w_prgr          s              2
     d w_mld1          s             50
     d w_ldor          s                   like(vvldor)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
6.32 C/Exec SQL
6.32 C+  Set Option DatFmt=*ISO
6.32 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2 Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F2=Registrering av tekstlinjer
      *
     c                   if        *inkb = *on
     c                   eval      w_line = w_line + 10
     c                   call      'LD110R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    laalty
     c                   goto      b2tagb
     c                   endif
      *
      * F21=Bytt mellom å utelate og ta med alle varetyper
      *
     c                   if        *inkv = *on
      *
     c                   if        *in83 = *off
     c                   eval      *in83 = *on
     c                   else
     c                   eval      *in83 = *off
     c                   endif
      *
     c***                exsr      hent_total
     c                   exsr      forny                                        CRT SFL
      *
     c                   goto      b2taga
      *
     c                   endif
      *
      * F22=Bytt mellom å vise salgspris eksl. mva og inkl. mva
      *
     c                   if        *inkw = *on
      *
     c                   if        *in84 = *off
     c                   eval      *in84 = *on
     c                   else
     c                   eval      *in84 = *off
     c                   endif
      *
     c                   exsr      hent_total
     c                   exsr      forny                                        CRT SFL
      *
     c                   goto      b2taga
      *
     c                   endif
      *
      * F23=Ny posisjonering (Varenr/Nobbnr/Lev.varenr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Posisjonering
      *
     c**                 if        b2tek1 <> *blank or
     c**                           b2vare <> 0 or
     c**                           b2nobb <> 0 or
     c**                           b2lvar <> *blank
     c                   if        b2vare <> 0
     c                   exsr      posisjoner
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
6.23 c                   if        p_orde = '1'
6.23 c                   eval      w_mld1 = a_meld(1)
6.23 c                   call      'AA005R'
6.23 c                   parm                    w_mld1
6.23 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      vtill5_kamp = p_kamp
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      vtill5_vare = b1vare
     c     vtill5_key    setll     vtill5
     c                   other
     c                   eval      vtill5_vare = *blank
     c     vtill5_key    setll     vtill5
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V'
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   movel     b2vare        vtill5_vare
     c     vtill5_key    setll     vtill5
     c                   goto      end_pos
     c                   endif
      *
      * Nobbnummer
      *
     c**                 if        b2nobb <> 0
     c**                 eval      w_seqe = 'N'
     c**                 eval      *in80 = *off
     c**                 eval      *in81 = *on
     c**                 eval      *in82 = *off
     c**                 eval      vvarl3_nobb = b2nobb
     c**   vvarl3_key    setll     vvarl3
     c**                 goto      end_pos
     c**                 endif
      *
      * Lev. varenummer
      *
     c**                 if        b2lvar <> *blank
     c**                 eval      w_seqe = 'L'
     c**                 eval      *in80 = *off
     c**                 eval      *in81 = *off
     c**                 eval      *in82 = *on
     c**                 eval      vvarl4_lvar = b2lvar
     c**   vvarl4_key    setll     vvarl4
     c**                 goto      end_pos
     c**                 endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c***                eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c**                 eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c**                 eval      b2nobb = 0
     c**                 eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
5.60 c***                eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Antall er utfylt, linje skal registreres
      *
     c                   if        b1anta <> 0
     c                   exsr      registrer
     c                   move      '->'          b1tek1
     c                   eval      b1anta = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registering av vare i bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     registrer     begsr
      *
     c                   eval      w_line = w_line + 10
     c                   eval      w_vare = b1vare
     c                   eval      w_anta = b1anta
      *
      * Viser registeringbildet
      *
     c                   eval      w_lety = 0
     c                   eval      p_orde = '0'
6.21 c                   eval      w_kamp = p_kamp
      *
     c                   call      'LD105R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    laalty
     c                   parm                    w_lety
     c                   parm                    w_vare
     c                   parm                    w_anta
     c                   parm                    b1enhe
     c                   parm                    p_kost
     c                   parm                    p_orde
6.21 c                   parm                    w_kamp
      *
      * Henter total sum for bestilling
      *
     c                   exsr      hent_total
      *
     c     end_reg       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c     nxt_vare      tag
      *
     c                   read      vtill5                                 15
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      * Er vi inne på riktig kampanje
     c                   if        vtkamp <> p_kamp
     c                   iter
     c                   endif
      * Hvis avvikende leverandør på tilbudsfila og bestilling, skal den ikke være med
     c                   if        vtldor <> 0 and
     c                             vtldor <> loldor
     c                   iter
     c                   endif
      *
     c                   eval      vvarlr_vare = vtvare
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   goto      nxt_vare
     c                   endif
      * Hvis avvikende leverandør på varen og bestilling, skal den ikke med
     c                   if        loldor <> vvldor
     c                   iter
     c                   endif
      * Hvis varen allerede er registrert på bestillingen, droppes den å vises
     c                   eval      lodtlv_vare = vtvare
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
     c                   if        not %eof
     c                   iter
     c                   endif
      *
     c                   if        vtfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *   Hent div. info fra lager
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    lolage
     c                   parm                    w_vtyp
     c                   parm                    w_udat
     c                   parm                    w_ldor
     c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
      *
6.32  * sjekker om logistikk-vare med best.leverandør
6.32 c/exec sql
6.32 c+ select vvlnob,
6.32 c+        vvllva,
6.32 c+        vvlmod
6.32 c+ into   :w_lv_nobb,
6.32 c+        :w_lv_llva,
6.32 c+        :w_lv_modn
6.32 c+ from   vvlepf
6.32 c+ where  vvlvnr = :vvvare and
6.32 c+        vvlldo = :loldor
6.32 c+ fetch first 1 rows only
6.32 c/end-exec
      *
     c                   if        *in83 = *off and
     c                             w_vtyp <> 'L'
     c                   goto      nxt_vare
     c                   endif
      * Må vurderes - siden det betyr utgår dato for varen
     c***                if        w_udat <> *loval and
     c***                          w_udat <= w_ddat
     c***                iter
     c**                 endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1sess = *blank
     c                   eval      b1sent = *blank
     c                   eval      b1stat = *blank
     c                   exsr      sjekk_vsor
     c                   if        b_sess = *on
     c                   eval      b1sess = 'P'
     c                   endif
     c                   eval      *in50  = *off
     c                   if        b_sent = *on
     c                   eval      b1sent = 'C'
     c                   if        faafsl = 1
     c                   eval      *in50  = *on
     c                   endif
     c                   endif
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1vtyp = w_vtyp
     c                   eval      b1vare = vvvare
6.31 c                   if        w_lv_nobb > 0
6.31 c                   eval      b1nobb = w_lv_nobb
6.31 c                   eval      b1lvar = w_lv_llva
6.31 c                   else
     c                   eval      b1nobb = vvnobb
     c                   eval      b1lvar = vvlvar
6.31 c                   endif
      * legger alle koder i et felt
     c                   eval      %subst(b1stat:1:1) = b1sent
     c                   eval      %subst(b1stat:2:1) = b1sess
     c                   eval      %subst(b1stat:3:1) = b1vtyp
      *
     c                   eval      w_lety = 0
6.21 c                   eval      w_kamp = p_kamp
      *
     c                   call      'VP750R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    lobdat
     c                   parm                    b1enhe
     c                   parm                    w_kopr
     c                   parm                    b1inpr
6.21 c                   parm                    w_kamp
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      b1nypo = vvvare
     c                   when      *in81 = *on
6.31 c                   if        w_lv_nobb > 0
6.31 c                   eval      b1nypo = %editc(w_lv_nobb : 'Z')
6.31 c                   else
     c                   eval      b1nypo = %editc(vvnobb : 'Z')
6.31 c                   endif
     c                   when      *in82 = *on
6.30 c*                  eval      b1nypo = vvlvar
6.31 c                   if        w_lv_nobb > 0
6.31 c                   movel     w_lv_llva     b1nypo
6.31 c                   else
6.30 c                   movel     vvlvar        b1nypo
6.31 c                   endif
     c                   endsl
      *
     c                   if        *in84 = *on
     c                   eval      b1inpr = b1inpr * w_mvat
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c**                 if        w_seqe = 'V'
     c**                 exsr      forny
     c**                 goto      end_bck
     c**                 endif
      *
     c                   if        *in15
     c                   readp     vtill5
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vtill5
      *
     c                   if        %eof or
     c                             vtfirm <> w_firm
     c                   leave
     c                   endif
      *   Hent varetype fra subprogram
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    lolage
     c                   parm                    w_vtyp
     c                   parm                    w_udat
     c                   parm                    w_ldor
     c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
      *
     c                   if        *in83 = *off and
     c                             w_vtyp <> 'L'
     c                   iter
     c                   endif
      * Må vurderes om skal gjelde ????
     c***                if        w_udat <> *loval and
     c***                          w_udat <= w_ddat
     c***                iter
     c***                endif
      * Må være samme leverandør på tilbud og bestilling
     c                   if        vtldor <> 0 and
     c                             vtldor <> loldor
     c                   iter
     c                   endif
      *
     c                   eval      vvarlr_vare = vtvare
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      * Hvis avvikende leverandør på varen og bestilling, skal den ikke med
     c                   if        loldor <> vvldor
     c                   iter
     c                   endif
      * Hvis varen allerede er registrert på bestillingen, droppes den å vises
     c                   eval      lodtlv_vare = vtvare
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
     c                   if        not %eof
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     vtill5_key    setll     vtill5
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av bestillingstotal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_total    begsr
      *
     c                   eval      w_toti = 0
     c                   eval      w_totr = 0
      *
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1
     c                   dow       not %eof
     c                   eval      w_toti = w_toti + ldsumi
     c                   eval      w_totr = w_totr + ldsumr
     c     lodtl1_key    reade     lodtl1
     c                   enddo
      *
     c                   eval      b2toti = w_toti - w_totr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om varen er i sortiment for C - sentrallagervarer
      *                           eller i et sortiment for P - periodestyrt varer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vsor    begsr
      *
     c                   eval      b_sent = *off
     c                   eval      b_sess = *off
      * sjekker sentral-lager-vare
      *
     c                   call      'VD771R'
     C                   parm                    w_firm
     C                   parm                    lolage
     C                   parm                    vvvare
     C                   parm                    b_sent
     C                   parm                    b_inlr
      * sjekker periodestyrt
      *
     c                   call      'VD772R'
     C                   parm                    w_firm
     C                   parm                    lolage
     C                   parm                    vvvare
     C                   parm                    b_sess
     C                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_kost
     c                   parm                    p_orde
     c                   parm                    p_kamp
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for posisjonering på leverandørs varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for oppslag på status-register
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bruker-register
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på bestilling-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på bestilling-linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på bestilling-linje
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på tilbudsregister
      *
     c     vtill5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill5_kamp
     c                   kfld                    vtill5_vare
      *
      * Key for oppslag på status-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Henter dagens dato
      *
     c                   time                    w_ddat
      *
      * Hent opplysninger fra koderegister
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * Henter opplysninger fra bruker-register
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1                             90
      *
      * Hent opplysninger fra bestilling-hode-register
      *
     c     lohel1_key    chain     lohel1r                            90
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    lobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Hent prisgruppe
      *
     c                   eval      w_avde = 0
     c                   call      'VL712R'
     c                   parm                    w_firm
     c                   parm                    lolage
     c                   parm                    w_avde
     c                   parm                    w_prgr
      *
      * Henter total sum for bestilling
      *
     c                   exsr      hent_total
      *
      * Fyller opp skjermbildet for første visning
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      b2ldor = loldor
     c                   eval      b2lnav = rlnavn
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
     c                   eval      b2kamp = p_kamp
      *
     c                   eval      vtill5_kamp = p_kamp
     c                   eval      vtill5_vare = *blank
     c     vtill5_key    setll     vtill5
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
**     M e l d i n g e r
Kostpris er endret på salgsordren                 <---
