     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
      *                                                                       *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RA220R                                              *
      * Beskrivelse . . : Sjekker om fil fra Svea er lest inn tidligere       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * 6.30  sst 130917  Nytt program                                        *
      *
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frsvepf    ip  af  512        disk
     frsvxl1    uf a e           k disk    rename(rsvxpfr:rsvxl1r)
      *
      *
     d                 ds
     d rpcin1                       512
      *
      * LOCAL DATA
      *
     d                uds
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d rsvxl1_rec      s                   like(rsvrec)
     d w_rec           s              2  0 inz(0)
     d lestfor         s              1
     d w_m50_1         s             50
     d w_m50_2         s             50
     d w_sv            s              1
     d w_xx            s              1
      *
      *
     irsvepf    NS  01
     i                                  1  512  rpcin1
      * ______________________________________________________________
      *   Hopp over første record da det kan komme flere filer samme dag
     c                   if        w_rec = 0
     c                   eval      w_rec = 1
     c                   eval      lestfor = *blank
     c                   goto      slutt
     c                   endif
      *
     c                   eval      rsvxl1_rec = rpcin1
6.30 c     rsvxl1_key    chain     rsvxl1
6.30 c                   if        %found(rsvxl1)
     c                   eval      w_m50_1 = 'Filen er lest før +
     c                                        av ' + rsvous +
     c                                        ' den ' + %char(rsvoda)
     c                   eval      w_m50_2 = 'Dersom du vil lese den +
     c                                        inn på nytt så svar J'
     c                   call      'AA007R'
     c                   parm                    w_m50_1
     c                   parm                    w_m50_2
     c                   parm                    w_sv
     c                   parm                    w_xx
     c                   if        w_sv = 'J'
     c                   eval      lestfor = 'U'
     c                   eval      rsvjom = 'J'
     c                   eval      rsvjav = l_user
     c                   time                    rsvjda
     c                   update    rsvxl1r
     c                   else
6.30 c                   eval      lestfor = 'J'
     c                   endif
     c                   else
     c                   eval      rsvrec  = rpcin1
     c                   eval      rsvjom  = *blank
     c                   eval      rsvjav  = *blank
     c                   eval      rsvjda  = *loval
     c                   eval      rsvous  = l_user
     c                   time                    rsvoda
     c                   time                    rsvoti
     c                   write     rsvxl1r
6.30 c                   endif
      *
6.30 c                   eval      *inlr = *on
      *
     c     slutt         tag
      *
      * ______________________________________________________________
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    lestfor
      *
     c     rsvxl1_key    klist
     c                   kfld                    rsvxl1_rec
      *
     c                   endsr
      * ----------------------------------------
