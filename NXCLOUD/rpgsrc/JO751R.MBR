     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO751R
      * Beskrivelse . . : Matching, Reversering av oppdatering
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010398 HKO  Nytt program
      *       060798 HKO  Henter 'gammel' informasjon fra matche-registeret
      *                   for oppdatering av vareregister EVR
      *                   Blanker Nobb-nummer
      *                   Sletter søketekster, og bygger nye
      *       130102 HKO  Fjernet bytting av varenummer i S36-statistikken
      *       041204 HKO  Endret parameter ved kall av VV740R og FO740C
6.11  *       110709 bof  Sjekker og fjerner evt. ean fra vare-enhet også
6.12  *       270909 bof  Endret fra vvenpf til vveppf og jvpppf til jvpkpf
6.30  *       240314 bof  Endret jvpkpf til jvpkpf
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl7    if   e           k disk    rename(jmatpfr:jmatl7r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     fvsoklu    uf   e           k disk    rename(vsokpfr:vsoklur)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fveanl2    uf   e           k disk    rename(veanpfr:veanl2r)
6.12 fvvepl1    uf   e           k disk    rename(vveppfr:vvepl1r)
6.30 fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
     d p_var2          s                   like(jovar2)
      *
      * Definering av variable i nøkler
      *
     d jmatl7_var2     s                   like(jovar2)
     d vvarlu_vare     s                   like(vvvare)
     d vsoklu_vare     s                   like(vsvare)
     d jmatlu_var1     s                   like(jovar1)
     d jvarl1_vare     s                   like(jvvare)
     d veanl2_vare     s                   like(vxvare)
6.12 d vvepl1_pvar     s                   like(vepvar)
6.30 d jvpkl1_vare     s                   like(jwvare)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      jmatl7_var2 = p_var2
     c     jmatl7_key    chain     jmatl7                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
      * Bytter varenummer i vareregister EVR, og oppdaterer registeret med
      * informasjon fra matche-registeret
      *
     c                   eval      vvarlu_vare = p_var2
     c     vvarlu_key    chain     vvarlur                            91
     c                   if        *in91 = *off
     c                   eval      vvvare = jovar1
     c                   eval      vvogrp = joogrp
     c                   eval      vvhgrp = johgrp
     c                   eval      vvugrp = jougrp
     c                   eval      vvtek1 = jotek1
     c                   eval      vvtek2 = jotek2
     c                   eval      vvldor = joldor
     c                   eval      vvlvar = jolvar
     c                   eval      vvnobb = 0
     c                   eval      vvnmod = 0
     c                   eval      vvnlev = 0
     c                   update    vvarlur
     c                   endif
      *
      * Forutsetter at egne søketekster i EVR kommer fra LVR, og sletter
      * disse
      *
     c                   eval      vsoklu_vare = p_var2
     c     vsoklu_key    setll     vsoklu
     c     vsoklu_key    reade     vsoklur                                91
     c                   dow       *in91 = *off
     c                   delete    vsoklur
     c     vsoklu_key    reade     vsoklur                                91
     c                   enddo
      *
      * Fjerner EAN-nummer tildelt av LVR-vare fra EAN-register i EVR
      *
     c                   eval      jvarl1_vare = p_var2
     c     jvarl1_key    chain     jvarl1                             91
      *
     c                   eval      veanl2_vare = p_var2
     c     veanl2_key    setll     veanl2
     c     veanl2_key    reade     veanl2r                                91
     c                   dow       *in91 = *off
     c                   if        vxeann = jvean1
     c                   delete    veanl2r
     c                   endif
     c     veanl2_key    reade     veanl2r                                91
     c                   enddo
      *
6.12 c                   eval      vvepl1_pvar = p_var2
6.12 c     vvepl1_key    setll     vvepl1
6.12 c     vvepl1_key    reade     vvepl1r                                91
6.11 c                   dow       *in91 = *off
6.12 c                   if        vepgti = jvean1
6.12 c                   delete    vvepl1r
6.11 c                   endif
6.12 c     vvepl1_key    reade     vvepl1r                                91
6.11 c                   enddo
      *
6.30 c                   eval      jvpkl1_vare = p_var2
6.30 c     jvpkl1_key    setll     jvpkl1
6.30 c     jvpkl1_key    reade     jvpkl1                                 91
     c                   dow       *in91 = *off
6.30 c                   if        jwgtin <> 0
     c                   eval      veanl2_vare = p_var2
     c     veanl2_key    setll     veanl2
     c     veanl2_key    reade     veanl2r                                92
     c                   dow       *in92 = *off
6.12 c                   if        vxeann = jwgtin
     c                   delete    veanl2r
     c                   endif
     c     veanl2_key    reade     veanl2r                                92
     c                   enddo
      *
6.12 c                   eval      vvepl1_pvar = p_var2
6.12 c     vvepl1_key    setll     vvepl1
6.12 c     vvepl1_key    reade     vvepl1r                                91
6.11 c                   dow       *in91 = *off
6.12 c                   if        vepgti = jwgtin
6.12 c                   delete    vvepl1r
6.11 c                   endif
6.12 c     vvepl1_key    reade     vvepl1r                                91
6.11 c                   enddo
     c                   endif
6.30 c     jvpkl1_key    reade     jvpkl1                                 91
     c                   enddo
      *
      * Kaller program for bytting av varenummer i EVR
      *
     c                   call      'VV740R'
     c                   parm                    w_firm
     c                   parm                    p_var2
     c                   parm                    jovar1
     c                   parm                    b_inlr
      *
      * Kaller program for bytting av varenummer i lager/innkjøp
      *
     c                   call      'FO740C'
     c                   parm                    w_firm
     c                   parm                    p_var2
     c                   parm                    jovar1
     c                   parm                    b_inlr
      *
      * Kaller program for oppdatering av søketekster i EVR
      *
     c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    jovar1
      *
      * Oppdaterer mathce-registeret med at varen ikke er matchet, og at
      * varen ikke er overført EVR
      *
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur                            91
     c                   if        *in91 = *off
     c                   eval      joldo2 = 0
     c                   eval      jovar2 = *blank
     c                   eval      jokode = 0
     c                   eval      jovkod = 0
     c                   update    jmatlur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_var2
      *
      * Key for oppslag på matche-register med LVR-varenummer
      *
     c     jmatl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl7_var2
      *
      * Key for oppdatering av vare-register EVR
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      * Key for oppdatering av søketekst-register EVR
      *
     c     vsoklu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsoklu_vare
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppdatering av EAN-register EVR
      *
     c     veanl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl2_vare
6.11  *
6.11  * Key for oppdatering av EAN på vare-pakning
6.11  *
6.12 c     vvepl1_key    klist
6.11 c                   kfld                    w_firm
6.12 c                   kfld                    vvepl1_pvar
      *
      * Key for oppslag på vare-pakning
      *
6.30 c     jvpkl1_key    klist
6.60 c                   kfld                    jvpkl1_vare
      *
      * Key for oppdatering av matche-register
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
