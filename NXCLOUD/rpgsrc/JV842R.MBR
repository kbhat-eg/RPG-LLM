     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV842R
      * Beskrivelse . . : Oppdater fra NIB, varegruppe
      *
      *                   Oppdaterer varegrupper (tekst/PSE)
      *                   Norgros/NIB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151105 GRo  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnxvgpf    uf   e           k disk
     fvogrlu    uf   e           k disk    rename(vogrpfr:vogrlur)
     fvhgrlu    uf   e           k disk    rename(vhgrpfr:vhgrlur)
     fvugrlu    uf   e           k disk    rename(vugrpfr:vugrlur)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
      *
      *  Dataelementer overgruppe
      *
     d                 ds
     d d_orec                       100
     d  d_ogrp                        2    overlay(d_orec:1)
     d   d_ogrp_num                   2  0 overlay(d_ogrp:1)
     d  d_osta                        1    overlay(d_orec:3)
     d   d_osta_num                   1  0 overlay(d_osta:1)
     d  d_obes                       35    overlay(d_orec:4)
     d  d_onod                       10    overlay(d_orec:39)
     d   d_onod_iso                    d   overlay(d_onod:1) datfmt(*iso)
     d  d_oned                       10    overlay(d_orec:49)
     d  d_ooda                       10    overlay(d_orec:59)
     d  d_oeda                       10    overlay(d_orec:69)
     d  d_oeti                       10    overlay(d_orec:79)
      *
      *  Dataelementer hovedgruppe
      *
     d                 ds
     d d_hrec                       100
     d  d_hgrp                        2    overlay(d_hrec:1)
     d   d_hgrp_num                   2  0 overlay(d_hgrp:1)
     d  d_hsta                        1    overlay(d_hrec:3)
     d   d_hsta_num                   1  0 overlay(d_hsta:1)
     d  d_hbes                       35    overlay(d_hrec:4)
     d  d_hnod                       10    overlay(d_hrec:39)
     d   d_hnod_iso                    d   overlay(d_hnod:1) datfmt(*iso)
     d  d_hned                       10    overlay(d_hrec:49)
     d  d_hoda                       10    overlay(d_hrec:59)
     d  d_heda                       10    overlay(d_hrec:69)
     d  d_heti                       10    overlay(d_hrec:79)
      *
      *  Dataelementer undergruppe
      *
     d                 ds
     d d_urec                       100
     d  d_ugrp                        3    overlay(d_urec:1)
     d   d_ugrp_num                   3  0 overlay(d_ugrp:1)
     d  d_usta                        1    overlay(d_urec:4)
     d   d_usta_num                   1  0 overlay(d_usta:1)
     d  d_ubes                       35    overlay(d_urec:5)
     d  d_ups1                        3    overlay(d_urec:40)
     d  d_ups2                        3    overlay(d_urec:43)
     d  d_unod                       10    overlay(d_urec:46)
     d   d_unod_iso                    d   overlay(d_unod:1) datfmt(*iso)
     d  d_uned                       10    overlay(d_urec:56)
     d  d_uoda                       10    overlay(d_urec:66)
     d  d_ueda                       10    overlay(d_urec:76)
     d  d_ueti                       10    overlay(d_urec:86)
      *
      * Definering av variable i nøkler
      *
     d vogrlu_oogr     s                   like(vgoogr)
     d vhgrlu_hogr     s                   like(vghogr)
     d vhgrlu_hhgr     s                   like(vghhgr)
     d vugrlu_uogr     s                   like(vguogr)
     d vugrlu_uhgr     s                   like(vguhgr)
     d vugrlu_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_kild          s              1    inz(*off)
     d b_ogrp          s              1    inz(*off)
     d b_hgrp          s              1    inz(*off)
     d b_ugrp          s              1    inz(*off)
     d b_gfel          s              1    inz(*off)
     d b_blnk          s              1    inz(*off)
      *
     d w_stda          s              3  0
     d w_slda          s              3  0
     d w_welm          s            400
     d w_wlen          s              3  0
     d w_decp          s              3  0
      *
     d w_str_i         s            400
     d w_str_u         s            400
     d w_posi          s              4  0
     d w_posu          s              4  0
     d w_str_x         s              1
      *
     d w_firm          s              3  0
      *
      *  Konstanter
      *
     d c_apos          c                   x'7D'
     d c_digits        c                   ' 0123456789'
     d c_nsep          c                   ','
      *
     d c_NIBk          c                   '0'
     d c_NVk           c                   '3'
      *
      * XML string konstanter
      *
     d c_xsle          c                   '</'
     d c_xend          c                   '>'
      *
      * XML feltnavn - meldingsspesifikke
      *
      * Hovedstruktur - Avsender
      *
     d c_xstr          c                   '<Avsender>'
     d c_kild          c                   '<Kilde>'
      *
      * Substruktur #1 - Overgruppe
      *
     d c_sogr_s        c                   '<Overgruppe>'
     d c_sogr_e        c                   '</Overgruppe>'
     d c_ogrp          c                   '<Ogrp>'
     d c_osta          c                   '<Status>'
     d c_obsk          c                   '<Beskrivelse>'
     d c_ohka          c                   '<Hovedkategori>'
     d c_oska          c                   '<Subkategori>'
     d c_ondo          c                   '<Nobb_oppr_dato>'
     d c_onde          c                   '<Nobb_endr_dato>'
     d c_ooda          c                   '<Oppr_dato>'
     d c_oeda          c                   '<Endr_dato>'
     d c_oetd          c                   '<Endr_tidspunkt>'
      *
      * Substruktur #2 - Hovedgruppe
      *
     d c_shgr_s        c                   '<Hovedgruppe>'
     d c_shgr_e        c                   '</Hovedgruppe>'
     d c_hgrp          c                   '<Hgrp>'
     d c_hsta          c                   '<Status>'
     d c_hbsk          c                   '<Beskrivelse>'
     d c_hhka          c                   '<Hovedkategori>'
     d c_hska          c                   '<Subkategori>'
     d c_hndo          c                   '<Nobb_oppr_dato>'
     d c_hnde          c                   '<Nobb_endr_dato>'
     d c_hoda          c                   '<Oppr_dato>'
     d c_heda          c                   '<Endr_dato>'
     d c_hetd          c                   '<Endr_tidspunkt>'
      *
      * Substruktur #3 - Undergruppe
      *
     d c_sugr_s        c                   '<Undergruppe>'
     d c_sugr_e        c                   '</Undergruppe>'
     d c_ugrp          c                   '<Ugrp>'
     d c_usta          c                   '<Status>'
     d c_ubsk          c                   '<Beskrivelse>'
     d c_uhka          c                   '<Hovedkategori>'
     d c_uska          c                   '<Subkategori>'
     d c_ups1          c                   '<PSE_prim>'
     d c_ups2          c                   '<PSE_sek>'
     d c_undo          c                   '<Nobb_oppr_dato>'
     d c_unde          c                   '<Nobb_endr_dato>'
     d c_uoda          c                   '<Oppr_dato>'
     d c_ueda          c                   '<Endr_dato>'
     d c_uetd          c                   '<Endr_tidspunkt>'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_inlr = *on
      *
      * Leser XML-fil varegruppe (overgruppe)
      *
     c                   read      nxvgpfr
     c                   dow       not %eof(nxvgpf)
      *
     c                   if        b_kild = *on
     c                   if        %subst(nxvgrp:1:12) = c_sogr_s
     c                   eval      b_ogrp = *on
     c                   exsr      beh_ogrp
     c                   endif
     c                   endif
      *
      *  Finn gyldig avsender/kilde
      *
     c                   eval      w_stda = %scan(c_xend:nxvgrp:1)
     c                   eval      w_slda = %scan(c_xsle:nxvgrp:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxvgrp:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxvgrp:1:7) = c_kild
     c                   exsr      uttr_kild
     c                   endsl
      *
     c                   delete    nxvgpfr
      *
     c                   read      nxvgpfr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av avsenderelement : Kilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_kild     begsr
      *
     c                   eval      b_kild = *off
      *
     c                   if        %trim(w_welm) = c_NIBk
     c                   eval      b_kild = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgruppe-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_ogrp      begsr
      *
     c                   eval      b_gfel = *off
     c                   exsr      ogrp_init
      *
     c                   dow       not %eof(nxvgpf)
      *
      *  Slutt på overgruppeelement
      *
     c                   if        %subst(nxvgrp:1:13) = c_sogr_e
     c                   exsr      skr_ogrp
     c                   leave
     c                   endif
      *
      *  Start på hovedgruppeelement
      *
     c                   if        %subst(nxvgrp:1:13) = c_shgr_s
     c                   if        b_gfel = *off
     c                   eval      b_hgrp = *on
     c                   exsr      beh_hgrp
     c                   endif
     c                   endif
      *
      *  Enkeltelement overgruppenivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxvgrp:1)
     c                   eval      w_slda = %scan(c_xsle:nxvgrp:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxvgrp:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxvgrp:1:6) = c_ogrp
     c                   exsr      uttr_ogrp
     c                   when      %subst(nxvgrp:1:8) = c_osta
     c                   exsr      uttr_osta
     c                   when      %subst(nxvgrp:1:13) = c_obsk
     c                   exsr      uttr_obsk
     c                   when      %subst(nxvgrp:1:15) = c_ohka
     c                   exsr      uttr_ohka
     c                   when      %subst(nxvgrp:1:13) = c_oska
     c                   exsr      uttr_oska
     c                   when      %subst(nxvgrp:1:16) = c_ondo
     c                   exsr      uttr_onod
     c                   when      %subst(nxvgrp:1:16) = c_onde
     c                   exsr      uttr_oned
     c                   when      %subst(nxvgrp:1:11) = c_ooda
     c                   exsr      uttr_ooda
     c                   when      %subst(nxvgrp:1:11) = c_oeda
     c                   exsr      uttr_oeda
     c                   when      %subst(nxvgrp:1:16) = c_oetd
     c                   exsr      uttr_oetd
     c                   endsl
      *
     c                   delete    nxvgpfr
      *
     c                   read      nxvgpfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av overgruppeinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ogrp_init     begsr
      *
     c                   eval      d_orec = *blank
     c                   eval      d_ogrp_num = *zero
     c                   eval      d_osta_num = *zero
     c                   eval      d_onod_iso = *loval
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Overgruppe-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ogrp     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_ogrp = %trim(w_welm)
     c                   eval      d_ogrp = %xlate(' ':'0':d_ogrp)
     c                   else
     c                   eval      b_gfel = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_osta     begsr
      *
     c                   eval      d_osta_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_osta = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_obsk     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_obes = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Hovedkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ohka     begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Subkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_oska     begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : NOBB oppr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_onod     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_onod = %trim(w_welm)
     c                   else
     c                   eval      d_onod_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : NOBB endr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_oned     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_oned = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Opprettet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ooda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_ooda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Endret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_oeda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_oeda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overgrp.element : Endringstidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_oetd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_oeti = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_ogrp      begsr
      *
     c                   if        b_gfel = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vogrlu_oogr = d_ogrp_num
     c     vogrlu_key    chain     vogrlur
      *
     c*                  if        d_osta = '4'
     c*                  if        %found
     c*                  delete    vogrlur
     c*                  endif
     c*                  leavesr
     c*                  endif
      *
     c                   if        %found
     c                   if        vgotxt <> d_obes
     c                   eval      vgotxt = d_obes
     c                   eval      vgoeus = l_user
     c                   time                    vgoeda
     c                   time                    vgoeti
     c                   update    vogrlur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgruppe-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_hgrp      begsr
      *
     c                   exsr      hgrp_init
      *
     c                   dow       not %eof(nxvgpf)
      *
      *  Slutt på hovedgruppeelement
      *
     c                   if        %subst(nxvgrp:1:14) = c_shgr_e
     c                   exsr      skr_hgrp
     c                   leave
     c                   endif
      *
      *  Start på undergruppeelement
      *
     c                   if        %subst(nxvgrp:1:13) = c_sugr_s
     c                   if        b_gfel = *off
     c                   eval      b_ugrp = *on
     c                   exsr      beh_ugrp
     c                   endif
     c                   endif
      *
      *  Enkeltelement hovedgruppenivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxvgrp:1)
     c                   eval      w_slda = %scan(c_xsle:nxvgrp:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxvgrp:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxvgrp:1:6) = c_hgrp
     c                   exsr      uttr_hgrp
     c                   when      %subst(nxvgrp:1:8) = c_hsta
     c                   exsr      uttr_hsta
     c                   when      %subst(nxvgrp:1:13) = c_hbsk
     c                   exsr      uttr_hbsk
     c                   when      %subst(nxvgrp:1:15) = c_hhka
     c                   exsr      uttr_hhka
     c                   when      %subst(nxvgrp:1:13) = c_hska
     c                   exsr      uttr_hska
     c                   when      %subst(nxvgrp:1:16) = c_hndo
     c                   exsr      uttr_hnod
     c                   when      %subst(nxvgrp:1:16) = c_hnde
     c                   exsr      uttr_hned
     c                   when      %subst(nxvgrp:1:11) = c_hoda
     c                   exsr      uttr_hoda
     c                   when      %subst(nxvgrp:1:11) = c_heda
     c                   exsr      uttr_heda
     c                   when      %subst(nxvgrp:1:16) = c_hetd
     c                   exsr      uttr_hetd
     c                   endsl
      *
     c                   delete    nxvgpfr
      *
     c                   read      nxvgpfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hovedgruppeinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hgrp_init     begsr
      *
     c                   eval      d_hrec = *blank
     c                   eval      d_hgrp_num = *zero
     c                   eval      d_hsta_num = *zero
     c                   eval      d_hnod_iso = *loval
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Hovedgruppe-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hgrp     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_hgrp = %trim(w_welm)
     c                   eval      d_hgrp = %xlate(' ':'0':d_hgrp)
     c                   else
     c                   eval      b_gfel = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hsta     begsr
      *
     c                   eval      d_hsta_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_hsta = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hbsk     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_hbes = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Hovedkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hhka     begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Subkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hska     begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : NOBB oppr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hnod     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_hnod = %trim(w_welm)
     c                   else
     c                   eval      d_hnod_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : NOBB endr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hned     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_hned = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Opprettet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hoda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_hoda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Endret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_heda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_heda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hovedgrp.element : Endringstidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hetd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_heti = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_hgrp      begsr
      *
     c                   if        b_gfel = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vhgrlu_hogr = d_ogrp_num
     c                   eval      vhgrlu_hhgr = d_hgrp_num
     c     vhgrlu_key    chain     vhgrlur
      *
     c*                  if        d_hsta = '4'
     c*                  if        %found
     c*                  delete    vhgrlur
     c*                  endif
     c*                  leavesr
     c*                  endif
      *
     c                   if        %found
     c                   if        vghtxt <> d_hbes
     c                   eval      vghtxt = d_hbes
     c                   eval      vgheus = l_user
     c                   time                    vgheda
     c                   time                    vgheti
     c                   update    vhgrlur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergruppe-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_ugrp      begsr
      *
     c                   exsr      ugrp_init
      *
     c                   dow       not %eof(nxvgpf)
      *
      *  Slutt på undergruppeelement
      *
     c                   if        %subst(nxvgrp:1:14) = c_sugr_e
     c                   exsr      skr_ugrp
     c                   leave
     c                   endif
      *
      *  Enkeltelement undergruppenivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxvgrp:1)
     c                   eval      w_slda = %scan(c_xsle:nxvgrp:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxvgrp:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxvgrp:1:6) = c_ugrp
     c                   exsr      uttr_ugrp
     c                   when      %subst(nxvgrp:1:8) = c_usta
     c                   exsr      uttr_usta
     c                   when      %subst(nxvgrp:1:13) = c_ubsk
     c                   exsr      uttr_ubsk
     c                   when      %subst(nxvgrp:1:15) = c_uhka
     c                   exsr      uttr_uhka
     c                   when      %subst(nxvgrp:1:13) = c_uska
     c                   exsr      uttr_uska
     c                   when      %subst(nxvgrp:1:10) = c_ups1
     c                   exsr      uttr_uup1
     c                   when      %subst(nxvgrp:1:9) = c_ups2
     c                   exsr      uttr_uup2
     c                   when      %subst(nxvgrp:1:16) = c_undo
     c                   exsr      uttr_unod
     c                   when      %subst(nxvgrp:1:16) = c_unde
     c                   exsr      uttr_uned
     c                   when      %subst(nxvgrp:1:11) = c_uoda
     c                   exsr      uttr_uoda
     c                   when      %subst(nxvgrp:1:11) = c_ueda
     c                   exsr      uttr_ueda
     c                   when      %subst(nxvgrp:1:16) = c_uetd
     c                   exsr      uttr_uetd
     c                   endsl
      *
     c                   delete    nxvgpfr
      *
     c                   read      nxvgpfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av undergruppeinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ugrp_init     begsr
      *
     c                   eval      d_urec = *blank
     c                   eval      d_ugrp_num = *zero
     c                   eval      d_usta_num = *zero
     c                   eval      d_unod_iso = *loval
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Undergruppe-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ugrp     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_ugrp = %trim(w_welm)
     c                   eval      d_ugrp = %xlate(' ':'0':d_ugrp)
     c                   else
     c                   eval      b_gfel = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_usta     begsr
      *
     c                   eval      d_usta_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_usta = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ubsk     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_ubes = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : PSE_prim
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_uup1     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_ups1 = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : PSE_sek
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_uup2     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_ups2 = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Hovedkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_uhka     begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Subkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_uska     begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : NOBB oppr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_unod     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_unod = %trim(w_welm)
     c                   else
     c                   eval      d_unod_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : NOBB endr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_uned     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_uned = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Opprettet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_uoda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_uoda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Endret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ueda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_ueda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av undergrp.element : Endringstidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_uetd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_ueti = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_ugrp      begsr
      *
     c                   if        b_gfel = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vugrlu_uogr = d_ogrp_num
     c                   eval      vugrlu_uhgr = d_hgrp_num
     c                   eval      vugrlu_uugr = d_ugrp_num
     c     vugrlu_key    chain     vugrlur
      *
     c*                  if        d_usta = '4'
     c*                  if        %found
     c*                  delete    vugrlur
     c*                  endif
     c*                  leavesr
     c*                  endif
      *
     c                   if        %found
     c                   if        vgutxt <> d_ubes or
     c                             (vguep1 <> d_ups1 and d_ups1 <> *blank) or
     c                             (vguep2 <> d_ups2 and d_ups2 <> *blank)
     c                   eval      vgutxt = d_ubes
     c                   eval      vguep1 = d_ups1
     c                   eval      vguep2 = d_ups2
     c                   eval      vgueus = l_user
     c                   time                    vgueda
     c                   time                    vgueti
     c                   update    vugrlur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakestilling av XML-escape tegn i tekstfelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_XML_esc  begsr
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      b_blnk = *off
      *
     c                   dow       w_posi < 395 and
     c                             b_blnk = *off
     c                   eval      w_posi = w_posi + 1
     c                   if        %subst(w_str_i:w_posi) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 395
     c                   select
     c                   when      %subst(w_str_i:w_posi:5) = '&amp;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '&'
     c                   eval      w_posi = w_posi + 4
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&lt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '<'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&gt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '>'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&apos;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = c_apos
     c                   eval      w_posi = w_posi + 5
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&quot;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '"'
     c                   eval      w_posi = w_posi + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for oppdatering av overgruppe
      *
     c     vogrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrlu_oogr
      *
      * Key for oppdatering av hovedgruppe
      *
     c     vhgrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrlu_hogr
     c                   kfld                    vhgrlu_hhgr
      *
      * Key for oppdatering av undergruppe
      *
     c     vugrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrlu_uogr
     c                   kfld                    vugrlu_uhgr
     c                   kfld                    vugrlu_uugr
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
