      /TITLE  ASOFAK Lev.vare, returnerer statuskode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW113R
      * Beskrivelse . . : Lev.vare, returnerer statuskode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       160801 HKO  Rettet feil ved kontroll av EAN-nummer
5.70  * PRGR  041108 BOF  Utivdet med prisgruppe
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.20  * 6.20  030411 bof  Utvidet vvprpf med prisgiver
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflval2    if   e           k disk    rename(flvapfr:flval2r)
     fflval4    if   e           k disk    rename(flvapfr:flval4r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fwfirm)
     d p_ldor          s                   like(fwldor)
     d p_line          s                   like(fwline)
     d p_lvar          s                   like(fwlvar)
     d p_tek1          s                   like(fwtek1)
     d p_eann          s                   like(fweann)
     d p_nobb          s                   like(fwnobb)
     d p_enhe          s                   like(fwenhe)
     d p_inpr          s                   like(fwinpr)
     d p_sapr          s                   like(fwsapr)
     d p_tvar          s                   like(fwtvar)
     d p_kvar          s              1  0
     d p_stat          s              1  0
     d p_indi          s              2  0
      *
      * Definering av variable i nøkler
      *
     d flval2_lvar     s                   like(fwlvar)
     d flval4_eann     s                   like(fweann)
     d venhl1_eenh     s                   like(vaeenh)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl5_lvar     s                   like(vvlvar)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
      *
      * Definering av variable
      *
     d b_oppd          s              1    inz(*off)
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
      *
8.01 d w_prgr          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer retur-parametre
      *
     c                   eval      p_stat = 0
     c                   eval      p_indi = 0
      *
      * Validering og retur av statuskode og feil-indikator
      *
     c                   exsr      sjekk_input
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for validering og retur av statuskode og feil-indikator
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
      * Leverandørs varenummer
      *
     c                   if        p_lvar = *blank
     c                   eval      p_stat = 1
     c                   eval      p_indi = 31
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      flval2_lvar = p_lvar
     c     flval2_key    setll     flval2
     c     flval2_key    reade     flval2                                 91
     c                   dow       *in91 = *off
     c                   if        fwline <> p_line
     c                   eval      p_stat = 1
     c                   eval      p_indi = 32
     c                   goto      end_sjekk
     c                   endif
     c     flval2_key    reade     flval2                                 91
     c                   enddo
      *
      * Varetekst
      *
     c                   if        p_tek1 = *blank
     c                   eval      p_stat = 2
     c                   eval      p_indi = 33
     c                   goto      end_sjekk
     c                   endif
      *
      * EAN-nummer
      *
     c                   if        p_eann <> 0
      *
     c                   eval      flval4_eann = p_eann
     c     flval4_key    setll     flval4
     c     flval4_key    reade     flval4                                 91
     c                   dow       *in91 = *off
     c                   if        fwline <> p_line
     c                   eval      p_stat = 3
     c                   eval      p_indi = 34
     c                   goto      end_sjekk
     c                   endif
     c     flval4_key    reade     flval4                                 91
     c                   enddo
6.11  *
6.11  * Kaller program for henting av varenr
6.11  *
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   if        vvldor <> fwldor or
     c                             vvlvar <> fwlvar
     c                   eval      p_stat = 3
     c                   eval      p_indi = 35
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Nobb-nummer
      *
     c                   if        p_nobb <> 0 and
     c                             p_nobb < 10000000
     c                   eval      p_stat = 4
     c                   eval      p_indi = 36
     c                   goto      end_sjekk
     c                   endif
      *
      * Enhet
      *
     c                   if        p_enhe = *blank
     c                   eval      p_stat = 5
     c                   eval      p_indi = 37
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      venhl1_eenh = p_enhe
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      p_stat = 5
     c                   eval      p_indi = 38
     c                   endif
      *
     c                   eval      vvarl5_lvar = p_lvar
     c     vvarl5_key    chain     vvarl5                             91
     c                   if        *in91 = *off
     c                   eval      b_oppd = *on
     c                   endif
      *
     c                   if        b_oppd = *on
5.70 c                   eval      vvprl1_vare = vvvare
6.20 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = w_prgr
5.70 c                   eval      vvprl1_enhe = p_enhe
5.70 c                   eval      vvprl1_lety = 0
5.70 c     vvprl1_key    chain     vvprl1                             91
5.70 c                   if        not %found(vvprl1)
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   endif
     c                   eval      vvprl1_vare = vvvare
     c                   eval      vvprl1_enhe = p_enhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    chain     vvprl1                             91
     c                   if        *in91 = *on
     c                   eval      p_stat = 5
     c                   eval      p_indi = 39
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Innkjøpspris
      *
     c                   if        p_inpr = 0
     c                   eval      p_stat = 6
     c                   eval      p_indi = 40
     c                   goto      end_sjekk
     c                   endif
      *
      * Salgspris
      *
     c                   if        p_sapr = 0
     c                   eval      p_stat = 6
     c                   eval      p_indi = 41
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekker om lev.varenummer allerede finnes som eget varenummer
      *
     c                   if        b_oppd = *off and
     c                             p_kvar = 1 and
     c                             p_tvar <> *blank
     c                   eval      vvarl1_vare = p_tvar
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      p_stat = 7
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Sjekker om vare skal utelates fra oppdatering
      *
     c                   if        b_oppd = *on
     c                   if        vvvare >= '10000000' or
     c                             vvkjus = 1
     c                   eval      p_stat = 8
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_line
     c                   parm                    p_lvar
     c                   parm                    p_tek1
     c                   parm                    p_eann
     c                   parm                    p_nobb
     c                   parm                    p_enhe
     c                   parm                    p_inpr
     c                   parm                    p_sapr
     c                   parm                    p_tvar
     c                   parm                    p_kvar
     c                   parm                    p_stat
     c                   parm                    p_indi
      *
      * Key for oppslag med leverandørs varenummer
      *
     c     flval2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval2_lvar
      *
      * Key for oppslag med EAN-nummer
      *
     c     flval4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval4_eann
      *
      * Key for oppslag på enhet-register
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-register med leverandørs varenummer
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppslag på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      *
      * Henter firma, leverandør og linjenummer fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
      *
     c                   endsr
