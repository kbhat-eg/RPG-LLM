     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . :
      * Program . . . . : CR701R
      * Beskrivelse . . : Readiness vare, leser inn min og maks fra Relex
      *
      * Spesial SH: Oppdaterer ABC-kode i lager-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030711 HKO  Nytt program
      *       090412 HKO  Tilpasset 6.20
7.01  *K000   190121 bsa  Implermentert spesial
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnrlxpf    if   e           k disk    rename(nrlxpfr:nrlxpfr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_lage          s                   like(vllage)
      *
     d b_frst          s              1    inz(*off)
      *
     d w_date          s               d
     d w_udat          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_date = %date
      *
     c                   read      nrlxpfr
     c                   dow       not %eof
      *
     c                   if        b_frst = *off
     c                   eval      w_firm = nrfirm
     c                   eval      w_lage = nrlage
     c                   eval      b_frst = *on
     c                   endif
      *
     c                   eval      vvarl1_vare = nrvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      neste_vare
     c                   endif
      *
     c                   eval      w_udat = vvudat
      *
      * Test på om lager skal oppdateres
      *
     c                   eval      vlagl1_vare = nrvare
     c                   eval      vlagl1_lage = w_lage
     c     vlagl1_key    chain     vlagl1r
     c                   if        not %found or
     c                             vlvtyp <> 'L' or
     c                             vlstda >= w_date or
     c                             vlmini = nrnmin and
     c                             vlmaks = nrnmax and
7.01 c                             vlkabc = nrkabc and
     c                             vlbmin <> 0
     c                   goto      neste_vare
     c                   endif
      *
      * ...Spesial Skattum
     c                   if        w_udat = *loval and
     c                             vludat <> *loval
     c                   eval      w_udat = vludat
     c                   endif
     c                   if        w_udat <> *loval and
     c                             w_udat < %date
     c                   eval      nrnmin = 0
     c                   eval      nrnmax = 0
     c                   endif
      *
      * Oppdaterer lager
      *
     c                   eval      vlaglu_vare = nrvare
     c                   eval      vlaglu_lage = w_lage
     c     vlaglu_key    chain     vlaglur
     c                   if        %found
     c                   eval      vlmini = nrnmin
     c                   eval      vlmaks = nrnmax
7.01 c                   eval      vlkabc = nrkabc
     c                   if        vlbmin = 0
     c                   eval      vlbmin = 1
     c                   endif
     c                   eval      vledat = %date
     c                   eval      vletim = %time
     c                   eval      vleusr = 'BN'
     c                   update    vlaglur
     c                   endif
      *
     c     neste_vare    tag
      *
     c                   read      nrlxpfr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppdatering av vare-lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
     c                   endsr
