**FREE
CTL-OPT MAIN(start) OPTION(*nodebugio : *SRCSTMT);
CTL-OPT datedit(*dmy);
CTL-OPT datfmt(*iso);
CTL-OPT dftactgrp(*no);

//      * System  . . . . : ASOFAK
//      * Program . . . . : XA410R
//      * Beskrivelse . . : Sletting av tilbud, akk = 0 (hele)
//      *
//      * Spesialversjon. :
//      * Tilpasset for . :
//      *
//      * Ref.   Dato  Sign Endringsbeskrivelse
//      * ----- ------ ---- -------------------------------------------------
//      *       220191 BKV  Nytt program

dcl-f FOHEPF usage(*input);
dcl-f VOTYPF usage(*input);



DCL-PROC start;
  DCL-PI *N EXTPGM;
    p_firm              packed(3:0);
    p_ordreNummer       packed(8:0);
    p_ordreSuffix       packed(2:0);
    p_kommentar1        char(30);
    p_kommentar2        char(30);
    p_slettekode        packed(2:0);
    p_konkurrentkode    packed(2:0);
    p_returTekst          char(50);
    p_returKode           packed(2:0);
  END-PI;

  dcl-s logTilbudOK  ind;
  dcl-s p_opdt  char(1) inz(*blank);
  dcl-s p_type  char(4) inz('*STD');

  dcl-s w_vaoakk packed(1:0);

  dcl-pr P_FO410R extpgm('FO410R');
    p_firm              packed(3:0);
    p_ordreNummer       packed(8:0);
    p_ordreSuffix       packed(2:0);
    p_type              char(4);
    p_kommentar1        char(30);
    p_kommentar2        char(30);
    p_opdt              char(1);
  end-pr;

  p_returTekst = 'Sletting feilet';
  p_returKode = -1;

  if (p_kommentar1 = *blank);
    p_returTekst = 'Sletting må ha en kommentar';
    p_returKode = -1;
  else;
    w_vaoakk =  FinnOrdreAkkumulator(p_firm  : p_ordreNummer : p_ordreSuffix :
                                     p_returTekst : p_returKode);
    if (w_vaoakk = 0);
      logTilbudOK = LoggTilbud(p_firm : p_ordreNummer :  p_slettekode : p_konkurrentkode :
                 p_kommentar1 : p_kommentar2 : p_returTekst :  p_returKode);
      if (logTilbudOK);
        CallP P_FO410R ( p_firm : p_ordreNummer : p_ordreSuffix :
                         p_type : p_kommentar1 : p_kommentar2 : p_opdt );
        p_returTekst = 'Sletting OK';
        p_returKode = 0;
      endif;
    elseif (w_vaoakk > 0);
      p_returTekst = 'Kan bare slette ordre/tilbud med behandlingskode = 0';
      p_returKode = -1;
    endif;
  endif;

  DSPLY (%subst(p_returTekst:1:40) + ' : ' + %char(p_returKode));

  close *all;

  *inlr = *on ;
END-PROC;


dcl-proc FinnOrdreAkkumulator;
  dcl-pi *N packed(1:0);
    p_firm              packed(3:0);
    p_ordreNummer       packed(8:0);
    p_ordreSuffix       packed(2:0);
    p_returTekst         char(50);
    p_returKode          packed(2:0);
  end-pi;

  dcl-s w_footyp char(2);
  dcl-s w_vaoakk packed(1:0);

  w_vaoakk = -1;

  EXEC SQL SELECT FOOTYP into :w_footyp FROM FOHEPF WHERE
           FOFIRM = :p_firm and FONUMM = :p_ordreNummer AND FOSUFF = :p_ordreSuffix;
  if (SQLCOD = 0) ;
    EXEC SQL SELECT VAOAKK into :w_vaoakk FROM VOTYPF WHERE
             VAOFIR = :p_firm and VAOTYP = :w_footyp;
    if (SQLCOD <> 0) ;
      p_returTekst = 'Akumuluator ikke funnet for ordretype:' + w_footyp;
      p_returKode = -1;
    endif;
  ELSE;
    p_returTekst = 'Ordre ikke funnet';
    p_returKode = -1;
  endif;

  return w_vaoakk;

END-PROC;

dcl-proc LoggTilbud;
  dcl-pi *N ind;
    p_firm             packed(3:0);
    p_ordreNummer      packed(8:0);
    p_slettekode       packed(2:0);
    p_konkurrentkode   packed(2:0);
    p_kommentar1       char(30);
    p_kommentar2       char(30);
    p_returTekst         char(50);
    p_returKode          packed(2:0);
  end-pi;

  dcl-s p_aktivitetsTtype   char(15) inz ('SLETTET');
  dcl-s Command             char(500) ;
  dcl-s xl700active         ind inz(*on);
  dcl-s p_slettegrunn       char(60);
  dcl-s w_xskkko            char(1);

  dcl-pr QCMDEXC extpgm ;
    *n char(500) options(*varsize) const ;
    *n packed(15:5) const ;
  end-pr ;

  dcl-pr P_XL700R extpgm('XL700R');
    p_ordreNummer       packed(8:0);
    p_aktivitetsTtype       char(15);
    p_slettekode       packed(2:0);
    p_konkurrentkode       packed(2:0);
    p_slettegrunn       char(60);
  end-pr;

  p_slettegrunn= p_kommentar1 + p_kommentar2;

  // logging gjøres i prg xl700r. kun hvis prg finnes
  Command = 'CHKOBJ OBJ(*LIBL/XL700R) OBJTYPE(*PGM)';
  monitor ;
    QCMDEXC(Command:%len(%trimr(Command))) ;
  on-error ;
    xl700active = *off;
  endmon ;

  if (xl700active = *on);

    if (p_slettekode = 0);
      p_returTekst = 'Slettekode må være satt';
      p_returKode = -1;
      return *off;
    endif;

    EXEC SQL SELECT XSKKKO into :w_xskkko FROM
             XTSLPF WHERE XSFIRM=0 AND XSSKOD = :p_slettekode;

    if ((w_xskkko = 'J') and (p_konkurrentkode = 0));
      p_returTekst = 'Konkurentkode må være satt';
      p_returKode = -1;
      return *off;
    endif;

    CallP P_XL700R ( p_ordreNummer : p_aktivitetsTtype : p_slettekode :
                     p_konkurrentkode : p_slettegrunn );
  endif;

  // TEST TEST TEST TEST
  //  p_returTekst = 'Konkurentkode må være satt';
  //  p_returKode = -1;
  //  return *off;


  return *on;

END-PROC;




