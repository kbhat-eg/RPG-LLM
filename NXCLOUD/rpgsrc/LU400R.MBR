      /TITLE  ASLAGR: Lagertelling
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LU400R
      *  Beskrivelse . . : Sletting av lagertelle-bunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            220900  Nytt program                            ASK
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flbhelu    uf   e           k disk    rename(lbhepfr:lbhelur)
     flbdtl1    if   e           k disk    rename(lbdtpfr:lbdtl1r)
     flbdtlu    uf   e           k disk    rename(lbdtpfr:lbdtlur)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i n√∏kler
      *
     d lbdtlu_batc     s                   like(lvbatc)
     d lbdtlu_numm     s                   like(lvnumm)
     d lbdtlu_line     s                   like(lvline)
     d lbdtl1_batc     s                   like(lvbatc)
     d lbdtl1_numm     s                   like(lvnumm)
     d lbhel1_batc     s                   like(lubatc)
     d lbhel1_numm     s                   like(lunumm)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
      *
      * Definering av variable i parametre
      *
     d p_batc          s                   like(lubatc)
     d p_deor          s              1
     d p_firm          s                   like(lufirm)
     d p_numm          s                   like(lunumm)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lufirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk tellebunt - hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lbhel1_key    chain     lbhelur                            92
     c                   if        *in92 = *off
     c                   unlock    lbhelu
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av tellebunt - linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lbdtl1_key    setll     lbdtl1r
      *
     c     lbdtl1_key    reade     lbdtl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      lbdtlu_line = lvline
     c     lbdtlu_key    chain     lbdtlur                            92
     c                   if        *in92 = *off
     c                   delete    lbdtlur
     c                   endif
      *
     c     lbdtl1_key    reade     lbdtl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av tellebunt - hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lbhelu_key    chain     lbhelur                            92
     c                   if        *in92 = *off
     c                   delete    lbhelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGER-TELLEBUNT-HODE
      *
     c     lbhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhel1_batc
     c                   kfld                    lbhel1_numm
      *
      *  Key for oppdatering av LAGER-TELLEBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for les av LAGER-TELLEBUNT-LINJE
      *
     c     lbdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl1_batc
     c                   kfld                    lbdtl1_numm
      *
      *  Key for oppdatering av LAGER-TELLEBUNT-LINJE
      *
     c     lbdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtlu_batc
     c                   kfld                    lbdtlu_numm
     c                   kfld                    lbdtlu_line
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_deor
     c                   parm                    p_firm
     c                   parm                    p_batc
     c                   parm                    p_numm
      *
      *  Legger inn startverdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      lbhel1_batc = p_batc
     c                   eval      lbhel1_numm = p_numm
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbhelu_numm = p_numm
     c                   eval      lbdtl1_batc = p_batc
     c                   eval      lbdtl1_numm = p_numm
     c                   eval      lbdtlu_batc = p_batc
     c                   eval      lbdtlu_numm = p_numm
     c                   eval      lbdtlu_line = 0
      *
     c                   endsr
