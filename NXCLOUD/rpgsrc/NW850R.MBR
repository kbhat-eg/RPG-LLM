     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NW850R
      * Beskrivelse . . : WAP, API - ekspedisjon-inngang
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051103 HKO  Nytt program
      *       180304 HKO  Tilpasset nye feltlenger
6.nu  *       160614 bof  Endring vedr. nummerutvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fhohelu    uf a e           k disk    rename(hohepfr:hohelur)
     fhodtlu    uf a e           k disk    rename(hodtpfr:hodtlur)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_avde          s              4  0
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                       147
      *
      * Datakø - inn (hode)
      *
     d                 ds
     d d_hode                       147
     d  d_kund                        9  0 overlay(d_hode:  1)
     d  d_kpro                       12    overlay(d_hode: 10)
     d  d_ladr                       35    overlay(d_hode: 22)
     d  d_dref                       35    overlay(d_hode: 57)
     d  d_rekv                       20    overlay(d_hode: 92)
     d  d_navn                       35    overlay(d_hode:112)
     d  d_paks                        1  0 overlay(d_hode:147)
      *
      * Datakø - inn (linje)
      *
     d                 ds
     d d_linj                        30
     d  d_vare                       16    overlay(d_linj: 1)
     d  d_enhe                        3    overlay(d_linj:17)
     d  d_anta                       11  3 overlay(d_linj:20)
      *
      * Datakø - ut
      *
     d                 ds
     d d_outp                        15
     d  d_refn                       15    overlay(d_outp:1)
      *
      * Definering av variable i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
      *
     d w_kpro          s              6  0
     d w_kod2          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
      *
     d w_firm          s                   like(hofirm)
6.nu d w_refn          s              8  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_frst = *on
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Skriver til output-datakø
      *
     c                   movel     w_refn        d_refn
      *
     c                   call      'QSNDDTAQ'
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_outl
     c                   parm                    d_outp
     c                   parm                    p_keyl
     c                   parm                    p_uniq
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordreinngang
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        b_frst = *on
     c                   exsr      behandl_hode
     c                   else
     c                   exsr      behandl_linje
     c                   endif
      *
     c                   eval      b_frst = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av ekspedisjon-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_hode  begsr
      *
     c                   eval      d_hode = d_inpu
      *
     c                   movel     d_kpro        w_kpro
      *
      * Henter neste referansenummer
      *
     c                   eval      w_kod2 = '0'
     c                   eval      w_fell = 'LRADIO'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
     c                   call      'AS100R'
     c                   parm                    w_kod2
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_refn
      *
      * Oppretter håndterminal ordre-hode
      *
     c                   eval      hofirm = w_firm
     c                   eval      horefn = w_refn
     c                   eval      hokund = d_kund
     c                   eval      hokpro = w_kpro
     c                   eval      hoselg = 0
     c                   eval      honavn = %trim(d_ladr)
     c                   eval      hodref = %trim(d_dref)
     c                   eval      horekv = %trim(d_rekv)
     c                   eval      hostat = 0
      *
     c                   if        honavn = *blank
     c                   eval      fkprl1_kund = d_kund
     c                   eval      fkprl1_kpro = w_kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      honavn = flnavn
     c                   endif
     c                   endif
      *
     c                   if        hodref = *blank
     c                   eval      hodref = %trim(d_navn)
     c                   endif
      *
     c                   time                    hodato
     c                   time                    hotime
      *
     c                   write     hohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_linje begsr
      *
     c                   eval      d_linj = d_inpu
      *
     c                   eval      hdfirm = w_firm
     c                   eval      hdrefn = w_refn
     c                   eval      hdvare = 0
     c                   eval      hdenhe = d_enhe
     c                   eval      hdanta = d_anta
      *
     c                   movel     d_vare        hdvare
      *
     c                   time                    hddato
     c                   time                    hdtime
      *
     c                   write     hodtlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = *on
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_avde
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      *
      * Hent firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
