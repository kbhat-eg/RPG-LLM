     h option(*nodebugio : *srcstmt) datedit(*dmy)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV824R
      * Beskrivelse . . : Oppdater fra SOLVE varegrupper
      *
      *                   Oppdaterer varegr  SOLVE  (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020519 BKV
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrlu    uf a e           k disk    rename(vogrpfr:vogrlur)
     fvhgrlu    uf a e           k disk    rename(vhgrpfr:vhgrlur)
     fvugrlu    uf a e           k disk    rename(vugrpfr:vugrlur)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
      *
      * Definering av variable i nøkler
      *
     d vogrlu_oogr     s                   like(vgoogr)
     d vhgrlu_hogr     s                   like(vghogr)
     d vhgrlu_hhgr     s                   like(vghhgr)
     d vugrlu_uogr     s                   like(vguogr)
     d vugrlu_uhgr     s                   like(vguhgr)
     d vugrlu_uugr     s                   like(vguugr)
      *
      * Brukes som parametre til jobblogging
      *
     d p_jbid          s             41
     d p_jsts          s              2  0
     d p_jmld          s            200
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
     d w_firm          s              3  0
      *
      * Definering av parametre
      *
     D p_filNavn       s            255a
     D p_lengde        s              3p 0
     d p_sistDato      s             10a
     d p_stat          s              1
      *
      *  Dataelementer varegruppe
      *
      *
      * Definering av variable
      *
     d w_uogr          s              2
     d w_uotx          s             35
     d w_uhgr          s              4
     d w_uhtx          s             25
     d w_uugr          s              7
     d w_uutx          s             35
      *
     D   navnet        s            100a   varying
6.33 D   len           s              3i 0
      *
      * Definering av konstanter
      *
     d c_digi          s             10    inz('0123456789')
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       len = p_lengde;
       navnet = %trim(%subst(p_filNavn:1:len));

       // setter navn på XML fil
       exec sql SET VAREGRUPPE_SOLVE_XML_V = :navnet;

       // bruk view til å lese XML fil
       exec sql
         declare c1 cursor for
            select
                 OVERGRUPPENR
                ,OVERGRUPPEBESKRIVELSE
                ,HOVEDGRUPPENR
                ,HOVDEGRUPPEBESKRIVELSE
                ,UNDERGRUPPENR
                ,UNDERGRUPPEBESKRIVELSE
                 from VAREGRUPPE_XML_SOLVE_VIEW
                for read only;

       exec sql
         Open c1;

       doW (1 = 1);

         exec sql
           Fetch c1 INTO :w_uogr, :w_uotx,:w_uhgr, :w_uhtx,:w_uugr, :w_uutx;

         if ((SQLCode < 0) or (SQLCode = 100));
           leave;
         endif;

         if %check(c_digi : %trim(w_uogr)) = 0;
           exsr skr_ogrp;
           exsr skr_hgrp;
           exsr skr_ugrp;
         endif;

       enddo;

       exec sql close c1;

       p_sistDato = %char(%date(): *iso);  // OK

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag

     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_ogrp      begsr
      *

      *
     c                   eval      vogrlu_oogr = %dec(w_uogr:2:0)
     c     vogrlu_key    chain     vogrlur
      *
     c                   if        %found
     c                   if        vgotxt <> w_uotx
     c                   eval      vgotxt = w_uotx
     c                   eval      vgoeus = l_user
     c                   time                    vgoeda
     c                   time                    vgoeti
     c                   update    vogrlur
     c                   endif
     c                   else
     c                   clear                   vogrlur
     c                   eval      vgofir = w_firm
     c                   eval      vgoogr = %dec(w_uogr:2:0)
     c                   eval      vgotxt = w_uotx
      *
     c                   eval      vgoous = l_user
     c                   time                    vgooda
     c                   time                    vgooti
     c                   eval      vgoeus = l_user
     c                   time                    vgoeda
     c                   time                    vgoeti
      *
     c                   write     vogrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_hgrp      begsr
      *
     c                   eval      vhgrlu_hogr = %dec(w_uogr:2:0)
     c                   eval      vhgrlu_hhgr = %dec(%SUBST(w_uhgr:3:2):2:0)
     c     vhgrlu_key    chain     vhgrlur
      *
     c                   if        %found
     c                   if        vghtxt <> w_uhtx
     c                   eval      vghtxt = w_uhtx
     c                   eval      vgheus = l_user
     c                   time                    vgheda
     c                   time                    vgheti
     c                   update    vhgrlur
     c                   endif
     c                   else
     c                   clear                   vhgrlur
     c                   eval      vghfir      = w_firm
     c                   eval      vghogr      = %dec(w_uogr:2:0)
     c                   eval      vghhgr      = %dec(%SUBST(w_uhgr:3:2):2:0)
     c                   eval      vghtxt      = w_uhtx
      *
     c                   eval      vgheus = l_user
     c                   time                    vgheda
     c                   time                    vgheti
     c                   eval      vghous = l_user
     c                   time                    vghoda
     c                   time                    vghoti
      *
     c                   write     vhgrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_ugrp      begsr
      *
     c                   eval      vugrlu_uogr = %dec(w_uogr:2:0)
     c                   eval      vugrlu_uhgr = %dec(%SUBST(w_uhgr:3:2):2:0)
     c                   eval      vugrlu_uugr = %dec(%SUBST(w_uugr:5:3):3:0)
     c     vugrlu_key    chain     vugrlur
      *
     c                   if        %found
     c                   if        (vgutxt <> w_uutx)
     c                   eval      vgutxt = w_uutx
     c                   eval      vgueus = l_user
     c                   time                    vgueda
     c                   time                    vgueti
     c                   update    vugrlur
     c                   endif
     c                   else
     c                   clear                   vugrlur
      *
     c                   eval      vgufir = w_firm
     c                   eval      vguogr = %dec(w_uogr:2:0)
     c                   eval      vguhgr = %dec(%SUBST(w_uhgr:3:2):2:0)
     c                   eval      vguugr = %dec(%SUBST(w_uugr:5:3):3:0)
     c                   eval      vgutxt = w_uutx
      *
     c                   eval      vguous = l_user
     c                   time                    vguoda
     c                   time                    vguoti
     c                   eval      vgueus = l_user
     c                   time                    vgueda
     c                   time                    vgueti
      *
     c                   write     vugrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_filNavn
     c                   parm                    p_lengde
     c                   parm                    p_sistDato
     c                   parm                    p_stat
      *
      * Key for oppdatering av overgruppe
      *
     c     vogrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrlu_oogr
      *
      * Key for oppdatering av hovedgruppe
      *
     c     vhgrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrlu_hogr
     c                   kfld                    vhgrlu_hhgr
      *
      * Key for oppdatering av undergruppe
      *
     c     vugrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrlu_uogr
     c                   kfld                    vugrlu_uhgr
     c                   kfld                    vugrlu_uugr
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
