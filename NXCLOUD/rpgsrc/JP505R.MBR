     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP505R
      * Beskrivelse . . : Betingelse, velge betingelse i ordre
      *
      *                   Programmet returnerer betingelsesnivå, betingelse
      *                   og innkjøpsprisfaktor/nettopris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280100 HKO  Nytt program
      *       180504 HKO  Leter ikke etter betingelse mot vareeier
      *       200905 HKO  Utvidet returparameter med varetype og
      *                   leveringstype
6.30  * 6.30  261114 Bsa  Innfører F tast for visning av NOBB info. Erstatter
6.30  *                   koding i jacada.
6.3x  * 6.30  310515 bof  Utvidet med prisgruppe
6.31  * 6.30  080816 nho  Viser om betingelse er aktiv
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
     fjbetlr    if   e           k disk    rename(jbetpfr:jbetlrr)
     fjbetl2    if   e           k disk    rename(jbetpfr:jbetl2r)
     fjbetl3    if   e           k disk    rename(jbetpfr:jbetl3r)
     fjbetl4    if   e           k disk    rename(jbetpfr:jbetl4r)
     fjbetl5    if   e           k disk    rename(jbetpfr:jbetl5r)
     fjbetl6    if   e           k disk    rename(jbetpfr:jbetl6r)
     fjbetl7    if   e           k disk    rename(jbetpfr:jbetl7r)
     fjbetl8    if   e           k disk    rename(jbetpfr:jbetl8r)
6.3x fjbetla    if   e           k disk    rename(jbetpfr:jbetlar)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.3x fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvvtyl1    if   e           k disk    rename(vvtypfr:vvtyl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
      *
     fjp505d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jpfirm)
     d p_ldor          s                   like(jpldor)
6.3x d p_prgr          s                   like(jpprgr)
     d p_vare          s                   like(jvvare)
     d p_bldo          s                   like(jpldor)
6.3x d p_bprg          s                   like(jpprgr)
     d p_bogr          s                   like(jpogrp)
     d p_bhgr          s                   like(jphgrp)
     d p_bugr          s                   like(jpugrp)
     d p_bmod          s                   like(jpmodn)
     d p_bvar          s                   like(jpvare)
     d p_bvty          s                   like(jpvtyp)
     d p_blet          s                   like(jplety)
     d p_bbet          s                   like(jpbeti)
     d p_ntpr          s              9  2
     d p_ifak          s              8  6
6.30 d p_nobb          s              8
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
6.3x d jbetl1_prgr     s                   like(jpprgr)
     d jbetl1_ogrp     s                   like(jpogrp)
     d jbetl1_hgrp     s                   like(jphgrp)
     d jbetl1_ugrp     s                   like(jpugrp)
     d jbetl1_modn     s                   like(jpmodn)
     d jbetl1_vare     s                   like(jpvare)
6.3x d jbetlr_prgr     s                   like(jpprgr)
     d jbetlr_ogrp     s                   like(jpogrp)
     d jbetlr_hgrp     s                   like(jphgrp)
     d jbetlr_ugrp     s                   like(jpugrp)
     d jbetlr_modn     s                   like(jpmodn)
     d jbetlr_vare     s                   like(jpvare)
     d jbetlr_vtyp     s                   like(jpvtyp)
     d jbetlr_lety     s                   like(jplety)
     d jbetlr_rsts     s                   like(jprsts)
     d jbetlr_line     s                   like(jpline)
     d jbetl2_hgrp     s                   like(jphgrp)
     d jbetl3_ugrp     s                   like(jpugrp)
     d jbetl4_modn     s                   like(jpmodn)
     d jbetl5_vare     s                   like(jpvare)
     d jbetl6_vtyp     s                   like(jpvtyp)
     d jbetl7_lety     s                   like(jplety)
     d jbetl8_beti     s                   like(jpbeti)
6.3x d jbetla_ogrp     s                   like(jpogrp)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_modn     s                   like(jumodn)
     d jvarl1_vare     s                   like(jvvare)
     d vvtyl1_vtyp     s                   like(vavtyp)
     d vltpl1_lety     s                   like(vylety)
6.3x d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(jpfirm)
     d w_ldor          s                   like(jpldor)
6.3x d w_prgr          s                   like(jpprgr)
      *
     d w_vare          s                   like(jpvare)
6.3x d w_ptxt          s                   like(vgotxt)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_mte1          s                   like(jumte1)
     d w_tek1          s                   like(jvtek1)
     d w_ttxt          s                   like(vavtxt)
     d w_lbes          s                   like(vybesk)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F23=Bytter mellom å vise faktor og nettopris
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   else
     c                   eval      *in80 = *on
     c                   endif
      *
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
      *
     c                   endif
      *
      * Posisjonering
      *
6.3x c                   if        b2prgr <> *blank or
6.3x c                             b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank or
     c                             b2vtyp <> *blank or
     c                             b2lety <> 0 or
     c                             b2beti <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jbetl2_hgrp = b1hgrp
     c     jbetl2_key    setll     jbetl2
     c                   when      w_seqe = 'U'
     c                   eval      jbetl3_ugrp = b1ugrp
     c     jbetl3_key    setll     jbetl3
     c                   when      w_seqe = 'M'
     c                   eval      jbetl4_modn = b1modn
     c     jbetl4_key    setll     jbetl4
     c                   when      w_seqe = 'V'
     c                   eval      jbetl5_vare = b1vare
     c     jbetl5_key    setll     jbetl5
     c                   when      w_seqe = 'T'
     c                   eval      jbetl6_vtyp = b1vtyp
     c     jbetl6_key    setll     jbetl6
     c                   when      w_seqe = 'L'
     c                   eval      jbetl7_lety = b1lety
     c     jbetl7_key    setll     jbetl7
     c                   when      w_seqe = 'B'
     c                   eval      jbetl8_beti = b1beti
     c     jbetl8_key    setll     jbetl8
     c                   other
6.3x c                   eval      jbetl1_prgr = b1prgr
     c                   eval      jbetl1_ogrp = b1ogrp
     c                   eval      jbetl1_hgrp = b1hgrp
     c                   eval      jbetl1_ugrp = b1ugrp
     c                   eval      jbetl1_modn = b1modn
     c                   eval      jbetl1_vare = b1vare
     c     jbetl1_key    setll     jbetl1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
6.3x  *
6.3x  * Posisjoner startverdi på overgruppe
6.3x  *
6.3x c                   if        b2prgr <> *blank
6.3x c                   eval      w_seqe = 'O'
6.3x c                   eval      jbetla_ogrp = b2ogrp
6.3x c     jbetla_key    setll     jbetla
6.3x c                   goto      end_pos
6.3x c                   endif
      *
      * prisgruppe
      *
     c                   if        b2ogrp <> 0
6.3x c                   eval      w_seqe = *blank
6.3x c                   eval      jbetl1_prgr = b2prgr
     c                   eval      jbetl1_ogrp = b2ogrp
     c                   eval      jbetl1_hgrp = b2hgrp
     c                   eval      jbetl1_ugrp = b2ugrp
     c                   eval      jbetl1_modn = b2modn
     c                   eval      jbetl1_vare = b2vare
     c     jbetl1_key    setll     jbetl1
     c                   goto      end_pos
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      jbetl2_hgrp = b2hgrp
     c     jbetl2_key    setll     jbetl2
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      jbetl3_ugrp = b2ugrp
     c     jbetl3_key    setll     jbetl3
     c                   goto      end_pos
     c                   endif
      *
      * Modulnummer
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      jbetl4_modn = b2modn
     c     jbetl4_key    setll     jbetl4
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      jbetl5_vare = b2vare
     c     jbetl5_key    setll     jbetl5
     c                   goto      end_pos
     c                   endif
      *
      * Varetype
      *
     c                   if        b2vtyp <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      jbetl6_vtyp = b2vtyp
     c     jbetl6_key    setll     jbetl6
     c                   goto      end_pos
     c                   endif
      *
      * Leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      jbetl7_lety = b2lety
     c     jbetl7_key    setll     jbetl7
     c                   goto      end_pos
     c                   endif
      *
      * Betingelse
      *
     c                   if        b2beti <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      jbetl8_beti = b2beti
     c     jbetl8_key    setll     jbetl8
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
     c                   eval      b2vtyp = *blank
     c                   eval      b2lety = 0
     c                   eval      b2beti = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg betingelse
      *
     c                   if        b1valg = 1
     c                   eval      p_bldo = b2ldor
6.3x c                   eval      p_bprg = b2prgr
     c                   eval      p_bogr = b1ogrp
     c                   eval      p_bhgr = b1hgrp
     c                   eval      p_bugr = b1ugrp
     c                   eval      p_bmod = b1modn
     c                   eval      p_bvar = b1vare
     c                   eval      p_bvty = b1vtyp
     c                   eval      p_blet = b1lety
     c                   eval      p_bbet = b1beti
     c                   eval      p_ntpr = b1ntpr
     c                   eval      p_ifak = b1fakt
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
      * Vis betingelse
      *
     c                   if        b1valg = 5
     c                   eval      c2ldor = b2ldor
6.3x c                   eval      c2prgr = b1prgr
     c                   eval      c2ogrp = b1ogrp
     c                   eval      c2hgrp = b1hgrp
     c                   eval      c2ugrp = b1ugrp
     c                   eval      c2modn = b1modn
     c                   eval      c2vare = b1vare
     c                   eval      c2vtyp = b1vtyp
     c                   eval      c2lety = b1lety
     c                   eval      c2rsts = b1rsts
     c                   eval      c2line = b1line
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis rabattelement
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b1vare
     c                   call      'JP510R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    b1prgr
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1modn
     c                   parm                    w_vare
     c                   parm                    b1vtyp
     c                   parm                    b1lety
     c                   parm                    b1rsts
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   exsr      finn_fant
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      jbetlr_ogrp = c2ogrp
6.3x c                   eval      jbetlr_prgr = c2prgr
     c                   eval      jbetlr_hgrp = c2hgrp
     c                   eval      jbetlr_ugrp = c2ugrp
     c                   eval      jbetlr_modn = c2modn
     c                   eval      jbetlr_vare = c2vare
     c                   eval      jbetlr_vtyp = c2vtyp
     c                   eval      jbetlr_lety = c2lety
     c                   eval      jbetlr_rsts = c2rsts
     c                   eval      jbetlr_line = c2line
     c     jbetlr_key    chain     jbetlr
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2beti = jpbeti
     c                   eval      c2eusr = jpeusr
     c                   eval      c2nobb = 0
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2dato = 0
     c                   if        jpfdat <> *loval
     c     *dmy          move      jpfdat        c2fdat
     c                   endif
     c                   if        jptdat <> *loval
     c     *dmy          move      jptdat        c2tdat
     c                   endif
     c                   if        jpdato <> *loval
     c     *dmy          move      jpdato        c2dato
     c                   endif
     c     *dmy          move      jpodat        c2odat
     c     *dmy          move      jpedat        c2edat
      *
      * Henter beskrivelse av nøkkelfelter
      *
     c                   eval      c2lnav = b2lnav
      *
6.3x c                   eval      c2ptxt = *blank
     c                   eval      c2otxt = *blank
     c                   eval      c2htxt = *blank
     c                   eval      c2utxt = *blank
     c                   eval      c2mte1 = *blank
     c                   eval      c2tek1 = *blank
     c                   eval      c2ttxt = *blank
     c                   eval      c2lbes = *blank
      *
6.3x c                   if        c2prgr <> *blank
6.3x c                   eval      ra30l1_dkod = c2prgr
6.3x c     ra30l1_key    chain     ra30l1
6.3x c                   eval      c2ptxt = redtxt
6.3x c                   endif
      *
     c                   if        c2ogrp <> 0
     c                   eval      vogrl1_oogr = c2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   eval      c2otxt = vgotxt
     c                   endif
      *
     c                   if        c2hgrp <> 0
     c                   eval      vhgrl1_hogr = c2ogrp
     c                   eval      vhgrl1_hhgr = c2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      c2htxt = vghtxt
     c                   endif
      *
     c                   if        c2ugrp <> 0
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   eval      c2utxt = vgutxt
     c                   endif
      *
     c                   if        c2modn <> 0
     c                   eval      jmodl1_modn = c2modn
     c     jmodl1_key    chain     jmodl1
     c                   eval      c2mte1 = jumte1
     c                   endif
      *
     c                   if        c2vare <> *blank
     c                   eval      jvarl1_vare = c2vare
     c     jvarl1_key    chain     jvarl1
     c                   eval      c2tek1 = jvtek1
     c                   eval      c2nobb = jvnobb
     c                   endif
      *
     c                   if        c2vtyp <> *blank
     c                   eval      vvtyl1_vtyp = c2vtyp
     c     vvtyl1_key    chain     vvtyl1
     c                   eval      c2ttxt = vavtxt
     c                   endif
      *
     c                   if        c2lety <> 0
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1
     c                   eval      c2lbes = vybesk
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * F13=Vise rabattelement
      *
     c                   if        *inkm = *on
     c                   call      'JP510R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    c2prgr
     c                   parm                    c2ogrp
     c                   parm                    c2hgrp
     c                   parm                    c2ugrp
     c                   parm                    c2modn
     c                   parm                    c2vare
     c                   parm                    c2vtyp
     c                   parm                    c2lety
     c                   parm                    c2rsts
     c                   parm                    c2line
     c                   goto      c2tagb
     c                   endif
6.30  *
6.30  * F18=NOBB informasjon
6.30  *
6.30 c                   if        *inks = *on
6.30 c                   exsr      vis_nobb
6.30 c                   goto      c2tagb
6.30 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   read      jbetl2                                 15
     c                   when      w_seqe = 'U'
     c                   read      jbetl3                                 15
     c                   when      w_seqe = 'M'
     c                   read      jbetl4                                 15
     c                   when      w_seqe = 'V'
     c                   read      jbetl5                                 15
     c                   when      w_seqe = 'T'
     c                   read      jbetl6                                 15
     c                   when      w_seqe = 'L'
     c                   read      jbetl7                                 15
     c                   when      w_seqe = 'B'
     c                   read      jbetl8                                 15
6.3x c                   when      w_seqe = 'O'
6.3x c                   read      jbetla                                 15
     c                   other
     c                   read      jbetl1                                 15
     c                   endsl
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        jpfirm <> w_firm or
     c                             jpldor <> w_ldor
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jbetl2_hgrp = jphgrp
     c                   when      w_seqe = 'U'
     c                   eval      jbetl3_ugrp = jpugrp
     c                   when      w_seqe = 'M'
     c                   eval      jbetl4_modn = jpmodn
     c                   when      w_seqe = 'V'
     c                   eval      jbetl5_vare = jpvare
     c                   when      w_seqe = 'T'
     c                   eval      jbetl6_vtyp = jpvtyp
     c                   when      w_seqe = 'L'
     c                   eval      jbetl7_lety = jplety
     c                   when      w_seqe = 'B'
     c                   eval      jbetl8_beti = jpbeti
6.3x c                   when      w_seqe = 'O'
6.3x c                   eval      jbetla_ogrp = jpogrp
     c                   other
6.3x c                   eval      jbetl1_prgr = jpprgr
     c                   eval      jbetl1_ogrp = jpogrp
     c                   eval      jbetl1_hgrp = jphgrp
     c                   eval      jbetl1_ugrp = jpugrp
     c                   eval      jbetl1_modn = jpmodn
     c                   eval      jbetl1_vare = jpvare
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1rsts = jprsts
     c                   eval      b1line = jpline
6.3x c                   eval      b1prgr = jpprgr
     c                   eval      b1ogrp = jpogrp
     c                   eval      b1hgrp = jphgrp
     c                   eval      b1ugrp = jpugrp
     c                   eval      b1modn = jpmodn
     c                   eval      b1vare = jpvare
     c                   eval      b1vtyp = jpvtyp
     c                   eval      b1lety = jplety
     c                   eval      b1beti = jpbeti
      *
      * Finner nettopris eller rabatt fra rabattelementene
      *
     c                   exsr      finn_fant
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'H'
     c     jbetl2_key    setgt     jbetl2
     c                   readp     jbetl2
     c                   when      w_seqe = 'U'
     c     jbetl3_key    setgt     jbetl3
     c                   readp     jbetl3
     c                   when      w_seqe = 'M'
     c     jbetl4_key    setgt     jbetl4
     c                   readp     jbetl4
     c                   when      w_seqe = 'V'
     c     jbetl5_key    setgt     jbetl5
     c                   readp     jbetl5
     c                   when      w_seqe = 'T'
     c     jbetl6_key    setgt     jbetl6
     c                   readp     jbetl6
     c                   when      w_seqe = 'L'
     c     jbetl7_key    setgt     jbetl7
     c                   readp     jbetl7
     c                   when      w_seqe = 'B'
     c     jbetl8_key    setgt     jbetl8
     c                   readp     jbetl8
6.3x c                   when      w_seqe = 'O'
6.3x c     jbetla_key    setgt     jbetla
6.3x c                   readp     jbetla
     c                   other
     c     jbetl1_key    setgt     jbetl1
     c                   readp     jbetl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   readp     jbetl2
     c                   when      w_seqe = 'U'
     c                   readp     jbetl3
     c                   when      w_seqe = 'M'
     c                   readp     jbetl4
     c                   when      w_seqe = 'V'
     c                   readp     jbetl5
     c                   when      w_seqe = 'T'
     c                   readp     jbetl6
     c                   when      w_seqe = 'L'
     c                   readp     jbetl7
     c                   when      w_seqe = 'B'
     c                   readp     jbetl8
6.3x c                   when      w_seqe = 'O'
6.3x c                   readp     jbetla
     c                   other
     c                   readp     jbetl1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   if        jpfirm <> w_firm or
     c                             jpldor <> w_ldor
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jbetl2_hgrp = jphgrp
     c                   when      w_seqe = 'U'
     c                   eval      jbetl3_ugrp = jpugrp
     c                   when      w_seqe = 'M'
     c                   eval      jbetl4_modn = jpmodn
     c                   when      w_seqe = 'V'
     c                   eval      jbetl5_vare = jpvare
     c                   when      w_seqe = 'T'
     c                   eval      jbetl6_vtyp = jpvtyp
     c                   when      w_seqe = 'L'
     c                   eval      jbetl7_lety = jplety
     c                   when      w_seqe = 'B'
     c                   eval      jbetl8_beti = jpbeti
6.3x c                   when      w_seqe = 'O'
6.3x c                   eval      jbetla_ogrp = jpogrp
     c                   other
     c                   eval      jbetl1_prgr = jpprgr
     c                   eval      jbetl1_ogrp = jpogrp
     c                   eval      jbetl1_hgrp = jphgrp
     c                   eval      jbetl1_ugrp = jpugrp
     c                   eval      jbetl1_modn = jpmodn
     c                   eval      jbetl1_vare = jpvare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'H'
     c     jbetl2_key    setll     jbetl2
     c                   when      w_seqe = 'U'
     c     jbetl3_key    setll     jbetl3
     c                   when      w_seqe = 'M'
     c     jbetl4_key    setll     jbetl4
     c                   when      w_seqe = 'V'
     c     jbetl5_key    setll     jbetl5
     c                   when      w_seqe = 'T'
     c     jbetl6_key    setll     jbetl6
     c                   when      w_seqe = 'L'
     c     jbetl7_key    setll     jbetl7
     c                   when      w_seqe = 'B'
     c     jbetl8_key    setll     jbetl8
6.3x c                   when      w_seqe = 'O'
6.3x c     jbetla_key    setll     jbetla
     c                   other
     c     jbetl1_key    setll     jbetl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
6.3x  *
6.3x  * Prisgruppe
6.3x  *
6.3x c                   if        w_afld = 'B2PRGR'
6.3x c                   call      'RA530R'
6.3x c                   parm                    w_firm
6.3x c                   parm                    b2prgr
6.3x c                   parm                    w_ptxt
6.3x c                   leavesr
6.3x c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
      * Modulnummer
      *
     c                   if        w_afld = 'B2MODN'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    w_mte1
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
      * Varetype
      *
     c                   if        w_afld = 'B2VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    b2vtyp
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne nettopris eller rabatt fra rabattelementene
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_fant     begsr
      *
     c                   eval      b1fant = *blank
      *
     c                   eval      w_vare = b1vare
     c                   call      'JP720R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    b1prgr
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1modn
     c                   parm                    w_vare
     c                   parm                    b1vtyp
     c                   parm                    b1lety
     c                   parm                    b1rsts
     c                   parm                    b1line
     c                   parm                    b1fakt
     c                   parm                    b1ntpr
      *
     c                   if        *in80 = *on
     c                   eval      b1fant = %editc(b1fakt : 'M')
     c                   else
     c                   eval      b1fant = %editc(b1ntpr : 'M')
     c                   endif
      *
6.31  *
6.31 c                   if        jpakti = 1
6.31 c                   eval      b1akti = 'A'
6.31 c                   else
6.31 c                   eval      b1akti = ' '
6.31 c                   endif
6.31 c*
     c                   endsr
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     vis_nobb      begsr
6.30 c
6.30 c                   eval      p_nobb = %char(c2nobb)
6.30 c                   call      'AP633R'
6.30 c                   parm                    p_nobb
6.30  *
6.30 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
6.3x c                   parm                    p_prgr
     c                   parm                    p_vare
     c                   parm                    p_bldo
     c                   parm                    p_bogr
     c                   parm                    p_bhgr
     c                   parm                    p_bugr
     c                   parm                    p_bmod
     c                   parm                    p_bvar
     c                   parm                    p_bvty
     c                   parm                    p_blet
     c                   parm                    p_bbet
     c                   parm                    p_ntpr
     c                   parm                    p_ifak
      *
      * Key for les på betingelse
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jbetl1_prgr
     c                   kfld                    jbetl1_ogrp
     c                   kfld                    jbetl1_hgrp
     c                   kfld                    jbetl1_ugrp
     c                   kfld                    jbetl1_modn
     c                   kfld                    jbetl1_vare
      *
      * Key for oppslag på betingelse
      *
     c     jbetlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jbetlr_prgr
     c                   kfld                    jbetlr_ogrp
     c                   kfld                    jbetlr_hgrp
     c                   kfld                    jbetlr_ugrp
     c                   kfld                    jbetlr_modn
     c                   kfld                    jbetlr_vare
     c                   kfld                    jbetlr_vtyp
     c                   kfld                    jbetlr_lety
     c                   kfld                    jbetlr_rsts
     c                   kfld                    jbetlr_line
      *
      * Key for posisjonering på hovedgruppe
      *
     c     jbetl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl2_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     jbetl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl3_ugrp
      *
      * Key for posisjonering på modulnummer
      *
     c     jbetl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl4_modn
      *
      * Key for posisjonering på varenummer
      *
     c     jbetl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl5_vare
      *
      * Key for posisjonering på varetype
      *
     c     jbetl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl6_vtyp
      *
      * Key for posisjonering på leveringstype
      *
     c     jbetl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl7_lety
      *
      * Key for posisjonering på betingelse
      *
     c     jbetl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl8_beti
      *
6.3x  *
6.3x  * Key for pos på ogrp
6.3x  *
6.3x c     jbetla_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    w_ldor
6.3x c                   kfld                    jbetla_ogrp
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på varetype
      *
     c     vvtyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvtyl1_vtyp
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
6.3x  *
6.3x  * Key for oppslag på prisgruppe
6.3x  *
6.3x c     ra30l1_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    ra30l1_dkod
      *
      *
      * Henter firma og leverandør fra parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
     c                   eval      b2ldor = w_ldor
      *
     c                   eval      jlevl1_ldor = p_ldor
     c     jlevl1_key    chain     jlevl1
     c                   eval      b2lnav = jlnavn
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     jbetl1_key    setll     jbetl1
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
