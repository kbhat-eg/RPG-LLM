     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM705R
      * Beskrivelse . . : Beregner salgspris og kostpris for enhet
      *
      * NB! Hardkodet moms-sats
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161097 HKO  Nytt program
      *       081297 HKO  Kaller program for avrunding av salgspris
      *                   inkl. mva
6.30  * 6.30  260113 bof  Omskriving i forb. endring pakning VA
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.20  * 6.20  260814 NHO  Innkjøpspriser ble ikke lagt ut i VTILLU
7.01  *       051021 bof  Feilretting - sette w_Dato til dagens dato
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
      *
      * Definering av parametre
      *
     d p_vkpr          s              9  2
     d p_vkai          s              9  2
     d p_vkaa          s              9  2
     d p_vkau          s              9  2
     d p_kopr          s              9  2
     d p_sapr          s              9  2
     d p_saim          s              9  2
      *
     d p_firm          s              3  0
     d p_vare          s                   like(jwvare)
6.30 d p_ldor          s                   like(jwldor)
     d p_enhe          s                   like(jwenhe)
      *
      * Definering av variabler i nøkler
      *
     d jvpkl2_vare     s                   like(jwvare)
6.30 d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_sapr          s              9  2
     d w_saim          s              9  2
     d w_kode          s              1  0
6.32 d w_dato          s                   like(jwodat)
      *
     d w_ofak          s                   like(jwofak)
     d w_ofap          s                   like(jwofap)
     d w_fakt          s                   like(jwofap)
      *
6.32 d b_pakn          s              1    inz(*off)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_kopr = 0
     c                   eval      p_sapr = 0
     c                   eval      p_saim = 0
7.01 c                   time                    w_dato
      *
      * Finner enhetens omregningsfaktor
      *
     c                   eval      jvpkl2_vare = p_vare
6.30 c                   eval      jvpkl2_ldor = p_ldor
     c                   eval      jvpkl2_enhe = p_enhe
6.32 c     jvpkl2_key    setll     jvpkl2
6.32 c     jvpkl2_key    reade     jvpkl2
6.32 c                   dow       not %eof(jvpkl2)
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
6.32 c                   leave
6.32 c                   endif
6.32 c     jvpkl2_key    reade     jvpkl2
6.32 c                   enddo
6.32 c                   if        b_pakn = *off
6.32 c                   goto      avslutt
6.32 c                   endif
      *
     c                   eval      w_ofap = jwofap
     c                   eval      w_ofak = jwofak
      *
     c                   if        w_ofap = 0 and w_ofak = 0
     c                   eval      w_fakt = 1
     c                   else
     c                   if        w_ofap > 0
     c                   eval      w_fakt = w_ofap
     c                   else
     c                   eval      w_fakt = w_ofak
     c                   endif
     c                   endif
      *
      * Beregner kostpris
      *
     c                   if        p_vkpr > 0
     c                   eval(h)   p_kopr = p_vkpr * w_fakt
     c                   else
     c                   eval(h)   p_kopr = p_vkai * w_fakt
     c                   endif
      *
6.20 c                   if        p_vkai > 0
6.20 c                   eval(h)   p_vkai = p_vkai * w_fakt
6.20 c                   endif
      * Beregner salgspris
      *
     c                   if        p_vkau > 0
     c                   eval(h)   p_sapr = p_vkau * w_fakt
     c                   else
     c                   eval(h)   p_sapr = p_vkaa * w_fakt
     c                   endif
      *
     c                   eval      p_saim = p_sapr * 1,25
      *
     c                   if        p_saim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    w_kode
     c                   parm                    p_saim
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
6.30 c                   parm                    p_ldor
     c                   parm                    p_enhe
     c                   parm                    p_vkpr
     c                   parm                    p_vkai
     c                   parm                    p_vkaa
     c                   parm                    p_vkau
     c                   parm                    p_kopr
     c                   parm                    p_sapr
     c                   parm                    p_saim
      *
      * Key for oppslag på vare-pakning m/enhet
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
6.30 c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
