     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASKORT
      * Program . . . . : RF810R
      * Beskrivelse . . : Korttransaksjoner, eksport til fil - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020910 BSA  Nytt program
6.10  *       020412 bsa  Aktiverign av GE kort
6.11  *       100812 bsa  Legger ut lager for valg ved kjøring
6.12  *       130812 bsa  Fjerner sjekk på flere kort. Da kjøres alle lager for
6.12  *                   denne korttypen.
6.20  * 6.20  290914 bsa  Sjekker om overføring allerede pågår.
6.30  * 6.30  300615 bsa  Bruker feil felt ved oppslag.
8.00  * 8.00  250522 sst  Endret fra 'GE Kort' til Santander
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukslr    if   e           k disk    rename(rukspfr:rukslrr)
     frukslu    uf   e           k disk    rename(rukspfr:rukslur)
      *
     frf810d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                       128
     d  d_ktyp                        2  0 overlay(d_list:1)
     d  d_fdat                         d   overlay(d_list:3)  datfmt(*iso)
     d  d_tdat                         d   overlay(d_list:13) datfmt(*iso)
     d  d_tidl                        1  0 overlay(d_list:23)
     d  d_lage                        2  0 overlay(d_list:24)
     d  d_sbes                       30    overlay(d_list:26)
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d b_kort          s              1    inz(*off)
      *
     d p_in03          s              1
     d p_ktyp          s              2  0
     d p_skty          s              2  0
     d p_slag          s              2  0
     d p_sbes          s             30
     d d_bmid          s             15
      *
     d w_firm          s              3  0
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_time          s               t
     d w_dato          s               d   datfmt(*iso)
     d w_posi          s              2  0
     d w_leng          s              2  0
     d w_suff          s             12
     d w_ktyp          s              2  0
     d w_ktpn          s             25
     d w_ant           s              2  0
6.20 d w_navn          s             10
6.20 d w_stat          s              1
      *
     d*w_gtxt          s                   like(ragtxt)
     drukslr_skty      s                   like(ruskty)
     drukslr_slag      s                   like(ruslag)
     drukslu_skty      s                   like(ruskty)
     drukslu_slag      s                   like(ruslag)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
     c                   eval      *in38 = *off
     c                   eval      *in40 = *off
6.20 c                   eval      *in41 = *off
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *  Hent info om valgt kort
     c                   eval      rukslr_skty = b1kort
6.30 c**                 eval      rukslr_slag = p_slag
6.30 c                   eval      rukslr_slag = b1lage
     c     rukslr_key    chain     rukslr
      *
      * Kaller program for oppretting av eksportfil
      *
     c                   eval      d_fdat = w_fdat
     c                   eval      d_tdat = w_tdat
     c                   eval      d_tidl = b1tidl
     c                   movel     rusbes        d_sbes
      *
      * Sjekk hvilken korteksport som skal gjelde
      * 1=Maxbo kort  (DNBNOR)
     c                   if        b1kort = 3
     c                   eval      d_ktyp = 3
6.11 c**                 eval      d_lage = p_slag
6.11 c                   eval      d_lage = b1lage
     c                   eval      d_bmid = ruskid
     c                   call      'RF811C'
     c                   parm                    d_list
     c                   parm                    d_bmid
     c                   exsr      upd_kort
      * 2=GE Moneybank
     c                   elseif    b1kort = 4
     c                   eval      d_ktyp = 4
6.11 c**                 eval      d_lage = p_slag
6.11 c                   eval      d_lage = b1lage
6.10 c                   eval      d_bmid = ruskid
     c                   call      'RF812C'
     c                   parm                    d_list
6.10 c                   parm                    d_bmid
     c                   exsr      upd_kort
      * 3=BM Kort (DNBNOR)
     c                   elseif    b1kort = 5
     c                   eval      d_ktyp = 5
6.11 c**                 eval      d_lage = p_slag
6.11 c                   eval      d_lage = b1lage
     c                   eval      d_bmid = ruskid
     c                   call      'RF814C'
     c                   parm                    d_list
     c                   parm                    d_bmid
     c                   exsr      upd_kort
      * 4=NBBL Fordelskort
     c                   elseif    b1kort = 6
     c                   eval      d_ktyp = 6
6.11 c**                 eval      d_lage = p_slag
6.11 c                   eval      d_lage = b1lage
     c                   eval      d_bmid = ruskid
     c                   call      'RF813C'
     c                   parm                    d_list
     c                   parm                    d_bmid
     c                   exsr      upd_kort
     c                   endif
      *
     c**                 exfmt     b1sbm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        1 = 2
     c                   exsr      xf1nul
     c                   endif
      *
     c                   if        w_afld = 'B1KORT'
     c                   eval      p_skty = 0
     c                   eval      p_slag = 0
     c                   eval      p_sbes = *blank
      *
     c                   call      'RA532R'
     c                   parm                    w_firm
     c                   parm                    p_skty
     c                   parm                    p_slag
     c                   parm                    p_sbes
      *
     c                   if        1 = 2
     c                   exfmt     f1win
     c                   endif
      *
     c*                  if        *inkc or *inkl
     c*                  goto      end_spØrr
     c*                  endif
      *
     c                   endif
6.11  *
6.11 c                   if        w_afld = 'B1LAGE'
6.11 c                   eval      p_skty = b1kort
6.11 c                   eval      p_slag = 0
6.11 c                   eval      p_sbes = *blank
6.11  *
6.11 c                   call      'RA532R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_skty
6.11 c                   parm                    p_slag
6.11 c                   parm                    p_sbes
6.11  *
6.11 c                   endif
      *
     c                   if        p_skty > 0
     c                   exsr      kort_txt
     c                   endif
      *
6.11 c                   eval      b1lage = p_slag
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å blanke ut hardkodet spørrebilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1nul        begsr
      *
     c                   eval      f1alt1 = *blank
     c                   eval      f1alt2 = *blank
     c                   eval      f1alt3 = *blank
     c                   eval      f1alt4 = *blank
     c                   eval      f1alt5 = *blank
     c                   eval      f1alt6 = *blank
     c                   eval      f1alt7 = *blank
     c                   eval      f1alt8 = *blank
     c                   eval      f1val1 = *blank
     c                   eval      f1val2 = *blank
     c                   eval      f1val3 = *blank
     c                   eval      f1val4 = *blank
     c                   eval      f1val5 = *blank
     c                   eval      f1val6 = *blank
     c                   eval      f1val7 = *blank
     c                   eval      f1val8 = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b_kort = *off
      *
      * Sjekk gyldig korttype
      *
     c                   if        b1kort > 6 or
     c                             b1kort <  3
     c                   eval      *in40 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekk om det finnes flere forekomster av korttypen BM Kort, Maxbo eller GE
      *
6.10 c                   if        b1kort = 5 or
6.10 c                             b1kort = 4 or
6.10 c                             b1kort = 3
     c                   exsr      sjekk_kort
     c                   if        b_kort = *on
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Sjekk datoinput
      *
     c**                 if        b1kort = 3
      *
     c*                  if        b1fdat = 0
     c*                  eval      *in31 = *on
     c*                  eval      b_feil = *on
     c*                  goto      end_sjekk
     c*                  endif
      *
     c                   if        b1fdat <> 0
     c     *dmy          test(d)                 b1fdat                 32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c**                 endif
      *
     c                   if        b1tdat = 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          test(d)                 b1tdat                 34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c**                 if        b1kort = 3
     c                   if        b1fdat <> 0
     c     *dmy          move      b1fdat        w_fdat
     c                   else
     c                   eval      w_fdat = *loval
     c                   endif
     c**                 endif
     c     *dmy          move      b1tdat        w_tdat
     c                   if        w_fdat > w_tdat
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekk omkjøring
      *
     c**                 if        b1kort = 3
      *
     c                   if        b1tidl  > 1
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c**                 endif
6.20  * Sjekk om overføring allerede skjer, eller at forrige avsluttet i error
6.20 c                   eval      w_stat = *blanks
6.20 c                   if        b1kort = 3
6.20 c                   eval      w_navn = 'LOGDNBKORT'
6.20 c                   elseif    b1kort = 4
6.20 c                   eval      w_navn = 'LOGGECKORT'
6.20 c                   elseif    b1kort = 5
6.20 c                   eval      w_navn = 'LOGDNBKORT'
6.20 c                   elseif    b1kort = 6
6.20 c                   eval      w_navn = 'KORTITUM'
6.20 c                   endif
6.20 c                   call      'AF712R'
6.20 c                   parm                    w_navn
6.20 c                   parm                    w_stat
6.20  *
6.20 c                   if        w_stat  <> *blanks
6.20 c                   eval      *in41 = *on
6.20 c                   eval      b_feil = *on
6.20 c                   goto      end_sjekk
6.20 c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker at det ikke finnes flere forekomster av samme kort
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_kort    begsr
      * Hvis lagerkoden er null, må vi sjekke at denne finnes, og at det ikke er flere av
      * samme korttype. (NB Sjekker ikke hvis lager er <> 0)
     c                   if        p_slag =  0
     c                   eval      w_ant = 0
     c                   eval      rukslr_skty = b1kort
     c                   eval      rukslr_slag = 0
     c     rukslr_key    setll     rukslr
     c     rukslr_key1   reade     rukslr
     c                   dow       not %eof
     c                   eval      w_ant = w_ant + 1
     c     rukslr_key1   reade     rukslr
     c                   enddo
6.12  * Ikke feilflagge hvis flere forekomster. Da kjøres alle kort av denne typen.
6.12 c**                 if        w_ant > 1
6.12 c**                 eval      b_kort = *on
6.12 c**                 goto      end_kort
6.12 c**                 endif
      *
     c                   endif
      *  Sjekk om valgt kort finnes
     c                   eval      rukslr_skty = b1kort
     c                   eval      rukslr_slag = p_slag
     c     rukslr_key    chain     rukslr
     c                   if        not %found
     c                   eval      b_kort = *on
     c                   goto      end_kort
     c                   endif
      *
     c     end_kort      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å legge inn tekster til kort
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     init_txt      begsr
      *
      * Legg inn tekster
      *
     c                   eval      f1alt1 = 'VISA Purchasing          '
     c                   eval      f1alt2 = 'Europay Purchasing       '
     c                   eval      f1alt3 = 'Maxbo Kort               '
8.00 c                   eval      f1alt4 = 'Santander                '
     c                   eval      f1alt5 = 'BM Kort                  '
     c                   eval      f1alt6 = 'NBBL Fordelskort         '
     c                   eval      f1alt7 = '                         '
     c                   eval      f1alt8 = '                         '
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å legge inn tekster til kort
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kort_txt      begsr
      *
      * Legg inn tekster
      *
     c                   select
     c                   when      p_skty = 1
     c                   eval      b1kort = 01
     c                   movel     f1alt1        b1ktxt
     c                   when      p_skty = 2
     c                   eval      b1kort = 02
     c                   movel     f1alt2        b1ktxt
     c                   when      p_skty = 3
     c                   eval      b1kort = 03
     c                   movel     f1alt3        b1ktxt
     c                   when      p_skty = 4
     c                   eval      b1kort = 04
     c                   movel     f1alt4        b1ktxt
     c                   when      p_skty = 5
     c                   eval      b1kort = 05
     c                   movel     f1alt5        b1ktxt
     c                   when      p_skty = 6
     c                   eval      b1kort = 06
     c                   movel     f1alt6        b1ktxt
     c                   when      p_skty = 7
     c                   eval      b1kort = 07
     c                   movel     f1alt7        b1ktxt
     c                   when      p_skty = 8
     c                   eval      b1kort = 08
     c                   movel     f1alt8        b1ktxt
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere kortregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_kort      begsr
      *
      * Oppdater med at utlegg av transaksjoner er kjørt
      *
     c                   eval      rukslu_skty = ruskty
     c                   eval      rukslu_slag = ruslag
     c     rukslu_key    chain     rukslu
     c                   if        %found
     c                   eval      rusdat = w_tdat
     c                   eval      rustim = w_time
     c                   eval      rususr = l_user
     c                   eval      d_bmid = ruskid
     c                   update    rukslur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_ktyp
     c                   parm                    p_in03
      *
      * Key for les av kortregister
      *
     c     rukslr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukslr_skty
     c                   kfld                    rukslr_slag
      *
      * Key for les av kortregister
      *
     c     rukslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukslu_skty
     c                   kfld                    rukslu_slag
      *
      * Key for les av kortregister
      *
     c     rukslr_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    rukslr_skty
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      p_in03 = '0'
     c                   eval      b1kort = p_ktyp
     c                   time                    w_dato
     c     *dmy          move      w_dato        b1tdat
     c                   time                    w_time
      *
      * Initierer skjermbildet
      *
     c                   exsr      init_txt
     c                   if        p_ktyp > 0
     c                   eval      p_skty = p_ktyp
     c                   exsr      kort_txt
     c                   endif
      *
     c                   endsr
