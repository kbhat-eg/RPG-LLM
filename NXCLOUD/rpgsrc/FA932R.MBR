    Hh decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FA932R
      * Beskrivelse . . : Oppslag mot Bisnode med webservice - foretak
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190517 ASK  Nytt program
      *       290519 ASK  Programmet er skrevet om med tanke på å gå bort i fra
      *                   Bisnode decision maker
8.01  * 800   031221 ask  lagt inn oppslag mot koderegister for setting av kredittgrenser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d p_fnum          s              9
     d p_navn          s             30
     d p_adr1          s             30
     d p_adr2          s             30
     d p_ponr          s              4  0
     d p_pste          s             30
     d p_tele          s             11
     d p_mobi          s             11
8.01 d p_kgre          s              8  0
     d p_rati          s              5
      *
     d XML_Input       s            256a   Varying
     d XML_Output1     s            256a   Varying
     d XML_Output2     s            256a   Varying
     d XML_Output3     s            256a   Varying
     d XML_Output4     s            256a   Varying
     d XML_path        s            256a
     d w_xmlp          s            100
     d options         s            200a   Varying
      *
     d w_url           s            200
     d w_reqf          s            100
     d w_rspf          s            100
     d w_xmlf          s            100    Varying
     d w_usrn          s             20
     d w_pass          s             50
     d w_usrk          s             20
     d w_pask          s             50
     d w_modu          s              1
     d w_scor          s              3
     d w_scorn         s              3  0
     d w_susr          s             10
     d w_rapp          s             25
     d w_scrm          s             25
     d w_pnum          s             11
     d w_stat          s              1  0
     d w_tell          s              3  0 inz(0)
     d w_date          s               d
     d w_autt          s              5
     d w_ftnr          s              9
     d w_duns          s              9
     d w_ktyp          s              1
     d w_kttx          s             10
     d w_navn          s             50
     d w_gadr          s             50
     d w_gpon          s              4
     d w_gste          s             25
     d w_padr          s             50
     d w_ppnr          s              4
     d w_ppon          s              4
     d w_pste          s             25
     d w_rate          s              5
     d w_ratx          s             20
     d w_limt          s             10
     d w_hend          s             20
     d w_grun          s             20
     d w_juri          s             20
     d w_okon          s             20
     d w_erfa          s             20
     d w_sekd          s              3
     d w_setx          s             20
     d w_besl          s             20
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_leng          s              2  0
     d w_start         s              2  0
     d w_slutt         s              2  0
     d w_tegn          s              1
      *
     d w_firm          s              3  0
8.01 d w_ksc1          s              3  0
8.01 d w_ksc2          s              3  0
8.01 d w_ksc3          s              3  0
8.01 d w_ksc4          s              3  0
8.01 d w_ksc5          s              3  0
8.01 d w_kgr1          s              8  0
8.01 d w_kgr2          s              8  0
8.01 d w_kgr3          s              8  0
8.01 d w_kgr4          s              8  0
8.01 d w_kgr5          s              8  0
      *
     d b_alle          s              1    inz(*off)
     d b_eof           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      /copy prototypeb
      /copy usec
      *
      * Datastruktur for XML fra Foretakinfo
      *
     d Identifikasjon  ds                  qualified
     d   Orgnr                        9a
     d   Dunsnr                       9a
      *
     d NavnAdresse     ds                  qualified
     d   Kodetype                     1a
     d   KodeTekst                   10a
     d   Navn                        50a
     d   GateAdresse                 50a
     d   GatePostnr                   4a
     d   GatePoststed                25a
     d   PostAdresse                 50a
     d   PostPostboks                 4a
     d   PostPostnr                   4a
     d   PostPoststed                25a
      *
     d Rating          ds                  qualified
     d   Rating                       5a
     d   RatingBeskrivelse...
     d                               20a
     d   Limit                       10a
     d   AktuellHendelse...
     d                               20a
     d   DelbGrunnfakta...
     d                               20a
     d   DelbEierJuridisk...
     d                               20a
     d   Delbokonomi                 20a
     d   DelbBetalingserfaring...
     d                               20a
      *
     d Grunnfakta      ds                  qualified
     d   SelskFormKode...
     d                                3a
     d   SelskFormTekst...
     d                               20a
     d Scoring         ds                  qualified
     d   Beslutning...
     d                               20a
     d   Score...
     d                                5a
7.01  *
7.01  * Oppslag mot switch register for å hente info
7.01  *
7.01  *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
8.01
8.01 C/Exec SQL
8.01 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
8.01 C/End-Exec
8.01
      *
      * Hovedprogram:
      *
      *  - slår opp med foretaksnummer
      *  - mottar internreferanse navn og adresseinformasjon
      *  - returnerer verdier til registreringsprogram
      *
      * ------------------------------------------------------------------------
      * Initierer, bygger requestfiler fra ForetaksinfoMal og kjører webservice
      * ------------------------------------------------------------------------
      *
      * Params file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/ForetakinfoMal.txt.params'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
     c                   eval      XML_Output1 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/Foretakinfo' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt.params'
      /Free
           // Ingen parametre på denne filen
               WrtSection('FinfoParams');

      /End-free
      *
     c                   callp     WrtHtmlToStmf(XML_Output1: 819)
      *
      * Headers file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/ForetakinfoMal.txt.headers'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
     c                   eval      w_usrn = w_usrk
     c                   eval      w_pass = w_pask
      *
      /Free
           // Legge inn personnumer fra parameter
               UpdHTMLVar('usrid':                  w_usrn);
               UpdHTMLVar('passw':                  w_pass);
               WrtSection('FsokHeader');

      /End-free
      *
     c                   eval      XML_Output2 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/Foretakinfo' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt.headers'
      *
     c                   callp     WrtHtmlToStmf(XML_Output2: 819)
      *
      * Body file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/ForetakinfoMal.txt'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
      /Free
           // Legge inn personnumer fra parameter
               UpdHTMLVar('foretaksnr':             p_fnum);
               UpdHTMLVar('rapport':                w_rapp);
               UpdHTMLVar('scoremod':               w_scrm);
               WrtSection('Foretak');

      /End-free
      *
     c                   eval      XML_Output3 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/Foretakinfo' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt'
     c                   eval      XML_Input   = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/ForetakinfoResp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt'
      *
     c                   callp     WrtHtmlToStmf(XML_Output3: 819)
      *
      * Initierer og kaller webservice for ForetakInfo
      *
     c*                  eval      w_url = 'https://test.dbonline.no' +
     c*                                    '/webservices/service/ForetakInfo'
     c                   eval      w_url = 'https://dbonline.no' +
     c                                     '/webservices/service/ForetakInfo'
     c                   eval      w_usrn = *blank
     c                   eval      w_pass = *blank
     c                   eval      w_autt = *blank
     c                   eval      w_reqf = XML_Output3
     c                   eval      w_rspf = XML_Input
     c                   eval      w_stat = 0
      *
     c                   call      'AW703C'
     c                   parm                    w_url
     c                   parm                    w_reqf
     c                   parm                    w_rspf
     c                   parm                    w_usrn
     c                   parm                    w_pass
     c                   parm                    w_autt
     c                   parm                    w_stat

     c                   callp     unlink(XML_Output1)
     c                   callp     unlink(XML_Output2)
     c                   callp     unlink(XML_Output3)
      *
      * ------------------------------------------------------------------------
      * Leser mottatt svar på Foretakinfo og tar vare på mottatte data
      * ------------------------------------------------------------------------
      *
      * Henter Identifikasjon
      *
      /Free
             options  = 'doc=file +
                                path=HentForetakResponse/Identifikasjon +
                                case=any +
                                ns=remove +
                                allowextra=yes +
                                allowmissing=yes';
             w_xmlf = %trim(w_rspf);

                   xml-into Identifikasjon %xml(w_xmlf: options);

      /End-free
      *
      /Free

             w_ftnr  = Identifikasjon.Orgnr;
             w_duns  = Identifikasjon.Dunsnr;

      /End-free
      *
      * Henter NavnAdresse
      *
      /Free
             options  = 'doc=file +
                                path=HentForetakResponse/NavnAdresse +
                                case=any +
                                ns=remove +
                                allowextra=yes +
                                allowmissing=yes';
             w_xmlf = %trim(w_rspf);

                   xml-into NavnAdresse %xml(w_xmlf: options);

      /End-free
      *
      /Free

             w_ktyp  = NavnAdresse.KodeType;
             w_kttx  = NavnAdresse.KodeTekst;
             w_navn  = NavnAdresse.Navn;
             w_gadr  = NavnAdresse.GateAdresse;
             w_gpon  = NavnAdresse.GatePostnr;
             w_gste  = NavnAdresse.GatePoststed;
             w_padr  = NavnAdresse.PostAdresse;
             w_ppnr  = NavnAdresse.PostPostboks;
             w_ppon  = NavnAdresse.PostPostnr;
             w_pste  = NavnAdresse.PostPoststed;

      /End-free
      *
      * Henter Rating
      *
      /Free
             options  = 'doc=file +
                                path=HentForetakResponse/Rating +
                                case=any +
                                ns=remove +
                                allowextra=yes +
                                allowmissing=yes';
             w_xmlf = %trim(w_rspf);

                   xml-into Rating %xml(w_xmlf: options);

      /End-free
      *
      /Free

             w_rate  = Rating.Rating;
             w_ratx  = Rating.RatingBeskrivelse;
             w_limt  = Rating.Limit;
             w_hend  = Rating.AktuellHendelse;
             w_grun  = Rating.DelbGrunnfakta;
             w_juri  = Rating.DelbEierJuridisk;
             w_okon  = Rating.DelbOkonomi;
             w_erfa  = Rating.DelbBetalingserfaring;

      /End-free
      *
      * Henter Selskapsform
      *
      /Free
             options  = 'doc=file +
                                path=HentForetakResponse/Grunnfakta +
                                case=any +
                                ns=remove +
                                allowextra=yes +
                                allowmissing=yes';
             w_xmlf = %trim(w_rspf);

                   xml-into Grunnfakta %xml(w_xmlf: options);

      /End-free
      *
      /Free

             w_sekd  = Grunnfakta.SelskFormKode;
             w_setx  = Grunnfakta.SelskFormTekst;

      /End-free
      *
      *
      * Henter Score
      *
      /Free
             options  = 'doc=file +
                                path=HentForetakResponse/Scoring +
                                case=any +
                                ns=remove +
                                allowextra=yes +
                                allowmissing=yes';
             w_xmlf = %trim(w_rspf);

                   xml-into Scoring %xml(w_xmlf: options);

      /End-free
      /Free

             w_besl  = Scoring.Beslutning;
             w_scor  = Scoring.Score;

      /End-free
     c                   callp     unlink(XML_Output1)
     c                   callp     unlink(XML_Output2)
     c                   callp     unlink(XML_Output3)
     c                   callp     unlink(XML_Output4)
      *
      *
      * Beregner kreditgrense
      *
     c                   eval      w_scorn = %dec(w_scor:3:0)

8.01  /FREE
8.01     p_kgre = 0;
8.01
8.01     if w_besl = 'Godkjent';
8.01
8.01  // Henter kredittgrenser fra selskap
8.01
8.01       exec sql
8.01       select  rkksc1, rkkgr1, rkksc2, rkkgr2, rkksc3, rkkgr3,
8.01               rkksc4, rkkgr4, rkksc5, rkkgr5
8.01         into :w_ksc1, :w_kgr1, :w_ksc2, :w_kgr2, :w_ksc3, :w_kgr3,
8.01              :w_ksc4, :w_kgr4, :w_ksc5, :w_kgr5
8.01        from ra33st
8.01         where rkktyp = :w_sekd;
8.01
8.01       if sqlcod = 100;
8.01  // Henter kredittgrenser fra overordnet type
8.01
8.01       exec sql
8.01       select  rkksc1, rkkgr1, rkksc2, rkkgr2, rkksc3, rkkgr3,
8.01               rkksc4, rkkgr4, rkksc5, rkkgr5
8.01         into :w_ksc1, :w_kgr1, :w_ksc2, :w_kgr2, :w_ksc3, :w_kgr3,
8.01              :w_ksc4, :w_kgr4, :w_ksc5, :w_kgr5
8.01        from ra33st
8.01         where rkktyp = '      ';
8.01       endif;
8.01
8.01           if sqlcod = 100;
8.01             p_kgre = 0;
8.01            else;
8.01             if w_ksc1 > 0 and
8.01                w_scorn > w_ksc1;
8.01                  p_kgre = w_kgr1;
8.01             endif;
8.01             if w_ksc2 > 0 and
8.01                w_scorn > w_ksc2;
8.01                  p_kgre = w_kgr2;
8.01             endif;
8.01             if w_ksc3 > 0 and
8.01                w_scorn > w_ksc3;
8.01                  p_kgre = w_kgr3;
8.01             endif;
8.01             if w_ksc4 > 0 and
8.01                w_scorn > w_ksc4;
8.01                  p_kgre = w_kgr4;
8.01             endif;
8.01             if w_ksc5 > 0 and
8.01                w_scorn > w_ksc5;
8.01                  p_kgre = w_kgr5;
8.01             endif;
8.01           endif;
8.01     endif;
8.01  /END-FREE
      *
      * Editerer parameter felter for retur til hovedprogram
      *
     c                   eval      p_navn = w_navn
     c                   eval      p_adr1 = w_gadr
     c                   eval      p_adr2 = *blank
     c                   eval      p_ponr = %dec(w_gpon:4:0)
     c                   eval      p_pste = w_gste
     c                   eval      p_tele = *blank
     c                   eval      p_mobi = *blank
     c*                  eval      p_rati = w_rate
     c                   eval      p_rati = w_scor
      *
     c                   callp     unlink(XML_Input)
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved ikke-treff mot Bisnode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_navn = *blank
     c                   eval      p_adr1 = *blank
     c                   eval      p_adr2 = *blank
     c                   eval      p_ponr = 0
     c                   eval      p_pste = *blank
     c                   eval      p_tele = *blank
     c                   eval      p_mobi = *blank
     c                   goto      avslutt
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_fnum
     c                   parm                    p_navn
     c                   parm                    p_adr1
     c                   parm                    p_adr2
     c                   parm                    p_ponr
     c                   parm                    p_pste
     c                   parm                    p_tele
     c                   parm                    p_mobi
     c                   parm                    p_kgre
     c                   parm                    p_rati
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Henter inn løpenummer teller for Bisnode
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'BISTEL'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * Tilordner faste verdier
      *
     c                   eval      w_susr = l_user
     c                   eval      w_rapp = 'TOTAL'
     c                   eval      w_scrm = '1'
      *
      * Eksternt kundesøk  hent påloggingsinformasjon Bisnode
      *
       CallP CO402R(l_filg:w_firm:0:'BISNODE - EKSTERNT_KUNDESØK':
                       co402_verdi1:co402_verdi2);

      * Brukerid
           w_start = 1;
           dou  w_tegn <> ' ';
            w_tegn = %subst(CO402_verdi2 : w_start : 1);
            w_start= w_start + 1;
           enddo;
           w_start= w_start - 1;
           w_slutt = %scan(' ' : co402_verdi2 : w_start);
           w_leng = w_slutt - w_start;

           w_usrk = %subst(CO402_verdi2 :w_start : w_leng);

      * Passord
           w_start = w_slutt;
           dou  w_tegn <> ' ';
            w_tegn = %subst(CO402_verdi2 : w_start : 1);
            w_start= w_start + 1;
           enddo;
           w_start= w_start - 1;
           w_slutt = %scan(' ' : co402_verdi2 : w_start);
           w_leng = w_slutt - w_start;

           w_pask = %subst(CO402_verdi2 :w_start : w_leng);
      *
     c                   endsr
