     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP701R
      * Beskrivelse . . : Betingelse, oppdater fra Nobb Kontrakt
      *
      * Programmet leser PC-filer, og oppretter betingelser og rabattelement
      * fra Nobb Kontrakt
      *
      * Betingelser:
      * Ved brudd på leverandør slettes alle "ikke egne" betingelser og
      * tilhørende rabattelement som ikke ble oppdatert for "forrige"
      * leverandør
      *
      * Rabattelement:
      * Ved brudd på leverandør slettes alle "ikke egne" rabattelement
      * for leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030901 HKO  Nytt program
      *       200303 HKO  Tatt hensyn til at verdi kan inneholde mer enn
      *                   to desimaler
      *       200405 HKO  På grunn av at betingelsesidentifikatoren
      *                   kan forandre seg i Nobb Kontrakt p.g.a. en feil
      *                   testes det også for endring av en betingelse
      *                   mot nivå og betingelsestekst
      *                   Dette ødelegger litt av sikkerheten i løsningen.
      *       130605 HKO  Utvidet nivå med varetype og leveringstype
      *       301006 HKO  Gjort programmet mer robust rundt innlesing av
      *                   verdi i rabattelementet
      *       250907 BOF  Gjort slik at lev.nr kan være 5 pos.lang ute å feile
      *       080108 BOF  Lagt inn test på helt blank linje                 le
      *       170108 BOF  Lagt inn test på blankt linjenr.                  le
      *       230108 BOF  Justert test på rab.element lev.nr..
      *       200608 BOF  Takle sletting når innlesing av 1 og 1 lev.
      *       300608 BOF  Rapportere endringer på betingelser
      *       120808 BOF  Beholde aktivisering på rabatt-element
6.21  *       151014 BOF  Lese inn fra til dato på rabatt-element
6.3x  *       050615 BOF  Prisgruppe på betingelse.
6.31  *       090316 BOF  Oppd. av prgr. som ligger inn, feileretting
6.32  *       070416 BOF  skal ikke slette egenreg.rab.elementer
6.33  *       210416 BOF  rettelse av 6.32
6.34  *       180317 BOF  Hindre at lokale rag.elem. blir slettet
6.35  *       161017 BOF  Endre dato test på sletting
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fpcbetpf   if   e           k disk
     fpcraepf   if   e           k disk
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
     fjbetla    if   e           k disk    rename(jbetpfr:jbetlar)
     fjbewl1    if   e           k disk    rename(jbewpfr:jbewl1r)
6.31 fjbewla    if   e           k disk    rename(jbewpfr:jbewlar)
     fjbetlu    uf a e           k disk    rename(jbetpfr:jbetlur)
     fjraelu    uf a e           k disk    rename(jraepfr:jraelur)
     fjrawl1    if   e           k disk    rename(jrawpfr:jrawl1r)
     f                                     prefix(jpw:3)
     fjwrkpf    uf a e           k disk    rename(jwrkpfr:jwrkpfr)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Betingelse-fil
      *
     d                 ds
     d d_bet                        240
     d  d_beti                       35    overlay(d_bet:1)
     d  d_ldor                        6  0 overlay(d_bet:46)
     d  d_ogrp                        2    overlay(d_bet:52)
     d  d_hgrp                        2    overlay(d_bet:54)
     d  d_ugrp                        3    overlay(d_bet:56)
     d  d_modn                        8    overlay(d_bet:59)
     d  d_nobb                        8    overlay(d_bet:67)
     d  d_line                        8  0 overlay(d_bet:141)
     d  d_linx                        8    overlay(d_bet:141)
      *
      * Rabattelement-fil
      *
     d                 ds
     d d_rae                        240
     d  d_eldx                        6    overlay(d_rae:21)
     d  d_eogr                        2    overlay(d_rae:27)
     d  d_ehgr                        2    overlay(d_rae:29)
     d  d_eugr                        3    overlay(d_rae:31)
     d  d_emod                        8    overlay(d_rae:34)
     d  d_enob                        8    overlay(d_rae:42)
     d  d_elin                        8  0 overlay(d_rae:50)
     d  d_efda                       10    overlay(d_rae:59)
6.21 d  d_etda                       10    overlay(d_rae:70)
     d  d_erae                       35    overlay(d_rae:83)
     d  d_ereg                        3    overlay(d_rae:125)
     d  d_ever                       11    overlay(d_rae:132)
      *
      * Definering av variable i nøkler
      *
     d jvarl3_nobb     s                   like(jvnobb)
     d jbetl1_ldor     s                   like(jpldor)
6.3x d jbetl1_prgr     s                   like(jpprgr)
     d jbetl1_ogrp     s                   like(jpogrp)
     d jbetl1_hgrp     s                   like(jphgrp)
     d jbetl1_ugrp     s                   like(jpugrp)
     d jbetl1_modn     s                   like(jpmodn)
     d jbetl1_vare     s                   like(jpvare)
     d jbetl1_vtyp     s                   like(jpvtyp)
     d jbetl1_lety     s                   like(jplety)
     d jbetl1_rsts     s                   like(jprsts)
     d jbetl1_line     s                   like(jpline)
     d jbetla_ldor     s                   like(jpldor)
     d jbetla_ogrp     s                   like(jpogrp)
     d jbetla_hgrp     s                   like(jphgrp)
     d jbetla_ugrp     s                   like(jpugrp)
     d jbetla_modn     s                   like(jpmodn)
     d jbetla_vare     s                   like(jpvare)
     d jbetla_vtyp     s                   like(jpvtyp)
     d jbetla_lety     s                   like(jplety)
     d jbetla_rsts     s                   like(jprsts)
     d jbetla_beti     s                   like(jpbeti)
     d jbetla_prgr     s                   like(jpprgr)
     d jbetlu_ldor     s                   like(jpldor)
     d jbetlu_prgr     s                   like(jpprgr)
     d jbetlu_ogrp     s                   like(jpogrp)
     d jbetlu_hgrp     s                   like(jphgrp)
     d jbetlu_ugrp     s                   like(jpugrp)
     d jbetlu_modn     s                   like(jpmodn)
     d jbetlu_vare     s                   like(jpvare)
     d jbetlu_vtyp     s                   like(jpvtyp)
     d jbetlu_lety     s                   like(jplety)
     d jbetlu_rsts     s                   like(jprsts)
     d jbetlu_line     s                   like(jpline)
     d jbewl1_ldor     s                   like(jpldor)
     d jbewl1_prgr     s                   like(jpprgr)
     d jbewl1_ogrp     s                   like(jpogrp)
     d jbewl1_hgrp     s                   like(jphgrp)
     d jbewl1_ugrp     s                   like(jpugrp)
     d jbewl1_modn     s                   like(jpmodn)
     d jbewl1_vare     s                   like(jpvare)
     d jbewl1_vtyp     s                   like(jpvtyp)
     d jbewl1_lety     s                   like(jplety)
     d jbewl1_rsts     s                   like(jprsts)
     d jbewl1_line     s                   like(jpline)
6.31 d jbewla_ldor     s                   like(jpldor)
6.31 d jbewla_ogrp     s                   like(jpogrp)
6.31 d jbewla_hgrp     s                   like(jphgrp)
6.31 d jbewla_ugrp     s                   like(jpugrp)
6.31 d jbewla_modn     s                   like(jpmodn)
6.31 d jbewla_vare     s                   like(jpvare)
6.31 d jbewla_vtyp     s                   like(jpvtyp)
6.31 d jbewla_lety     s                   like(jplety)
6.31 d jbewla_rsts     s                   like(jprsts)
6.31 d jbewla_beti     s                   like(jpbeti)
6.31 d jbewla_prgr     s                   like(jpprgr)
     d jraelu_eldo     s                   like(jpeldo)
     d jraelu_eprg     s                   like(jpeprg)
     d jraelu_eogr     s                   like(jpeogr)
     d jraelu_ehgr     s                   like(jpehgr)
     d jraelu_eugr     s                   like(jpeugr)
     d jraelu_emod     s                   like(jpemod)
     d jraelu_evar     s                   like(jpevar)
     d jraelu_evty     s                   like(jpevty)
     d jraelu_elet     s                   like(jpelet)
     d jraelu_ebrs     s                   like(jpebrs)
     d jraelu_ebli     s                   like(jpebli)
     d jrawl1_eldo     s                   like(jpeldo)
     d jrawl1_eprg     s                   like(jpeprg)
     d jrawl1_eogr     s                   like(jpeogr)
     d jrawl1_ehgr     s                   like(jpehgr)
     d jrawl1_eugr     s                   like(jpeugr)
     d jrawl1_emod     s                   like(jpemod)
     d jrawl1_evar     s                   like(jpevar)
     d jrawl1_evty     s                   like(jpevty)
     d jrawl1_elet     s                   like(jpelet)
     d jrawl1_ebrs     s                   like(jpebrs)
     d jrawl1_ebli     s                   like(jpebli)
      *
      * Definering av variable
      *
     d b_nega          s              1    inz(*off)
     d b_frst          s              1    inz(*on)
     d b_head          s              1    inz(*off)
     d b_endr          s              1    inz(*off)
6.3x d b_prgr          s              1    inz(*off)
6.3x d b_akti_be       s              1    inz(*off)
      *
     d d_eldo          s              6  0
     d w_verd          s             11
     d w_verd_10       s             10  0
     d w_stat          s             10
6.21 d w_dato          s             10
6.21 d w_efda          s                   like(jpefda)
6.21 d w_etda          s                   like(jpetda)
      *
     d w_firm          s                   like(jpfirm)
6.3x d w_prgr          s                   like(jpprgr)
     d w_ogrp          s                   like(jpogrp)
     d w_hgrp          s                   like(jphgrp)
     d w_ugrp          s                   like(jpugrp)
     d w_modn          s                   like(jpmodn)
     d w_vare          s                   like(jpvare)
     d w_elin          s                   like(jpelin)
     d w_edat          s                   like(jpedat)
     d w_etim          s                   like(jpetim)
6.3x d w_efir          s                   like(jpefir)
6.3x d w_eldo          s                   like(jpeldo)
6.3x d w_eogr          s                   like(jpeogr)
6.3x d w_ehgr          s                   like(jpehgr)
6.3x d w_eugr          s                   like(jpeugr)
6.3x d w_emod          s                   like(jpemod)
6.3x d w_evar          s                   like(jpevar)
6.3x d w_evty          s                   like(jpevty)
6.3x d w_elet          s                   like(jpelet)
     d w_ebrs          s                   like(jpebrs)
     d w_ebli          s                   like(jpebli)
     d w_erst          s                   like(jperst)
6.3x d w_erae          s                   like(jperae)
6.3x d w_eeus          s                   like(jpeeus)
6.3x d w_eoda          s                   like(jpeoda)
6.3x d w_eeda          s                   like(jpeeda)
6.3x d w_eeti          s                   like(jpeeti)
     d w_ldor_b        s                   like(jpldor)
     d w_ogrp_b        s                   like(jpogrp)
     d w_hgrp_b        s                   like(jphgrp)
     d w_ugrp_b        s                   like(jpugrp)
     d w_modn_b        s                   like(jpmodn)
     d w_vare_b        s                   like(jpvare)
     d w_vtyp_b        s                   like(jpvtyp)
     d w_lety_b        s                   like(jplety)
     d w_rsts_b        s                   like(jprsts)
     d w_line_b        s                   like(jpline)
     d w_beti_b        s                   like(jpbeti)
     d w_fdat_b        s                   like(jpfdat)
     d w_tdat_b        s                   like(jptdat)
     d w_dato_b        s                   like(jpdato)
     d w_akti_b        s                   like(jpakti)
     d w_odat_b        s                   like(jpodat)
     d w_edat_b        s                   like(jpedat)
     d w_etim_b        s                   like(jpetim)
     d w_eusr_b        s                   like(jpeusr)
6.31 d w_line_g        s                   like(jpline)
6.31 d w_beti_g        s                   like(jpbeti)
      *
     d s_ldor          s                   like(jpldor)
     d s_eldo          s                   like(jpeldo)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer felles endringsdato og -tidspunkt
      *
     c                   time                    w_edat
     c                   time                    w_etim
      *
      * Leser og oppretter/oppdaterer betingelser
      *
     c                   read      pcbetpf
     c                   dow       not %eof
     c                   exsr      oppdat_bet
     c                   read      pcbetpf
     c                   enddo
      *
      * Sletter gamle betingelser og rabattelement for siste leverandør
      *
     c                   if        b_frst = *off
     c                   exsr      slett_gamle
     c                   endif
      *
      * Leser og oppretter/oppdaterer rabattelement
      *
     c                   read      pcraepf
     c                   dow       not %eof
     c                   exsr      oppdat_rae
6.3x c                   exsr      oppdat_rae_pg
     c                   read      pcraepf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av betingelser og rabattelement
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_bet    begsr
      *
     c                   eval      d_bet = pcbet
     c                   if        d_bet = *blank or
     c                             d_linx = *blank
     c                   leavesr
     c                   endif
      *
     c                   eval      w_ogrp = 0
     c                   eval      w_hgrp = 0
     c                   eval      w_ugrp = 0
     c                   eval      w_modn = 0
     c                   eval      w_vare = *blank
      *
      * Avbryter dersom leverandør ikke er utfylt
      *
     c                   if        d_ldor = 0
     c                   leavesr
     c                   endif
      *
      * Sletter gamle betingelser og rabattelement for forrige leverandør
      *
     c                   if        b_frst = *off and
     c                             d_ldor <> s_ldor
     c                   exsr      slett_gamle
     c                   endif
      *
     c                   eval      b_frst = *off
     c                   eval      s_ldor = d_ldor
      *
      * Henter betingelse. Dersom betingelse på varenummernivå, hentes
      * varegruppe og modul fra vare.
      *
     c                   if        d_ogrp <> '--'
     c                   move      d_ogrp        w_ogrp
     c                   endif
      *
     c                   if        d_hgrp <> '--'
     c                   move      d_hgrp        w_hgrp
     c                   endif
      *
     c                   if        d_ugrp <> '---'
     c                   move      d_ugrp        w_ugrp
     c                   endif
      *
     c                   if        d_modn <> '--------'
     c                   move      d_modn        w_modn
     c                   endif
      *
     c                   if        d_nobb <> '--------'
     c                   move      d_nobb        jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   eval      w_ogrp = jvogrp
     c                   eval      w_hgrp = jvhgrp
     c                   eval      w_ugrp = jvugrp
     c                   eval      w_modn = jvmodn
     c                   eval      w_vare = jvvare
     c                   endif
      *
      * Oversetter betingelsestekst til ASCII
      *
     c                   eval      d_beti = %trim(d_beti)
      *
     c     X'46':'Æ'     xlate     d_beti        d_beti
     c     X'A0':'æ'     xlate     d_beti        d_beti
     c     X'77':'Ø'     xlate     d_beti        d_beti
     c     X'90':'ø'     xlate     d_beti        d_beti
     c     X'2A':'Å'     xlate     d_beti        d_beti
     c     X'EF':'å'     xlate     d_beti        d_beti
      *
      * Oppdaterer/oppretter betingelse
      *
     c                   clear                   jbetlur
      *
     c                   eval      jbetlu_ldor = d_ldor
     c                   eval      jbetlu_prgr = *blank
     c                   eval      jbetlu_ogrp = w_ogrp
     c                   eval      jbetlu_hgrp = w_hgrp
     c                   eval      jbetlu_ugrp = w_ugrp
     c                   eval      jbetlu_modn = w_modn
     c                   eval      jbetlu_vare = w_vare
     c                   eval      jbetlu_vtyp = *blank
     c                   eval      jbetlu_lety = 0
     c                   eval      jbetlu_rsts = 0
     c                   eval      jbetlu_line = d_line
     c     jbetlu_key    chain     jbetlur
      *
     c                   if        not %found
     c                   eval      jbetla_ldor = d_ldor
     c                   eval      jbetla_ogrp = w_ogrp
     c                   eval      jbetla_hgrp = w_hgrp
     c                   eval      jbetla_ugrp = w_ugrp
     c                   eval      jbetla_modn = w_modn
     c                   eval      jbetla_vare = w_vare
     c                   eval      jbetla_vtyp = *blank
     c                   eval      jbetla_lety = 0
     c                   eval      jbetla_rsts = 0
     c                   eval      jbetla_beti = d_beti
     c                   eval      jbetla_prgr = *blank
     c     jbetla_key    chain     jbetla
     c                   if        %found
     c                   eval      jbetlu_ldor = jpldor
     c                   eval      jbetlu_prgr = jpprgr
     c                   eval      jbetlu_ogrp = jpogrp
     c                   eval      jbetlu_hgrp = jphgrp
     c                   eval      jbetlu_ugrp = jpugrp
     c                   eval      jbetlu_modn = jpmodn
     c                   eval      jbetlu_vare = jpvare
     c                   eval      jbetlu_vtyp = *blank
     c                   eval      jbetlu_lety = 0
     c                   eval      jbetlu_rsts = jprsts
     c                   eval      jbetlu_line = jpline
     c     jbetlu_key    chain     jbetlur
     c                   endif
     c                   endif
      *
     c                   eval      jpprgr = *blank
     c                   eval      jpogrp = w_ogrp
     c                   eval      jphgrp = w_hgrp
     c                   eval      jpugrp = w_ugrp
     c                   eval      jpmodn = w_modn
     c                   eval      jpvare = w_vare
     c                   eval      jprsts = 0
     c                   eval      jpline = d_line
     c                   eval      jpbeti = d_beti
     c                   eval      jpfdat = *loval
     c                   eval      jptdat = *loval
     c                   eval      jpdato = *loval
     c                   eval      jpeusr = l_user
     c                   eval      jpetim = w_etim
     c                   eval      jpedat = w_edat
      *
     c                   if        %found(jbetlu)
     c                   update    jbetlur
     c                   else
     c                   eval      jpfirm = w_firm
     c                   eval      jpldor = d_ldor
     c                   time                    jpodat
     c                   write     jbetlur
     c                   endif
      *
      * verdier fra betingelse til evt. prisgruppe
      *
     c                   eval      w_ldor_b = jpldor
     c                   eval      w_ogrp_b = jpogrp
     c                   eval      w_hgrp_b = jphgrp
     c                   eval      w_ugrp_b = jpugrp
     c                   eval      w_modn_b = jpmodn
     c                   eval      w_vare_b = jpvare
     c                   eval      w_vtyp_b = jpvtyp
     c                   eval      w_lety_b = jplety
     c                   eval      w_rsts_b = jprsts
     c                   eval      w_line_b = jpline
     c                   eval      w_beti_b = jpbeti
     c                   eval      w_fdat_b = jpfdat
     c                   eval      w_tdat_b = jptdat
     c                   eval      w_dato_b = jpdato
     c                   eval      w_akti_b = jpakti
     c                   eval      w_odat_b = jpodat
     c                   eval      w_edat_b = jpedat
     c                   eval      w_etim_b = jpetim
     c                   eval      w_eusr_b = jpeusr
6.3x  *
6.3x c     ra30l1_key    setll     ra30l1
6.3x c     ra30l1_key    reade     ra30l1
6.3x c                   dow       not %eof(ra30l1)
6.3x  *
6.3x c                   exsr      sjekk_prgr_be
6.3x c                   if        b_prgr = *on
6.3x  *
6.3x  * Oppdaterer/oppretter betingelse
6.3x  *
6.3x c                   clear                   jbetlur
6.3x  *
6.3x c                   eval      jbetlu_ldor = w_ldor_b
6.3x c                   eval      jbetlu_prgr = redkod
6.3x c                   eval      jbetlu_ogrp = w_ogrp_b
6.3x c                   eval      jbetlu_hgrp = w_hgrp_b
6.3x c                   eval      jbetlu_ugrp = w_ugrp_b
6.3x c                   eval      jbetlu_modn = w_modn_b
6.3x c                   eval      jbetlu_vare = w_vare_b
6.3x c                   eval      jbetlu_vtyp = w_vtyp_b
6.3x c                   eval      jbetlu_lety = w_lety_b
6.3x c                   eval      jbetlu_rsts = w_rsts_b
6.31 c                   eval      jbetlu_line = w_line_g
6.31 c                   eval      jbetla_beti = w_beti_g
6.3x c     jbetlu_key    chain     jbetlur
6.3x  *
6.3x  *
6.31 c                   eval      jpline = w_line_b
6.3x c                   eval      jpbeti = w_beti_b
6.3x c                   eval      jpfdat = w_fdat_b
6.3x c                   eval      jptdat = w_tdat_b
6.3x c                   eval      jpdato = w_dato_b
6.3x c                   eval      jpeusr = w_eusr_b
6.3x c                   eval      jpetim = w_etim_b
6.3x c                   eval      jpedat = w_edat_b
6.3x c                   if        b_akti_be = *on
6.3x c                   eval      jpakti = 1
6.3x c                   endif
6.3x  *
6.3x c                   if        %found(jbetlu)
6.3x c                   update    jbetlur
6.3x c                   else
6.3x c                   eval      jpfirm = w_firm
6.3x c                   eval      jpldor = jbetlu_ldor
6.3x c                   eval      jpprgr = jbetlu_prgr
6.3x c                   eval      jpogrp = jbetlu_ogrp
6.3x c                   eval      jphgrp = jbetlu_hgrp
6.3x c                   eval      jpugrp = jbetlu_ugrp
6.3x c                   eval      jpmodn = jbetlu_modn
6.3x c                   eval      jpvare = jbetlu_vare
6.3x c                   eval      jpvtyp = jbetlu_vtyp
6.3x c                   eval      jplety = jbetlu_lety
6.3x c                   eval      jprsts = jbetlu_rsts
6.3x c                   eval      jpline = jbetlu_line
6.3x c                   eval      jpbeti = jbetla_beti
6.3x c                   time                    jpodat
6.3x c                   write     jbetlur
6.3x c                   endif
6.31  *
6.31 c                   exsr      upd_rae_key
6.3x  *
     c                   endif
6.3x  *
6.3x c     ra30l1_key    reade     ra30l1
6.3x c                   enddo
6.3x
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av ikke oppdaterte betingelser og
      * rabattelement
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_gamle   begsr
      *
      * Sletter gamle "ikke egne" betingelser og rabattelementer for
      * leverandør
      *
     c                   eval      jbetlu_ldor = s_ldor
     c     jbetlu_key2   setll     jbetlu
     c     jbetlu_key2   reade     jbetlur
     c                   dow       not %eof
      *
6.35 c                   if        jprsts = 0
6.35 c                   if        (jpedat <> w_edat and
6.35 c                             jpetim <> w_etim) or
6.35 c                             (jpedat =  w_edat and
6.35 c                             jpetim <> w_etim)
      *
     c                   delete    jbetlur
      *
     c                   eval      jraelu_eldo = jpldor
6.3x c                   eval      jraelu_eprg = jpprgr
     c                   eval      jraelu_eogr = jpogrp
     c                   eval      jraelu_ehgr = jphgrp
     c                   eval      jraelu_eugr = jpugrp
     c                   eval      jraelu_emod = jpmodn
     c                   eval      jraelu_evar = jpvare
     c                   eval      jraelu_evty = jpvtyp
     c                   eval      jraelu_elet = jplety
     c                   eval      jraelu_ebrs = jprsts
     c                   eval      jraelu_ebli = jpline
     c     jraelu_key    setll     jraelu
     c     jraelu_key    reade     jraelur
     c                   dow       not %eof
6.33 c                   if        jperst = 0
     c                   delete    jraelur
6.33 c                   endif
     c     jraelu_key    reade     jraelur
     c                   enddo
      *
     c                   endif
6.35 c                   endif
      *
     c     jbetlu_key2   reade     jbetlur
     c                   enddo
      *
     c                   endsr
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for oppdatering av rabattelement sin nøkkel i forhold
6.31  * linjenr. Dette gjelder de med prisgruppe.
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     upd_rae_key   begsr
6.31  *
6.31 c                   eval      jraelu_eldo = w_ldor_b
6.31 c                   eval      jraelu_eprg = redkod
6.31 c                   eval      jraelu_eogr = w_ogrp_b
6.31 c                   eval      jraelu_ehgr = w_hgrp_b
6.31 c                   eval      jraelu_eugr = w_ugrp_b
6.31 c                   eval      jraelu_emod = w_modn_b
6.31 c                   eval      jraelu_evar = w_vare_b
6.31 c                   eval      jraelu_evty = w_vtyp_b
6.31 c                   eval      jraelu_elet = w_lety_b
6.31 c                   eval      jraelu_ebrs = w_rsts_b
6.31 c                   eval      jraelu_ebli = w_line_g
6.31 c     jraelu_key    setll     jraelur
6.31 c     jraelu_key    reade     jraelur
6.31 c                   dow       not %eof(jraelu)
6.32 c                   if        jperst = 0
6.31 c                   delete    jraelur
6.32 c                   endif
6.31 c     jraelu_key    reade     jraelur
6.31 c                   enddo
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av rabattelement
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_rae    begsr
      *
     c                   eval      d_rae = pcrae
      *
      * Avbryter dersom leverandør ikke er utfylt
      *
     c     ' ':'0'       xlate     d_eldx        d_eldx
     c                   move      d_eldx        d_eldo
     c                   if        d_eldo = 0
     c                   leavesr
     c                   endif
      *
      * Ved "ny" leverandør slettes eksisterende "ikke egne" rabattelement
      * for leverandøren
      *
     c                   if        d_eldo <> s_eldo
     c                   eval      jraelu_eldo = d_eldo
     c     jraelu_key2   setll     jraelu
     c     jraelu_key2   reade     jraelur
     c                   dow       not %eof
6.34 c                   if        (jperst = 0)
6.34 c**                           (jpeprg = *blank)
     c                   delete    jraelur
     c                   endif
     c     jraelu_key2   reade     jraelur
     c                   enddo
     c                   endif
      *
     c                   eval      s_eldo = d_eldo
      *
      * Initierer fil
      *
     c                   clear                   jraelur
      *
      * Henter rabattelement
      *
     c                   if        d_eogr <> '--'
     c                   move      d_eogr        jpeogr
     c                   endif
      *
     c                   if        d_ehgr <> '--'
     c                   move      d_ehgr        jpehgr
     c                   endif
      *
     c                   if        d_eugr <> '---'
     c                   move      d_eugr        jpeugr
     c                   endif
      *
     c                   if        d_emod <> '--------'
     c                   move      d_emod        jpemod
     c                   endif
      *
     c                   if        d_enob <> '--------'
     c                   move      d_enob        jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   eval      jpeogr = jvogrp
     c                   eval      jpehgr = jvhgrp
     c                   eval      jpeugr = jvugrp
     c                   eval      jpemod = jvmodn
     c                   eval      jpevar = jvvare
     c                   endif
      *
      * Oversetter rabattelement-tekst til ASCII
      *
     c                   eval      d_erae = %trim(d_erae)
      *
     c     X'46':'Æ'     xlate     d_erae        d_erae
     c     X'A0':'æ'     xlate     d_erae        d_erae
     c     X'77':'Ø'     xlate     d_erae        d_erae
     c     X'90':'ø'     xlate     d_erae        d_erae
     c     X'2A':'Å'     xlate     d_erae        d_erae
     c     X'EF':'å'     xlate     d_erae        d_erae
      *
      * Sjekker om betingelse finnes for element
      *
     c                   eval      jbetl1_ldor = d_eldo
6.3x c                   eval      jbetl1_prgr = *blank
     c                   eval      jbetl1_ogrp = jpeogr
     c                   eval      jbetl1_hgrp = jpehgr
     c                   eval      jbetl1_ugrp = jpeugr
     c                   eval      jbetl1_modn = jpemod
     c                   eval      jbetl1_vare = jpevar
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = 0
     c                   eval      jbetl1_rsts = 0
     c                   eval      jbetl1_line = d_elin
     c     jbetl1_key    chain     jbetl1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * Oppretter rabattelement
      *
      * ..finner regel
      *
     c                   select
     c                   when      %trim(d_ereg) = '%'
     c                   eval      jpereg = '1'
     c                   when      %trim(d_ereg) = '(%)'
     c                   eval      jpereg = '2'
     c                   when      %trim(d_ereg) = 'NT'
     c                   eval      jpereg = 'N'
     c                   when      %trim(d_ereg) = 'KR'
     c                   eval      jpereg = 'K'
     c                   endsl
6.21  *
6.21  * ..finner fra / til dato
6.21  *
6.21 c                   if        d_efda <> *blank
6.21 c                   eval      w_dato = %subst(d_efda:7:4) + '-' +
6.21 c                                      %subst(d_efda:4:2) + '-' +
6.21 c                                      %subst(d_efda:1:2)
6.21 c                   move      w_dato        w_efda
6.21 c                   else
6.21 c                   eval      w_efda = *loval
6.21 c                   endif
6.21  *
6.21 c                   if        d_etda <> *blank
6.21 c                   eval      w_dato = %subst(d_etda:7:4) + '-' +
6.21 c                                      %subst(d_etda:4:2) + '-' +
6.21 c                                      %subst(d_etda:1:2)
6.21 c                   move      w_dato        w_etda
6.21 c                   else
6.21 c                   eval      w_etda = *loval
6.21 c                   endif
      *
      * ..finner verdi. Må fjerne ledende fortegn og desimalskilletegn
      *
     c                   if        %scan('-' : d_ever) = 0
     c                   eval      b_nega = *off
     c                   else
     c                   eval      b_nega = *on
     c                   endif
      *
     c     ' ':'0'       xlate     d_ever        w_verd
     c     '-':'0'       xlate     w_verd        w_verd
     c     '+':'0'       xlate     w_verd        w_verd
      *
     c                   select
     c                   when      %scan(',' : w_verd) = 0
     c                   eval      w_verd = %subst(w_verd:4:8) + ',00'
     c                   when      %scan(',' : w_verd) = 10
     c                   eval      w_verd = %subst(w_verd:2:10) + '0'
     c                   when      %scan(',' : w_verd) = 8
     c                   eval      w_verd = '0' + %subst(w_verd:1:10)
     c                   when      %scan(',' : w_verd) = 7
     c                   eval      w_verd = '00' + %subst(w_verd:1:9)
     c                   when      %scan(',' : w_verd) = 6
     c                   eval      w_verd = '000' + %subst(w_verd:1:8)
     c                   when      %scan(',' : w_verd) = 5
     c                   eval      w_verd = '0000' + %subst(w_verd:1:7)
     c                   when      %scan(',' : w_verd) = 4
     c                   eval      w_verd = '00000' + %subst(w_verd:1:6)
     c                   when      %scan(',' : w_verd) = 3
     c                   eval      w_verd = '000000' + %subst(w_verd:1:5)
     c                   when      %scan(',' : w_verd) = 2
     c                   eval      w_verd = '0000000' + %subst(w_verd:1:4)
     c                   endsl
      *
     c                   eval      w_verd = %subst(w_verd:1:8) +
     c                                      %subst(w_verd:10:2)
      *
     c                   movel     w_verd        w_verd_10
      *
     c                   eval      jpever = w_verd_10 / 100
      *
     c                   if        b_nega = *on and
     c                             jpereg = 'N'
     c                   eval      jpever = jpever * -1
     c                   endif
      *
     c                   if        b_nega = *on and
     c                             jpereg = 'K'
     c                   eval      jpever = jpever * -1
     c                   endif
      *
     c                   if        b_nega = *off and
     c                             (jpereg = '1' or jpereg = '2')
     c                   eval      jpever = jpever * -1
     c                   endif
      *
     c                   eval      w_elin = w_elin + 1
      *
     c                   eval      jpefir = w_firm
     c                   eval      jpeldo = d_eldo
6.3x c                   eval      jpeprg = *blank
     c                   eval      jpeogr = jpeogr
     c                   eval      jpehgr = jpehgr
     c                   eval      jpeugr = jpeugr
     c                   eval      jpemod = jpemod
     c                   eval      jpevar = jpevar
     c                   eval      jpebrs = 0
     c                   eval      jpebli = d_elin
     c                   eval      jperst = 0
     c                   eval      jpelin = w_elin
     c                   eval      jperae = d_erae
6.21 c                   eval      jpefda = w_efda
6.21 c                   eval      jpetda = w_etda
     c                   eval      jpeeus = l_user
      *
6.3x c                   eval      w_prgr = *blank
     c                   exsr      sjekk_endr
     c                   if        jpwakt = 1  and
     c                             jpwrae = jperae
     c                   eval      jpeakt = 1
     c                   endif
      *
     c                   time                    jpeoda
     c                   time                    jpeeda
     c                   time                    jpeeti
      *
     c                   write     jraelur
6.3x  *
6.3x c                   eval      w_efir = jpefir
6.3x c                   eval      w_eldo = jpeldo
6.3x c                   eval      w_eogr = jpeogr
6.3x c                   eval      w_ehgr = jpehgr
6.3x c                   eval      w_eugr = jpeugr
6.3x c                   eval      w_emod = jpemod
6.3x c                   eval      w_evar = jpevar
6.3x c                   eval      w_evty = jpevty
6.3x c                   eval      w_elet = jpelet
6.3x c                   eval      w_ebrs = jpebrs
6.3x c                   eval      w_ebli = jpebli
6.3x c                   eval      w_erst = jperst
6.3x c                   eval      w_erae = jperae
6.3x c                   eval      w_eeus = jpeeus
6.3x c                   eval      w_eoda = jpeoda
6.3x c                   eval      w_eeda = jpeeda
6.3x c                   eval      w_eeti = jpeeti
      *
     c                   endsr
6.3x  *
6.3x  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3x  * Subrutine for oppdatering av rabattelement pr. prisgruppe
6.3x  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3x  *
6.3x c     oppdat_rae_pg begsr
6.3x  *
6.3x  * behandler alle prisgruper
6.3x  *
6.3x  *
6.3x c     ra30l1_key    setll     ra30l1
6.3x c     ra30l1_key    reade     ra30l1
6.3x c                   dow       not %eof(ra30l1)
6.3x  *
6.3x  * Sjekker om betingelse finnes for element
6.3x  *
6.3x c                   eval      jbetl1_ldor = w_eldo
6.3x c                   eval      jbetl1_prgr = redkod
6.3x c                   eval      jbetl1_ogrp = w_eogr
6.3x c                   eval      jbetl1_hgrp = w_ehgr
6.3x c                   eval      jbetl1_ugrp = w_eugr
6.3x c                   eval      jbetl1_modn = w_emod
6.3x c                   eval      jbetl1_vare = w_evar
6.3x c                   eval      jbetl1_vtyp = w_evty
6.3x c                   eval      jbetl1_lety = w_elet
6.3x c                   eval      jbetl1_rsts = w_ebrs
6.3x c                   eval      jbetl1_line = w_ebli
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x  *
6.3x c                   eval      jpefir = w_firm
6.3x c                   eval      jpeldo = w_eldo
6.3x c                   eval      jpeprg = redkod
6.3x c                   eval      jpeogr = w_eogr
6.3x c                   eval      jpehgr = w_ehgr
6.3x c                   eval      jpeugr = w_eugr
6.3x c                   eval      jpemod = w_emod
6.3x c                   eval      jpevar = w_evar
6.3x c                   eval      jpevty = w_evty
6.3x c                   eval      jpelet = w_elet
6.3x c                   eval      jpebrs = w_ebrs
6.3x c                   eval      jpebli = w_ebli
6.3x c                   eval      jperst = w_erst
6.3x c                   eval      jpelin = w_elin
6.3x c                   eval      jperae = w_erae
6.3x c                   eval      jpefda = w_efda
6.3x c                   eval      jpetda = w_etda
6.3x c                   eval      jpeeus = w_eeus
6.3x c                   eval      jpeoda = w_eoda
6.3x c                   eval      jpeeda = w_eeda
6.3x c                   eval      jpeeti = w_eeti
6.3x  *
6.3x c                   time                    jpeoda
6.3x c                   time                    jpeeda
6.3x c                   time                    jpeeti
6.3x  *
6.3x c                   write     jraelur
      *
     c                   endif
6.3x  *
6.3x c     ra30l1_key    reade     ra30l1
6.3x c                   enddo
6.3x
6.3x  *
6.3x c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om det har vært endring i rabattelementet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_endr    begsr
      *
     c                   eval      b_endr = *off
      *
     c                   eval      jrawl1_eldo = jpeldo
6.3x c                   eval      jrawl1_eprg = w_prgr
     c                   eval      jrawl1_eogr = jpeogr
     c                   eval      jrawl1_ehgr = jpehgr
     c                   eval      jrawl1_eugr = jpeugr
     c                   eval      jrawl1_emod = jpemod
     c                   eval      jrawl1_evar = jpevar
     c                   eval      jrawl1_evty = jpevty
     c                   eval      jrawl1_elet = jpelet
     c                   eval      jrawl1_ebrs = jpebrs
     c                   eval      jrawl1_ebli = jpebli
     c     jrawl1_key    chain     jrawl1
     c                   if        %found(jrawl1)
     c                   if        d_erae <> jperae
     c                   eval      b_endr = *on
     c*                  exsr      skriv
     c                   endif
     c                   else
     c                   exsr      skriv
     c                   endif
      *
     c                   endsr
6.3x  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3x  * Subrutine for å sjekke om rabattelement har hatt prisgruppe tidligere
6.3x  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3x  *
6.3x c     sjekk_prgr    begsr
6.3x  *
6.3x c                   eval      b_prgr = *off
6.3x  *
6.3x c                   eval      jrawl1_eldo = jpeldo
6.3x c                   eval      jrawl1_eprg = redkod
6.3x c                   eval      jrawl1_eogr = jpeogr
6.3x c                   eval      jrawl1_ehgr = jpehgr
6.3x c                   eval      jrawl1_eugr = jpeugr
6.3x c                   eval      jrawl1_emod = jpemod
6.3x c                   eval      jrawl1_evar = jpevar
6.3x c                   eval      jrawl1_evty = jpevty
6.3x c                   eval      jrawl1_elet = jpelet
6.3x c                   eval      jrawl1_ebrs = jpebrs
6.3x c                   eval      jrawl1_ebli = jpebli
6.3x c     jrawl1_key    chain     jrawl1
6.3x c                   if        %found(jrawl1)
6.3x c                   eval      b_prgr = *on
6.3x c*                  exsr      skriv
6.3x c                   endif
6.3x  *
6.3x c                   endsr
6.3x  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3x  * Subrutine for å sjekke om betingelse har hatt prisgruppe tidligere
6.3x  * som har vært aktivisert
6.3x  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3x  *
6.3x c     sjekk_prgr_be begsr
6.3x  *
6.3x c                   eval      b_prgr = *off
6.3x c                   eval      b_akti_be = *off
6.3x  *
6.3x c                   eval      jbewl1_ldor = w_ldor_b
6.3x c                   eval      jbewl1_prgr = redkod
6.3x c                   eval      jbewl1_ogrp = w_ogrp_b
6.3x c                   eval      jbewl1_hgrp = w_hgrp_b
6.3x c                   eval      jbewl1_ugrp = w_ugrp_b
6.3x c                   eval      jbewl1_modn = w_modn_b
6.3x c                   eval      jbewl1_vare = w_vare_b
6.3x c                   eval      jbewl1_vtyp = w_vtyp_b
6.3x c                   eval      jbewl1_lety = w_lety_b
6.3x c                   eval      jbewl1_rsts = w_rsts_b
6.3x c                   eval      jbewl1_line = w_line_b
6.3x c     jbewl1_key    chain     jbewl1
6.31  *
6.31 c                   if        not %found
6.31 c                   eval      jbewla_ldor = w_ldor_b
6.31 c                   eval      jbewla_ogrp = w_ogrp_b
6.31 c                   eval      jbewla_hgrp = w_hgrp_b
6.31 c                   eval      jbewla_ugrp = w_ugrp_b
6.31 c                   eval      jbewla_modn = w_modn_b
6.31 c                   eval      jbewla_vare = w_vare_b
6.31 c                   eval      jbewla_vtyp = *blank
6.31 c                   eval      jbewla_lety = 0
6.31 c                   eval      jbewla_rsts = 0
6.31 c                   eval      jbewla_beti = w_beti_b
6.31 c                   eval      jbewla_prgr = redkod
6.31 c     jbewla_key    chain     jbewla
6.31 c                   endif
6.31 c                   if        %found
6.31 c                   eval      w_line_g = jpline
6.31 c                   eval      w_beti_g = jpbeti
6.31 c                   eval      b_prgr = *on
6.31 c                   if        jpakti = 1
6.31 c                   eval      b_akti_be = *on
6.31 c                   endif
6.31 c*                  exsr      skriv
6.31 c                   endif
6.31  *
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive endringer vedr. rabatt-elementer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv         begsr
      *
     c                   if        b_head = *off
     c                   exsr      skriv_head
     c                   eval      b_head = *on
     c                   endif
      *
     c                   if        b_endr = *on
     c                   eval      w_stat = 'endring'
     c                   else
     c                   eval      w_stat = 'ny'
     c                   endif
      *
     c                   eval      jwdata =  %char(jpeldo)   + X'05' +
     c                                     %trim(jpeprg)     + X'05' +
     c                                     %char(jpeogr)     + X'05' +
     c                                     %char(jpehgr)     + X'05' +
     c                                     %char(jpeugr)     + X'05' +
     c                                     %char(jpemod)     + X'05' +
     c                                     %trim(jpevar)     + X'05' +
     c                                     %trim(jpevty)     + X'05' +
     c                                     %char(jpelet)     + X'05' +
     c                                     %char(jpebli)     + X'05' +
     c                                     %char(jpelin)     + X'05' +
     c                                     %trim(jperae)     + X'05' +
     c                                     %trim(jpereg)     + X'05' +
     c                                     %char(jpever)     + X'05' +
     c                                     %trim(w_stat)     + X'05'
     c                   write     jwrkpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     skriv_head    begsr
      *
      *
     c                   eval      jwdata =  %trim('Leverandør')   + X'05' +
     c                                     %trim('Prgr')   + X'05' +
     c                                     %trim('Ogrp')   + X'05' +
     c                                     %trim('Hgrp')     + X'05' +
     c                                     %trim('Ugrp')     + X'05' +
     c                                     %trim('Modul')    + X'05' +
     c                                     %trim('Varenr')   + X'05' +
     c                                     %trim('Varetype') + X'05' +
     c                                     %trim('Lev.typ')  + X'05' +
     c                                     %trim('Linjenr')  + X'05' +
     c                                     %trim('Linjenr' ) + X'05' +
     c                                     %trim('Rab.el.')  + X'05' +
     c                                     %trim('Regel')    + X'05' +
     c                                     %trim('Verdi')    + X'05'
     c                   write     jwrkpfr
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på betingelse
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetl1_ldor
6.3x c                   kfld                    jbetl1_prgr
     c                   kfld                    jbetl1_ogrp
     c                   kfld                    jbetl1_hgrp
     c                   kfld                    jbetl1_ugrp
     c                   kfld                    jbetl1_modn
     c                   kfld                    jbetl1_vare
     c                   kfld                    jbetl1_vtyp
     c                   kfld                    jbetl1_lety
     c                   kfld                    jbetl1_rsts
     c                   kfld                    jbetl1_line
      *
     c     jbetla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetla_ldor
     c                   kfld                    jbetla_ogrp
     c                   kfld                    jbetla_hgrp
     c                   kfld                    jbetla_ugrp
     c                   kfld                    jbetla_modn
     c                   kfld                    jbetla_vare
     c                   kfld                    jbetla_vtyp
     c                   kfld                    jbetla_lety
     c                   kfld                    jbetla_rsts
     c                   kfld                    jbetla_beti
6.3x c                   kfld                    jbetla_prgr
      *
      * Key for oppdatering av betingelser
      *
     c     jbetlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlu_ldor
6.3x c                   kfld                    jbetlu_prgr
     c                   kfld                    jbetlu_ogrp
     c                   kfld                    jbetlu_hgrp
     c                   kfld                    jbetlu_ugrp
     c                   kfld                    jbetlu_modn
     c                   kfld                    jbetlu_vare
     c                   kfld                    jbetlu_vtyp
     c                   kfld                    jbetlu_lety
     c                   kfld                    jbetlu_rsts
     c                   kfld                    jbetlu_line
      *
     c     jbetlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlu_ldor
      *
      * Key for oppdatering av rabattelement
      *
     c     jraelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jraelu_eldo
6.3x c                   kfld                    jraelu_eprg
     c                   kfld                    jraelu_eogr
     c                   kfld                    jraelu_ehgr
     c                   kfld                    jraelu_eugr
     c                   kfld                    jraelu_emod
     c                   kfld                    jraelu_evar
     c                   kfld                    jraelu_evty
     c                   kfld                    jraelu_elet
     c                   kfld                    jraelu_ebrs
     c                   kfld                    jraelu_ebli
      *
     c     jraelu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    jraelu_eldo
6.3x  *
6.3x c     jraelu_key3   klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    jraelu_eldo
6.3x c                   kfld                    jraelu_eprg
6.3x  *
      * Key for les av backupfil for å sjekke endringer
      *
     c     jrawl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrawl1_eldo
6.3x c                   kfld                    jrawl1_eprg
     c                   kfld                    jrawl1_eogr
     c                   kfld                    jrawl1_ehgr
     c                   kfld                    jrawl1_eugr
     c                   kfld                    jrawl1_emod
     c                   kfld                    jrawl1_evar
     c                   kfld                    jrawl1_evty
     c                   kfld                    jrawl1_elet
     c                   kfld                    jrawl1_ebrs
     c                   kfld                    jrawl1_ebli
6.3x  *
6.3x  * Key for les av backupfil betingelser for å sjekke endringer
6.3x  *
6.3x c     jbewl1_key    klist
5.70 c                   kfld                    w_firm
6.3x c                   kfld                    jbewl1_ldor
6.3x c                   kfld                    jbewl1_prgr
6.3x c                   kfld                    jbewl1_ogrp
6.3x c                   kfld                    jbewl1_hgrp
6.3x c                   kfld                    jbewl1_ugrp
6.3x c                   kfld                    jbewl1_modn
6.3x c                   kfld                    jbewl1_vare
6.3x c                   kfld                    jbewl1_vtyp
6.3x c                   kfld                    jbewl1_lety
6.3x c                   kfld                    jbewl1_rsts
6.3x c                   kfld                    jbewl1_line
6.31  *
6.31 c     jbewla_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    jbewla_ldor
6.31 c                   kfld                    jbewla_ogrp
6.31 c                   kfld                    jbewla_hgrp
6.31 c                   kfld                    jbewla_ugrp
6.31 c                   kfld                    jbewla_modn
6.31 c                   kfld                    jbewla_vare
6.31 c                   kfld                    jbewla_vtyp
6.31 c                   kfld                    jbewla_lety
6.31 c                   kfld                    jbewla_rsts
6.31 c                   kfld                    jbewla_beti
6.31 c                   kfld                    jbewla_prgr
5.70  *
5.70  * Key for les på prisgruppe
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      s_ldor = 0
      *
     c                   endsr
