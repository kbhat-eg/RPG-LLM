     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RC651R
      * Beskrivelse . . : Inkasso - utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                            Sign
      * --------- ------  ---------------------------------------------- ----
      * R3.40     220202  Rettet feil i utvalg av enkeltkunder           KOB
T3.30 * R3.40     210502  Rettet feil i utvalg av 10 enkeltkunder        HFL
T3.30 * R3.40     140802  Kreditposter tas med uavhengig av forfallsdato.HFL
T5.40 * R5.40     090106  Endret test på antall purringer.               HFL
T5.40 * R5.40     240106  Det ble ikke tatt hensyn til løpedager.        HFL
      * R5.60      021108 Endret til bruk av ny timestamp på RKWBPF      KOB
6.10  * V6.10     230909  Lagt inn sporingsfelter                        bsa
6.11  * V6.11     060510  Endret sporinfunksjonalitet                    bsa
6.12  * V6.20     040414  Lagt inn hardkodet test på bilagskode 97       ask
6.13  * V6.20     090514  Kontrollerer om kunde er utelatt fra inkasso   ask
7.00  * V7.00     020519  Lagt inn bilagskoder som ikke skal med.        ask
      * __________________________________________________________________________________________
Korr- *SYS  Referanse  Dato   Endringsbeskrivelse                                 Sig
versj.*---  --------- ------  --------------------------------------------------- ---
7.00  *K00  NXS-1234  140420  Ikke sende til Inkasso dersom RKINKK = 'N'          sst
      *                       Ikke oppdatere kunde med RKINNK = 'N'  (Skattum)    sst
9.01  * V6.20     140415  Lagt inn hardkodet test på bilagskode 65       ask
      *
      *  BRUK AV INDIKATORER                                            *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
6.13 frkbplr    if   e           k disk    rename(rkbppfr:rkbplrr)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     frkwbpf    uf a e           k disk    rename(rkwbpfr:rkwbpfr)
7.02 frkwblu    uf   e           k disk    rename(rkwbpfr:rkwblur)
7.00 frca0l1    if   e           k disk    rename(rca0pfr:rca0l1r)
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  ppros                461    463  1
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pparam               300    490
      *
     d l_user                911    920
7.02 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_fdat          s               d   datfmt(*iso)
     d w_forf          s               d   datfmt(*iso)
     d w_rest          s                   like(rnrest)
     d w_firm          s                   like(rkfirm)
      *
     d                 ds
     d w_dato                         6  0
     d  w_dag                         2  0 overlay(w_dato)
     d  w_mnd                         2  0 overlay(w_dato:3)
     d  w_aar                         2  0 overlay(w_dato:5)
      *
     d w_udat          s              6  0
      *
      *__________________________________________________________________
      *    NB   NB  NB   NB   NB   MB   NB   NB   NB   NB   NB   NB   NB
      *         RKINKA er def her for } kunne teste
     d rkinka          s              1  0 inz(0)
      *    NB   NB  NB   NB   NB   MB   NB   NB   NB   NB   NB   NB   NB
      *__________________________________________________________________
      *
      * Definering av variable
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
     d n               s              2  0
     d wkun            s              7  0
     d rktrl1_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
6.13 d rkbplr_bkun     s                   like(rkbkun)
7.02 d rkwblu_kund     s                   like(rkkund)
7.00 d rca0l1_firm     s                   like(rc0fir)
      * Eksternt program    (Brukes ikke foreløpig) (Skattum ble for spes.)
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.02 d u_inkk          s              1    inz(*off)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Utplukk av kunder til inkassovarsel                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      w_firm = l_firm
      *
     c     rkunlu_key    setll     rkunlu                                 61
     c     rkunlu_key    reade     rkunlu                                 61
      *
     c                   dow       *in61 = *off
      *
      * Kunden skal ikke behandles
      *
      *                            rkrenk = 1,  Skal ikke renteberegnes
      *                                     2,  Skal ikke renteberegnes
      *                                           og ikke purres
      *                            rkrenk = 3,  Skal renteberegnes
      *                                          men ikke purres
     c                   if        rkrenk = 2
     c**                           or ppkod = 'P'
     c**                              and rkrenk = ' '
     c                             or ppkod = 'I'
     c                                and (rkrenk = 2 or rkrenk = 3)
     c                   goto      nykund
     c                   endif
     **  START HEr
     ***
      *   Mestergruppen har en kode som herer RKINKA i RKBPPF
6.13  * Sjekk om kunde er utelatt fra inkasso
6.13  *
6.13 c                   eval      rkbplr_bkun = rkkund
6.13 c     rkbplr_key    chain     rkbplr
6.13 c                   if        %found and
6.13 c                             rkinka = 1
6.13 c                   goto      nykund
6.13 c                   endif
6.30  *  Alt. dersom NX = 'RC651C_Ikke_oppd_Kunde_m_INKK=N'   på skal ikke kunde behandles dersom rk
6.30  *                            (Skattum)
6.30 c                   if        U_Inkk = *on
6.30 c                             and rkinkk = 'N'
6.30 c                   goto      nykund
6.30 c                   endif
      *
      * Sjekk om kunden er oppgitt i tabell
      *
     c                   eval      n = 1
     c                   xfoot     kun           wkun
      *
     c                   if        wkun <> 0
     c                   dou       n > 10
     c                   if        rkkund = kun(n)
     c                   goto      kunmed
     c                   endif
     c                   eval      n = n + 1
     c                   enddo
T3.30c                   goto      nykund
     c                   endif
      *
     c                   if        rkkund < pknrf or
     c                             rkkund > pknrt
     c                   goto      nykund
     c                   endif
      *
     c     kunmed        tag
      *
     c                   if        rksald <= 0
     c                   goto      nykund
     c                   endif
      *
     c                   if        rkkatg < pkatf or
     c                             rkkatg > pkatt
     c                   goto      nykund
     c                   endif
      *
     c                   if        rkslgr < pself or
     c                             rkslgr > pselt
     c                   goto      nykund
     c                   endif
      *
     c                   exsr      beh_trans
      *
7.02 c                   if        u_inkk = *on
7.02 c                   exsr      slett_trans
7.02 c                   goto      nykund
7.02 c                   endif
      *
     c                   if        w_rest <= 0
     c                   eval      rkinkk = 'N'
     c                   else
     c                   eval      rkinkk = ' '
     c                   endif
      *
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RC651R    '
     c                   update    rkunlur
      *
     c     nykund        tag
      *
     c                   eval      w_rest = 0
      *
     c     rkunlu_key    reade     rkunlu                                 61
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
7.02 c                   if        u_inkk = *off
7.02 c                   exsr      slett_trans_a
7.02 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Utplukk av kundeposter til inkassovarsel                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beh_trans     begsr
      *
     c                   eval      w_rest = 0
      *
     c                   eval      rktrl1_kund = rkkund
      *
     c     rktrl1_key    setll     rktrl1                                 62
     c     rktrl1_key    reade     rktrl1                                 62
      *
     c                   dow       *in62 = *off
      *
      * Sjekk avdeling
      *
     c                   if        rnavde = *zeros
     c                   eval      rnavde = rkavde
     c                   endif
      *
     c                   if        pavdf <> *zeros and
     c                             rnavde < pavdf
     c                   goto      neste
     c                   endif
      *
     c                   if        pavdt <> *zeros and
     c                             rnavde > pavdt
     c                   goto      neste
     c                   endif
      *
      * Sendt til inkasso tidligere
      *
     c                   if        rninko = 'I' or
     c                             rninko = 'J'
     c                   goto      neste
     c                   endif
      *
      * Reklamasjon
      *
     c                   if        rnbilk = 89  or
     c                             rnrekl = 'J'
     c                   goto      neste
     c                   endif
      *
      * Restbeløp = 0
      *
     c                   if        rnrest = 0
     c                   goto      neste
     c                   endif
      *
      * Sjekk antall purringer
      *
     c                   if        rnantp < ppunr
     c                   goto      neste
     c                   endif
7.01  *
7.00  * Utelater alle poster med bilagskoder fra rca0pf
7.00  *
7.00 c                   if           rnbilk = rcubk1
7.00 c                             or rnbilk = rcubk2
7.00 c                             or rnbilk = rcubk3
7.00 c                             or rnbilk = rcubk4
7.00 c                             or rnbilk = rcubk5
7.00 c                   goto      neste
7.00 c                   endif
9.01  *
9.01  * Utelater alle poster med bilagskode 65 (saknr 57598)
9.01  *
9.01 c                   if        rnbilk = 65
9.01 c                   goto      neste
9.01 c                   endif
      *
T3.30 * Kreditposter tas med uavhengig av forfallsdato
      *
T3.30c                   if        rnrest < 0
T3.30c                   goto      ikkeff
T3.30c                   endif
      *
      * Sjekk forfallsdato mot
      * inkassodato + løpedager
      *
     c     rnfdat        adddur    plope : *d    w_forf
      *
T5.40c                   if        w_forf >= w_fdat
     c                   goto      neste
     c                   endif
      *
T3.30c     ikkeff        tag
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
      *
     c                   eval      w_rest = w_rest + rnrest
      *
     c     neste         tag
      *
     c     rktrl1_key    reade     rktrl1                                 62
     c                   enddo
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Slett poster som gir negativ saldo  alle kunder                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     slett_trans_a begsr
      *
     c     rkwbpf_key    setll     rkwbpf                                 63
     c     rkwbpf_key    reade     rkwbpf                                 63
      *
     c                   dow       *in63 = *off
      *
      * Sjekk kunde
      *
     c                   eval      rkunl1_kund = rnkund
     c     rkunl1_key    chain     rkunl1                             64
      *
     c                   if        rkinkk = 'N'
     c                   delete    rkwbpfr
     c                   endif
      *
     c     rkwbpf_key    reade     rkwbpf                                 63
     c                   enddo
      *
     c                   endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.02  * Slett poster som gir negativ saldo                              *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.02  *
7.02 c     slett_trans   begsr
7.02  *
7.02  * Behandle en kunde
7.02  *
7.02 c                   eval      rkwblu_kund = rkkund
7.02 c     rkwblu_key    setll     rkwblu                                 63
7.02 c     rkwblu_key    reade     rkwblu                                 63
7.02  *
7.02 c                   dow       *in63 = *off
7.02  *
7.02 c                   delete    rkwblur
7.02  *
7.02 c     rkwblu_key    reade     rkwblu                                 63
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *dmy          move      pfdat         w_fdat
      *
      * Key for lesing av kunderegister
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
      *
      * Key for lesing av kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
6.13  *
6.13  * Key for oppslag på kundetilleggsregister
6.13  *
6.13 c     rkbplr_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    rkbplr_bkun
      *
      * Key for lesing av kundeposteringer
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
      * Key for lesing av utplukkede posteringer
      *
     c     rkwbpf_key    klist
     c                   kfld                    w_firm
      *
      * Key for lesing av utplukkede posteringer
      *
7.02 c     rkwblu_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    rkwblu_kund
7.00  *
7.00  * Key for lesing av statusinfo(her bilagskoder som skal utelates)
7.00  *
7.00 c     rca0l1_key    klist
7.00 c                   kfld                    w_firm
7.00  *
7.00 c                   eval      w_firm = l_firm
7.00 c     rca0l1_key    chain     rca0l1
7.01  *
7.01  * Skal inkassokode settes = 'N'   (Skattum variant)
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RC651C_Ikke_oppd_Kunde_m_INKK=N':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_Inkk = *on;
7.01   endif;
7.00  *
     c                   endsr
