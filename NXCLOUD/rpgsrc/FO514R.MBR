     H/TITLE  ASOFAK: Spørring mot ordre-linje-register, Varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO514R
      * Beskrivelse . . : Spørring mot ordre-linje/ Varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * R6.30 170616 nho  Flytter ut lev.dato fra linje hvis <> 0
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     FFO514D    CF   E             WORKSTN
     F                                     SFILE(D1SFL:SRRN02)
      *
     Frkunl1    IF   E           K DISK
     F                                     RENAME(RKUNPFR:rkunl1r)
      *
     FFOHEL1    IF   E           K DISK
     F                                     RENAME(FOHEPFR:FOHEL1R)
     FFODTLV    IF   E           K DISK
     F                                     RENAME(FODTPFR:FODTLVR)
      *
      * Definering av variable
      *
     d w_alf1          s              1
      *
      *
     D                UDS
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
     D  l_fnav               951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Kaller opp subrutine for å vise ordre-linjer                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVEL     WPVARE        D2VARE
     C                   MOVEL     WPTEK1        D2TEK1
      *
     C                   EXSR      XD1BLD
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandle bilde for VISE VARELINJER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   XD1BLD        BEGSR
      *
      *  Hent opplysninger fra ORDRE-LINJE-REGISTER
      *
     C                   MOVE      *BLANK        PRIENH
     C     PRIKEY        SETLL     FODTLV
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
      *
      *  Send Alt
      *
     C     D2TAGA        TAG
     C                   WRITE     D2CMD
      *
      *  Bare data
      *
     C     D2TAGB        TAG
     C                   EXSR      XDSP02                                       DSP SFL
      *
      *  F1=Vise bla-alternativer
      *
     C     *INKA         IFEQ      *ON
     C                   MOVE      '0'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    HEDFIR
     C                   PARM                    WPOTYP
     C                   ENDIF
      *
      *  F3=Avslutt
      *
     C     *INKC         CABEQ     *ON           XD1SLT
      *
      *  F5=Forny
      *
     C     *INKE         IFEQ      *ON
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
      *  Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT02                                       CRT SFL
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     C                   DO        SSRN02
     C                   READC     D1SFL
      *
     c                   if        %eof
     C                   LEAVE
     C                   ENDIF
      *
      *  Sjekk alle linjer
      *
     C****>                MOVE D1LINE    PRILIN
     C****>                EXSR XD7WIN
      *
     C                   ENDDO
      *
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGA
      *
      *  Avslutter behandlingen av sub-file
      *
     CSR   XD1SLT        ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XLSP02        BEGSR
      *
     C     SFRN02        IFNE      *ZERO
     C     SFRN02        CHAIN     D1SFL                              60
     C                   END
      *
     C*>                   MOVE D1LINE    PRILIN
      *
     C     PRIKEY        SETLL     FODTLV
      *
     C                   EXSR      XCLR02                                       TØM SFL
     C                   EXSR      XCRT02                                       CRT SFL
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP02        BEGSR
      *
     C     SRRN02        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     D2CTL
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR02        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     D2CTL
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN02
     C                   Z-ADD     0             SFRN02
     C                   Z-ADD     0             SSRN02
     C                   Z-ADD     0             SPGE02
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCRT02        BEGSR
     C                   ADD       15            SPGE02                         SFL END
     C                   Z-ADD     SSRN02        SRRN02
      *
     C     SRRN02        DOUEQ     SPGE02
      *
     C                   READ      FODTLV                                 15
      *
     C     *IN15         IFEQ      *OFF
     C     FDVARE        IFNE      PRIVAR
     C                   MOVE      *ON           *IN15
     C                   END
     C                   END
      *
     C     *IN15         CABEQ     '1'           XEND02                         EOF
      *
     C                   MOVE      FDNUMM        HEDNUM
     C                   MOVE      FDSUFF        HEDSUF
     C     HEDKEY        CHAIN     FOHEL1R                            90
     C                   MOVE      FOLKUN        D1KUND
     C                   MOVEL     FONAVN        D1NAVN
     C                   MOVE      FOOTYP        D1OTYP
      *
     C     WPOTYP        IFNE      *BLANK
     C     FOOTYP        IFNE      WPOTYP
     C                   ITER
     C                   END
     C                   END
      *
     C                   ADD       1             SRRN02
     C                   MOVE      FDNUMM        D1NUMM
     C                   MOVE      FDSUFF        D1SUFF
     C                   MOVE      FDLINE        D1LINE
     C                   MOVE      FDANTA        D1ANTA
     C                   MOVE      FDENH1        D1ENH1
     C                   MOVE      FDANTC        D1ANTC
      *
     c                   eval      d1dato = 0
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        d1dato
     c                   endif
6.30 c                   if        fdldat <> *loval
6.30 c     *dmy          move      fdldat        d1dato
6.30 c                   endif
      *
     C     D1KUND        IFEQ      *ZERO
     C                   MOVE      FOKUND        D1KUND
     C                   END
      *
     C     D1NAVN        IFEQ      *BLANK
     C                   MOVE      D1KUND        KUNNUM
     C     KUNKEY        CHAIN     rkunl1r                            66
     C  N66              MOVEL     RKNAVN        D1NAVN
     C                   END
      *
     C                   WRITE     D1SFL
     C                   ENDDO
      *
     C     XEND02        TAG
      *
     C     SRRN02        IFGT      SSRN02
     C     SSRN02        ADD       1             SFRN02
     C                   ENDIF
      *
     C                   Z-ADD     SRRN02        SSRN02
     C                   Z-ADD     SFRN02        SVRN02
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   MOVE      *BLANK        WSEQEN            1
      *
     C                   Z-ADD     0             SPGE02            3 0
     C                   Z-ADD     0             SRRN02            4 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
      *
     C     *LIKE         DEFINE    SRRN02        SFRN02
     C     *LIKE         DEFINE    SRRN02        SSRN02
      *
     C     *LIKE         DEFINE    w_alf1        WRETUR
     C     *LIKE         DEFINE    w_alf1        WPENOR
     C     *LIKE         DEFINE    w_alf1        WPIPOR
     C     *LIKE         DEFINE    FDLINE        WWLINE
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    FOOTYP        WPOTYP
     C     *LIKE         DEFINE    w_alf1        WPPLOR
     C     *LIKE         DEFINE    FDFIRM        WPFIRM
     C     *LIKE         DEFINE    FDVARE        WPVARE
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    RKFIRM        KUNFIR
     C     *LIKE         DEFINE    RKKUND        KUNNUM
      *
     C     *LIKE         DEFINE    FOFIRM        HEDFIR
     C     *LIKE         DEFINE    FONUMM        HEDNUM
     C     *LIKE         DEFINE    FOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    FDFIRM        PRIFIR
     C     *LIKE         DEFINE    FDVARE        PRIVAR
     C     *LIKE         DEFINE    FDNUMM        PRINUM
     C     *LIKE         DEFINE    FDSUFF        PRISUF
     C     *LIKE         DEFINE    FDENH1        PRIENH
      *
      *  Key for file : KUNDE-REGISTER
      *
     C     KUNKEY        KLIST
     C                   KFLD                    KUNFIR
     C                   KFLD                    KUNNUM
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : VARE-ORDRE-ENHET-REGISTER
      *
     C     PRIKEY        KLIST
     C                   KFLD                    PRIFIR
     C                   KFLD                    PRIVAR
     C                   KFLD                    PRINUM
     C                   KFLD                    PRISUF
     C                   KFLD                    PRIENH
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPLOR
     C                   PARM                    WPOTYP
     C                   PARM                    WPFIRM
     C                   PARM                    WPVARE
     C                   PARM                    WPTEK1           35
      *
      *  Legger inn firma i nøkklene
      *
     C                   MOVE      WPFIRM        KUNFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        PRIFIR
      *
      *  Legger inn kundenummer i nøkklene
      *
     C                   MOVE      WPVARE        PRIVAR
      *
      *  Nullstiller felter
      *
     C                   MOVE      *ZERO         PRINUM
     C                   MOVE      *ZERO         PRISUF
     C                   MOVE      *BLANK        PRIENH
      *
     C                   ENDSR
