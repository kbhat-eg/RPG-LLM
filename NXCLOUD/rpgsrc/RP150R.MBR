     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP150R
      * Beskrivelse . . : Lønnstaker  , vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110315 BOF  Nytt program
7.xx  * 7.xx  090516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjerpindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     filonl1    if   e           k disk    rename(ilonpfr:ilonl1r)
     filonl2    if   e           k disk    rename(ilonpfr:ilonl2r)
     filonlu    uf a e           k disk    rename(ilonpfr:ilonlur)
      *
     fRP150d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Inforpation Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ilonl1_rlnr     s                   like(ilrlnr)
     d ilonl2_rnav     s                   like(ilrnav)
     d ilonlu_rlnr     s                   like(ilrlnr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_rkli          s                   like(ilrkli)
      *
     d s_rlnr          s                   like(ilrlnr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjerpbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjerpbilde for CMD-taster
      * C1BLD  - C1 Skjerpbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjerpbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2rlnr <> 0 or
     c                             b2rnav <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ilonl2_rnav = b1rnav
     c     ilonl2_key    setll     ilonl2
     c                   other
     c                   eval      ilonl1_rlnr = b1rlnr
     c     ilonl1_key    setll     ilonl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Lønnstaker
      *
     c                   if        b2rlnr <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ilonl1_rlnr = b2rlnr
     c     ilonl1_key    setll     ilonl1
     c                   goto      end_pos
     c                   endif
      *
      * rnavrivelse
      *
     c                   if        b2rnav <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ilonl2_rnav = b2rnav
     c     ilonl2_key    setll     ilonl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2rlnr = 0
     c                   eval      b2rnav = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1rlnr = b1rlnr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1rlnr = b1rlnr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1rlnr = b1rlnr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1rlnr = b1rlnr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjerpbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1rlnr = b2rlnr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1rlnr = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1rlnr = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp inforpasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ilonl1_rlnr = c1rlnr
     c     ilonl1_key    chain     ilonl1
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1rlnr = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2rlnr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ilonl1_rlnr = c1rlnr
     c     ilonl1_key    chain     ilonl1
      *
     c                   if        %found
     c                   eval      c2rnav = ilrnav
     c                   eval      c2radr = ilradr
     c                   eval      c2rad2 = ilrad2
     c                   eval      c2rpnr = ilrpnr
     c                   eval      c2rste = ilrste
     c                   eval      c2ravd = ilravd
     c                   eval      c2rlty = ilrlty
     c                   eval      c2rmob = ilrmob
     c                   eval      c2repo = ilrepo
     c                   eval      c2eusr = ilreav
     c     *dmy          move      ilroda        c2odat
     c     *dmy          move      ilreda        c2edat
     c                   else
     c                   eval      c2rnav = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2radr = *blank
     c                   eval      c2rad2 = *blank
     c                   eval      c2rpnr = 0
     c                   eval      c2rste = *blank
     c                   eval      c2ravd = 0
     c                   eval      c2rlty = *blank
     c                   eval      c2rmob = *blank
     c                   eval      c2repo = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1rlnr = k1rlnr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2rnav = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ilonlur
      *
     c                   eval      ilonlu_rlnr = c1rlnr
     c     ilonlu_key    chain     ilonlur
      *
     c                   eval      ilrnav = c2rnav
     c                   eval      ilradr = c2radr
     c                   eval      ilrad2 = c2rad2
     c                   eval      ilrpnr = c2rpnr
     c                   eval      ilrste = c2rste
     c                   eval      ilravd = c2ravd
     c                   eval      ilrlty = c2rlty
     c                   eval      ilrmob = c2rmob
     c                   eval      ilrepo = c2repo
     c                   eval      ilreav = c2eusr
      *
     c                   time                    ilreda
     c                   time                    ilreti
     c                   eval      ilreav = l_user
      *
     c                   if        %found
     c                   update    ilonlur
     c                   else
     c                   eval      ilrkli = w_rkli
     c                   eval      ilrlnr = c1rlnr
     c                   time                    ilroda
     c                   write     ilonlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1rnav = ilrnav
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ilonl1_rlnr = d1rlnr
     c     ilonl1_key    chain     ilonl1
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ilonlu_rlnr = d1rlnr
     c     ilonlu_key    chain     ilonlur
      *
     c                   delete    ilonlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_rlnr = k1rlnr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ilonl1_rlnr = k1rlnr
     c     ilonl1_key    chain     ilonl1
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1rlnr = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ilonl1_rlnr = k1rlnr
     c     ilonl1_key    chain     ilonl1
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1rlnr = s_rlnr
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ilonl2
     c                   other
     c                   read      ilonl1
     c                   endsl
      *
     c                   if        %eof or
     c                             ilrkli <> w_rkli
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ilonl2_rnav = ilrnav
     c                   other
     c                   eval      ilonl1_rlnr = ilrlnr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1rlnr = ilrlnr
     c                   eval      b1rnav = ilrnav
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ilonl2_key    setgt     ilonl2
     c                   readp     ilonl2
     c                   other
     c     ilonl1_key    setgt     ilonl1
     c                   readp     ilonl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ilonl2
     c                   other
     c                   readp     ilonl1
     c                   endsl
      *
     c                   if        %eof or
     c                             ilrkli <> w_rkli
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ilonl2_rnav = ilrnav
     c                   other
     c                   eval      ilonl1_rlnr = ilrlnr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ilonl2_key    setll     ilonl2
     c                   other
     c     ilonl1_key    setll     ilonl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på Lønnstaker
      *
     c     ilonl1_key    klist
     c                   kfld                    w_rkli
     c                   kfld                    ilonl1_rlnr
      *
      * Key for posisjonering på rnavrivelse
      *
     c     ilonl2_key    klist
     c                   kfld                    w_rkli
     c                   kfld                    ilonl2_rnav
      *
      * Key for oppdatering av Lønnstaker
      *
     c     ilonlu_key    klist
     c                   kfld                    w_rkli
     c                   kfld                    ilonlu_rlnr
      *
      *
      * Henter verdier fra LDA, og initierer skjerpbildet
      *
     c                   eval      w_rkli = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ilonl1_rlnr = 0
     c     ilonl1_key    setll     ilonl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
