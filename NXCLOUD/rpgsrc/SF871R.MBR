     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF871R
      * Beskrivelse . . : Salgsstatistikk, samler grunnlag per-trans
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040721 hko  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstalh    uf   e           k disk    rename(sstapfr:sstalhr)
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
      *
     fsos1pf    o  a e           k disk
      *
      * Definering av parametre
      *
     d p_fgrp          s              3
     d p_firm          s              3
      *
      * Definering av variable i nøkler
      *
     d sstalh_erap     s                   like(sferap)
     d ra07l1_gkod     s                   like(ragkod)
     d rkunl1_kund     s                   like(rkkund)
     d ra11l1_kkod     s                   like(rakkod)
     d ra06l1_fkod     s                   like(rafkod)
     d ra09l1_ikod     s                   like(raikod)
     d rlevl1_levr     s                   like(rllevr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_modn     s                   like(jumodn)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(sffirm)
      *
     d w_time          s               t   timfmt(*iso)
     d w_ftid          s               t   timfmt(*iso)
     d w_ttid          s               t   timfmt(*iso)
     d w_bupr          s              9  2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   clear                   sos1pfr
      *
      * Henter firma-informasjon
      *
     c                   eval      aafgrp = p_fgrp
      *
     c     afirl1_key    chain     afirl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Leser salgsstatistikk, og markerer behandlende transer som lest
      *
     c                   eval      sstalh_erap = 0
     c     sstalh_key    setll     sstalh
     c     sstalh_key    reade     sstalhr
     c                   dow       not %eof
      *
     c                   exsr      oppdater
      *
     c     sstalh_key    reade     sstalhr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av eksportfil/aggregert statistikkfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   eval      ragtxt = *blank
      *
     c                   eval      ra07l1_gkod = sfsavd
     c     ra07l1_key    chain     ra07l1
      *
     c                   eval      rknavn = *blank
     c                   eval      rkftnr = 0
      *
     c                   eval      rkunl1_kund = sfvkun
     c     rkunl1_key    chain     rkunl1
      *
     c                   eval      raktxt = *blank
      *
     c                   eval      ra11l1_kkod = sfkkat
     c     ra11l1_key    chain     ra11l1
      *
     c                   eval      raftxt = *blank
      *
     c                   eval      ra06l1_fkod = sfrkat
     c     ra06l1_key    chain     ra06l1
      *
     c                   eval      raitxt = *blank
      *
     c                   eval      ra09l1_ikod = sfselg
     c     ra09l1_key    chain     ra09l1
      *
     c                   eval      rlnavn = *blank
      *
     c                   eval      rlevl1_levr = sfldor
     c     rlevl1_key    chain     rlevl1
      *
     c                   eval      vgutxt = *blank
      *
     c                   eval      vugrl1_uogr = sfogrp
     c                   eval      vugrl1_uhgr = sfhgrp
     c                   eval      vugrl1_uugr = sfugrp
     c     vugrl1_key    chain     vugrl1
      *
     c                   eval      jumte1 = *blank
      *
     c                   eval      jmodl1_modn = sfmodn
     c     jmodl1_key    chain     jmodl1
      *
     c                   eval      s1paar = sfpaar
     c                   eval      s1pmnd = sfpmnd
     c                   eval      s1savd = sfsavd
     c                   eval      s1vkun = sfvkun
     c                   eval      s1kkat = sfkkat
     c                   eval      s1rkat = sfrkat
     c                   eval      s1selg = sfselg
      *
     c                   eval      s1binr = sfbinr
     c                   eval      s1line = sfline
     c                   eval      s1bida = sfbida
     c                   eval      s1otyp = sfotyp
     c                   eval      s1ornr = sfornr
     c                   eval      s1orsu = sforsu
     c                   eval      s1orli = sforli
     c                   eval      s1orda = sforda
     c                   eval      s1bong = sfbong
     c                   eval      s1benr = sfbenr
     c                   eval      s1bsuf = sfbsuf
     c                   eval      s1blin = sfblin
      *
     c                   eval      s1vare = sfvare
     c                   eval      s1nobb = sfnobb
     c                   eval      s1tek1 = sftek1
     c                   eval      s1tek2 = sftek2
     c                   eval      s1ogrp = sfogrp
     c                   eval      s1hgrp = sfhgrp
     c                   eval      s1ugrp = sfugrp
     c                   eval      s1vgrp = (sfogrp*100000)+(sfhgrp*1000)+sfugrp
     c                   eval      s1modn = sfmodn
     c                   eval      s1vtyp = sfvtyp
     c                   eval      s1kamp = sfkamp
     c                   eval      s1kpri = sfkpri
     c                   eval      s1lvre = sflvre
     c                   eval      s1ldor = sfldor
     c                   eval      s1lvar = sflvar
      *
     c                   eval      s1enhe = sfenhe
     c                   eval      s1anta = sfanta
     c                   eval      s1prgr = sfprgr
     c                   eval      s1lety = sflety
     c                   eval      s1sumb = sfsumb
     c                   eval      s1rab1 = sfrab1
     c                   eval      s1rab2 = sfrab2
     c                   eval      s1rabo = sfrabo
     c                   eval      s1sumn = sfsumb - sfrab1 - sfrab2 - sfrabo
     c                   eval      s1sumk = sfsumk
     c                   eval      s1dekb = s1sumn - sfsumk
      *
     c                   eval      s1dekg = 0
      *
     c                   if        s1sumn <> 0
     c                   select
     c                   when      (100 - (s1sumk * 100 / s1sumn)) < -999,99
     c                   eval      s1dekg = -999,99
     c                   when      (100 - (s1sumk * 100 / s1sumn)) > 999,99
     c                   eval      s1dekg = 999,99
     c                   other
     c                   eval(h)   s1dekg = 100 - (s1sumk * 100 / s1sumn)
     c                   endsl
     c                   endif
      *
      * Henter prisinformasjon i salgsøyeblikket
      *
     c                   eval      b_inlr = *on
     c                   time                    w_time
      *
     c                   call      'VP704R'
     c                   parm                    w_firm
     c                   parm                    s1ldor
     c                   parm                    s1vare
     c                   parm                    s1enhe
     c                   parm                    s1prgr
     c                   parm                    s1lety
     c                   parm                    s1orda
     c                   parm                    w_time
     c                   parm                    s1sapr
     c                   parm                    w_bupr
     c                   parm                    s1kopr
     c                   parm                    s1inpr
     c                   parm                    s1pdat
     c                   parm                    s1kfda
     c                   parm                    w_ftid
     c                   parm                    s1ktda
     c                   parm                    w_ttid
     c                   parm                    s1gkam
     c                   parm                    s1ksap
     c                   parm                    s1kkop
     c                   parm                    s1kinp
     c                   parm                    b_inlr
      *
     c                   if        s1kopr = 0 and
     c                             s1inpr <> 0
     c                   eval      s1kopr = s1inpr
     c                   endif
      *
     c                   if        s1kkop = 0 and
     c                             s1kinp <> 0
     c                   eval      s1kkop = s1kinp
     c                   endif
      *
     c                   write     sos1pfr
      *
     c                   eval      sferap = 1
     c                   update    sstalhr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_fgrp
     c                   parm                    p_firm
      *
      * Key for les på salgsstatistikk
      *
     c     sstalh_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstalh_erap
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_gkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key oppslag på varegruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = %dec(p_firm:3:0)
      *
     c                   endsr
