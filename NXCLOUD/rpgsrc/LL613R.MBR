     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LL613R                                        *
      * Beskrivelse . . : Summering av enkeltposter til ABC-analyse     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     flwa1l1    if   e           k disk    rename(lwa1pfr:lwa1l1r)
     flwa2l2    o    e             disk    rename(lwa2pfr:lwa2l2r)
      *
      /eject
      ********************  I N P U T  S E C T I O N  ********************
      *
      *
     d                 ds
     d n_brd                         15
     d  n_ogrp                        2  0 overlay(n_brd)
     d  n_hgrp                        2  0 overlay(n_brd:3)
     d  n_ugrp                        3  0 overlay(n_brd:5)
     d  n_vare                        8  0 overlay(n_brd:8)
      *
     d                 ds
     d g_brd                         15
     d  g_ogrp                        2  0 overlay(g_brd)
     d  g_hgrp                        2  0 overlay(g_brd:3)
     d  g_ugrp                        3  0 overlay(g_brd:5)
     d  g_vare                        8  0 overlay(g_brd:8)
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_faar                        4  0 overlay(p_prec)
     d  p_fper                        2  0 overlay(p_prec:5)
     d  p_taar                        4  0 overlay(p_prec:7)
     d  p_tper                        2  0 overlay(p_prec:11)
     d  p_fvar                        8  0 overlay(p_prec:13)
     d  p_tvar                        8  0 overlay(p_prec:21)
     d  p_flag                        2  0 overlay(p_prec:29)
     d  p_tlag                        2  0 overlay(p_prec:31)
     d  p_fogr                        2  0 overlay(p_prec:33)
     d  p_togr                        2  0 overlay(p_prec:35)
     d  p_fhgr                        2  0 overlay(p_prec:37)
     d  p_thgr                        2  0 overlay(p_prec:39)
     d  p_fugr                        3  0 overlay(p_prec:41)
     d  p_tugr                        3  0 overlay(p_prec:44)
     d  p_levn                        6  0 overlay(p_prec:47)
     d  p_sakb                        4    overlay(p_prec:53)
     d  p_sort                        1  0 overlay(p_prec:57)
     d  p_glag                        1  0 overlay(p_prec:58)
      *
      * Local Data Area
      *
     d                uds
     d l_prog                800    805
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d w_ogrp          s                   like(mlogrp)
     d w_hgrp          s                   like(mlhgrp)
     d w_ugrp          s                   like(mlugrp)
     d w_vare          s              8  0
     d w_dekt          s                   like(mldekt)
     d w_kost          s                   like(mlkost)
     d w_lagv          s                   like(mllagv)
     d w_omst          s                   like(mlomst)
     d w_enhl          s                   like(mlenhl)
     d g_enhl          s                   like(mlenhl)
      *
     d wtodek          s                   like(mldekt)
     d wtokos          s                   like(mlkost)
     d wtolve          s                   like(mllagv)
     d wtooms          s                   like(mlomst)
      *
     d w_firm          s                   like(mlfirm)
     d w_parm          s            128
      *
      /eject
      *
      *---------------------------------------------------------------
      *  Behandle enkeltposter
      *---------------------------------------------------------------
      *
     c     les           tag
      *
     c                   read      lwa1l1r                                60
      *
     c                   if        *in60 = *on
     c                   eval      *inlr = *on
     c                   exsr      brkto
     c                   goto      slutt
     c                   endif
      *
      * Bygg opp bruddbegrep
      *
     c                   select
     c                   when      p_sort = 1
     c                   eval      n_vare = mlvare
     c                   when      p_sort = 2
     c                   eval      n_ogrp = mlogrp
     c                   when      p_sort = 3
     c                   eval      n_ogrp = mlogrp
     c                   eval      n_hgrp = mlhgrp
     c                   when      p_sort = 4
     c                   eval      n_ogrp = mlogrp
     c                   eval      n_hgrp = mlhgrp
     c                   eval      n_ugrp = mlugrp
     c                   endsl
      *
      * Ta vare på innleste felt
      *
     c                   eval      w_ogrp = mlogrp
     c                   eval      w_hgrp = mlhgrp
     c                   eval      w_ugrp = mlugrp
     c                   eval      w_vare = mlvare
     c                   eval      w_enhl = mlenhl
      *
      * Første gang -
      * ikke test på brudd
      *
     c                   if        g_brd = *zeros
     c                   goto      neste
     c                   endif
      *
     c                   if        n_brd <> g_brd
     c                   exsr      brkto
     c                   endif
      *
      * Summer til bruddsummer
      *
     c     neste         tag
      *
     c                   eval      w_omst = w_omst + mlomst
     c                   eval      w_kost = w_kost + mlkost
     c                   eval      w_dekt = w_dekt + mldekt
     c                   eval      w_lagv = w_lagv + mllagv
      *
     c                   eval      g_brd = n_brd
     c                   eval      g_enhl = w_enhl
      *
     c                   goto      les
      *
      /eject
      *
      *  END OF JOB
      *
     c     slutt         tag
      *
      * Legg ut totaler
      *
     c                   eval      mwvare = *zeros
     c                   eval      mwogrp = *zeros
     c                   eval      mwhgrp = *zeros
     c                   eval      mwugrp = *zeros
     c                   eval      mwenhl = *blank
      *
     c                   if        p_glag = 1
     c                   eval      mwomst = wtooms
     c                   endif
      *                                                                 .
     c                   if        p_glag = 2
     c                   eval      mwomst = wtodek
     c                   endif
      *                                                                 .
     c                   if        p_glag = 3
     c                   eval      mwomst = wtokos
     c                   endif
      *                                                                 .
     c                   eval      mwlagv = wtolve
      *
     c                   write     lwa2l2r
      *
      /eject
      *
      *   Subrutine for behandling ved brudd
      *
     c     brkto         begsr
      *
     c                   eval      mwfirm = l_firm
      *
     c                   eval      mwenhl = g_enhl
      *
     c                   select
     c                   when      p_sort = 1
     c                   eval      mwvare = g_vare
     c                   eval      mwogrp = g_ogrp
     c                   eval      mwhgrp = g_hgrp
     c                   eval      mwugrp = g_ugrp
     c                   when      p_sort = 2
     c                   eval      mwvare = *zeros
     c                   eval      mwogrp = g_ogrp
     c                   eval      mwhgrp = *zeros
     c                   when      p_sort = 3
     c                   eval      mwvare = *zeros
     c                   eval      mwogrp = g_ogrp
     c                   eval      mwhgrp = g_hgrp
     c                   when      p_sort = 4
     c                   eval      mwvare = *zeros
     c                   eval      mwogrp = g_ogrp
     c                   eval      mwhgrp = g_hgrp
     c                   eval      mwugrp = g_ugrp
     c                   endsl
      *                                                                 .
     c                   select
     c                   when      p_glag = 1
     c                   eval      mwomst = w_omst
     c                   when      p_glag = 2
     c                   eval      mwomst = w_dekt
     c                   when      p_glag = 3
     c                   eval      mwomst = w_kost
     c                   endsl
      *                                                                 .
     c                   eval      mwlagv = w_lagv
      *
      * Skriv record
      *
     c                   write     lwa2l2r
      *
      * Summer til totaler
      *
     c                   eval      wtooms = wtooms + w_omst
     c                   eval      wtokos = wtokos + w_kost
     c                   eval      wtodek = wtodek + w_dekt
     c                   eval      wtolve = wtolve + w_lagv
      *
      * Nullstill arbeidsfelt
      *
     c                   eval      w_omst = *zeros
     c                   eval      w_kost = *zeros
     c                   eval      w_dekt = *zeros
     c                   eval      w_lagv = *zeros
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Sett startverdier
      *
     c                   eval      n_brd = *zeros
     c                   eval      g_brd = *zeros
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
