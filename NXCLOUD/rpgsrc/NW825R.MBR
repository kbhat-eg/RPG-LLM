     h option(*nodebugio) datedit(*dmy) decedit('.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NW825R
      * Beskrivelse . . : WAP, API - hente vareinformasjon -liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       301003 HKO  Nytt program
      *       251103 HKO  Returnerer ikke utgåtte varer
      *       180304 HKO  Tilpasset nye feltlenger
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.3q  *       260515 bof  Datoformat sql
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_avde          s              4  0
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                        92
     d  d_kund                        9  0 overlay(d_inpu: 1)
     d  d_kpro                       12    overlay(d_inpu:10)
     d  d_stek                       50    overlay(d_inpu:22)
     d  d_vari                       16    overlay(d_inpu:72)
     d  d_antv                        5  0 overlay(d_inpu:88)
      *
      * Datakø - ut
      *
     d                 ds
     d d_outp                       281
     d  d_vare                       16    overlay(d_outp:  1)
     d  d_nobb                        8  0 overlay(d_outp: 17)
     d  d_tek1                       35    overlay(d_outp: 25)
     d  d_tek2                       35    overlay(d_outp: 60)
     d  d_kpri                       10    overlay(d_outp: 95)
     d  d_enh1                        3    overlay(d_outp:105)
     d  d_enh2                        3    overlay(d_outp:108)
     d  d_enh3                        3    overlay(d_outp:111)
     d  d_sap1                       10    overlay(d_outp:114)
     d  d_sap2                       10    overlay(d_outp:124)
     d  d_sap3                       10    overlay(d_outp:134)
     d  d_rab1                        6    overlay(d_outp:144)
     d  d_rab2                        6    overlay(d_outp:150)
     d  d_rab3                        6    overlay(d_outp:156)
     d  d_ntp1                       10    overlay(d_outp:162)
     d  d_ntp2                       10    overlay(d_outp:172)
     d  d_ntp3                       10    overlay(d_outp:182)
     d  d_lag1                       12    overlay(d_outp:192)
     d  d_lag2                       12    overlay(d_outp:204)
     d  d_lag3                       12    overlay(d_outp:216)
     d  d_omr1                       13    overlay(d_outp:228)
     d  d_omr2                       13    overlay(d_outp:241)
     d  d_omr3                       13    overlay(d_outp:254)
     d  d_enhe                        3    overlay(d_outp:267)
     d  d_anta                       12    overlay(d_outp:270)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_vare          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
6.20 d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
     d w_kund          s              6  0
     d w_kpro          s              6  0
     d w_stek          s             35
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_tell          s              5  0
      *
     d w_firm          s                   like(vvfirm)
     d w_vari          s                   like(vvvare)
6.20 d w_lage          s              2  0
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av forespørsel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        d_antv = 0 or
     c                             d_stek = *blank
     c                   eval      p_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_vari = d_vari
      *
      * Utleder søketekster ut av søketekst
      *
     c                   eval      w_stek = %trim(d_stek)
      *
     c                   call      'AS700R'
     c                   parm                    w_stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Initierer SQL for søk
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvsfir,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
      * Søker
      *
     c                   eval      w_tell = 0
     c                   eval      b_vare = *off
      *
     c                   dou       w_tell = d_antv
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvsfir,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   leave
     c                   endif
      *
     c                   if        w_ste2 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   eval      vvarl1_vare = vvsvar
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   iter
     c                   endif
      *
      * Tester på om varen er utgått
      *
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat < w_ddat
     c                   iter
     c                   endif
      *
      * Tester på om input-varenummer er nådd
      *
     c                   if        w_vari <> *blank and
     c                             b_vare = *off
     c                   if        w_vari = vvvare
     c                   eval      b_vare = *on
     c                   endif
     c                   iter
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
      * Henter informasjon
      *
     c                   eval      w_kund = d_kund
     c**                 eval      w_kpro = d_kpro
     c                   movel     d_kpro        w_kpro
      *
     c                   eval      d_outp = *blank
      *
     c                   call      'VV990R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    d_outp
      *
      * Skriver til output-datakø
      *
     c                   call      'QSNDDTAQ'
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_outl
     c                   parm                    d_outp
     c                   parm                    p_keyl
     c                   parm                    p_uniq
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = *on
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_avde
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      w_lage = l_lage
      *
      * Henter dagens dato
      *
6.20 c                   time                    w_ddat
      *
     c                   endsr
