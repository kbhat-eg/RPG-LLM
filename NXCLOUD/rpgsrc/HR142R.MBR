     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR142R
      * Beskrivelse . . : Lagerlokasjonsspørring, radioterminal
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141005 ASK  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmdlol1    if   e           k disk    rename(mdlopfr:mdlol1r)
     fmdlol2    if   e           k disk    rename(mdlopfr:mdlol2r)
      *
     fhr142d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(mdfirm)
     d p_lage          s                   like(mdlage)
     d p_loka          s                   like(mdloka)
      *
      * Definering av variabler i nøkler
      *
     d mdlol1_lage     s                   like(mdlage)
     d mdlol1_loka     s                   like(mdloka)
     d mdlol2_loka     s                   like(mdloka)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(mdfirm)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(7)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lage <> 0 or
     c                             b2loka <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      *in22 = *on
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_seqe = *blank
     c     mdlol1_key    setll     mdlol1
     c                   else
     c     mdlol2_key    setll     mdlol2
     c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   eval      *in22 = *on
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på lager
      *
     c                   if        b2lage <> 0
     c                   eval      w_seqe = ' '
     c                   eval      mdlol1_lage = b2lage
     c                   eval      mdlol1_loka = *blank
     c     mdlol1_key    setll     mdlol1
     c                   endif
      *
     c                   if        b2loka <> *blank
     c                   eval      w_seqe = 'L'
     c                   eval      mdlol2_loka = b2loka
     c     mdlol2_key    setll     mdlol2
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lage = 0
     c                   eval      b2loka = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Velge post
      *
     c                   if        b1valg = 1
     c                   eval      p_lage = b1lage
     c                   eval      p_loka = b1loka
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = *blank
     c                   read      mdlol1                                 15
     c                   else
     c                   read      mdlol2                                 15
     c                   endif
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        mdfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   if        p_lage <> 0 and
     c                             mdlage <> p_lage
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1lage = mdlage
     c                   eval      b1loka = mdloka
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_loka
      *
      * Key for posisjonering på lager/lokasjon
      *
     c     mdlol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlol1_lage
     c                   kfld                    mdlol1_loka
      *
      * Key for posisjonering på lokasjon
      *
     c     mdlol2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mdlol2_loka
      *
      *
      * Henter parametre, og initierer skjermbilde
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      mdlol1_lage = p_lage
     c                   eval      mdlol1_loka = *blank
     c     mdlol1_key    setll     mdlol1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
