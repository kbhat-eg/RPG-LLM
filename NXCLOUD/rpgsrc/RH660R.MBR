     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH660R                                        *
      * Beskrivelse . . : Hovedbok - mva-oppgjør - parameter            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * V5.00     150103  Satt testkjøring som default.            HFL
T5.30 * V5.30     080904  Lagt inn mulighet for regnskapsår > *yearHFL
T5.30 * V5.30     090805  Måned 8 foreslo feil mva-periode         HFL
8.00  *       sst 020322  Lagt inn felt for beskrivelse ut på Saft-rapp.*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     frh660d    cf   e             workstn
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhpmva                         1  0
     d rhpmv0                         1
     d rhptst                         1
     d rhpost                         1
     d rhpsps                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d wpakod          s              2  0
     d wpdat1          s              6  0
     d p_in03          s              1
     d wpokok          s              1
     d wwaar           s              2  0
     d w_firm          s              3  0
     d w_user          s                   like(l_user)
     d w_navn          s                   like(l_fnav)
T5.30d var             s             40    dim(2) ctdata perrcd(1)               Varsel m/svarkrav
T5.30d wsavli          s              2  0
T5.30d wsavpo          s              3  0
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
8.00 c                   eval      *in46 = *off
      *
      * Bare data
      *
     c     a2tagb        tag
     c                   exfmt     a2bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           p_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      * Sjekke om kjøredato er gyldig
      *
     c                   if        a2pkda = *zero                                        .....
     c                   move      udate         a2pkda                                      .
     c                   endif                                                           .....
      *
     c                   move      a2pkda        wwaar
      *
     c     *dmy          test(d)                 a2pkda                 33
     c                   if        *in33 = *on                                           .....
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
8.00  *
8.00 c                   if        a2ptst <> 'J'
8.00 c                             and *in46 = *off
8.00 c                   eval      *in46 = *on
8.00 c     *dtaara       define                  saft_mva        100
8.00 c                   in        *dtaara
8.00 c                   eval      a2besk = %subst(saft_mva:1:30)
8.00 c                   goto      a2tagb
8.00 c                   endif
8.00 c                   if        *in46 = *on
8.00 c                             and a2besk = *blank
8.00 c                   eval      *in36 = *on
8.00 c                   goto      a2tagb
8.00 c                   else
8.00 c     *lock         in        *dtaara
8.00 c                   eval      %subst(saft_mva:1:30) = a2besk
8.00 c                   out       *dtaara
8.00 c                   endif
      *
      *  Kontroll av regnskapsår
T5.30 *  Varsel ved regnskapsår større enn *year
      *
     c                   if        a2prÅr > *year                                        .....
T5.30c                   movel     var(1)        m1mld1
T5.30c                   movel     var(2)        m1mld2
T5.30c                   move      'N'           m1svar
T5.30c                   exsr      xm1win
T5.30c                   if        m1svar = 'N'
     c                   eval      *in34 = *on                                               .
     c                   goto      a2taga                                                    .
T5.30c                   endif
     c                   endif                                                           .....
      *
      * Skriv parameter til LDA
      *
     c                   move      a2pkda        rhpkda
     c                   move      a2prÅr        rhprÅr
     c                   move      a2pper        rhpper
     c                   move      a2pmva        rhpmva
     c                   move      a2pmv0        rhpmv0
     c                   move      a2ptst        rhptst
     c                   move      a2psps        rhpsps
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * M1WIN Behandle bilde for melding med svar                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     xm1win        begsr
      *
T5.30c                   eval      wsavli = 6
T5.30c                   eval      wsavpo = 22
T5.30c                   eval      w_arow = wsavli
T5.30c                   eval      w_acol = wsavpo
      *
T5.30c                   exfmt     m1win
      *
T5.30c                   if        *inkc = *on
T5.30c                   move      'N'           m1svar
T5.30c                   endif
      *
T5.30c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
      *
     c                   eval      wpdat1 = 0
     c                   eval      wpokok = *blank
      *
      *
      * Legge inn firmanummer og navn fra parameter
      *
     c                   move      l_user        w_user
     c                   move      l_firm        w_firm
     c                   movel     l_fnav        w_navn
      *
     c                   move      udate         a2pkda
      *
      * Sett inn std.verdier i parameterbilde
      *
     c                   if        *month = 01                                           .....
     c                             or *month = 02                                            .
     c                   eval      a2prÅr = *year - 1                                        .
     c                   move      12            a2pper                                      .
     c                   move      6             a2pmva                                      .
     c                   endif                                                           .....
      *
     c                   if        *month = 03                                           .....
     c                             or *month = 04                                            .
     c                   move      *year         a2prÅr                                      .
     c                   move      02            a2pper                                      .
     c                   move      1             a2pmva                                      .
     c                   endif                                                           .....
      *
     c                   if        *month = 05                                           .....
     c                             or *month = 06                                            .
     c                   move      *year         a2prÅr                                      .
     c                   move      04            a2pper                                      .
     c                   move      2             a2pmva                                      .
     c                   endif                                                           .....
      *
     c                   if        *month = 07                                           .....
     c                             or *month = 08                                            .
     c                   move      *year         a2prÅr                                      .
     c                   move      06            a2pper                                      .
     c                   move      3             a2pmva                                      .
     c                   endif                                                           .....
      *
E5.30c                   if        *month = 09                                           .....
     c                             or *month = 10                                            .
     c                   move      *year         a2prÅr                                      .
     c                   move      08            a2pper                                      .
     c                   move      4             a2pmva                                      .
     c                   endif                                                           .....
      *
     c                   if        *month = 11                                           .....
     c                             or *month = 12                                            .
     c                   move      *year         a2prÅr                                      .
     c                   move      10            a2pper                                      .
     c                   move      5             a2pmva                                      .
     c                   endif                                                           .....
      *
     c                   move      'N'           a2pmv0
E5.00c                   move      'J'           a2ptst
     c                   move      'N'           a2psps
     c                   eval      p_in03 = ' '
      *
      * Hent bruker-record
      *
     c     ausrl1_key    chain     ausrl1                             63
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for bruker-file
      *
     c     ausrl1_key    klist
     c                   kfld                    w_user
      *
     c                   endsr
**
Valgt periode er neste år.
Bekreft dersom dette er riktig.
