     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : VG802R
      * Beskrivelse . . : Oppdatering kosttransaksjoner / FOVFPF
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230119 ask  Nytt program
8.01  * 800   260521 ask  Lagt inn sjekk på varetype- oppdaterer bare 'L'
8.02  * 800   101123 ask  Lagt inn omregning ved salg på enhet som avviker lager enhet
8.03  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
8.02 fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     ffovfpf    o  a e             disk
      *
      * Definering av lda
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
     d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
     d  d_ksp1                        4  0 overlay(d_hele:11)
     d  d_kav2                        4  0 overlay(d_hele:15)
     d  d_kko2                        6  0 overlay(d_hele:19)
     d  d_ksp2                        4  0 overlay(d_hele:25)
     d  d_kav3                        4  0 overlay(d_hele:29)
     d  d_kko3                        6  0 overlay(d_hele:33)
     d  d_ksp3                        4  0 overlay(d_hele:39)
     d  d_kav4                        4  0 overlay(d_hele:43)
     d  d_kko4                        6  0 overlay(d_hele:47)
     d  d_ksp4                        4  0 overlay(d_hele:53)
     d  d_kav5                        4  0 overlay(d_hele:57)
     d  d_kko5                        6  0 overlay(d_hele:61)
     d  d_ksp5                        4  0 overlay(d_hele:67)
     d  d_kav6                        4  0 overlay(d_hele:71)
     d  d_kko6                        6  0 overlay(d_hele:75)
     d  d_ksp6                        4  0 overlay(d_hele:81)
     d  d_kav7                        4  0 overlay(d_hele:85)
     d  d_kko7                        6  0 overlay(d_hele:89)
     d  d_ksp7                        4  0 overlay(d_hele:95)
     d  d_khnv                        4    overlay(d_hele:99)
      *
      * Definering av parametre
      *
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_vare          s                   like(fdvare)
     d p_lage          s                   like(fdlage)
     d p_dato          s              8  0
     d p_cost          s              9  2
      *
      * Definering av variable i nøkler
      *
     d vkhvl1_kkod     s                   like(vakkod)
     d vkhvl1_klty     s                   like(vaklty)
     d vkhvl1_koty     s                   like(vakoty)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
8.02 d vvenlr_vare     s                   like(vevare)
8.02 d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_peri          s                   like(ffperi)
     d w_dato          s               d   datfmt(*iso)
     d w_hele          s            128
     d w_stat          s              1
     d w_belp          s             11  2
     d w_belh          s             11  2
     d w_avde          s              4  0
     d w_spes          s              4  0
     d w_kont          s              6  0
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_type          s              2
8.01 d w_vtyp          s              1
8.02 d w_cost          s              9  2
8.02 d w_enhe          s              3
8.02 d w_enhl          s              3
     d w_biln          s                   like(ffbiln)

8.01 c/Exec SQL
8.01 c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
8.01 c+             commit=*none
8.01 c/End-Exec

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - behandling av bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent kode opplysninger
      *
     c     fstsl1_key    chain     fstsl1r
      *
     c     vgkkir_key    chain     vgkkir
      *
      * Henter bilagsnummer for hele ordren dersom postering skal gjøres, hvis ikke - avslutt
      *
     c                   if        vgkrgn = 'J'
     c                   exsr      hntnum
     c                   else
     c                   goto      avslutt
     c                   endif
      *
      *  Henter inn standard kontohenvisning
      *
     c                   eval      vkhvl1_kkod = faakhv
     c     vkhvl1_key    chain     vkhvl1
      *
      *  Finner dagens dato og periode
      *
     c                   time                    w_dato
     c                   eval      w_peri = (%subdt(w_dato:*months) * 100)
     c                                     + (%subdt(w_dato:*years) - 2000)
      *
      *  Finner ordreinformasjon
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1r
     c                   if        not %found(fohel1)
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
      *
      *
      * Sjekker om oppdatering skal gjøres
      *
     c                   if        not %found(votyl1)
     c                   goto      avslutt
     c                   endif
      *
     c                   if        vgoppd <> 'M'
     c                             and vgoppd <> 'F'
     c                   goto      avslutt
     c                   endif
      *
     c                   if        vgoppd = 'M'
     c                             and vaoakk <> 2
     c                   goto      avslutt
     c                   endif
      *
     c                   if        vgoppd = 'F'
     c                             and vaoakk <> 3
     c                   goto      avslutt
     c                   endif
      *
      * Leser i gjennom ordren og gjør oppdateringsjobben
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
8.01  *
8.01  * Bare varetype "L" skal oppdatere
8.01  *
8.01   exec SQL
8.02     select vvvtyp, vvenhe, vvenhl
8.02       into :w_vtyp, :w_enhe, :w_enhl
8.01        from vvarpf
8.01        where vvfirm = :w_firm and vvvare = :fdvare;
8.01
8.01 c                   if        w_vtyp <> 'L'
8.01 c                   goto      les_neste
8.01 c                   endif
      *
      * Direktelevert skal ikke oppdatere
      *
     c                   if        fdlety = 1
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1r
      *
      * Hent kontohenvisning basert på varelinje - legger inn std hvis ikke funnet
      *
     c                   exsr      hntkon
     c                   if        w_stat = '9'
     c                   eval      d_kav1 = vakav1
     c                   eval      d_kko1 = vakko1
     c                   eval      d_ksp1 = vaksp1
     c                   eval      d_kav2 = vakav2
     c                   eval      d_kko2 = vakko2
     c                   eval      d_ksp2 = vaksp2
     c                   eval      d_kav3 = vakav3
     c                   eval      d_kko3 = vakko3
     c                   eval      d_ksp3 = vaksp3
     c                   eval      d_kav4 = vakav4
     c                   eval      d_kko4 = vakko4
     c                   eval      d_ksp4 = vaksp4
     c                   eval      d_kav5 = vakav5
     c                   eval      d_kko5 = vakko5
     c                   eval      d_ksp5 = vaksp5
     c                   eval      d_kav6 = vakav6
     c                   eval      d_kko6 = vakko6
     c                   eval      d_ksp6 = vaksp6
     c                   eval      d_kav7 = vakav7
     c                   eval      d_kko7 = vakko7
     c                   eval      d_ksp7 = vaksp7
     c                   endif
      *
      * Hent glidende gjennomsnittlig kostpris for postering
      *
     c                   eval      p_vare = fdvare
     c                   eval      p_lage = fdlage
     c                   eval      p_dato = %dec(w_dato)
      *
     c                   call      'VG701R'
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_dato
     c                   parm                    p_cost
      *
     c                   if        p_cost = 0
     c                   goto      les_neste
     c                   endif
      *
8.02  * Sjekk på salg avvikende fra lagerenhet
8.02  *
8.02 c                   if        fdenh1 <> w_enhl
8.02 c                   exsr      beregn_kost
8.02 c                   else
8.02 c                   eval      w_cost = p_cost
8.02 c                   endif
8.02  *
8.02 c                   eval(h)   w_belp = w_cost * fdanta
      *
      * Før kostpostering
      *
     c                   eval      w_belh = w_belp
     c                   eval      w_avde = d_kav3
     c                   eval      w_kont = vgkkos
     c                   eval      w_spes = d_ksp3
      *
     c                   exsr      skriv_post
      *
      * Før motpost
      *
     c                   eval      w_belh = w_belp * -1
     c                   eval      w_avde = d_kav3
     c                   eval      w_kont = d_kko3
     c                   eval      w_spes = d_ksp3
      *
     c                   exsr      skriv_post
      *
     c     les_neste     tag
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriver hovedboks postering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_post    begsr
      *
     c                   eval      fffirm = w_firm
     c                   eval      ffperi = w_peri
     c                   eval      ffbunt = 0
     c                   eval      ffbiln = w_biln
     c                   eval      ffbill = fdline
     c                   eval      ffbdat = w_dato
     c                   eval      fffdat = *loval
     c                   eval      ffbilk = vgkbik
     c                   eval      fftext = 'Ordrenr ' + %char(fonumm) +
     c                                      '/' + %char(fdline)
     c                   eval      ffdavd = 0
     c                   eval      ffdkto = 0
     c                   eval      ffdsps = 0
     c                   eval      ffdmom = ' '
     c                   eval      ffbelp = w_belh
     c                   eval      ffcavd = 0
     c                   eval      ffckto = 0
     c                   eval      ffcsps = 0
     c                   eval      ffcmom = ' '
     c                   eval      ffproj = *blank
     c                   eval      ffarbo = *blank
     c                   eval      ffakti = *blank
     c                   eval      ffkref = 0
     c                   eval      ffkund = fokund
     c                   eval      ffvalk = *blank
     c                   eval      ffvalb = 0
     c                   eval      ffmngd = 0
     c                   eval      ffautx = *blank
      *
      * Legger inn riktig
      * kontobegrep
      *
     c                   if        ffbelp < 0
     c                   eval      ffcavd = w_avde
     c                   eval      ffckto = w_kont
     c                   eval      ffcsps = w_spes
     c                   eval      ffbelp = ffbelp * -1
     c                   else
     c                   eval      ffdavd = w_avde
     c                   eval      ffdkto = w_kont
     c                   eval      ffdsps = w_spes
     c                   endif
      *
     c                   eval      ffmvag = 0
      *
     c                   write     fovfpfr
      *
     c                   endsr
8.02  *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Subrutine for beregning av kost-pris i salgsenhet
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     beregn_kost   begsr
8.02  *
8.02  * Finn omregningsfaktor på salgsenhet og beregn priser i forhold til basisenhet
8.02  *
8.02 c                   eval      vvenlr_vare = fdvare
8.02 c                   eval      vvenlr_enhe = fdenh1
8.02 c     vvenlr_key    chain     vvenlr
8.02 c                   if        %found(vvenlr)
8.02 c                   eval(h)   w_cost = p_cost * veomre
8.02 c                   endif
8.02  *
8.02  * Finn omregningsfaktor på lagerenhet og beregn priser dersom lagerenhet er ulik basisenhet
8.02  *
8.02 c                   if        w_enhe <> w_enhl
8.02 c                   eval      vvenlr_enhe = w_enhl
8.02 c     vvenlr_key    chain     vvenlr
8.02 c                   if        %found(vvenlr)
8.02 c                   eval(h)   w_cost = w_cost / veomre
8 02 c                   endif
8.02 c                   endif
8.02  *
8.02 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fast = *blank
     c                   eval      w_fell = vgkbis
     c                   eval      w_type = *blank
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_biln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter avdeling/konto/spesifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntkon        begsr
      *
      *  Legger ut parametere for konto-henvisnings-program
      *
     c                   eval      w_stat = *blank
     c                   eval      w_hele = *zero
      *
      *  Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_stat
     c                   parm                    fdlety
     c                   parm                    footyp
     c                   parm                    fdogrp
     c                   parm                    fdhgrp
     c                   parm                    fdugrp
     c                   parm                    fdvare
     c                   parm                    w_hele
     c                   eval      d_hele = w_hele
      *
      *  Overstyrer avdeling dersom denne
      *  finnes på ordrehode
      *
     c                   if        foavde > 0
     c                   eval      d_kav1 = foavde
     c                   eval      d_kav2 = foavde
     c                   eval      d_kav3 = foavde
     c                   eval      d_kav4 = foavde
     c                   eval      d_kav5 = foavde
     c                   eval      d_kav6 = foavde
     c                   eval      d_kav7 = foavde
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Key for statuskoder
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Key for kontohenvisninger
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
     c                   kfld                    vkhvl1_klty
     c                   kfld                    vkhvl1_koty
      *
      * Key for ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *  Key for ordre hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      *  Key for ordre linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
8.02  *
8.02  *  Key for oppdslag på vare/enhet
8.02  *
8.02 c     vvenlr_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    vvenlr_vare
8.02 c                   kfld                    vvenlr_enhe
      *
      * Henter inn firmakode fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
