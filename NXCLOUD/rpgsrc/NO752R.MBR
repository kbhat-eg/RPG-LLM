     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO752R
      * Beskrivelse . . : Ekstern ordre-hode, henter SmartKalk fil (csv-fil)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090320 ask  Nytt program
8.01  *       230322 bof  Har fjernet ubrukte kolonner slik at lass treffer riktig
8.01  *                   (det var slik det lå i ASKORR700)
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fneowpf    if   e           k disk    rename(neowpfr:neowpfr)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprlr    if   e           k disk    rename(fkprpfr:fkprlrr)
     fnkeol1    if   e           k disk    rename(nkeopfr:nkeol1r)
     ffenklr    if   e           k disk    rename(fenkpfr:fenklrr)
     fnohelu    uf a e           k disk    rename(nohepfr:nohelur)
     fnodtlu    uf a e           k disk    rename(nodtpfr:nodtlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_lety          s              1  0
     d p_mapp          s             50
     d p_file          s             50
     d p_feil          s              1
      *
      * Definering av variabler i nøkler
      *
     d nkeol1_kode     s                   like(nkkode)
     d rkunl1_kund     s                   like(rkkund)
     d fenklr_enhf     s                   like(feenhf)
     d fkprlr_kund     s                   like(flkund)
     d fkprlr_kpro     s                   like(flkpro)
      *
      * Definering av variabler
      *
     d w_tkod          s              1    inz('0')
     d w_ttyp          s              2    inz(*blank)
     d w_fast          s             10    inz(*blank)
     d w_numm          s              8  0
     d w_posi          s              3  0
     d w_leng          s              3  0
     d w_head          s             10
      *
     d w_line          s                   like(ndline)
     d w_proj          s              6  0
     d w_vare          s             15
     d w_lgva          s             15
     d w_varn          s              8  0
     d w_vtek          s             35
     d w_sps1          s             35
     d w_sps2          s             35
     d w_anta          s              9  2
     d w_men4          s             10  4
     d w_meng          s             10  3
     d w_enhe          s              3
     d w_pris          s             10  2
     d w_bel3          s             10  3
     d w_belp          s             10  2
     d w_ldat          s              8  0
     d w_lass          s              2  0
      *
     d w_firm          s                   like(ndfirm)
     d w_fsys          s                   like(nofsys)
     d w_prec          s           1000
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_fsys = 'SKC'
     c                   eval      p_feil = *off
      *
      * Tømmer arbeidsfil før innlesning
      *
     c/exec sql
     c+ delete from neowpf
     c/end-exec
      *
      * Kaller CL-program for innlesing av Smartkalk regneark
      *
     c                   call      'NO752C'
     c                   parm                    p_mapp
     c                   parm                    p_file
     c                   parm                    p_feil
      *
     c                   if        p_feil = *on
     c                   goto      avslutt
     c                   endif
      *
      * Oppretter ordre-hode
      *
     c                   exsr      ordre_hode
      *
     c                   if        p_feil = *on
     c                   goto      avslutt
     c                   endif
      *
      * Leser og oppretter ordre-linjer
      *
      *
     c                   eval      w_line = 0
     c                   read      neowpf
     c                   dow       not %eof
     c                   eval      w_prec = nwirad
      *
      * Sjekk om overskrift - behandle vanlige linjer
      *
     c                   eval      w_leng = %scan(';':w_prec) - 1
     c                   eval      w_head = %subst(w_prec:1:w_leng)
      *
     c                   if        w_head <> 'PROSJEKTNR'
     c                   exsr      ordre_linje
     c                   endif
      *
     c                   read      neowpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_hode    begsr
      *
      * Skriver til ekstern ordre-hode
      *
     c                   eval      nofirm = w_firm
     c                   eval      nofsys = w_fsys
     c                   eval      notell = 0
     c                   eval      nonref = *blank
     c                   eval      nofrsp = 0
     c                   eval      nolage = 0
     c                   eval      nouser = *blank
     c                   eval      nokund = p_kund
      *
      * Henter kundeinformasjon
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      noknav = rknavn
     c                   eval      nonavn = rknavn
     c                   eval      noadr1 = rkgate
     c                   eval      noadr2 = rkadr2
     c                   movel     rkponr        noponr
     c                   eval      nosted = rksted
     c                   else
     c                   eval      noknav = *blank
     c                   eval      nonavn = *blank
     c                   eval      noadr1 = *blank
     c                   eval      noadr2 = *blank
     c                   movel     *blank        noponr
     c                   endif
      *
      * Henter prosjektinformasjon
      *
     c                   eval      fkprlr_kund = p_kund
     c                   eval      fkprlr_kpro = p_kpro
     c     fkprlr_key    chain     fkprlr
     c                   if        %found
     c                   eval      nokpro = flkpro
     c                   eval      nopnav = flnavn
     c                   eval      nonavn = flnavn
     c                   eval      noadr1 = fladr1
     c                   eval      noadr2 = fladr2
     c                   eval      noponr = %char(flponr)
     c                   eval      nosted = *blank
     c                   else
     c                   eval      nokpro = 0
     c                   eval      nopnav = *blank
     c                   endif
      *
      *  Legger inn øvrig informasjon
      *
     c                   eval      nolety = p_lety
     c                   eval      noldat = *loval
     c                   eval      nodref = *blank
     c                   eval      norekv = *blank
     c                   eval      nomeld = *blank
     c                   eval      nounav = *blank
     c                   eval      noutlf = *blank
     c                   eval      noumob = *blank
     c                   eval      noufax = *blank
     c                   eval      noumai = *blank
     c                   eval      nonumm = 0
     c                   eval      noeusr = l_user
      *
     c                   time                    nobdat
     c                   time                    noodat
     c                   time                    nootim
     c                   time                    noedat
     c                   time                    noetim
      *
     c                   move      *blank        nonref
     c                   move      0             nolage
      *
     c                   if        nonavn = *blank and
     c                             noadr1 <> *blank
     c                   eval      nonavn = noadr1
     c                   eval      noadr1 = *blank
     c                   endif
      *
      * Henter neste nummer fra nummer-serie
      *
     c                   eval      nkeol1_kode = w_fsys
     c     nkeol1_key    chain     nkeol1r
     c                   if        not %found or
     c                             nkteln = *blank
     c                   eval      p_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   call      'AS100R'
     c                   parm                    w_tkod
     c                   parm                    w_firm
     c                   parm                    nkteln
     c                   parm                    w_ttyp
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   if        w_numm = 0
     c                   eval      p_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      notell = w_numm
      *
      * Skriver til fil
      *
     c                   time                    noodat
     c                   time                    nootim
     c                   eval      noousr = l_user
     c                   time                    noedat
     c                   time                    noetim
     c                   eval      noeusr = l_user
     c                   write     nohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_linje   begsr
      *
     c                   exsr      linje_red
     c                   exsr      log_vare
     c                   clear                   nodtlur
     c                   eval      w_line = w_line + 1
      *
      * Redigerer felter
      *
     c                   eval      ndfirm = w_firm
     c                   eval      ndfsys = w_fsys
     c                   eval      ndtell = w_numm
     c                   eval      ndline = w_line
      *
     c                   eval      ndvare = w_vare
     c                   eval      ndtek1 = w_vtek
     c                   eval      ndtek2 = w_sps1
     c                   eval      ndanta = w_meng
     c                   eval      ndenhe = w_enhe
     c                   eval      ndntpr = w_pris
     c                   eval      ndlety = p_lety
     c                   eval      ndldat = *loval
     c                   eval      ndlass = w_lass
     c
      *
      * Skriver til fil
      *
     c                   time                    ndodat
     c                   time                    ndotim
     c                   eval      ndousr = l_user
     c                   time                    ndedat
     c                   time                    ndetim
     c                   eval      ndeusr = l_user
     c                   write     nodtlur
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter data fra CSV-fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje_red     begsr
      *
      * Prosjektnr
      *
     c                   eval      w_posi = 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   eval      w_proj = +
     c                               %dec(%subst(w_prec:w_posi:w_leng):6:0)
      *
      * Artikkelnr
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   if        w_leng > 0
     c                   eval      w_vare = %subst(w_prec:w_posi:w_leng)
     c                   else
     c                   eval      w_vare = '0'
     c                   endif
      *
      * Artikkel tekst
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   eval      w_vtek = %subst(w_prec:w_posi:w_leng)
      *
      * Spesifikasjon 1
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   eval      w_sps1 = %subst(w_prec:w_posi:w_leng)
      *
      * Spesifikasjon 2
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   eval      w_sps2 = %subst(w_prec:w_posi:w_leng)
      *
      * Antall
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   if        w_leng > 0
     c                   eval      w_anta = +
     c                              %dec(%subst(w_prec:w_posi:w_leng):8:2)
     c                   else
     c                   eval      w_anta = 0
     c                   endif
      *
      * Mengde
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   if        w_leng > 0
     c                   eval      w_men4 = +
     c                               %dec(%subst(w_prec:w_posi:w_leng):10:4)
     c                   eval(h)   w_meng = w_men4
     c                   else
     c                   eval      w_meng = 0
     c                   endif
      *
      * Enhet
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   eval      w_enhe = %subst(w_prec:w_posi:w_leng)
      *
      * Pris
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   if        w_leng > 0
     c                   eval      w_pris = +
     c                              %dec(%subst(w_prec:w_posi:w_leng):10:2)
     c                   else
     c                   eval      w_pris = 0
     c                   endif
      *
      * Beløp
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   if        w_leng > 0
     c                   eval      w_bel3 = +
     c                               %dec(%subst(w_prec:w_posi:w_leng):10:3)
     c                   eval(h)   w_belp = w_bel3
     c                   else
     c                   eval      w_belp = 0
     c                   endif
      *
      * Leveringsdato
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   if        w_leng > 0
     c                   eval      w_ldat = +
     c                              %dec(%subst(w_prec:w_posi:w_leng):8:0)
     c                   else
     c                   eval      w_ldat = 0
     c                   endif
      *
      * Dummy les - hpooer over felter som ikke er i bruk
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
      *
8.01  * fjernet  overhopping her
      *
      * Lass nummer
      *
     c                   eval      w_posi = w_posi + w_leng + 1
     c                   eval      w_leng = %scan(';':w_prec:w_posi) - w_posi
     c                   if        w_leng > 0
     c                   eval      w_lass = +
     c                              %dec(%subst(w_prec:w_posi:w_leng):2:0)
     c                   else
     c                   eval      w_lass = 0
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om logistikkvare finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     log_vare      begsr
      *
     c                   eval      w_varn = %dec(w_vare:8:0)
     c                   eval      w_lgva = *blank
      *
     c/exec sql
     c+ select jvqvnr
     c+ into   :w_lgva
     c+ from   jvklpf
     c+ where  jvquno = :w_varn
     c/end-exec
      *
     c                   if        w_lgva <> *blank
     c                   eval      w_vare = %char(w_lgva)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = *on
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_lety
     c                   parm                    p_mapp
     c                   parm                    p_file
     c                   parm                    p_feil
      *
      * Key for oppslag på ordre-status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på prosjekt
      *
     c     fkprlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlr_kund
     c                   kfld                    fkprlr_kpro
      *
      * Key for oppslag på ekstern ordre-kode
      *
     c     nkeol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeol1_kode
      *
      * Key for oppslag på enhetskonvertering
      *
     c     fenklr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenklr_enhf
      *
      *
      * Henter verdier fra parametre
      *
     c                   eval      w_firm = p_firm
      *
      * Henter kode-informasjon
      *
     c     fstsl1_key    chain     fstsl1
      *
     c                   endsr
