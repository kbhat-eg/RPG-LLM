     H/TITLE  ASOFAK: Spørring mot ordre-linje-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO510R
      * Beskrivelse . . : Spørring mot ordre-linje/ Bestillingsantall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     FFO510D    CF   E             WORKSTN
     F                                     SFILE(D1SFL:SRRN02)
      *
     FVOTYL1    IF   E           K DISK
     F                                     RENAME(VOTYPFR:VOTYL1R)
      *
     FFOHELU    UF   E           K DISK
     F                                     RENAME(FOHEPFR:FOHELUR)
     FFOHEL1    IF   E           K DISK
     F                                     RENAME(FOHEPFR:FOHEL1R)
      *
     FFODTL1    IF   E           K DISK
     F                                     RENAME(FODTPFR:FODTL1R)
     FFODTLL    IF   E           K DISK
     F                                     RENAME(FODTPFR:FODTLLR)
     FFODTLK    IF   E           K DISK
     F                                     RENAME(FODTPFR:FODTLKR)
      *
     FFPLOLU    UF A E           K DISK
     F                                     RENAME(FPLOPFR:FPLOLUR)
     FFPLLLU    UF A E           K DISK
     F                                     RENAME(FPLLPFR:FPLLLUR)
      *
      * Definering av variable
      *
     d w_alf1          s              1
      *
      *
     D                UDS
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
     D  l_fnav               951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Kaller opp subrutine for å vise ordre-linjer                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      WPKUND        D2KUND
     C                   MOVE      WPNAVN        D2KUNA
      *
     C                   EXSR      XD1BLD
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandle bilde for VISE VARELINJER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   XD1BLD        BEGSR
      *
      *  Hent opplysninger fra ORDRE-LINJE-REGISTER
      *
     C                   MOVE      *ZERO         D1LINE
     C                   EXSR      XLSP02
      *
      *  Send Alt
      *
     C     D2TAGA        TAG
     C                   WRITE     D2CMD
      *
      *  Bare data
      *
     C     D2TAGB        TAG
     C                   EXSR      XDSP02                                       DSP SFL
      *
      *  F1=Vise bla-alternativer
      *
     C     *INKA         IFEQ      *ON
     C                   MOVE      '0'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    HEDFIR
     C                   PARM                    WPOTYP
     C                   ENDIF
      *
      *  F2=Vis bare linjer med bestillings-antall
      *
     C     *INKB         IFEQ      *ON
     C     *IN93         IFEQ      *OFF
     C                   MOVE      *ON           *IN93
     C                   ELSE
     C                   MOVE      *OFF          *IN93
     C                   END
     C                   MOVE      *ZERO         D1LINE
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGB
     C                   END
      *
      *  F3=Avslutt
      *
     C     *INKC         CABEQ     *ON           XD1SLT
      *
      *  F5=Forny
      *
     C     *INKE         IFEQ      *ON
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
      *  F8=Vis bare linjer med innkommet-antall
      *
     C     *INKH         IFEQ      *ON
     C     *IN94         IFEQ      *OFF
     C                   MOVE      *ON           *IN94
     C                   ELSE
     C                   MOVE      *OFF          *IN94
     C                   END
     C                   MOVE      *ZERO         D1LINE
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGB
     C                   END
      *
      *  Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT02                                       CRT SFL
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     C                   DO        SSRN02
     C                   READC     D1SFL
      *
     c                   if        %eof
     C                   LEAVE
     C                   ENDIF
      *
      *  Sjekk alle linjer
      *
     C     D1KODE        IFEQ      '1'
     C     D1ANTC        IFNE      *ZERO
     C                   MOVE      D1LINE        PLLLIN
     C                   EXSR      UPLUKK
     C                   MOVE      *BLANK        D1KODE
     C                   UPDATE    D1SFL
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDDO
      *
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGA
      *
      *  Avslutter behandlingen av ordrelinjer
      *
     CSR   XD1SLT        ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XLSP02        BEGSR
      *
     C     SFRN02        IFNE      *ZERO
     C     SFRN02        CHAIN     D1SFL                              60
     C                   END
      *
     C                   MOVE      D1LINE        KLLLIN
      *
     C     KLLKEY        SETLL     FODTLL
     C     KLKKEY        SETLL     FODTLK
      *
     C                   EXSR      XCLR02                                       TØM SFL
     C                   EXSR      XCRT02                                       CRT SFL
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP02        BEGSR
      *
     C     SRRN02        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     D2CTL
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR02        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     D2CTL
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN02
     C                   Z-ADD     0             SFRN02
     C                   Z-ADD     0             SSRN02
     C                   Z-ADD     0             SPGE02
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCRT02        BEGSR
     C                   ADD       15            SPGE02                         SFL END
     C                   Z-ADD     SSRN02        SRRN02
      *
     C     SRRN02        DOUEQ     SPGE02
      *
     C     WPVALG        IFEQ      1
     C                   READ      FODTLL                                 15
     C                   ELSE
     C                   READ      FODTLK                                 15
     C                   ENDIF
      *
     C     *IN15         IFEQ      *OFF
     C     FDKUND        IFNE      KLLKUN
     C                   MOVE      *ON           *IN15
     C                   END
     C                   END
      *
     C     *IN15         IFEQ      *OFF
     C     WPVALG        IFEQ      2
     C     FDVARE        IFEQ      *BLANK
     C                   ITER
     C                   END
     C                   END
     C                   END
      *
      *  Test1=Hopper over alle linjer som ikke er best.antall
      *
     C     *IN15         IFEQ      *OFF
     C     *IN93         IFEQ      *ON
     C     FDANTB        IFEQ      *ZERO
     C                   ITER
     C                   END
     C                   END
     C                   END
      *
      *  Test1=Hopper over alle linjer som ikke er innkommet antall
      *
     C     *IN15         IFEQ      *OFF
     C     *IN94         IFEQ      *ON
     C     FDANTC        IFEQ      *ZERO
     C                   ITER
     C                   END
     C                   END
     C                   END
      *
     C     *IN15         CABEQ     '1'           XEND02                         EOF
      *
     C                   MOVE      FDNUMM        HEDNUM
     C                   MOVE      FDSUFF        HEDSUF
     C     HEDKEY        CHAIN     FOHEL1R                            90
      *
     c                   eval      d1dato = 0
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        d1dato
     c                   endif
      *
     C     WPOTYP        IFNE      *BLANK
     C     FOOTYP        IFNE      WPOTYP
     C                   ITER
     C                   END
     C                   END
      *
     C                   ADD       1             SRRN02
     C                   MOVE      FDNUMM        D1NUMM
     C                   MOVE      FDSUFF        D1SUFF
     C                   MOVE      FDLINE        D1LINE
     C                   MOVEL     FDVARE        D1VARE
     C                   MOVEL     FDTEK1        D1TEK1
     C                   MOVE      FDENH1        D1ENH1
     C                   MOVE      FDANTA        D1ANTA
     C                   MOVE      FDANTC        D1ANTC
      *
     C                   WRITE     D1SFL
     C                   ENDDO
      *
     C     XEND02        TAG
      *
     C     SRRN02        IFGT      SSRN02
     C     SSRN02        ADD       1             SFRN02
     C                   ENDIF
      *
     C                   Z-ADD     SRRN02        SSRN02
     C                   Z-ADD     SFRN02        SVRN02
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter en plukket ordre/ordrelinje                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     UPLUKK        BEGSR
      *
      *  Hent opplysninger fra ORDRE-LINJE-REGISTER
      *
     C                   MOVE      D1NUMM        LINNUM
     C                   MOVE      D1SUFF        LINSUF
     C                   MOVE      D1LINE        LINLIN
     C     LINKEY        CHAIN     FODTL1R                            90
      *
      *  Oppdaterer ORDRE-HODE-REGISTER, Ordren er til behandling
      *
     C                   CALL      'FO709R'
     C                   PARM                    FDNUMM
     C                   PARM                    FDSUFF
      *
     C                   MOVE      FDNUMM        HEDNUM
     C                   MOVE      FDSUFF        HEDSUF
     C     HEDKEY        CHAIN     FOHELUR                            90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      3             FOKODE
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = dsuser
     C                   UPDATE    FOHELUR
     C                   END
      *
      *  Hent opplysninger fra ORDREYTYPE-REGISTER
      *
     C                   MOVE      FOOTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
      *
      *  Legger ut ORDRE-HODE til UTPLUKK-PARAM-REGISTER
      *
     C                   MOVE      FDNUMM        PLONUM
     C                   MOVE      FDSUFF        PLOSUF
     C     PLOKEY        CHAIN     FPLOLUR                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      PLOFIR        FKPFIR
     C                   MOVE      PLONUM        FKPONR
     C                   MOVE      PLOSUF        FKPSUF
     C                   MOVE      VAONXT        FKPOTY
     C                   MOVE      DSWSID        FKPWSD
     C                   MOVE      DSUSER        FKPUSR
     C                   MOVE      *ZERO         FKPALT
     C                   MOVE      *ZERO         FKPDIR
     C                   Z-ADD     1             FKPRES                         PLUKKER LINJER
     C                   MOVE      *ZERO         FKPKOL
     C                   MOVE      *ZERO         FKPNET
     C                   MOVE      *ZERO         FKPBRU
     C                   MOVE      *BLANK        FKPLMT
     C                   MOVE      *ZERO         FKPFPR
     C                   MOVE      *BLANK        FKPFTE
     C                   MOVE      *ZERO         FKPEPR
     C                   MOVE      *BLANK        FKPETE
     C                   WRITE     FPLOLUR
     C                   END
      *
      *  Legger ut ORDRE-LINJE til Plukk-linje-register
      *
     C                   MOVE      FDNUMM        PLLNUM
     C                   MOVE      FDSUFF        PLLSUF
     C                   MOVE      FDLINE        PLLLIN
     C     PLLKEY        CHAIN     FPLLLUR                            96
      *
      *  Legg inn verdier i UTPLUKK-LINJE-REGISTER
      *
     C                   Z-ADD     FDANTC        FKLANT
     C                   MOVE      *BLANK        FKLKOD
      *
     C     *IN96         IFEQ      *OFF
     C                   UPDATE    FPLLLUR
     C                   ELSE
     C                   MOVE      PLLFIR        FKLFIR
     C                   MOVE      PLLNUM        FKLONR
     C                   MOVE      PLLSUF        FKLSUF
     C                   MOVE      PLLLIN        FKLLIN
     C                   WRITE     FPLLLUR
     C                   END
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   MOVE      *BLANK        WSEQEN            1
      *
     C                   Z-ADD     0             SPGE02            3 0
     C                   Z-ADD     0             SRRN02            4 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
      *
     C     *LIKE         DEFINE    SRRN02        SFRN02
     C     *LIKE         DEFINE    SRRN02        SSRN02
      *
     C     *LIKE         DEFINE    w_alf1        WRETUR
     C     *LIKE         DEFINE    w_alf1        WPENOR
     C     *LIKE         DEFINE    w_alf1        WPIPOR
     C     *LIKE         DEFINE    FDLINE        WWLINE
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    FOOTYP        WPOTYP
     C     *LIKE         DEFINE    w_alf1        WPPLOR
     C     *LIKE         DEFINE    FDFIRM        WPFIRM
     C     *LIKE         DEFINE    FDKUND        WPKUND
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    FOFIRM        HEDFIR
     C     *LIKE         DEFINE    FONUMM        HEDNUM
     C     *LIKE         DEFINE    FOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    FDFIRM        LINFIR
     C     *LIKE         DEFINE    FDNUMM        LINNUM
     C     *LIKE         DEFINE    FDSUFF        LINSUF
     C     *LIKE         DEFINE    FDLINE        LINLIN
      *
     C     *LIKE         DEFINE    FDFIRM        KLLFIR
     C     *LIKE         DEFINE    FDKUND        KLLKUN
     C     *LIKE         DEFINE    FDNUMM        KLLNUM
     C     *LIKE         DEFINE    FDSUFF        KLLSUF
     C     *LIKE         DEFINE    FDLINE        KLLLIN
     C     *LIKE         DEFINE    FDVARE        KLLVAR
      *
     C     *LIKE         DEFINE    FKPFIR        PLOFIR
     C     *LIKE         DEFINE    FKPONR        PLONUM
     C     *LIKE         DEFINE    FKPSUF        PLOSUF
      *
     C     *LIKE         DEFINE    FKLFIR        PLLFIR
     C     *LIKE         DEFINE    FKLONR        PLLNUM
     C     *LIKE         DEFINE    FKLSUF        PLLSUF
     C     *LIKE         DEFINE    FKLLIN        PLLLIN
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     C     LINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINNUM
     C                   KFLD                    LINSUF
     C                   KFLD                    LINLIN
      *
      *  Key for file : KUNDE-ORDRE-LINJE-REGISTER
      *
     C     KLLKEY        KLIST
     C                   KFLD                    KLLFIR
     C                   KFLD                    KLLKUN
     C                   KFLD                    KLLNUM
     C                   KFLD                    KLLSUF
     C                   KFLD                    KLLLIN
      *
      *  Key for file : KUNDE-VARE-ORDRE-LINJE-REGISTER
      *
     C     KLKKEY        KLIST
     C                   KFLD                    KLLFIR
     C                   KFLD                    KLLKUN
     C                   KFLD                    KLLVAR
     C                   KFLD                    KLLNUM
     C                   KFLD                    KLLSUF
     C                   KFLD                    KLLLIN
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        KLIST
     C                   KFLD                    PLOFIR
     C                   KFLD                    PLONUM
     C                   KFLD                    PLOSUF
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     C     PLLKEY        KLIST
     C                   KFLD                    PLLFIR
     C                   KFLD                    PLLNUM
     C                   KFLD                    PLLSUF
     C                   KFLD                    PLLLIN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPLOR
     C                   PARM                    WPOTYP
     C                   PARM                    WPVALG            1 0
     C                   PARM                    WPFIRM
     C                   PARM                    WPKUND
     C                   PARM                    WPNAVN           30
      *
      *  Legger inn firma i nøkklene
      *
     C                   MOVE      WPFIRM        OTYFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        LINFIR
     C                   MOVE      WPFIRM        KLLFIR
     C                   MOVE      WPFIRM        PLOFIR
     C                   MOVE      WPFIRM        PLLFIR
      *
      *  Legger inn kundenummer i nøkklene
      *
     C                   MOVE      WPKUND        KLLKUN
      *
      *  Nullstiller felter
      *
     C                   MOVE      *ZERO         KLLNUM
     C                   MOVE      *ZERO         KLLSUF
     C                   MOVE      *ZERO         KLLLIN
      *
      *  Midlertidig løsning inntil
      *  prorammet blir skrevet om
      *
     c                   move      *blank        d1kode            1
      *
     C                   ENDSR
