     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VP681R
      * Beskrivelse . . : Vare-pris, prisfil til kunder - styreprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300599 HKO  Nytt program
      *       260400 HKO  Lagt inn valg av mappenavn
      *       240101 HKO  Lagt inn membernavn ved kall til utskriftsprogram
      *       160204 HKO  Tester på recordformat
      *       260606 HKO  Henter leveringstype fra kunde distribusjonsliste
      *                   dersom leveringstype ikke er valgt
5.70  * PRGR  110509 BOF  Lagt inn valg av lager - hvis lagerspes.priser
7.01  *k00  030620 BOF NXS-1195  utvidet med to parametere
7.02  *k00  220617 bof  Paramter for å kun ta ut nobbnr.
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkdil1    if   e           k disk    rename(fkdipfr:fkdil1r)
      *
      * Definering av datastrukturer
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
7.02 d d_list                        82
     d  d_firm                        3  0 overlay(d_list)
     d  d_rkat                        3  0 overlay(d_list:4)
     d  d_kund                        6  0 overlay(d_list:7)
     d  d_kpro                        6  0 overlay(d_list:13)
     d  d_rfmt                       10    overlay(d_list:19)
     d  d_vsor                       10    overlay(d_list:29)
     d  d_ogrp                        2  0 overlay(d_list:39)
     d  d_hgrp                        2  0 overlay(d_list:41)
     d  d_ugrp                        3  0 overlay(d_list:43)
     d  d_ldor                        6  0 overlay(d_list:46)
     d  d_rfm2                       10    overlay(d_list:52)
     d  d_sval                        1  0 overlay(d_list:62)
     d  d_tval                        1  0 overlay(d_list:63)
     d  d_2val                        1  0 overlay(d_list:64)
     d  d_bval                        1  0 overlay(d_list:65)
     d  d_lety                        1  0 overlay(d_list:66)
     d  d_dato                         d   overlay(d_list:67) datfmt(*iso)
5.70 d  d_lage                        2  0 overlay(d_list:79)
7.02 d  d_nval                        1  0 overlay(d_list:81)
      *
      * Definering av parametre
      *
7.02 d p_list          s             82
     d p_fldr          s             12
      *
      * Definering av variabler i nøkler
      *
     d fkdil1_kund     s                   like(fhkund)
     d fkdil1_kpro     s                   like(fhkpro)
      *
      * Definering av variable
      *
     d w_vsor          s             10
     d w_ogrp          s              2  0
     d w_hgrp          s              2  0
     d w_ugrp          s              3  0
     d w_ldor          s              6  0
     d w_rfmt          s             10
     d w_sval          s              1  0
     d w_tval          s              1  0
     d w_2val          s              1  0
     d w_bval          s              1  0
     d w_dato          s               d   datfmt(*iso)
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_memb          s             10
7.02 d w_nval          s              1  0
      *
     d w_firm          s                   like(fhfirm)
     d w_kund          s                   like(fhkund)
     d w_kpro          s                   like(fhkpro)
     d w_lety          s                   like(fhlety)
     d w_ftis          s                   like(fhftis)
5.70 d w_lage          s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 c                   eval      w_sval = d_sval
7.01 c                   eval      w_tval = d_tval
      *
      * Behandling avhengig av valgte parametre
      *
     c                   select
     c                   when      d_rkat <> 0
     c                   exsr      behandl_rkat
     c                   when      d_kpro <> 0
     c                   exsr      behandl_kpro
     c                   when      d_kund <> 0
     c                   exsr      behandl_kund
     c                   other
     c                   exsr      behandl_tot
     c                   endsl
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på rabattkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_rkat  begsr
      *
      * Leser alle kunder med valgt rabattkategori
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rkrabk <> d_rkat
     c                   goto      neste_kunde
     c                   endif
      *
      * Leser alle kombinasjoner av kunde/kundeprosjekt for kunde.
      * Det skal kun skrives til prisfil dersom 'prisfil' er valgt i
      * distribusjonslisten, og dersom recordformatet i distr.listen er
      * likt valgt recordformat.
      *
     c                   eval      fkdil1_kund = rkkund
     c     fkdil1_key2   setll     fkdil1
     c     fkdil1_key2   reade     fkdil1                                 91
     c                   dow       *in91 = *off
      *
     c                   if        fhpfil = 0
     c                   goto      neste_distr
     c                   endif
      *
     c                   if        d_rfmt <> *blank and
     c                             d_rfmt <> fhrfmt
     c                   goto      neste_distr
     c                   endif
      *
     c                   eval      w_kund = fhkund
     c                   eval      w_kpro = fhkpro
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
     c                   if        d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   else
     c                   eval      w_vsor = d_vsor
     c                   endif
     c                   if        d_lety = 0
     c                   eval      w_lety = fhlety
     c                   else
     c                   eval      w_lety = d_lety
     c                   endif
     c                   if        d_rfm2 = *blank
     c                   eval      w_rfmt = fhrfmt
     c                   else
     c                   eval      w_rfmt = d_rfm2
     c                   endif
5.70 c                   eval      w_lage = d_lage
7.01 c*                  eval      w_tval = d_tval
7.01 c*                  eval      w_sval = d_sval
      *
     c                   exsr      prisfil
      *
     c     neste_distr   tag
      *
     c     fkdil1_key2   reade     fkdil1                                 91
     c                   enddo
      *
     c     neste_kunde   tag
      *
     c     rkunl1_key    reade     rkunl1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på kundeprosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_kpro  begsr
      *
     c                   eval      w_kund = d_kund
     c                   eval      w_kpro = d_kpro
      *
     c                   eval      fkdil1_kund = d_kund
     c                   eval      fkdil1_kpro = d_kpro
     c     fkdil1_key1   chain     fkdil1                             90
      *
     c                   eval      w_vsor = d_vsor
     c                   if        *in90  = *off and
     c                             d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   endif
      *
     c                   eval      w_lety = d_lety
5.70 c                   eval      w_lage = d_lage
7.01 c                   if        %found(fkdil1)
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
7.01 c                   endif
7.01 c                   if        *in90  = *off and
     c                             d_lety = 0
     c                   eval      w_lety = fhlety
     c                   endif
      *
     c                   eval      w_rfmt = d_rfm2
     c                   if        *in90  = *off and
     c                             d_rfm2 = *blank
     c                   eval      w_rfmt = fhrfmt
     c                   endif
      *
     c                   exsr      prisfil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_kund  begsr
      *
     c                   eval      w_kund = d_kund
      *
     c                   eval      fkdil1_kund = d_kund
     c     fkdil1_key2   setll     fkdil1
     c     fkdil1_key2   reade     fkdil1                                 90
      *
      * Behandling dersom distribusjonsliste ikke finnes for kunde
      *
     c                   if        *in90 = *on
     c                   eval      w_kpro = 0
     c                   eval      w_vsor = d_vsor
     c                   eval      w_lety = d_lety
5.70 c                   eval      w_lage = d_lage
     c                   eval      w_rfmt = d_rfm2
     c                   exsr      prisfil
     c                   endif
      *
      * Behandling dersom distribusjonsliste finnes for kunde
      *
     c                   dow       *in90 = *off
      *
     c                   eval      w_kpro = fhkpro
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
7.01 c                   if        d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   else
     c                   eval      w_vsor = d_vsor
     c                   endif
     c                   if        d_lety = 0
     c                   eval      w_lety = fhlety
     c                   else
     c                   eval      w_lety = d_lety
     c                   endif
     c                   if        d_rfm2 = *blank
     c                   eval      w_rfmt = fhrfmt
     c                   else
     c                   eval      w_rfmt = d_rfm2
     c                   endif
5.70 c                   eval      w_lage = d_lage
     c                   exsr      prisfil
      *
     c     fkdil1_key2   reade     fkdil1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hele distribusjonslisten
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_tot   begsr
      *
      * Leser hele kunde-distribusjonslisten
      * Det skal kun skrives til prisfil dersom 'prisfil' er valgt i
      * distribusjonslisten, og dersom recordformatet i distr.listen er
      * likt valgt recordformat.
      *
     c     fkdil1_key3   setll     fkdil1
     c     fkdil1_key3   reade     fkdil1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        fhpfil = 0
     c                   goto      neste_distt
     c                   endif
      *
     c                   if        d_rfmt <> *blank and
     c                             d_rfmt <> fhrfmt
     c                   goto      neste_distt
     c                   endif
      *
     c                   eval      w_kund = fhkund
     c                   eval      w_kpro = fhkpro
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
7.01 c                   if        d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   else
     c                   eval      w_vsor = d_vsor
     c                   endif
     c                   if        d_lety = 0
     c                   eval      w_lety = fhlety
     c                   else
     c                   eval      w_lety = d_lety
     c                   endif
     c                   if        d_rfm2 = *blank
     c                   eval      w_rfmt = fhrfmt
     c                   else
     c                   eval      w_rfmt = d_rfm2
     c                   endif
5.70 c                   eval      w_lage = d_lage
      *
     c                   exsr      prisfil
      *
     c     neste_distt   tag
      *
     c     fkdil1_key3   reade     fkdil1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift til prisfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prisfil       begsr
      *
     c                   if        w_rfmt = *blank
     c                   leavesr
     c                   endif
      *
     c                   if        d_ogrp <> 0
     c                   eval      w_vsor = *blank
     c                   endif
      *
     c                   eval      w_ogrp = d_ogrp
     c                   eval      w_hgrp = d_hgrp
     c                   eval      w_ugrp = d_ugrp
     c                   eval      w_ldor = d_ldor
7.01 c*                   eval      w_sval = d_sval
7.01 c*                   eval      w_tval = d_tval
     c                   eval      w_2val = d_2val
     c                   eval      w_bval = d_bval
     c                   eval      w_dato = d_dato
7.02 c                   eval      w_nval = d_nval
      *
     c                   time                    w_tmst
     c     *iso0         move      w_tmst        w_tmst_20
      *
     c                   eval      w_memb = 'P' + %subst(w_tmst_20:9:9)
      *
     c                   call      'VP682C'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    w_vsor
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_ldor
     c                   parm                    w_rfmt
     c                   parm                    fhendr
     c                   parm                    fhdato
     c                   parm                    w_sval
     c                   parm                    w_tval
     c                   parm                    w_2val
     c                   parm                    w_bval
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_lage
     c                   parm                    w_ftis
     c                   parm                    p_fldr
     c                   parm                    w_memb
7.02 c                   parm                    w_nval
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
     c                   parm                    p_fldr
      *
      * Key for les på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på kunde distribusjonsregister
      *
     c     fkdil1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
     c                   kfld                    fkdil1_kpro
      *
     c     fkdil1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
      *
     c     fkdil1_key3   klist
     c                   kfld                    w_firm
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = d_firm
      *
      * Henter timestamp, for å benytte denne som unik nøkkel på
      * forsendelsen.
      *
     c                   time                    w_ftis
      *
     c                   endsr
