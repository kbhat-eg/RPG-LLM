     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JM601R
      * Beskrivelse . . : Utskrift av kampanje
      *
      * Programmet skriver kun ut kampanjevarer hvor varen er opprettet
      * i EVR. Prisene hentes ikke fra EVR, men beregnes.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141097 HKO  Nytt program
5.50  *       150108 bof  Henter enhet1,2,3 fra subprogram
5.70  * PRGR  171208 bof  utvidet med varetype fra lager
os71  * os71  161210 bsa  Lagt inn korreksjoner på SQL pga. OS/400 feil
os71  *                   Det er også lagt inn endring på SQL,
os71  *                   som er nødvendig ved overgang til v6.1
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  120112 bsa  Utvidet vp755r med kampanje
6.30  * 6.20  270114 bof  Utvidet paramter til jm705r
6.3q  *       260515 bof  Datoformat sql
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjkavpf    if   e           k disk    rename(jkavpfr:jkavpfr)
     fjvarpf    if   e           k disk    rename(jvarpfr:jvarpfr)
     fjkahlr    if   e           k disk    rename(jkahpfr:jkahlrr)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fjm601p    o    e             printer
      *
      * Definering av parametre
      *
     d p_parm          s              8
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
5.70 d l_lagr               1001   1002  0
      *
      * Definering av datastrukturer
      *
      *  Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                         8
     d  d_firm                        3  0 overlay(d_list)
     d  d_kamp                        5    overlay(d_list:4)
      *
      * Definering av variabler i nøkler
      *
     d jkavpf_vkam     s                   like(jmvkam)
     d jkahlr_kamp     s                   like(jmkamp)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jlevl1_ldor     s                   like(jlldor)
     d vvarl1_vare     s                   like(vvvare)
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_enh1          s                   like(vpenhe)
     d p_kop1          s                   like(vpkopr)
     d p_inp1          s                   like(vpinpr)
     d p_enh2          s                   like(vpenhe)
     d p_kop2          s                   like(vpkopr)
     d p_inp2          s                   like(vpinpr)
     d p_omr2          s             12  6
     d p_enh3          s                   like(vpenhe)
     d p_kop3          s                   like(vpkopr)
     d p_inp3          s                   like(vpinpr)
     d p_omr3          s             12  6
      *
      * Definering av variable
      *
     d b_EOF           s              1    inz(*off)
     d w_timestamp     s             12  0
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_vldo_alf      s              6
      *
     d w_firm          s                   like(jmfirm)
     d w_kamp          s                   like(jmkamp)
     d w_kopr          s                   like(jmvkpr)
     d w_enhe          s                   like(vvenhe)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(jlnavn)
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
8.01 d w_prgr          s              2
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
      *
     d s_ogrp          s                   like(jvogrp)
     d s_hgrp          s                   like(jvhgrp)
     d s_ugrp          s                   like(jvugrp)
     d s_vldo          s                   like(jmvldo)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      * Initiering av header
      *
     c                   exsr      hode_init
      *
      * Behandler utskift
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_init     begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter kampanje-hode informasjon
      *
     c                   eval      jkahlr_kamp = w_kamp
     c     jkahlr_key    chain     jkahlr                             90
     c                   if        *in90 = *off
     c                   eval      a1kamp = jmkamp
     c                   eval      a1besk = jmbesk
     c     *dmy          move      jmfdat        a1fdat
     c     *dmy          move      jmtdat        a1tdat
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c/exec sql
     c+ declare kampanje cursor for
os71 c+ select v.jvogrp,
os71 c+        v.jvhgrp,
os71 c+        v.jvugrp,
os71 c+        k.jmvldo,
os71 c+        v.jvtek1,
os71 c+        k.jmvvar,
os71 c+        v.jvnobb,
os71 c+        k.jmvkai,
os71 c+        k.jmvkpr,
os71 c+        k.jmvkaa,
os71 c+        k.jmvkau
os71 c+ from   jkavpf as k inner join
os71 c+        jvarpf as v on
os71 c+        k.jmvvar = v.jvvare
os71 c+ where  k.jmvfir = :w_firm and
os71 c+        k.jmvkam = :w_kamp
os71 c+ order by v.jvogrp, v.jvhgrp, v.jvugrp, k.jmvldo, v.jvtek1, k.jmvvar
     c+ for read only
     c/end-exec
      *
     c/exec sql
     c+ open kampanje
     c/end-exec
      *
      * Leser cursor kampanje
      *
     c                   dow       b_EOF = *off
      *
     c/exec sql
     c+ fetch kampanje
     c+ into  :jvogrp,
     c+       :jvhgrp,
     c+       :jvugrp,
     c+       :jmvldo,
     c+       :jvtek1,
     c+       :jmvvar,
     c+       :jvnobb,
     c+       :jmvkai,
     c+       :jmvkpr,
     c+       :jmvkaa,
     c+       :jmvkau
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   leave
     c                   endif
      *
      * Leser neste post dersom varen ikke finnes i EVR
      *
     c                   eval      vvarl1_vare = jmvvar
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91
     c                   iter
     c                   endif
      *
      * Behandler brudd
      *
     c                   if        jvogrp <> s_ogrp
     c                   exsr      hode_ogrp
     c                   endif
      *
     c                   if        jvogrp <> s_ogrp or
     c                             jvhgrp <> s_hgrp
     c                   exsr      hode_hgrp
     c                   endif
      *
     c                   if        jvogrp <> s_ogrp or
     c                             jvhgrp <> s_hgrp or
     c                             jvugrp <> s_ugrp
     c                   exsr      hode_ugrp
     c                   endif
      *
     c                   if        jvogrp <> s_ogrp or
     c                             jvhgrp <> s_hgrp or
     c                             jvugrp <> s_ugrp or
     c                             jmvldo <> s_vldo
     c                   exsr      hode_vldo
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      hent_enhe
     c                   exsr      detalj
      *
      * Dersom enhet-2 eller enhet-3 finnes på vare, kalles subrutine for
      * utskrift av detaljlinje med priser for disse enhetene
      *
     c                   if        p_enh2 <> *blank
     c                   eval      w_enhe = p_enh2
     c                   exsr      detalj_enh
     c                   endif
      *
     c                   if        p_enh3 <> *blank
     c                   eval      w_enhe = p_enh3
     c                   exsr      detalj_enh
     c                   endif
      *
      * Tar vare på brudd-variable
      *
     c                   eval      s_ogrp = jvogrp
     c                   eval      s_hgrp = jvhgrp
     c                   eval      s_ugrp = jvugrp
     c                   eval      s_vldo = jmvldo
      *
     c                   enddo
      *
      * Lukker cursor kampanje
      *
     c/exec sql
     c+ close kampanje
     c/end-exec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ogrp     begsr
      *
     c                   eval      w_otxt = *blank
      *
     c                   eval      vogrl1_oogr = jvogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_hgrp     begsr
      *
     c                   eval      w_htxt = *blank
      *
     c                   eval      vhgrl1_hogr = jvogrp
     c                   eval      vhgrl1_hhgr = jvhgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ugrp     begsr
      *
     c                   eval      w_utxt = *blank
      *
     c                   eval      vugrl1_uogr = jvogrp
     c                   eval      vugrl1_uhgr = jvhgrp
     c                   eval      vugrl1_uugr = jvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c                   move      jvogrp        w_ogrp_alf
     c                   move      jvhgrp        w_hgrp_alf
     c                   move      jvugrp        w_ugrp_alf
      *
     c                   eval      a2txt1 = %trim( %trim(w_otxt) + ' - ' +
     c                                      %trim(w_htxt) + ' - ' +
     c                                      %trim(w_utxt) + ' (' +
     c                                      w_ogrp_alf + ' ' +
     c                                      w_hgrp_alf + ' ' +
     c                                      w_ugrp_alf + ')')
      *
     c                   eval      a2txt2 = a2txt1
      *
     c                   write     a2hdr                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på leverandøre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_vldo     begsr
      *
     c                   eval      w_lnav = *blank
      *
     c                   eval      jlevl1_ldor = jmvldo
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lnav = jlnavn
     c                   endif
      *
     c                   move      jmvldo        w_vldo_alf
      *
     c                   eval      a3txt1 = %trim( %trim(w_lnav) + ' (' +
     c                                      w_vldo_alf + ')')
      *
     c                   eval      a3txt2 = a3txt1
      *
     c                   write     a3hdr                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
5.70  *
5.70  * finner varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
      *
     c                   eval      d1tek1 = jvtek1
     c                   eval      d1vvar = jmvvar
     c                   eval      d1nobb = jvnobb
5.70 c                   eval      d1vtyp = w_vtyp
     c                   eval      d1enhe = p_enh1
      *
      * Kaller program for beregning av salgspris for enhet
      *
     c                   call      'JM705R'
     c                   parm                    w_firm
     c                   parm                    jmvvar
6.30 c                   parm                    vvnlev
     c                   parm                    p_enh1
     c                   parm                    jmvkpr
     c                   parm                    jmvkai
     c                   parm                    jmvkaa
     c                   parm                    jmvkau
     c                   parm                    w_kopr
     c                   parm                    d1sapr
     c                   parm                    d1saim
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje for enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj_enh    begsr
      *
     c                   eval      d2enhe = w_enhe
      *
      * Kaller program for beregning av salgspris for enhet
      *
     c                   call      'JM705R'
     c                   parm                    w_firm
     c                   parm                    jmvvar
6.30 c                   parm                    vvnlev
     c                   parm                    w_enhe
     c                   parm                    jmvkpr
     c                   parm                    jmvkai
     c                   parm                    jmvkaa
     c                   parm                    jmvkau
     c                   parm                    w_kopr
     c                   parm                    d2sapr
     c                   parm                    d2saim
      *
      * Skriv detaljlinje
      *
     c                   write     d2dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente enhet 1, 2 og 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_enhe     begsr
      *
     c                   eval      p_firm = w_firm
     c                   eval      p_vare = vvvare
     c                   eval      p_lety = 0
     c                   time                    p_dato
     c                   call      'VP755R   '
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    w_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enh1
     c                   parm                    p_kop1
     c                   parm                    p_inp1
     c                   parm                    p_enh2
     c                   parm                    p_kop2
     c                   parm                    p_inp2
     c                   parm                    p_omr2
     c                   parm                    p_enh3
     c                   parm                    p_kop3
     c                   parm                    p_inp3
     c                   parm                    p_omr3
6.22 c                   parm                    w_kamp
     c
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for oppslag på kampanje-hode
      *
     c     jkahlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlr_kamp
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vareregister i EVR
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      d_list = p_parm
      *
     c                   eval      w_firm = d_firm
     c                   eval      w_kamp = d_kamp
     c                   eval      w_lage = l_lagr
      *
     c                   call      'VL711R  '
     c                   parm                    w_prgr
      *
     c                   endsr
