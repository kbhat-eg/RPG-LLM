     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : rk791R
      * Beskrivelse . . : Leser csv fil med kontaktpersoner.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201120 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frukplu    uf a e           k disk    rename(rukppfr:rukplur)
      *
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av recordbeskrivelse - eksportfil/kunder
      *
     d                 ds
     d d_inpu                      1024
     d  d_kund                        6    overlay(d_inpu:1)
     d   d_kundn                      6  0 overlay(d_kund:1)
     d  d_alfa                       40    overlay(d_inpu:7)
     d  d_enav                       30    overlay(d_inpu:47)
     d  d_fnav                       30    overlay(d_inpu:77)
     d  d_kpnr                        6    overlay(d_inpu:107)
     d   d_kpnrn                      6  0 overlay(d_kpnr:1)
     d  d_krol                       20    overlay(d_inpu:113)
     d  d_ktlf                       11    overlay(d_inpu:133)
     d  d_mtlf                       11    overlay(d_inpu:144)
     d  d_mail                       50    overlay(d_inpu:155)
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d rukplu_kund     s                   like(rukund)
     d rukplu_levr     s                   like(rulevr)
     d rukplu_kpnr     s                   like(rukpnr)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
     d w_kpnr          s              6  0
     d w_test          s             50
     d w_kund          s              6  0
     d w_pos           s              5u 0
     d w_posi          s              2  0
     d w_posb          s              2  0
     d s_kpnr          s                   like(rukpnr)
     d w_retk          s              1
      *
     d b_ok            s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digits          c                   ' 0123456789'
     d c_blan          s             15    inz('               ')
     d digitx          c                   '0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser eksportfil/kunder og oppdaterer kundereg.
      *
     c                   read      lwexpf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
     c                   if        b_ok = *ON
     c                   exsr      oppr_kun
     c                   endif
      *
     c                   read      lwexpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av kunde kontaktperson
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_kun      begsr
      *
     c                   if        d_enav = *blank and
     c                             d_fnav = *Blank
     c                   leavesr
     c                   endif
      * sjekk om kunde finnes
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found(rkunl1)
     c                   leavesr
     c                   endif
      *
      * Oversetter ansii æ,ø og å til ebcdic-verdier
      *
     c     X'46':'Æ'     xlate     d_enav        d_enav
     c     X'77':'Ø'     xlate     d_enav        d_enav
     c     X'2A':'Å'     xlate     d_enav        d_enav
     c     X'EF':'Å'     xlate     d_enav        d_enav
     c     X'90':'ø'     xlate     d_enav        d_enav
      *
     c     X'46':'Æ'     xlate     d_fnav        d_fnav
     c     X'77':'Ø'     xlate     d_fnav        d_fnav
     c     X'2A':'Å'     xlate     d_fnav        d_fnav
     c     X'EF':'Å'     xlate     d_fnav        d_fnav
     c     X'90':'ø'     xlate     d_fnav        d_fnav
      *
     c     X'46':'Æ'     xlate     d_krol        d_krol
     c     X'77':'Ø'     xlate     d_krol        d_krol
     c     X'2A':'Å'     xlate     d_krol        d_krol
     c     X'EF':'Å'     xlate     d_krol        d_krol
     c     X'90':'ø'     xlate     d_krol        d_krol
      *
     c                   eval      d_alfa = %trim(d_enav) + ' ' +
     c                                      %trim(d_fnav)
      *
      * sjekker mail adresse - må innehold @ og et punktum etter @
      *
     c                   if        d_mail <> *blank
     c                   eval      d_mail = %xlate(Lo:Up:d_mail)
     c                   eval      w_pos  = %scan('@':d_mail)
     c                   if        w_pos  = 0
     c                   eval      d_mail = *blank
     c                   else
     c                   eval      w_test = *blank
     c                   eval      w_test = %subst(d_mail:w_pos+1:50-w_pos)
     c                   eval      w_posb = %scan('.':w_test)
     c                   if        w_posb = 0
     c                   eval      d_mail = *blank
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        w_kpnr = 0
      * Hent sist brukte kontaktnr
     c                   eval      w_retk = ' '
     c                   call      'RS003R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    s_kpnr
     c                   eval      w_kpnr = s_kpnr
     c                   endif
      *
     c                   clear                   rukplur
     c                   eval      rukplu_kund = w_kund
     c                   eval      rukplu_levr = 0
     c                   eval      rukplu_kpnr = w_kpnr
     c     rukplu_key    chain     rukplu
     c                   eval      rualfa = %xlate(Lo:Up:d_alfa)
     c                   eval      rufnav = d_fnav
     c                   eval      ruenav = d_enav
     c                   eval      rukrol = %xlate(Lo:Up:d_krol)
     c                   if        %len(%trim(d_ktlf)) = 8
     c                   eval      ruktlf = %subst(d_ktlf:1:2) + ' ' +
     c                                      %subst(d_ktlf:3:2) + ' ' +
     c                                      %subst(d_ktlf:5:2) + ' ' +
     c                                      %subst(d_ktlf:7:2) + ' '
     c                   else
     c                   eval      ruktlf = d_ktlf
     c                   endif
     c                   if        %len(%trim(d_mtlf)) = 8
     c                   eval      rumtlf = %subst(d_mtlf:1:2) + ' ' +
     c                                      %subst(d_mtlf:3:2) + ' ' +
     c                                      %subst(d_mtlf:5:2) + ' ' +
     c                                      %subst(d_mtlf:7:2) + ' '
     c                   else
     c                   eval      rumtlf = d_mtlf
     c                   endif
     c                   eval      rumail = d_mail
     c                   eval      rueusr = l_user
     c                   time                    ruedat
     c                   time                    ruetim
     c                   if        %found(rukplu)
     c                   update    rukplur
     c                   else
     c                   eval      rufirm = w_firm
     c                   eval      rukund = rukplu_kund
     c                   eval      rulevr = rukplu_levr
     c                   eval      rukpnr = rukplu_kpnr
     c                   eval      rueusr = l_user
     c                   time                    ruedat
     c                   time                    ruetim
     c                   write     rukplur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av beløp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c                   eval      b_ok = *off
     c     '"':' '       xlate     exclin        exclin
      *
      * kundenr
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_kund = %trimr(%subst(exclin:1:w_posi-1))
      *
     c                   if        %check(digits : d_kund) > 0
     c                   leavesr
     c                   endif
     c                   if        d_kund = *blank
     c                   leavesr
     c                   endif
     c                   eval      w_kund = %dec(d_kund:6:0)
      *
      * kontaktpersonnr.
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_kpnr = %trim(%subst(exclin:1:w_posi-1))
     c                   if        %check(digits : d_kpnr) > 0
     c                   leavesr
     c                   endif
     c                   if        d_kpnr = *Blank
     c                   eval      w_kpnr = 0
     c                   else
     c                   eval      w_kpnr = %dec(d_kpnr:6:0)
     c                   endif
      *
      * Etternavn
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_enav = %trim(%subst(exclin:1:w_posi-1))
      *
      * Fornavn
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_fnav = %trim(%subst(exclin:1:w_posi-1))
      *
      * rolle
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_krol = %trim(%subst(exclin:1:w_posi-1))
      *
      * kontor tlf.
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_ktlf = %trim(%subst(exclin:1:w_posi-1))
      *
      * mobil tlf
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_mtlf = %trim(%subst(exclin:1:w_posi-1))
      *
      * Mail
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   if        w_posi > 0
     c                   eval      d_mail = %trim(%subst(exclin:1:w_posi-1))
     c                   else
     c                   eval      d_mail = %trim(%subst(exclin:1:50))
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppdatering av kontaktperson register
      *
     c     rukplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukplu_kund
     c                   kfld                    rukplu_levr
     c                   kfld                    rukplu_kpnr
      *
     c                   time                    w_udat
     c                   eval      w_firm = l_firm
      *
     c                   endsr
