     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO595R
      * Beskrivelse . . : Ordre-hode, Kjørekontor bilde
      *
      * Spesialversjon. : Sortere på lev.dato og kjørerute
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       171110 bof  Nytt program
6.21  *       120511 bof  Rettet feil vedr. kundeprosjekt og mob.nr
6.22  *       030516 bsa  Feil i sjekk når leveringsdato nulles
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.31  * 6.30  120416 bof  feilretting parm mot FU900r
6.32  * 6.30  100516 bsa  F4 mulighet kjørerute.
7.xx  * 7.00  310316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       060613 bof  Sortere på lev.dato og kjørerute
7.02  *       130613 bof  Vise ordrtype og rutenr i bildeskift P/PI rødt
      *
8.01  *K000   140122 bsa  Utvidet til bredt skjermbilde.
8.02  *K000   120922 bsa  Hvis forskjell på varekunde og fakturakunde, blir
8.02  *K000               det feil i visning av kunde/prosjekt.
8.02  *K000               Fortsetter endring 8.02. Forutsetter at det er faktura-
8.02  *K000               kunde på varekunden, og ikke valgt kundeprosjekt.
8.03  *K000   120922 bsa  Fortsetter endring 8.02. Forutsetter at det er faktura-
8.03  *K000               kunde på varekunden, og ikke valgt kundeprosjekt.
8.04  * 8.xx  140222 bsa  Utvidet lengde på B1BYT3 i skjermfila
8.05  * 8.xx  140222 bsa  Kan ikke ha default mappe fra "BM Handel"
8.06  * 8.xx  050922 bsa  Vasker innhold i enkelte felter.
8.07  *K000   030123 bsa  Hvis vi kjører med ekstern flåtestyring, kjør
8.07  *K000               request til API for opprettelse av transport.
8.07  *K000               Flåtestyring på lagernivå.
8.08  *K000   180123 bsa  Flagg om skaffetekst i transportlogg.
8.09  *K000   060223 bsa  Flagg om sjekk på loggkode i transportlogging
8.10  *K000   030523 bsa  Feil ved håndtering av mobilnummer
8.11  *K000   040723 bsa  Feil felt legges i kjørerute.
8.12  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelb    if   e           k disk    rename(fohepfr:fohelbr)
     ffoheln    if   e           k disk    rename(fohepfr:fohelnr)
7.01 ffohelx    if   e           k disk    rename(fohepfr:fohelxr)
     ffohela    if   e           k disk    rename(fohepfr:fohelar)
5.60 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
     fra25l1    if   e           k disk    rename(ra25pfr:ra25l1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
5.62 fausrlr    if   e           k disk    rename(ausrpfr:ausrlrr)
5.54 ffloglu    uf a e           k disk    rename(flogpfr:floglur)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
8.03 favall1    if   e           k disk    rename(avalpfr:avall1r)
8.07 ffloglr    if   e           k disk    rename(flogpfr:floglrr)
      *
     ffo595d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
8.05 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
5.54  *
5.54  * Definering av datastruktur
5.54  *
5.54 d                 ds
5.54 d d_urec                        80
5.54 d  d_firm                        3  0 overlay(d_urec)
5.60 d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_numm                        8  0 overlay(d_urec:8)
6.nu d  d_suff                        2  0 overlay(d_urec:16)
6.31 d  d_kodb                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
      *
      * Definering av variabler i nøkler
      *
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohelb_kjor     s                   like(fokjor)
     d fohelb_kjnr     s                   like(fokjnr)
     d foheln_ldat     s                   like(foldat)
     d foheln_otyp     s                   like(footyp)
     d foheln_kjor     s                   like(fokjor)
     d foheln_kjnr     s                   like(fokjnr)
     d fohela_kuna     s                   like(fonavn)
7.01 d fohelx_ldat     s                   like(foldat)
7.01 d fohelx_kjor     s                   like(fokjor)
7.01 d fohelx_kjnr     s                   like(fokjnr)
7.01 d fohelx_kund     s                   like(fokund)
     d fusrl1_user     s                   like(fbuser)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d rkunl1_kund     s                   like(rkkund)
     d ra17l1_qkod     s                   like(raqkod)
     d ra25l1_yrut     s                   like(rayrut)
     d votyl1_otyp     s                   like(vaotyp)
     d vinfl1_ityp     s                   like(vaityp)
5.54 d floglu_aarr     s                   like(fuaarr)
5.54 d floglu_numm     s                   like(funumm)
5.54 d floglu_suff     s                   like(fusuff)
5.54 d floglu_kode     s                   like(fukode)
5.54 d floglu_date     s                   like(fudate)
5.54 d floglu_time     s                   like(futime)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
5.62 d ausrlr_user     s                   like(abuser)
5.62 d fodtl1_numm     s                   like(fdnumm)
5.62 d fodtl1_suff     s                   like(fdsuff)
8.03 d avall1_apgm     s                   like(avapgm)
8.03 d avall1_aflt     s                   like(avaflt)
8.07 d floglr_aarr     s                   like(fuaarr)
8.07 d floglr_numm     s                   like(funumm)
8.07 d floglr_suff     s                   like(fusuff)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_oppt          s              1    inz(*off)
     d b_avbr          s              1    inz(*off)
5.62 d b_funn          s              1    inz(*off)
8.07 d b_trpl          s              1    inz(*off)
      *
8.03 d u_navn          s              1    inz(*off)
      *
     d w_retu          s              1
     d w_plti_6        s              6  0
      *
     d w_firm          s                   like(fofirm)
     d w_ynav          s                   like(raytxt)
5.54 d w_numm          s                   like(fonumm)
5.60 d w_aarr          s              4  0
5.54 d w_suff          s                   like(fosuff)
5.54 d w_svar          s              1
5.54 d w_kode          s              1
5.54 d w_stat          s              1
5.54 d w_date          s               d   datfmt(*iso)
5.54 d w_time          s               t   timfmt(*iso)
5.54 d w_user          s             10
5.54 d w_wsid          s             10
5.54 d w_kod2          s              1
5.54 d w_kund          s                   like(fokund)
5.54 d w_knav          s             30
5.54 d w_ltxt          s             30
7.01 d w_retk          s              1
7.01 d w_filn          s             12
8.06 d w_len           s              3  0
8.06 d w_teller        s              3  0
8.07 d w_reqs          s              5
8.07 d w_dato          s               d   datfmt(*iso)
8.08 d w_skaf          s              1  0
8.09 d w_logg          s              1  0
5.54  *
5.54  * Definering av save-variable
5.54  *
5.54 d s_date          s               d   datfmt(*iso)
5.54 d s_time          s               t   timfmt(*iso)
      *
      * Definering av konstanter
      *
8.01 d c_sfil          s              2  0 inz(17)
8.07 d u_807           s              1    inz(*off)
      *
8.07   dcl-s co402_verdi1  char(1);
8.07   dcl-s co402_verdi2  char(2048);
8.07   DCL-PR CO402R EXTPGM('CO402R');
8.07     p_filgrp            char(3)     const;
8.07     p_firm              packed(3:0) const;
8.07     p_lager             packed(2:0) const;
8.07     p_nokkel            char(50)    const;
8.07     p_verdi1            char(1);
8.07     p_verdi2            char(2048);
8.07   END-PR;
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
5.55 c                   when      *inka
5.55 c                   exsr      blaalt
5.55 c                   goto      b2tagb
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xl1win
     c                   goto      b2tagb
8.02 c*                  when      *inkx
8.02 c*                  if        *in51 = *off
8.02 c*                  eval      *in51 = *on
8.02 c*                  else
8.02 c*                  eval      *in51 = *off
8.02 c*                  endif
8.02 c*                  exsr      forny
8.02 c*                  goto      b2tagb
8.02 c*                  goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldat <> 0 or
     c                             b2kjor <> 0 or
     c                             b2numm <> 0 or
     c                             b2kuna <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
5.55  *
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  * F1 Behandle bilde for bla-alternativer
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  *
5.55 c     blaalt        begsr
5.55  *
5.55  *  Send Alt
5.55  *
5.55 c     xf1tag        tag
5.55 c                   write     f1win
5.55 c                   exfmt     f1win
5.55  *
5.55  *  F12=Avslutt subrutinen
5.55  *
5.55 c                   if        *inkc or *inkl
5.55 c                   goto      xf1end
5.55 c                   endif
      *
     c                   eval      b2lage = 0
     c                   eval      b2ko02 = *blank
     c                   eval      b2ko03 = *blank
     c                   eval      b2ko04 = *blank
     c                   eval      b2ko05 = *blank
     c                   eval      b2ko06 = *blank
     c                   if        f1ko02 = 1
     c                   eval      b2ko02 = '2'
     c                   endif
     c                   if        f1ko03 = 1
     c                   eval      b2ko03 = '3'
     c                   endif
     c                   if        f1ko04 = 1
     c                   eval      b2ko04 = '4'
     c                   endif
     c                   if        f1ko05 = 1
     c                   eval      b2ko05 = '5'
     c                   endif
     c                   if        f1ko06 = 1
     c                   eval      b2ko06 = '5'
     c                   endif
     c                   if        f1lage <> 0
     c                   eval      b2lage = f1lage
     c                   endif
5.55  *
5.55  * Posisjonerer databasen, og fyll opp subfilen pånytt
5.55  *
5.55 c                   eval      foheln_ldat = *loval
5.55 c                   eval      foheln_otyp = *blank
5.55 c                   eval      foheln_kjor = 0
5.55 c                   eval      foheln_kjnr = 0
5.55 c     foheln_key    setll     foheln
5.55  *
5.55 c                   exsr      clr_subfile
5.55 c                   exsr      crt_subfile
5.55  *
5.55 c     xf1end        endsr
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  * F6 Utskrift
5.55  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.55  *
5.55 c     xl1win        begsr
5.55  *
5.55  *  Send Alt
      *
     c                   eval      l1kjor = 0
     c                   eval      l1lage = 0
     c                   eval      l1valg = 0
7.01 c                   eval      l1excl = 0
8.05 c**                 eval      l1mapp = 'N50/STEINKJ'
8.05 c                   eval      l1mapp = l_filg
     c                   time                    w_date
     c     *dmy          move      w_date        l1ldat
5.55  *
5.55 c     l1taga        tag
5.55 c                   write     l1win
5.55 c                   exfmt     l1win
5.55  *
5.55  *  F12=Avslutt subrutinen
5.55  *
5.55 c                   if        *inkc or *inkl
5.55 c                   goto      xl1end
5.55 c                   endif
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      l1taga
     c                   endif
      *
      * lager
     c                   if        l1lage <> 0
6.20 c                   call      'RS710R'
6.20 c                   parm                    w_retu
6.20 c                   parm                    l1lage
6.20 c                   parm                    w_ltxt
6.20 c                   if        w_retu <> *blank
6.20 c                   eval      *in33 = *on
6.20 c                   goto      l1taga
     c                   endif
     c                   endif
      *
     c                   if        l1kjor <> 0
      * kjørerute
     c                   eval      ra25l1_yrut = l1kjor
     c     ra25l1_key    chain     ra25l1
     c                   if        not %found(ra25l1)
     c                   eval      *in32 = *on
     c                   goto      l1taga
     c                   endif
     c                   endif
      *
      * leveringsdato
     c                   if        l1ldat <> 0
     c     *dmy          test(d)                 l1ldat                 31
     c                   if        *in31 = *on
     c                   goto      l1taga
     c                   endif
     c                   endif
      *
6.22 c                   if        l1ldat <> *loval and
6.22 c                             l1ldat <> 0
     c     *dmy          move      l1ldat        w_date
     c                   else
     c                   eval      w_date = *loval
     c                   endif
7.01  *
7.01  * mappe
7.01  *
7.01 c                   if        l1excl = 1  and
7.01 c                             l1mapp = *blank
7.01 c                   eval      *in34 = *on
7.01 c                   goto      l1taga
7.01 c                   endif
      *
7.01  * danne filnavn
7.01  *
7.01 c                   eval      w_filn = 'KJ' +
7.01 c                                      %char(l1ldat) +
7.01 c                                      '.xls'
7.01  *
      *
     c                   if        l1valg = 0
     c                   call      'FO656C '
     c                   parm                    w_firm
     c                   parm                    l1lage
     c                   parm                    l1kjor
     c                   parm                    w_date
7.01 c                   parm                    l1excl
7.01 c                   parm                    l1mapp
7.01 c                   parm                    w_filn
7.01 c                   parm                    w_retk
     c                   else
      *
5.54 c                   eval      w_numm = b1numm
5.54 c                   eval      w_suff = b1suff
     c                   call      'FO658C'
     c                   parm                    w_firm
     c                   parm                    l1lage
     c                   parm                    l1kjor
     c                   parm                    w_date
7.01 c                   parm                    l1excl
7.01 c                   parm                    l1mapp
7.01 c                   parm                    w_filn
7.01 c                   parm                    w_retk
      *
     c                   endif
5.55  *
5.55 c     xl1end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = b1kjor
     c                   eval      fohelb_kjnr = b1kjnr
     c     fohelb_key    setll     fohelb
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    setll     fohel1
     c                   when      w_seqe = 'K'
     c                   eval      fohela_kuna = b1kuna
     c     fohela_key    setll     fohela
7.01 c                   when      w_seqe = 'X'
7.01 c                   if        b1ldat <> 0
7.01 c                   eval      fohelx_ldat = %date(b1ldat:*dmy)
7.01 c                   else
7.01 c                   eval      fohelx_ldat = *loval
7.01 c                   endif
7.01 c                   eval      fohelx_kjor = b1kjor
7.01 c     fohelx_key    setll     fohelx
     c                   other
     c                   if        b1ldat <> 0
     c                   eval      foheln_ldat = %date(b1ldat:*dmy)
     c                   else
     c                   eval      foheln_ldat = *loval
     c                   endif
     c                   eval      foheln_otyp = b1otyp
     c                   eval      foheln_kjor = b1kjor
     c                   eval      foheln_kjnr = b1kjnr
     c     foheln_key    setll     foheln
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
7.01  *
7.01  * Leveringsdato + Kjørerute
7.01  *
7.01 c                   if        b2ldat <> 0 and
7.01 c                             b2kjor <> 0
7.01 c                   eval      w_seqe = 'X'
7.01 c                   eval      fohelx_kjor = b2kjor
7.01 c                   eval      fohelx_ldat = *loval
7.01 c     *dmy          test(d)                 b2ldat                 31
7.01 c                   if        *in31 = *off
7.01 c     *dmy          move      b2ldat        fohelx_ldat
7.01 c                   endif
7.01 c     fohelx_key    setll     fohelx
7.01 c                   goto      end_pos
7.01 c                   endif
      *
      *
      * Leveringsdato
      *
     c                   if        b2ldat <> 0
     c                   eval      w_seqe = *blank
     c                   eval      foheln_ldat = *loval
     c     *dmy          test(d)                 b2ldat                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2ldat        foheln_ldat
     c                   endif
      * her er den lagt blank så lenge
     c                   eval      foheln_otyp = *blank
     c                   eval      foheln_kjor = b2kjor
     c                   eval      foheln_kjnr = 0
     c     foheln_key    setll     foheln
     c                   goto      end_pos
     c                   endif
      *
      * Kjørerute
      *
     c                   if        b2kjor <> 0
     c                   eval      w_seqe = 'R'
     c                   eval      fohelb_kjor = b2kjor
     c                   eval      fohelb_kjnr = 0
     c     fohelb_key    setll     fohelb
     c                   goto      end_pos
     c                   endif
      *
      * Ordrenr
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      fohel1_numm = b2numm
     c                   eval      fohel1_suff = 0
     c     fohel1_key    setll     fohel1
     c                   goto      end_pos
     c                   endif
      *
      * Navn
      *
     c                   if        b2kuna <> *blank
     c                   eval      w_seqe = 'K'
     c                   eval      fohela_kuna = b2kuna
     c     fohela_key    setll     fohela
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldat = 0
     c                   eval      b2kjor = 0
     c                   eval      b2numm = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre ordreinformasjon
      *
     c                   if        b1valg = 2
     c                   eval      c2numm = b1numm
     c                   eval      c2suff = b1suff
     c                   exsr      xc2win
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis ordre
      *
     c                   if        b1valg = 5
     c                   call      'FO505R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv plukkliste
      *
     c                   if        b1valg = 6
     c                   exsr      utskrift
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Hvem behandler ordre
      *
     c                   if        b1valg = 8
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
5.54  *
5.54  * Viser loggfilen med ordrehistorikk
5.54  *
5.54 c                   if        b1valg = 60
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54 c                   eval      w_numm = b1numm
5.54 c                   eval      w_suff = b1suff
5.54 c                   call      'FU500R'
5.54 c                   parm                    w_firm
5.60 c                   parm                    w_aarr
6.nu c                   parm                    w_numm
5.54 c                   parm                    w_suff
5.54 c                   eval      b1valg = 0
5.54  ***                exsr      xfarge
5.54 c                   update    b1sfl
5.54 c                   iter
5.54 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14  = *on
     c                   write     b2ctl
     c                   eval      *in14  = *off
      *
     c                   eval      *in15  = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   read      fohelb
     c                   when      w_seqe = 'O'
     c                   read      fohel1
     c                   when      w_seqe = 'K'
     c                   read      fohela
7.01 c                   when      w_seqe = 'X'
7.01 c                   read      fohelx
     c                   other
     c                   read      foheln
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *off
     c                   iter
     c                   endif
      *
      * Fyll opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = fokjor
     c                   eval      fohelb_kjnr = fokjnr
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   when      w_seqe = 'K'
     c                   eval      fohela_kuna = fokuna
7.01 c                   when      w_seqe = 'X'
7.01 c                   eval      fohelx_ldat = foldat
7.01 c                   eval      fohelx_kjor = fokjor
     c                   other
     c                   eval      foheln_ldat = foldat
     c                   eval      foheln_otyp = footyp
     c                   eval      foheln_kjor = fokjor
     c                   eval      foheln_kjnr = fokjnr
     c                   endsl
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   movel     rknavn        b1knav
8.02  *
8.02  * Hvis varekunde, fakturakunde og prosjekt, legg ut varekunde
8.02  *
8.02 c                   if        fokund <> 0 and
8.02 c                             folkun <> 0 and
8.02 c                             fokund <> folkun and
8.03 c                             fokpro <> 0 or
8.03 c                             u_navn = *on and
8.03 c                             folkun <> 0
8.02 c                   eval      rkunl1_kund = folkun
8.02 c     rkunl1_key    chain     rkunl1r
8.02 c                   if        %found
8.02 c                   movel     rknavn        b1knav
8.02 c                   endif
8.02 c                   endif
      *
6.21 c*                  if        fokpro <> 0
6.21 c*                  eval      fkprl1_kund = fokund
6.21 c*                  eval      fkprl1_kpro = fokpro
6.21 c*    fkprl1_key    chain     fkprl1r
6.21 c*                  if        %found(fkprl1)
6.21 c*                  movel     flnavn        b1knav
6.21 c*                  endif
6.21 c*                  endif
      *
8.10 c                   eval      b1mobi = *blank
8.10 c**                 movel     fomobi        b1mobi
8.10 c                   eval      b1mobi = %trim(fomobi)
      *
     c                   if        fomobi = *blank
     c                   if        fltlfm<> *blank
     c                   movel     fltlfm        b1mobi
     c                   else
     c                   movel     rkmobn        b1mobi
     c                   endif
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldat = 0
     c                   eval      b1otyp = footyp
     c                   eval      b1kuna = fokuna
     c*                  eval      b1plda = foplda
     c                   eval      b1kjor = fokjor
     c                   eval      b1kjnr = fokjnr
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1kund = fokund
     c                   eval      b1kpro = fokpro
     c                   if        foadr1 <> *blank
     c                   movel     foadr1        b1lnav
     c                   else
     c                   movel     fonavn        b1lnav
     c                   endif
     c                   eval      b1selg = foselg
     c                   if        fomerk = 1
     c                   eval      b1merk = 'X'
     c                   else
     c                   eval      b1merk = *blank
     c                   endif
     c*                  eval      b1plda = 0
     c*                  eval      b1plti = 0
     c                   eval      b1ityp = foityp
     c                   eval      b1kode = *blank
5.60 c                   eval      b1bdat = fobdat
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        b1ldat
     c                   endif
      *
     c*                  if        s_date <> *loval
     c*    *dmy          move      s_date        b1plda
     c*                  endif
     c*                  if        s_time <> *loval
     c*    *hms          move      s_time        w_plti_6
     c*                  movel     w_plti_6      b1plti
     c*                  endif
5.54  * Henter inn høyeste ordrestatus
5.54  * og legger over alt i en streng
5.54  *
5.54 c                   eval      w_kod2 = *blank
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54 c                   call      'FU706R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    b1numm
5.54 c                   parm                    b1suff
5.54 c                   parm                    b1kod2
      *
8.02 c**                 eval      b1bytt = *blank
     c                   eval      b1byt2 = *blank
7.02 c                   eval      b1byt3 = *blank
7.02  *
7.02 c                   eval      *in50  = *off
7.02 c                   if        b1otyp = 'P' or
7.02 c                             b1otyp = 'PI'
7.02 c                   eval      *in50  = *on
7.02 c                   endif
      *
      *
8.02 c**                 if        *in51  = *off
8.02 c**                 movel     fodref        b1bytt
8.02 c                   movel     fodref        b1kref
8.06 c                   exsr      newlo_vask
     c                   eval      b1byt2 = %editc(b1kjor : 'Z')
8.02 c**                 movel     b1otyp        b1byt3
8.02 c**                 movel     b1otyp        b1byt2
8.02 c                   eval      b1byt3 = b1kod2+'  '+b1otyp
8.02 c**                 else
8.02 c**                 movel     b1mobi        b1bytt
8.02 c**                 move      b1kod2        b1byt2
8.02 c**                 move      b1kod2        b1byt3
8.11 c**                 eval      b1byt2 = %editc(b1kjnr : 'Z')
8.02 c**                 endif
5.54  *
      *
3.34 c                   eval      *in41  = *off
3.34 c                   eval      *in42  = *off
3.34 c                   eval      *in43  = *off
3.34 c                   eval      *in44  = *off
      *
     c                   select
3.34 c                   when      vaifrg = 'H'
3.34 c                   eval      *in41  = *on
3.34 c                   when      vaifrg = 'G'
3.34 c                   eval      *in42  = *on
3.34 c                   when      vaifrg = 'B'
3.34 c                   eval      *in43  = *on
3.34 c                   when      vaifrg = 'R'
3.34 c                   eval      *in44  = *on
3.34 c                   endsl
3.34  *
     c*                  select
3.12 c*                  when      fokode = 1
3.12 c*                  eval      b1kode = '**'
3.12 c*                  when      fokode = 2
3.12 c*                  eval      b1kode = 'UT'
3.12 c*                  when      fokode = 3
3.12 c*                  eval      b1kode = 'OK'
3.12 c*                  when      fokode = 5
3.12 c*                  eval      b1kode = 'VE'
3.12 c*                  when      fokode = 7
3.12 c*                  eval      b1kode = 'BU'
3.12 c*                  endsl
5.54  *
5.54  *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'R'
     c     fohelb_key    setgt     fohelb
     c                   readp     fohelb
     c                   when      w_seqe = 'O'
     c     fohel1_key    setgt     fohel1
     c                   readp     fohel1
     c                   when      w_seqe = 'K'
     c     fohela_key    setgt     fohela
     c                   readp     fohela
7.01 c                   when      w_seqe = 'X'
7.01 c     fohelx_key    setgt     fohelx
7.01 c                   readp     fohelx
     c                   other
     c     foheln_key    setgt     foheln
     c                   readp     foheln
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   readp     fohelb
     c                   when      w_seqe = 'O'
     c                   readp     fohel1
     c                   when      w_seqe = 'K'
     c                   readp     fohela
7.01 c                   when      w_seqe = 'X'
7.01 c                   readp     fohelx
     c                   other
     c                   readp     foheln
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   eval      fohelb_kjor = fokjor
     c                   eval      fohelb_kjnr = fokjnr
     c                   when      w_seqe = 'O'
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   when      w_seqe = 'K'
     c                   eval      fohela_kuna = fokuna
7.01 c                   when      w_seqe = 'X'
7.01 c                   eval      fohelx_ldat = foldat
7.01 c                   eval      fohelx_kjor = fokjor
     c                   other
     c                   eval      foheln_ldat = foldat
     c                   eval      foheln_otyp = footyp
     c                   eval      foheln_kjor = fokjor
     c                   eval      foheln_kjnr = fokjnr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'R'
     c     fohelb_key    setll     fohelb
     c                   when      w_seqe = 'O'
     c     fohel1_key    setll     fohel1
     c                   when      w_seqe = 'K'
     c     fohela_key    setll     fohela
7.01 c                   when      w_seqe = 'X'
7.01 c     fohelx_key    setll     fohelx
     c                   other
     c     foheln_key    setll     foheln
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om rad skal være med i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     chk_subfile   begsr
      *
     c                   eval      b_subf = *off
      *
      * Viser ikke ordre dersom det ikke kjøres med kjørekontorløsning
      *
     c                   if        faakjk = 0
     c                   leavesr
     c                   endif
      *
      * Viser kun ordre med leveringstype med kode for beh. i kjørekontor
      *
     c                   eval      ra17l1_qkod = folmat
     c     ra17l1_key    chain     ra17l1
     c                   if        not %found or
     c                             raqkjo <> 1
     c                   leavesr
     c                   endif
      *
      * Viser kun ordre med behandlingskode 1  og
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             (vaoakk <> 1 and
     c                             vaoakk <> 2)
     c                   leavesr
     c                   endif
5.62  *
5.62  * Sjekker lagerfilter
5.62  *
5.62 c                   if        f1lage <> 0
5.62 c                   exsr      xsjekk_lag
5.62 c                   if        b_funn = ' '
5.62 c                   leavesr
5.62 c                   endif
5.62 c                   endif
      *
5.54  *
5.54  * Skal bare vise ordre som er
      * - klar for plukking 2
      * - plukkliste kjørt  3
      * - under plukking    4
      * - ankommet lager    5
      * - ferdig plukket    6
      *
5.54  * Henter inn siste kode  og sjekker om
5.54  * koden er som over
5.54  *
5.54 c                   eval      w_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
5.54 c                   call      'FU706R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    fonumm
5.54 c                   parm                    fosuff
5.54 c                   parm                    w_kode
5.54  *
5.54 c                   if        w_kode = '2' or
5.55 c                             w_kode = '3' or
5.54 c                             w_kode = '4' or
5.54 c                             w_kode = '5' or
5.54 c                             w_kode = '6' or
5.54 c                             w_kode = '7'
5.54 c                   else
5.54 c                   leavesr
5.54 c                   endif
5.55  *
5.55 c                   if        w_kode = '2' and
5.55 c                             f1ko02 = 1
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   if        w_kode = '3' and
5.55 c                             f1ko03 = 1
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   if        w_kode = '4' and
5.55 c                             f1ko04 = 1
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   if        w_kode = '5' and
5.55 c                             f1ko05 = 1
5.55 c                   leavesr
5.55 c                   endif
5.55  *
5.55 c                   if        w_kode = '6' and
5.55 c                             f1ko06 = 1
5.55 c                   leavesr
5.55 c                   endif
5.54  *
5.54  * Henter ut dato og kl.
5.54  *
5.54 c*                  eval      s_date = *loval
5.54 c*                  eval      s_time = *loval
5.54 c*                  eval      w_kode = '2'
5.60 c*                  eval      w_aarr = %subdt(fobdat:*years)
5.54 c*                  call      'FU704R'
5.60 c*                  parm                    w_aarr
6.nu c*                  parm                    fonumm
5.54 c*                  parm                    fosuff
5.54 c*                  parm                    w_kode
5.54 c*                  parm                    w_svar
5.54 c*                  parm                    w_date
5.54 c*                  parm                    w_time
5.54 c*                  parm                    w_user
5.54 c*                  parm                    w_wsid
5.54  *
5.54 c*                  if        w_svar = 'J'
5.54  *
5.54 c*                  if        w_date <> *loval
5.54 c*                  eval      s_date = w_date
5.54 c*                  endif
5.54 c*                  if        w_time <> *loval
5.54 c*                  eval      s_time = w_time
5.54 c*                  endif
5.54 c*                  endif
      *
     c                   eval      b_subf = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12  = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13  = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12  = *off
     c                   eval      *in13  = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B2KJOR'
     c                   call      'RA525R'
     c                   parm                    w_firm
     c                   parm                    b2kjor
     c                   parm                    w_ynav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C2KJOR'
     c                   call      'RA525R'
     c                   parm                    w_firm
     c                   parm                    c2kjor
     c                   parm                    w_ynav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'L1KJOR'
     c                   call      'RA525R'
     c                   parm                    w_firm
     c                   parm                    l1kjor
     c                   parm                    w_ynav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'L1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    l1lage
     c                   parm                    w_ltxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2KUNA'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_knav
     c                   movel     w_knav        B2KUNA
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive plukkliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Dersom ordre er opptatt kalles program som viser hvem som bruker
      * orderen
      *
     c                   exsr      sjekk_ordre
     c                   if        b_oppt = *on
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   leavesr
     c                   endif
      *
      * Skriver kjøreliste
      *
     c                   call      'FO657C'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å avbryte plukking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avbr_plukking begsr
      *
      * Dersom ordre er opptatt kalles program som viser hvem som bruker
      * orderen
      *
     c                   exsr      sjekk_ordre
     c                   if        b_oppt = *on
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   leavesr
     c                   endif
      *
      * Håndterer skjermbilde
      *
     c                   exsr      xc2win
     c                   if        b_avbr = *off
     c                   leavesr
     c                   endif
      *
      * Oppdaterer ordre med blanket plukker-informasjon
      *
     c                   eval      fohelu_numm = b1numm
     c                   eval      fohelu_suff = b1suff
     c     fohelu_key    chain     fohelu
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      foselg = 0
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
     c                   eval      b_forn = *on
5.54  *
5.54  * Fjerner post som sier at ordren er under plukking
5.54  *
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54 c                   eval      floglu_aarr = w_aarr
5.54 c                   eval      floglu_numm = b1numm
5.54 c                   eval      floglu_suff = b1suff
5.54 c                   eval      floglu_kode = '4'
5.54 c                   eval      floglu_date = *hival
5.54 c                   eval      floglu_time = *hival
5.54 c     floglu_key    setll     floglur
5.54 c                   readp     floglur
5.54 c                   if        %eof
5.54 c                   goto      avbr_next
5.54 c                   endif
5.54  *
5.54 c                   if        funumm = b1numm and
5.54 c                             fusuff = b1suff and
5.54 c                             fukode = '4'
5.54 c                   delete    floglur
5.54 c                   endif
5.56  *
5.56  * Fjerner også post som sier at ordre er ferdig plukket
5.56  *
5.56 c     avbr_next     tag
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.56 c                   eval      floglu_aarr = w_aarr
5.56 c                   eval      floglu_numm = b1numm
5.56 c                   eval      floglu_suff = b1suff
5.56 c                   eval      floglu_kode = '6'
5.56 c                   eval      floglu_date = *hival
5.56 c                   eval      floglu_time = *hival
5.56 c     floglu_key    setll     floglur
5.56 c                   readp     floglur
5.56 c                   if        %eof
5.56 c                   leavesr
5.56 c                   endif
5.56  *
5.56 c                   if        funumm = b1numm and
5.56 c                             fusuff = b1suff and
5.56 c                             fukode = '6'
5.56 c                   delete    floglur
5.56 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å merke ordre "Ferdig plukket"
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     merk_plukking begsr
5.54  *
5.54  * Er ordre allerede merket "Ferdig plukket"
5.54  *
5.54 c                   eval      w_kode = '6'
5.60 c                   eval      w_aarr = %subdt(b1bdat:*years)
5.54  *
5.54 c                   call      'FU702R'
5.60 c                   parm                    w_aarr
6.nu c                   parm                    b1numm
5.54 c                   parm                    b1suff
5.54 c                   parm                    w_kode
5.54 c                   parm                    w_svar
5.54  *
5.54 c                   if        w_svar = 'J'
5.54 c                   leavesr
5.54 c                   endif
5.54  *
5.54  *
5.54  * Oppdaterer loggfilen med "Ferdig plukket"
5.54  *
5.54 c                   eval      d_firm = l_firm
5.54 c                   eval      d_user = l_user
5.54 c                   eval      d_wsid = l_wsid
5.60 c                   eval      d_aarr = %subdt(b1bdat:*years)
5.54 c                   eval      d_numm = b1numm
5.54 c                   eval      d_suff = b1suff
5.54 c                   eval      d_kodb = '6'
5.54 c                   time                    d_date
5.54 c                   time                    d_time
5.54  *
5.54  * Kaller program for oppdatering
5.54  *
5.54 c                   call      'FU900R'
5.54 c                   parm                    w_stat
5.54 c                   parm                    d_urec
5.54  *
      *
      * Oppdaterer ordre med ordre informasjonskode
      *
     c***>               eval      fohelu_numm = b1numm
     c***>               eval      fohelu_suff = b1suff
     c***> fohelu_key    chain     fohelu
     c***>               if        not %found
     c***>               leavesr
     c***>               endif
      *
     c***>               eval      foityp = vaityp
      *
     c***>               update    fohelur
      *
     c***>               eval      b_forn = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om ordren allerede er til behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ordre   begsr
      *
     c                   eval      b_oppt = *off
      *
      * Sjekker om ordren er i bruk
      *
     c                   eval      fohelr_numm = b1numm
     c                   eval      fohelr_suff = b1suff
     c     fohelr_key    chain     fohelr
     c                   if        %found and
     c                             fokode <> 0
     c                   eval      b_oppt = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2 Behandle bilde for endring av leveringsdato , kjørerute og rekkef.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
     c                   eval      b_avbr = *off
      *
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    chain     fohel1r
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2numm = fonumm
     c                   eval      c2suff = fosuff
     c                   eval      c2kuna = fokuna
8.03 c                   eval      c2lnav = foadr1
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      c2kuna = rknavn
     c                   endif
      *
     c                   if        fokpro <> 0
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1r
     c                   if        %found
     c                   eval      c2kuna = flnavn
     c                   endif
     c                   endif
8.02  *
8.02  * Hvis forskjell på varekunde og fakturakunde, velg varekunde
8.02  *
8.02 c                   if        fokund <> 0 and
8.02 c                             folkun <> 0 and
8.03 c                             fokund <> folkun or
8.03 c                             u_navn = *on and
8.03 c                             folkun <> 0
8.02  *
8.02 c                   eval      rkunl1_kund = folkun
8.02 c     rkunl1_key    chain     rkunl1r
8.02 c                   if        %found
8.02 c                   eval      c2kuna = rknavn
8.02 c                   endif
8.02  *
8.02 c                   if        fokpro <> 0
8.02 c                   eval      fkprl1_kund = folkun
8.02 c                   eval      fkprl1_kpro = fokpro
8.02 c     fkprl1_key    chain     fkprl1r
8.02 c                   if        %found
8.02 c                   eval      c2lnav = flnavn
8.02 c                   endif
8.02 c                   endif
8.02  *
8.02 c                   endif
      *
     c                   eval      c2kjor = fokjor
     c                   eval      ra25l1_yrut = fokjor
     c     ra25l1_key    chain     ra25l1
     c                   if        %found(ra25l1)
     c                   eval      c2ytxt = raytxt
     c                   else
     c                   eval      c2ytxt = *blank
     c                   endif
      *
     c                   eval      c2kjnr = fokjnr
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        c2ldat
     c                   else
     c                   eval      c2ldat = 0
     c                   endif
     c                   if        fomerk = 1
     c                   eval      c2merk = 'X'
     c                   else
     c                   eval      c2merk = *blank
     c                   endif
5.54  *
     c     c2taga        tag
      *
     c                   write     c2win
     c                   exfmt     c2win
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
6.32 c                   if        *inkd
6.32 c                   exsr      spØrring
6.32 c                   if        w_ynav <> *blanks
6.32 c                   eval      c2ytxt = w_ynav
6.32 c                   endif
6.32 c                   goto      c2taga
6.32 c                   endif
      *
      *
      *
      * Validering
      *
      * leveringsdato
      *
     c     *dmy          test(d)                 c2ldat                 31
     c                   if        *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * kjørerute
      *
     c                   if        c2kjor <> 0
     c                   eval      ra25l1_yrut = c2kjor
     c     ra25l1_key    chain     ra25l1
     c                   if        not %found(ra25l1)
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * oppdater
      *
     c                   eval      fohelu_numm = c2numm
     c                   eval      fohelu_suff = c2suff
     c     fohelu_key    chain     fohelur
     c                   if        %found(fohelu)
     c                   eval      fokjor = c2kjor
     c                   eval      fokjnr = c2kjnr
     c                   if        c2merk = 'X'
     c                   eval      fomerk = 1
     c                   else
     c                   eval      fomerk = 0
     c                   endif
     c     *dmy          move      c2ldat        foldat
     c                   update    fohelur
     c                   endif
8.07  *
8.07  * Hvis vi kjører med integrasjon til transportstyrer, må vi sende request.
8.07  * - sjekk at forsendelsesmåten har flåtestyring.
8.07  *
8.07 c                   if        u_807 = *on
8.07  * Må ha loggkode allerede for at den skal sendes. Primært brukes dette
8.07  * for opppdatering av feks leveringsdato.
8.07 c                   eval      b_trpl = *off
8.07 c                   eval      w_aarr = %subdt(w_dato:*years)
8.07 c                   eval      floglr_aarr = w_aarr
8.07 c                   eval      floglr_numm = fonumm
8.07 c                   eval      floglr_suff = fosuff
8.07 c     floglr_key    setll     floglr
8.07 c     floglr_key    reade     floglr
8.07 c                   dow       not %eof
8.07 c                   if        fukode = '2' or
8.07 c                             fukode = '4' or
8.07 c                             fukode = '6'
8.07 c                   eval      b_trpl = *on
8.07 c                   endif
8.07 c     floglr_key    reade     floglr
8.07 c                   enddo
8.07  *
8.07  * Sjekk på forrige år, hvis ikke ordren er plukket. Året følger
8.07  * første hendelse på ordren.
8.07  *
8.07 c                   if        b_trpl = *off
8.07  *
8.07 c                   eval      floglr_aarr = w_aarr - 1
8.07 c     floglr_key    setll     floglr
8.07 c     floglr_key    reade     floglr
8.07 c                   dow       not %eof
8.07 c                   if        fukode = '2' or
8.07 c                             fukode = '4' or
8.07 c                             fukode = '6'
8.07 c                   eval      b_trpl = *on
8.07 c                   endif
8.07 c     floglr_key    reade     floglr
8.07 c                   enddo
8.07  *
8.07 c                   endif
8.07  * Kjør logging
8.07 c                   if        b_trpl = *on
8.07  *
8.07 c                   eval      w_reqs = *blanks
8.08 c                   eval      w_skaf = 0
8.09 c                   eval      w_logg = 0
8.07 c                   call      'AP058R '
8.07 c                   parm                    fofirm
8.07 c                   parm                    fonumm
8.07 c                   parm                    fosuff
8.08 c                   parm                    w_skaf
8.09 c                   parm                    w_logg
8.07 c                   parm                    w_reqs
8.07  *
8.07 c                   endif
8.07  *
8.07 c                   endif
8.07  *
      *
     c                   if        b1valg = 2
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        b1ldat
     c                   else
     c                   eval      b1ldat = 0
     c                   endif
     c                   if        fomerk = 1
     c                   eval      b1merk = 'X'
     c                   else
     c                   eval      b1merk = *blank
     c                   endif
     c                   eval      b1kjor = fokjor
     c                   endif
      *
     c                   eval      c2ldat = 0
     c                   eval      c2kjor = 0
     c                   eval      c2merk = *blank
      *
     c                   endsr
5.62  *
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  * Sjekker om lager finnes på ordrelinje
5.62  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  *
5.62 c     xsjekk_lag    begsr
5.62  *
5.62  * Sjekker om oppgitt lagernummer er representert på
5.62  * en av varelinjene på ordren
5.62  *
5.62 c                   eval      b_funn = ' '
5.62 c                   eval      fodtl1_numm = fonumm
5.62 c                   eval      fodtl1_suff = fosuff
5.62 c     fodtl1_key    setll     fodtl1
5.62 c     fodtl1_key    reade     fodtl1
5.62  *
5.62 c                   dow       not %eof
5.62 c                   if        fdlage = f1lage
5.62 c                   eval      b_funn = '1'
5.62 c                   leave
5.62 c                   endif
5.62 c     fodtl1_key    reade     fodtl1
5.62 c                   enddo
5.62  *
5.62 c                   endsr
      *
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  * Newlook vask av data
8.06  *  Fjerner tegn som gjør at Newlook feiltolker
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  *
8.06  /FREE
8.06
8.06   begsr newlo_vask;
8.06     for w_teller = 1 to 5 ;
8.06       w_len = %len(%trim(b1kref));
8.06       if (w_len > 0);
8.06       b1kref = %ScanRpl('.':'':b1kref:w_len); // newlook tåler ikke . på slutten
8.06       endif;
8.06     endfor ;
8.06   endsr;
8.06
8.06  /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på ordre-hode
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
      *
      * Key for posisjonering på ordrenr
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for posisjonering på kjørerute
      *
     c     fohelb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelb_kjor
     c                   kfld                    fohelb_kjnr
      *
      * Key for posisjonering på leveringsdato
      *
     c     foheln_key    klist
     c                   kfld                    w_firm
     c                   kfld                    foheln_ldat
     c                   kfld                    foheln_otyp
     c                   kfld                    foheln_kjor
     c                   kfld                    foheln_kjnr
      *
      * Key for posisjonering på plukkliste selgernr
      *
     c     fohela_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohela_kuna
      *
7.01  *
7.01  * Key for posisjonering på leveringsdato + kjørerute
7.01  *
7.01 c     fohelx_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    fohelx_ldat
7.01 c                   kfld                    fohelx_kjor
7.01  *
7.01  * Key for posisjonering på plukkliste selgernr
7.01  *
7.01 c**   fohela_key    klist
7.01 c**                 kfld                    w_firm
7.01 c**                 kfld                    fohela_kuna
7.01  *
      *
      * Key for oppslag på ofak kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på ofak-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde-prosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag forsendelsesmåte
      *
     c     ra17l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra17l1_qkod
      *
      * Key for oppslag kjøreruter
      *
     c     ra25l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra25l1_yrut
      *
      * Key for oppslag på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre informasjonskode
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
5.54  *
5.54  * Key for oppdatering av logg-filen
5.54  *
5.54 c     floglu_key    klist
5.54 c                   kfld                    w_firm
5.54 c                   kfld                    floglu_aarr
5.54 c                   kfld                    floglu_numm
5.54 c                   kfld                    floglu_suff
5.54 c                   kfld                    floglu_kode
5.54 c                   kfld                    floglu_date
5.54 c                   kfld                    floglu_time
5.55  *
5.55  * Key for les av bruker
5.62  *
5.62 c     ausrlr_key    klist
5.62 c                   kfld                    ausrlr_user
5.62  *
5.62  * Key for ordrelinje-register
5.62  *
5.62 c     fodtl1_key    klist
5.62 c                   kfld                    w_firm
5.62 c                   kfld                    fodtl1_numm
5.62 c                   kfld                    fodtl1_suff
8.03  *
8.03  * Key for file : Firmastyrt validering
8.03  *
8.03 c     avall1_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    avall1_apgm
8.03 c                   kfld                    avall1_aflt
8.07  *
8.07  * Key for oppslag på loggfil
8.07  *
8.07 c     floglr_key    klist
8.07 c                   kfld                    w_firm
8.07 c                   kfld                    floglr_aarr
8.07 c                   kfld                    floglr_numm
8.07 c                   kfld                    floglr_suff
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * hente lager fra bruker
      *
     c                   eval      ausrlr_user = l_user
     c     ausrlr_key    chain     ausrlr
     c                   if        %found(ausrlr)
     c                   eval      f1lage = ablagr
     c                   endif
      *
      * Hente inn ofak kode-register
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Henter opplysninger fra bruker
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22  = *on
     c                   eval      b2ko02 = *blank
     c                   eval      b2ko03 = *blank
     c                   eval      b2ko04 = *blank
     c                   eval      b2ko05 = *blank
     c                   eval      b2ko06 = *blank
8.03  *
8.03  * Firmastyrt validering av utvalgte felter
8.03  *
8.03 c                   eval      avall1_apgm = 'FO100R'
8.03 c                   eval      avall1_aflt = 'FONAVN'
8.03 c     avall1_key    chain     avall1
8.03 c                   if        %found
8.03 c                   if        avaval = 1
8.03 c                   eval      u_navn = *on
8.03 c                   endif
8.03 c                   endif
8.07  *
8.07 c                   time                    w_dato
8.07  *
8.07  * Endring 8.07 - Koblet til ekstern flåtestyring
8.07  *
8.07   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.07                    co402_verdi1:co402_verdi2);
8.07   if co402_verdi1 = '1';
8.07     u_807 = *on;
8.07   endif;
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      foheln_ldat = *loval
     c                   eval      foheln_otyp = *blank
     c                   eval      foheln_kjor = 0
     c                   eval      foheln_kjnr = 0
     c     foheln_key    setll     foheln
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
