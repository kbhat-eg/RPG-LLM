     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPLU
      * Program . . . . : st121R
      * Beskrivelse . . : Kundetilpass konfig
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090819 BKV  Nytt program
7.01  *K0000  160920 bsa  Må tillate både store og små bokstaver i nøkkel.
7.02  *K0000  070122 sst  Skrevet om til nytt register,  SKTIST
7.03  *K0000  090522 sst  Delt opp verdi 2 i 2 omganger i bildet ved F8
      *                   Dvs feltet i register er mye lenger enn i bildet.
      *                   Vedlikeholder derfor først col 1-448 så 449-896
      *                   Resten blir foreløoig ikke vedlikeholdt.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       41 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
       //dcl-f sktist keyed RENAME(sktist:sktistR);
      *
     fsktist    uf a e           k disk    rename(sktist:sktistr)
     fsktiiu    uf a e           k disk    rename(sktist:sktiiur)
     fsktiir    if   e           k disk    rename(sktist:sktiirr)
     fsktii1    if   e           k disk    rename(sktist:sktii1r)
     fsktii2    if   e           k disk    rename(sktist:sktii2r)
     fsktii3    if   e           k disk    rename(sktist:sktii3r)
     fsktii4    if   e           k disk    rename(sktist:sktii4r)
     fat121d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering tabell DS
      *
7.02   //dcl-ds Data extname('sktist') qualified ;
       //end-ds ;
      *
      * Definering av variabler i nøkler
      *
7.02 d  sktii1_filg    s                   like(aktfgp)
7.02 d  sktii1_nokl    s                   like(aktnok)
7.02  *
7.02 d  sktii2_firm    s                   like(aktfir)
7.02 d  sktii2_nokl    s                   like(aktnok)
7.02  *
7.02 d  sktii3_lage    s                   like(aktlag)
7.02 d  sktii3_nokl    s                   like(aktnok)
7.02  *
7.02 d  sktii4_nokl    s                   like(aktnok)
7.02 d  sktii4_filg    s                   like(aktfgp)
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
      *
     d* w_firm          s                   like(data.aktfir)
     d w_aktuid        s             18  0
     d w_aktfgp        s              3
     d w_aktfir        s              3  0
     d w_aktlag        s              3  0
     d w_aktnok        s             50
     d w_aktvd1        s              1
     d w_aktvd2        s           2048
     d w_aktkjd        s             50
     d w_aktsys        s             50
7.03 d w_aktmod        s             50
     d w_aktety        s             50
     d w_aktprs        s             50
     d w_aktbsk        s           2000
     d w_akteus        s             10
     d w_aktoda        s               d   datfmt(*iso)
     d w_akteda        s               d   datfmt(*iso)
      *
     d sqlquery        s           4096
7.02 d w_fris          s             50
     d w_teller        s              3  0
     d b_open_sok      s              1    inz(*off)
      *
     d w_firm          s                    like(w_aktfir)
      *
7.02 d scan1           s             20
7.02 d scan2           s             20
7.02 d scan3           s             20
7.02 d l1              s              3  0
7.02 d l2              s              3  0
7.02 d l3              s              3  0
7.02 d i               s              3  0
7.02 d j               s              3  0
7.02 d w_alt           s           2000
7.02 d w_seqe          s              1
7.03 d w_a             s              1
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(18)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, commit=*none, DlyPrp=*YES
     C+  , SRTSEQ=*LANGIDSHR, LANGID=NOR
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      *in30 = *on
     c                   exsr      xc1bld
     c                   goto      b2taga
7.02 c                   when      *inki
7.02 c                   eval      b2fris = w_fris
7.02 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2tfgr <> *blank or
     c                             b2tfir <> 0 or
     c                             b2tlag <> 0 or
     c                             b2tnok <> *blank or
     c                             b2fris <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      b_open_sok = *on
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
7.02 c*                  eval      *in41 = *off
     c                   eval      b_open_sok = *on
      *
7.02 c                   select
7.02 c                   when      b2tfgr <> *blank
7.02 c                   eval      w_seqe = *blank
7.02 c                   eval      sktii1_filg = b2tfgr
7.02 c                   eval      sktii1_nokl = *blank
7.02 c     sktii1_key    setll     sktii1
7.02  *
7.02 c                   when      b2tfir <> *zero
7.02 c                   eval      w_seqe = '1'
7.02 c                   eval      sktii2_firm = b2tfir
7.02 c                   eval      sktii2_nokl = *blank
7.02 c     sktii2_key    setll     sktii2
7.02  *
7.02 c                   when      b2tlag <> *zero
7.02 c                   eval      w_seqe = '2'
7.02 c                   eval      sktii3_lage = b2tlag
7.02 c                   eval      sktii3_nokl = *blank
7.02 c     sktii3_key    setll     sktii3
7.02  *
7.02 c                   when      b2tnok <> *blank
7.02 c                   eval      w_seqe = '3'
7.02 c                   eval      sktii4_nokl = b2tnok
7.02 c                   eval      sktii4_filg = *blank
7.02 c     sktii4_key    setll     sktii4
7.02  *
7.02 c                   endsl
      *
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tfgr = *blank
     c                   eval      b2tfir = 0
     c                   eval      b2tlag = 0
     c                   eval      b2tnok = *blank
     c                   eval      b2fris = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
7.02  *  Posisjonering cursor
7.02 c                   if        *in41 = *off
7.02 c                   eval      *in41 = *on
7.02 c                   else
7.02 c                   eval      *in41 = *off
7.02 c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
7.02 c*                  eval      *in41 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      *in30 = *off
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopi
      *
     c                   if        b1valg = 3
     c                   eval      *in30 = *on
     c                   exsr      xc1bld
     c                   eval      *in30 = *off
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c*                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1tuid = b1tuid
     c                   exsr      xd1win
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c*                   eval      c1gave = b1gave
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
     c     c1taga        tag
      *
     c                   if        b1valg = 3
     c                   eval      c1tfgp = b1tfgp
     c                   eval      c1tfir = b1tfir
     c                   eval      c1tlag = b1tlag
     c                   eval      c1tnok = b1tnok
     c                   else
     c                   eval      c1tfgp = *blank
     c                   eval      c1tfir = 0
     c                   eval      c1tlag = 0
     c                   eval      c1tnok = *blank
     c                   endif
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c/exec sql
     c+ select count(*)
     c+ into  :w_teller
7.02 c+ from sktist
     c+ where aktfgp = :c1tfgp and
     c+  aktfir = :c1tfir and
     c+  aktlag = :c1tlag and
     c+  aktnok = :c1tnok
     c/end-exec
     c                   if        w_teller > 0
     c                   exsr      xc1msg
     c*                   if        b_anul = *on
     c                   goto      c1taga
     c*                   endif
     c                   endif
     c                   eval      b_forn = *on
      *
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      c1tfgp = *blank
     c                   eval      c1tfir = 0
     c                   eval      c1tlag = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c/exec sql
7.02 c+ select aktfgp, firma, lager, nokkel
     c+ into  :w_aktfgp,:w_aktfir,:w_aktlag,:w_aktnok
7.02 c+ from sktist
7.02 c+ where uid = :d1tuid
     c/end-exec
     c                   eval      d1tfgp = w_aktfgp
     c                   eval      d1tfir = w_aktfir
     c                   eval      d1tlag = w_aktlag
     c                   eval      d1tnok = w_aktnok
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter nokklel
      *
     c/exec sql
     c+ delete
7.02 c+ from sktist
7.02 c+ where uid = :d1tuid
     c/end-exec
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
7.03 c                   eval      w_a = '1'
7.03 c     startc2       tag
7.03  *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   if        b_oppr = *on  and b1valg <> 3
     c                   eval      c2tfgp = c1tfgp
     c                   eval      c2tfir = c1tfir
     c                   eval      c2tlag = c1tlag
     c                   eval      c2tnok = c1tnok
     c                   eval      c2tvd1 = *blank
     c                   eval      c2tvd2 = *blank
     c                   eval      c2tkjd = *blank
     c                   eval      c2tsys = *blank
     c                   eval      c2tmdl = *blank
     c                   eval      c2tety = *blank
     c                   eval      c2tprs = *blank
     c                   eval      c2tbsk = *blank
     c                   eval      c2eusr = l_user
     c                   else
7.03  *
7.03 c     sktiir_key    chain     sktiir
7.03 c                   eval      aktvd2 = aktvd2
7.03 c*/exec sql
7.03 c*+ select aktfgp, firma, lager, nokkel, verdi1, verdi2
7.03 c*+ , kjede, system, modul, pris, beskrivelse
7.03 c*+ , endretav, opprettetdato, endretdato
7.03 c*+ into  :w_aktfgp,:w_aktfir,:w_aktlag,:w_aktnok,:w_aktvd1,:w_aktvd2
7.03 c*+ ,:w_aktkjd,:w_aktsys,:w_aktmod,:w_aktprs,:w_aktbsk
7.03 c*+ ,:w_akteus, :w_aktoda, :w_akteda
7.03 c*+ from sktist
7.03 c*+ where uid = :b1tuid
7.03 c*/end-exec
      *
     c                   if        b1valg = 3
     c                   eval      c2tfgp = c1tfgp
     c                   eval      c2tfir = c1tfir
     c                   eval      c2tlag = c1tlag
     c                   eval      c2tnok = c1tnok
     c                   else
7.03 c                   eval      c2tfgp = aktfgp
7.03 c                   eval      c2tfir = aktfir
7.03 c                   eval      c2tlag = aktlag
7.03 c                   eval      c2tnok = aktnok
7.03 c                   endif
7.03 c                   eval      c2tvd1 = aktvd1
7.03  *
7.03 c                   if        w_a    = '1'
7.03 c                   eval      c2tvd2 = %subst(aktvd2:1:448)
7.03 c                   else
7.03 c                   eval      c2tvd2 = %subst(aktvd2:449:448)
7.03 c                   endif
7.03 c                   eval      w_aktvd2 = aktvd2
7.03  *
7.03 c                   eval      c2tkjd = aktkjd
7.03 c                   eval      c2tsys = aktsys
7.03 c                   eval      c2tmdl = aktmod
7.03 c                   eval      c2tety = aktety
7.03 c                   eval      c2tprs = aktprs
7.03 c                   eval      c2tbsk = aktbsk
7.03 c                   eval      c2eusr = akteus
7.03 c     *dmy          move      aktoda        c2odat
7.03 c     *dmy          move      akteda        c2edat
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   eval      b_forn = *off
     c                   goto      end_c2bld
     c                   endsl
      *
      * Oppdatering er valgt
      *
     c                   if        *in30 = *on
     c                   time                    w_time
7.03  *
7.03 c                   if        w_a   = '1'
7.03 c                   eval      %subst(w_aktvd2:1:448) = c2tvd2
7.03 c                   else
7.03 c                   eval      %subst(w_aktvd2:449:448) = c2tvd2
7.03 c                   endif
7.03  *
     c                   if        b1valg = 2
     c/exec sql
7.02 c+ update sktist set aktfgp=:c2tfgp, firma = :c2tfir, lager = :c2tlag
7.03 c+ ,nokkel = :c2tnok , verdi1 = :c2tvd1, verdi2 = :w_aktvd2
     c+ ,kjede = :c2tkjd, system = :c2tsys, modul = :c2tmdl
7.02 c+ ,pris = :c2tprs, beskrivelse = :c2tbsk
     c+ , endretdato = :w_date, endretkl = :w_time, endretav = :l_user
7.02 c+ where uid = :b1tuid
     c/end-exec
     c                   elseif    b1valg = 3 or b_oppr = *on
     c/exec sql
7.02 C+ INSERT INTO sktist
7.02 c+ (aktfgp,firma,lager,nokkel,verdi1,verdi2,kjede
7.02 c+ ,system,modul,pris,beskrivelse,opprettetav
     c+ ,opprettetkl,opprettetdato,endretdato,endretkl,endretav)
     c+ values (
     c+ :c2tfgp,:c2tfir,:c2tlag,:c2tnok,:c2tvd1,
7.03 c+ :w_aktvd2,:c2tkjd,:c2tsys,:c2tmdl,
     c+ :c2tprs,:c2tbsk,:l_user,:w_time,:w_date,:w_date,:w_time,:l_user
     c+ )
     c/end-exec
     c                   endif
7.03  *
7.03 c                   if        *inkh
7.03 c                   eval      w_a = '2'
7.03 c                   goto      startc2
7.03 c                   endif
7.03  *
     c                   if        sqlcod < 0
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
     c
7.02 c                   exsr      if_fri_sok
7.02 c                   dou       w_srrn = w_spge
7.02  *
7.02 c                   select
7.02 c                   when      w_seqe = '1'
7.02 c                   read      sktii2
7.02 c                   when      w_seqe = '2'
7.02 c                   read      sktii3
7.02 c                   when      w_seqe = '3'
7.02 c                   read      sktii4
7.02 c                   other
7.02 c                   read      sktii1
7.02 c                   endsl
7.02  *
7.02 c                   if        %eof
7.02 c                   eval      *in15 = *on
7.02 c                   leave
7.02 c                   endif
7.02  *  Utvalgte filgrupper
7.02 c                   if        b2tfgr <> *blank and
7.02 c                             aktfgp <> b2tfgr
7.02 c                   iter
7.02 c                   endif
7.02  *  Utvalgte firma
7.02 c                   if        b2tfir <> *zero  and
7.02 c                             aktfir <> b2tfir
7.02 c                   iter
7.02 c                   endif
7.02  *  Utvalgte lager
7.02 c                   if        b2tlag <> *zero  and
7.02 c                             aktlag  <> b2tlag
7.02 c                   iter
7.02 c                   endif
7.02  *  Utvalgte Friteskt?
7.02 c                   eval      w_alt = %trim(aktnok) +
7.02 c                                     %trim(aktvd2) +
7.02 c                                     %trim(aktkjd) +
7.02 c                                     %trim(aktsys) +
7.02 c                                     %trim(aktmod) +
7.02 c                                     %trim(aktbsk) +
7.02 c                                     %trim(aktfgp) +
7.02 c                                     %trim(%char(aktfir)) +
7.02 c                                     %trim(%char(aktlag))
7.02 c
7.02 c                   if        scan1 <> *blank
7.02 c     lo:up         xlate     w_alt         w_alt
7.02 c     scan1:l1      scan      w_alt         i
7.02 c                   if        not %found
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02 c                   if        scan2 <> *blank
7.02 c     scan2:l2      scan      w_alt         i
7.02 c                   if        not %found
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02 c                   if        scan3 <> *blank
7.02 c     scan3:l3      scan      w_alt         i
7.02 c                   if        not %found
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02  *
7.02 c                   select
7.02 c                   when      w_seqe = '2'
7.02 c                   eval      sktii2_firm = aktfir
7.02 c                   eval      sktii2_nokl = aktnok
7.02 c                   when      w_seqe = '3'
7.02 c                   eval      sktii3_lage = aktlag
7.02 c                   eval      sktii3_nokl = aktnok
7.02 c                   when      w_seqe = '4'
7.02 c                   eval      sktii4_nokl = aktnok
7.02 c                   eval      sktii4_filg = aktfgp
7.02 c                   other
7.02 c                   eval      sktii1_filg = aktfgp
7.02 c                   eval      sktii1_nokl = aktnok
7.02 c                   endsl
7.02  *
     c                   eval      w_srrn = w_srrn + 1
      *
7.02 c                   eval      b1valg = 0
7.02 c                   eval      b1tfgp = aktfgp
7.02 c                   eval      b1tfir = aktfir
7.02 c                   eval      b1tlag = aktlag
7.02 c                   eval      b1tnok = aktnok
7.02 c                   eval      b1tvd1 = aktvd1
7.02 c                   eval      b1tbsk = aktbsk
7.02 c                   eval      b1tuid = uid
7.02  *
7.02 c                   write     b1sfl
7.02  *
7.02 c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for å dele opp frisøk
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     if_fri_sok    begsr
7.02  *
7.02  *   Del opp frisøk i inntil 3 ord
7.02 c                   if        b2fris <> *blank
7.02 c                   eval      w_fris = b2fris
7.02 c     ' '           scan      b2fris        i
7.02 c                   if        i > 0
7.02 c                   eval      l1 = i - 1
7.02 c                   eval      scan1 = %subst(b2fris:1:l1)
7.02 c                   eval      j = i + 1
7.02 c     ' '           scan      b2fris:j      i
7.02 c                   if        i > 0
7.02 c                   eval      l2 = i - j
7.02 c                   eval      scan2 = %subst(b2fris:j:l2)
7.02 c                   eval      j = i + 1
7.02 c     ' '           scan      b2fris:j      i
7.02 c                   if        i > 0
7.02 c                   eval      l3 = i - j
7.02 c                   eval      scan3 = %subst(b2fris:j:l3)
7.02 c                   endif
7.02 c                   endif
7.02 c                   endif
7.02 c                   else
7.02 c                   eval       scan1 = *blank
7.02 c                   eval       scan2 = *blank
7.02 c                   eval       scan3 = *blank
7.02 c                   endif
7.02  *    Hvis frisøk start på nyttt på filene
7.02 c                   if        scan1 <> *blank
7.02 c                   select
7.02 c                   when      w_seqe = *blank
7.02 c                   eval      sktii1_filg = b2tfgr
7.02 c                   eval      sktii1_nokl = *blank
7.02 c     sktii1_key    setll     sktii1
7.02  *
7.02 c                   when      w_seqe = '1'
7.02 c                   eval      sktii2_firm = b2tfir
7.02 c                   eval      sktii2_nokl = *blank
7.02 c     sktii2_key    setll     sktii2
7.02  *
7.02 c                   when      w_seqe = '2'
7.02 c                   eval      sktii3_lage = b2tlag
7.02 c                   eval      sktii3_nokl = *blank
7.02 c     sktii3_key    setll     sktii3
7.02  *
7.02 c                   when      w_seqe = '3'
7.02 c                   eval      sktii4_nokl = b2tnok
7.02 c                   eval      sktii4_filg = *blank
7.02 c     sktii4_key    setll     sktii4
7.02  *
7.02 c                   endsl
7.02 c                   endif
7.02  *
7.02 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c*                   if        w_afld = 'B2KUND'
     c*                   call      'RK500R'
     c*                   parm                    w_firm
     c*                   parm                    b2kund
     c*                   parm                    w_knav
     c*                   leavesr
     c*                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
7.03 c     sktiir_key    klist
7.03 c                   kfld                    b1tfgp
7.03 c                   kfld                    b1tfir
7.03 c                   kfld                    b1tlag
7.03 c                   kfld                    b1tnok
7.03  *
7.02 c     sktii1_key    klist
7.02 c                   kfld                    sktii1_filg
7.02 c                   kfld                    sktii1_nokl
7.02  *
7.02 c     sktii2_key    klist
7.02 c                   kfld                    sktii2_firm
7.02 c                   kfld                    sktii2_nokl
7.02  *
7.02 c     sktii3_key    klist
7.02 c                   kfld                    sktii3_lage
7.02 c                   kfld                    sktii3_nokl
7.02  *
7.02 c     sktii4_key    klist
7.02 c                   kfld                    sktii4_nokl
7.02 c                   kfld                    sktii4_filg
7.02  *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
7.02 c                   eval      *in41 = *on
     c                   time                    w_date
     c                   time                    w_time
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      b_open_sok = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
