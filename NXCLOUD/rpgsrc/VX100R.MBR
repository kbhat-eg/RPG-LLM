     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VX100R
      * Beskrivelse . . : EAN-nummer, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220399 HKO  Nytt program
      *       050307 BOF  Utvidet med enhet
5.60  *       080908 BOF  Justert litt vedr. time-stamp problemer
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  100709 bof  Test om ean-nr ligger på vare-enhet
6.11  * 6.10  100709 bof  Tatt bort mulighet til å legge på enhet på ean her
6.12  * 6.10  250909 bof  Endret fra VVENL6 til VVEPL2
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
     fveanlr    if   e           k disk    rename(veanpfr:veanlrr)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.12 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
      *
     fvx100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d veanl1_eann     s                   like(vxeann)
     d veanl2_vare     s                   like(vxvare)
     d veanlr_eann     s                   like(vxeann)
     d veanlu_eann     s                   like(vxeann)
     d vvarl1_vare     s                   like(vvvare)
6.12 d vvepl2_pgti     s                   like(vepgti)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*hms)
      *
     d w_firm          s                   like(vxfirm)
     d w_vare          s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
     d w_enhe          s                   like(vxenhe)
      *
     d s_eann          s                   like(vxeann)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2eann <> 0 or
     c                             b2vare <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      veanl2_vare = b1vare
     c     veanl2_key    setll     veanl2
     c                   other
     c                   eval      veanl1_eann = b1eann
     c     veanl1_key    setll     veanl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på EAN-nummer
      *
     c                   if        b2eann <> 0
     c                   eval      w_seqe = ' '
     c                   eval      veanl1_eann = b2eann
     c     veanl1_key    setll     veanl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V'
     c                   movel     b2vare        veanl2_vare
     c     veanl2_key    setll     veanl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2eann = 0
     c                   eval      b2vare = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1eann = b1eann
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1eann = b1eann
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1eann = b1eann
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1eann = b1eann
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1eann = b2eann
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1eann = 0
     c                   goto      end_c1bld
     c                   endif
6.11  *
6.11  * Sjekker om inntastet ean-nr allerede ligger på vare-enhet
6.11  *
6.12 c                   eval      vvepl2_pgti = c1eann
6.12 c     vvepl2_key    chain     vvepl2
6.12 c                   if        %found(vvepl2)
6.11 c                   eval      *in33 = *on
6.11 c                   goto      c1taga
6.11 c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      veanlr_eann = c1eann
     c     veanlr_key    chain     veanlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1eann = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2eann = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      veanlr_eann = c1eann
     c     veanlr_key    chain     veanlr                             90
      *
     c                   if        *in90 = *off
      *
     c                   movel     vxvare        c2vare
     c                   eval      c2tek1 = *blank
     c                   eval      c2eusr = vxeusr
      *
5.60 c                   if        vxtime <> *loval
     c                   move      vxtime        w_dato
     c                   move      vxtime        w_time
     c     *dmy          move      w_dato        c2odat
     c     *hms          move      w_time        c2otim
5.60 c                   else
5.60 c                   eval      c2odat = 0
5.60 c                   eval      c2otim = 0
5.60 c                   endif
      *
     c     *dmy          move      vxedat        c2edat
      *
     c                   eval      vvarl1_vare = vxvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c2tek1 = vvtek1
     c                   endif
      *
     c                   else
      *
     c                   eval      c2vare = 0
     c                   eval      c2tek1 = *blank
     c                   eval      c2odat = 0
     c                   eval      c2otim = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
      *
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1eann = k1eann
     c                   eval      veanlr_eann = k1eann
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   if        w_afld = 'C2VARE'
     c                   eval      w_vare = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c2tek1
     c                   movel     w_vare        c2vare
     c                   endif
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2vare = 0
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   movel     c2vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      c2tek1 = vvtek1
      *
      * Oppdatering av EAN-register
      *
     c                   clear                   veanlur
      *
     c                   eval      veanlu_eann = c1eann
     c     veanlu_key    chain     veanlur                            90
      *
     c                   movel     c2vare        vxvare
     c                   eval      vxeusr = l_user
      *
     c                   time                    vxetim
     c                   time                    vxedat
      *
     c                   if        *in90 = *off
     c                   update    veanlur
     c                   else
     c                   eval      vxfirm = w_firm
     c                   eval      vxeann = veanlr_eann
     c                   time                    vxtime
6.10 c                   time                    vxodat
6.10 c                   time                    vxotim
6.10 c                   eval      vxousr = l_user
     c                   write     veanlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1vare = vxvare
     c                   eval      b1tek1 = c2tek1
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      veanlr_eann = d1eann
     c     veanlr_key    chain     veanlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      veanlu_eann = d1eann
     c     veanlu_key    chain     veanlur                            90
     c                   delete    veanlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_eann = k1eann
      *
      * Fyller kopierings-bildet
      *
     c                   eval      veanlr_eann = k1eann
     c     veanlr_key    chain     veanlr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1eann = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      veanlr_eann = k1eann
     c     veanlr_key    chain     veanlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
6.11  *
6.11  * Sjekker om inntastet ean-nr allerede ligger på vare-enhet
6.11  *
6.12 c                   eval      vvepl2_pgti = c1eann
6.12 c     vvepl2_key    chain     vvepl2
6.12 c                   if        %found(vvepl2)
6.11 c                   eval      *in33 = *on
6.11 c                   goto      k1taga
6.11 c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1eann = s_eann
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   read      veanl2
     c                   other
     c                   read      veanl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vxfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      veanl2_vare = vxvare
     c                   other
     c                   eval      veanl1_eann = vxeann
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1eann = vxeann
     c                   eval      b1vare = vxvare
     c                   eval      b1tek1 = *blank
     c                   eval      b1odat = 0
      *
     c                   eval      vvarl1_vare = vxvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
5.60 c                   if        vxtime <> *loval
     c                   move      vxtime        w_dato
     c     *dmy          move      w_dato        b1odat
5.60 c                   else
5.60 c                   eval      b1odat = 0
5.60 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     veanl2_key    setgt     veanl2
     c                   readp     veanl2
     c                   other
     c     veanl1_key    setgt     veanl1
     c                   readp     veanl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     veanl2
     c                   other
     c                   readp     veanl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vxfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      veanl2_vare = vxvare
     c                   other
     c                   eval      veanl1_eann = vxeann
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     veanl2_key    setll     veanl2
     c                   other
     c     veanl1_key    setll     veanl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2vare
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på EAN-nummer
      *
     c     veanl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl1_eann
      *
      * Key for posisjonering på beskrivelse
      *
     c     veanl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl2_vare
      *
      * Key for oppslag på EAN-nummer
      *
     c     veanlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlr_eann
      *
      * Key for oppdatering av EAN-register
      *
     c     veanlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlu_eann
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
6.11  *
6.11  * Key for oppslag på vare-enhet
6.11  *
6.12 c     vvepl2_key    klist
6.11 c                   kfld                    w_firm
6.12 c                   kfld                    vvepl2_pgti
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      veanl1_eann = 0
     c     veanl1_key    setll     veanl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
