      /title  ASOFAK FIX: Bytter varegruppe i innkjøpsstatistikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX828R
      * Beskrivelse . . : FIX: Bytter varegruppe i innkjøpsstatistikk
      *
      *                   Leser gjennom innkjøpsstatstikk, og bytter
      *                   varegruppe i.h.t. vareregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150200 HKO  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsistpf    uf a e           k disk    rename(sistpfr:sistpfr)
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser hele innkjøpsstatistikken for oppdatering
      *
     c                   read      sistpfr                                90
     c                   dow       *in90 = *off
      *
     c                   if        sivare <> *blank
     c                   eval      vvarl1_firm = sifirm
     c                   eval      vvarl1_vare = sivare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c     vvarl1_key    chain     vvasl1                             91
     c                   endif
     c                   if        *in91 = *off
     c                   eval      siogrp = vvogrp
     c                   eval      sihgrp = vvhgrp
     c                   eval      siugrp = vvugrp
     c                   eval      siahgr = vvahgr
     c                   eval      siaugr = vvaugr
     c                   endif
     c                   endif
      *
     c                   update    sistpfr
      *
     c                   read      sistpfr                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
     c                   endsr
