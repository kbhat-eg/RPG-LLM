     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AF726R
      * Beskrivelse . . : Lager xml til mail utsendelse
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       271114 bsa  Nytt program
6.nu  * R6.30 091116 bsa  Endring ihht. nummerutvidelse
6.30  * 6.30  020215 bsa  Skriver til datakø for å trigge java sniffer
6.31  * 6.30  161117 bsa  Feil i malnavn
6.32  * 6.30  250718 bof  Lagt inn clear av buffer
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
     d p_serv          s             35
     d p_stat          s              1
     d p_rec1          s             50
     d p_rec2          s             50
     d p_rec3          s             50
     d p_send          s             50
     d p_subj          s            100
     d p_txt1          s            100
     d p_txt2          s            100
     d p_txt3          s            100
     d p_txt4          s            100
     d p_avsl          s            100
     d p_lr            s              1
     d p_str_in        s            512
     d p_str_out       s            512
6.30 d p_filg          s             10
6.30 d p_xmlf          s            256
      *
     d w_subj          s            512
     d w_txt1          s            512
     d w_txt2          s            512
     d w_txt3          s            512
     d w_txt4          s            512
     d w_avsl          s            512
      *
      * Definering av variabler i nøkler
      *
     d afpslr_ufil     s                   like(afufil)
     d afpslr_uide     s                   like(afuide)
      *
     d w_path          s             50
     d w_xmlp          s             50
     d w_kode          s              1
     d w_firm          s              3  0
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
6.nu d w_numm          s              8  0
     d w_jnav          s             15
     d w_jkey          s             41
     d w_jstat         s              2  0
     d w_jtext         s            200
     d w_jtxt          s             35
     d w_tims          s               z
     d w_mtxt          s           1024
6.30 d w_dtaq          s              1
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
     d b_first         s              1    inz(*off)
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a
     d Out_path        s            256a
      *
     d tmpdate         s               d
     d crlf            c                   const(X'0d25')
      *
     d                 ds
     d w_dato                        26
     d  w_date                       10    overlay(w_dato:1)
     d  w_ski1                        1    overlay(w_dato:11)
     d  w_time                        2    overlay(w_dato:12)
     d  w_ski2                        1    overlay(w_dato:14)
     d  w_minu                        2    overlay(w_dato:15)
     d  w_ski3                        1    overlay(w_dato:17)
     d  w_seku                        9    overlay(w_dato:18)
      *
      *
      /copy prototypeb
      /copy usec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis vi ikke har path til xml eller IFS avslutter vi uten videre
     c                   if        XML_path = *blanks or
     c                             OUT_path = *blanks
     c                   goto      avslutt
     c                   endif
      * Hent inn mal
     c                   exsr      xml_env
      * Henter tilbake mailserver
     c                   eval      p_serv = *blank
     c                   eval      p_stat = *blank
     c                   call      'AM705C'
     c                   parm                    p_serv
     c                   parm                    p_stat
      *
      * Scann av strenger
      *
     c                   eval      w_subj = *blanks
     c                   eval      w_txt1 = *blanks
     c                   eval      w_txt2 = *blanks
     c                   eval      w_txt3 = *blanks
     c                   eval      w_txt4 = *blanks
     c                   eval      w_avsl = *blanks
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_subj        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_subj
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_txt1
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt2        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_txt2
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt3        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_txt3
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt4        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_txt4
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_avsl        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_avsl
      *
     c                   eval      w_mtxt = %trim(w_txt1) + crlf +
     c                                      %trim(w_txt2) + crlf +
     c                                      %trim(w_txt3) + crlf +
     c                                      %trim(w_txt4) + crlf +
     c                                      %trim(w_avsl)
      *
      * Skriv mail
      *
      /Free
           // Skriv mailkommand
               WrtSection('MailCommandStart');
               if %Trim(p_rec1)<> *blanks;
               UpdHTMLVar('Receivers':     %trim(p_rec1));
               WrtSection('MailReceiver');
               endif;
               if %Trim(p_rec2)<> *blanks;
               UpdHTMLVar('Receivers':     %trim(p_rec2));
               WrtSection('MailReceiver');
               endif;
               if %Trim(p_rec3)<> *blanks;
               UpdHTMLVar('Receivers':     %trim(p_rec3));
               WrtSection('MailReceiver');
               endif;

               UpdHTMLVar('CC':                     ' ');
               UpdHTMLVar('BCC':                 p_send);
               UpdHTMLVar('Sender':              p_send);
               UpdHTMLVar('Subject':             w_subj);
               UpdHTMLVar('Message':             w_mtxt);
          //   UpdHTMLVar('SMTPServer':          p_serv);
               UpdHTMLVar('SMTPServer':             ' ');
               UPDHTMLVar('SMTPUser':               ' ');
               UPDHTMLVar('SMTPPw':                 ' ');
               WrtSection('MailCommandEnd');

      /End-free
      *
      * Skriv xml til IFS
      *
     c                   exsr      xml_end
      *
      * Avslutt jobbiden
      *
     c                   call      'AB710R'
     c                   parm                    w_jkey
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av mal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_env       begsr
      *
      * Hent inn xml templaten
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
6.32 c                   callp     ClrHtmlBuffer
      *
      /Free
           // Finne nytt batch nummer
               UpdHTMLVar('BatchNo':                  w_jkey);
               UpdHTMLVar('Batchtype':               'MAIL');
               UpdHTMLVar('Username':                 l_user);
               UpdHTMLVar('Batchtime':                w_dato);
               WrtSection('Topp');

           // Finne credentials
               UpdHTMLVar('Schema':             'ADB'+l_filg);
               UpdHTMLVar('Companyno':         %char(l_firm));
               WrtSection('Credential');
      /End-free
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_end       begsr
      *
      /Free
           // Skriv siste tagg
               WrtSection('Footer');

      /End-free
      *
      * Lag navn til xml fila
      *
     c                   eval      w_path = %trim(Out_path)
     c                   eval      XML_Output = %trim(w_path)+l_filg+'/'+
     c                             'Mail_' + %trim(w_jkey) + '.xml'
      *
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
6.30  * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
6.30 c                   if        w_dtaq = '1'
6.30 c                   eval      p_xmlf = xml_output
6.30 c                   eval      p_filg = 'ADB'+l_filg
6.30 c                   call      'AP629C'
6.30 c                   parm                    p_filg
6.30 c                   parm                    p_xmlf
6.30 c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_rec1
     c                   parm                    p_rec2
     c                   parm                    p_rec3
     c                   parm                    p_send
     c                   parm                    p_subj
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_txt4
     c                   parm                    p_avsl
      *
      * Key for oppslag på AFPS
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
     c                   eval      XML_path = *blanks
     c                   eval      afpslr_ufil = l_filg
6.31 c                   eval      afpslr_uide = 'MAILMAL   '
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   movel     afudss        XML_path
     c                   endif
      *
     c                   eval      OUT_path = *blanks
     c                   eval      afpslr_uide = 'XMLPATH   '
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   movel     afudss        OUT_path
     c                   endif
6.30  *
6.30  * Finn ut om vi skal skrive til datakø
6.30  *
6.30 c                   eval      w_dtaq = '0'
6.30 c                   eval      afpslr_uide = 'DATAQ     '
6.30 c     afpslr_key    chain     afpslrr
6.30 c                   if        %found
6.30 c                   movel     afudss        w_dtaq
6.30 c                   endif
6.30  *
      * Lager batchnummer for denne utskriften
     c                   eval      w_kode = '0'
     c                   eval      w_firm = l_firm
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * Vi logger at jobben har startet
      *
     c                   eval      w_jnav = 'PDF' + %char(w_numm)
     c                   eval      w_jtxt = 'Mail generering har startet !'
      *
     c                   call      'AB700R'
     c                   parm                    w_jnav
     c                   parm                    w_jkey
     c                   parm                    w_jtxt
      *
      * Vi logger at vi genererer xml for utskriften
      *
     c                   eval      w_jstat = 10
     c                   eval      w_jtext = 'Generering av mail for ' + w_jnav
      *
     c                   call      'AB705R'
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
      *
     c                   time                    w_tims
     c                   movel     w_tims        w_dato
     c                   eval      w_ski1 = 'T'
     c                   eval      w_ski2 = ':'
     c                   eval      w_ski3 = ':'
      *
     c                   endsr
