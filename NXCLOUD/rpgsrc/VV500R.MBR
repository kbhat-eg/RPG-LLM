     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV500R
      * Beskrivelse . . : Vare, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150399 HKO  Nytt program
      *       251103 HKO  Viser ikke utgåtte varer
      *       140504 HKO  Søk blir med over ved overgang til skafferegister
6.10  * R6.10 100909 bof  Utvidet med sortiment (1 pos.)
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  091110 bof  Utvidet med status for C-sentrallagerv. P=periodev.
6.22  * 6.20  051210 bof  Hvis sentral-lagervare, settes farge lik rød
6.23  * 6.20  150211 bof  Sentral-lagervare kode for styring av farge
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  180515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
6.3q  *       260515 bof  Datoformat sql
6.31  *       131118 bkv  TLS
7.xx  * 7.00  220116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl2    if   e           k disk    rename(vvarpfr:vvarl2r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvarl6    if   e           k disk    rename(vvarpfr:vvarl6r)
     fvvarl7    if   e           k disk    rename(vvarpfr:vvarl7r)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.10 flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.21 fvsdtl7    if   e           k disk    rename(vsdtpfr:vsdtl7r)
6.21 fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
6.23 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvv500d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_tek1          s                   like(vvtek1)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl2_tek1     s                   like(vvtek1)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvarl6_ogrp     s                   like(vvogrp)
     d vvarl7_ogrp     s                   like(vvogrp)
     d vvarl7_hgrp     s                   like(vvhgrp)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d rlevl1_levr     s                   like(rllevr)
6.21 d vsdtl7_vare     s                   like(vdvare)
6.21 d vshel1_vsor     s                   like(vcvsor)
6.21 d vshel1_lage     s                   like(vclage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_open_sok      s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
6.10 d b_ksor          s              1    inz(*off)
6.21 d b_sess          s              1    inz(*off)
6.21 d b_sent          s              1    inz(*off)
6.21 d b_inlr          s              1    inz(*off)
      *
     d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d s_stek          s             35
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_lage          s              2  0
6.31 d w_vtyp          s                   like(vvvtyp)
6.31 d w_ldor          s                   like(vvldor)
6.31 d w_undv          s              8  0
6.31 d w_logl          s             15
6.31 d w_jvqvnr        s                   like(vvvare)
6.33 d w_jvquno        s              8  0
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
      *
     d s_ogrp          s                   like(vvogrp)
     d s_hgrp          s                   like(vvhgrp)
     d s_ugrp          s                   like(vvugrp)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(08)
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2ogrp = s_ogrp
     c                   eval      b2hgrp = s_hgrp
     c                   eval      b2ugrp = s_ugrp
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * F8=Spørring mot skaffe-register (VA)
      *
     c                   if        *inkh = *on
      *
     c                   eval      w_vare = p_vare
     c                   call      'JV505R'
     c                   parm                    w_firm
     c                   parm                    s_ogrp
     c                   parm                    s_hgrp
     c                   parm                    s_ugrp
     c                   parm                    s_stek
     c                   parm                    p_vare
     c                   parm                    p_tek1
      *
     c                   if        p_vare <> w_vare
     c                   eval      b_valg_ok = *on
     c                   goto      avslutt
     c                   else
     c                   goto      b2tagb
     c                   endif
      *
     c                   endif
      *
      * F23=Bytt posisjonering (Varenr/Lev.varenr/Nobbnr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Søking
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2vare <> 0 or
     c                             b2nobb <> 0 or
     c                             b2lvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    setll     vvarl6
     c                   when      w_seqe = 'H'
     c     vvarl7_key    setll     vvarl7
     c                   when      w_seqe = 'U'
     c     vvarl8_key    setll     vvarl8
     c                   when      w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = b1vare
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c                   eval      vvarl3_nobb = b1nobb
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c                   eval      vvarl4_lvar = b1lvar
     c     vvarl4_key    setll     vvarl4
     c                   other
     c                   eval      vvarl2_tek1 = b1tek1
     c     vvarl2_key    setll     vvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
      * Validering av varegruppe
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp <> 0
     c                   eval      *in31 = *on
     c                   goto      end_sØk2
     c                   endif
      *
     c                   if        b2hgrp = 0 and
     c                             b2ugrp <> 0
     c                   eval      *in32 = *on
     c                   goto      end_sØk2
     c                   endif
      *
      * Tar vare på søk
      *
     c                   eval      s_ogrp = b2ogrp
     c                   eval      s_hgrp = b2hgrp
     c                   eval      s_ugrp = b2ugrp
     c                   eval      s_stek = b2stek
      *
      * Søk via søketekst
      *
     c                   if        b2stek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'S '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'SU'
     c                   endsl
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk på overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp =  0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'O'
     c                   eval      vvarl6_ogrp = b2ogrp
     c     vvarl6_key    setll     vvarl6
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på hovedgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'H'
     c                   eval      vvarl7_ogrp = b2ogrp
     c                   eval      vvarl7_hgrp = b2hgrp
     c     vvarl7_key    setll     vvarl7
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på undergruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vvarl8_ogrp = b2ogrp
     c                   eval      vvarl8_hgrp = b2hgrp
     c                   eval      vvarl8_ugrp = b2ugrp
     c     vvarl8_key    setll     vvarl8
     c                   goto      end_sØk
     c                   endif
      *
     c     end_sØk       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2stek = *blank
      *
     c     end_sØk2      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vvarl2_tek1 = b2tek1
     c     vvarl2_key    setll     vvarl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V'
     c                   movel     b2vare        vvarl1_vare
     c     vvarl1_key    setll     vvarl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på nobb-nummer
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      vvarl3_nobb = b2nobb
     c     vvarl3_key    setll     vvarl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på lev. varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'L'
     c                   eval      vvarl4_lvar = b2lvar
     c     vvarl4_key    setll     vvarl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c                   eval      b2nobb = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_vare = b1vare
     c                   eval      p_tek1 = b1tek1
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  declare sok_u cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvsugr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvsugr = :b2ugrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvsugr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_u
     c/end-exec
     c/exec sql
     c+                  open sok_u
     c/end-exec
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvsfir,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    reade     vvarl6                                 15
     c                   when      w_seqe = 'H'
     c     vvarl7_key    reade     vvarl7                                 15
     c                   when      w_seqe = 'U'
     c     vvarl8_key    reade     vvarl8                                 15
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_u
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvsugr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvsfir,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'V'
     c                   read      vvarl1                                 15
     c                   when      w_seqe = 'N'
     c                   read      vvarl3                                 15
     c                   when      w_seqe = 'L'
     c                   read      vvarl4                                 15
     c                   other
     c                   read      vvarl2                                 15
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      vvarlr_vare = vvsvar
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        vvfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   iter
     c                   endif
6.31  *
6.31  * henter varetype
6.31  *
6.31 c                   call      'VL710R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    vvvare
6.31 c                   parm                    w_lage
6.31 c                   parm                    w_vtyp
6.31 c                   parm                    w_udat
6.31 c                   parm                    w_ldor
6.31 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = vvvare
     c                   when      w_seqe = 'N'
     c                   eval      vvarl3_nobb = vvnobb
     c                   when      w_seqe = 'L'
     c                   eval      vvarl4_lvar = vvlvar
     c                   other
     c                   eval      vvarl2_tek1 = vvtek1
     c                   endsl
      *
6.21 c                   eval      b1sess = *blank
6.21 c                   eval      b1sent = *blank
6.21 c                   eval      b1stat = *blank
6.21 c                   exsr      sjekk_vsor
6.21 c                   if        b_sess = *on
6.21 c                   eval      b1sess = 'P'
6.21 c                   endif
6.22 c                   eval      *in50  = *off
6.21 c                   if        b_sent = *on
6.23 c                   if        faafsl = 1
6.22 c                   eval      *in50  = *on
6.23 c                   endif
6.21 c                   eval      b1sent = 'C'
6.21 c                   endif
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1vare = vvvare
6.31 c                   if        w_lv_nobb > 0
6.31 c                   eval      b1nobb = w_lv_nobb
6.31 c                   eval      b1lvar = w_lv_llva
6.31 c                   else
     c                   eval      b1nobb = vvnobb
     c                   eval      b1lvar = vvlvar
6.31 c                   endif
6.10 c                   if        b_ksor = *on
6.10 c                   movel     vvsort        b1sort
6.10 c                   else
6.10 c                   eval      b1sort = *blank
6.10 c                   endif
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      b1nypo = vvvare
     c                   when      *in81 = *on
6.30 c*                  eval      b1nypo = vvlvar
6.30 c                   movel     vvlvar        b1nypo
     c                   when      *in82 = *on
6.31 c*                   eval      b1nypo = %editc(vvnobb : 'Z')
6.31 c                   eval      b1nypo = %editc(b1nobb : 'Z')
     c                   endsl
      *
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b1navn = rlnavn
     c                   else
     c                   eval      b1navn = *blank
     c                   endif
      *
6.31  * sjekk farge
6.31 c                   eval      w_jvqvnr = vvvare
6.31 c                   exsr      finn_logivar
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'O'  or
     c                             w_seqe = 'H'  or
     c                             w_seqe = 'U'  or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   exsr      forny
     c                   goto      end_bck
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setgt     vvarl1
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setgt     vvarl3
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setgt     vvarl4
     c                   readp     vvarl4
     c                   other
     c     vvarl2_key    setgt     vvarl2
     c                   readp     vvarl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c                   readp     vvarl4
     c                   other
     c                   readp     vvarl2
     c                   endsl
      *
     c                   if        %eof or
     c                             vvfirm <> w_firm
     c                   leave
     c                   endif
      *
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = vvvare
     c                   when      w_seqe = 'N'
     c                   eval      vvarl3_nobb = vvnobb
     c                   when      w_seqe = 'L'
     c                   eval      vvarl4_lvar = vvlvar
     c                   other
     c                   eval      vvarl2_tek1 = vvtek1
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setll     vvarl4
     c                   other
     c     vvarl2_key    setll     vvarl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å sjekke om varen er i sortiment for C - sentrallagervarer
6.21  *                           eller i et sortiment for P - periodestyrt varer
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sjekk_vsor    begsr
6.21  *
6.21 c                   eval      b_sent = *off
6.21 c                   eval      b_sess = *off
6.21 c                   eval      vsdtl7_vare = vvvare
6.21 c     vsdtl7_key    setll     vsdtl7
6.21 c     vsdtl7_key    reade     vsdtl7
6.21 c                   dow       not %eof(vsdtl7)
6.21  *
6.21 c                   eval      vshel1_vsor = vdvsor
6.21 c                   eval      vshel1_lage = vdlage
6.21 c     vshel1_key    chain     vshel1
6.21 c                   if        %found(vshel1)
6.21 c                   if        vctype = 'C' or
6.21 c                             vctype = 'P'
6.21  *
6.21  * hvis lager er 0 -generelt sortiment eller at sortiment
6.21  * finnes på lager for ordre
6.21 c                   if        vclage = l_lage  or
6.21 c                             vclage = 0
6.21 c                   if        vctype = 'C'
6.21 c                   eval      b_sent = *on
6.21 c                   endif
6.21 c                   if        vctype = 'P'
6.21 c                   eval      b_sess = *on
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c     vsdtl7_key    reade     vsdtl7
6.21 c                   enddo
6.21  *
6.21 c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Finn info om logistikkvare for riktig prisgiver
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     finn_logivar  begsr
6.31  *
6.31 c                   eval      *in51 = *off
6.31 c                   eval      *in52 = *off
6.31 c
6.31 c                   if        %trim(w_jvqvnr) = ''
6.31 c                   leavesr
6.31 c                   endif
6.31 c
6.31 c                   eval      w_logl = ''
6.31 c                   eval      w_undv = 0
     c                   movel     w_jvqvnr      w_jvquno
6.31 c/exec sql
6.31 c+                  select jvquno, jvqvnr
6.31 c+                  into :w_undv, :w_logl
6.31 c+                  from jvklpf
6.31 c+                  where (jvquno = :w_jvquno or
6.31 c+                  jvqvnr = :w_jvqvnr)
6.31 c+                  and jvqfir = :w_firm
6.31 c+                  fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_logl = w_jvqvnr
6.31 c                   eval      *in52 = *on
6.31 c                   elseif    w_undv > 0
6.31 c                   eval      *in51 = *on
6.31 c                   endif
6.31 c
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_tek1
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på varetekst
      *
     c     vvarl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl2_tek1
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for posisjonering på leverandørs varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for søk på overgruppe
      *
     c     vvarl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl6_ogrp
      *
      * Key for søk på overgruppe/hovedgruppe
      *
     c     vvarl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl7_ogrp
     c                   kfld                    vvarl7_hgrp
      *
      * Key for søk på overgruppe/hovedgruppe/undergruppe
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      * Key for oppslag mot leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
6.10  *
6.10  * Key for oppslag på LAGR-koderegister
6.10  *
6.10 c     lstsl1_key    klist
6.10 c                   kfld                    w_firm
6.21  *
6.21  * Key for les av varer fra sortiment
6.21  *
6.21 c     vsdtl7_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vsdtl7_vare
6.21  *
6.21  * Key for les av soritment hode
6.21  *
6.21 c     vshel1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vshel1_vsor
6.21 c                   kfld                    vshel1_lage
6.23  *
6.23  * Key for oppslag på status-register
6.23  *
6.23 c     fstsl1_key    klist
6.23 c                   kfld                    w_firm
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
6.23  *
6.23  * leser status ofak
6.23  *
6.23 c     fstsl1_key    chain     fstsl1r                            90
6.10  *
6.10  * Henter lager status - kjede-sortiment ?
6.10  *
6.10 c                   eval      b_ksor  = *off
6.10 c     lstsl1_key    chain     lstsl1                             91
6.10 c                   if        %found(lstsl1) and
6.10 c                             laakso <> *blank
6.10 c                   eval      b_ksor  = *on
6.10 c                   endif
      *
      * Henter dagens dato
      *
6.20 c                   time                    w_ddat
     c                   eval      w_lage = l_lage
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vvarl2_key    setll     vvarl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
