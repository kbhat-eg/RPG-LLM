     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO701R
      *  Beskrivelse . . : Endring av antall/pris/linjebeløp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *  6.10      250110  BSA Nytt program
6.11  *  6.10      090611  Justert på referanse                     bof
6.nu  *  6.30  050614 bof  endring i forb. nummerutvidelse
6.30  * 6.30  040615 bkv  Bestilling økt fra 6 til 8
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
      *
      * Definering av variable i parametre
      *
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_line          s                   like(ldline)
     d p_anta          s                   like(ldanta)
     d p_inkp          s                   like(ldipny)
     d p_stat          s              1
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
      *
      * Definering av øvrige variable
      *
     d w_dato          s               d   datfmt(*iso)
     d w_inlr          s              1
     d w_lety          s              1  0
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_inpr          s              9  2
     d w_kopr          s              9  2
     d w_firm          s                   like(ldfirm)
     d w_enhe          s                   like(ldenh1)
     d w_lisu          s             11  2
     d w_dekn          s             11  2
     d w_grad          s             11  2
     d w_rabk          s             11  2
     d w_sumi          s             11  2
     d w_sumk          s             11  2
     d w_sumr          s             11  2
     d w_toti          s             11  2
     d w_totk          s             11  2
     d w_totr          s             11  2
     d w_anta          s                   like(ldanta)
     d w_ipny          s                   like(ldipny)
     d w_kpny          s                   like(ldkpny)
     d w_rab1          s                   like(ldrab1)
     d w_rab2          s                   like(ldrab2)
6.12 d w_raba          s                   like(ldsumi)
     d w_stat          s             12
6.12 d w_kfak          s              8  6
6.12 d w_osts          s              1
6.30 d w_numa          s              8
6.11 d w_lina          s              4
6.12  *
6.12  * Lagerjustering
6.12  *
6.12 d                 ds
6.nu d hlrec                        128
6.12 d  hlvare                       15    overlay(hlrec)
6.12 d  hllage                        2  0 overlay(hlrec:16)
6.12 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
6.12 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
6.12 d  hlotyp                        2    overlay(hlrec:36)
6.12 d  hlltyp                        2    overlay(hlrec:38)
6.12 d  hlanta                       11  3 overlay(hlrec:40)
6.12 d  hlkopr                        9  2 overlay(hlrec:51)
6.12 d  hlrefr                       20    overlay(hlrec:60)
6.12 d  hlsyst                        1    overlay(hlrec:80)
6.12 d  hlaltk                        2    overlay(hlrec:81)
6.12 d  hlrest                        1    overlay(hlrec:83)
6.12 d  hltype                        1    overlay(hlrec:84)
6.12 d  hlenhe                        3    overlay(hlrec:85)
6.12 d  hlinlr                        1    overlay(hlrec:88)
6.12 d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - les bestillingslinjer og rekalkuler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent opplysninger fra BESTILLING-HODE
      *
     c     lohel1_key    chain     lohel1r
     c                   if        not %found
     c                   eval      p_stat = '9'
     c                   goto      pgm_end
     c                   endif
      *
      *  Les BESTILLING-LINJER
      *
     c     lodtl1_key    chain     lodtl1r
     c                   if        not %found
     c                   eval      p_stat = '9'
     c                   goto      pgm_end
     c                   endif
      *
     c                   if        p_anta <> 0
      *
6.12  * Først korrigerer vi bort opprinnelig antall
6.12  *   (Legger over verdier i overførings-felter)
6.12  *
6.12 c                   eval      hlvare = ldvare
6.12 c                   eval      hllage = lolage
6.12 c                   eval      hlenhe = ldenh1
6.12 c                   eval      hldato = *loval
6.12 c                   eval      hltime = *loval
6.12 c                   eval      hlotyp = lootyp
6.12 c                   eval      hlltyp = ldltyp
6.12  *
6.12 c                   eval      hlanta = ldanta * -1
6.12 c                   eval      hlkopr = ldkpny
6.11 c                   move      ldnumm        w_numa
6.11 c                   move      ldline        w_lina
6.11 c                   eval      hlrefr = 'B:' + w_numa +
6.11 c                                      ' L:' + w_lina
6.12 c                   eval      hlonum = ldnumm
6.12 c                   eval      hlolin = ldline
6.12  *
6.12 c                   eval      hlsyst = 'L'
6.12 c                   eval      hlaltk = *blank
6.12 c                   eval      hlrest = *blank
6.12 c                   move      ldlety        hllety
6.12  *
6.12  * Kaller lageroppdaterings-program
6.12  *
6.12 c                   eval      w_stat = *blank
6.12  *
6.12 c                   call      'VL001R'
6.12 c                   parm                    w_stat
6.12 c                   parm                    hlrec
6.12  *
6.12  * Så legger vi til nytt antall
6.12  *   (Legger over verdier i overførings-felter)
6.12  *
6.12 c                   eval      hlvare = ldvare
6.12 c                   eval      hllage = lolage
6.12 c                   eval      hlenhe = ldenh1
6.12 c                   eval      hldato = *loval
6.12 c                   eval      hltime = *loval
6.12 c                   eval      hlotyp = lootyp
6.12 c                   eval      hlltyp = ldltyp
6.12  *
6.12 c                   eval      hlanta = p_anta
6.12 c                   eval      hlkopr = ldkpny
6.11 c                   move      ldnumm        w_numa
6.11 c                   move      ldline        w_lina
6.11 c                   eval      hlrefr = 'B:' + w_numa +
6.11 c                                      ' L:' + w_lina
6.12 c                   eval      hlonum = ldnumm
6.12 c                   eval      hlolin = ldline
6.12  *
6.12 c                   eval      hlsyst = 'L'
6.12 c                   eval      hlaltk = *blank
6.12 c                   eval      hlrest = *blank
6.12 c                   move      ldlety        hllety
6.12  *
6.12  * Kaller lageroppdaterings-program
6.12  *
6.12 c                   eval      w_stat = *blank
6.12  *
6.12 c                   call      'VL001R'
6.12 c                   parm                    w_stat
6.12 c                   parm                    hlrec
6.12  * Oppdater med kvantum
6.12 c     lodtlu_key    chain     lodtlu
6.12 c                   if        %found
6.12 c                   eval      ldanta = p_anta
      *
     c                   eval      ldeusr = l_user
     c                   time                    ldedat
     c                   time                    ldetim
      *
6.12 c                   update    lodtlur
6.12 c                   endif
      *
     c                   endif
      *
      * Hvis avvikende innkjøpspris må vi rekalkulrer kostpris på ordrelinja
      *
     c                   if        p_inkp  <> 0 and
     c                             ldipny  <> p_inkp
      *
6.12  * Finn påslagsfaktor
6.12  *
6.12 c                   eval      w_kfak = 0
6.12 c                   eval      w_osts = *blank
6.12  *
6.12 c                   call      'JV752R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    ldvare
6.12 c                   parm                    ldldor
6.12 c                   parm                    ldlety
6.12 c                   parm                    ldenh1
6.12 c                   parm                    w_kfak
6.12 c                   parm                    w_osts
      *
      *  Oppdater med nye priser
      *
6.12 c     lodtlu_key    chain     lodtlu
6.12 c                   if        %found
6.12 c                   eval(h)   ldipny = p_inkp
6.12 c                   eval(h)   ldkpny = p_inkp * w_kfak
      *
     c                   eval      ldeusr = l_user
     c                   time                    ldedat
     c                   time                    ldetim
      *
6.12 c                   update    lodtlur
     c                   endif
      *
     c                   endif
      *
      *  Så rekalkulerer linja (NB Betingelser osv skal ikke hentes inn på nytt)
      *
6.12 c     lodtlu_key    chain     lodtlu
6.12 c                   if        %found
6.12  *
6.12  * Beregning av totaler
6.12  *
6.12 c                   eval      w_sumi = ldipny * ldanta
6.12 c                   eval      w_sumr = 0
6.12  *
6.12  * Rabatt
6.12  *
6.12 c                   if        ldrab1 <> 0
6.12 c                   eval(h)   w_raba = (w_sumi - w_sumr) * ldrab1 / 100
6.12 c                   eval      w_sumr = w_sumr + w_raba
6.12 c                   endif
6.12  *
6.12 c                   if        ldrab2 <> 0
6.12 c                   eval(h)   w_raba = (w_sumi - w_sumr) * ldrab2 / 100
6.12 c                   eval      w_sumr = w_sumr + w_raba
6.12 c                   endif
6.12  *
6.12  * Legger inn totaler
6.12  *
6.12 c                   eval      ldsumi = w_sumi
6.12 c                   eval      ldsumr = w_sumr
6.12 c                   eval      ldsumk = ldkpny * ldanta
      *
     c                   eval      ldeusr = l_user
     c                   time                    ldedat
     c                   time                    ldetim
      *
6.12 c                   update    lodtlur
6.12  *
6.12 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_anta
     c                   parm                    p_inkp
     c                   parm                    p_stat
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for les av BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      *  Key for les av BESTILLING-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c                   eval      lodtl1_line = p_line
     c                   eval      lodtlu_numm = p_numm
     c                   eval      lodtlu_suff = p_suff
     c                   eval      lodtlu_line = p_line
      *
     c                   endsr
