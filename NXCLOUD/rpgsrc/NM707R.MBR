     h option(*nodebugio) datedit(*dmy)
     h alwnull(*usrctl)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM707R
      * Beskrivelse . . : Ehandel MGR, eksport av vare/lager
      *
      * Spesialversjon. : MGR Ehandelsløsning
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180316 ASK  Nytt program
6.31  * 6.30  211218 ask  Utvidet for trelast-sortiment
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
9.01  *       180118 ASK  Lagt inn nullstilling av verdier før prishenting
9.02  * 9.02  121117 ASK  Legger ut butikkid på filen
9.03  * 7.00  090421 ask  Legger ut 0 i beholdning om den er negativ
9.04  * 800   060223 ask  Skriver post bare dersom lage er et ebiz lager
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fcsodi2    if   e           k disk    rename(csodst:csodi2r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
9.04 fra91l2    if   e           k disk    rename(ra91pfr:ra91l2r)
      *
     fnetvarlag uf a e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
      *
      * Definering av pamametre
      *
     d p_firm          s              3  0
     d p_vare          s             15
8.01 d p_prgr          s              2
     d p_lage          s              2  0
     d p_lety          s              1  0
     d p_dato          s               d   datfmt(*iso)
     d p_enh1          s              3
     d p_sap1          s              9  2
     d p_bup1          s              9  2
     d p_enh2          s              3
     d p_sap2          s              9  2
     d p_bup2          s              9  2
     d p_omr2          s             12  6
     d p_enh3          s              3
     d p_sap3          s              9  2
     d p_bup3          s              9  2
     d p_omr3          s             12  6
6.31 d p_inlr          s              1
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         20
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                10    overlay(d_bgm: 5) inz('IMLDATAWHI')
     d  d_bgmfill2                    2    overlay(d_bgm:15) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:17) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - lager/vare
      *
     d                 ds
     d d_whi                        200
     d  d_whircd                      3    overlay(d_whi:  1) inz('WHI')
     d  d_whifill1                    1    overlay(d_whi:  4) inz(';')
     d  d_whilage                     2  0 overlay(d_whi:  5)
     d  d_whifill2                    1    overlay(d_whi:  7) inz(';')
     d  d_whivare                     8    overlay(d_whi:  8)
     d  d_whifill3                    1    overlay(d_whi: 16) inz(';')
     d  d_whildor                     6  0 overlay(d_whi: 17)
     d  d_whifill4                    1    overlay(d_whi: 23) inz(';')
     d  d_whivtyp                     1    overlay(d_whi: 24)
     d  d_whifill5                    1    overlay(d_whi: 25) inz(';')
     d  d_whiltid                     3  0 overlay(d_whi: 26)
     d  d_whifill6                    1    overlay(d_whi: 29) inz(';')
     d  d_whilsald                    8  0 overlay(d_whi: 30)
     d  d_whifill7                    1    overlay(d_whi: 38) inz(';')
     d  d_whilenh                     3    overlay(d_whi: 39)
     d  d_whifill8                    1    overlay(d_whi: 42) inz(';')
     d  d_whildato                    6  0 overlay(d_whi: 43)
     d  d_whifill9                    1    overlay(d_whi: 49) inz(';')
     d  d_whiltime                    6  0 overlay(d_whi: 50)
     d  d_whifill10                   1    overlay(d_whi: 56) inz(';')
     d  d_whiibest                    8  0 overlay(d_whi: 57)
     d  d_whifill11                   1    overlay(d_whi: 65) inz(';')
     d  d_whibdato                    6  0 overlay(d_whi: 66)
     d  d_whifill12                   1    overlay(d_whi: 73) inz(';')
     d  d_whienh1                     3    overlay(d_whi: 74)
     d  d_whifill13                   1    overlay(d_whi: 77) inz(';')
     d  d_whipri1                    10    overlay(d_whi: 78)
     d  d_whifill14                   1    overlay(d_whi: 88) inz(';')
     d  d_whienh2                     3    overlay(d_whi: 89)
     d  d_whifill15                   1    overlay(d_whi: 92) inz(';')
     d  d_whipri2                    10    overlay(d_whi: 93)
     d  d_whifill16                   1    overlay(d_whi:103) inz(';')
     d  d_whienh3                     3    overlay(d_whi:104)
     d  d_whifill17                   1    overlay(d_whi:107) inz(';')
     d  d_whipri3                    10    overlay(d_whi:108)
9.02 d  d_whifill18                   1    overlay(d_whi:118) inz(';')
9.02 d  d_whiorgid                   25    overlay(d_whi:119)
9.02 d  d_whifill19                   1    overlay(d_whi:144) inz(';')
9.02 d  d_whinobbnr                   8  0 overlay(d_whi:145)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d csodi2_dkod     s                   like(csdkod)
     d vlagl1_vare     s                   like(vlvare)
     d vvarlr_vare     s                   like(vvvare)
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
     d lodtlv_vare     s                   like(ldvare)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
9.04 d ra91l2_lage     s                   like(rrlage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
6.31 d w_ldor          s                   like(vvldor)
     d w_tell          s             12  0
     d w_tmst          s               z
     d w_dato          s               d
     d w_ldat          s               d
     d w_ltim          s               t
     d w_bdat          s               d
     d w_best          s              8  0
     d w_date          s               d   datfmt(*iso)
     d w_sapr          s              9
6.31 d w_lv_modn       s                   like(vvnmod)
6.31 d w_lv_nobb       s                   like(vvnobb)
6.31 d w_lv_lvar       s                   like(vvlvar)
6.31 d w_lv_nlev       s                   like(vvnlev)
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgr
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   time                    w_dato
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netvarlagr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netvarlagr
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netvarlagr
      *
      * Lager/vare
      * - leser lager grunnsortiment og legger ut poster på alle lager
      *
     c                   eval      csodi2_dkod = 'Grunnsortiment'
     c     csodi2_key    setll     csodi2
     c     csodi2_key    reade     csodi2
     c                   dow       %eof(csodi2) = *off
      *
     c                   eval      vlagl1_vare = csdvnu
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1r
     c                   dow       %eof(vlagl1) = *off
      *
      * Varetype og ledetid
      *
     c                   eval      d_whilage = vllage
     c                   eval      d_whivare = vlvare
     c                   eval      d_whivtyp = vlvtyp
     c                   eval      d_whiltid = vlltid
      *
      * Finner vare leverandør
      *
     c                   if        vlbldo > 0
     c                   eval      d_whildor = vlbldo
6.31 c                   eval      w_ldor = vlbldo
     c                   else
     c                   eval      vvarlr_vare = vlvare
     c     vvarlr_key    chain     vvarlrr
     c                   if        %found
     c                   eval      d_whildor = vvldor
6.31 c                   eval      w_ldor = vvldor
     c                   else
     c                   eval      d_whildor = 0
     c                   endif
     c                   endif
6.31  *
6.31  * Sjekker logistikkvare og finner nobbnummer
6.31  *
6.31 c                   exsr      sjekk_logi
6.31 c                   if        w_lv_nobb <> 0
6.31 c                   eval      d_whinobbnr = w_lv_nobb
6.31 c                   else
6.31 c                   eval      d_whinobbnr = vvnobb
6.31 c                   endif
      *
      * Lagersaldo og bestillingsinformasjon på lagervarer
      *
     c                   if        vlvtyp = 'L'
9.03 c                   if        vlbehl < 0
9.03 c                   eval      d_whilsald = 0
9.03 c                   else
     c                   eval(h)   d_whilsald = vlbehl
9.03 c                   endif
     c                   eval      d_whilenh = vlenhl
     c                   time                    w_ldat
     c     *ymd          move      w_ldat        d_whildato
     c                   time                    w_ltim
     c     *hms          move      w_ltim        d_whiltime
     c                   exsr      finn_best
     c                   else
     c                   eval      d_whilsald = 0
     c                   eval      d_whilenh = *blank
     c                   eval      d_whildato = 0
     c                   eval      d_whiltime = 0
     c                   eval      d_whiibest = 0
     c                   eval      d_whibdato = 0
     c                   endif
      *
      * Finner pris på aktuelt lager
      *
     c                   eval      p_firm = w_firm
     c                   eval      p_vare = vlvare
     c                   eval      p_lage = vllage
     c                   eval      p_lety = 0
     c                   eval      p_dato = w_date
     c                   eval      p_inlr = ' '
9.01  *
9.01  * Nullstiller verdier før henting av priser for å unngå feil på utfil
9.01  *
9.01 c                   eval      p_enh1 = *blank
9.01 c                   eval      p_enh2 = *blank
9.01 c                   eval      p_enh3 = *blank
9.01 c                   eval      p_sap1 = 0
9.01 c                   eval      p_sap2 = 0
9.01 c                   eval      p_sap3 = 0
      *
     c                   call      'VP717R   '
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enh1
     c                   parm                    p_sap1
     c                   parm                    p_bup1
     c                   parm                    p_enh2
     c                   parm                    p_sap2
     c                   parm                    p_bup2
     c                   parm                    p_omr2
     c                   parm                    p_enh3
     c                   parm                    p_sap3
     c                   parm                    p_bup3
     c                   parm                    p_omr3
     c                   parm                    p_inlr
      *
     c                   eval      d_whienh1 = p_enh1
     c                   move      p_sap1        w_sapr
     c                   eval      d_whipri1 = %subst(w_sapr:1:7) + ',' +
     c                                         %subst(w_sapr:8:2)
     c                   eval      d_whienh2 = p_enh2
     c                   move      p_sap2        w_sapr
     c                   eval      d_whipri2 = %subst(w_sapr:1:7) + ',' +
     c                                         %subst(w_sapr:8:2)
     c                   eval      d_whienh3 = p_enh3
     c                   move      p_sap3        w_sapr
     c                   eval      d_whipri3 = %subst(w_sapr:1:7) + ',' +
     c                                         %subst(w_sapr:8:2)
      *
9.04  * Skriver lagerpost  - sjekker om lager er ebiz lager
      *
9.04 c                   eval      ra91l2_lage = vllage
9.04 c     ra91l2_key    chain     ra91l2r
9.04 c                   if        %found and
9.04 c                             rrebiz = 0
9.02 c                   eval      d_whiorgid = aoheie
     c                   eval      data = d_whi
     c                   write     netvarlagr
     c                   eval      w_tell = w_tell + 1
9.04 c                   endif
      *
     c     vlagl1_key    reade     vlagl1r
     c                   enddo
      *
     c     les_neste     tag
     c     csodi2_key    reade     csodi2
     c                   enddo
      *
      *
      * Meldingshale
      *
     c     les_slutt     tag
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netvarlagr
      *
      *
      * Avslutt
      *
     c     avslutt       tag
      *
      * Et siste kall til VP717R for å stenge filer
      *
     c                   eval      p_inlr = '1'
      *
     c                   call      'VP717R   '
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enh1
     c                   parm                    p_sap1
     c                   parm                    p_bup1
     c                   parm                    p_enh2
     c                   parm                    p_sap2
     c                   parm                    p_bup2
     c                   parm                    p_omr2
     c                   parm                    p_enh3
     c                   parm                    p_sap3
     c                   parm                    p_bup3
     c                   parm                    p_omr3
     c                   parm                    p_inlr
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne første bestilling til et lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_best     begsr
      *
      *
      *  Legger inn startverdi for lesingen
      *
     c                   if        vlvare = '21667126'
     c                   eval      vlvare = vlvare
     c                   endif
     c                   movel     vlvare        lodtlv_vare
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlvr                                90
      *
     c                   dow       not %eof(lodtlv)
      *
      * Sjekk linjetype
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        vallag <> 1
     c                   goto      les_best
     c                   endif
      *
      * Sjekk leveringstype
      *
     c                   if        ldlety = 1
     c                   goto      les_best
     c                   endif
      *
      *  Hent bestillingshode informasjon
      *
     c                   eval      lohelr_numm = ldnumm
     c                   eval      lohelr_suff = ldsuff
     c     lohelr_key    chain     lohelr                             92
     c                   if        *in92 = *on
     c                   goto      les_best
     c                   endif
      *
      * Sjekk riktig ordretype og lageroppdateringskode
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1                             93
     c                   if        vaosys <> 1 or
     c                             vaoakk <> 1 or
     c                             vaolag <> 1
     c                   goto      les_best
     c                   endif
      *
      * Sjekk riktig lager
      *
     c                   if        lolage <> vllage
     c                   goto      les_best
     c                   endif
      *
      * Sjekk hvilken leveringsdato som er registrert
      *
     c                   if        ldldat <> *loval
     c                   eval      w_bdat = ldldat
     c                   else
     c                   if        lomoda <> *loval
     c                   eval      w_bdat = lomoda
     c                   else
     c                   if        loldat <> *loval
     c                   eval      w_bdat = loldat
     c                   else
     c                   eval      w_bdat = lobdat
     c                   endif
     c                   endif
     c                   endif
      *
     c*                  if        ldenh1 <> vvenhl
     c*                  eval      w_anta = ldanta
     c*                  eval      w_enh1 = ldenh1
     c*                  exsr      omr_antall
     c*                  else
     c                   eval(h)   w_best = ldanta
     c*                  endif
      *
     c                   if        vaokre = '-'
     c                   eval      w_best = w_best * -1
     c                   endif
      *
     c                   if        valkre = '-'
     c                   eval      w_best = w_best * -1
     c                   endif
      *
     c                   eval      d_whiibest = w_best
     c     *ymd          move      w_bdat        d_whibdato
      *
     c     les_best      tag
     c     lodtlv_key    reade     lodtlvr
     c                   enddo
      *
     c                   endsr
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å finne logistikk vare
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_logi    begsr
6.31  *
6.31  *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_nobb = 0
6.31 c                   eval      w_lv_modn = 0
6.31 c                   eval      w_lv_nlev = 0
6.31 c                   eval      w_lv_lvar =*blank
6.31 c/exec sql
6.31 c+ select vvlmod,
6.31 c+        vvlnob,
6.31 c+        vvllva,
6.31 c+        vvlnle
6.31 c+ into   :w_lv_modn,
6.31 c+        :w_lv_nobb,
6.31 c+        :w_lv_lvar,
6.31 c+        :w_lv_nlev
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :vlvare and
6.31 c+        vvlldo = :w_ldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for les på sortiment linjer
      *
     c     csodi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    csodi2_dkod
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
      *
      *  Key for les av BESTILLINGS-LINJE
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key les av BESTILLINGS-HODE
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av ORDRE-TYPE
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for les av LINJETYPE
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
9.04  *
9.04  * Key for les på regionskobling
9.04  *
9.04 c     ra91l2_key    klist
9.04 c                   kfld                    w_firm
9.04 c                   kfld                    ra91l2_lage
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
      *
     c                   endsr
