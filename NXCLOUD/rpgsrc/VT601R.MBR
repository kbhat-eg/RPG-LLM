     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VT601R
      * Beskrivelse . . : Tilbudspriser, utskrift
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140799 HKO  Nytt program
5.70  * PRGR  301108 bof  Utvidet med prisgruppe
6.11  *       080311 bof  feilretting på lagernr
      *
6.20  * 6.20  110511 NHO  Endring key på filer
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl9    if   e           k disk    rename(vvarpfr:vvarl9r)
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvt601p    o    e             printer
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                        10
5.70 d l_lage               1001   1002  0
      *
      *  Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_dato                         d   overlay(d_list:4) datfmt(*iso)
     d  d_kamp                        5    overlay(d_list:14)
     d  d_fper                         d   overlay(d_list:19) datfmt(*iso)
     d  d_tper                         d   overlay(d_list:29) datfmt(*iso)
     d  d_fogr                        2  0 overlay(d_list:39)
     d  d_fhgr                        2  0 overlay(d_list:41)
     d  d_fugr                        3  0 overlay(d_list:43)
     d  d_togr                        2  0 overlay(d_list:46)
     d  d_thgr                        2  0 overlay(d_list:48)
     d  d_tugr                        3  0 overlay(d_list:50)
6.11 d  d_lage                        2  0 overlay(d_list:54)
      *
      * Definering av parametre
      *
     d p_list          s             64
      *
      * Definering av variabler i nøkler
      *
     d vvarl9_ogrp     s                   like(vvogrp)
     d vvarl9_hgrp     s                   like(vvhgrp)
     d vvarl9_ugrp     s                   like(vvugrp)
6.20 d vtill1_ogrp     s                   like(vtogrp)
6.20 d vtill1_hgrp     s                   like(vthgrp)
6.20 d vtill1_ugrp     s                   like(vtugrp)
6.20 d vtill1_ldor     s                   like(vtldor)
     d vtill1_vare     s                   like(vtvare)
     d vtill1_prgr     s                   like(vtprgr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
6.21 d b_inlr          s              1    inz(*off)
     d b_tilb          s              1    inz(*off)
     d b_frst          s              1    inz(*on)
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_ldor_alf      s              6
     d w_kode          s              1  0
      *
     d w_firm          s                   like(vvfirm)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
     d w_ogrp_gml      s                   like(vvogrp)
     d w_hgrp_gml      s                   like(vvhgrp)
     d w_ugrp_gml      s                   like(vvugrp)
     d w_ldor_gml      s                   like(vvldor)
5.70 d w_lage          s              2  0
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
6.21 d w_vtyp          s                   like(vvvtyp)
6.21 d w_ldor          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initiering av header
      *
     c                   exsr      hode_init
      *
      * Behandler utskift
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_init     begsr
      *
     c                   eval      b_frst = *on
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1user = l_user
     c     *dmy          move      w_udat        a1date
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parameter informasjon
      *
     c                   eval      a2kamp = d_kamp
     c                   eval      a2fogr = d_fogr
     c                   eval      a2fhgr = d_fhgr
     c                   eval      a2fugr = d_fugr
     c                   eval      a2togr = d_togr
     c                   eval      a2thgr = d_thgr
     c                   eval      a2tugr = d_tugr
      *
     c                   if        d_dato <> *loval
     c                   eval      *in31 = *on
     c     *dmy          move      d_dato        a2dato
     c                   endif
      *
     c                   if        d_kamp <> *blank
     c                   eval      *in32 = *on
     c                   endif
      *
     c                   if        d_fper <> *loval
     c                   eval      *in33 = *on
     c     *dmy          move      d_fper        a2fper
     c                   endif
      *
     c                   if        d_tper <> *loval
     c     *dmy          move      d_tper        a2tper
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      vvarl9_ogrp = d_fogr
     c                   eval      vvarl9_hgrp = d_fhgr
     c                   eval      vvarl9_ugrp = d_fugr
     c     vvarl9_key    setll     vvarl9
     c                   read      vvarl9                                 90
     c                   dow       *in90 = *off
      *
      * Test på om varen skal være med
      *
     c                   if        vvfirm <> d_firm
     c                   leave
     c                   endif
      *
     c                   if        d_togr <> 0 and
     c                             vvogrp > d_togr
     c                   leave
     c                   endif
      *
     c                   if        d_thgr <> 0 and
     c                             vvogrp = d_togr and
     c                             vvhgrp > d_thgr
     c                   leave
     c                   endif
      *
     c                   if        d_tugr <> 0 and
     c                             vvogrp = d_togr and
     c                             vvhgrp = d_thgr and
     c                             vvugrp > d_tugr
     c                   leave
     c                   endif
      *
      * Sjekker om det finnes tilbudspriser på varen som passer til
      * utplukket
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
6.21 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   eval      b_tilb = *off
      *
     c                   eval      vtill1_vare = vvvare
     c                   eval      vtill1_prgr = w_prgr
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1                                 91
     c                   dow       *in91 = *off
      *
     c                   if        d_dato <> *loval and
     c                             vtfdat <= d_dato and
     c                             vttdat >= d_dato
     c                   eval      b_tilb = *on
     c                   leave
     c                   endif
      *
     c                   if        d_kamp <> *blank and
     c                             vtkamp = d_kamp
     c                   eval      b_tilb = *on
     c                   leave
     c                   endif
      *
     c                   if        d_fper <> *loval and
     c                             vtfdat = d_fper and
     c                             vttdat = d_tper
     c                   eval      b_tilb = *on
     c                   leave
     c                   endif
      *
     c     vtill1_key    reade     vtill1                                 91
     c                   enddo
      *
      * Avbryter dersom tilbudspris ikke ble funnet på varen
      *
     c                   if        b_tilb = *off
     c                   goto      les_neste
     c                   endif
      *
      * Behandler brudd på varegruppe og leverandør
      *
     c                   if        vvogrp <> w_ogrp_gml
     c                   exsr      hode_ogrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml
     c                   exsr      hode_hgrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml
     c                   exsr      hode_ugrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml or
     c                             vvldor <> w_ldor_gml
     c                   exsr      hode_ldor
     c                   endif
      *
      * Leser tilbudspriser tilhørende vare, og skriver detaljlinje for
      * alle tilbudspriser som passer til utplukket
      *
     c                   eval      vtill1_vare = vvvare
     c                   eval      vtill1_prgr = w_prgr
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1                                 91
     c                   dow       *in91 = *off
      *
     c                   if        d_dato <> *loval and
     c                             vtfdat <= d_dato and
     c                             vttdat >= d_dato
     c                   exsr      detalj
     c                   endif
      *
     c                   if        d_kamp <> *blank and
     c                             vtkamp = d_kamp
     c                   exsr      detalj
     c                   endif
      *
     c                   if        d_fper <> *loval and
     c                             vtfdat = d_fper and
     c                             vttdat = d_tper
     c                   exsr      detalj
     c                   endif
      *
     c     vtill1_key    reade     vtill1                                 91
     c                   enddo
      *
      * Tar vare på brudd-variable
      *
     c                   eval      w_ogrp_gml = vvogrp
     c                   eval      w_hgrp_gml = vvhgrp
     c                   eval      w_ugrp_gml = vvugrp
     c                   eval      w_ldor_gml = vvldor
      *
     c     les_neste     tag
      *
     c                   read      vvarl9                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ogrp     begsr
      *
     c                   eval      w_otxt = *blank
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
     c                   write     a1hdr
      *
     c                   if        b_frst = *on
     c                   eval      b_frst = *off
     c                   write     a2hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_hgrp     begsr
      *
     c                   eval      w_htxt = *blank
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ugrp     begsr
      *
     c                   eval      w_utxt = *blank
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c                   move      vvogrp        w_ogrp_alf
     c                   move      vvhgrp        w_hgrp_alf
     c                   move      vvugrp        w_ugrp_alf
      *
     c                   eval      a3txt1 = %trim( %trim(w_otxt) + ' - ' +
     c                                      %trim(w_htxt) + ' - ' +
     c                                      %trim(w_utxt) + ' (' +
     c                                      w_ogrp_alf + ' ' +
     c                                      w_hgrp_alf + ' ' +
     c                                      w_ugrp_alf + ')')
      *
     c                   eval      a3txt2 = a3txt1
      *
     c                   write     a3hdr                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på leverandøre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ldor     begsr
      *
     c                   eval      w_lnav = *blank
      *
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lnav = rlnavn
     c                   endif
      *
     c                   move      vvldor        w_ldor_alf
      *
     c                   eval      a4txt1 = %trim( %trim(w_lnav) + ' (' +
     c                                      w_ldor_alf + ')')
      *
     c                   eval      a4txt2 = a4txt1
      *
     c                   write     a4hdr                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1vare = vvvare
5.70 c                   eval      d1prgr = vtprgr
6.30 c                   if        w_lv_nobb > 0
6.30 c                   eval      d1nobb = w_lv_nobb
6.30 c                   else
     c                   eval      d1nobb = vvnobb
6.30 c                   endif
6.21 c                   eval      d1vtyp = w_vtyp
     c                   eval      d1enh1 = vtenh1
     c                   eval      d1tipr = vttip1
     c                   eval      d1tiim = 0
      *
      * Beregner salgspris inkl. mva
      *
     c                   eval      d1tiim = d1tipr * w_mvat
      *
     c                   if        d1tiim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d1tiim
     c                   endif
      *
      * Skriv detaljlinje enhet-1
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
      * Enhet-2
      *
     c                   if        vtenh2 <> *blank or
     c                             vttip2 <> 0
      *
     c                   eval      d2enh2 = vtenh2
     c                   eval      d2tipr = vttip2
     c                   eval      d2tiim = 0
      *
     c                   eval      d2tiim = d2tipr * w_mvat
      *
     c                   if        d2tiim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d2tiim
     c                   endif
      *
     c                   write     d2dtl                                30
     c                   exsr      overflow
      *
     c                   endif
      *
      * Enhet-3
      *
     c                   if        vtenh3 <> *blank or
     c                             vttip3 <> 0
      *
     c                   eval      d3enh3 = vtenh3
     c                   eval      d3tipr = vttip3
     c                   eval      d3tiim = 0
      *
     c                   eval      d3tiim = d3tipr * w_mvat
      *
     c                   if        d3tiim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d3tiim
     c                   endif
      *
     c                   write     d3dtl                                30
     c                   exsr      overflow
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på vareregister
      *
     c     vvarl9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
     c                   kfld                    vvarl9_ugrp
      *
      * Key for les på tilbudspriser
      *
     c     vtill1_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vtill1_ogrp
6.20 c                   kfld                    vtill1_hgrp
6.20 c                   kfld                    vtill1_ugrp
6.20 c                   kfld                    vtill1_ldor
     c                   kfld                    vtill1_vare
     c                   kfld                    vtill1_prgr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe-register
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = d_firm
      *
      * Henter mva-faktor i.h.t. dagens dato
      *
     c                   time                    w_udat
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
     c                   if        d_lage <> 0
5.70 c                   eval      w_lage = d_lage
5.70 c                   eval      w_avde = 0
5.70  *
5.70 c                   call      'VL712R   '
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70 c                   else
5.70 c                   eval      w_lage = l_lage
5.70 c                   eval      w_prgr = *blank
     c                   endif
      *
      *
     c                   endsr
