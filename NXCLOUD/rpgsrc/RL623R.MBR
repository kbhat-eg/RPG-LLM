      /TITLE  Utskrift av leverandøretiketter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL023R                                        *
      * Beskrivelse . . : Utskrift av leverandøretiketter               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * R5.00     270902  Bruker eksterntdefinert printerfil.      HFL
      * R5.20     131003  Utelater passive leverandører            KOB  *
      * R5.40     260505  Lagt inn utskrift til laser              KOB  *
T5.40 * R5.40     161205  Feil når 3 i bredden, er rettet          HFL  *
T5.40 * R5.40     190406  Eget format for laser-etiketter          HFL  *
T5.50 * R5.50     220607  Kan velge inntil 21 enkeltleverandører   HFL  *
6.20  * V6.20     080907  Endret lengde på distrikt                NHO  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlavl1r)
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
     frl623p    o    e             printer oflind(*in30)
      *
      *
      *************************  T A B E L L E R  ************************
      *
     d f               s              1    dim(30)
     d lnr             s              6  0 dim(3)                               Lev.nr
     d lna             s             30    dim(3)                               Navn
     d la1             s             30    dim(3)                               Adresse 1
     d la2             s             30    dim(3)                               Adresse 2
     d lst             s             30    dim(3)                               Poststed
     d lpe             s             30    dim(3)                               Kontaktperson
      *
T5.40 * Definering av variable
      *
T5.40d w_lper          s             30
      *
     d                 ds
     d w_pdat                         6  0
     d  w_pdag                        2  0 overlay(w_pdat)
     d  w_pmnd                        2  0 overlay(w_pdat:3)
     d  w_paar                        2  0 overlay(w_pdat:5)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
T5.50 *RESKONTRO - Datastruktur for parameter
     d                uds
     d pdato                 300    305  0                                      Pr. dato
     d paarf                          4  0                                      Pr. år
     d paart                          4  0                                      Pr. år
     d pperf                          2  0                                      Periode f.o.m.
     d ppert                          2  0                                      Periode t.o.m.
     d pavdf                          4  0                                      Avdeling f.o.m.
     d pavdt                          4  0                                      Avdeling t.o.m.
     d pkatf                          4  0                                      Kategori f.o.m.
     d pkatt                          4  0                                      Kategori t.o.m.
     d pself                          4  0                                      Selger f.o.m.
     d pselt                          4  0                                      Selger t.o.m.
     d pknrf                          6  0                                      Nr. f.o.m.
     d pknrt                          6  0                                      Nr. t.o.m.
     d kun                            6  0 dim(21)                              Tabell-enkeltkunder
6.20 d*pdisf                          2  0                                      Distrikt f.o.m.
6.20 d pdisf                          4  0                                      Distrikt f.o.m.
6.20 d*pdist                          2  0                                      Distrikt t.o.m.
6.20 d pdist                          4  0                                      Distrikt t.o.m.
     d plagf                          2  0                                      Lager f.o.m
     d plagt                          2  0                                      Lager t.o.m
     d psagr                          1                                         Saldotest(J/N)
     d psabe                          7  0                                      Saldogrense i kroner
     d pktid                          1                                         Kredittest(J/N)
     d pktdg                          5  0                                      Kreditgrense i dager
     d pkgrn                          1                                         Kredit overskredet
     d pedat                         10d   datfmt(*iso)                         Endringsdato
     d ppass                          1                                         Passive
      *
      *  Hva skal skrives på liste                                Valg: J/N=Ja/N
     d pvalu                          1                                         Valutabeløp
     d pkper                          1                                         K.person
     d ptlf                           1                                         Tlf.nr
     d pkomp                          1                                         Komprimert
     d psald                          1                                         Kun saldo
     d pkjop                          1                                         Kun saldo
      *
      *  Hvordan skal liste sorteres                              Valg: J/N=Ja/N
     d pkats                          1                                         Kategori
     d pavds                          1                                         Avdeling
     d psels                          1                                         Selger
     d psort                          1                                         Alfa/Numerisk
     d petik                          1  0                                      Etikettype
     d ptype                          1                                         Listetype
     d prabk                          1                                         Rabattkategori
     d ppsla                          1                                         Påslagstabell
     d pssal                          1                                         Stigende saldo
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920                                         Bruker
     d l_firm                944    946  0                                      Firma
     d l_fnav                951    980                                         Navn
      *
     d n               s              2  0
T5.50d m               s              2  0
     d wantre          s              5  0
     d wantdl          s              5  0
     d w_alfa          s                   like(rlalfa)
     d w_firm          s                   like(rlfirm)
     d w_levr          s                   like(rllevr)
T5.50d tstkt           s              7  0
      *
      *
      * Les leverandørregister
      * avhengig av valg num./alfa
      *
     c     lesk          tag
      *
     c                   if        psort = ' '                                           .....
     c                   read      rlevl1                                 10                 .
     c                   else                                                                .
     c                   read      rlevl2                                 10                 .
     c                   endif                                                           .....
      *
     c                   if        rlfirm > w_firm                                       .....
     c                   eval      *in10 = *on                                               .
     c                   endif                                                           .....
      *
     c                   if        *in10 = *on
     c                   goto      slutt                                        FERDIG
     c                   endif
      *
      * Sjekk om passiv
      *
     c                   if        ppass  = ' ' and                                      .....
     c                             rlpass = 'J'                                          .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk lev.nr. fra/til
      *
     c                   if        rllevr < pknrf or                                     .....
     c                             rllevr > pknrt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        rlavde < pavdf or                                     .....
     c                             rlavde > pavdt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk leverandørkategori fra/til
      *
     c                   if        rlkatg < pkatf or                                     .....
     c                             rlkatg > pkatt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk lager fra/til
      *
     c                   if        rllagr < plagf or                                     .....
     c                             rllagr > plagt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk distrikt fra/til
      *
     c                   if        rldist < pdisf or                                     .....
     c                             rldist > pdist                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
T5.50 * Sjekk om enkeltleverndører er valgt
      *
T5.50c                   if        tstkt = *zeros                                        .....
T5.50c                   goto      skriv_levr                                                .
T5.50c                   endif                                                           .....
      *
T5.50 * Leverandørnr. ligger utenfor fra - til
T5.50 * sjekk mot tabell
      *
T5.50c                   eval      m = 1
T5.50c                   dou       m > 21                                                .....
      *                                                                 .
T5.50c                   if        kun(m) <> *zeros                                          .
      *                                                                 .
T5.50c                   if        rllevr = kun(m)                                           .
T5.50c                   goto      skriv_levr                                                .
T5.50c                   endif                                                               .
      *                                                                 .
T5.50c                   endif                                                               .
      *                                                                 .
T5.50c                   eval      m = m + 1                                                 .
T5.50c                   enddo                                                           .....
      *
T5.50 * Konto heller ikke i tabell -
T5.50 * les neste postering
      *
T5.50c                   goto      neste
      *
T5.50c     skriv_levr    tag
      *
     c                   eval      n = n + 1
     c                   move      rllevr        lnr(n)
     c                   movel     rlnavn        lna(n)
     c                   movel     rlgate        la1(n)
     c                   movel     rladr2        la2(n)
     c                   movel     rlsted        lst(n)
      *
      * Skal kontaktperson skrives?
      *
T5.40c                   if        pkper  = 'J' and                                      .....
T5.40c                             rlpers <> *blank                                      .....
T5.40c                   eval      w_lper = 'Attn. ' +
T5.40c                                       %subst(rlpers:1:30)
T5.40c                   else                                                                .
T5.40c                   eval      w_lper = *blank
T5.40c                   endif                                                           .....
E5.40c                   movel     w_lper        lpe(n)
      *
      * Skriv enkeltetiketter
      *
     c                   eval      lnr1  = lnr(1)
     c                   eval      lna1  = lna(1)
     c                   eval      la11  = la1(1)
     c                   eval      la21  = la2(1)
     c                   eval      lst1  = lst(1)
T5.40c                   eval      lpe1  = lpe(1)
      *
     c                   if        petik = 1                                             .....
     c                   eval      wantdl = wantdl + 1
     c                   write     mlapp1                                                    .
     c                   exsr      xnull                                                     .
     c                   endif                                                           .....
      *
      * Skriv 3 i bredden eller laser
      *
E5.40c                   if        petik = 3                                             .....
E5.40c                             and n = 3 or                                              .
     c                             petik = 4                                             .....
     c                             and n = 3                                                 .
     c                   eval      lnr2  = lnr(2)
     c                   eval      lna2  = lna(2)
     c                   eval      la12  = la1(2)
     c                   eval      la22  = la2(2)
     c                   eval      lst2  = lst(2)
T5.40c                   eval      lpe2  = lpe(2)
      *
     c                   eval      lnr3  = lnr(3)
     c                   eval      lna3  = lna(3)
     c                   eval      la13  = la1(3)
     c                   eval      la23  = la2(3)
     c                   eval      lst3  = lst(3)
T5.40c                   eval      lpe3  = lpe(3)
     c                   eval      wantdl = wantdl + n
      *
T5.40c                   if        petik = 3                                             .....
     c                   write     mlapp3                                                    .
T5.40c                   else                                                            .....
      *
T5.40 * Sjekker om overflow
      *
T5.40c                   if        *in30 = *on                                               .
T5.40c                   write     head                                                      .
T5.40c                   eval      *in30 = *off                                              .
T5.40c                   endif                                                           .....
      *
T5.40c                   write     mlapp4                                                    .
T5.40c                   endif                                                           .....
     c                   exsr      xnull                                                     .
     c                   endif                                                           .....
      *
     c     neste         tag
      *
     c                   eval      wantre = wantre + 1
      *
     c                   goto      lesk
      *
     c     slutt         tag
      *
      * Skriv gjenstående etiketter
      *
     c                   if        petik = 3                                             .....
E5.40c                             and n > *zeros or                                         .
T5.40c                             petik = 4                                             .....
T5.40c                             and n > *zeros                                            .
E5.40c                   if        n     = 2                                             .....
     c                   eval      lnr2  = lnr(2)
     c                   eval      lna2  = lna(2)
     c                   eval      la12  = la1(2)
     c                   eval      la22  = la2(2)
     c                   eval      lst2  = lst(2)
T5.40c                   eval      lpe2  = lpe(2)
     c                   else                                                            .....
     c                   eval      lnr2  = *zeros
     c                   eval      lna2  = *blanks
     c                   eval      la12  = *blanks
     c                   eval      la22  = *blanks
     c                   eval      lst2  = *blanks
T5.40c                   eval      lpe2  = *blanks
     c                   endif                                                           .....
      *
     c                   eval      lnr3  = *zeros
     c                   eval      lna3  = *blanks
     c                   eval      la13  = *blanks
     c                   eval      la23  = *blanks
     c                   eval      lst3  = *blanks
T5.40c                   eval      lpe3  = *blanks
      *
     c                   eval      wantdl = wantdl + n
T5.40c                   if        petik = 3                                             .....
     c                   write     mlapp3                                                    .
T5.40c                   else                                                            .....
      *
T5.40 * Sjekker om overflow
      *
T5.40c                   if        *in30 = *on                                               .
T5.40c                   write     head                                                      .
T5.40c                   eval      *in30 = *off                                              .
T5.40c                   endif                                                           .....
      *
T5.40c                   write     mlapp4                                                    .
T5.40c                   endif                                                           .....
     c                   exsr      xnull                                                     .
     c                   endif                                                           .....
      *
     c                   eval      *inlr = *on
      *
     clr                 if        petik = 1                                             .....
     clr                 write     total1                                                    .
     clr                 else                                                                .
     clr                 write     total1                                                    .
     clr                 endif                                                           .....
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for nullstilling av tabeller                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnull         begsr
      *                                                                 .
      * Nullstiller                                                     .
      *                                                                 .
     c                   eval      n = *zeros                                                .
     c                   eval      lnr = *zeros                                              .
     c                   eval      lna = *blanks                                             .
     c                   eval      la1 = *blanks                                             .
     c                   eval      la2 = *blanks                                             .
     c                   eval      lst = *blanks                                             .
     c                   eval      lpe = *blanks                                             .
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Sette startverdier
      *
     c                   if        petik = 1 or                                          .....
     c                             petik = 3                                             .....
     c                   write     linst1                                                    .
     c                   endif                                                           .....
      *
     c                   move      l_firm        w_firm
     c                   eval      w_levr = *zeros
     c                   eval      w_alfa = *blank
T5.50c                   xfoot     kun           tstkt
      *
     c                   if        psort = ' '                                           .....
     c     key01         setll     rlevl1                                                    .
     c                   else                                                                .
     c     key02         setll     rlevl2                                                    .
     c                   endif                                                           .....
      *
      * Key for leverandørregister
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_levr
      *
      * Key for leverandørregister
      * via alfafeltet
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    w_alfa
     c                   kfld                    w_levr
      *
T5.40 * Posisjonerer første linje
      *
T5.40c                   if        petik = 4                                             .....
T5.40c                   write     head                                                      .
T5.40c                   endif                                                           .....
      *
     c                   endsr
0063  /eject
      *
