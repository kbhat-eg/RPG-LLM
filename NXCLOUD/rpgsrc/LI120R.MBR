      /TITLE  ASOFAK EDI-Faktura, spørring - parameter
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LI120R
      * Beskrivelse . . : EDI-Faktura, spørring - parameter (loggprogram)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050315 bsa  Nytt program. Tatt utgangspunkt i spesial fra MGR
6.30  * 6.30  250615 bsa  Sjekker mot gyldige meldingstyper.
7.xx  * 7.00  030316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  150819 bsa  Takler bredere skjermbilder
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fli120d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_dato_alf      s             10
      *
     d w_firm          s                   like(rlfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer bildet
      *
     c                   time                    w_dato
     c                   move      w_dato        w_dato_alf
     c                   movel     w_dato_alf    b1aarr
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller spørreprogram
      *
     c                   call      'LI121R'
     c                   parm                    w_firm
     c                   parm                    b1aarr
     c                   parm                    b1levn
     c                   parm                    b1refn
     c                   parm                    b1type
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LEVN'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1levn
     c                   parm                    b1lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1lnav = *blank
      *
      * År
      *
     c                   if        b1aarr = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1levn <> 0
     c                   eval      rlevl1_levr = b1levn
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
      * Type
      *
     c                   eval      *in34  = *off
6.30 c                   if        b1type <> *blanks and
6.30 c                             b1type <> 'OBKR'  and
6.30 c                             b1type <> 'FAKT'
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rleVl1_levr
      *
      *
      * Henter firmakode fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
