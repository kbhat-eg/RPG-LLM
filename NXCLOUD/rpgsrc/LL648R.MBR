      /TITLE  Utskrift av omløpshastighet
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LL648R
      * Beskrivelse . . : Utskrift av omløpshastighet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           150900  Nytt program                             KOB
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwa1l1    if   e           k disk    rename(lwa1pfr:lwa1l1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fsstalb    if   e           k disk    rename(sstapfr:sstalbr)
     fsistlb    if   e           k disk    rename(sistpfr:sistlbr)
      *
     fll648p    o    e             printer
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        6  0 overlay(p_prec:41)
     d  p_sort                        1  0 overlay(p_prec:45)
     d  p_brud                        1  0 overlay(p_prec:46)
     d  p_vlin                        1  0 overlay(p_prec:47)
     d  p_lagi                        1  0 overlay(p_prec:48)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d sstalb_paar     s                   like(sfpaar)
     d sstalb_vare     s                   like(sfvare)
     d sstalb_bida     s                   like(sfbida)
     d sistlb_paar     s                   like(sipaar)
     d sistlb_vare     s                   like(sivare)
     d sistlb_bida     s                   like(sibida)
      *
      * Definering av variable
      *
     d w_ant3l         s             11  2
     d w_ant3s         s             11  2
     d w_kos3s         s             11  2
     d w_kos3k         s             11  2
      *
     d w_parm          s            128
     d w_firm          s                   like(vvfirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_dato          s              6  0
     d w_dmnd          s              2  0
     d w_daar          s              4  0
     d w_faar          s              4  0
     d w_vare          s              8  0
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_tell          s              3  0
     d w_forste        s              1  0
     d w_anta_salg     s             11  2
     d w_kost_salg     s             11  2
     d w_anta_kjop     s             11  2
     d w_kost_kjop     s             11  2
      *
     d g_vare          s                   like(mlvare)
     d g_ogrp          s                   like(vgoogr)
     d g_hgrp          s                   like(vghhgr)
     d g_ugrp          s                   like(vguugr)
     d g_lage          s                   like(mllage)
      *
     d b_feil          s              1    inz(*off)
     d b_init          s              1    inz(*on)
      *
      * - - - - - - - - - -
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * A2HDR  - Overgruppe heading
      * A3HDR  - Hovedgruppe heading
      * A4HDR  - Undergruppe heading
      * A6HDR  - Lager heading
      * D1DET  - Detail pr vare/lager
      * T1SUM  - Sum pr vare
      * T2SUM  - Sum pr overgruppe
      * T3SUM  - Sum pr hovedgruppe
      * T4SUM  - Sum pr undergruppe
      * T6SUM  - Sum pr lager
      * T7SUM  - Sum pr firma
      *
      * Skriv første heading
      *
     c                   if        p_sort <> 2
     c                   write     a1hdr                                30
     c                   endif
      *
      * Lese file
      *
     c                   eval      g_lage = *hival
     c                   eval      g_ogrp = *hival
     c                   eval      g_hgrp = *hival
     c                   eval      g_ugrp = *hival
      *
     c                   read      lwa1l1                                 90
      *
     c                   dow       *in90 = *off
      *
      * Ikke skriv summer på første
      *
     c                   if        w_forste = 1
      *
      * Sjekk om sortert på vare/lager
      *
     c                   if        p_sort = 1 or
     c                             p_sort = 2 or
     c                             p_sort = 3
     c                   if        mlvare <> w_vare
     c                   exsr      sum_t1
     c                   endif
     c                   endif
     c/space 3
      *
      * Sjekk om sortert på varegruppe/Varenummer/Lager
      *
     c                   if        p_sort = 2
      *
      * Brudd på undergruppe ?
      *
     c                   if        mlogrp <> g_ogrp or
     c                             mlhgrp <> g_hgrp or
     c                             mlugrp <> g_ugrp
     c                   exsr      sum_t2
     c                   endif
      *
      * Brudd på hovedgruppe ?
      *
     c                   if        mlogrp <> g_ogrp or
     c                             mlhgrp <> g_hgrp
     c                   exsr      sum_t3
     c                   endif
      *
      * Brudd på overgruppe ?
      *
     c                   if        mlogrp <> g_ogrp
     c                   exsr      sum_t4
     c                   endif
      *
     c                   endif
     c/space 3
     c/space 3
      *
      * Sjekk om sortert på lager
      *
     c                   if        p_sort  = 3
     c                   if        mllage <> g_lage
     c                   exsr      sum_t6
     c                   endif
     c                   endif
     c/space 3
      *
      * Sjekk om sortert på lager/varegruppe/varenummer
      *
     c                   if        p_sort = 4
      *
      * Brudd på lager ?
      *
     c                   if        mllage <> g_lage
      *
     c                   exsr      sum_t2
     c                   exsr      sum_t3
     c                   exsr      sum_t4
     c                   exsr      sum_t6
      *
     c                   else
      *
      * Brudd på undergruppe ?
      *
     c                   if        mlogrp <> g_ogrp or
     c                             mlhgrp <> g_hgrp or
     c                             mlugrp <> g_ugrp
     c                   exsr      sum_t2
     c                   endif
      *
      * Brudd på hovedgruppe ?
      *
     c                   if        mlogrp <> g_ogrp or
     c                             mlhgrp <> g_hgrp
     c                   exsr      sum_t3
     c                   endif
      *
      * Brudd på overgruppe ?
      *
     c                   if        mlogrp <> g_ogrp
     c                   exsr      sum_t4
     c                   endif
      *
     c                   endif
      *
     c                   endif
     c/space 3
      *
     c                   endif
      *
     c                   eval      w_forste = 1
     c/eject
      *
      * Brudd på lager ?
      *
     c                   if        p_sort  = 3 or
     c                             p_sort  = 4
     c                   if        mllage <> g_lage
     c                   exsr      hed_a6
     c                   endif
     c                   endif
      *
      * Sjekk om sortert på varegruppe
      *
     c                   if        p_sort = 2 or
     c                             p_sort = 4
      *
      * Brudd på overgruppe ?
      *
     c                   if        mlogrp <> g_ogrp
     c                   if        p_sort = 2
     c                   exsr      overflow
     c                   endif
     c                   exsr      hed_a2
     c                   endif
      *
      * Brudd på hovedgruppe ?
      *
     c                   if        mlogrp <> g_ogrp or
     c                             mlhgrp <> g_hgrp
     c                   exsr      hed_a3
     c                   endif
      *
      * Brudd på undergruppe ?
      *
     c                   if        mlogrp <> g_ogrp or
     c                             mlhgrp <> g_hgrp or
     c                             mlugrp <> g_ugrp
     c                   exsr      hed_a4
     c                   endif
      *
     c                   endif
      *
      * Utskrift av detaljer
      *
     c                   movel     mlvare        d1vare
     c                   movel     mlvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      d1tek1 = vvtek1
     c                   else
     c                   eval      d1tek1 = 'Ukjent vare'
     c                   endif
      *
      * Hent salg siste 12 mnd fra statistikk
      *
     c                   exsr      finn_salg
      *
      * Hent innkjøp siste 12 mnd fra statistikk
      *
     c                   exsr      finn_innkj
      *
     c                   eval      d1lage = mllage
     c                   eval      d1enhl = vvenhl
     c                   eval      d1enhe = vvenhe
      *
      * Beregninger
      *
     c                   eval      d1ants = w_anta_salg
     c                   eval      d1antl = mlbehl
     c                   eval(h)   d1lagv = ((mlbehl * mlkpri) + w_kost_salg -
     c                                    w_kost_kjop)/2
     c                   eval      d1salv = w_kost_salg
      *
     c                   if        d1lagv > 0
     c                   if        d1salv / d1lagv > 99999,99
     c                   eval      d1omlh = 99999,99
     c                   else
     c                   eval(h)   d1omlh = d1salv / d1lagv
     c                   endif
     c                   else
     c                   eval      d1omlh = *zeros
     c                   endif
      *
     c                   if        p_vlin = 0
      *
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
     c                   endif
      *
     c                   eval      g_vare = mlvare
     c                   eval      g_lage = mllage
     c                   eval      g_ogrp = mlogrp
     c                   eval      g_hgrp = mlhgrp
     c                   eval      g_ugrp = mlugrp
      *
      * Summer innen varenummer
      *
     c                   if        p_sort = 1 or
     c                             p_sort = 2
     c                   eval      w_tell = w_tell + 1
     c                   eval      w_vare = mlvare
      *
     c                   eval      t1ants = t1ants + d1ants
     c                   eval      t1antl = t1antl + d1antl
     c                   eval      t1lagv = t1lagv + d1lagv
     c                   eval      t1salv = t1salv + d1salv
     c                   endif
      *
      * Summer innen varegruppe
      *
     c                   if        p_sort = 2 or
     c                             p_sort = 3
      *
     c                   eval      t2ants = t2ants + d1ants
     c                   eval      t2antl = t2antl + d1antl
     c                   eval      t2lagv = t2lagv + d1lagv
     c                   eval      t2salv = t2salv + d1salv
      *
     c                   eval      t3ants = t2ants + d1ants
     c                   eval      t3antl = t3antl + d1antl
     c                   eval      t3lagv = t2lagv + d1lagv
     c                   eval      t3salv = t2salv + d1salv
      *
     c                   eval      t4ants = t4ants + d1ants
     c                   eval      t4antl = t4antl + d1antl
     c                   eval      t4lagv = t4lagv + d1lagv
     c                   eval      t4salv = t4salv + d1salv
     c                   endif
      *
      * Summer innen lager
      *
     c                   if        p_sort = 3 or
     c                             p_sort = 4
     c                   eval      t6ants = t6ants + d1ants
     c                   eval      t6antl = t6antl + d1antl
     c                   eval      t6lagv = t6lagv + d1lagv
     c                   eval      t6salv = t6salv + d1salv
     c                   endif
      *
      * Summer innen firma
      *
     c                   eval      t7ants = t7ants + d1ants
     c                   eval      t7antl = t7antl + d1antl
     c                   eval      t7lagv = t7lagv + d1lagv
     c                   eval      t7salv = t7salv + d1salv
      *
      * Nullstill
      *
     c                   eval      d1ants = *zeros
     c                   eval      d1antl = *zeros
     c                   eval      d1lagv = *zeros
     c                   eval      d1salv = *zeros
      *
     c                   read      lwa1l1                                 90
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
      * Sjekk om sortert på varegruppe
      *
     c                   if        p_sort = 2 or
     c                             p_sort = 4
      *
     c                   exsr      sum_t2
      *
     c                   exsr      sum_t3
      *
     c                   exsr      sum_t4
      *
     c                   endif
      *
      * Sjekk om sortert på lager
      *
     c                   if        p_sort = 3 or
     c                             p_sort = 4
     c                   exsr      sum_t6
      *
     c                   endif
      *
      * Firmatotal
      *
     c                   exsr      sum_t7
      *
     c                   eval      *inlr = *on
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv sum varenummer (Flere lager)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_t1        begsr
      *
     c                   if        w_tell > 1
     c                             and p_sort < 3
     c                   eval      t1vare = g_vare
      *
     c                   if        t1lagv > 0
     c                   if        t1salv / t1lagv > 99999,99
     c                   eval      t1omlh = 99999,99
     c                   else
     c                   eval(h)   t1omlh = t1salv / t1lagv
     c                   endif
     c                   else
     c                   eval      t1omlh = *zeros
     c                   endif
      *
     c                   if        p_vlin = 0
     c                   write     t1sum                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      t1ants = 0
     c                   eval      t1antl = 0
     c                   eval      t1salv = 0
     c                   eval      t1lagv = 0
     c                   eval      w_tell = 0
      *
     c     end_t1        endsr
     c/space 3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv sum undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_t2        begsr
      *
     c                   eval      t4ogrp = g_ogrp
     c                   eval      t4hgrp = g_hgrp
     c                   eval      t4ugrp = g_ugrp
     c                   eval      t4ugtx = a4ugtx
      *
     c                   if        t4lagv > 0
     c                   if        t4salv / t4lagv > 99999,99
     c                   eval      t4omlh = 99999,99
     c                   else
     c                   eval(h)   t4omlh = t4salv / t4lagv
     c                   endif
     c                   else
     c                   eval      t4omlh = *zeros
     c                   endif
      *
     c                   write     t4sum                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c                   eval      t4ants = 0
     c                   eval      t4antl = 0
     c                   eval      t4salv = 0
     c                   eval      t4lagv = 0
      *
     c     end_t2        endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv sum hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_t3        begsr
      *
     c                   eval      t3ogrp = g_ogrp
     c                   eval      t3hgrp = g_hgrp
     c                   eval      t3hgtx = a3hgtx
      *
     c                   if        t3lagv > 0
     c                   if        t3salv / t3lagv > 99999,99
     c                   eval      t3omlh = 99999,99
     c                   else
     c                   eval(h)   t3omlh = t3salv / t3lagv
     c                   endif
     c                   else
     c                   eval      t3omlh = *zeros
     c                   endif
      *
     c                   write     t3sum                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c                   eval      t3ants = 0
     c                   eval      t3antl = 0
     c                   eval      t3salv = 0
     c                   eval      t3lagv = 0
      *
     c     end_t3        endsr
     c/space 3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv sum overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_t4        begsr
      *
     c                   eval      t2ogrp = g_ogrp
     c                   eval      t2ogtx = a2ogtx
      *
     c                   if        t2lagv > 0
     c                   if        t2salv / t2lagv > 99999,99
     c                   eval      t2omlh = 99999,99
     c                   else
     c                   eval(h)   t2omlh = t2salv / t2lagv
     c                   endif
     c                   else
     c                   eval      t2omlh = *zeros
     c                   endif
      *
     c                   write     t2sum                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c                   eval      t2ants = 0
     c                   eval      t2antl = 0
     c                   eval      t2salv = 0
     c                   eval      t2lagv = 0
      *
     c     end_t4        endsr
     c/space 3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv sum lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_t6        begsr
      *
     c                   eval      t6lage = g_lage
     c                   eval      t6ltek = a6ltek
      *
     c                   if        t6lagv > 0
     c                   if        t6salv / t6lagv > 99999,99
     c                   eval      t6omlh = 99999,99
     c                   else
     c                   eval(h)   t6omlh = t6salv / t6lagv
     c                   endif
     c                   else
     c                   eval      t6omlh = *zeros
     c                   endif
      *
     c                   write     t6sum                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c                   eval      t6ants = 0
     c                   eval      t6antl = 0
     c                   eval      t6salv = 0
     c                   eval      t6lagv = 0
      *
     c     end_t6        endsr
     c/space 3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriv sum firma
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_t7        begsr
      *
     c                   eval      t7firm = l_firm
      *
     c                   if        t7lagv > 0
     c                   if        t7salv / t7lagv > 99999,99
     c                   eval      t7omlh = 99999,99
     c                   else
     c                   eval(h)   t7omlh = t7salv / t7lagv
     c                   endif
     c                   else
     c                   eval      t7omlh = *zeros
     c                   endif
      *
     c                   write     t7sum                                30
      *
     c     end_t7        endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på overgruppe ?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a2        begsr
      *
     c                   eval      vogrl1_oogr = mlogrp
     c     vogrl1_key    chain     vogrl1                             92
     c                   eval      a2ogrp = mlogrp
     c                   if        *in92 = *off
     c                   eval      a2ogtx = vgotxt
     c                   else
     c                   eval      a2ogtx = 'Ukjent overgruppe'
     c                   endif
      *
     c                   if        p_vlin = 0
     c                   write     a2hdr                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
     c                   endif
      *
     c     end_a2        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på hovedgruppe ?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a3        begsr
      *
     c                   eval      vhgrl1_hogr = mlogrp
     c                   eval      vhgrl1_hhgr = mlhgrp
     c     vhgrl1_key    chain     vhgrl1                             93
     c                   eval      a3ogrp = mlogrp
     c                   eval      a3hgrp = mlhgrp
     c                   if        *in93 = *off
     c                   eval      a3hgtx = vghtxt
     c                   else
     c                   eval      a3hgtx = 'Ukjent hovedgruppe'
     c                   endif
      *
     c                   if        p_vlin = 0
     c                   write     a3hdr                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
     c                   endif
      *
     c     end_a3        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på undergruppe ?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a4        begsr
      *
     c                   eval      vugrl1_uogr = mlogrp
     c                   eval      vugrl1_uhgr = mlhgrp
     c                   eval      vugrl1_uugr = mlugrp
     c     vugrl1_key    chain     vugrl1                             94
     c                   eval      a4ogrp = mlogrp
     c                   eval      a4hgrp = mlhgrp
     c                   eval      a4ugrp = mlugrp
     c                   if        *in94 = *off
     c                   eval      a4ugtx = vgutxt
     c                   else
     c                   eval      a4ugtx = 'Ukjent undergruppe'
     c                   endif
      *
     c                   if        p_vlin = 0
     c                   write     a4hdr                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
     c                   endif
      *
     c     end_a4        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Brudd på lager?
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hed_a6        begsr
      *
     c                   eval      a6lage = mllage
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    mllage
     c                   parm                    a6ltek
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      a6ltek = 'Ukjent lager'
     c                   endif
      *
     c                   if        b_init = *off
     c                   exsr      overflow
     c                   else
     c                   eval      b_init = '0'
     c                   endif
      *
     c                   write     a6hdr                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c     end_a6        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_salg     begsr
      *
     c                   eval      w_anta_salg = 0
     c                   eval      w_kost_salg = 0
     c                   extrct    w_date:*m     w_dmnd
     c                   eval      sstalb_vare = vvarl1_vare
      *
      * Les statistikkposter fra forrige år og akkumuler i tabell
      *
     c                   eval      sstalb_paar = w_faar
     c     sstalb_key    setll     sstalb
     c     sstalb_key    reade     sstalb                                 92
      *
     c                   dow       *in92 = *off
      *
     c                   if        sflage = mllage and
     c                             sfpmnd >= w_dmnd
     c                   eval      w_anta_salg = w_anta_salg + sfanta
     c                   eval      w_kost_salg = w_kost_salg + sfsumk
     c                   endif
      *
     c     sstalb_key    reade     sstalb                                 92
     c                   enddo
      *
      * Les statistikkposter fra dette år og akkumuler i tabell
      *
     c                   eval      sstalb_paar = w_daar
     c     sstalb_key    setll     sstalb
     c     sstalb_key    reade     sstalb                                 92
      *
     c                   dow       *in92 = *off and
     c                             sfpmnd < w_dmnd
      *
     c                   if        sflage = mllage
     c                   eval      w_anta_salg = w_anta_salg + sfanta
     c                   eval      w_kost_salg = w_kost_salg + sfsumk
     c                   endif
      *
     c     sstalb_key    reade     sstalb                                 92
     c                   enddo
      *
     c     salg_end      endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne statistikkinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_innkj    begsr
      *
     c                   eval      w_anta_kjop = 0
     c                   eval      w_kost_kjop = 0
     c                   extrct    w_date:*m     w_dmnd
     c                   eval      sistlb_vare = vvarl1_vare
      *
      * Les statistikkposter fra forrige år og akkumuler i tabell
      *
     c                   eval      sistlb_paar = w_faar
     c     sistlb_key    setll     sistlb
     c     sistlb_key    reade     sistlb                                 92
      *
     c                   dow       *in92 = *off
      *
     c                   if        silage = mllage and
     c                             sipmnd >= w_dmnd
     c                   eval      w_anta_kjop = w_anta_kjop + sianta
     c                   eval      w_kost_kjop = w_kost_kjop + sisumk
     c                   endif
      *
     c     sistlb_key    reade     sistlb                                 92
     c                   enddo
      *
      * Les statistikkposter fra dette år og akkumuler i tabell
      *
     c                   eval      sistlb_paar = w_daar
     c     sistlb_key    setll     sistlb
     c     sistlb_key    reade     sistlb                                 92
      *
     c                   dow       *in92 = *off and
     c                             sipmnd < w_dmnd
      *
     c                   if        silage = mllage
     c                   eval      w_anta_kjop = w_anta_kjop + sianta
     c                   eval      w_kost_kjop = w_kost_kjop + sisumk
     c                   endif
      *
     c     sistlb_key    reade     sistlb                                 92
     c                   enddo
      *
     c     inkj_end      endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les av salgsstatistikk
      *
     c     sstalb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstalb_paar
     c                   kfld                    sstalb_vare
      *
      * Key for les av innkjøpsstatistikk
      *
     c     sistlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistlb_paar
     c                   kfld                    sistlb_vare
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   extrct    w_date:*m     w_dmnd
     c                   extrct    w_date:*y     w_daar
     c                   eval      w_faar = w_daar - 1
      *
     c                   eval      w_firm = l_firm
      *
      * Redigere heading
      *
     c     *dmy          move      w_date        a1date
     c     *hms          move      w_time        a1time
      *
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
      *
     c                   eval      *in71  = p_sort = 1
     c                   eval      *in72  = p_sort = 2
     c                   eval      *in73  = p_sort = 3
     c                   eval      *in74  = p_sort = 4
      *
     c                   endsr
