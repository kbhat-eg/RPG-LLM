     H datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH291R                                              *
      * Beskrivelse . . : Bygger ny saldo - hovedbok                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *           310302  Nytt program                                   KOB  *
      *           040603  Endret slik at saldorecord slettes/ikke nullst.KOB  *
6.10  * V6.10     210909  Lagt inn sporingsfelter                        BSA  *
6.31  *           040719  utvidet tabell i hht. utvidelse i RHSAPF       bof
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhtrl1    if   e           k disk    rename(rhtrpfr:rhtrl1r)
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
      *
6.31 d sal             s             15  2 dim(14)                               SALDOMATRISE
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d p               s              2  0
      *
     d w_kont_gml      s                   like(rikont)
     d w_avde_gml      s                   like(riavde)
     d w_spes_gml      s                   like(rispes)
      *
     d w_type          s                   like(ritype)
     d w_firm          s                   like(rifirm)
     d w_raar          s                   like(riraar)
      *
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
      *
     c     dummy         tag
      *
      * Nullstill saldi
      *
     c     rhsalu_keys   setll     rhsalu                                 60
     c     rhsalu_keys   reade     rhsalu                                 60
      *
     c                   dow       *in60 = *off
      *
     c                   if        riraar = rhprÅr and
     c                             ritype = 1
     c                   delete    rhsalur
     c                   endif
      *
     c     rhsalu_keys   reade     rhsalu                                 60
     c                   enddo
      *
     c/eject
      *
      * Les posteringer og oppdater saldi
      *
     c     rhtrl1_key    setll     rhtrl1                                 61
     c     rhtrl1_key    reade     rhtrl1                                 61
      *
     c                   eval      w_kont_gml = rbkont
     c                   eval      w_avde_gml = rbavde
     c                   eval      w_spes_gml = rbspes
      *
     c                   dow       *in61 = *off
      *
     c                   if        rbkont <> w_kont_gml or
     c                             rbavde <> w_avde_gml or
     c                             rbspes <> w_spes_gml
     c                   exsr      oppdater
     c                   eval      w_kont_gml = rbkont
     c                   eval      w_avde_gml = rbavde
     c                   eval      w_spes_gml = rbspes
     c                   eval      sal = 0
     c                   endif
      *
     c                   eval      p = rbrper
      *
     c                   if        rbbilk <> 98
     c                   eval      sal(p) = sal(p) + rbneto
     c                   else
     c                   eval      sal(14) = sal(14) + rbneto
     c                   endif
      *
     c     rhtrl1_key    reade     rhtrl1                                 61
     c                   enddo
      *
     c/space 3
      *
     c     slutt         tag
      *
     c                   exsr      oppdater
      *
     c                   eval      *inlr = *on
     c                   return
      *
     c/eject
      *
      * Subprogram for oppdatering av saldo
      *
     c     oppdater      begsr
      *
     c                   eval      rhsalu_kont = w_kont_gml
     c                   eval      rhsalu_avde = w_avde_gml
     c                   eval      rhsalu_spes = w_spes_gml
     c                   eval      rhsalu_raar = w_raar
     c                   eval      rhsalu_type = 1
     c     rhsalu_key    chain     rhsalu                             62
      *
     c                   eval      risa01 = sal(01)
     c                   eval      risa02 = sal(02)
     c                   eval      risa03 = sal(03)
     c                   eval      risa04 = sal(04)
     c                   eval      risa05 = sal(05)
     c                   eval      risa06 = sal(06)
     c                   eval      risa07 = sal(07)
     c                   eval      risa08 = sal(08)
     c                   eval      risa09 = sal(09)
     c                   eval      risa10 = sal(10)
     c                   eval      risa11 = sal(11)
     c                   eval      risa12 = sal(12)
     c                   eval      risa13 = sal(13)
     c                   eval      risa00 = sal(14)
      *
6.10 c                   time                    riedat
6.10 c                   time                    rietim
6.10 c                   eval      rieusr = l_user
      *
     c                   if        %found
     c                   update    rhsalur
     c                   else
     c                   eval      rifirm = w_firm
     c                   eval      rikont = w_kont_gml
     c                   eval      riavde = w_avde_gml
     c                   eval      rispes = w_spes_gml
     c                   eval      riraar = rhprÅr
     c                   eval      ritype = 1
6.10 c                   time                    riodat
6.10 c                   time                    riotim
6.10 c                   eval      riousr = l_user
     c                   write     rhsalur
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = rhprÅr
      *
     c                   eval      w_kont_gml = 0
     c                   eval      w_avde_gml = 0
     c                   eval      w_spes_gml = 0
     c                   eval      sal = 0
      *
      * Key for nullstilling av saldo
      *
     c     rhsalu_keys   klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av saldo
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for posteringer - hovedbok
      *
     c     rhtrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
      *
     c                   endsr
