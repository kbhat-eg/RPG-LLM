     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SP528R
      * Beskrivelse . . : Endring av kunde-statistikk, Oppdateringen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.41 260606 KOB  Lagt inn oppdatering av rabattkategori
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fsstal1    uf   e           k disk    rename(sstapfr:sstal1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
      * Datastruktur - parameter
      *
     d                 ds
     d d_parm                        96
     d  d_firm                       03  0 overlay(d_parm:01)
     d  d_paar                       04  0 overlay(d_parm:04)
     d  d_fper                       02  0 overlay(d_parm:08)
     d  d_tper                       02  0 overlay(d_parm:10)
     d  d_kkat                       01  0 overlay(d_parm:12)
     d  d_dist                       01  0 overlay(d_parm:13)
     d  d_selg                       01  0 overlay(d_parm:14)
     d  d_avde                       01  0 overlay(d_parm:15)
     d  d_lagr                       01  0 overlay(d_parm:16)
     d  d_rkat                       01  0 overlay(d_parm:17)
     d  d_nobb                       01  0 overlay(d_parm:18)
     d  d_nmod                       01  0 overlay(d_parm:19)
     d  d_ldor                       01  0 overlay(d_parm:20)
     d  d_lvar                       01  0 overlay(d_parm:21)
     d  d_vgrp                       01  0 overlay(d_parm:22)
     d  d_agrp                       01  0 overlay(d_parm:23)
      *
      * Definering av variabler i nøkler
      *
     d sstal1_paar     s                   like(sfpaar)
     d sstal1_binr     s                   like(sfbinr)
     d sstal1_line     s                   like(sfline)
     d sstal1_bida     s                   like(sfbida)
     d vvarl1_vare     s                   like(vvvare)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
      *
     d w_oppd          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeide med handlinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Posisjonering
      *
     c                   exsr      posisjoner
      *
      * Les statistikkregisteret og oppdater
      *
     c                   exsr      oppdat
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese og oppdatere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
     c                   dou       *in15  = *on
      *
     c                   read      sstal1                                 15
      *
     c                   if        *in15                                        EOF
     c                   goto      end_oppdat
     c                   endif
      *
     c                   if        sffirm <> w_firm or
     c                             sfpaar <> d_paar
     c                   eval      *in15 = *on
     c                   goto      end_oppdat
     c                   endif
      *
     c                   eval      w_oppd  = 0
      *
      * Test om valg kunde
      *
     c                   if        d_kkat <> 0 or
     c                             d_dist <> 0 or
     c                             d_selg <> 0 or
     c                             d_avde <> 0 or
     c                             d_lagr <> 0 or
     c                             d_rkat <> 0
      *
     c                   eval      rkunl1_kund = sfvkun
     c     rkunl1_key    chain     rkunl1                             90
      *
     c                   if        *in90 =*off
      *
     c                   if        d_kkat <> 0 and
     c                             sfkkat <> rkkatg
     c                   eval      sfkkat  = rkkatg
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_dist <> 0 and
     c                             sfdist <> rkdist
     c                   eval      sfdist  = rkdist
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_selg <> 0 and
     c                             sfselg <> rkslgr
     c                   eval      sfselg  = rkslgr
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_avde <> 0 and
     c                             sfsavd <> rkavde
     c                   eval      sfsavd  = rkavde
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_lagr <> 0 and
     c                             sflage <> rklagr
     c                   eval      sflage  = rklagr
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_rkat <> 0 and
     c                             sfrkat <> rkrabk
     c                   eval      sfrkat  = rkrabk
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Test om valg vare
      *
     c                   if        d_nobb <> 0 or
     c                             d_nmod <> 0 or
     c                             d_ldor <> 0 or
     c                             d_lvar <> 0 or
     c                             d_vgrp <> 0 or
     c                             d_agrp <> 0
      *
     c                   eval      vvarl1_vare = sfvare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c     vvarl1_key    chain     vvasl1                             90
     c                   endif
      *
     c                   if        *in90 =*off
      *
     c                   if        d_nobb <> 0 and
     c                             sfnobb <> vvnobb
     c                   eval      sfnobb  = vvnobb
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_nmod <> 0 and
     c                             sfmodn <> vvnmod
     c                   eval      sfmodn  = vvnmod
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_ldor <> 0 and
     c                             sfldor <> vvldor
     c                   eval      sfldor  = vvldor
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_lvar <> 0 and
     c                             sflvar <> vvlvar
     c                   eval      sflvar  = vvlvar
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_vgrp <> 0 and
     c                             sfogrp <> vvogrp or
     c                             sfhgrp <> vvhgrp or
     c                             sfugrp <> vvugrp
     c                   eval      sfogrp  = vvogrp
     c                   eval      sfhgrp  = vvhgrp
     c                   eval      sfugrp  = vvugrp
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   if        d_agrp <> 0 and
     c                             sfahgr <> vvahgr or
     c                             sfaugr <> vvaugr
     c                   eval      sfahgr  = vvahgr
     c                   eval      sfaugr  = vvaugr
     c                   eval      w_oppd  = 1
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Oppdater hvis endring
      *
     c                   if        w_oppd = 1
      *
     c                   update    sstal1r
      *
     c                   endif
      *
     c                   enddo
      *
     c     end_oppdat    tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på år/periode
      *
     c                   eval      sstal1_paar = d_paar
     c                   eval      sstal1_binr = *zeros
     c                   eval      sstal1_line = *zeros
     c                   eval      sstal1_bida = *loval
     c     sstal1_key    setll     sstal1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på år
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_paar
     c                   kfld                    sstal1_binr
     c                   kfld                    sstal1_line
     c                   kfld                    sstal1_bida
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    wwparm           96
     c                   move      wwparm        d_parm
      *
      * Henter firmakode fra local
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
