     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO713R
      * Beskrivelse . . : Maskinell matching mot varenummer fra-til og
      *                   leverandørens varenummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121196 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl1    if   e           k disk    rename(jmatpfr:jmatl1r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
     d p_va1f          s                   like(jovar1)
     d p_va1t          s                   like(jovar1)
     d p_ldo2          s                   like(joldo2)
      *
      * Definering av variable i nøkler
      *
     d jmatlu_var1     s                   like(jovar1)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     jmatl1_key    setll     jmatl1
     c                   read      jmatl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        jovar1 < p_va1f
     c                   goto      les_neste
     c                   endif
      *
     c                   if        jovar1 > p_va1t
     c                   leave
     c                   endif
      *
     c                   if        jokode <> 0
     c                   goto      les_neste
     c                   endif
      *
     c                   if        jovkod <> 0
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      jvarl8_ldor = p_ldo2
     c                   eval      jvarl8_lvar = jolvar
     c     jvarl8_key    chain     jvarl8                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
      * Oppdaterer matche-register med matchet vare
      *
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur                            92
     c                   if        *in92 = *off
     c                   eval      jovar2 = jvvare
     c                   eval      joldo2 = jvldor
     c                   eval      jokode = 2
     c                   update    jmatlur
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      jmatl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_va1f
     c                   parm                    p_va1t
     c                   parm                    p_ldo2
      *
      * Key for les på matcheregister
      *
     c     jmatl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av matcheregister
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      * Key for oppslag på vare
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
