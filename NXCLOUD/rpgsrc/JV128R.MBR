     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV128R
      * Beskrivelse . . : Overføring nye en vare til EVR, parameterprog
      *
      * Spesialversjon. : Kan oveføre enkelt varer
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250615 bof  Nytt program
6.31  *       050815 bof  Utvidet med parameter til JV750R
6.32  *       170616 bof  Skal kun behandle en vare
6.33  *       160419 bsa  Synker lengden på felter
7.xx  * 7.00  030117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       150419 bsa  Merker om varen skal opprettes i tabell for
7.01  *                   utstillingsvarer.
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjle9lr    if   e           k disk    rename(jle9pfr:jle9lrr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvdtlu    uf   e           k disk    rename(jvdtpfr:jvdtlur)
      *
     fjv128d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_ldor_alf      s              6
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d jvarl1_vare     s                   like(jvvare)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jvdtlu_dvar     s                   like(jvdvar)
     d vvarl1_vare     s                   like(vvvare)
7.01 d jle9lr_xldo     s                   like(jlxldo)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
6.32 d w_vare          s                   like(jvvare)
6.33 d w_feil          s             60
     d w_navn          s             30
     d w_retk          s              1
      *
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
8.01 d w_prgr          s              2
5.70 d w_prut          s              1
6.30 d w_ffak          s              8  6
6.30 d w_inp2          s              9  2
6.31 d w_pgbe          s              1
6.31 d w_pgfr          s              1
6.3y d w_oppd          s              1
      *
     d w_grpr          s              9  2
     d w_gdat          s               d   datfmt(*iso)
     d w_enhe          s              3
     d w_enh1          s              3
     d w_enh2          s              3
     d w_enh3          s              3
     d w_eni1          s              3
     d w_eni2          s              3
     d w_eni3          s              3
     d w_enp1          s              3
     d w_enp2          s              3
     d w_enp3          s              3
     d w_ofp1          s             12  6
     d w_ofp2          s             12  6
     d w_ofp3          s             12  6
     d w_ofv2          s             12  6
     d w_ofv3          s             12  6
     d w_ofi1          s             12  6
     d w_ofi2          s             12  6
     d w_ofi3          s             12  6
     d w_bldo          s              6  0
     d w_bogr          s              2  0
     d w_bhgr          s              2  0
     d w_bugr          s              3  0
     d w_bmod          s              8  0
     d w_bvar          s             15
     d w_bvty          s              1
     d w_blet          s              1  0
     d w_bbet          s             35
     d w_ntpr          s              8  2
     d w_ifak          s              8  6
     d w_pogr          s              2  0
     d w_phgr          s              2  0
     d w_pugr          s              3  0
     d w_pldo          s              6  0
     d w_pmod          s              8  0
     d w_pvar          s             15
     d w_ppko          s              1
     d w_pvty          s              1
     d w_plet          s              1  0
     d w_kfak          s              8  6
     d w_kbel          s              7  2
     d w_sfak          s              8  6
     d w_midl          s              1  0
     d w_inpr          s              9  2
     d w_kopr          s              9  2
     d w_s1pr          s              9  2
     d w_s1im          s              9  2
     d w_s2pr          s              9  2
     d w_s2im          s              9  2
     d w_s3pr          s              9  2
     d w_s3im          s              9  2
     d w_dekn          s              5  2
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_pkod          s              1
     d w_skod          s              1  0
     d w_lety          s              1  0
     d w_gprk          s              1  0
6.38 d w_dato          s               d   datfmt(*iso)
6.38 d w_utgp          s              1  0
     d w_eldo          s                   like(jvldor)
6.11 d w_osts          s              1
     d w_lvar          s                   like(jvlvar)
     d w_xxxx          s                   like(jvldor)
7.01 d w_vuts          s              1
      *
     d w_levr          s                   like(jlenum)
      *
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
6.30 d d_list                        50
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldor                        6  0 overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_ldop                        6  0 overlay(d_list:17)
     d  d_skaf                        1  0 overlay(d_list:23)
     d  d_kntr                        1  0 overlay(d_list:24)
8.01 d  d_prgr                        2    overlay(d_list:25)
8.01 d  d_osor                       10    overlay(d_list:27)
8.01 d  d_dato                         d   overlay(d_list:37)
8.01 d  d_utgp                        1  0 overlay(d_list:48)
8.01 d  d_dumm                        1    overlay(d_list:49)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
6.30  *
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
     c                   eval      jvdtl1_dvar = b1vare
     c     jvdtl1_key    chain     jvdtl1
      *
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   eval      vvarl1_vare = b1vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      w_feil = 'Vare finnes allerede i EVR, kan   +
     c                                      ikke overføres                  '
     c                   eval      b1text = *blank
     c                   movel     w_feil        b1text
     c                   goto      b1taga
     c                   endif
7.01  *
7.01 c                   eval      jle9lr_xldo = b1pgiv
7.01 c     jle9lr_key    chain     jle9lr
7.01 c                   if        %found(jle9lr) and
     c                             jlxaut = 0
     c                   eval      w_feil = 'Leverandør er ikke åpnet for overf+
     c                                      øring av enkeltvarer            '
     c                   eval      b1text = *blank
     c                   movel     w_feil        b1text
     c                   goto      b1taga
     c                   endif
7.01  *
7.01 c                   eval      jle9lr_xldo = b1pgiv
7.01 c     jle9lr_key    chain     jle9lr
7.01 c                   if        not %found(jle9lr)
     c                   eval      w_feil = 'Leverandør er ikke åpnet for overf+
     c                                      øring av enkeltvarer            '
     c                   eval      b1text = *blank
     c                   movel     w_feil        b1text
     c                   goto      b1taga
     c                   endif
      *
      *
     c                   exsr      hent_pris
     c                   if        w_osts <>  *blank
     c                   eval      b1text = *blank
     c                   select
     c                   when      w_osts = '1'
     c                   eval      w_feil = 'Mangler/ugyldig varegruppe        +
     c                                                                        '
     c                   when      w_osts = '2'
     c                   eval      w_feil = 'Mangler knytning mot leverandør i +
     c                                       EVR                              '
     c                   when      w_osts = '3'
     c                   eval      w_feil = 'Mangler forpakning,stat.enhet el. +
     c                                       lag.enhet                        '
     c                   when      w_osts = '4'
     c                   eval      w_feil = 'Pris ble ikke funnet, eller valgt +
     c                                                                        '
     c                   when      w_osts = '5'
     c                   eval      w_feil = 'Betingelse ikke funnet, eller er  +
     c                                        ikke valgt                      '
     c                   when      w_osts = '6'
     c                   eval      w_feil = 'Mangler påslagsfaktor             +
     c                                                                        '
     c                   when      w_osts = '7'
     c                   eval      w_feil = 'Det er negativ dekning            +
     c                                                                        '
     c                   when      w_osts = '8'
     c                   eval      w_feil = 'Varen utelates - feil             +
     c                                                                        '
     c                   when      w_osts = '9'
     c                   eval      w_feil = 'Feil oppsto under valdiering av   +
     c                                       vare                             '
     c                   when      w_osts = 'S'
     c                   eval      w_feil = 'Varen er utgått i VA              +
     c                                                                        '
     c                   when      w_osts = 'U'
     c                   eval      w_feil = 'Varen har utgåttdato i VA         +
     c                                                                        '
     c                   when      w_osts = 'E'
     c                   eval      w_feil = 'Enhetsfeil.                       +
     c                                                                        '
     c                   when      w_osts = 'O'
     c                   eval      w_feil = 'Enhet med forskjellige omr. fakt  +
     c                                       VA/EVR                           '
     c                   when      w_osts = 'G'
     c                   eval      w_feil = 'En annen vare funnet i EVR med lik+
     c                                       t GTIN                           '
     c                   when      w_osts = 'L'
     c                   eval      w_feil = 'Varetype=L på et eller flere lager+
     c                                                                        '
     c                   when      w_osts = 'H'
     c                   eval      w_feil = 'Mangler pris/enhet hos hovedlevera+
     c                                      dør                               '
     c                   movel     w_feil        b1text
     c                   endsl
     c                   movel     w_feil        b1text
     c                   goto      b1taga
     c                   endif
      *
     c                   eval      jvdtlu_dvar = b1vare
     c     jvdtl1_key    chain     jvdtlu
     c                   if        %found(jvdtlu)
     c                   eval      jvdhko = 0
     c                   update    jvdtlur
     c                   endif
      *
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_ldor = 0
     c                   eval      d_ogrp = 0
     c                   eval      d_hgrp = 0
     c                   eval      d_ugrp = 0
     c                   eval      d_ldop = b1pgiv
     c                   eval      d_skaf = 0
     c                   eval      d_kntr = 0
5.70 c                   eval      d_prgr = *blank
6.10 c                   eval      d_osor = *blank
6.30 c                   eval      d_dato = w_udat
6.30 c                   eval      d_utgp = 1
6.30 c                   eval      d_dumm = '*'
      *
      *
      * Kaller overføringsprogram
      *
6.32 c                   eval      w_vare = b1vare
7.01 c                   eval      w_vuts = b1vuts
     c                   call      'JV620C'
     c                   parm                    d_list
6.32 c                   parm                    w_vare
7.01 c                   parm                    w_vuts
      *
     c                   eval      b1vare = *blank
     c                   eval      b1vtxt = *blank
     c                   eval      b1pnav = *blank
     c                   eval      b1pgiv = 0
     c                   eval      w_feil = 'Vare er overført til EVR          +
     c                                                 '
     c                   eval      b1text = *blank
     c                   movel     w_feil        b1text
     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1PGIV'
     c                   call      'JL500R'
     c                   parm                    b1pgiv
     c                   parm                    b1pnav
     c                   leavesr
     c                   endif
      *
      * vare
      *
     c                   if        w_afld = 'B1VARE'
     c                   call      'JV503R'
     c                   parm                    w_firm
     c                   parm                    b1pgiv
     c                   parm                    b1vare
     c                   parm                    b1vtxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1vtxt = *blank
     c                   eval      b1pnav = *blank
      *
      * Leverandør
      *
     c                   if        b1pgiv = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Prisgivende leverandør
      *
     c                   if        b1pgiv <> 0
     c                   eval      jlevl1_ldor = b1pgiv
     c     jlevl1_key    chain     jlevl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   if        jlenum = 0
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1pnav = jlnavn
     c                   endif
      *
      * Vare
      *
     c                   eval      jvarl1_vare = b1vare
     c     jvarl1_key    chain     jvarl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1vtxt = jvtek1
7.01  *
7.01  * Vare i utstilling
7.01  *
7.01 c                   if        b1vuts = *blanks
7.01 c                   eval      b1vuts = '0'
7.01 c                   endif
7.01  *
7.01 c                   if        b1vuts <> '0' and
7.01 c                             b1vuts <> '1'
7.01 c                   eval      *in32  = *on
7.01 c                   eval      b_feil = *on
7.01 c                   leavesr
7.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som henter status kode for overf. fra VA til EVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     hent_pris     begsr
      *
      * Henter salgspris fra fellesprogram
      *
     c                   eval      w_pkod = jvdpko
     c                   eval      w_skod = jvdsko
     c                   eval      w_lety = 0
     c                   eval      w_gprk = 0
     c                   eval      w_xxxx = 0
      *
     c                   call      'JV750R'
     c                   parm                    w_firm
     c                   parm                    jvvare
6.10 c                   parm                    w_prgr
     c                   parm                    jlldor
     c                   parm                    w_pkod
     c                   parm                    w_skod
     c                   parm                    w_lety
     c                   parm                    w_gprk
6.38 c                   parm                    w_udat
6.38 c                   parm                    w_utgp
      *
     c                   parm                    w_osts
      *
     c                   parm                    w_xxxx
     c                   parm                    w_lvar
     c                   parm                    w_eldo
     c                   parm                    w_grpr
     c                   parm                    w_gdat
      *
     c                   parm                    w_enhe
     c                   parm                    w_enh1
     c                   parm                    w_enh2
     c                   parm                    w_enh3
     c                   parm                    w_eni1
     c                   parm                    w_eni2
     c                   parm                    w_eni3
     c                   parm                    w_enp1
     c                   parm                    w_enp2
     c                   parm                    w_enp3
     c                   parm                    w_ofp1
     c                   parm                    w_ofp2
     c                   parm                    w_ofp3
     c                   parm                    w_ofv2
     c                   parm                    w_ofv3
     c                   parm                    w_ofi1
     c                   parm                    w_ofi2
     c                   parm                    w_ofi3
      *
     c                   parm                    w_bldo
     c                   parm                    w_bogr
     c                   parm                    w_bhgr
     c                   parm                    w_bugr
     c                   parm                    w_bmod
     c                   parm                    w_bvar
     c                   parm                    w_bvty
     c                   parm                    w_blet
     c                   parm                    w_bbet
     c                   parm                    w_ntpr
     c                   parm                    w_ifak
      *
     c                   parm                    w_pogr
     c                   parm                    w_phgr
     c                   parm                    w_pugr
     c                   parm                    w_pldo
     c                   parm                    w_pmod
     c                   parm                    w_pvar
     c                   parm                    w_ppko
     c                   parm                    w_pvty
     c                   parm                    w_plet
     c                   parm                    w_kfak
     c                   parm                    w_kbel
     c                   parm                    w_sfak
      *
     c                   parm                    w_midl
     c                   parm                    w_inpr
     c                   parm                    w_kopr
     c                   parm                    w_s1pr
     c                   parm                    w_s1im
     c                   parm                    w_s2pr
     c                   parm                    w_s2im
     c                   parm                    w_s3pr
     c                   parm                    w_s3im
     c                   parm                    w_dekn
6.10 c                   parm                    w_prut
6.30 c                   parm                    w_ffak
6.30 c                   parm                    w_inp2
6.31 c                   parm                    w_pgbe
6.3y c                   parm                    w_pgfr
6.3y c                   parm                    w_oppd
      *
      *
     c                   endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på overgruppe
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
7.01  *
7.01  * Key for les av tilleggsregister
7.01  *
7.01 c     jle9lr_key    klist
7.01 c                   kfld                    jle9lr_xldo
      *
6.30 c                   time                    w_udat
     c                   eval      w_firm = l_firm
7.01 c                   eval      b1vuts = '0'
      *
     c                   endsr
