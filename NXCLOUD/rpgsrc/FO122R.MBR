     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO122R
      * Beskrivelse . . : Vedlikehold av leveransedata
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260402 HKO  Skrevet om logikken i programmet
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  070909 BSA  Utvidet referansefelter fra 20 til 30 posisjoner
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  290316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelk    if   e           k disk    rename(fohepfr:fohelkr)
     ffohelo    if   e           k disk    rename(fohepfr:fohelor)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
      *
     ffo122d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohelk_kund     s                   like(fokund)
     d fohelk_numm     s                   like(fonumm)
     d fohelk_suff     s                   like(fosuff)
     d fohelo_otyp     s                   like(footyp)
     d fohelo_numm     s                   like(fonumm)
     d fohelo_suff     s                   like(fosuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(fofirm)
     d w_navn          s                   like(rknavn)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(15)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * H1WIN  - H1 Vindu for valg av bla-alternativer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F1=Vise bla-alternativer
      *
     c                   if        *inka = *on
     c                   exsr      xh1win
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      h1kund <> 0
     c                   eval      fohelk_numm = b1numm
     c                   eval      fohelk_suff = b1suff
     c                   if        h1valg = 1
     c     fohelk_key    setgt     fohelk
     c                   else
     c     fohelk_key    setll     fohelk
     c                   endif
     c                   when      h1otyp <> *blank
     c                   eval      fohelo_numm = b1numm
     c                   eval      fohelo_suff = b1suff
     c                   if        h1valg = 1
     c     fohelo_key    setgt     fohelo
     c                   else
     c     fohelo_key    setll     fohelo
     c                   endif
     c                   other
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c                   if        h1valg = 1
     c     fohel1_key    setgt     fohel1
     c                   else
     c     fohel1_key    setll     fohel1
     c                   endif
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Valgt kunde
      *
     c                   if        h1kund <> 0
     c                   eval      fohelk_kund = h1kund
     c                   eval      fohelk_numm = b2numm
     c                   if        h1valg = 1
     c                   eval      fohelk_suff = 99
     c     fohelk_key    setgt     fohelk
     c                   else
     c                   eval      fohelk_suff = 00
     c     fohelk_key    setll     fohelk
     c                   endif
     c                   goto      end_pos
     c                   endif
      *
      * Valgt ordretype
      *
     c                   if        h1otyp <> *blank
     c                   eval      fohelo_otyp = h1otyp
     c                   eval      fohelo_numm = b2numm
     c                   if        h1valg = 1
     c                   eval      fohelo_suff = 99
     c     fohelo_key    setgt     fohelo
     c                   else
     c                   eval      fohelo_suff = 00
     c     fohelo_key    setll     fohelo
     c                   endif
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonert på ordrenummer
      *
     c                   eval      fohel1_numm = b2numm
      *
     c                   if        h1valg = 1
     c                   eval      fohel1_suff = 99
     c     fohel1_key    setgt     fohel1
     c                   else
     c                   eval      fohel1_suff = 00
     c     fohel1_key    setll     fohel1
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vedlikehold
      *
     c                   eval      fohelu_numm = b1numm
     c                   eval      fohelu_suff = b1suff
     c     fohelu_key    chain     fohelur
      *
     c                   eval      foavde = b1avde
     c                   eval      fovref = b1vref
     c                   eval      foldat = *loval
      *
     c                   if        b1ldat <> 0
     c     *dmy          move      b1ldat        foldat
     c                   endif
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
     c                   iter
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      h1kund <> 0
     c                   if        h1valg = 1
     c                   readp     fohelk
     c                   else
     c                   read      fohelk
     c                   endif
     c                   when      h1otyp <> *blank
     c                   if        h1valg = 1
     c                   readp     fohelo
     c                   else
     c                   read      fohelo
     c                   endif
     c                   other
     c                   if        h1valg = 1
     c                   readp     fohel1
     c                   else
     c                   read      fohel1
     c                   endif
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        h1kund <> 0 and
     c                             h1kund <> fokund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        h1kund = 0
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> footyp
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
     c                   endif
      *
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> footyp
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      h1kund <> 0
     c                   eval      fohelk_numm = fonumm
     c                   eval      fohelk_suff = fosuff
     c                   when      h1otyp <> *blank
     c                   eval      fohelo_numm = fonumm
     c                   eval      fohelo_suff = fosuff
     c                   other
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   endsl
      *
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1otyp = footyp
     c                   eval      b1kuna = fonavn
     c                   eval      b1avde = foavde
     c                   eval      b1ldat = 0
     c                   eval      b1vref = fovref
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        b1ldat
     c                   endif
      *
     c                   if        b1kuna = *blank
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      b1kuna = rknavn
     c                   else
     c                   eval      b1kuna = fokuna
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      h1kund <> 0
     c                   if        h1valg = 1
     c     fohelk_key    setll     fohelk
     c                   read      fohelk
     c                   else
     c     fohelk_key    setgt     fohelk
     c                   readp     fohelk
     c                   endif
     c                   when      h1otyp <> *blank
     c                   if        h1valg = 1
     c     fohelo_key    setll     fohelo
     c                   read      fohelo
     c                   else
     c     fohelo_key    setgt     fohelo
     c                   readp     fohelo
     c                   endif
     c                   other
     c                   if        h1valg = 1
     c     fohel1_key    setll     fohel1
     c                   read      fohel1
     c                   else
     c     fohel1_key    setgt     fohel1
     c                   readp     fohel1
     c                   endif
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      h1kund <> 0
     c                   if        h1valg = 1
     c                   read      fohelk
     c                   else
     c                   readp     fohelk
     c                   endif
     c                   when      h1otyp <> *blank
     c                   if        h1valg = 1
     c                   read      fohelo
     c                   else
     c                   readp     fohelo
     c                   endif
     c                   other
     c                   if        h1valg = 1
     c                   read      fohel1
     c                   else
     c                   readp     fohel1
     c                   endif
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        h1kund <> 0 and
     c                             h1kund <> fokund
     c                   leave
     c                   endif
      *
     c                   if        h1kund = 0
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> footyp
     c                   leave
     c                   endif
     c                   endif
      *
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> footyp
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      h1kund <> 0
     c                   eval      fohelk_numm = fonumm
     c                   eval      fohelk_suff = fosuff
     c                   when      h1otyp <> *blank
     c                   eval      fohelo_numm = fonumm
     c                   eval      fohelo_suff = fosuff
     c                   other
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      h1kund <> 0
     c                   if        h1valg = 1
     c     fohelk_key    setgt     fohelk
     c                   else
     c     fohelk_key    setll     fohelk
     c                   endif
     c                   when      h1otyp <> *blank
     c                   if        h1valg = 1
     c     fohelo_key    setgt     fohelo
     c                   else
     c     fohelo_key    setll     fohelo
     c                   endif
     c                   other
     c                   if        h1valg = 1
     c     fohel1_key    setgt     fohel1
     c                   else
     c     fohel1_key    setll     fohel1
     c                   endif
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av bilde for bla-alternativer (H1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   exfmt     h1bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_h1win
     c                   endif
      *
     c                   if        *inkd
     c                   if        w_afld = 'H1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    h1kund
     c                   parm                    w_navn
     c                   endif
     c                   goto      h1taga
     c                   endif
      *
     c                   if        h1valg = 1
     c                   eval      b2numm = 999999
     c                   else
     c                   eval      b2numm = 0
     c                   endif
      *
     c                   exsr      posisjoner
      *
     c     end_h1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på ordre
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for valg av kunde
      *
     c     fohelk_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelk_kund
     c                   kfld                    fohelk_numm
     c                   kfld                    fohelk_suff
      *
      * Key for valg av ordretype
      *
     c     fohelo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelo_otyp
     c                   kfld                    fohelo_numm
     c                   kfld                    fohelo_suff
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fohel1_numm = 0
     c                   eval      fohel1_suff = 0
     c     fohel1_key    setll     fohel1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
