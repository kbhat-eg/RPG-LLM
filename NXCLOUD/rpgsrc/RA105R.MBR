     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA105R
      * Beskrivelse . . : MVA-koder, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250302 KOB  Nytt program
7.xx  * 7.00  010316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.00  *       140120 sst  Lagt til std.mk vedr SAF-T
7.01  *       261020 sst  Slå opp i std. momskode for å finne tekst
8.00  *       040322 sst  Lagt til nye koder vedr mva-oppg på xml
8.01  *       110322 sst  Lagt inn logging av endringer mva-sats
8.02  *       010422 sst  Justering på 8.01
8.03  *       050522 sst  Krav om endring av 'Gjelder fra dato' ved endring av sats
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
     fra05l2    if   e           k disk    rename(ra05pfr:ra05l2r)
     fra05lr    if   e           k disk    rename(ra05pfr:ra05lrr)
     fra05lu    uf a e           k disk    rename(ra05pfr:ra05lur)
7.00 frat5pf    uf a e           k disk    rename(rat5pfr:rat5pfr)
7.01 fras5pf    if   e           k disk    rename(ras5pfr:ras5pfr)
8.01 fral5pf    uf a e           k disk    rename(ral5pfr:ral5pfr)
      *
     fra105d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra05l1_ekod     s                   like(raekod)
     d ra05l2_etxt     s                   like(raetxt)
     d ra05lr_ekod     s                   like(raekod)
     d ra05lu_ekod     s                   like(raekod)
7.00 d rat5pf_ekod     s                   like(rtekod)
8.01 d ral5pf_firm     s                   like(rtlfir)
8.01 d ral5pf_ekod     s                   like(rtlkod)
8.01 d ral5pf_ltda     s                   like(rtltda)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(raefir)
      *
     d s_ekod          s                   like(raekod)
8.01 d s_epro          s                   like(raepro)
8.01 d s_ltda          s                   like(raemda)
8.01 d s_lsmk          s                   like(rtesmk)
8.03 d s_emda          s                   like(c2emda)
8.01 d w_ltda          s                   like(raemda)
8.03 d m1              s             50
8.03 d m2              s             50
8.03 d sv              s              3
8.03 d in03            s              1  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ekod <> *blank or
     c                             b2etxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra05l2_etxt = b1etxt
     c     ra05l2_key    setll     ra05l2
     c                   other
     c                   eval      ra05l1_ekod = b1ekod
     c     ra05l1_key    setll     ra05l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * MVA-kode
      *
     c                   if        b2ekod <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      ra05l1_ekod = b2ekod
     c     ra05l1_key    setll     ra05l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2etxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra05l2_etxt = b2etxt
     c     ra05l2_key    setll     ra05l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ekod = *blank
     c                   eval      b2etxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ekod = b1ekod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ekod = b1ekod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ekod = b1ekod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ekod = b1ekod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ekod = b2ekod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1ekod = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1ekod = *blank
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra05lr_ekod = c1ekod
     c     ra05lr_key    chain     ra05lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ekod = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ekod = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c     xc2start      tag
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra05lr_ekod = c1ekod
     c     ra05lr_key    chain     ra05lr
7.00 c     ra05lr_key    chain     rat5pf
      *
7.00 c                   if        %found(ra05lr)
     c                   eval      c2etxt = raetxt
     c                   eval      c2epro = raepro
     c                   eval      c2einv = raeinv
      *
     c                   if        raemda = *loval
     c                   move      *zeros        c2emda
     c                   else
     c     *dmy          move      raemda        c2emda
     c                   endif
      *
     c                   eval      c2egpr = raegpr
     c                   eval      c2egin = raegin
     c                   eval      c2ebmk = raebmk
     c                   eval      c2ebpr = raebpr
7.00 c                   if        %found(rat5pf)
7.00 c                   eval      c2esmk = rtesmk
8.00 c                   eval      c2spss = rtspss
8.00 c                   eval      c2prut = rtprut
7.00 c                   else
7.00 c                   eval      c2esmk = *zero
7.00 c                   eval      c2spss = 'N'
7.00 c                   eval      c2prut = *blank
7.00 c                   endif
7.01  *
7.01 c                   eval      c2stxt = *blank
7.01 c                   if        c2esmk > *zero
7.01 c     c2esmk        chain     ras5pf
7.01 c                   if        %found(ras5pf)
7.01 c                   eval      c2stxt = rsetxt
7.01 c                   endif
7.01 c                   endif
      *
     c                   if        raepda = *loval
     c                   move      *zeros        c2epda
     c                   else
     c     *dmy          move      raepda        c2epda
     c                   endif
      *
     c                   eval      c2ebgp = raebgp
     c                   eval      c2eusr = raeusr
     c     *dmy          move      raedat        c2edat
     c                   else
     c                   eval      c2etxt = *blank
     c                   eval      c2epro = *zeros
     c                   eval      c2einv = *zeros
     c                   eval      c2emda = *zeros
     c                   eval      c2egpr = *zeros
     c                   eval      c2egin = *zeros
     c                   eval      c2ebmk = *zeros
     c                   eval      c2ebpr = *zeros
     c                   eval      c2epda = *zeros
7.00 c                   eval      c2esmk = *zeros
8.00 c                   eval      c2spss = 'N'
8.00 c                   eval      c2prut = *blank
     c                   eval      c2ebgp = *zeros
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ekod = k1ekod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
8.01  *  Ta vare på inngangsverdier
8.01 c                   eval      s_epro = c2epro
8.03 c                   eval      s_emda = c2emda
8.01 c                   if        c2emda <> *zero
8.01 c     *dmy          move      c2emda        s_ltda
8.02 c                   else
8.02 c                   eval      s_ltda = *loval
8.02 c                   endif
8.01 c                   eval      s_lsmk = c2esmk
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
7.00 c                   call      'RA5S5R'
7.00 c                   parm                    c2esmk
7.00 c                   parm                    c2stxt
     c                   goto      c2taga
     c                   endif
8.01  *
8.01  * Historikk
8.01  *
8.01 c                   if        *inkg
8.01 c                   exsr      HistMva
8.01 c                   goto      xc2Start
8.01 c                   endif
      *
      * Validering av input
      *
     c                   if        c2etxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          test(d)                 c2emda                 32
     c                   if        *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          test(d)                 c2epda                 33
     c                   if        *in33 = *on
     c                   goto      c2taga
     c                   endif
8.03  *
8.03  *  Sjekk om endringsdato er endret dersom sats er endret
8.03  *
8.03 c                   if        c2epro <> s_epro
8.03 c                             and c2emda = s_emda
8.03 c                   eval      m1 = 'Du har endret mva-prosent +
8.03 c                                   uten å endre gjelder fra'
8.03 c                   eval      m2 = 'dato.   Svar >J< dersom +
8.03 c                                   dato skal beholdes.'
8.03 c                   call      'AA007R'
8.03 c                   parm                    m1
8.03 c                   parm                    m2
8.03 c                   parm                    sv
8.03 c                   parm                    in03
8.03 c                   if        sv <> 'J'
8.03 c                   goto      c2taga
8.03 c                   endif
8.03 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra05lur
      *
     c                   eval      ra05lu_ekod = c1ekod
     c     ra05lu_key    chain     ra05lur
7.00 c     ra05lu_key    chain     rat5pfr
      *
     c                   eval      raetxt = c2etxt
     c                   eval      raepro = c2epro
     c                   eval      raeinv = c2einv
     c     *dmy          move      c2emda        raemda
     c                   eval      raegpr = c2egpr
     c                   eval      raegin = c2egin
     c                   eval      raebmk = c2ebmk
7.00 c                   eval      rtesmk = c2esmk
8.00 c                   eval      rtspss = c2spss
8.00 c                   eval      rtprut = c2prut
      *
     c                   if        c2ebpr = *zeros
     c                   eval      raebpr = 100
     c                   else
     c                   eval      raebpr = c2ebpr
     c                   endif
      *
     c     *dmy          move      c2epda        raepda
      *
     c                   if        c2ebgp = *zeros
     c                   eval      raebgp = 100
     c                   else
     c                   eval      raebgp = c2ebgp
     c                   endif
      *
     c                   time                    raedat
     c                   time                    raetim
     c                   eval      raeusr = l_user
      *
7.00 c                   if        %found(ra05lu)
     c                   update    ra05lur
     c                   else
     c                   eval      raefir = w_firm
     c                   eval      raekod = c1ekod
     c                   write     ra05lur
     c                   endif
7.00  *
7.00  *   Oppdatere tilleggsregister
7.00  *
7.00 c                   time                    rteeda
7.00 c                   time                    rteeti
7.00 c                   eval      rteeus = l_user
7.00 c                   if        %found(rat5pf)
7.00 c                   update    rat5pfr
7.00 c                   else
7.00 c                   eval      rtefir = w_firm
7.00 c                   eval      rtekod = c1ekod
7.00 c                   write     rat5pfr
7.00 c                   endif
8.01  *
8.01  *   Oppdatere endringslogg
8.01  *
     c     *dmy          move      c2emda        w_ltda
8.01 c                   if           c2epro <> s_epro
8.01 c                             or w_ltda <> s_ltda
8.01 c                   eval      ral5pf_firm = l_firm
8.01 c                   eval      ral5pf_ekod = raekod
8.01 c                   eval      ral5pf_ltda = raemda - %days(1)
8.01 c     ral5pf_key    chain     ral5pf
8.01 c                   eval      rtlmpr = s_epro
8.01 c                   eval      rtlsmk = s_lsmk
8.01 c                   time                    rtleda
8.01 c                   time                    rtleti
8.01 c                   eval      rtleus = l_user
8.01 c                   if        %found(ral5pf)
8.01 c                   update    ral5pfr
8.01 c                   else
8.01 c                   eval      rtlfir = w_firm
8.01 c                   eval      rtlkod = c1ekod
8.01 c                   eval      rtltda = raemda - %days(1)
8.01 c                   write     ral5pfr
8.01 c                   endif
8.01 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1etxt = raetxt
     c                   eval      b1epro = raepro
     c     *dmy          move      raemda        b1emda
     c                   eval      b1egpr = raegpr
     c                   eval      b1ebmk = raebmk
7.00 c                   eval      b1esmk = rtesmk
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra05lr_ekod = d1ekod
     c     ra05lr_key    chain     ra05lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra05lu_ekod = d1ekod
     c     ra05lu_key    chain     ra05lur
      *
     c                   delete    ra05lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ekod = k1ekod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra05lr_ekod = k1ekod
     c     ra05lr_key    chain     ra05lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1ekod = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra05lr_ekod = k1ekod
     c     ra05lr_key    chain     ra05lr
7.00 c                   if        %found(ra05lr)
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ekod = s_ekod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
8.01  *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  * Subrutine for behandling av historikk     (L1WIN)
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     HistMva       begsr
8.01  *
     c                   eval      l1lin1 = *blank
     c                   eval      l1lin2 = *blank
     c                   eval      l1lin3 = *blank
     c                   eval      l1lin4 = *blank
     c                   eval      l1lin5 = *blank
     c                   eval      l1lin6 = *blank
     c                   eval      l1lin7 = *blank
      *
8.01 c                   eval      ral5pf_firm = l_firm
8.01 c                   eval      ral5pf_ekod = c1ekod
8.01 c                   eval      ral5pf_ltda = *hival
8.01 c     ral5pf_key    setgt     ral5pf
8.01 c                   readp     ral5pf
8.01  *
8.01 c                   dow       not %eof(ral5pf)
8.01 c                             and rtlkod = c1ekod
8.01 c                             and l1lin7 = *blank
8.01 c                   select
8.01 c                   when      l1lin1 = *blank
8.01 c                   eval      l1lin1 = rtlkod + '    ' +
8.01 c                             %char(rtltda) + '        ' +
8.01 c                             %char(rtlmpr)
8.01 c                   when      l1lin2 = *blank
8.01 c                   eval      l1lin2 = rtlkod + '    ' +
8.01 c                             %char(rtltda) + '        ' +
8.01 c                             %char(rtlmpr)
8.01 c                   when      l1lin3 = *blank
8.01 c                   eval      l1lin3 = rtlkod + '    ' +
8.01 c                             %char(rtltda) + '        ' +
8.01 c                             %char(rtlmpr)
8.01 c                   when      l1lin4 = *blank
8.01 c                   eval      l1lin4 = rtlkod + '    ' +
8.01 c                             %char(rtltda) + '        ' +
8.01 c                             %char(rtlmpr)
8.01 c                   when      l1lin5 = *blank
8.01 c                   eval      l1lin5 = rtlkod + '    ' +
8.01 c                             %char(rtltda) + '        ' +
8.01 c                             %char(rtlmpr)
8.01 c                   when      l1lin6 = *blank
8.01 c                   eval      l1lin6 = rtlkod + '    ' +
8.01 c                             %char(rtltda) + '        ' +
8.01 c                             %char(rtlmpr)
8.01 c                   when      l1lin7 = *blank
8.01 c                   eval      l1lin7 = rtlkod + '    ' +
8.01 c                             %char(rtltda) + '        ' +
8.01 c                             %char(rtlmpr)
8.01 c                   endsl
8.01 c                   readp     ral5pf
8.01 c                   enddo
8.01  *
8.01 c                   exfmt     l1win
8.01  *
8.01 c     end_HistMva   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra05l2
     c                   other
     c                   read      ra05l1
     c                   endsl
      *
     c                   if        %eof or
     c                             raefir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
7.00  *
7.00  *    Hent opp tilleggsregiter
7.00  *
7.00 c                   eval      rat5pf_ekod = raekod
7.00 c     rat5pf_key    chain     rat5pf
7.00 c                   if        not %found(rat5pf)
7.00 c                   eval      rtesmk = *zero
8.00 c                   eval      rtspss = 'N'
8.00 c                   eval      rtprut = *blank
7.00 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra05l2_etxt = raetxt
     c                   other
     c                   eval      ra05l1_ekod = raekod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ekod = raekod
     c                   eval      b1etxt = raetxt
     c                   eval      b1epro = raepro
     c     *dmy          move      raemda        b1emda
     c                   eval      b1egpr = raegpr
     c                   eval      b1ebmk = raebmk
7.00 c                   eval      b1esmk = rtesmk
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra05l2_key    setgt     ra05l2
     c                   readp     ra05l2
     c                   other
     c     ra05l1_key    setgt     ra05l1
     c                   readp     ra05l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra05l2
     c                   other
     c                   readp     ra05l1
     c                   endsl
      *
     c                   if        %eof or
     c                             raefir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra05l2_etxt = raetxt
     c                   other
     c                   eval      ra05l1_ekod = raekod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra05l2_key    setll     ra05l2
     c                   other
     c     ra05l1_key    setll     ra05l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på mva-kode
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra05l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l2_etxt
      *
      * Key for les på mva-kode
      *
     c     ra05lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05lr_ekod
      *
      * Key for oppdatering av mva-kode
      *
     c     ra05lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05lu_ekod
7.00  *
7.00 c     rat5pf_key    klist
7.00 c                   kfld                    w_firm
7.00 c                   kfld                    rat5pf_ekod
8.01  *
8.01 c     ral5pf_key    klist
8.01 c                   kfld                    ral5pf_firm
8.01 c                   kfld                    ral5pf_ekod
8.01 c                   kfld                    ral5pf_ltda
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra05l1_ekod = *blank
     c     ra05l1_key    setll     ra05l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
