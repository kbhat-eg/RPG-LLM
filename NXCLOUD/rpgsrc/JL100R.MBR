     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVARE
      * Program . . . . : JL100R
      * Beskrivelse . . : Leverandør, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
7.01  * Spesialversjon. : Tilleggsregister, med kode for aut.overf.
7.01  * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020999 HKO  Nytt program
      *       110909 HKO  Utvidet med mer informasjon
      *                   Lagt inn valg for kalkulering fra Nobbpris
      *       150905 HKO  Lagt inn frisøk
5.60  *       220508 bof  Lagt inn nytt felt : prioritert leverandør
6.10  *       240809 bof  Endringer i forb. eget kontaktperson-register
6.11  *       100909 bof  Lagt inn grønt punkt og miljømerke
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  040316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       120320 bof  Fjernet 2 felter fra bildet  MGR pris
7.02  *       290615 bof  Kode for automatisk overføring
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjlevl2    if   e           k disk    rename(jlevpfr:jlevl2r)
     fjlevlr    if   e           k disk    rename(jlevpfr:jlevlrr)
7.02 fjle9lr    if   e           k disk    rename(jle9pfr:jle9lrr)
     fjlevlu    uf a e           k disk    rename(jlevpfr:jlevlur)
7.02 fjle9lu    uf a e           k disk    rename(jle9pfr:jle9lur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fjl100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d jlevl2_navn     s                   like(jlnavn)
     d jlevlr_ldor     s                   like(jlldor)
     d jlevlu_ldor     s                   like(jlldor)
     d rlevl1_levr     s                   like(rllevr)
7.02 d jle9lr_xldo     s                   like(jlxldo)
7.02 d jle9lu_xldo     s                   like(jlxldo)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
      *
     d s_stek          s             35
      *
     d w_valg          s              1  0
     d w_firm          s                   like(rlfirm)
      *
     d s_ldor          s                   like(jlldor)
      *
7.01  * Eksternt program
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2navn <> *blank or
     c                             b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'L'
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    setll     jlevl1
     c                   other
     c                   eval      jlevl2_navn = b1navn
     c     jlevl2_key    setll     jlevl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS701R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Navn
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jlevl2_navn = b2navn
     c     jlevl2_key    setll     jlevl2
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      jlevl1_ldor = b2ldor
     c     jlevl1_key    setll     jlevl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2navn = *blank
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b1ldor
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ldor = b1ldor
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ldor = b1ldor
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b1ldor
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kontaktpersoner
      *
     c                   if        b1valg = 7
     c                   eval      w_valg = 5
     c                   call      'JL110R '
     c                   parm                    b1ldor
     c                   parm                    w_valg
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * GS1 Lokasjon
      *
     c                   if        b1valg = 8
     c                   eval      w_valg = 5
     c                   call      'JL111R '
     c                   parm                    b1ldor
     c                   parm                    w_valg
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ldor = b2ldor
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1ldor = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jlevlr_ldor = c1ldor
     c     jlevlr_key    chain     jlevlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ldor = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ldor = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jlevlr_ldor = c1ldor
     c     jlevlr_key    chain     jlevlr
      *
     c                   if        %found
     c                   eval      c2navn = jlnavn
     c                   eval      c2adr1 = jladr1
     c                   eval      c2adr2 = jladr2
     c                   eval      c2ponr = jlponr
     c                   eval      c2sted = jlsted
     c                   eval      c2tlfn = jltlfn
     c                   eval      c2mobn = jlmobn
     c                   eval      c2faxn = jlfaxn
     c                   eval      c2mail = jlmail
     c                   eval      c2urla = jlurla
     c                   eval      c2orgn = jlorgn
6.11 c*                  eval      c2eanl = jleanl
     c                   eval      c2valu = jlvalu
     c                   eval      c2land = jlland
     c                   eval      c2dtyp = jldtyp
     c                   eval      c2kvav = jlkvav
5.60 c                   eval      c2prio = jlprio
6.11 c                   eval      c2grpk = jlgrpk
6.11 c                   eval      c2mmrk = jlmmrk
     c                   eval      c2kmod = jlkmod
     c                   eval      c2kkal = jlkkal
     c                   eval      c2fkal = jlfkal
     c                   eval      c2enum = jlenum
     c                   eval      c2enav = *blank
     c                   eval      c2eusr = jleusr
     c     *dmy          move      jlodat        c2odat
     c     *dmy          move      jledat        c2edat
     c                   else
     c                   eval      c2navn = *blank
     c                   eval      c2adr1 = *blank
     c                   eval      c2adr2 = *blank
     c                   eval      c2ponr = *blank
     c                   eval      c2sted = *blank
     c                   eval      c2tlfn = *blank
     c                   eval      c2mobn = *blank
     c                   eval      c2faxn = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2urla = *blank
     c                   eval      c2orgn = 0
6.11 c*                  eval      c2eanl = 0
     c                   eval      c2valu = *blank
     c                   eval      c2land = *blank
     c                   eval      c2dtyp = 0
     c                   eval      c2kvav = 0
5.50 c                   eval      c2prio = 0
6.11 c                   eval      c2grpk = 0
6.11 c                   eval      c2mmrk = *blank
     c                   eval      c2kmod = 0
     c                   eval      c2kkal = 0
     c                   eval      c2fkal = 0
     c                   eval      c2enum = 0
     c                   eval      c2enav = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
7.02  *
7.02 c                   eval      jle9lr_xldo = c1ldor
7.02 c     jle9lr_key    chain     jle9lr
7.02 c                   if        %found(jle9lr)
7.02 c                   eval      c2xaut = jlxaut
7.02 c                   else
7.02 c                   eval      c2xaut = 0
7.02 c                   endif
      *
      * Henter navn på leverandør i EVR
      *
     c                   if        c2enum <> 0
     c                   eval      rlevl1_levr = c2enum
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      c2enav = rlnavn
     c                   endif
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ldor = k1ldor
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c2enum
     c                   parm                    c2enav
     c                   goto      c2taga
     c                   endif
      *
      * Kontaktpersoner
      *
     c                   if        *inkj
     c                   eval      w_valg = b1valg
     c                   call      'JL110R '
     c                   parm                    c1ldor
     c                   parm                    w_valg
     c                   goto      c2taga
     c                   endif
      *
      * GS1 Lokasjon
      *
     c                   if        *inkk
     c                   eval      w_valg = b1valg
     c                   call      'JL111R '
     c                   parm                    c1ldor
     c                   parm                    w_valg
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2navn = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      c2enav = *blank
     c                   if        c2enum <> 0
     c                   eval      rlevl1_levr = c2enum
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      c2enav = rlnavn
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jlevlur
      *
     c                   eval      jlevlu_ldor = c1ldor
     c     jlevlu_key    chain     jlevlur
      *
     c                   eval      jlnavn = c2navn
     c                   eval      jladr1 = c2adr1
     c                   eval      jladr2 = c2adr2
     c                   eval      jlponr = c2ponr
     c                   eval      jlsted = c2sted
     c                   eval      jltlfn = c2tlfn
     c                   eval      jlmobn = c2mobn
     c                   eval      jlfaxn = c2faxn
     c                   eval      jlmail = c2mail
     c                   eval      jlurla = c2urla
     c                   eval      jlorgn = c2orgn
6.11 c*                  eval      jleanl = c2eanl
     c                   eval      jlvalu = c2valu
     c                   eval      jlland = c2land
     c                   eval      jldtyp = c2dtyp
     c                   eval      jlkvav = c2kvav
5.60 c                   eval      jlprio = c2prio
6.11 c                   eval      jlgrpk = c2grpk
6.11 c                   eval      jlmmrk = c2mmrk
     c                   eval      jlkmod = c2kmod
     c                   eval      jlkkal = c2kkal
     c                   eval      jlfkal = c2fkal
     c                   eval      jlenum = c2enum
      *
     c                   time                    jledat
     c                   time                    jletim
     c                   eval      jleusr = l_user
      *
     c                   if        %found
     c                   update    jlevlur
     c                   else
     c                   eval      jlldor = c1ldor
     c                   eval      jlfaxo = *blank
     c                   eval      jlrsts = 1
     c                   time                    jlodat
     c                   write     jlevlur
     c                   endif
7.02  *
7.02 c                   clear                   jle9lur
7.02  *
7.02 c                   eval      jle9lu_xldo = c1ldor
7.02 c     jle9lu_key    chain     jle9lur
7.02  *
     c                   eval      jlxaut = c2xaut
7.02 c                   time                    jlxeda
7.02 c                   time                    jlxeti
7.02 c                   eval      jlxeus = l_user
7.02  *
7.02 c                   if        %found
7.02 c                   update    jle9lur
7.02 c                   else
7.02 c                   eval      jlxldo = c1ldor
7.02 c                   time                    jlxoda
7.02 c                   write     jle9lur
7.02 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1navn = jlnavn
     c                   eval      b1kvav = jlkvav
     c                   eval      b1kmod = jlkmod
     c                   eval      b1kkal = jlkkal
     c                   eval      b1enum = jlenum
     c                   eval      b1enav = c2enav
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jlevlr_ldor = d1ldor
     c     jlevlr_key    chain     jlevlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jlevlu_ldor = d1ldor
     c     jlevlu_key    chain     jlevlur
      *
     c                   delete    jlevlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ldor = k1ldor
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jlevlr_ldor = k1ldor
     c     jlevlr_key    chain     jlevlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1ldor = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jlevlr_ldor = k1ldor
     c     jlevlr_key    chain     jlevlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = s_ldor
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
     c+                  select jlldor,
     c+                         jlnavn,
     c+                         jlkvav,
     c+                         jlkmod,
     c+                         jlkkal,
     c+                         jlenum
     c+                  from   jlevpf
     c+                  where  upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste1 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste2 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste3 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste4 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste5
     c+                  order by jlnavn, jlldor
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :jlldor,
     c+                        :jlnavn,
     c+                        :jlkvav,
     c+                        :jlkmod,
     c+                        :jlkkal,
     c+                        :jlenum
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'L'
     c                   read      jlevl1                                 15
     c                   other
     c                   read      jlevl2                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = *blank
     c                   eval      jlevl2_navn = jlnavn
     c                   when      w_seqe = 'L'
     c                   eval      jlevl1_ldor = jlldor
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldor = jlldor
     c                   eval      b1navn = jlnavn
     c                   eval      b1kvav = jlkvav
     c                   eval      b1kmod = jlkmod
     c                   eval      b1kkal = jlkkal
     c                   eval      b1enum = jlenum
     c                   eval      b1enav = *blank
     c                   eval      b1rsts = *blank
      *
      * Henter navn på leverandør i EVR
      *
     c                   if        jlenum <> 0
     c                   eval      rlevl1_levr = jlenum
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1enav = rlnavn
     c                   endif
     c                   endif
      *
      * Merker rader som er lagt inn lokalt
      *
     c                   if        jlrsts = 1
     c                   eval      b1rsts = 'L'
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'S'
     c                   exsr      forny
     c                   leavesr
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     jlevl1_key    setgt     jlevl1
     c                   readp     jlevl1
     c                   other
     c     jlevl2_key    setgt     jlevl2
     c                   readp     jlevl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     jlevl1
     c                   other
     c                   readp     jlevl2
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      jlevl1_ldor = jlldor
     c                   other
     c                   eval      jlevl2_navn = jlnavn
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     jlevl1_key    setll     jlevl1
     c                   other
     c     jlevl2_key    setll     jlevl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for posisjonering på navn
      *
     c     jlevl2_key    klist
     c                   kfld                    jlevl2_navn
      *
      * Key for les på leverandør
      *
     c     jlevlr_key    klist
     c                   kfld                    jlevlr_ldor
      *
      * Key for oppdatering av leverandør
      *
     c     jlevlu_key    klist
     c                   kfld                    jlevlu_ldor
      *
      * Key for oppslag på leverandør i EVR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
7.02  *
7.02  * Key for les av tilleggsregister
7.02  *
7.02 c     jle9lr_key    klist
7.02 c                   kfld                    jle9lr_xldo
7.02  *
7.02  * Key for upd av tilleggsregister
7.02  *
7.02 c     jle9lu_key    klist
7.02 c                   kfld                    jle9lu_xldo
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   *in41 = *on;
7.01   CallP CO402R(l_filg:w_firm:0:'JL100R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     *in41 = *off;
7.01   endif;
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jlevl2_navn = *blank
     c     jlevl2_key    setll     jlevl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
