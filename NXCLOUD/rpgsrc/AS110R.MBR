     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : AS110R
      * Beskrivelse . . : Henter høyeste suffiks
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *K0000  090424 bsa  Nytt program
8.01  *K0000  160424 bsa  Hvis den runder 99, må vi starte på nytt.
8.02  *K0000  180424 bsa  Hvis den runder 99, endre logikk.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fsohel3    if   e           k disk    rename(sohepfr:sohel3r)
     ffdellr    if   e           k disk    rename(fdelpfr:fdellrr)
      *
      * Definering av data-struktur
      *
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d sohel3_aarr     s                   like(soaarr)
     d sohel3_numm     s                   like(sonumm)
     d sohel3_binr     s                   like(sobinr)
     d sohel3_suff     s                   like(sosuff)
     d fdellr_aarr     s                   like(fyaarr)
     d fdellr_numm     s                   like(fynumm)
     d fdellr_suff     s                   like(sosuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_suff          s              3  0
     d w_osuf          s                   like(fosuff)
     d w_hsuf          s                   like(fosuff)
     d w_dsuf          s                   like(fosuff)
      *
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Beskrivelse av spesielle felter som er benyttet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_suff = 0
     c                   eval      w_osuf = 0
     c                   eval      w_hsuf = 0
     c                   eval      w_dsuf = 0
     c                   eval      fohelr_numm = p_numm
     c                   eval      sohel3_numm = p_numm
     c                   eval      fdellr_numm = p_numm
8.02 c                   eval      fohel1_numm = p_numm
      *
      * Hent høyeste suffiks fra ordretabellen
      *
     c                   exsr      hnt_o_suf
      *
      * Hent høyeste suffiks fra historisk fakturatabell
      *
     c                   exsr      hnt_h_suf
      *
      * Hent høyeste suffikks fra slettede ordre
      *
     c                   exsr      hnt_d_suf
      *
8.02 c**                 eval      w_suff = w_osuf
      *
     c                   if        w_hsuf >= w_suff and
     c                             w_hsuf > 0
     c                   eval      w_suff = w_hsuf + 1
     c                   endif
      *
     c                   if        w_dsuf >= w_suff and
     c                             w_dsuf > 0
     c                   eval      w_suff = w_dsuf + 1
     c                   endif
8.01  *
8.01  * Hvis suffikset har rundet 100, må vi begynne på nytt
8.01  *
8.01 c                   if        w_suff > 99
8.01 c                   eval      w_suff = 0
8.01 c                   endif
      *
      * Dobbeltsjekk at suffiks ikke allerede finnes
      *
     c     ny_suffiks    tag
     c                   eval      fohel1_suff = w_suff
     c     fohel1_key    chain     fohel1
     c                   if        %found
     c                   eval      w_suff = w_suff +1
     c                   goto      ny_suffiks
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      p_suff = w_suff
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix fra ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_o_suf     begsr
      *
8.02 c                   eval      fohel1_suff = 99
8.02 c     fohel1_key    chain     fohel1
8.02 c                   if        %found
8.02 c                   eval      w_osuf = 99
8.02 c                   goto      suffiks
8.02 c                   endif
      *
     c                   eval      fohelr_suff = 99
     c     fohelr_key    setll     fohelr
     c     fohelr_key2   readpe    fohelr
     c                   if        not %eof
8.02  * Hvis 98 har vi gått rundt
8.02 c                   if        fosuff = 98
8.02 c                   eval      w_osuf = 99
8.02 c                   else
     c                   eval      w_osuf = fosuff
8.02 c                   endif
     c                   else
8.02 c                   eval      w_osuf = -1
     c                   endif
      *
8.02 c     suffiks       tag
      *
8.02 c                   eval      w_suff = w_osuf + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix fra historisk faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_h_suf     begsr
      *
     c                   eval      sohel3_aarr = *year
     c                   eval      sohel3_numm = p_numm
     c                   eval      sohel3_binr = 0
     c                   eval      sohel3_suff = 0
     c     sohel3_key    setll     sohel3
     c     sohel3_key2   reade     sohel3
     c                   dow       not %eof
     c                   if        sosuff > w_hsuf
     c                   eval      w_hsuf = sosuff
     c                   endif
     c     sohel3_key2   reade     sohel3
     c                   enddo
      *
      * Sjekk også forrige år, hvis det skulle vært fakturert noe der.
      *
     c                   eval      sohel3_aarr = *year - 1
     c                   eval      sohel3_numm = p_numm
     c                   eval      sohel3_binr = 0
     c                   eval      sohel3_suff = 0
     c     sohel3_key    setll     sohel3
     c     sohel3_key2   reade     sohel3
     c                   dow       not %eof
     c                   if        sosuff > w_hsuf
     c                   eval      w_hsuf = sosuff
     c                   endif
     c     sohel3_key2   reade     sohel3
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter neste ledige suffix fra slettet tabeller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_d_suf     begsr
      *
     c                   eval      fdellr_aarr = *year
     c                   eval      fdellr_numm = p_numm
     c                   eval      fdellr_suff = 0
     c     fdellr_key    setll     fdellr
     c     fdellr_key2   reade     fdellr
     c                   dow       not %eof
     c                   if        fysuff > w_dsuf
     c                   eval      w_dsuf = fysuff
     c                   endif
     c     fdellr_key2   reade     fdellr
     c                   enddo
      *
      * Sjekk også forrige år, hvis det skulle vært fakturert noe der.
      *
     c                   eval      fdellr_aarr = *year - 1
     c                   eval      fdellr_numm = p_numm
     c                   eval      fdellr_suff = 0
     c     fdellr_key    setll     fdellr
     c     fdellr_key2   reade     fdellr
     c                   dow       not %eof
     c                   if        fysuff > w_dsuf
     c                   eval      w_dsuf = fysuff
     c                   endif
     c     fdellr_key2   reade     fdellr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
      *
      * Key for file : Historisk  fakturaregister - SOHE
      *
     c     sohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel3_aarr
     c                   kfld                    sohel3_numm
     c                   kfld                    sohel3_binr
     c                   kfld                    sohel3_suff
      *
      * Key for file : Historisk  fakturaregister - SOHE
      *
     c     sohel3_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    sohel3_aarr
     c                   kfld                    sohel3_numm
      *
      * Key for file : Sletta tabeller - FDEL
      *
     c     fdellr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fdellr_aarr
     c                   kfld                    fdellr_numm
     c                   kfld                    fdellr_suff
      *
      * Key for file : Sletta tabeller - FDEL
      *
     c     fdellr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fdellr_aarr
     c                   kfld                    fdellr_numm
      *
      * Hente inn brukerregister
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
