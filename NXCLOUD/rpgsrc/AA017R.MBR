     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AA017R
      * Beskrivelse . . : Nummer-serier, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230604 AMD  Nytt program
5.41  * R5.41 270605 AMD  Viser databibliotek i skjermbilde i stedet
      *                   for firmanavn da det er misvisende
6.nu  * R6.30 180614 bof  Utvidet for bruk av 8 siffer i nummerserie
6.nu  *                   Kun enkelte serier tillates for 8 siffer,testes
6.nu  *                   mot statusregister.
6.30  * R6.30 120615 bof  lagt inn test for AKIDR
6.31  * R6.30 060717 bof  BETING skal også ha 8 siffer
6.32  * R6.30 171117 bsa  KBILAG skal også ha 8 siffer. Kortbetaling Santander
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *K000   220821 bsa  LEDIML skal også ha 8 siffer. Løpenummer EDI meldinger
8.02  *K000   020921 bsa  AKID13 skal kunne ha 8 siffer. Brukes for løpenummer
8.02  *K000               ved KID og Santander KID på 13 siffer.
8.03  * 800   051121 ask  POSBIL skal kunne ha 8 siffer. Brukes for bilagsnummer
8.03  *                   pr. bong ved overføring til SAP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fanuml1    if   e           k disk    rename(anumpfr:anuml1r)
     fanuml2    if   e           k disk    rename(anumpfr:anuml2r)
     fanumlr    if   e           k disk    rename(anumpfr:anumlrr)
6.nu ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.nu flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.nu fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fanumlu    uf a e           k disk    rename(anumpfr:anumlur)
      *
     faa017d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
5.41 d l_sbib                934    943
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d anuml1_firm     s                   like(anfirm)
     d anuml1_fell     s                   like(anfell)
     d anuml1_type     s                   like(antype)
     d anuml1_fast     s                   like(anfast)
     d anuml2_firm     s                   like(anfirm)
     d anuml2_fell     s                   like(anfell)
     d anuml2_nfom     s                   like(annfom)
     d anumlr_firm     s                   like(anfirm)
     d anumlr_fell     s                   like(anfell)
     d anumlr_type     s                   like(antype)
     d anumlr_fast     s                   like(anfast)
     d anumlu_firm     s                   like(anfirm)
     d anumlu_fell     s                   like(anfell)
     d anumlu_type     s                   like(antype)
     d anumlu_fast     s                   like(anfast)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(anfirm)
      *
     d s_firm          s                   like(anfirm)
     d s_fell          s                   like(anfell)
     d s_type          s                   like(antype)
     d s_fast          s                   like(anfast)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2fell <> *blank or
     c                             b2firm <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      anuml1_firm = b1firm
     c                   eval      anuml1_fell = b1fell
     c                   eval      anuml1_type = b1type
     c                   eval      anuml1_fast = b1fast
     c     anuml1_key    setll     anuml1
     c                   other
     c                   eval      anuml1_firm = b1firm
     c                   eval      anuml1_fell = b1fell
     c                   eval      anuml1_type = b1type
     c                   eval      anuml1_fast = b1fast
     c     anuml1_key    setll     anuml1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Her er det foreløpig lagt inn følgende løsning;
      * -----------------------------------------------
      * Ved posisjonering benyttes det bare en logisk file.
      * Dersom man oppgir firma eller identifikasjon aktiveres
      * posisjoneringen.  Ta opp senere om det bør være to
      * logiske filer her slik at dersom man oppgir firmanummer
      * kommer det i firmanummerrekkefølge og dersom man oppgir
      * identifikasjon kommer denne på tvers av alle firmanummer.
      * (Men hva da med de som kjører med flere firma og kun ønsker
      *  å få opp firma 000 ?)
      *
      * Logikken for to logiske filer er behold i programmet selv
      * om det ser litt rart ut at to betingelser velger samme logiske
      * fil.  Men her kan det komme endringer og derfor der ikke dette
      * tatt bort.
      *
      *
      * Identifikasjon
      *
     c                   if        b2fell <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      anuml1_firm = b2firm
     c                   eval      anuml1_fell = b2fell
     c     anuml1_key    setll     anuml1
     c                   goto      end_pos
     c                   endif
      *
      * Firmanummer
      *
     c                   if        b2firm <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      anuml1_firm = b2firm
     c                   eval      anuml1_fell = b2fell
     c     anuml1_key    setll     anuml1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2fell = *blank
     c                   eval      b2firm = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1firm = b1firm
     c                   eval      c1fell = b1fell
     c                   eval      c1type = b1type
     c                   eval      c1fast = b1fast
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1firm = b1firm
     c                   eval      k1fell = b1fell
     c                   eval      k1type = b1type
     c                   eval      k1fast = b1fast
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1firm = b1firm
     c                   eval      d1fell = b1fell
     c                   eval      d1type = b1type
     c                   eval      d1fast = b1fast
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1firm = b1firm
     c                   eval      c1fell = b1fell
     c                   eval      c1type = b1type
     c                   eval      c1fast = b1fast
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1firm = b2firm
     c                   eval      c1fell = b2fell
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1fell = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1fell = *blank
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      anumlr_firm = c1firm
     c                   eval      anumlr_fell = c1fell
     c                   eval      anumlr_type = c1type
     c                   eval      anumlr_fast = c1fast
     c     anumlr_key    chain     anumlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1firm = 0
     c                   eval      c1fell = *blank
     c                   eval      c1type = *blank
     c                   eval      c1fast = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2firm = 0
     c                   eval      b2fell = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      anumlr_firm = c1firm
     c                   eval      anumlr_fell = c1fell
     c                   eval      anumlr_type = c1type
     c                   eval      anumlr_fast = c1fast
     c     anumlr_key    chain     anumlr
      *
     c                   if        %found
     c                   eval      c2txt1 = antxt1
     c                   eval      c2txt2 = antxt2
     c                   eval      c2wrap = anwrap
     c                   eval      c2Årnu = anÅrnu
     c                   eval      c2auto = anauto
     c                   eval      c2nfom = annfom
     c                   eval      c2ntom = anntom
     c                   eval      c2sist = ansist
     c                   eval      c2leng = anleng
     c*>                 eval      c2eusr = aheusr                    ¤¤¤¤¤¤
     c*>   *dmy          move      ahodat        c2odat               ¤¤¤¤¤¤
     c*>   *dmy          move      ahedat        c2edat               ¤¤¤¤¤¤
     c                   else
     c                   eval      c2txt1 = *blank
     c                   eval      c2txt2 = *blank
     c                   eval      c2wrap = *blank
     c                   eval      c2Årnu = *blank
     c                   eval      c2auto = *blank
     c                   eval      c2nfom = 0
     c                   eval      c2ntom = 0
     c                   eval      c2sist = 0
     c                   eval      c2leng = 0
     c*>                 eval      c2eusr = *blank                    ¤¤¤¤¤¤
     c*>                 eval      c2odat = 0                         ¤¤¤¤¤¤
     c*>                 eval      c2edat = 0                         ¤¤¤¤¤¤
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1firm = k1firm
     c                   eval      c1fell = k1fell
     c                   eval      c1type = k1type
     c                   eval      c1fast = k1fast
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2txt1 = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk, høyeste skal være større enn laveste
      *
     c                   if        c2ntom <= c2nfom
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
6.nu  *
6.nu  * Sjekk, tillates 8 siffer på valgt nummerserie ?
6.nu  *
6.nu c                   if        c2leng = 1
6.nu c                   if        c1fell <> faaork and
6.nu c                             c1fell <> faafak and
6.nu c                             c1fell <> faadel and
6.nu c                             c1fell <> laabrk and
6.nu c                             c1fell <> laabin and
6.nu c                             c1fell <> laadel and
6.nu c                             c1fell <> baaord and
6.30 c                             c1fell <> baabnr and
6.30 c                             c1fell <> 'AKIDDR'  and
6.31 c                             c1fell <> 'BETING'  and
8.01 c                             c1fell <> 'KBILAG'  and
8.02 c                             c1fell <> 'LEDIML'  and
8.02 c                             c1fell <> 'AKID13'  and
8.02 c                             c1fell <> 'POSBIL'
6.nu c                   eval      *in35 = *on
6.nu c                   goto      c2taga
6.nu c                   endif
6.nu c                   endif
6.nu c                   if        c2leng = 0
6.nu c                   if        c2nfom > 999999 or
6.nu c                             c2ntom > 999999
6.nu c                   eval      *in36 = *on
6.nu c                   goto      c2taga
6.nu c                   endif
6.nu c                   endif
6.nu c
      *
      *  Sjekk, felles serie
      *
     c                   eval      anuml2_firm = c1firm                                      .
     c                   eval      anuml2_nfom = 0                                           .
     c     anuml2_key    setll     anuml2
      *
     c     c2tagc        tag
     c                   read      anuml2
      *
     c                   if        %eof
     c                   goto      c2tage
     c                   endif
      *
     c                   if        anfirm <> c1firm
     c                   goto      c2tage
     c                   endif
      *
     c                   if        anfell <> c1fell
     c                   goto      c2tage
     c                   endif
      *
     c                   if        antype =  c1type
     c                   if        anfast =  c1fast
     c                   goto      c2tagc
     c                   endif
     c                   endif
      *
     c                   if        c2nfom >= annfom
     c                   if        c2nfom <= anntom
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2ntom >= annfom
     c                   if        c2ntom <= anntom
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   goto      c2tagc
      *
     c     c2tage        tag
      *
      *  Sjekk, er siste brukte null legges laveste inn
      *
     c                   if        c2sist = *zero
     c                   eval      c2sist = c2nfom
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   anumlur
      *
     c                   eval      anumlu_firm = c1firm
     c                   eval      anumlu_fell = c1fell
     c                   eval      anumlu_type = c1type
     c                   eval      anumlu_fast = c1fast
     c     anumlu_key    chain     anumlur
      *
     c                   eval      antxt1 = c2txt1
     c                   eval      antxt2 = c2txt2
     c                   eval      anwrap = c2wrap
     c                   eval      anÅrnu = c2Årnu
     c                   eval      anauto = c2auto
     c                   eval      annfom = c2nfom
     c                   eval      anntom = c2ntom
     c                   eval      ansist = c2sist
     c                   eval      anleng = c2leng
      *
     c*>                 time                    ahedat               ¤¤¤¤¤¤
     c*>                 time                    ahetim               ¤¤¤¤¤¤
     c*>                 eval      aheusr = l_user                    ¤¤¤¤¤¤
      *
     c                   if        %found
     c                   update    anumlur
     c                   else
     c                   eval      anfirm = c1firm
     c                   eval      anfell = c1fell
     c                   eval      antype = c1type
     c                   eval      anfast = c1fast
     c*>                 time                    ahodat               ¤¤¤¤¤¤
     c                   write     anumlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1txt1 = antxt1
     c                   eval      b1sist = ansist
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      anumlr_firm = d1firm
     c                   eval      anumlr_fell = d1fell
     c                   eval      anumlr_type = d1type
     c                   eval      anumlr_fast = d1fast
     c     anumlr_key    chain     anumlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      anumlu_firm = d1firm
     c                   eval      anumlu_fell = d1fell
     c                   eval      anumlu_type = d1type
     c                   eval      anumlu_fast = d1fast
     c     anumlu_key    chain     anumlur
      *
     c                   delete    anumlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_firm = k1firm
     c                   eval      s_fell = k1fell
     c                   eval      s_type = k1type
     c                   eval      s_fast = k1fast
      *
      * Fyller kopierings-bildet
      *
     c                   eval      anumlr_firm = k1firm
     c                   eval      anumlr_fell = k1fell
     c                   eval      anumlr_type = k1type
     c                   eval      anumlr_fast = k1fast
     c     anumlr_key    chain     anumlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1fell = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      anumlr_firm = k1firm
     c                   eval      anumlr_fell = k1fell
     c                   eval      anumlr_type = k1type
     c                   eval      anumlr_fast = k1fast
     c     anumlr_key    chain     anumlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1firm = s_firm
     c                   eval      c1fell = s_fell
     c                   eval      c1type = s_type
     c                   eval      c1fast = s_fast
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      anuml1
     c                   other
     c                   read      anuml1
     c                   endsl
      *
     c*>                 if        %eof or                            ¤¤¤¤¤¤
     c*>                           anfirm <> w_firm                   ¤¤¤¤¤¤
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      anuml1_firm = anfirm
     c                   eval      anuml1_fell = anfell
     c                   eval      anuml1_type = antype
     c                   eval      anuml1_fast = anfast
     c                   other
     c                   eval      anuml1_firm = anfirm
     c                   eval      anuml1_fell = anfell
     c                   eval      anuml1_type = antype
     c                   eval      anuml1_fast = anfast
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1firm = anfirm
     c                   eval      b1fell = anfell
     c                   eval      b1type = antype
     c                   eval      b1fast = anfast
     c                   eval      b1txt1 = antxt1
     c                   eval      b1sist = ansist
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     anuml1_key    setgt     anuml1
     c                   readp     anuml1
     c                   other
     c     anuml1_key    setgt     anuml1
     c                   readp     anuml1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     anuml1
     c                   other
     c                   readp     anuml1
     c                   endsl
      *
     c*>                 if        %eof or                            ¤¤¤¤¤¤
     c*>                           anfirm <> w_firm                   ¤¤¤¤¤¤
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      anuml1_firm = anfirm
     c                   eval      anuml1_fell = anfell
     c                   eval      anuml1_type = antype
     c                   eval      anuml1_fast = anfast
     c                   other
     c                   eval      anuml1_firm = anfirm
     c                   eval      anuml1_fell = anfell
     c                   eval      anuml1_type = antype
     c                   eval      anuml1_fast = anfast
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     anuml1_key    setll     anuml1
     c                   other
     c     anuml1_key    setll     anuml1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på identifikasjon
      *
     c     anuml1_key    klist
     c                   kfld                    anuml1_firm
     c                   kfld                    anuml1_fell
     c                   kfld                    anuml1_type
     c                   kfld                    anuml1_fast
      *
      * Key for felles test
      *
     c     anuml2_key    klist
     c                   kfld                    anuml2_firm
     c                   kfld                    anuml2_fell
     c                   kfld                    anuml2_nfom
      *
      * Key for les på identifikasjon
      *
     c     anumlr_key    klist
     c                   kfld                    anumlr_firm
     c                   kfld                    anumlr_fell
     c                   kfld                    anumlr_type
     c                   kfld                    anumlr_fast
6.nu  *
6.nu  * Key for les på status ofak
6.nu  *
6.nu c     fstsl1_key    klist
6.nu c                   kfld                    w_firm
6.nu  *
6.nu  * Key for les på status lager
6.nu  *
6.nu c     lstsl1_key    klist
6.nu c                   kfld                    w_firm
6.nu  *
6.nu  * Key for les på status butikk
6.nu  *
6.nu c     bstsl1_key    klist
6.nu c                   kfld                    w_firm
      *
      * Key for oppdatering av identifikasjon
      *
     c     anumlu_key    klist
     c                   kfld                    anumlu_firm
     c                   kfld                    anumlu_fell
     c                   kfld                    anumlu_type
     c                   kfld                    anumlu_fast
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c*>                 eval      w_firm = l_firm                    ¤¤¤¤¤¤
      *
     c                   eval      *in22 = *on
6.nu  *
6.nu  * Les alle statusregister
6.nu  *
6.nu c     fstsl1_key    chain     fstsl1
6.nu c     lstsl1_key    chain     lstsl1
6.nu c     bstsl1_key    chain     bstsl1
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      anuml1_firm = 0
     c                   eval      anuml1_fell = *blank
     c                   eval      anuml1_type = *blank
     c                   eval      anuml1_fast = *blank
     c     anuml1_key    setll     anuml1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
