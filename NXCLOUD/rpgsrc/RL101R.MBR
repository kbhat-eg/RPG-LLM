      /TITLE  Vedlikehold leverandørregister
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON
      * Program . . . . : RL101R
      * Beskrivelse . . : Vedlikehold leverandørregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                            Sign
      * --------- ------  ---------------------------------------------- ----
T5.00 * V5.00     131202  Flere param. i call RS718R.                    HFL
T5.00 * V5.00     050203  Timestampfeil.                                 HFL
T5.10 * V5.20     241003  Fjernet bruk av spesialtabell for alfafelt     KOB
T5.10 * V5.20     201103  Lagt inn fakturaleverandør på F6-bildet        KOB
T5.10 * V5.30     060404  Lagt inn vårt kundenummer hos leverandør       KOB
T5.30 * V5.30     080704  Kontroll på gironr. (Mod. 11).                 HFL
T5.30 * V5.30     250804  Saksbehandler er nummerisk (=selger).          HFL
T5.30 * V5.30     130904  Nytt felt RLEANB Bistillingsmottaker.          HFL
T5.33 * V5.30     040205  Lagt inn merke når notatblokk på leverandøren  HFL
T5.33 * V5.40     250505  Lagt inn vedlikehold av besøksadresse          KOB
T5.31 * V5.40     020605  Lagt inn overgang til kontaktperson            KOB
T5.40 * V5.40     170605  Leser RKLAL2 i stedet for RKLALU.              HFL
T5.40 * V5.40     030605  Lagt inn kontroll mot foretaknummer            KOB
T5.40 * V5.40     030605  Lagt inn kontroll mot eannummer                KOB
T5.40 * V5.40     030605  Lagt inn kontroll mot bankgironr.              KOB
T5.40 * V5.40     030605  Lagt inn kontroll mot telefonnr.               KOB
T5.40 * V5.40     030605  Lagt inn kontroll mot e-mailadresse            KOB
T5.40 * V5.40     030605  Lagt inn kontroll mot alfaindeks og postnr.    KOB
T5.40 * V5.40     030805  Utvidet parameterliste for RF020R              HFL
 5.42 * V5.42     260805  Editering av lever.  tlfnr. (nn nn nn nn)      KOB
 5.42 * V5.42     260805  Editering av lever.  tlfx.. (nn nn nn nn)      KOB
 5.42 * V5.42     260805  Editering av lever.  mobil. (nn nn nn nn)      KOB
T5.40 * V5.40     230806  Distrikt tekst mistet 2 bokstaver.             HFL
T5.40 * V5.43     221006  Lagt inn standardverdi på etikettkoder (2)     KOB
T5.40 * V5.50     310107  Lagt ut dato og bruker for opprettelse i F2    KOB
T5.50 * V5.50     300507  Testet på endret dato og la ut opprettet dato. HFL
T5.50 * V5.60     220208  Lagt inn mod11 kontroll på foretaksnr.         KOB
T5.50 *                   bankkonto og personnummer                      KOB
T5.50 * V5.60     220208  Lagt inn muligheter for "dummy" informasjon    KOB
T5.50 *                   for felter som kreves utfylt, men hvor info    KOB
T5.50 *                   mangler. 9999... for nummeriske felt.          KOB
      *                   "." godtas for alfafelt. Gir ikke melding om   KOB
      *                   duplikat ved kontroll.                         KOB
T5.50 * V5.60     190908  Fjernet slettekode fra F2-bildet, ikke i bruk  KOB
6.10  * V6.10     230909  Lagt inn sporingsfelter                        BSA
6.11  * V6.10     070510  Endring i  sporingsfelter                      BSA
6.20  * V6.20     240412  Avdelingstekst er forkortet i skjermbildet     BSA
6.21  * V6.20     190313  justert på endringdato logikk                  bof
6.22  * R6.20     110414  Notatblokk ble ikke vist                       nho
6.30  *  6.30     290314  Ny kode for forsendelsesmåte bestilling        bof
6.31  *  6.30     290814  hvis sperr/pass, så kontr. best/bestf. mm      bof
6.32  *  6.30     131216  Nye tester ved. ny avgifstkode                 bsa
6.33  *  6.30     030317  Test på kontr.sif. på org.nr skal gjøres
      *                   dersom noe tastes i feltet
7.00  *  7.00     010220  Laget inn vedlikehold av NOBB deltakernr       hko
7.01  *  7.00     250220  Tar i bruk feltet RLISOK til "fast             bsa
7.01  *                   forretningssted".                              bsa
7.02  *  7.00     070121  Lagt inn kall til program for låst valutakurs  ask
8.01  *           020523  Sjekk om nobb.lev ligger inne fra før          bof
      *
      *
      *  BRUK AV INDIKATORER
      *
      *       KC = F3  = Exit
      *       KE = F5  = Forny
      *       KL = F12 = Annuller
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevlu    uf a e           k disk    rename(rlevpfr:rlevlur)
     frlbapf    if   e           k disk    rename(rlbapfr:rlbapfr)
     frlbalu    uf a e           k disk    rename(rlbapfr:rlbalur)
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
T5.20f                                     prefix(x:1)
     frlevl4    if   e           k disk    rename(rlevpfr:rlevl4r)
T5.20f                                     prefix(x:1)
     frlevl5    if   e           k disk    rename(rlevpfr:rlevl5r)
T5.20f                                     prefix(x:1)
     frlevl6    if   e           k disk    rename(rlevpfr:rlevl6r)
T5.20f                                     prefix(x:1)
     frlevl7    if   e           k disk    rename(rlevpfr:rlevl7r)
T5.20f                                     prefix(x:1)
     frlevl8    if   e           k disk    rename(rlevpfr:rlevl8r)
T5.20f                                     prefix(x:1)
     fra14l1    if   e           k disk    rename(ra14pfr:ra14l1r)
E5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
6.31 flohelk    if   e           k disk    rename(lohepfr:lohelkr)
6.31 flfhel5    if   e           k disk    rename(lfhepfr:lfhel5r)
6.31 fmppalr    if   e           k disk    rename(mppapfr:mppalrr)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
T5.50frukpl7    if   e           k disk    rename(rukppfr:rukpl7r)
     frl101d    cf   e             workstn
      *
     d mld             s             11    dim(4) ctdata perrcd(1)
     d wpf             s              1    dim(30)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d rlevlu_levr     s                   like(rllevr)
     d rlevl2_alfa     s                   like(rlalfa)
     d rlevl4_ftnr     s                   like(rlftnr)
     d rlevl5_eanl     s                   like(rleanl)
     d rlevl6_bgir     s                   like(rlbgir)
     d rlevl7_tlfn     s                   like(rltlfn)
     d rlevl8_emal     s                   like(rlemal)
     d rlbapf_levr     s                   like(rllevr)
     d rlbalu_levr     s                   like(rllevr)
     d apospf_ponr     s                   like(apponr)
E5.40d rklal2_syst     s                   like(rxsyst)
E5.40d rklal2_pros     s                   like(rxpros)
E5.40d rklal2_akti     s                   like(rxakti)
E5.40d rklal2_levr     s                   like(rxkont)
T5.33d ra14l1_lkat     s                   like(rlkatg)
T5.50d rukpl7_levr     s                   like(rulevr)
6.31 d lohelk_ldor     s                   like(loldor)
6.31 d lfhel5_ldor     s                   like(lhldor)
6.31 d mppalr_ldor     s                   like(mpldor)
      *
      * Parameter - program RF020R
      *                                                                -----
     i/COPY QCPYLESRC,RF020PAR
6.20 d*                ds
6.20 d*rxulda                       175
6.20 d* rxstat                        1    overlay(rxulda)
6.20 d* rxfirm                        3  0 overlay(rxulda:2)
6.20 d* rxsyst                        1    overlay(rxulda:5)
6.20 d* rxpros                        6  0 overlay(rxulda:6)
6.20 d* rxakti                        3    overlay(rxulda:12)
6.20 d* rxf001                        3    overlay(rxulda:15)
6.20 d* rxkont                        6  0 overlay(rxulda:18)
6.20 d* rxspes                        4  0 overlay(rxulda:24)
6.20 d* rxavde                        4  0 overlay(rxulda:28)
6.20 d* rxlinj                        4  0 overlay(rxulda:32)
6.20 d* rxtgrp                        3    overlay(rxulda:36)
6.20 d* rxtext                       78    overlay(rxulda:39)
6.20 d* rxf002                       13    overlay(rxulda:117)
6.20 d* rxvalg                        1  0 overlay(rxulda:130)
6.20 d* rxtist                         z   overlay(rxulda:131)
6.20 d* rxbiln                        6  0 overlay(rxulda:158)
6.20 d* rxrper                        2  0 overlay(rxulda:164)
6.20 d* rxraar                        4  0 overlay(rxulda:166)
6.20 d* rxonr                         6  0 overlay(rxulda:170)
     d rxupar          s                   like(rxulda)
      *
      * Parametre
      *
     d p_stat          s              1
     d p_bkod          s                   like(rlbetm)
     d p_ckod          s                   like(rlbetb)
     d p_fkod          s                   like(rlrabk)
     d p_gkod          s                   like(rlavde)
     d p_hkod          s                   like(rldist)
     d p_jkod          s                   like(rllagr)
     d p_lkod          s                   like(rlland)
     d p_nkod          s                   like(rlkatg)
     d p_pkod          s                   like(rlvalk)
     d p_qkod          s                   like(rlform)
     d p_rkod          s                   like(rllbet)
     d p_zkod          s                   like(rlsaks)
     d p_tx20          s             20
     d p_tx25          s             25
T5.30 * Felt for kontroll av kid
T5.30d kid             s              1    dim(25)
T5.30d nyki            s              1    dim(25)
T5.30d w_feltx         s             25
T5.30d tall            s              1    dim(24)
T5.30d ktr             s              1
T5.30d w_kidi          s             25
T5.30d w_ki24          s             24
T5.30d w_sist          s              1
T5.30d v               s              2  0
T5.30d w               s              2  0
T5.30d tell            s              1  0
T5.39d psum            s              3  0
T5.30d vekt            s              1  0
T5.30d faktor          s              1  0
T5.30d prod            s              2  0
T5.30d difktr          s              2  0
T5.30d divtal          s              2  0
T5.30d rest            s              2  0
      *
T5.00d p_tx2           s             25
T5.00d p_tx3           s             25
     d p_tx30          s             30
      *
     d wactli          s              2  0
     d wactpo          s              3  0
     d whalfa          s                   like(rlalfa)
     d wpakti          s              3
     d wpantp          s              2  0
     d wpavde          s              4  0
     d wpfunk          s              1  0
     d wpponr          s                   like(rlponr)
6.22 d*wppros          s              6  0
6.22 d wppros          s              6
     d wpretk          s              1
E5.33d wpspes          s              4  0
     d wpsyst          s              1
     d wseqen          s              1
     d w_alfa          s                   like(rlalfa)
     d w_edat          s                   like(rledat)
     d w_firm          s                   like(rlfirm)
     d w_ponr          s                   like(rlponr)
     d w_rest          s             11  2
     d w_1bel          s                   like(ra1bel)
     d w_1bkl          s                   like(ra1bkl)
     d w_3mfl          s                   like(ra3mfl)
     d w_ftst          s              1  0
     d w_feil          s              1  0
     d w_kund          s              6  0
5.42 d w_tlfn          s             15
     d w_alfa01        s              1
     d w_alfa10        s             10
     d w_alfa29        s             29
     d w_alfa49        s             49
6.32 d w_mld1          s             50
8.01 d w_anta          s              6  0
      *
6.32 d s_mkod          s                   like(rlmkod)
      *
     d p_valg          s              1  0
     d p_levr          s                   like(rllevr)
6.NU d p_biln          s              6  0
     d p_rest          s             11  2
     d p_retk          s              1
     d p_4lnr          s              6  0

      *
6.31 d b_best          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
      *
      /eject
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * C2BLD  - C2 BILDE for endre/vise/opprette
      * E2WIN  - E2 Vindu for vedlikehold av besøksadresse
      * F2WIN  - F2 Vindu for vedlikehold av OFAK-koder
      * G2WIN  - G2 Vindu for vedlikehold av Fakturakunde/EAN-kunde
      * ASUME  -    Record for å styre at vindu legges over
      *             eksisterende data.
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * HOVEDRUTINE                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2BLD Behandle bilde for ny - endring - vise                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Hent leverandør
      *
     c                   move      p_levr        rlevl1_levr
     c     rlevl1_key    chain     rlevl1                             60
      *
     c                   if        *in60 = *off
     c                   movel     mld(2)        c2nyen
     c                   move      rllevr        c2levr
      *
     c                   move      rlalfa        c2alfa
     c                   move      rlnavn        c2navn
     c                   move      rlgate        c2gate
     c                   move      rladr2        c2adr2
     c                   move      rlsted        c2sted
     c                   move      rlponr        c2ponr
     c                   move      rlpass        c2pass
     c                   move      rlbetb        c2betb
     c                   move      rlsaks        c2saks
     c                   move      rlbetm        c2betm
     c                   move      rlkatg        c2katg
     c                   move      rlland        c2land
     c                   move      rlsprk        c2sprk
     c                   move      rltlfn        c2tlfn
     c                   move      rlmobn        c2mobn
     c                   move      rltlfx        c2tlfx
     c                   move      rlbgir        c2bgir
     c                   move      rlkunr        c2kunr
     c                   move      rlftnr        c2ftnr
     c                   move      rlmkod        c2mkod
     c                   move      rlvalk        c2valk
     c                   move      rlavde        c2avde
     c                   move      rlrabk        c2rabk
     c                   move      rlorab        c2orab
     c                   move      rldist        c2dist
     c                   move      rllagr        c2lagr
     c                   move      rlform        c2form
     c                   move      rllbet        c2lbet
     c                   move      rlfrie        c2frie
7.00 c                   move      rlnobb        c2nobb
     c                   move      rlemal        c2emal
      *
     c                   if        rledat = *loval
     c                   move      *zeros        c2edat
     c                   else
     c     *dmy          move      rledat        c2edat
     c                   endif
      *
     c                   move      rlesig        c2esig
      *
     c                   move      rllevr        e2levr
     c                   move      rlnavn        e2navn
     c                   move      rlbgat        e2bgat
     c                   move      rlbadr        e2badr
     c                   move      rlbpnr        e2bpnr
     c                   move      rlweba        e2weba
      *
     c                   move      rlrest        f2rest
     c                   move      rlpakk        f2pakk
     c                   move      rlbonu        f2bonu
     c                   move      rlavts        f2avts
     c                   move      rlpurr        f2purr
     c                   move      rlspes        f2spes
     c                   move      rlprio        f2prio
     c                   move      rlorsk        f2orsk
     c                   move      rlstko        f2stko
     c                   move      rlkpri        f2kpri
     c                   move      rlppro        f2ppro
     c                   move      rlremk        f2remk
     c                   move      rlhyle        f2hyle
     c                   move      rllvko        f2lvko
     c                   move      rlflev        f2flev
7.01 c                   move      rlisok        f2isok
      *
E5.50c                   if        rlndat = *loval
     c                   move      *zeros        f2ndat
     c                   else
     c     *dmy          move      rlndat        f2ndat
     c                   endif
      *
     c                   move      rlnsig        f2nsig
      *
     c                   move      rlfact        g2fact
     c                   move      rlfacn        g2facn
     c                   move      rledik        g2edik
     c                   move      rleanl        g2eanl
T5.30c                   move      rleanb        g2eanb
6.32 c                   eval      s_mkod = rlmkod
      *
      * Hent tekster fra koderegister
      *
     c                   eval      w_ftst = 0
     c                   eval      w_feil = 0
     c                   exsr      xnulle
     c                   exsr      xsr020
      *
     c                   else
      *
     c                   movel     mld(1)        c2nyen
     c                   move      p_levr        c2levr
     c                   eval      c2alfa = *blank
     c                   eval      c2navn = *blank
     c                   eval      c2gate = *blank
     c                   eval      c2adr2 = *blank
     c                   eval      c2sted = *blank
     c                   eval      c2ponr = *zero
     c                   eval      c2pass = *blank
     c                   move      w_1bel        c2betb
E5.30c                   eval      c2saks = *zero
     c                   eval      c2betm = *zero
     c                   eval      c2katg = *zero
     c                   eval      c2land = *blank
     c                   eval      c2sprk = *blank
     c                   eval      c2tlfn = *blank
     c                   eval      c2mobn = *blank
     c                   eval      c2tlfx = *blank
     c                   eval      c2bgir = *zero
     c                   eval      c2kunr = *blank
     c                   eval      c2ftnr = *zero
     c                   eval      c2mkod = *zero
     c                   eval      c2valk = *blank
     c                   eval      c2avde = *zero
     c                   eval      c2rabk = *zero
     c                   eval      c2orab = *zero
     c                   eval      c2dist = *zero
     c                   eval      c2lagr = *zero
     c                   eval      c2form = *zero
     c                   eval      c2lbet = *zero
     c                   eval      c2frie = *blank
7.00 c                   eval      c2nobb = 0
     c                   eval      c2emal = *blank
     c                   move      udate         c2edat
     c                   eval      c2esig = l_user
      *                                                                 .
     c                   eval      e2bgat = *blank
     c                   eval      e2badr = *blank
     c                   eval      e2bpnr = *zero
     c                   eval      e2weba = *blank
      *
     c                   eval      f2rest = *zero
     c                   eval      f2pakk = *zero
     c                   eval      f2bonu = *zero
     c                   eval      f2avts = *blank
     c                   eval      f2purr = *zero
     c                   eval      f2spes = *zero
     c                   eval      f2prio = *zero
     c                   eval      f2orsk = *zero
     c                   eval      f2stko = *blank
     c                   eval      f2kpri = *zero
     c                   eval      f2ppro = *zero
     c                   eval      f2remk = *blank
     c                   eval      f2hyle = 2
     c                   eval      f2lvko = 2
     c                   eval      f2flev = *zero
     c                   eval      f2navn = *blank
7.01 c                   eval      f2isok = '0'
      *
     c                   eval      g2fact = *zero
     c                   eval      g2facn = *zero
     c                   eval      g2edik = *blank
     c                   eval      g2eanl = *zero
     c                   eval      g2navn = *blank
      *
     c                   endif
      *
     c                   if        *in30 = *on
     c                   movel     mld(3)        c2nyen
     c                   endif
      *
     c     c2tagb        tag
      *
      * Posisjonere cursor
      *
     c                   if        *in32 = *off and
     c                             *in33 = *off and
     c                             *in34 = *off and
     c                             *in35 = *off and
     c                             *in36 = *off and
     c                             *in37 = *off and
     c                             *in38 = *off and
     c                             *in39 = *off and
     c                             *in40 = *off and
     c                             *in41 = *off and
     c                             *in42 = *off and
     c                             *in43 = *off and
     c                             *in44 = *off and
     c                             *in45 = *off
     c                   move      *on           *in31
     c                   endif
      *
T5.33 * Finnes notatblokk på leverandøren?
      *
E5.40c                   eval      rklal2_syst = 'L'
6.22 c**                 eval      rklal2_pros = *zeros
6.22 c                   eval      rklal2_pros = *blanks
E5.40c                   eval      rklal2_akti = *blanks
E5.40c                   eval      rklal2_levr = c2levr
E5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.33c                   if        *in94 = *on
T5.33c                   eval      C2notb = 'NOTATARK'
T5.33c                   endif
      *
     c                   eval      w_feil = 0
     c                   exfmt     c2bld
     c                   exsr      xnulle
      *
      * Oppkall av notatark.
      *
     c                   if        *inkb = *on
     c                   move      w_firm        rxfirm
     c                   move      wpsyst        rxsyst
E5.40c                   move      wppros        rxpros
     c                   move      wpakti        rxakti
     c                   move      p_levr        rxkont
     c                   move      wpspes        rxspes
     c                   move      wpavde        rxavde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   move      p_valg        rxvalg
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
     c                   move      rxulda        rxupar
     c                   call      'RF020R'
     c                   parm                    rxupar
     c                   move      rxupar        rxulda
     c                   goto      c2tagb
     c                   endif
      *
      * Avslutt
      *
     c                   if        *inkc or *inkl
      *
      * Reduser sist brukte leverandørnummer
      *
     c                   if        *in60
     c                   eval      p_4lnr = p_levr
     c                   eval      p_retk = 'S'
     c                   call      'RS002R'
     c                   parm                    p_retk
     c                   parm                    w_firm
     c                   parm                    p_4lnr
      *
     c                   endif
      *
     c                   goto      c2tag1
     c                   endif
      *
      * Call program for posteringer
      *
     c                   if        *inke = *on
     c                   exsr      xe2bld
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for OFAK-koder
      *
     c                   if        *inkf = *on
     c                   exsr      xf2win
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for EAN-koder
      *
     c                   if        *inkg = *on
     c                   exsr      xg2win
     c                   goto      c2tagb
     c                   endif
7.02  *
7.02  * Call program for låst valutakurs
7.02  *
7.02 c                   if        *inki = *on
7.02 c                   if        rlvalk <> '   'and
7.02 c                             rlvalk <> 'NOK'
7.02 c                   call      'RL120R'
7.02 c                   parm                    p_levr
7.02 c                   endif
7.02 c                   goto      c2tagb
7.02 c                   endif
      *
      * Call program for utenlandsk bank
      *
     c                   if        *inkh = *on
     c                   exsr      xh2win
     c                   goto      c2tagb
     c                   endif
5.31  *
5.31  * Call program for kontaktperson
5.31  *
5.31 c                   if        *inkj = *on
      *
     c                   eval      w_kund = 0
      *
5.31 c                   call      'RF210R'
5.31 c                   parm                    w_firm
5.31 c                   parm                    w_kund
5.31 c                   parm                    c2levr
      *
5.31 c                   goto      c2tagb
5.31 c                   endif
      *
      * Call program for Besøksadresse
      *
     c                   if        *inkk = *on
     c                   exsr      xe2win
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for poststed
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2PONR'
     c                   call      'AF010R'
     c                   parm                    wpponr
      *
     c                   if        wpponr <> *zero
     c                   move      wpponr        c2ponr
     c                   endif
      *
     c                   move      *on           *in32
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for betalingsmåte
      *
     c                   if        w_afld = 'C2BETM'
     c                   call      'RA502R'
     c                   parm                    w_firm
     c                   parm                    p_bkod
     c                   parm                    p_tx20
      *
     c                   if        p_bkod <> *zero
     c                   move      p_bkod        c2betm
     c                   move      p_tx20        c2btxt
     c                   endif
      *
     c                   move      *on           *in33
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for betalingsbetingelse
      *
     c                   if        w_afld = 'C2BETB'
     c                   call      'RA503R'
     c                   parm                    w_firm
     c                   parm                    p_ckod
     c                   parm                    p_tx20
      *
     c                   if        p_ckod <> *zero
     c                   move      p_ckod        c2betb
     c                   move      p_tx20        c2ctxt
     c                   endif
      *
     c                   move      *on           *in34
     c                   goto      c2tagb
     c                   endif
      *
E5.30 * Call program for saksbehandler (selger)
      *
     c                   if        w_afld = 'C2SAKS'
E5.30c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    p_zkod
     c                   parm                    p_tx20
      *
     c                   if        p_zkod <> *zero
     c                   move      p_zkod        c2saks
     c                   movel     p_tx20        c2ztxt
     c                   endif
      *
     c                   move      *on           *in46
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for rabattkategori
      *
     c                   if        w_afld = 'C2RABK'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    p_fkod
     c                   parm                    p_tx20
      *
     c                   if        p_fkod <> *zero
     c                   move      p_fkod        c2rabk
     c                   move      p_tx20        c2ftxt
     c                   endif
      *
     c                   move      *on           *in35
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for avdeling
      *
     c                   if        w_afld = 'C2AVDE'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    P_tx20
      *
     c                   if        p_gkod <> *zero
     c                   move      p_gkod        c2avde
6.20 c                   movel     p_tx20        c2gtxt
     c                   endif
      *
     c                   move      *on           *in36
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for distrikt
      *
     c                   if        w_afld = 'C2DIST'
     c                   call      'RA508R'
     c                   parm                    w_firm
     c                   parm                    p_Hkod
     c                   parm                    p_tx20
      *
     c                   if        p_hkod <> *zero
     c                   move      p_hkod        c2dist
E5.40c                   movel     p_tx20        c2htxt
     c                   endif
      *
     c                   move      *on           *in37
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for lager
      *
     c                   if        w_afld = 'C2LAGR'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    p_jkod
     c                   parm                    p_tx20
      *
     c                   if        p_jkod <> *zero
     c                   move      p_jkod        c2lagr
     c                   movel     p_tx20        c2jtxt
     c                   endif
      *
     c                   move      *on           *in39
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for leverandørkategori
      *
     c                   if        w_afld = 'C2KATG'
     c                   call      'RA514R'
     c                   parm                    w_firm
     c                   parm                    p_nkod
     c                   parm                    p_tx20
      *
     c                   if        p_nkod <> *zero
     c                   move      p_nkod        c2katg
     c                   movel     p_tx20        c2ntxt
     c                   endif
      *
     c                   move      *on           *in40
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for land
      *
     c                   if        w_afld = 'C2LAND'
     c                   call      'RA512R'
     c                   parm                    w_firm
     c                   parm                    p_lkod
     c                   parm                    p_tx20
      *
     c                   if        p_lkod <> *blank
     c                   move      p_lkod        c2land
     c                   move      p_tx20        c2ltxt
     c                   endif
      *
     c                   move      *on           *in41
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for valuta
      *
     c                   if        w_afld = 'C2VALK'
     c                   call      'RA516R'
     c                   parm                    w_firm
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *
     c                   if        p_pkod <> *zero
     c                   move      p_pkod        c2valk
     c                   move      p_tx20        c2pmna
     c                   endif
      *
     c                   move      *on           *in42
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for forsendelsesmåte
      *
     c                   if        w_afld = 'C2FORM'
6.30 c                   call      'RA533R'
     c                   parm                    w_firm
     c                   parm                    p_qkod
     c                   parm                    p_tx20
      *
     c                   if        p_qkod <> *zero
     c                   move      p_qkod        c2form
     c                   move      p_tx20        c2qtxt
     c                   endif
      *
     c                   move      *on           *in43
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for leveringsbetingelse
      *
     c                   if        w_afld = 'C2LBET'
     c                   call      'RA518R'
     c                   parm                    w_firm
     c                   parm                    p_rkod
     c                   parm                    p_tx25
      *
     c                   if        p_rkod <> *zero
     c                   move      p_rkod        c2lbet
     c                   movel     p_tx25        c2rtx1
     c                   endif
      *
     c                   move      *on           *in44
     c                   goto      c2tagb
     c                   endif
      *
      * F4 trykket på ugyldig felt
      *
     c                   move      *on           *in46
     c                   goto      c2tagb
     c                   endif
      *
      *
      * Kontroll av skjermbildet
      *
     c                   eval      w_ftst = 1
     c                   eval      w_feil = 0
     c                   exsr      xnulle
     c                   exsr      xsr020
      *
     c                   if        w_feil <> 0
     c                   goto      c2tagb
     c                   endif
      *
      * Konvertere alfafelt til store bokstaver
      *
     c                   if        c2alfa = *blank
     c                   eval      c2alfa = c2navn
     c                   endif
      *
     c     lo:up         xlate     c2alfa        c2alfa
      *
      * Konvertere navn/adresse til store bokstaver, dersom statuskode=0
      *
     c                   if        w_1bkl = 0
     c     lo:up         xlate     c2navn        c2navn
     c     lo:up         xlate     c2gate        c2gate
     c     lo:up         xlate     c2adr2        c2adr2
     c     lo:up         xlate     c2sted        c2sted
     c                   endif
      *
     c                   eval      rlevlu_levr = p_levr
     c     rlevlu_key    chain     rlevlur                            60
      *
     c                   move      c2alfa        rlalfa
     c                   move      c2navn        rlnavn
     c                   move      c2gate        rlgate
     c                   move      c2adr2        rladr2
     c                   move      c2sted        rlsted
     c                   move      c2ponr        rlponr
     c                   move      c2pass        rlpass
     c                   move      c2betb        rlbetb
     c                   move      c2saks        rlsaks
     c                   move      c2betm        rlbetm
     c                   move      c2katg        rlkatg
     c                   move      c2land        rlland
     c                   move      c2sprk        rlsprk
5.42  *
5.42  * Editer telefon/mobil/faks - men ikke hvis "." - dummy
5.42  *
5.42 c                   movel     c2tlfn        w_tlfn
     c                   if        c2tlfn <> '.'
5.42 c                   call      'RÅ950R'
5.42 c                   parm                    w_tlfn
     c                   endif
5.42 c                   movel     w_tlfn        rltlfn
5.42  *
5.42 c                   movel     c2mobn        w_tlfn
     c                   if        c2mobn <> '.'
5.42 c                   call      'RÅ950R'
5.42 c                   parm                    w_tlfn
     c                   endif
5.42 c                   movel     w_tlfn        rlmobn
5.42  *
5.42 c                   movel     c2tlfx        w_tlfn
     c                   if        c2tlfx <> '.'
5.42 c                   call      'RÅ950R'
5.42 c                   parm                    w_tlfn
     c                   endif
5.42 c                   movel     w_tlfn        rltlfx
5.42  *
     c                   move      c2bgir        rlbgir
     c                   move      c2kunr        rlkunr
     c                   move      c2ftnr        rlftnr
     c                   move      c2mkod        rlmkod
     c                   move      c2valk        rlvalk
     c                   move      c2avde        rlavde
     c                   move      c2rabk        rlrabk
     c                   move      c2orab        rlorab
     c                   move      c2dist        rldist
     c                   move      c2lagr        rllagr
     c                   move      c2form        rlform
     c                   move      c2lbet        rllbet
     c                   move      c2frie        rlfrie
7.00 c                   move      c2nobb        rlnobb
     c                   move      c2emal        rlemal
      *
     c                   eval      rlbgat = e2bgat
     c                   eval      rlbadr = e2badr
     c                   eval      rlbpnr = e2bpnr
     c                   eval      rlweba = e2weba
      *
     c                   move      f2rest        rlrest
     c                   move      f2pakk        rlpakk
     c                   move      f2bonu        rlbonu
     c                   move      f2avts        rlavts
     c                   move      f2purr        rlpurr
     c                   move      f2spes        rlspes
     c                   move      f2prio        rlprio
     c                   move      f2orsk        rlorsk
     c                   move      f2stko        rlstko
     c                   move      f2kpri        rlkpri
     c                   move      f2ppro        rlppro
     c                   move      f2remk        rlremk
     c                   move      f2hyle        rlhyle
     c                   move      f2lvko        rllvko
     c                   move      f2flev        rlflev
7.01 c                   move      f2isok        rlisok
      *
     c                   move      g2fact        rlfact
     c                   move      g2facn        rlfacn
     c                   move      g2edik        rledik
     c                   move      g2eanl        rleanl
T5.30c                   move      g2eanb        rleanb
      *
      *
      * Oppdater leverandørregister
      *
     c                   if        *in60 = *off
      *
     c                   if        *in30 = *off
     c                   time                    rledat
     c                   time                    rletim
     c                   move      l_user        rlesig
6.11 c                   time                    rl2dpt
6.11 c                   eval      rlepgm = 'RL101R    '
     c                   update    rlevlur
     c                   endif
     c                   else
     c                   move      w_firm        rlfirm
     c                   move      c2levr        rllevr
     c                   time                    rlndat
     c                   time                    rlntim
     c                   move      l_user        rlnsig
6.21 c                   time                    rledat
6.21 c                   time                    rletim
6.21 c                   move      l_user        rlesig
6.11 c                   time                    rl2dpt
6.11 c                   eval      rlepgm = 'RL101R    '
     c                   write     rlevlur
     c                   endif
      *
     c                   call      'RL750R'
     c                   parm                    rlfirm
     c                   parm                    rllevr
      *
     c     c2tag1        tag
      *
     c                   if        p_valg = 2
     c                   move      *off          *in30
     c                   endif
      *
     c                   goto      xslutt
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2BLD Vise posteringer                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2bld        begsr
      *
     c                   if        p_valg = 2
     c                   call      'RL112R'
     c                   parm                    p_valg
     c                   parm                    p_levr
      *
     c                   move      *off          *in30
     c                   endif
      *
     c                   if        p_valg = 5
     c                   call      'RL512R'
     c                   parm                    p_valg
     c                   parm                    p_levr
     c                   parm                    p_biln
     c                   parm                    p_rest
      *
     c                   move      *on           *in30
     c                   endif
      *
     c                   move      *on           *in31
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2WIN Behandle bilde for vedlikehold av besøksadresse           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2win        begsr
      *
     c     e2tagb        tag
      *
     c                   exfmt     e2win
      *
     c     *inkc         cabeq     *on           e2tag1
     c     *inkl         cabeq     *on           e2tag1
      *
      * Call program for post-sted
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'E2BPNR'
     c                   call      'AF010R'
     c                   parm                    wpponr
      *                                                                 .
     c                   if        wpponr <> *zero
     c                   move      wpponr        e2bpnr
     c                   endif
      *                                                                 .
     c                   goto      e2tagb
     c                   endif
      *
     c     e2tag1        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * F2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xf2win        begsr
      *
     c                   eval      wactli = 4
     c                   eval      wactpo = 22
      *
7.01 c                   eval      *in50 = *off
      *
     c     f2tagb        tag
      *
     c                   exfmt     f2win
      *
     c                   eval      w_feil = 0
      *
      * Call leverandører
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'F2FLEV'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    f2flev
     c                   parm                    p_tx30
      *
     c                   eval      f2navn = p_tx30
     c                   goto      f2tagb
     c                   endif
      *
     c     *inkc         cabeq     *on           f2tag1
     c     *inkl         cabeq     *on           f2tag1
      *
      * Sjekke om fakturaleverandør er gyldig
      *
     c                   if        f2flev <> *zero
     c                   call      'RS760R'
     c                   parm                    p_stat
     c                   parm                    f2flev
     c                   parm                    p_tx30
      *
     c                   if        p_stat <> ' '
     c                   move      *on           *in33
     c                   eval      w_feil = 1
     c                   move      p_tx30        f2navn
     c                   goto      f2tagb
     c                   endif
     c                   endif
      *
7.01 c                   if        f2isok = ' '
7.01 c                   eval      f2isok = '0'
7.01 c                   endif
7.01  *
7.01 c                   if        f2isok <> '0' and
7.01 c                             f2isok <> '1'
7.01 c                   move      *on           *in50
7.01 c                   eval      w_feil = 1
7.01 c                   goto      f2tagb
7.01 c                   endif
      *
     c                   if        w_feil <> 0
     c                   goto      f2tagb
     c                   endif
      *
     c     f2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * G2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xg2win        begsr
      *
     c                   eval      wactli = 6
     c                   eval      wactpo = 15
      *
     c                   exfmt     g2win
      *
     c     *inkc         cabeq     *on           g2tag1
     c     *inkl         cabeq     *on           g2tag1
      *
      *
      *
     c     g2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * H2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xh2win        begsr
      *
     c                   eval      rlbapf_levr = p_levr
     c     rlbapf_key    chain     rlbapf                             60
      *
     c                   if        *in60 = *off
     c                   move      rvhswa        h2hswa
     c                   move      rvhba1        h2hba1
     c                   move      rvhba2        h2hba2
     c                   move      rvhba3        h2hba3
     c                   move      rvhba4        h2hba4
     c                   move      rvhbko        h2hbko
     c                   move      rvhbku        h2hbku
     c                   move      rvhbki        h2hbki
     c                   move      rvhbvk        h2hbvk
      *
     c                   else
      *
     c                   eval      h2hswa = *blank
     c                   eval      h2hba1 = *blank
     c                   eval      h2hba2 = *blank
     c                   eval      h2hba3 = *blank
     c                   eval      h2hba4 = *blank
     c                   eval      h2hbko = *blank
     c                   eval      h2hbku = *blank
     c                   eval      h2hbki = *blank
     c                   eval      h2hbvk = *blank
      *
     c                   endif
      *
     c                   eval      wactli = 6
     c                   eval      wactpo = 15
      *
     c     h2tagb        tag
      *
     c                   exfmt     h2win
      *
     c     *inkc         cabeq     *on           h2tag1
     c     *inkl         cabeq     *on           h2tag1
      *
      * Kontroll av skjermbildet
      *
     c                   if        w_feil <> 0
     c                   goto      h2tagb
     c                   endif
      *
     c                   eval      rlbalu_levr = p_levr
     c     rlbalu_key    chain     rlbalur                            60
      *
     c                   move      h2hswa        rvhswa
     c                   move      h2hba1        rvhba1
     c                   move      h2hba2        rvhba2
     c                   move      h2hba3        rvhba3
     c                   move      h2hba4        rvhba4
     c                   move      h2hbko        rvhbko
     c                   move      h2hbku        rvhbku
     c                   move      h2hbki        rvhbki
     c                   move      h2hbvk        rvhbvk
      *
6.10 c                   time                    rvedat
6.10 c                   time                    rvetim
6.10 c                   eval      rveusr = l_user
     c                   if        *in60 = *off
     c                   update    rlbalur
     c                   else
     c                   move      w_firm        rvfirm
     c                   move      c2levr        rvlevr
6.10 c                   time                    rvodat
6.10 c                   time                    rvotim
6.10 c                   eval      rvousr = l_user
     c                   write     rlbalur
     c                   endif
      *
     c     h2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kontroll  C2 bilde (endringer)                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsr020        begsr
      *
      * Sjekke om navn er utfyllt
      *
     c                   if        c2navn = *blank  and
     c                             w_ftst = 1
     c                   move      *on           *in45
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om postnummer er gyldig
      *
     c                   if        c2ponr <> *zero
     c                   move      c2ponr        apospf_ponr
     c     apospf_key    chain     apospfr                            62
      *
     c                   if        *in62 = *on
     c                   move      *on           *in32
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   eval      c2sted = *blank
     c                   movel     apponr        c2sted
     c                   move      apsted        c2sted
     c                   endif
     c                   endif
      *
      * Sjekke om betalingsmåte er gyldig
      *
     c                   if        c2betm <> *zero
     c                   move      c2betm        p_bkod
     c                   call      'RS702R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in33
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2btxt
     c                   endif
     c                   endif
      *
      * Sjekke om betalingsbetingelse er gyldig
      *
     c                   if        c2betb <> *zero
     c                   move      c2betb        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in34
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2ctxt
     c                   endif
     c                   endif
      *
E5.30 * Sjekke om saksbehandler er gyldig (selger)
      *
E5.30c                   if        c2saks <> *zero
     c                   move      c2saks        p_zkod
E5.30c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_zkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in46
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   movel     p_tx20        c2ztxt
     c                   endif
     c                   endif
      *
      * Sjekke om rabattkategori er gyldig
      *
     c                   if        c2rabk <> *zero
     c                   move      c2rabk        p_fkod
     c                   call      'RS706R'
     c                   parm                    p_stat
     c                   parm                    p_fkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in35
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2ftxt
     c                   endif
     c                   endif
      *
      * Sjekke om avdeling er gyldig
      *
     c                   if        c2avde <> *zero
     c                   move      c2avde        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in36
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
6.20 c                   movel     p_tx20        c2gtxt
     c                   endif
     c                   endif
      *
      * Sjekke om distrikt er gyldig
      *
     c                   if        c2dist <> *zero
     c                   move      c2dist        p_hkod
     c                   call      'RS708R'
     c                   parm                    p_stat
     c                   parm                    p_hkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in37
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
E5.40c                   movel     p_tx20        c2htxt
     c                   endif
     c                   endif
      *
      * Sjekke om lager er gyldig
      *
     c                   if        c2lagr <> *zero
     c                   move      c2lagr        p_jkod
     c                   call      'RS710R'
     c                   parm                    p_stat
     c                   parm                    p_jkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in39
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2jtxt
     c                   endif
     c                   endif
      *
      * Sjekke om leverandørkategori er gyldig
      *
     c                   if        c2katg <> *zero
     c                   move      c2katg        p_nkod
     c                   call      'RS714R'
     c                   parm                    p_stat
     c                   parm                    p_nkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in40
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   movel     p_tx20        c2ntxt
     c                   endif
     c                   endif
      *
      * Sjekke om land er gyldig
      *
     c                   if        c2land <> *blank
     c                   move      c2land        p_lkod
     c                   call      'RS712R'
     c                   parm                    p_stat
     c                   parm                    p_lkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in41
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2ltxt
     c                   endif
     c                   endif
      *
      * Sjekke om valuta er gyldig
      *
     c                   if        c2valk <> *blank
     c                   move      c2valk        p_pkod
     c                   call      'RS716R'
     c                   parm                    p_stat
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in42
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2pmna
     c                   endif
     c                   endif
      *
      * Sjekke om forsendelsesmåte er gyldig
      *
     c                   if        c2form <> *zeros
     c                   move      c2form        p_qkod
6.30 c                   call      'RS731R'
     c                   parm                    p_stat
     c                   parm                    p_qkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in43
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2qtxt
     c                   endif
     c                   endif
      *
      * Sjekke om leveringsbetingelse er gyldig
      *
     c                   if        c2lbet <> *zeros
     c                   move      c2lbet        p_rkod
     c                   call      'RS718R'
     c                   parm                    p_stat
     c                   parm                    p_rkod
     c                   parm                    p_tx25
T5.00c                   parm                    p_tx2
T5.00c                   parm                    p_tx3
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in44
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   else
     c                   movel     p_tx25        c2rtx1
     c                   endif
     c                   endif
      *
      * Sjekke om fakturaleverandør er gyldig
      *
     c                   if        f2flev <> *zeros
     c                   call      'RS760R'
     c                   parm                    p_retk
     c                   parm                    f2flev
     c                   parm                    p_tx30
      *
     c                   eval      f2navn = p_tx30
     c                   endif
      *
      * Krav til utfylling av standardfelter på leverandør
      *
     c                   if        w_3mfl = '1'  and
     c                             w_ftst = 1
      *
      * Sjekke om adresse er utfyllt
      *
     c                   if        c2gate = *blank
     c                   move      *on           *in65
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om adresse er utfylt med dummyverdi
      *
     c                   if        c2gate <> ' '
     c                   move      c2gate        w_alfa29
     c                   movel     c2gate        w_alfa01
     c                   if        w_alfa01 = '.' and
     c                             w_alfa29 <> ' '
     c                   move      *on           *in27
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
     c                   endif
      *
      * Sjekke om postnr./poststed er utfyllt
      *
     c                   if        c2sted = *blank and
     c                             c2ponr = *zero
     c                   move      *on           *in32
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
6.31  *
6.31  * Sjekk om det finnes best./best.f/best.parm hvis man
6.31  * setter lev. passiv eller sperret.
6.31  *
6.31 c                   if        c2pass = 'J' or
6.31 c                             c2sprk = 'J'
6.31 c                   exsr      sjekk_best
6.31 c                   if        b_best = *on and
6.31 c                             c2sprk = 'J'
6.31 c                   eval      *in47  = *on
6.31 c                   eval      *in81  = *on
6.31 c                   eval      w_feil = 1
6.31 c                   goto      xa2010
6.31 c                   endif
6.31 c                   if        b_best = *on and
6.31 c                             c2pass = 'J'
6.31 c                   eval      *in50  = *on
6.31 c                   eval      *in81  = *on
6.31 c                   eval      w_feil = 1
6.31 c                   goto      xa2010
6.31 c                   endif
6.31 c                   endif
6.32  *
6.32  *  Sjekker landkode mot utfylt avgiftskode
6.32  *
6.32 c                   if        (c2land = *blank or
6.32 c                              c2land = 'NO')
6.32 c                   if        c2mkod = 2
6.32  * norsk kunde - ikke tillatt med 2
6.32 c                   eval      w_mld1 = 'Norsk lev. kan ikke +
6.32 c                                        ha 2 i avgiftskode.+
6.32 c                                                 '
6.32 c                   call      'AA005R'
6.32 c                   parm                    w_mld1
6.32 c                   eval      c2mkod = s_mkod
6.32 c                   eval      w_feil = 1
6.32 c                   goto      xa2010
6.32 c                   endif
6.32 c                   else
6.32 c                   if         c2mkod = 1
6.32  * utenlandsk kunde - ikke tillatt med 1
6.32 c                   eval      w_mld1 = 'Utenlandsk kunde kan+
6.32 c                                        ikke ha 1 i avgiftsk+
6.32 c                                       ode      '
6.32 c                   call      'AA005R'
6.32 c                   parm                    w_mld1
6.32 c                   eval      c2mkod = s_mkod
6.32 c                   eval      w_feil = 1
6.32 c                   goto      xa2010
6.32 c                   endif
6.32 c                   endif
      *
      * Hent inn kontrollfelt fra leverandørkategori
      *
     c                   eval      ra14l1_lkat = c2katg
     c     ra14l1_key    chain     ra14l1
      *
      * Sjekke om bankkontonummer skal være utfylt
      *
     c                   if        ranbgi = 'J'
      *
     c                   if        c2bgir = 0
     c                   move      *on           *in72
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om bankkontonummerer gyldig modulus 11
      *
     c                   if        c2bgir > 0
      *
     c                   eval      wplist = *blanks
     c                   move      c2bgir        w_chks            1
     c                   move      c2bgir        wplist
     c                   call      'ASMD11'
     c                   parm                    wplist           25
      *
     c                   move      wplist        w_sist            1
      *
     c                   if        w_sist <> w_chks
     c                   move      *on           *in23
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Sjekke om foretaksnummer skal være utfylt
      *
     c                   if        ranorg = 'J'
      *
     c                   if        c2ftnr = 0
     c                   move      *on           *in49
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
6.33 c                   endif
      *
      * Sjekke om foretaksnummer er gyldig modulus 11
      *
     c                   if        c2ftnr > 0  and
     c                             c2ftnr <> 999999999
      *
     c                   eval      wplist = *blanks
     c                   move      c2ftnr        w_chks            1
     c                   move      c2ftnr        wplist
     c                   call      'ASMD11'
     c                   parm                    wplist           25
      *
     c                   move      wplist        w_sist            1
      *
     c                   if        w_sist <> w_chks
     c                   move      *on           *in21
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
      *
      * Sjekke om telefon/mobil er utfylt
      *
     c                   if        rantlf = 'J'
      *
     c                   if        c2tlfn = *blank and
     c                             c2mobn = *blank
     c                   move      *on           *in68
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om telefon er utfylt med dummyverdi
      *
     c                   move      c2tlfn        w_alfa10
     c                   movel     c2tlfn        w_alfa01
     c                   if        w_alfa01 = '.' and
     c                             w_alfa10 <> ' '
     c                   move      *on           *in24
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om telefax er utfylt
      *
     c                   if        ranfax = 'J'
      *
     c                   if        c2tlfx = *blank
     c                   move      *on           *in69
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om telefax er utfylt med dummyverdi
      *
     c                   move      c2tlfx        w_alfa10
     c                   movel     c2tlfx        w_alfa01
     c                   if        w_alfa01 = '.' and
     c                             w_alfa10 <> ' '
     c                   move      *on           *in25
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
      *
      * Sjekke om e-mail adresse er utfylt
      *
     c                   if        ranema = 'J'
      *
     c                   if        c2emal = *blank
     c                   move      *on           *in70
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om e-mail adresse er utfylt med dummyverdi
      *
     c                   move      c2emal        w_alfa49
     c                   movel     c2emal        w_alfa01
     c                   if        w_alfa01 = '.' and
     c                             w_alfa49 <> ' '
     c                   move      *on           *in25
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
      *
      * Sjekke om kontaktperson er utfylt
      *
     c                   if        rankpe = 'J'
      *
      * Finnes kontaktperson på leverandøren
      *
     c                   eval      rukpl7_levr = c2levr
     c     rukpl7_key    setll     rukpl7                                 60
      *
     c                   if        *in60 = *off
     c                   move      *on           *in67
     c                   move      *on           *in40
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
      *
      * Sjekke om foretaksnummer finnes fra før
      *
     c                   if        c2ftnr <> *zeros and
     c                             c2ftnr <> 999999999 and
     c                             w_ftst = 1
     c                   eval      rlevl4_ftnr = c2ftnr
     c     rlevl4_key    setll     rlevl4                                 63
     c     rlevl4_key    reade     rlevl4                                 63
     c                   dow       *in63 = *off
E5.20c                   if        xllevr <> c2levr
E5.20c                   eval      v2levr = xllevr
E5.20c                   eval      v2navn = xlnavn
      *
     c                   eval      w_alin = 6
     c                   eval      w_acol = 25
      *
     c                   exfmt     v2msg
      *
     c                   if        *inkc = *on
     c                   move      *on           *in48
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
      *
     c     rlevl4_key    reade     rlevl4                                 63
     c                   enddo
      *
     c                   endif
      *
      * Sjekke om alfaindex finnes fra før på samme postnr.
      *
     c                   if        c2alfa <> *blank and
     c                             w_ftst = 1
     c                   eval      rlevl2_alfa = c2alfa
     c     rlevl2_key    setll     rlevl2                                 64
     c     rlevl2_key    reade     rlevl2                                 64
     c                   dow       *in64 = *off
E5.20c                   if        xlalfa = c2alfa and
E5.20c                             xlponr = c2ponr and
     c                             xllevr <> rllevr
E5.20c                   eval      v2levr = xllevr
E5.20c                   eval      v2navn = xlnavn
      *
     c                   eval      w_alin = 6
     c                   eval      w_acol = 25
      *
     c                   exfmt     w2msg
      *
     c                   if        *inkc = *on
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
     c     rlevl2_key    reade     rlevl2                                 64
     c                   enddo
      *
     c                   endif
      *
      * Sjekke om bankgironummer finnes fra før
      *
     c                   if        c2bgir <> *zeros and
     c                             c2bgir <> 99999999999 and
     c                             w_ftst = 1
     c                   eval      rlevl6_bgir = c2bgir
     c     rlevl6_key    setll     rlevl6                                 63
     c     rlevl6_key    reade     rlevl6                                 63
     c                   dow       *in63 = *off
E5.20c                   if        xllevr <> rllevr
E5.20c                   eval      x2levr = xllevr
E5.20c                   eval      x2navn = xlnavn
      *
     c                   eval      w_alin = 6
     c                   eval      w_acol = 25
      *
     c                   exfmt     x2msg
      *
     c                   if        *inkc = *on
     c                   move      *on           *in48
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
     c     rlevl6_key    reade     rlevl6                                 63
     c                   enddo
      *
     c                   endif
      *
      * Sjekke om telefonnummer finnes fra før
      *
     c                   if        c2tlfn <> *blank and
     c                             c2tlfn <> '.' and
     c                             w_ftst = 1
     c                   eval      rlevl7_tlfn = c2tlfn
     c     rlevl7_key    setll     rlevl7                                 63
     c     rlevl7_key    reade     rlevl7                                 63
     c                   dow       *in63 = *off
     c                   if        xllevr <> rllevr
     c                   eval      y2levr = xllevr
     c                   eval      y2navn = xlnavn
      *
     c                   eval      w_alin = 6
     c                   eval      w_acol = 25
      *
     c                   exfmt     y2msg
      *
     c                   if        *inkc = *on
     c                   move      *on           *in48
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
     c     rlevl7_key    reade     rlevl7                                 63
     c                   enddo
      *
     c                   endif
      *
     c                   endif
      *
      * Sjekke om e-mail adresse finnes fra før
      *
     c                   if        c2emal <> *blank and
     c                             c2emal <> '.' and
     c                             w_ftst = 1
     c                   eval      rlevl8_emal = c2emal
     c     rlevl8_key    setll     rlevl8                                 63
     c     rlevl8_key    reade     rlevl8                                 63
     c                   dow       *in63 = *off
     c                   if        xllevr <> rllevr
     c                   eval      z2levr = xllevr
     c                   eval      z2navn = xlnavn
      *
     c                   eval      w_alin = 6
     c                   eval      w_acol = 25
      *
     c                   exfmt     z2msg
      *
     c                   if        *inkc = *on
     c                   move      *on           *in48
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
     c     rlevl8_key    reade     rlevl8                                 63
     c                   enddo
      *
     c                   endif
      *
6.33 c***                endif
      *
      * Sjekke om ean-nummer finnes fra før
      *
     c                   if        g2eanl <> *zeros and
     c                             w_ftst = 1
     c                   eval      rlevl5_eanl = g2eanl
     c     rlevl5_key    setll     rlevl5                                 63
     c     rlevl5_key    reade     rlevl5                                 63
     c                   dow       *in63 = *off
E5.20c                   if        xllevr <> rllevr
E5.20c                   eval      t2levr = xllevr
E5.20c                   eval      t2navn = xlnavn
      *
     c                   eval      w_alin = 6
     c                   eval      w_acol = 25
      *
     c                   exfmt     t2msg
      *
     c                   if        *inkc = *on
     c                   move      *on           *in48
     c                   move      *on           *in81
     c                   eval      w_feil = 1
     c                   goto      xa2010
     c                   endif
      *
     c                   endif
     c     rlevl5_key    reade     rlevl5                                 63
     c                   enddo
      *
     c                   endif
8.01  *
8.01  * Sjekke om nobb-leverandør finnes fra før
8.01  *
8.01 c                   if        c2nobb <> 0
8.01 c                   eval      w_anta = 0
8.01 c/exec sql
8.01 c+ select count(*)
8.01 c+ into  :w_anta
8.01 c+ from rlevpf
8.01 c+ where rlnobb = :c2nobb and
8.01 c+  rlfirm = :w_firm  and
8.01 c+  rllevr <> :c2levr
8.01 c/end-exec
8.01 c                   if        w_anta > 0
8.01 c                   eval      w_mld1 = 'Nobb leverandørnummer +
8.01 c                                        finnes på annen leverandør'
8.01 c                   call      'AA005R'
8.01 c                   parm                    w_mld1
8.01 c                   eval      w_feil = 1
8.01 c                   goto      xa2010
8.01 c                   endif
8.01 c
8.01 c                   endif
      *
     c     xa2010        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * 0-Stilling av arbeidsindikatorer og arbeidsfelt                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnulle        begsr
      *
     c                   eval      *in21 = *off
     c                   eval      *in23 = *off
     c                   eval      *in24 = *off
     c                   eval      *in25 = *off
     c                   eval      *in27 = *off
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
     c                   eval      *in38 = *off
     c                   eval      *in39 = *off
     c                   eval      *in40 = *off
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
     c                   eval      *in46 = *off
6.31 c                   eval      *in47 = *off
     c                   eval      *in49 = *off
6.31 c                   eval      *in50 = *off
     c                   eval      *in60 = *off
     c                   eval      *in61 = *off
     c                   eval      *in62 = *off
     c                   eval      *in63 = *off
     c                   eval      *in64 = *off
     c                   eval      *in65 = *off
     c                   eval      *in67 = *off
     c                   eval      *in68 = *off
     c                   eval      *in69 = *off
     c                   eval      *in70 = *off
     c                   eval      *in72 = *off
      *
     c                   endsr
6.31  /eject
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.31  * Sjekker om det finnes best./best.f. og best.parameter           *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.31  *
6.31 c     sjekk_best    begsr
6.31  *
6.31 c                   eval      b_best = *off
6.31  *
6.31 c                   eval      lohelk_ldor = c2levr
6.31 c     lohelk_key    chain     lohelk
6.31 c                   if        %found(lohelk)
6.31 c                   eval      b_best = *on
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   eval      lfhel5_ldor = c2levr
6.31 c     lfhel5_key    chain     lfhel5
6.31 c                   if        %found(lfhel5)
6.31 c                   eval      b_best = *on
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   eval      mppalr_ldor = c2levr
6.31 c     mppalr_key    chain     mppalr
6.31 c                   if        %found(mppalr)
6.31 c                   eval      b_best = *on
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31 c                   endsr
6.31  *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wactli = 0
     c                   eval      wactpo = 0
     c                   eval      w_feil = 1
     c                   eval      wpfunk = *zero
     c                   eval      wpantp = *zero
     c                   eval      wseqen = *blank
      *
      * Parametre til Notatark.
      *
     c                   move      'L'           wpsyst
     c                   eval      wpspes = 0
     c                   eval      wpavde = 0
6.22 c*                  eval      wppros = 0
6.22 c                   eval      wppros = *blanks
     c                   eval      wpakti = *blanks
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_levr
     c                   parm                    p_valg
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
     c                   move      p_levr        c2levr
     c                   eval      p_biln = 0
     c                   eval      p_rest = 0
      *
      * Sperre for registrering ved spørring
      *
     c                   if        p_valg = 5
     c                   move      *on           *in30
     c                   endif
      *
      * Hent leverandør
      *
     c                   eval      rlevl1_levr = p_levr
     c     rlevl1_key    chain     rlevl1                             61
      *
      * Hent statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             61
      *
     c                   if        *in61 = *off
     c                   move      ra1bel        w_1bel
     c                   move      ra1bkl        w_1bkl
     c                   else
     c                   move      30            w_1bel
     c                   move      0             w_1bkl
     c                   endif
      *
      * Hent statuskode 3
      *
     c     raa3l1_key    chain     raa3l1
      *
     c                   if        %found(raa3l1) and
     c                             ra3mfl = '1'
     c                   eval      w_3mfl = ra3mfe
     c                   else
     c                   eval      w_3mfl = ' '
     c                   endif
      *
      **   Dato + klokkeslett
      *
      *
      * Key for leverandørregisteret via leverandørnummer
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for leverandørregisteret via leverandørnummer
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlu_levr
      *
      * Key for leverandørregister via alfaindex
      *
     c     rlevl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl2_alfa
      *
      * Key for leverandørregister via foretaksnummer
      *
     c     rlevl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl4_ftnr
      *
      * Key for leverandørregister via ean-leverandørnummer
      *
     c     rlevl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl5_eanl
      *
      * Key for leverandørregister via bankgironummer
      *
     c     rlevl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl6_bgir
      *
      * Key for leverandørregister via telefonnummer
      *
     c     rlevl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl7_tlfn
      *
      * Key for leverandørregister via e-postadresse
      *
     c     rlevl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl8_emal
      *
      * Key for bankregisteret via leverandørnummer
      *
     c     rlbapf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlbapf_levr
      *
      * Key for bankregisteret via leverandørnummer
      *
     c     rlbalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlbalu_levr
      *
T5.33 * Key for notatblokk
      *
E5.40c     rklal2_key    klist
T5.33c                   kfld                    w_firm
E5.40c                   kfld                    rklal2_syst
E5.40c                   kfld                    rklal2_pros
E5.40c                   kfld                    rklal2_akti
E5.40c                   kfld                    rklal2_levr
      *
      * Key for statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på statuskode 3
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
5.41  *
      * Key for postnummer-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppslag på leverndørkategori
      *
     c     ra14l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra14l1_lkat
      *
T5.50 * Key for kontaktpersonregister pr. leverandør
      *
T5.50c     rukpl7_key    klist
T5.50c                   kfld                    w_firm
T5.50c                   kfld                    rukpl7_levr
6.31  *
6.31  * Key for bestillinger
6.31  *
6.31 c     lohelk_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    lohelk_ldor
6.31  *
6.31  * Key for bestilling forslag
6.31  *
6.31 c     lfhel5_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    lfhel5_ldor
6.31  *
6.31  * Key for best.f. paramter
6.31  *
6.31 c     mppalr_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    mppalr_ldor
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
