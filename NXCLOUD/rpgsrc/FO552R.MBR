     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO552R
      * Beskrivelse . . : Finn ordre via bongnummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210605 ASK  Nytt program
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * R6.30 300616 nho  w_snum var ikke endret til 8 lang
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbodtl4    if   e           k disk    rename(bodtpfr:bodtl4r)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
      *
     ffo552d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(bdfirm)
     d p_bong          s                   like(bdbong)
6.nu d p_numm          s                   like(bdornr)
      *
      * Definering av variabler i nøkler
      *
     d bodtl4_bong     s                   like(bdbong)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_wsid     s                   like(bowsid)
     d bohel1_bong     s                   like(bobong)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(bofirm)
     d w_ddat          s               d   datfmt(*iso)
     d w_daar          s              4  0
     d w_faar          s              4  0
6.30 d*w_snum          s              6  0
6.30 d w_snum          s              8  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      bodtl4_bong = p_bong
      *
     c     bodtl4_key    setll     bodtl4
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_numm = b1ornr
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     bodtl4_key    reade     bodtl4
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        bdornr <> w_snum
     c                   eval      bohel1_aarr = bdaarr
     c                   eval      bohel1_wsid = bdwsid
     c                   eval      bohel1_bong = bdbong
      *
     c     bohel1_key    chain     bohel1
     c                   if        %found
     c                   eval      w_snum = bdornr
     c                   eval      b1valg = 0
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1aarr = boaarr
     c                   eval      b1ornr = bdornr
     c                   eval      b1wsid = bowsid
     c     *dmy          move      bobdat        b1bdat
     c                   eval      b1vref = bovref
      *
     c                   write     b1sfl
     c                   endif
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_bong
6.nu c                   parm                    p_numm
      *
      * Key for posisjonering på bongnummer
      *
     c     bodtl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl4_bong
      *
      * Key for oppslag på bongnummer
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohel1_aarr
     c                   kfld                    bohel1_wsid
     c                   kfld                    bohel1_bong
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      *in22 = *on
      *
     c                   time                    w_ddat
     c                   eval      w_daar = %subdt(w_ddat:*years)
     c                   eval      w_faar = w_daar - 1
      *
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      bodtl4_bong = p_bong
      *
     c     bodtl4_key    setll     bodtl4
     c                   exsr      crt_subfile
      *
     c                   endsr
