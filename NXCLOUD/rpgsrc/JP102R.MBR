     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP102R
      * Beskrivelse . . : Vedlikehold av betingelser
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050696 HKO  Nytt program
      *       150598 HKO  Lagt om til Nobb-kontrakt
      *       140898 HKO  Lagt inn F13 for kall av vedlikehold av
      *                   rabattelement fra C2BLD.
      *                   Lagt inn automatisk kall av vedlikehold av
      *                   rabattelement ved oppretting og kopiering
      *       070400 HKO  Ved kopiering av betingelser ble ikke rabatt-
      *                   element merket med rsts = 1. Dette gjorde at
      *                   disse ikke ble med på oppdatering.
      *       181003 HKO  Henter varegruppe fra modul
      *       130605 HKO  Utvidet nivå med varetype og leveringstype
6.10  * v6.10 310810 BSA  Indikatorstyrt skifte av F23. Jacada klarer
6.10  *                   ikke oversette dette til F tast.
6.30  * v6.30 300614 bof  Filter + masseaktivisering
6.31  * v6.30 040714 bof  Overføre til fraktbetingelse
6.32  * v6.30 071014 bof  Justeringer vedr. fraktbetingelse
6.33  * 6.30  201014 Bsa  Innfører F tast for visning av NOBB info. Erstatter
6.33  *                   koding i jacada.
6.34  * 6.30  210515 bof  Gir melding hvis samme vare har akt. på flere linjer
6.35  * 6.30  220715 bof  Utvidet F10 sjekke duplikate akt. på vare
6.3x  * 6.30  310515 bof  Utvidet med prisgruppe
6.36  * 6.30  150915 bsa  Diverse rettelser ved posisjonering
6.37  * 6.30  151115 bof  Feilretting vedr. prigsruppe på element
6.38  * 6.30  140316 nho  Rekompilert pga endring i bildet
6.39  * 6.30  010616 nho  Lagt inn flere felt i nøkkel for
      *                   posisjonering
6.3a  * 6.30  050816 bsa  Posisjonerer seg på opprettet betingelse.
6.3b  * 6.30  230517 bof  Hvis kopi. av L, så skal denne sette til L.
6.3b  *                   Hvis kopi. så sette aktivisering på
6.3c  * 6.30  040817 bof  Feilretting vedr. kopiering prgr+Lokal --> L
6.3d  * 6.30  311018 bof  Utvidet med logistikk-vare behandling.
6.3e  * 6.30  311018 bof  Utvidet nytt pkt 15, vise varer under modul
6.3f  * 6.30  171218 bof  feilretting vedr. filter på log.vare
7.xx  * 7.00  280716 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
     fjbetlr    if   e           k disk    rename(jbetpfr:jbetlrr)
     fjbetlu    uf a e           k disk    rename(jbetpfr:jbetlur)
     fjbetl2    if   e           k disk    rename(jbetpfr:jbetl2r)
     fjbetl3    if   e           k disk    rename(jbetpfr:jbetl3r)
     fjbetl4    if   e           k disk    rename(jbetpfr:jbetl4r)
     fjbetl5    if   e           k disk    rename(jbetpfr:jbetl5r)
     fjbetl6    if   e           k disk    rename(jbetpfr:jbetl6r)
     fjbetl7    if   e           k disk    rename(jbetpfr:jbetl7r)
     fjbetl8    if   e           k disk    rename(jbetpfr:jbetl8r)
6.34 fjbetlb    if   e           k disk    rename(jbetpfr:jbetlbr)
6.3x fjbetla    if   e           k disk    rename(jbetpfr:jbetlar)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.3x fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjrael1    if   e           k disk    rename(jraepfr:jrael1r)
     fjrael4    if   e           k disk    rename(jraepfr:jrael4r)
     fjraelu    uf a e           k disk    rename(jraepfr:jraelur)
     fvvtyl1    if   e           k disk    rename(vvtypfr:vvtyl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
6.31 fjfbelu    uf a e           k disk    rename(jfbepfr:jfbelur)
6.31 fjfbel8    if   e           k disk    rename(jfbepfr:jfbel8r)
      *
     fjp102d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jpfirm)
     d p_ldor          s                   like(jpldor)
6.33 d p_nobb          s              8
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
6.3x d jbetl1_prgr     s                   like(jpprgr)
     d jbetl1_ogrp     s                   like(jpogrp)
     d jbetl1_hgrp     s                   like(jphgrp)
     d jbetl1_ugrp     s                   like(jpugrp)
     d jbetl1_modn     s                   like(jpmodn)
     d jbetl1_vare     s                   like(jpvare)
     d jbetl1_vtyp     s                   like(jpvtyp)
     d jbetl1_lety     s                   like(jplety)
6.3x d jbetlu_prgr     s                   like(jpprgr)
     d jbetlu_ogrp     s                   like(jpogrp)
     d jbetlu_hgrp     s                   like(jphgrp)
     d jbetlu_ugrp     s                   like(jpugrp)
     d jbetlu_modn     s                   like(jpmodn)
     d jbetlu_vare     s                   like(jpvare)
     d jbetlu_vtyp     s                   like(jpvtyp)
     d jbetlu_lety     s                   like(jplety)
     d jbetlu_rsts     s                   like(jprsts)
     d jbetlu_line     s                   like(jpline)
6.3x d jbetlr_prgr     s                   like(jpprgr)
     d jbetlr_ogrp     s                   like(jpogrp)
     d jbetlr_hgrp     s                   like(jphgrp)
     d jbetlr_ugrp     s                   like(jpugrp)
     d jbetlr_modn     s                   like(jpmodn)
     d jbetlr_vare     s                   like(jpvare)
     d jbetlr_vtyp     s                   like(jpvtyp)
     d jbetlr_lety     s                   like(jplety)
     d jbetlr_rsts     s                   like(jprsts)
     d jbetlr_line     s                   like(jpline)
     d jbetl2_hgrp     s                   like(jphgrp)
     d jbetl3_ugrp     s                   like(jpugrp)
     d jbetl4_modn     s                   like(jpmodn)
     d jbetl5_vare     s                   like(jpvare)
     d jbetl6_vtyp     s                   like(jpvtyp)
     d jbetl7_lety     s                   like(jplety)
     d jbetl8_beti     s                   like(jpbeti)
6.34 d jbetlb_vare     s                   like(jpvare)
6.3x d jbetla_ogrp     s                   like(jpogrp)
6.39 d jbetla_hgrp     s                   like(jphgrp)
6.39 d jbetla_ugrp     s                   like(jpugrp)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_modn     s                   like(jumodn)
     d jvarl1_vare     s                   like(jvvare)
6.3x d jrael1_eprg     s                   like(jpeprg)
     d jrael1_eogr     s                   like(jpeogr)
     d jrael1_ehgr     s                   like(jpehgr)
     d jrael1_eugr     s                   like(jpeugr)
     d jrael1_emod     s                   like(jpemod)
     d jrael1_evar     s                   like(jpevar)
     d jrael1_evty     s                   like(jpevty)
     d jrael1_elet     s                   like(jpelet)
     d jrael1_ebrs     s                   like(jpebrs)
     d jrael1_ebli     s                   like(jpebli)
     d jrael4_eogr     s                   like(jpeogr)
6.3x d jrael4_eprg     s                   like(jpeprg)
     d jrael4_ehgr     s                   like(jpehgr)
     d jrael4_eugr     s                   like(jpeugr)
     d jrael4_emod     s                   like(jpemod)
     d jrael4_evar     s                   like(jpevar)
     d jrael4_evty     s                   like(jpevty)
     d jrael4_elet     s                   like(jpelet)
     d jrael4_ebrs     s                   like(jpebrs)
     d jrael4_ebli     s                   like(jpebli)
     d jrael4_eakt     s                   like(jpeakt)
6.3x d jraelu_eprg     s                   like(jpeprg)
     d jraelu_eogr     s                   like(jpeogr)
     d jraelu_ehgr     s                   like(jpehgr)
     d jraelu_eugr     s                   like(jpeugr)
     d jraelu_emod     s                   like(jpemod)
     d jraelu_evar     s                   like(jpevar)
     d jraelu_evty     s                   like(jpevty)
     d jraelu_elet     s                   like(jpelet)
     d jraelu_ebrs     s                   like(jpebrs)
     d jraelu_ebli     s                   like(jpebli)
     d vvtyl1_vtyp     s                   like(vavtyp)
     d vltpl1_lety     s                   like(vylety)
6.31 d jfbelu_fprg     s                   like(jpfprg)
6.31 d jfbelu_fogr     s                   like(jpfogr)
6.31 d jfbelu_fhgr     s                   like(jpfhgr)
6.31 d jfbelu_fugr     s                   like(jpfugr)
6.31 d jfbelu_fmod     s                   like(jpfmod)
6.31 d jfbelu_fvar     s                   like(jpfvar)
6.31 d jfbelu_fvty     s                   like(jpfvty)
6.31 d jfbelu_flet     s                   like(jpflet)
6.3x d jfbel8_fbpg     s                   like(jpfprg)
6.31 d jfbel8_fbog     s                   like(jpfogr)
6.31 d jfbel8_fbhg     s                   like(jpfhgr)
6.31 d jfbel8_fbug     s                   like(jpfugr)
6.31 d jfbel8_fbmo     s                   like(jpfmod)
6.31 d jfbel8_fbva     s                   like(jpfvar)
6.31 d jfbel8_fbvt     s                   like(jpfvty)
6.31 d jfbel8_fbke     s                   like(jpfbke)
6.31 d jfbel8_fbrs     s                   like(jpfbrs)
6.31 d jfbel8_fbli     s                   like(jpfbli)
6.31 d jfbel8_fbbe     s                   like(jpfbbe)
6.3x d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_visn          s              1    inz(*off)
6.32 d b_frak          s              1    inz(*off)
     d b_element       s              1    inz(*off)
      *
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(jpfirm)
     d w_ldor          s                   like(jpldor)
      *
6.3x d w_prgr          s                   like(jpprgr)
     d w_ogrp          s                   like(jpogrp)
     d w_hgrp          s                   like(jphgrp)
     d w_ugrp          s                   like(jpugrp)
     d w_modn          s                   like(jpmodn)
     d w_vare          s                   like(jpvare)
     d w_vtyp          s                   like(jpvtyp)
     d w_lety          s                   like(jplety)
     d w_beti          s                   like(jpbeti)
      *
     d w_oogr          s                   like(vgoogr)
     d w_hhgr          s                   like(vghhgr)
     d w_uugr          s                   like(vguugr)
     d w_otxt          s                   like(vgotxt)
6.3x d w_ptxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_mte1          s                   like(jumte1)
     d w_tek1          s                   like(jvtek1)
     d w_ttxt          s                   like(vavtxt)
     d w_lbes          s                   like(vybesk)
6.31 d w_krti          s              9  2
6.31 d w_ifak          s              8  6
6.34 d w_anta          s              9  0
6.3d d w_lv_nobb       s                   like(jvnobb)
6.3d d w_lv_modn       s                   like(jvmodn)
6.3d d w_undv          s              8  0
6.3d d w_logl          s             15
6.3d d w_teller        s              8  0
6.3d d w_jvqvnr        s                   like(jpfvar)
6.33 d w_jvquno        s              8  0
      *
6.3x d s_prgr          s                   like(jpprgr)
     d s_ogrp          s                   like(jpogrp)
     d s_hgrp          s                   like(jphgrp)
     d s_ugrp          s                   like(jpugrp)
     d s_modn          s                   like(jpmodn)
     d s_vare          s                   like(jpvare)
     d s_vtyp          s                   like(jpvtyp)
     d s_lety          s                   like(jplety)
     d s_rsts          s                   like(jprsts)
     d s_line          s                   like(jpline)
     d s_beti          s                   like(jpbeti)
      *
6.3a d x_prgr          s                   like(jpprgr)
6.3a d x_ogrp          s                   like(jpogrp)
6.3a d x_hgrp          s                   like(jphgrp)
6.3a d x_ugrp          s                   like(jpugrp)
6.3a d x_modn          s                   like(jpmodn)
6.3a d x_vare          s                   like(jpvare)
6.3a d x_vtyp          s                   like(jpvtyp)
6.3a d x_lety          s                   like(jplety)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3d C/Exec SQL
6.3d C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3d C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
6.30 c                   when      *inka
6.30 c                   exsr      xf1win
6.30 c                   exsr      forny
6.30 c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
6.30 c                   when      *inki
6.30 c                   exsr      xc3win
6.30 c                   exsr      forny
6.30 c                   goto      b2taga
6.34 c                   when      *inkj
6.34 c                   exsr      xc5msg
6.34 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Bytter mellom å vise faktor og nettopris i bildet
      *
     c                   if        *inkx
     c                   if        b_visn   = *off
     c                   eval      b_visn   = *on
6.10 c                   eval      *in35    = *on
     c                   eval      b2over   = 'Nettopris'
6.10 c**                 eval      b2cmdkey = '     F23=Vise faktor'
     c                   else
     c                   eval      b_visn   = *off
6.10 c                   eval      *in35    = *off
     c                   eval      b2over   = 'Faktor'
6.10 c**                 eval      b2cmdkey = 'F23=Vise nettopriser'
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
6.3x c                   if        b2prgr <> *blank or
     c                             b2ogrp <> 0      or
     c                             b2hgrp <> 0      or
     c                             b2ugrp <> 0      or
     c                             b2modn <> 0      or
     c                             b2vare <> *blank or
     c                             b2vtyp <> *blank or
     c                             b2lety <> 0 or
     c                             b2beti <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jbetl2_hgrp = b1hgrp
     c     jbetl2_key    setll     jbetl2
     c                   when      w_seqe = 'U'
     c                   eval      jbetl3_ugrp = b1ugrp
     c     jbetl3_key    setll     jbetl3
     c                   when      w_seqe = 'M'
     c                   eval      jbetl4_modn = b1modn
     c     jbetl4_key    setll     jbetl4
     c                   when      w_seqe = 'V'
     c                   eval      jbetl5_vare = b1vare
     c     jbetl5_key    setll     jbetl5
     c                   when      w_seqe = 'T'
     c                   eval      jbetl6_vtyp = b1vtyp
     c     jbetl6_key    setll     jbetl6
     c                   when      w_seqe = 'L'
     c                   eval      jbetl7_lety = b1lety
     c     jbetl7_key    setll     jbetl7
     c                   when      w_seqe = 'B'
     c                   eval      jbetl8_beti = b1beti
     c     jbetl8_key    setll     jbetl8
6.3x c                   when      w_seqe = 'O'
6.3x c                   eval      jbetla_ogrp = b1ogrp
6.3x c     jbetla_key    setll     jbetla
     c                   other
6.3a  * Hvis retur fra nyopprettet betingelse, posisjoner ved den nye.
6.3a c                   if        x_prgr <> *blanks or
6.3a c                             x_ogrp <> 0       or
6.3a c                             x_hgrp <> 0       or
6.3a c                             x_ugrp <> 0       or
6.3a c                             x_modn <> 0       or
6.3a c                             x_vare <> *blanks or
6.3a c                             x_vtyp <> *blanks or
6.3a c                             x_lety <> 0
6.3a c                   eval      b1prgr = x_prgr
6.3a c                   eval      b1ogrp = x_ogrp
6.3a c                   eval      b1hgrp = x_hgrp
6.3a c                   eval      b1ugrp = x_ugrp
6.3a c                   eval      b1modn = x_modn
6.3a c                   eval      b1vare = x_vare
6.3a c                   eval      b1vtyp = x_vtyp
6.3a c                   eval      b1lety = x_lety
6.3a c                   eval      x_prgr = *blanks
6.3a c                   eval      x_ogrp = 0
6.3a c                   eval      x_hgrp = 0
6.3a c                   eval      x_ugrp = 0
6.3a c                   eval      x_modn = 0
6.3a c                   eval      x_vare = *blanks
6.3a c                   eval      x_vtyp = *blanks
6.3a c                   eval      x_lety = 0
6.3a c                   endif
6.3a  *
6.3x c                   eval      jbetl1_prgr = b1prgr
     c                   eval      jbetl1_ogrp = b1ogrp
     c                   eval      jbetl1_hgrp = b1hgrp
     c                   eval      jbetl1_ugrp = b1ugrp
     c                   eval      jbetl1_modn = b1modn
     c                   eval      jbetl1_vare = b1vare
     c                   eval      jbetl1_vtyp = b1vtyp
     c                   eval      jbetl1_lety = b1lety
     c     jbetl1_key    setll     jbetl1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Overgruppe
      *
6.3x c                   if        b2prgr <> *blank
     c                   eval      w_seqe      = *blank
6.3x c                   eval      jbetl1_prgr = b2prgr
     c                   eval      jbetl1_ogrp = b2ogrp
     c                   eval      jbetl1_hgrp = b2hgrp
     c                   eval      jbetl1_ugrp = b2ugrp
     c                   eval      jbetl1_modn = b2modn
     c                   eval      jbetl1_vare = b2vare
     c                   eval      jbetl1_vtyp = b2vtyp
     c                   eval      jbetl1_lety = b2lety
     c     jbetl1_key    setll     jbetl1
     c                   goto      end_pos
     c                   endif
6.3x  *
6.3x  * Overgruppe
6.3x  *
6.3x c                   if        b2ogrp <> 0
6.3x c                   eval      w_seqe      = 'O'
6.3x c                   eval      jbetla_ogrp = b2ogrp
6.39 c                   eval      jbetla_hgrp = b2hgrp
6.39 c                   eval      jbetla_ugrp = b2ugrp
6.3x c     jbetla_key    setll     jbetla
6.3x c                   goto      end_pos
6.3x c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe      = 'H'
     c                   eval      jbetl2_hgrp = b2hgrp
     c     jbetl2_key    setll     jbetl2
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe      = 'U'
     c                   eval      jbetl3_ugrp = b2ugrp
     c     jbetl3_key    setll     jbetl3
     c                   goto      end_pos
     c                   endif
      *
      * Modulnummer
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe      = 'M'
     c                   eval      jbetl4_modn = b2modn
     c     jbetl4_key    setll     jbetl4
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe      = 'V'
     c                   eval      jbetl5_vare = b2vare
     c     jbetl5_key    setll     jbetl5
     c                   goto      end_pos
     c                   endif
      *
      * Varetype
      *
     c                   if        b2vtyp <> *blank
     c                   eval      w_seqe      = 'T'
     c                   eval      jbetl6_vtyp = b2vtyp
     c     jbetl6_key    setll     jbetl6
     c                   goto      end_pos
     c                   endif
      *
      * Leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe      = 'L'
     c                   eval      jbetl7_lety = b2lety
     c     jbetl7_key    setll     jbetl7
     c                   goto      end_pos
     c                   endif
      *
      * Betingelse
      *
     c                   if        b2beti <> *blank
     c                   eval      w_seqe      = 'B'
     c                   eval      jbetl8_beti = b2beti
     c     jbetl8_key    setll     jbetl8
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
6.36 c                   eval      b2prgr = *blank
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
     c                   eval      b2vtyp = *blank
     c                   eval      b2lety = 0
     c                   eval      b2beti = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b2ldor
6.36 c                   eval      c1prgr = b1prgr
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   eval      c1vtyp = b1vtyp
     c                   eval      c1lety = b1lety
     c                   eval      c1rsts = b1rsts
     c                   eval      c1line = b1line
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
6.3x c                   eval      k1prgr = b1prgr
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1modn = b1modn
     c                   eval      k1vare = b1vare
     c                   eval      k1vtyp = b1vtyp
     c                   eval      k1lety = b1lety
     c                   eval      k1rsts = b1rsts
     c                   eval      k1line = b1line
     c                   exsr      xk1win
     c                   eval      b1valg = 0
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
6.3x c                   eval      d1prgr = b1prgr
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1modn = b1modn
     c                   eval      d1vare = b1vare
     c                   eval      d1vtyp = b1vtyp
     c                   eval      d1lety = b1lety
     c                   eval      d1rsts = b1rsts
     c                   eval      d1line = b1line
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b2ldor
6.36 c                   eval      c1prgr = b1prgr
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   eval      c1vtyp = b1vtyp
     c                   eval      c1lety = b1lety
     c                   eval      c1rsts = b1rsts
     c                   eval      c1line = b1line
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Rabattelement
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b1vare
     c                   call      'JP110R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    b1prgr
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1modn
     c                   parm                    w_vare
     c                   parm                    b1vtyp
     c                   parm                    b1lety
     c                   parm                    b1rsts
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   exsr      finn_fant
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis vareavtale
      *
     c                   if        b1valg = 8
     c                   eval      b1valg = 0
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Aktiviser/passiviser betingelse
      *
     c                   if        b1valg = 9
     c                   exsr      aktiviser_bet
     c                   eval      b1valg = 0
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
6.3e  *
6.3e  * Vise varer under modul
6.3e  *
6.3e c                   if        b1valg = 15
6.3e c                   call      'JV548R'
6.3e c                   parm                    w_firm
6.3e c                   parm                    b1modn
6.3e c                   eval      b1valg = 0
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
6.3e c                   update    b1sfl
6.3e c                   iter
6.3e c                   endif
6.31  *
6.31  * Aktiviser/passiviser betingelse
6.31  *
6.31 c                   if        b1valg = 16
6.31 c                   exsr      xc4win
6.31 c                   eval      b1valg = 0
6.31 c                   exsr      finn_fant
6.3d  * sjekk om logistikk-vare
6.3d c                   eval      w_jvqvnr = b1vare
6.3d c                   exsr      finn_logivar
6.31 c                   update    b1sfl
6.31 c                   iter
6.31 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
6.36 c                   eval      c1beti = *blanks
     c                   eval      c1ldor = b2ldor
6.36 c                   eval      c1prgr = b2prgr
     c                   eval      c1ogrp = b2ogrp
     c                   eval      c1hgrp = b2hgrp
     c                   eval      c1ugrp = b2ugrp
     c                   eval      c1modn = b2modn
     c                   eval      c1vare = b2vare
     c                   eval      c1vtyp = b2vtyp
     c                   eval      c1lety = b2lety
     c                   eval      c1beti = b2beti
     c                   eval      c1rsts = 1
     c                   eval      c1line = 0
      *
     c                   eval      c1lnav = b2lnav
      *
     c                   eval      c1otxt = *blank
6.3x c                   eval      c1ptxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1mte1 = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1ttxt = *blank
     c                   eval      c1lbes = *blank
      *
     c     c1taga        tag
      *
     c                   eval      s_beti = c1beti
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Validering av nøkkel
      *
6.3x c                   eval      w_prgr = c1prgr
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_modn = c1modn
     c                   eval      w_vare = c1vare
     c                   eval      w_vtyp = c1vtyp
     c                   eval      w_lety = c1lety
     c                   eval      w_beti = c1beti
      *
     c                   eval      w_otxt = *blank
6.3x c                   eval      w_ptxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_mte1 = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_ttxt = *blank
     c                   eval      w_lbes = *blank
      *
     c                   exsr      sjekk_key
      *
6.3x c                   eval      c1prgr = w_prgr
     c                   eval      c1ogrp = w_ogrp
     c                   eval      c1hgrp = w_hgrp
     c                   eval      c1ugrp = w_ugrp
     c                   eval      c1modn = w_modn
     c                   eval      c1vare = w_vare
     c                   eval      c1vtyp = w_vtyp
     c                   eval      c1lety = w_lety
     c                   eval      c1beti = w_beti
      *
     c                   eval      c1otxt = w_otxt
6.3x c                   eval      c1ptxt = w_ptxt
     c                   eval      c1htxt = w_htxt
     c                   eval      c1utxt = w_utxt
     c                   eval      c1mte1 = w_mte1
     c                   eval      c1tek1 = w_tek1
     c                   eval      c1ttxt = w_ttxt
     c                   eval      c1lbes = w_lbes
      *
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Dersom betingelsestekst er endret nullstilles reg.status og linjenr
      *
     c                   if        c1beti <> s_beti
     c                   eval      c1rsts = 1
     c                   eval      c1line = 0
     c                   eval      s_beti = c1beti
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   if        c1line <> 0
6.36 c                   eval      jbetlr_prgr = c1prgr
     c                   eval      jbetlr_ogrp = c1ogrp
     c                   eval      jbetlr_hgrp = c1hgrp
     c                   eval      jbetlr_ugrp = c1ugrp
     c                   eval      jbetlr_modn = c1modn
     c                   eval      jbetlr_vare = c1vare
     c                   eval      jbetlr_vtyp = c1vtyp
     c                   eval      jbetlr_lety = c1lety
     c                   eval      jbetlr_rsts = c1rsts
     c                   eval      jbetlr_line = c1line
     c     jbetlr_key    chain     jbetlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
6.36 c                   eval      b2prgr = *blank
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
     c                   eval      b2vtyp = *blank
     c                   eval      b2lety = 0
     c                   eval      b2beti = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å overføre valgt linje til frakttabell
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     xc4win        begsr
6.32  *
6.32  * Valider kun om betingelser er på varenivå
6.32  *
6.32 c                   if        b1vare = *blank
6.32 c                   exfmt     c4wina
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32  * Valider kun kronetillegg
6.32  *
6.32 c                   eval      b_frak = *off
6.32  *
6.37 c                   eval      jrael1_eprg = b1prgr
6.32 c                   eval      jrael1_eogr = b1ogrp
6.32 c                   eval      jrael1_ehgr = b1hgrp
6.32 c                   eval      jrael1_eugr = b1ugrp
6.32 c                   eval      jrael1_emod = b1modn
6.32 c                   eval      jrael1_evar = b1vare
6.32 c                   eval      jrael1_evty = b1vtyp
6.32 c                   eval      jrael1_elet = b1lety
6.32 c                   eval      jrael1_ebrs = b1rsts
6.32 c                   eval      jrael1_ebli = b1line
6.32 c     jrael1_key    setll     jrael1
6.32 c     jrael1_key    reade     jrael1
6.32 c                   dow       not %eof(jrael1)
6.32 c                   if        jpereg = 'K'
6.32 c                   eval      b_frak = *on
6.32 c                   endif
6.32 c     jrael1_key    reade     jrael1                                 91
6.32 c                   enddo
6.32  *
6.32 c                   if        b_frak = *off
6.32 c                   exfmt     c4winb
6.32 c                   leavesr
6.32 c                   endif
6.31  *
6.31 c                   eval      b_anul = *off
6.31  *
6.32 c     c4tagb        tag
6.31 c                   exfmt     c4win
6.31  *
6.31  * Behandling av funksjonstaster
6.31  *
6.31 c                   if        *inkc or *inkl
6.31 c                   eval      b_anul = *on
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31  *
6.31  * overfører betingelse til frakt
6.31 c                   eval      w_krti = 0
6.32 c*                  eval      w_ifak = 0
6.31  *
6.37 c                   eval      jrael1_eprg = b1prgr
6.31 c                   eval      jrael1_eogr = b1ogrp
6.31 c                   eval      jrael1_ehgr = b1hgrp
6.31 c                   eval      jrael1_eugr = b1ugrp
6.31 c                   eval      jrael1_emod = b1modn
6.31 c                   eval      jrael1_evar = b1vare
6.31 c                   eval      jrael1_evty = b1vtyp
6.31 c                   eval      jrael1_elet = b1lety
6.31 c                   eval      jrael1_ebrs = b1rsts
6.31 c                   eval      jrael1_ebli = b1line
6.31 c     jrael1_key    setll     jrael1
6.31 c     jrael1_key    reade     jrael1
6.31 c                   dow       not %eof(jrael1)
6.31 c                   select
6.32 c*                  when      jpereg = '1'
6.32 c*                  eval(h)   w_ifak = w_ifak - (jpever / 100 * w_ifak)
6.32 c*                  when      jpereg = '2'
6.32 c*                  eval(h)   w_ifak = w_ifak - (jpever / 100)
6.31 c                   when      jpereg = 'K'
6.31 c                   eval      w_krti = w_krti + jpever
6.31 c                   endsl
6.31 c     jrael1_key    reade     jrael1                                 91
6.31 c                   enddo
6.31  *
6.31  *  Danner linje på betingelse
6.31  *
5.32 c                   if        w_krti <> 0
6.31 c                   clear                   jfbelur
6.31  *
6.3x c                   eval      jfbelu_fprg = b1prgr
6.31 c                   eval      jfbelu_fogr = b1ogrp
6.31 c                   eval      jfbelu_fhgr = b1hgrp
6.31 c                   eval      jfbelu_fugr = b1ugrp
6.31 c                   eval      jfbelu_fmod = b1modn
6.31 c                   eval      jfbelu_fvar = b1vare
6.31 c                   eval      jfbelu_fvty = b1vtyp
6.31 c                   eval      jfbelu_flet = b1lety
6.31 c     jfbelu_key    chain     jfbelur                            90
6.31 c                   eval      jpfkrt = w_krti
6.31 c                   time                    jpfeda
6.31 c                   time                    jpfeti
6.31 c                   eval      jpfeus = l_user
6.31 c
6.31 c                   eval      jpfbfi = w_firm
6.31 c                   eval      jpfblo = w_ldor
6.3x c                   eval      jpfprg = b1prgr
6.31 c                   eval      jpfbog = b1ogrp
6.31 c                   eval      jpfbhg = b1hgrp
6.31 c                   eval      jpfbug = b1ugrp
6.31 c                   eval      jpfbmo = b1modn
6.31 c                   eval      jpfbva = b1vare
6.31 c                   eval      jpfbvt = b1vtyp
6.31 c                   eval      jpfbke = b1lety
6.31 c                   eval      jpfbrs = b1rsts
6.31 c                   eval      jpfbli = b1line
6.31 c                   eval      jpfbbe = b1beti
6.31  *
6.31 c                   if        %found(jfbelu)
6.31 c                   update    jfbelur
6.31 c                   else
6.31 c                   eval      jpffir = w_firm
6.31 c                   eval      jpfldo = w_ldor
6.3x c                   eval      jpprgr = jfbelu_fprg
6.31 c                   eval      jpfogr = jfbelu_fogr
6.31 c                   eval      jpfhgr = jfbelu_fhgr
6.31 c                   eval      jpfugr = jfbelu_fugr
6.31 c                   eval      jpfmod = jfbelu_fmod
6.31 c                   eval      jpfvar = jfbelu_fvar
6.31 c                   eval      jpfvty = jfbelu_fvty
6.31 c                   eval      jpflet = jfbelu_flet
6.31 c                   time                    jpfoda
6.31 c                   write     jfbelur
6.31 c                   endif
6.32 c                   endif
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      *in90 = *on
     c                   eval      b_element = *off
      *
     c                   if        c1line <> 0
6.3x c                   eval      jbetlr_prgr = c1prgr
     c                   eval      jbetlr_ogrp = c1ogrp
     c                   eval      jbetlr_hgrp = c1hgrp
     c                   eval      jbetlr_ugrp = c1ugrp
     c                   eval      jbetlr_modn = c1modn
     c                   eval      jbetlr_vare = c1vare
     c                   eval      jbetlr_vtyp = c1vtyp
     c                   eval      jbetlr_lety = c1lety
     c                   eval      jbetlr_rsts = c1rsts
     c                   eval      jbetlr_line = c1line
     c     jbetlr_key    chain     jbetlr                             90
     c                   endif
      *
     c                   if        *in90 = *off
     c                   eval      c1beti = jpbeti
     c                   eval      c2eusr = jpeusr
     c                   eval      c2nobb = 0
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2dato = 0
     c     *dmy          move      jpodat        c2odat
     c     *dmy          move      jpedat        c2edat
     c                   if        jpfdat <> *loval
     c     *dmy          move      jpfdat        c2fdat
     c                   endif
     c                   if        jptdat <> *loval
     c     *dmy          move      jptdat        c2tdat
     c                   endif
     c                   if        jpdato <> *loval
     c     *dmy          move      jpdato        c2dato
     c                   endif
     c                   else
     c                   eval      c2nobb = 0
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2dato = 0
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
      * Kopiering
      *
     c                   if        b1valg = 3
6.3x c                   eval      c1prgr = k1prgr
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1modn = k1modn
     c                   eval      c1vare = k1vare
     c                   eval      c1vtyp = k1vtyp
     c                   eval      c1lety = k1lety
     c                   eval      c1rsts = 1
6.3x c                   if        c1prgr <> *blank
6.3x c                   eval      c1line = k1line
6.3x c                   else
     c                   eval      c1line = 0
6.3x c                   endif
     c                   eval      c1beti = k1beti
6.3x c                   eval      jbetlr_prgr = k1prgr
     c                   eval      jbetlr_ogrp = k1ogrp
     c                   eval      jbetlr_hgrp = k1hgrp
     c                   eval      jbetlr_ugrp = k1ugrp
     c                   eval      jbetlr_modn = k1modn
     c                   eval      jbetlr_vare = k1vare
     c                   eval      jbetlr_vtyp = k1vtyp
     c                   eval      jbetlr_lety = k1lety
     c                   eval      jbetlr_rsts = 1
6.3x c                   if        c1prgr <> *blank
6.3x c                   eval      jbetlr_line = k1line
6.3x c                   else
     c                   eval      jbetlr_line = 0
6.3x c                   endif
     c                   endif
      *
      * Henter beskrivelse av nøkkelfelter
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
      *
     c                   eval      c1lnav = b2lnav
      *
     c                   eval      c1otxt = *blank
6.3x c                   eval      c1ptxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1mte1 = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1ttxt = *blank
     c                   eval      c1lbes = *blank
6.3x  *
6.3x c                   if        c1prgr <> *blank
6.3x c                   eval      ra30l1_dkod = c1prgr
6.3x c     ra30l1_key    chain     ra30l1
6.3x c                   eval      c1ptxt = redtxt
6.3x c                   endif
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   eval      c1otxt = vgotxt
     c                   endif
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      c1htxt = vghtxt
     c                   endif
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   eval      c1utxt = vgutxt
     c                   endif
      *
     c                   if        c1modn <> 0
     c                   eval      jmodl1_modn = c1modn
     c     jmodl1_key    chain     jmodl1
     c                   eval      c1mte1 = jumte1
     c                   endif
      *
     c                   if        c1vtyp <> *blank
     c                   eval      vvtyl1_vtyp = c1vtyp
     c     vvtyl1_key    chain     vvtyl1
     c                   eval      c1ttxt = vavtxt
     c                   endif
      *
     c                   if        c1lety <> 0
     c                   eval      vltpl1_lety = c1lety
     c     vltpl1_key    chain     vltpl1
     c                   eval      c1lbes = vybesk
     c                   endif
      *
     c                   endif
      *
      * Henter vareinformasjon
      *
     c                   if        c1vare <> *blank
6.3d c                   eval      w_lv_nobb = 0
6.3d c/exec sql
6.3d c+ select jvquno
6.3d c+        jvllva
6.3d c+ into   :w_lv_nobb
6.3d c+ from   jvklpf
6.3d c+ where  jvqfir = :w_firm and
6.3d c+        jvqvnr = :c1vare and
6.3d c+        jvqldo = :w_ldor
6.3d c/end-exec
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1
     c                   eval      c1tek1 = jvtek1
6.3d c                   if        w_lv_nobb <> 0
6.3d c                   eval      c2nobb = w_lv_nobb
6.3d c                   else
     c                   eval      c2nobb = jvnobb
6.3d c                   endif
     c                   endif
      *
      * Sperrer felter dersom 'vise'
      *
     c                   if        b1valg = 5
     c                   eval      *in30  = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
6.33  *
6.33  * F18=Viser NOBB informasjon
6.33  *
6.33 c                   if        *inks = *on
6.33 c                   exsr      vis_nobb
6.33 c                   goto      c2tagb
6.33 c                   endif
      *
      * Validering av input
      *
     c                   if        c1beti = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
      *
     c                   if        c2fdat <> 0
     c     *dmy          test (d)                c2fdat                 32
     c                   if        *in32
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
     c                   if        c2tdat <> 0
     c     *dmy          test (d)                c2tdat                 33
     c                   if        *in33
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
     c                   if        c2fdat <> 0 and
     c                             c2tdat <> 0
     c     *dmy          move      c2fdat        w_fdat
     c     *dmy          move      c2tdat        w_tdat
     c                   if        w_fdat > w_tdat
     c                   eval      *in34 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jbetlur
      *
     c                   eval      *in90 = *on
      *
     c                   if        c1line <> 0
6.3x c                   eval      jbetlu_prgr = c1prgr
     c                   eval      jbetlu_ogrp = c1ogrp
     c                   eval      jbetlu_hgrp = c1hgrp
     c                   eval      jbetlu_ugrp = c1ugrp
     c                   eval      jbetlu_modn = c1modn
     c                   eval      jbetlu_vare = c1vare
     c                   eval      jbetlu_vtyp = c1vtyp
     c                   eval      jbetlu_lety = c1lety
     c                   eval      jbetlu_rsts = c1rsts
     c                   eval      jbetlu_line = c1line
     c     jbetlu_key    chain     jbetlur                            90
     c                   endif
      *
     c                   eval      jpbeti = c1beti
      *
     c                   eval      jpfdat = *loval
     c                   if        c2fdat <> 0
     c     *dmy          move      c2fdat        jpfdat
     c                   endif
      *
     c                   eval      jptdat = *loval
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        jptdat
     c                   endif
      *
     c                   eval      jpeusr = l_user
     c                   time                    jpedat
     c                   time                    jpetim
      *
      * Endrer/oppretter rad.
      * Dersom ny rad, opprettes også rad i rabattelemet-register,
      * program kalles for å hente neste ledige linjenummer og
      * c1rsts og c1line tildeles verdi
      * Dersom kopiering kopieres rabattelementer over til ny betingelse
      *
     c                   if        *in90 = *off
     c                   update    jbetlur
     c                   else
      *
     c                   eval      jpfirm = w_firm
     c                   eval      jpldor = w_ldor
6.3x c                   eval      jpprgr = c1prgr
     c                   eval      jpogrp = c1ogrp
     c                   eval      jphgrp = c1hgrp
     c                   eval      jpugrp = c1ugrp
     c                   eval      jpmodn = c1modn
     c                   eval      jpvare = c1vare
     c                   eval      jpvtyp = c1vtyp
     c                   eval      jplety = c1lety
6.3a  * Tar vare på verdier for å sette startverdier ved refresh.
6.3a c                   eval      x_prgr = c1prgr
6.3a c                   eval      x_ogrp = c1ogrp
6.3a c                   eval      x_hgrp = c1hgrp
6.3a c                   eval      x_ugrp = c1ugrp
6.3a c                   eval      x_modn = c1modn
6.3a c                   eval      x_vare = c1vare
6.3a c                   eval      x_vtyp = c1vtyp
6.3a c                   eval      x_lety = c1lety
6.3x  * hvis kopiering av noe til prisgruppe,så settes ikke lokal
6.3x c                   if        b1valg = 3 and
6.3b c                             c1prgr <> *blank and
6.3b c                             k1rsts = 0
6.3x c                   eval      jprsts = 0
6.3x c                   else
     c                   eval      jprsts = 1
6.3x c                   endif
6.3b c                   if        b1valg = 3
6.3b c                   eval      jpakti = 1
6.3b c                   else
     c                   eval      jpakti = 0
6.3b c                   endif
      *
     c                   time                    jpodat
      *
6.3x c                   if        b1valg <> 3
     c                   call      'JP710R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    jpprgr
     c                   parm                    jpogrp
     c                   parm                    jphgrp
     c                   parm                    jpugrp
     c                   parm                    jpmodn
     c                   parm                    jpvare
     c                   parm                    jpvtyp
     c                   parm                    jplety
     c                   parm                    jprsts
     c                   parm                    jpline
     c                   else
6.3x c                   eval      jpline = k1line
6.3x c                   endif
      *
     c                   write     jbetlur
      *
     c                   eval      c1rsts = jprsts
     c                   eval      c1line = jpline
      *
     c                   if        b_oppr = *on
     c                   eval      b_element = *on
     c                   eval      jpefir = w_firm
     c                   eval      jpeldo = w_ldor
6.3x c                   eval      jpeprg = c1prgr
     c                   eval      jpeogr = c1ogrp
     c                   eval      jpehgr = c1hgrp
     c                   eval      jpeugr = c1ugrp
     c                   eval      jpemod = c1modn
     c                   eval      jpevar = c1vare
     c                   eval      jpevty = c1vtyp
     c                   eval      jpelet = c1lety
     c                   eval      jpebrs = c1rsts
     c                   eval      jpebli = c1line
     c                   eval      jperst = 1
     c                   eval      jpelin = 1
     c                   eval      jperae = c1beti
     c                   eval      jpereg = '1'
     c                   eval      jpever = 0
     c                   eval      jpefda = jpfdat
     c                   eval      jpetda = jptdat
     c                   eval      jpeakt = 0
     c                   eval      jpeoda = jpodat
     c                   eval      jpeeda = jpedat
     c                   eval      jpeeti = jpetim
     c                   eval      jpeeus = jpeusr
     c                   write     jraelur
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      b_element = *on
6.3x c                   eval      jrael1_eprg = s_prgr
     c                   eval      jrael1_eogr = s_ogrp
     c                   eval      jrael1_ehgr = s_hgrp
     c                   eval      jrael1_eugr = s_ugrp
     c                   eval      jrael1_emod = s_modn
     c                   eval      jrael1_evar = s_vare
     c                   eval      jrael1_evty = s_vtyp
     c                   eval      jrael1_elet = s_lety
     c                   eval      jrael1_ebrs = s_rsts
     c                   eval      jrael1_ebli = s_line
     c     jrael1_key    setll     jrael1
     c     jrael1_key    reade     jrael1                                 91
     c                   dow       *in91 = *off
6.3x c                   eval      jpeprg = c1prgr
     c                   eval      jpeogr = c1ogrp
     c                   eval      jpehgr = c1hgrp
     c                   eval      jpeugr = c1ugrp
     c                   eval      jpemod = c1modn
     c                   eval      jpevar = c1vare
     c                   eval      jpevty = c1vtyp
     c                   eval      jpelet = c1lety
     c                   eval      jpebrs = c1rsts
     c                   eval      jpebli = c1line
6.3x  * hvis kopiering og prisgruppe er satt, settes ikke rsts
6.3c c                   if        c1prgr <> *blank  and
6.3c c                             c1rsts = 0
6.3x c                   eval      jperst = 0
6.3x c                   else
     c                   eval      jperst = 1
6.3x c                   endif
     c                   eval      jpeakt = 0
     c                   eval      jpeoda = jpodat
     c                   eval      jpeeda = jpedat
     c                   eval      jpeeti = jpetim
     c                   eval      jpeeus = jpeusr
     c                   write     jraelur
     c     jrael1_key    reade     jrael1                                 91
     c                   enddo
     c                   endif
      *
     c                   endif
      *
      * F13=Rabattelementer, eller ny/kopiert betingelse
      *
     c                   if        *inkm = *on or
     c                             b_element = *on
      *
     c                   eval      b_element = *off
      *
     c                   call      'JP110R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    jpprgr
     c                   parm                    jpogrp
     c                   parm                    jphgrp
     c                   parm                    jpugrp
     c                   parm                    jpmodn
     c                   parm                    jpvare
     c                   parm                    jpvtyp
     c                   parm                    jplety
     c                   parm                    jprsts
     c                   parm                    jpline
      *
     c                   if        *inkm = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1beti = jpbeti
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * C3 Behandle bilde  å aktivsere alle betingelser for en leverandør
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     xc3win        begsr
6.30  *
6.30  *  Send Alt
     c                   eval      c3valg = 0
6.30  *
6.30 c     xc3tag        tag
6.30  *
6.30 c     c3taga        tag
6.30 c                   exfmt     c3win
6.30  *
6.30  * Behandling av funksjonstaster
6.30  *
6.30 c                   select
6.30 c                   when      *inkc or *inkl
6.30 c                   goto      end_c3win
6.30 c                   endsl
6.30  *
6.30  * aktivisere alle betingelser for leverandør
6.30  *
6.30 c     jbetl1_key2   setll     jbetl1
6.30 c     jbetl1_key2   reade     jbetl1
6.30 c                   dow       not %eof(jbetl1)
6.30 c                   if        jpvare <> *blank
6.3x c                   eval      jbetlu_prgr = jpprgr
6.30 c                   eval      jbetlu_ogrp = jpogrp
6.30 c                   eval      jbetlu_hgrp = jphgrp
6.30 c                   eval      jbetlu_ugrp = jpugrp
6.30 c                   eval      jbetlu_modn = jpmodn
6.30 c                   eval      jbetlu_vare = jpvare
6.30 c                   eval      jbetlu_vtyp = jpvtyp
6.30 c                   eval      jbetlu_lety = jplety
6.30 c                   eval      jbetlu_rsts = jprsts
6.30 c                   eval      jbetlu_line = jpline
6.30 c     jbetlu_key    chain     jbetlur
6.30 c                   if        %found(jbetlu)
6.30 c                   if        c3valg = 1
6.30 c                   eval      jpakti = 1
6.30 c                   else
6.30 c                   if        c3valg = 0
6.30 c                   eval      jpakti = 0
6.30 c                   endif
6.30 c                   endif
6.30 c                   time                    jpedat
6.30 c                   time                    jpetim
6.30 c                   eval      jpeusr = l_user
6.30 c                   update    jbetlur
6.30 c                   endif
6.40 c                   endif
6.30 c     jbetl1_key2   reade     jbetl1
6.30 c                   enddo
6.34  *
6.34  * sjekker om samme vare har blitt aktivisert flere ganger
6.34  *
6.34 c                   exsr      xc5msg
6.30  *
6.30 c     end_c3win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
6.36 c                   eval      jbetlr_prgr = d1prgr
     c                   eval      jbetlr_ogrp = d1ogrp
     c                   eval      jbetlr_hgrp = d1hgrp
     c                   eval      jbetlr_ugrp = d1ugrp
     c                   eval      jbetlr_modn = d1modn
     c                   eval      jbetlr_vare = d1vare
     c                   eval      jbetlr_vtyp = d1vtyp
     c                   eval      jbetlr_lety = d1lety
     c                   eval      jbetlr_rsts = d1rsts
     c                   eval      jbetlr_line = d1line
     c     jbetlr_key    chain     jbetlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
6.3x c                   eval      jbetlu_prgr = d1prgr
     c                   eval      jbetlu_ogrp = d1ogrp
     c                   eval      jbetlu_hgrp = d1hgrp
     c                   eval      jbetlu_ugrp = d1ugrp
     c                   eval      jbetlu_modn = d1modn
     c                   eval      jbetlu_vare = d1vare
     c                   eval      jbetlu_vtyp = d1vtyp
     c                   eval      jbetlu_lety = d1lety
     c                   eval      jbetlu_rsts = d1rsts
     c                   eval      jbetlu_line = d1line
     c     jbetlu_key    chain     jbetlur                            90
     c                   delete    jbetlur
      *
      * Sletter tilhørende rabattelementer
      *
6.3x c                   eval      jraelu_eprg = d1prgr
     c                   eval      jraelu_eogr = d1ogrp
     c                   eval      jraelu_ehgr = d1hgrp
     c                   eval      jraelu_eugr = d1ugrp
     c                   eval      jraelu_emod = d1modn
     c                   eval      jraelu_evar = d1vare
     c                   eval      jraelu_evty = d1vtyp
     c                   eval      jraelu_elet = d1lety
     c                   eval      jraelu_ebrs = d1rsts
     c                   eval      jraelu_ebli = d1line
     c     jraelu_key    setll     jraelu
     c     jraelu_key    reade     jraelur                                91
     c                   dow       *in91 = *off
     c                   delete    jraelur
     c     jraelu_key    reade     jraelur                                91
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
6.3x c                   eval      s_prgr = k1prgr
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_modn = k1modn
     c                   eval      s_vare = k1vare
     c                   eval      s_vtyp = k1vtyp
     c                   eval      s_lety = k1lety
     c                   eval      s_rsts = k1rsts
     c                   eval      s_line = k1line
      *
      * Fyller kopierings-bildet
      *
6.3x c                   eval      jbetlr_prgr = k1prgr
     c                   eval      jbetlr_ogrp = k1ogrp
     c                   eval      jbetlr_hgrp = k1hgrp
     c                   eval      jbetlr_ugrp = k1ugrp
     c                   eval      jbetlr_modn = k1modn
     c                   eval      jbetlr_vare = k1vare
     c                   eval      jbetlr_vtyp = k1vtyp
     c                   eval      jbetlr_lety = k1lety
     c                   eval      jbetlr_rsts = k1rsts
     c                   eval      jbetlr_line = k1line
     c     jbetlr_key    chain     jbetlr                             90
      *
     c                   eval      k1beti = jpbeti
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Validering av nøkkel
      *
6.3x c                   eval      w_prgr = k1prgr
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_modn = k1modn
     c                   eval      w_vare = k1vare
     c                   eval      w_vtyp = k1vtyp
     c                   eval      w_lety = k1lety
     c                   eval      w_beti = k1beti
      *
     c                   eval      w_otxt = *blank
6.3x c                   eval      w_ptxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_mte1 = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_ttxt = *blank
     c                   eval      w_lbes = *blank
      *
     c                   exsr      sjekk_key
      *
6.3x c                   eval      k1prgr = w_prgr
     c                   eval      k1ogrp = w_ogrp
     c                   eval      k1hgrp = w_hgrp
     c                   eval      k1ugrp = w_ugrp
     c                   eval      k1modn = w_modn
     c                   eval      k1vare = w_vare
     c                   eval      k1vtyp = w_vtyp
     c                   eval      k1lety = w_lety
     c                   eval      k1beti = w_beti
      *
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
6.36  *
6.36 c                   eval      jbetlr_prgr = k1prgr
6.36 c                   eval      jbetlr_ogrp = k1ogrp
6.36 c                   eval      jbetlr_hgrp = k1hgrp
6.36 c                   eval      jbetlr_ugrp = k1ugrp
6.36 c                   eval      jbetlr_modn = k1modn
6.36 c                   eval      jbetlr_vare = k1vare
6.36 c                   eval      jbetlr_vtyp = k1vtyp
6.36 c                   eval      jbetlr_lety = k1lety
6.36 c                   eval      jbetlr_rsts = k1rsts
6.36 c                   eval      jbetlr_line = k1line
6.36 c     jbetlr_key    chain     jbetlr                             90
6.36 c                   if        %found(jbetlr)
6.36 c                   eval      *in46 = *on
6.36 c                   goto      k1taga
6.36 c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1modn = s_modn
     c                   eval      c1vare = s_vare
     c                   eval      c1vtyp = s_vtyp
     c                   eval      c1lety = s_lety
     c                   eval      c1rsts = s_rsts
     c                   eval      c1line = s_line
      *
     c                   exsr      xc2bld
      *
     c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * F1 Behandle bilde for bla-alternativer
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     xf1win        begsr
6.30  *
6.30  *  Send Alt
6.30  *
6.30 c     xf1tag        tag
6.30  *
6.30 c     f1taga        tag
6.30 c                   exfmt     f1win
6.30  *
6.30  * Behandling av funksjonstaster
6.30  *
6.30 c                   select
6.30 c                   when      *inkc or *inkl
6.30 c                   goto      end_f1win
6.30 c                   when      *inkd
6.30 c                   exsr      spØrring
6.30 c                   goto      f1taga
6.30 c                   endsl
6.30  *
6.30  * Validering
6.30  *
6.30 c                   if        f1ogrp = 0 and
6.30 c                             (f1hgrp <> 0 or
6.30 c                              f1ugrp <> 0)
6.30 c                   eval      *in33 = *on
6.30 c                   goto      f1taga
6.30 c                   endif
6.30  *
6.30 c                   if        f1ugrp <> 0 and
6.30 c                             (f1ogrp <> 0 or
6.30 c                              f1hgrp <> 0)
6.30 c                   eval      *in35 = *on
6.30 c                   goto      f1taga
6.30 c                   endif
6.30  *
6.30 c     end_f1win     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c     w_seqe        wheneq    'H'
     c                   read      jbetl2                                 15
     c     w_seqe        wheneq    'U'
     c                   read      jbetl3                                 15
     c     w_seqe        wheneq    'M'
     c                   read      jbetl4                                 15
     c     w_seqe        wheneq    'V'
     c                   read      jbetl5                                 15
     c     w_seqe        wheneq    'T'
     c                   read      jbetl6                                 15
     c     w_seqe        wheneq    'L'
     c                   read      jbetl7                                 15
     c     w_seqe        wheneq    'B'
     c                   read      jbetl8                                 15
6.3x c     w_seqe        wheneq    'O'
6.3x c                   read      jbetla                                 15
     c                   other
     c                   read      jbetl1                                 15
     c                   endsl
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        jpfirm <> p_firm or
     c                             jpldor <> p_ldor
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
6.30  *
6.30  * Filter
6.30  *
6.3x c                   if        f1prgr <> *blank and
6.3x c                             f1prgr <> jpprgr
6.3x c                   iter
6.3x c                   endif
6.30  *
6.30 c                   if        f1ogrp <> 0
6.30  *
6.30 c                   if        f1ogrp <> jpogrp
6.30 c                   iter
6.30 c                   endif
6.30  *
6.30 c                   if        f1hgrp <> 0
6.30  *
6.30 c                   if        f1hgrp <> jphgrp
6.30 c                   iter
6.30 c                   endif
6.30  *
6.30 c                   if        f1ugrp <> 0 and
6.30 c                             f1hgrp <> jphgrp
6.30 c                   iter
6.30 c                   endif
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   if        f1modn <> 0 and
6.30 c                             f1modn <> jpmodn
6.30 c                   iter
6.30 c                   endif
6.35  *
6.35 c                   if        f1vare <> *blank and
6.35 c                             f1vare <> jpvare
6.35 c                   iter
6.35 c                   endif
6.35  *
6.35 c                   if        f1ko01 =  1 and
6.35 c                             jpakti <> 1
6.35 c                   iter
6.35 c                   endif
6.35  *
6.35 c                   if        f1ko01 =  2 and
6.35 c                             jpakti =  1
6.35 c                   iter
6.35 c                   endif
6.35  *
6.3f c                   if        f1lvar <> *blank
6.3f c                   movel     jpvare        w_lv_nobb
6.3d c/exec sql
6.3d c+ select count(*)
6.3d c+ into   :w_teller
6.3d c+ from   jvklpf
6.3d c+ where  jvqfir = :w_firm and
6.3f c+        jvqvnr = :f1lvar and
6.3d c+        jvquno = :w_lv_nobb and
6.3d c+        jvqldo = :w_ldor
6.3d c/end-exec
6.3d c                   if        w_teller = 0
6.3d c                   iter
6.3d c                   endif
6.3d c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jbetl2_hgrp = jphgrp
     c                   when      w_seqe = 'U'
     c                   eval      jbetl3_ugrp = jpugrp
     c                   when      w_seqe = 'M'
     c                   eval      jbetl4_modn = jpmodn
     c                   when      w_seqe = 'V'
     c                   eval      jbetl5_vare = jpvare
     c                   when      w_seqe = 'T'
     c                   eval      jbetl6_vtyp = jpvtyp
     c                   when      w_seqe = 'L'
     c                   eval      jbetl7_lety = jplety
     c                   when      w_seqe = 'B'
     c                   eval      jbetl8_beti = jpbeti
6.3x c                   when      w_seqe = 'O'
6.3x c                   eval      jbetla_ogrp = jpogrp
     c                   other
6.3x c                   eval      jbetl1_prgr = jpprgr
     c                   eval      jbetl1_ogrp = jpogrp
     c                   eval      jbetl1_hgrp = jphgrp
     c                   eval      jbetl1_ugrp = jpugrp
     c                   eval      jbetl1_modn = jpmodn
     c                   eval      jbetl1_vare = jpvare
     c                   eval      jbetl1_vtyp = jpvtyp
     c                   eval      jbetl1_lety = jplety
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1rsts = jprsts
     c                   eval      b1line = jpline
6.3x c                   eval      b1prgr = jpprgr
     c                   eval      b1ogrp = jpogrp
     c                   eval      b1hgrp = jphgrp
     c                   eval      b1ugrp = jpugrp
     c                   eval      b1modn = jpmodn
     c                   eval      b1vare = jpvare
     c                   eval      b1vtyp = jpvtyp
     c                   eval      b1lety = jplety
     c                   eval      b1beti = jpbeti
     c                   eval      b1akti = *blank
     c                   eval      b1rkod = *blank
      *
     c                   if        jpakti = 1
     c                   eval      b1akti = 'A'
     c                   endif
      *
     c                   if        jprsts = 1
     c                   eval      b1rkod = 'L'
     c                   endif
      *
      * Finner nettopris eller rabatt fra rabattelementene
      *
     c                   exsr      finn_fant
6.3d  *
6.3d  * sjekk om logistikk-vare
6.3d  *
6.3d c                   eval      w_jvqvnr = jpvare
6.3d c                   exsr      finn_logivar
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'H'
     c     jbetl2_key    setgt     jbetl2
     c                   readp     jbetl2
     c                   when      w_seqe = 'U'
     c     jbetl3_key    setgt     jbetl3
     c                   readp     jbetl3
     c                   when      w_seqe = 'M'
     c     jbetl4_key    setgt     jbetl4
     c                   readp     jbetl4
     c                   when      w_seqe = 'V'
     c     jbetl5_key    setgt     jbetl5
     c                   readp     jbetl5
     c                   when      w_seqe = 'T'
     c     jbetl6_key    setgt     jbetl6
     c                   readp     jbetl6
     c                   when      w_seqe = 'L'
     c     jbetl7_key    setgt     jbetl7
     c                   readp     jbetl7
     c                   when      w_seqe = 'B'
     c     jbetl8_key    setgt     jbetl8
     c                   readp     jbetl8
6.3x c                   when      w_seqe = 'O'
6.3x c     jbetla_key    setgt     jbetla
6.3x c                   readp     jbetla
     c                   other
     c     jbetl1_key    setgt     jbetl1
     c                   readp     jbetl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   readp     jbetl2
     c                   when      w_seqe = 'U'
     c                   readp     jbetl3
     c                   when      w_seqe = 'M'
     c                   readp     jbetl4
     c                   when      w_seqe = 'V'
     c                   readp     jbetl5
     c                   when      w_seqe = 'T'
     c                   readp     jbetl6
     c                   when      w_seqe = 'L'
     c                   readp     jbetl7
     c                   when      w_seqe = 'B'
     c                   readp     jbetl8
6.3x c                   when      w_seqe = 'O'
6.3x c                   readp     jbetla
     c                   other
     c                   readp     jbetl1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   if        jpfirm <> p_firm or
     c                             jpldor <> p_ldor
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jbetl2_hgrp = jphgrp
     c                   when      w_seqe = 'U'
     c                   eval      jbetl3_ugrp = jpugrp
     c                   when      w_seqe = 'M'
     c                   eval      jbetl4_modn = jpmodn
     c                   when      w_seqe = 'V'
     c                   eval      jbetl5_vare = jpvare
     c                   when      w_seqe = 'T'
     c                   eval      jbetl6_vtyp = jpvtyp
     c                   when      w_seqe = 'L'
     c                   eval      jbetl7_lety = jplety
     c                   when      w_seqe = 'B'
     c                   eval      jbetl8_beti = jpbeti
6.3x c                   when      w_seqe = 'O'
6.3x c                   eval      jbetla_ogrp = jpogrp
     c                   other
6.3x c                   eval      jbetl1_prgr = jpprgr
     c                   eval      jbetl1_ogrp = jpogrp
     c                   eval      jbetl1_hgrp = jphgrp
     c                   eval      jbetl1_ugrp = jpugrp
     c                   eval      jbetl1_modn = jpmodn
     c                   eval      jbetl1_vare = jpvare
     c                   eval      jbetl1_vtyp = jpvtyp
     c                   eval      jbetl1_lety = jplety
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'H'
     c     jbetl2_key    setll     jbetl2
     c                   when      w_seqe = 'U'
     c     jbetl3_key    setll     jbetl3
     c                   when      w_seqe = 'M'
     c     jbetl4_key    setll     jbetl4
     c                   when      w_seqe = 'V'
     c     jbetl5_key    setll     jbetl5
     c                   when      w_seqe = 'T'
     c     jbetl6_key    setll     jbetl6
     c                   when      w_seqe = 'L'
     c     jbetl7_key    setll     jbetl7
     c                   when      w_seqe = 'B'
     c     jbetl8_key    setll     jbetl8
6.3x c                   when      w_seqe = 'O'
6.3x c     jbetla_key    setll     jbetla
     c                   other
     c     jbetl1_key    setll     jbetl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
6.3x  *
6.3x  * Prisgruppe
6.3x  *
6.3x c                   if        w_afld = 'B2PRGR'
6.3x c                   call      'RA530R'
6.3x c                   parm                    w_firm
6.3x c                   parm                    b2prgr
6.3x c                   parm                    w_ptxt
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        w_afld = 'C1PRGR'
6.3x c                   call      'RA530R'
6.3x c                   parm                    w_firm
6.3x c                   parm                    c1prgr
6.3x c                   parm                    c1ptxt
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        w_afld = 'K1PRGR'
6.3x c                   call      'RA530R'
6.3x c                   parm                    w_firm
6.3x c                   parm                    k1prgr
6.3x c                   parm                    w_ptxt
6.3x c                   leavesr
6.3x c                   endif
      *
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   eval      c1hgrp = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1ugrp = 0
     c                   eval      c1utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   eval      k1hgrp = 0
     c                   eval      k1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'F1OGRP'
6.30 c                   eval      f1hgrp = 0
6.30 c                   eval      f1ugrp = 0
6.30 c                   call      'VG510R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    f1ogrp
6.30 c                   parm                    w_otxt
6.30 c                   leavesr
6.30 c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   eval      c1ugrp = 0
     c                   eval      c1utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   eval      k1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'F1HGRP'
6.30 c                   eval      f1ugrp = 0
6.30 c                   call      'VG511R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    f1ogrp
6.30 c                   parm                    f1hgrp
6.30 c                   parm                    w_htxt
6.30 c                   leavesr
6.30 c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'K1UGRP'
6.30 c                   call      'VG512R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    k1ogrp
6.30 c                   parm                    k1hgrp
6.30 c                   parm                    k1ugrp
6.30 c                   parm                    w_utxt
6.30 c                   leavesr
6.30 c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
      * Modulnummer
      *
     c                   if        w_afld = 'B2MODN'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    w_mte1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1MODN'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mte1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1MODN'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    k1modn
     c                   parm                    w_mte1
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'F1MODN'
6.30 c                   call      'JU500R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    f1modn
6.30 c                   parm                    w_mte1
6.30 c                   leavesr
6.30 c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1tek1
     c                   eval      c1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      k1vare = w_vare
     c                   leavesr
     c                   endif
      *
      * Varetype
      *
     c                   if        w_afld = 'B2VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    b2vtyp
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    c1vtyp
     c                   parm                    c1ttxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    k1vtyp
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    c1lbes
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1lety
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for validering av nøkkel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_key     begsr
      *
     c                   eval      b_feil = *off
      *
      * Betingelse må oppgis
      *
     c                   if        w_beti = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Modul
      *
     c                   if        w_modn <> 0
      *
     c                   eval      jmodl1_modn = w_modn
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_ogrp = juogrp
     c                   eval      w_hgrp = juhgrp
     c                   eval      w_ugrp = juugrp
     c                   eval      w_mte1 = jumte1
      *
     c                   endif
      *
      * Vare
      *
     c                   if        w_vare <> *blank
      *
6.3d c                   eval      w_lv_modn = 0
6.3d c/exec sql
6.3d c+ select jvquno, jvmodn
6.3d c+ into   :w_lv_nobb, :w_lv_modn
6.3d c+ from   jvklpf
6.3d c+ inner join jvarpf on jvnobb = jvquno
6.3d c+ where  jvqfir = :w_firm and
6.3d c+        jvqvnr = :c1vare and
6.3d c+        jvqldo = :w_ldor
6.3d c/end-exec
      *
     c                   eval      jvarl1_vare = w_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_ogrp = jvogrp
     c                   eval      w_hgrp = jvhgrp
     c                   eval      w_ugrp = jvugrp
6.3d c                   if        w_lv_modn <> 0
6.3d c                   eval      w_modn = w_lv_modn
6.3d c                   else
     c                   eval      w_modn = jvmodn
6.3d c                   endif
     c                   eval      w_tek1 = jvtek1
      *
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0
      *
     c                   if        w_ogrp = 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_htxt = vghtxt
      *
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
      *
     c                   if        w_ogrp = 0 or
     c                             w_hgrp = 0
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_utxt = vgutxt
      *
     c                   endif
      *
      * Varetype
      *
     c                   if        w_vtyp <> *blank
     c                   eval      vvtyl1_vtyp = w_vtyp
     c     vvtyl1_key    chain     vvtyl1                             40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ttxt = vavtxt
     c                   endif
      *
     c                   if        w_vtyp <> *blank and
     c                             w_vtyp <> 'S'
     c                   eval      *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_lety <> 0
     c                   eval      vltpl1_lety = w_lety
     c     vltpl1_key    chain     vltpl1                             42
     c                   if        *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_lbes = vybesk
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne nettopris eller rabatt fra rabattelementene
      * Betingelsen merkes dersom det finnes aktive rabattelement
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_fant     begsr
      *
     c                   eval      b1fant = *blank
      *
     c                   eval      w_vare = b1vare
     c                   call      'JP720R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
6.3x c                   parm                    b1prgr
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1modn
     c                   parm                    w_vare
     c                   parm                    b1vtyp
     c                   parm                    b1lety
     c                   parm                    b1rsts
     c                   parm                    b1line
     c                   parm                    b1fakt
     c                   parm                    b1ntpr
      *
     c                   if        b_visn = *on
     c                   eval      b1fant = %editw(b1ntpr : '     0 ,  -')
     c                   else
     c                   eval      b1fant = %editw(b1fakt : '  0 ,     -')
     c                   endif
      *
      * Merker betingelsen dersom det finnes aktiviserte rabattelement
      *
6.3x c                   eval      jrael4_eprg = b1prgr
     c                   eval      jrael4_eogr = b1ogrp
     c                   eval      jrael4_ehgr = b1hgrp
     c                   eval      jrael4_eugr = b1ugrp
     c                   eval      jrael4_emod = b1modn
     c                   eval      jrael4_evar = w_vare
     c                   eval      jrael4_evty = w_vtyp
     c                   eval      jrael4_elet = w_lety
     c                   eval      jrael4_ebrs = b1rsts
     c                   eval      jrael4_ebli = b1line
     c                   eval      jrael4_eakt = 1
     c     jrael4_key    chain     jrael4                             91
     c                   if        *in91 = *off
     c                   move      '*'           b1akti
     c                   else
     c                   move      ' '           b1akti
     c                   endif
6.31  *
6.31  * Merkerer hvis linjer er overført til fraktbetingelses tabellen
6.31  *
6.31 c                   eval      jfbel8_fbpg = b1prgr
6.31 c                   eval      jfbel8_fbog = b1ogrp
6.31 c                   eval      jfbel8_fbhg = b1hgrp
6.31 c                   eval      jfbel8_fbug = b1ugrp
6.31 c                   eval      jfbel8_fbmo = b1modn
6.31 c                   eval      jfbel8_fbva = w_vare
6.31 c                   eval      jfbel8_fbvt = w_vtyp
6.31 c                   eval      jfbel8_fbke = w_lety
6.31 c                   eval      jfbel8_fbrs = b1rsts
6.31 c                   eval      jfbel8_fbli = b1line
6.31 c                   eval      jfbel8_fbbe = b1beti
6.31 c     jfbel8_key    chain     jfbel8
6.31 c                   if        %found(jfbel8)
6.31 c                   if        b1akti = '*' or
     c                             b1akti = 'A'
6.31 c                   move      '!'           b1akti
6.31 c                   endif
6.31 c                   move      'F'           b1akti
6.31 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for aktivisering/passivisering av betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     aktiviser_bet begsr
      *
6.3x c                   eval      jbetlu_prgr = b1prgr
     c                   eval      jbetlu_ogrp = b1ogrp
     c                   eval      jbetlu_hgrp = b1hgrp
     c                   eval      jbetlu_ugrp = b1ugrp
     c                   eval      jbetlu_modn = b1modn
     c                   eval      jbetlu_vare = b1vare
     c                   eval      jbetlu_vtyp = b1vtyp
     c                   eval      jbetlu_lety = b1lety
     c                   eval      jbetlu_rsts = b1rsts
     c                   eval      jbetlu_line = b1line
     c     jbetlu_key    chain     jbetlur                            91
     c                   if        *in91 = *on
     c                   leavesr
     c                   endif
      *
      * Oppdaterer betingelse med valg
      *
     c                   if        jpakti = 1
     c                   eval      jpakti = 0
     c                   else
     c                   eval      jpakti = 1
     c                   endif
      *
     c                   update    jbetlur
      *
      * Oppdater subfile
      *
     c                   if        b1valg <> 0
     c                   if        jpakti = 1
     c                   movel     'A'           b1akti
     c                   else
     c                   movel     ' '           b1akti
     c                   endif
     c                   endif
      *
     c                   endsr
      *
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     vis_nobb      begsr
6.33 c
6.33 c                   eval      p_nobb = %char(c2nobb)
6.33 c                   call      'AP633R'
6.33 c                   parm                    p_nobb
6.33  *
6.33 c                   endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Finner evt. varer som er aktivisert mer enn en gang på samme leverandør
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     xc5msg        begsr
6.34  *
6.34 c                   eval      w_anta = 1
6.34 c                   eval      s_vare = *blank
6.34 c     jbetlb_key    setll     jbetlb
6.34 c     jbetlb_key    reade     jbetlb
6.34 c                   dow       not %eof(jbetlb)
6.34 c                   if        jpvare <> *blank and
6.34 c                             jpakti = 1
6.34 c                   if        s_vare =  jpvare
6.34 c                   eval      w_anta = w_anta + 1
6.34 c                   endif
6.34 c                   if        w_anta > 1
6.35 c*                  eval      c5vare = s_vare
6.34 c                   goto      c5taga
6.34 c                   endif
6.34 c                   eval      s_vare = jpvare
6.34 c                   endif
6.34 c     jbetlb_key    reade     jbetlb
6.34 c                   enddo
6.34  *
6.34 c     c5taga        tag
6.34 c                   if        w_anta > 1
6.35 c                   eval      f1vare = s_vare
6.35 c                   exsr      forny
6.34 c                   endif
6.34  *
6.34 c                   endsr
6.3d  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3d  * Finn info om logistikkvare for riktig prisgiver
6.3d  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3d  *
6.3d c     finn_logivar  begsr
6.3d  *
6.3d c                   eval      *in50 = *off
6.3d c                   eval      *in51 = *off
6.3d c
6.3d c                   if        %trim(w_jvqvnr) = ''
6.3d c                   leavesr
6.3d c                   endif
6.3d c
6.3d c                   eval      w_logl = ''
6.3d c                   eval      w_undv = 0
     c                   movel     w_jvqvnr      w_jvquno
6.3ddc/exec sql
6.3d c+                  select jvquno, jvqvnr
6.3d c+                  into :w_undv, :w_logl
6.3d c+                  from jvklpf
6.3d c+                  where (jvquno = :w_jvquno or
6.3d c+                  jvqvnr = :w_jvqvnr)
6.3d c+                  and jvqfir = :w_firm and
6.3d c+                  jvqldo = :w_ldor
6.3d c+                  fetch first 1 rows only
6.3d c/end-exec
6.3d c                   if        w_logl = w_jvqvnr
6.3d c                   eval      *in51 = *on
6.3d c                   elseif    w_undv > 0
6.3d c                   eval      *in50 = *on
6.3d c                   endif
6.3d c
6.3d c                   endsr
6.3d  *
6.3d  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3d  * Subrutine for å vise info
6.3d  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3d  *
6.3d c     xb13win       begsr
6.3d  *
6.3d c                   exfmt     b13win
6.3d  *
6.3d c     end_xb13win   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for les på betingelse
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jbetl1_prgr
     c                   kfld                    jbetl1_ogrp
     c                   kfld                    jbetl1_hgrp
     c                   kfld                    jbetl1_ugrp
     c                   kfld                    jbetl1_modn
     c                   kfld                    jbetl1_vare
     c                   kfld                    jbetl1_vtyp
     c                   kfld                    jbetl1_lety
      *
      * Key for oppslag på betingelse
      *
     c     jbetlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.36 c                   kfld                    jbetlr_prgr
     c                   kfld                    jbetlr_ogrp
     c                   kfld                    jbetlr_hgrp
     c                   kfld                    jbetlr_ugrp
     c                   kfld                    jbetlr_modn
     c                   kfld                    jbetlr_vare
     c                   kfld                    jbetlr_vtyp
     c                   kfld                    jbetlr_lety
     c                   kfld                    jbetlr_rsts
     c                   kfld                    jbetlr_line
      *
      * Key for les av betingelse
      *
     c     jbetl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
      *
      * Key for oppdatering av betingelse
      *
     c     jbetlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jbetlu_prgr
     c                   kfld                    jbetlu_ogrp
     c                   kfld                    jbetlu_hgrp
     c                   kfld                    jbetlu_ugrp
     c                   kfld                    jbetlu_modn
     c                   kfld                    jbetlu_vare
     c                   kfld                    jbetlu_vtyp
     c                   kfld                    jbetlu_lety
     c                   kfld                    jbetlu_rsts
     c                   kfld                    jbetlu_line
      *
      * Key for posisjonering på hovedgruppe
      *
     c     jbetl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl2_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     jbetl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl3_ugrp
      *
      * Key for posisjonering på modulnummer
      *
     c     jbetl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl4_modn
      *
      * Key for posisjonering på varenummer
      *
     c     jbetl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl5_vare
      *
      * Key for posisjonering på varetype
      *
     c     jbetl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl6_vtyp
      *
      * Key for posisjonering på varenummer
      *
     c     jbetl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl7_lety
      *
      * Key for posisjonering på betingelse
      *
     c     jbetl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetl8_beti
6.34  *
6.34  * Key for les av alle varer
6.34  *
6.34 c     jbetlb_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    w_ldor
6.3x  *
6.3x  * Key for les av alle varer
6.3x  *
6.3x c     jbetla_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    w_ldor
6.3x c                   kfld                    jbetla_ogrp
6.39 c                   kfld                    jbetla_hgrp
6.39 c                   kfld                    jbetla_ugrp
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
6.3x  *
6.3x  * Key for oppslag på prisgruppe
6.3x  *
6.3x c     ra30l1_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    ra30l1_dkod
      *
      * Key for les på rabattelementer
      *
     c     jrael1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jrael1_eprg
     c                   kfld                    jrael1_eogr
     c                   kfld                    jrael1_ehgr
     c                   kfld                    jrael1_eugr
     c                   kfld                    jrael1_emod
     c                   kfld                    jrael1_evar
     c                   kfld                    jrael1_evty
     c                   kfld                    jrael1_elet
     c                   kfld                    jrael1_ebrs
     c                   kfld                    jrael1_ebli
      *
     c     jrael4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jrael4_eprg
     c                   kfld                    jrael4_eogr
     c                   kfld                    jrael4_ehgr
     c                   kfld                    jrael4_eugr
     c                   kfld                    jrael4_emod
     c                   kfld                    jrael4_evar
     c                   kfld                    jrael4_evty
     c                   kfld                    jrael4_elet
     c                   kfld                    jrael4_ebrs
     c                   kfld                    jrael4_ebli
     c                   kfld                    jrael4_eakt
      *
      * Key for oppdatering av rabattelementer
      *
     c     jraelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jraelu_eprg
     c                   kfld                    jraelu_eogr
     c                   kfld                    jraelu_ehgr
     c                   kfld                    jraelu_eugr
     c                   kfld                    jraelu_emod
     c                   kfld                    jraelu_evar
     c                   kfld                    jraelu_evty
     c                   kfld                    jraelu_elet
     c                   kfld                    jraelu_ebrs
     c                   kfld                    jraelu_ebli
      *
      * Key for oppslag på varetype
      *
     c     vvtyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvtyl1_vtyp
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
6.31  *
6.31  * Key for oppdatering av frakt betingelse
6.31  *
6.31 c     jfbelu_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    w_ldor
6.31 c                   kfld                    jfbelu_fprg
6.31 c                   kfld                    jfbelu_fogr
6.31 c                   kfld                    jfbelu_fhgr
6.31 c                   kfld                    jfbelu_fugr
6.31 c                   kfld                    jfbelu_fmod
6.31 c                   kfld                    jfbelu_fvar
6.31 c                   kfld                    jfbelu_fvty
6.31 c                   kfld                    jfbelu_flet
6.31  *
6.31  * Key for les av frakt betingelse
6.31  *
6.31 c     jfbel8_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    w_ldor
6.31 c                   kfld                    jfbel8_fbpg
6.31 c                   kfld                    jfbel8_fbog
6.31 c                   kfld                    jfbel8_fbhg
6.31 c                   kfld                    jfbel8_fbug
6.31 c                   kfld                    jfbel8_fbmo
6.31 c                   kfld                    jfbel8_fbva
6.31 c                   kfld                    jfbel8_fbvt
6.31 c                   kfld                    jfbel8_fbke
6.31 c                   kfld                    jfbel8_fbrs
6.31 c                   kfld                    jfbel8_fbli
6.31 c                   kfld                    jfbel8_fbbe
      *
      *
      * Henter firma og leverandør fra parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
     c                   eval      b2ldor = p_ldor
      *
     c                   eval      jlevl1_ldor = p_ldor
     c     jlevl1_key    chain     jlevl1                             90
     c                   eval      b2lnav = jlnavn
      *
     c                   eval      b_visn   = *off
     c                   eval      b2over   = 'Faktor'
6.10 c**                 eval      b2cmdkey = 'F23=Vise nettopriser'
6.10 c                   eval      *in35 = *off
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     jbetl1_key    setll     jbetl1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
