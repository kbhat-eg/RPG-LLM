     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : JA200R
      * Beskrivelse . . : Avrudningsnøkkel, Vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210114 sst  Nytt program
6.30  *       120214 bof  Må fylle ut en gyldig regel.
6.31  *       120214 bof  Feilretting filter
6.32  *       240315 bof  Utvidet med felter avr.nob og avr. enhet
7.xx  * 7.00  010816 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.00  *       080425 ask  Kompilert pga. level check på RA30LR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjav1lr    if   e           k disk    rename(jav1pfr:jav1lrr)
     fjav1lu    uf a e           k disk    rename(jav1pfr:jav1lur)
     fjav1l1    if   e           k disk    rename(jav1pfr:jav1l1r)
     fjav1l2    if   e           k disk    rename(jav1pfr:jav1l2r)
     fjav1l3    if   e           k disk    rename(jav1pfr:jav1l3r)
     fjav1l4    if   e           k disk    rename(jav1pfr:jav1l4r)
     fjav1l5    if   e           k disk    rename(jav1pfr:jav1l5r)
     fjav1l6    if   e           k disk    rename(jav1pfr:jav1l6r)
     fjav2l1    if   e           k disk    rename(jav2pfr:jav2l1r)
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
     fra30lr    if   e           k disk    rename(ra30pfr:ra30lrr)
     fra11pf    if   e           k disk    rename(ra11pfr:ra11pfr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlb    if   e           k disk    rename(vvarpfr:vvarlbr)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fja200d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d jav1lr_ogrp     s                   like(ja1ogr)
     d jav1lr_hgrp     s                   like(ja1hgr)
     d jav1lr_ugrp     s                   like(ja1ugr)
     d jav1lr_ldor     s                   like(ja1lev)
     d jav1lr_modn     s                   like(ja1mod)
     d jav1lr_vare     s                   like(ja1vnr)
     d jav1lu_ogrp     s                   like(ja1ogr)
     d jav1lu_hgrp     s                   like(ja1hgr)
     d jav1lu_ugrp     s                   like(ja1ugr)
     d jav1lu_ldor     s                   like(ja1lev)
     d jav1lu_modn     s                   like(ja1mod)
     d jav1lu_vare     s                   like(ja1vnr)
     d jav1l1_ogrp     s                   like(ja1ogr)
     d jav1l1_hgrp     s                   like(ja1hgr)
     d jav1l1_ugrp     s                   like(ja1ugr)
     d jav1l1_ldor     s                   like(ja1lev)
     d jav1l1_modn     s                   like(ja1mod)
     d jav1l1_vare     s                   like(ja1vnr)
     d jav1l2_hgrp     s                   like(ja1hgr)
     d jav1l2_ugrp     s                   like(ja1ugr)
     d jav1l3_ugrp     s                   like(ja1ugr)
     d jav1l4_ldor     s                   like(ja1lev)
     d jav1l5_modn     s                   like(ja1mod)
     d jav1l6_vare     s                   like(ja1vnr)
     d jav2l1_regl     s                   like(ja2rgl)
     d ra06pf_fkod     s                   like(rafkod)
     d ra11pf_kkod     s                   like(rakkod)
     d ra30lr_dkod     s                   like(redkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jlevl1_ldor     s                   like(jlldor)
     d vvarl1_vare     s                   like(vvvare)
     d vvarlb_nmod     s                   like(vvnmod)
     d venhl1_enhe     s                   like(vaeenh)
     d vltpl1_lety     s                   like(vylety)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_dato_fra      s               d   datfmt(*iso)
     d w_dato_til      s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_txt30         s             30
      *
     d w_mtxt          s             35
      *
     d w_firm          s                   like(ja1fir)
     d w_ogrp          s                   like(ja1ogr)
     d w_hgrp          s                   like(ja1hgr)
     d w_ugrp          s                   like(ja1ugr)
     d w_ldor          s                   like(ja1lev)
     d w_modn          s                   like(ja1mod)
     d w_vare          s                   like(ja1vnr)
     d w_enhe          s                   like(vaeenh)
     d w_lety          s                   like(vylety)
     d w_dtxt          s                   like(redtxt)
     d w_rtxt          s                   like(raftxt)
     d w_ktxt          s                   like(raktxt)
     d w_knav          s                   like(rknavn)
     d w_gtxt          s                   like(vgutxt)
     d w_lnav          s                   like(jlnavn)
     d w_vtxt          s                   like(vvtek1)
     d w_etxt          s                   like(vaetxt)
     d w_ltxt          s                   like(vybesk)
     d w_pnav          s                   like(flnavn)
      *
     d s_ogrp          s                   like(ja1ogr)
     d s_hgrp          s                   like(ja1hgr)
     d s_ugrp          s                   like(ja1ugr)
     d s_ldor          s                   like(ja1lev)
     d s_modn          s                   like(ja1mod)
     d s_vare          s                   like(ja1vnr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Filter
      *
     c                   if        *inka
     c                   exsr      filter
     c                   goto      b2taga
     c                   endif
      *
      * Bytting mellom å vise ordinære rabatter/priser og datostyrte
      * rabatter/priser
      *
     c                   if        *inkx
     c                   if        *in80 = *off
     c                   eval      *in80 = *on
     c                   else
     c                   eval      *in80 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2ldor <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      jav1l1_ogrp = b1ogrp
     c                   eval      jav1l1_hgrp = b1hgrp
     c                   eval      jav1l1_ugrp = b1ugrp
     c     jav1l1_key    setll     jav1l1
     c                   when      w_seqe = 'H'
     c                   eval      jav1l2_hgrp = b1hgrp
     c                   eval      jav1l2_ugrp = b1ugrp
     c     jav1l2_key    setll     jav1l2
     c                   when      w_seqe = 'U'
     c                   eval      jav1l3_ugrp = b1ugrp
     c     jav1l3_key    setll     jav1l3
     c                   when      w_seqe = 'L'
     c                   eval      jav1l4_ldor = b1ldor
     c     jav1l4_key    setll     jav1l4
     c                   when      w_seqe = 'M'
     c                   eval      jav1l5_modn = b1modn
     c     jav1l5_key    setll     jav1l5
     c                   when      w_seqe = 'V'
     c                   eval      jav1l6_vare = b1vare
     c     jav1l6_key    setll     jav1l6
     c                   other
     c                   eval      jav1l1_ogrp = b1ogrp
     c                   eval      jav1l1_hgrp = b1hgrp
     c                   eval      jav1l1_ugrp = b1ugrp
     c                   eval      jav1l1_ldor = b1ldor
     c                   eval      jav1l1_modn = b1modn
     c                   eval      jav1l1_vare = b1vare
     c     jav1l1_key    setll     jav1l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      jav1l1_ogrp = b2ogrp
     c                   eval      jav1l1_hgrp = b2hgrp
     c                   eval      jav1l1_ugrp = b2ugrp
     c                   eval      jav1l1_ldor = b2ldor
     c                   eval      jav1l1_modn = b2modn
     c                   eval      jav1l1_vare = b2vare
     c     jav1l1_key    setll     jav1l1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      jav1l2_hgrp = b2hgrp
     c                   eval      jav1l2_ugrp = b2ugrp
     c     jav1l2_key    setll     jav1l2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      jav1l3_ugrp = b2ugrp
     c     jav1l3_key    setll     jav1l3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      jav1l4_ldor = b2ldor
     c     jav1l4_key    setll     jav1l4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på modulnr
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      jav1l5_modn = b2modn
     c     jav1l5_key    setll     jav1l5
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      jav1l6_vare = b2vare
     c     jav1l6_key    setll     jav1l6
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1ldor = b1ldor
     c                   eval      k1modn = b1modn
     c                   eval      k1vare = b1vare
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1ldor = b1ldor
     c                   eval      d1modn = b1modn
     c                   eval      d1vare = b1vare
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ogrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1ldor = 0
     c                   eval      c1modn = 0
     c                   eval      c1vare = *blank
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1ogrp = 0 and
     c                             c1hgrp = 0 and
     c                             c1ugrp = 0 and
     c                             c1ldor = 0 and
     c                             c1vare = *blank and
     c                             c1modn = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_ldor = c1ldor
     c                   eval      w_modn = c1modn
     c                   eval      w_vare = c1vare
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
     c                   eval      jav1lr_ogrp = c1ogrp
     c                   eval      jav1lr_hgrp = c1hgrp
     c                   eval      jav1lr_ugrp = c1ugrp
     c                   eval      jav1lr_ldor = c1ldor
     c                   eval      jav1lr_modn = c1modn
     c                   eval      jav1lr_vare = c1vare
     c     jav1lr_key    chain     jav1lr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ogrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1ldor = 0
     c                   eval      c1vare = *blank
     c                   eval      c1modn = 0
      *
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1lnav = *blank
     c                   eval      c1mtxt = *blank
     c                   eval      c1vtxt = *blank
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_oppr = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      jav1lr_ogrp = c1ogrp
     c                   eval      jav1lr_hgrp = c1hgrp
     c                   eval      jav1lr_ugrp = c1ugrp
     c                   eval      jav1lr_ldor = c1ldor
     c                   eval      jav1lr_modn = c1modn
     c                   eval      jav1lr_vare = c1vare
     c     jav1lr_key    chain     jav1lr                             90
      *
     c                   if        *in90 = *off
      *
     c                   eval      c2regl = ja1rgl
     c                   eval      jav2l1_regl = ja1rgl
     c     jav2l1_key    chain     jav2l1
     c                   if        %found(jav2l1)
     c                   eval      c2rtxt = ja2txt
     c                   else
     c                   eval      c2rtxt = *blank
     c                   endif
     c                   eval      c2eusr = ja1eus
6.32 c                   eval      c21nob = ja1nob
6.32 c                   eval      c21enh = ja1enh
      *
     c     *dmy          move      ja1oda        c2odat
     c     *dmy          move      ja1eda        c2edat
      *
     c                   else
      *
6.32 c                   eval      c21nob = 0
6.32 c                   eval      c21enh = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
      *
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1ldor = k1ldor
     c                   eval      c1modn = k1modn
     c                   eval      c1vare = k1vare
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   endif
      *sven
      *
      * Validering av input
      *
      *  Regel
6.30 c                   if        c2regl = 0
6.30 c                   eval      *in48 = *on
6.30 c                   goto      c2tagb
6.30 c                   endif
      *
     c     tstrgl        tag
     c                   if        c2regl <> 0
     c                   eval      jav2l1_regl = c2regl
     c     jav2l1_key    chain     jav2l1
     c                   if        not %found(jav2l1)
     c                   exfmt     nyregel
     c                   call      'JA201R  '
     c                   parm                    w_firm
     c                   parm                    c2regl
     c                   parm                    c2rtxt
     c                   if        *inkc = *on
     c                   goto      c2tagb
     c                   endif
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Oppdatering av rabattmatrise
      *
     c                   clear                   jav1lur
      *
     c                   eval      jav1lu_ogrp = c1ogrp
     c                   eval      jav1lu_hgrp = c1hgrp
     c                   eval      jav1lu_ugrp = c1ugrp
     c                   eval      jav1lu_ldor = c1ldor
     c                   eval      jav1lu_modn = c1modn
     c                   eval      jav1lu_vare = c1vare
     c     jav1lu_key    chain     jav1lur                            90
      *
     c                   eval      ja1rgl = c2regl
6.32 c                   eval      ja1nob = c21nob
6.32 c                   eval      ja1enh = c21enh
     c                   eval      ja1eus = l_user
      *
     c                   time                    ja1eti
     c                   time                    ja1eda
      *
     c                   if        *in90 = *off
     c                   time                    ja1eda
     c                   time                    ja1eti
     c                   eval      ja1eus = l_user
     c                   update    jav1lur
     c                   else
     c                   eval      ja1fir = w_firm
     c                   eval      ja1ogr = c1ogrp
     c                   eval      ja1hgr = c1hgrp
     c                   eval      ja1ugr = c1ugrp
     c                   eval      ja1lev = c1ldor
     c                   eval      ja1mod = c1modn
     c                   eval      ja1vnr = c1vare
     c                   time                    ja1oda
     c                   eval      ja1ous = l_user
     c                   time                    ja1eda
     c                   time                    ja1eti
     c                   eval      ja1eus = l_user
     c                   write     jav1lur
     c                   endif
      *
      * Oppdater subfile
      *
      *
     c                   if        b_oppr = *on or
6.30 c                             b1valg = 2  or
6.30 c                             b1valg = 3
6.30 c                   eval      b1regl = ja1rgl
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jav1lr_ogrp = d1ogrp
     c                   eval      jav1lr_hgrp = d1hgrp
     c                   eval      jav1lr_ugrp = d1ugrp
     c                   eval      jav1lr_ldor = d1ldor
     c                   eval      jav1lr_modn = d1modn
     c                   eval      jav1lr_vare = d1vare
     c     jav1lr_key    chain     jav1lr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rabattmatrise-nivå
      *
     c                   eval      jav1lu_ogrp = d1ogrp
     c                   eval      jav1lu_hgrp = d1hgrp
     c                   eval      jav1lu_ugrp = d1ugrp
     c                   eval      jav1lu_ldor = d1ldor
     c                   eval      jav1lu_modn = d1modn
     c                   eval      jav1lu_vare = d1vare
     c     jav1lu_key    chain     jav1lur                            90
     c                   delete    jav1lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_ldor = k1ldor
     c                   eval      s_modn = k1modn
     c                   eval      s_vare = k1vare
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jav1lr_ogrp = k1ogrp
     c                   eval      jav1lr_hgrp = k1hgrp
     c                   eval      jav1lr_ugrp = k1ugrp
     c                   eval      jav1lr_ldor = k1ldor
     c                   eval      jav1lr_modn = k1modn
     c                   eval      jav1lr_vare = k1vare
     c     jav1lr_key    chain     jav1lr                             90
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jav1lr_ogrp = k1ogrp
     c                   eval      jav1lr_hgrp = k1hgrp
     c                   eval      jav1lr_ugrp = k1ugrp
     c                   eval      jav1lr_ldor = k1ldor
     c                   eval      jav1lr_modn = k1modn
     c                   eval      jav1lr_vare = k1vare
     c     jav1lr_key    chain     jav1lr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_ldor = k1ldor
     c                   eval      w_modn = k1modn
     c                   eval      w_vare = k1vare
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1ldor = s_ldor
     c                   eval      c1modn = s_modn
     c                   eval      c1vare = s_vare
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   read      jav1l1
     c                   when      w_seqe = 'H'
     c                   read      jav1l2
     c                   when      w_seqe = 'U'
     c                   read      jav1l3
     c                   when      w_seqe = 'L'
     c                   read      jav1l4
     c                   when      w_seqe = 'M'
     c                   read      jav1l5
     c                   when      w_seqe = 'V'
     c                   read      jav1l6
     c                   other
     c                   read      jav1l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ja1fir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        f1regl <> 0 and
     c                             ja1rgl <> f1regl
     c                   iter
     c                   endif
      *
     c                   if        f1levr <> 0 and
     c                             ja1lev <> f1levr
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      jav1l1_ogrp = ja1ogr
     c                   eval      jav1l1_hgrp = ja1hgr
     c                   eval      jav1l1_ugrp = ja1ugr
     c                   when      w_seqe = 'H'
     c                   eval      jav1l2_hgrp = ja1hgr
     c                   eval      jav1l2_ugrp = ja1ugr
     c                   when      w_seqe = 'U'
     c                   eval      jav1l3_ugrp = ja1ugr
     c                   when      w_seqe = 'L'
     c                   eval      jav1l4_ldor = ja1lev
     c                   when      w_seqe = 'M'
     c                   eval      jav1l5_modn = ja1mod
     c                   when      w_seqe = 'V'
     c                   eval      jav1l6_vare = ja1vnr
     c                   other
     c                   eval      jav1l1_ogrp = ja1ogr
     c                   eval      jav1l1_hgrp = ja1hgr
     c                   eval      jav1l1_ugrp = ja1ugr
     c                   eval      jav1l1_ldor = ja1lev
     c                   eval      jav1l1_modn = ja1mod
     c                   eval      jav1l1_vare = ja1vnr
     c                   endsl
      *
     c                   eval      b1ogrp = ja1ogr
     c                   eval      b1hgrp = ja1hgr
     c                   eval      b1ugrp = ja1ugr
     c                   eval      b1ldor = ja1lev
     c                   eval      b1modn = ja1mod
     c                   eval      b1vare = ja1vnr
6.32 c                   eval      b11enh = ja1enh
6.32 c                   if        ja1nob = 1
6.32 c                   eval      b11nob = 'J'
6.32 c                   else
6.32 c                   eval      b11nob = 'N'
6.32 c                   endif
      *
     c                   eval      jav2l1_regl = ja1rgl
     c     jav2l1_key    chain     jav2l1
     c                   if        %found(jav2l1)
     c                   eval      b1regl = ja1rgl
     c                   eval      b1text = ja2txt
     c                   else
     c                   eval      b1regl = ja1rgl
     c                   eval      b1text = *blank
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'O'
     c     jav1l1_key    setgt     jav1l1
     c                   readp     jav1l1
     c                   when      w_seqe = 'H'
     c     jav1l2_key    setgt     jav1l2
     c                   readp     jav1l2
     c                   when      w_seqe = 'U'
     c     jav1l3_key    setgt     jav1l3
     c                   readp     jav1l3
     c                   when      w_seqe = 'L'
     c     jav1l4_key    setgt     jav1l4
     c                   readp     jav1l4
     c                   when      w_seqe = 'M'
     c     jav1l5_key    setgt     jav1l5
     c                   readp     jav1l5
     c                   when      w_seqe = 'V'
     c     jav1l6_key    setgt     jav1l6
     c                   readp     jav1l6
     c                   other
     c     jav1l1_key    setgt     jav1l1
     c                   readp     jav1l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   readp     jav1l1
     c                   when      w_seqe = 'H'
     c                   readp     jav1l2
     c                   when      w_seqe = 'U'
     c                   readp     jav1l3
     c                   when      w_seqe = 'L'
     c                   readp     jav1l4
     c                   when      w_seqe = 'M'
     c                   readp     jav1l5
     c                   when      w_seqe = 'V'
     c                   readp     jav1l6
     c                   other
     c                   readp     jav1l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ja1fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        f1regl <> 0 and
     c                             ja1rgl <> f1regl
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      jav1l1_ogrp = ja1ogr
     c                   eval      jav1l1_hgrp = ja1hgr
     c                   eval      jav1l1_ugrp = ja1ugr
     c                   when      w_seqe = 'H'
     c                   eval      jav1l2_hgrp = ja1hgr
     c                   eval      jav1l2_ugrp = ja1ugr
     c                   when      w_seqe = 'U'
     c                   eval      jav1l3_ugrp = ja1ugr
     c                   when      w_seqe = 'L'
     c                   eval      jav1l4_ldor = ja1lev
     c                   when      w_seqe = 'M'
     c                   eval      jav1l5_modn = ja1mod
     c                   when      w_seqe = 'V'
     c                   eval      jav1l6_vare = ja1vnr
     c                   other
     c                   eval      jav1l1_ogrp = ja1ogr
     c                   eval      jav1l1_hgrp = ja1hgr
     c                   eval      jav1l1_ugrp = ja1ugr
     c                   eval      jav1l1_ldor = ja1lev
     c                   eval      jav1l1_modn = ja1mod
     c                   eval      jav1l1_vare = ja1vnr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'O'
     c     jav1l1_key    setll     jav1l1
     c                   when      w_seqe = 'H'
     c     jav1l2_key    setll     jav1l2
     c                   when      w_seqe = 'U'
     c     jav1l3_key    setll     jav1l3
     c                   when      w_seqe = 'L'
     c     jav1l4_key    setll     jav1l4
     c                   when      w_seqe = 'M'
     c     jav1l5_key    setll     jav1l5
     c                   when      w_seqe = 'V'
     c     jav1l6_key    setll     jav1l6
     c                   other
     c     jav1l1_key    setll     jav1l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Avrundingsregel
      *
     c                   if        w_afld = 'C2REGL'
     c                   call      'JA201R'
     c                   parm                    w_firm
     c                   parm                    c2regl
     c                   parm                    c2rtxt
     c                   leavesr
     c                   endif
      *
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'JL500R'
     c*                  parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'JL500R'
     c*                  parm                    w_firm
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'JL500R'
     c*                  parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
      * Modulnr
      *
     c                   if        w_afld = 'B2MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    k1modn
     c                   parm                    w_txt30
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_txt30
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1vtxt
     c                   eval      c1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_txt30
     c                   eval      k1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Filter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     filter        begsr
      *
     c     f1tagb        tag
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'F1REGL'
     c                   call      'JA201R'
     c                   parm                    w_firm
     c                   parm                    f1regl
     c                   parm                    f1rtxt
     c                   goto      f1tagb
     c                   endif
      *
     c                   if        w_afld = 'F1LEVR'
     c                   call      'JL500R'
     c                   parm                    f1levr
     c                   parm                    f1lnav
     c                   goto      f1tagb
     c                   endif
      *
     c                   endif
      *
      * Validering
      *
     c                   if        f1regl <> 0
     c                   eval      jav2l1_regl = f1regl
     c     jav2l1_key    chain     jav2l1                             31
     c                   if        *in31 = *on
     c                   goto      f1tagb
     c                   endif
     c                   eval      f1rtxt = ja2txt
     c                   endif
      *
      *
      * Henter verdier til liste-bildet
      *
     c                   eval      b2frgl = f1regl
     c                   eval      b2flev = f1levr
      *
      * Nullstiller og avbryter dersom regel ikke er utfylt
      *
     c                   if        f1regl = 0
     c                   eval      f1rtxt = *blank
     c                   goto      end_filter
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. filter
      *
     c     end_filter    tag
      *
     c                   eval      *in22 = *off
      *
     c                   eval      w_seqe = *blank
      *
6.31 c                   eval      jav1l1_ogrp = 0
6.31 c                   eval      jav1l1_hgrp = 0
6.31 c                   eval      jav1l1_ugrp = 0
6.31 c                   eval      jav1l1_ldor = 0
6.31 c                   eval      jav1l1_modn = 0
6.31 c                   eval      jav1l1_vare = *blank
     c     jav1l1_key    setll     jav1l1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_dtxt = *blank
     c                   eval      w_rtxt = *blank
     c                   eval      w_ktxt = *blank
     c                   eval      w_knav = *blank
     c                   eval      w_pnav = *blank
     c                   eval      w_gtxt = *blank
     c                   eval      w_lnav = *blank
     c                   eval      w_mtxt = *blank
     c                   eval      w_vtxt = *blank
     c                   eval      w_etxt = *blank
     c                   eval      w_ltxt = *blank
      *
     c                   if        w_modn <> 0
     c                   if        w_ogrp <> 0 or
     c                             w_ldor <> 0 or
     c                             w_vare <> *blank
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        w_vare <> *blank
     c                   if        w_ogrp <> 0 or
     c                             w_ldor <> 0 or
     c                             w_modn <> 0
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             41
     c                   if        *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_gtxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             42
     c                   if        *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_gtxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             43
     c                   if        *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_gtxt = vgutxt
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_ldor <> 0
     c                   eval      jlevl1_ldor = w_ldor
     c     jlevl1_key    chain     jlevl1                             44
     c                   if        *in44 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_lnav = jlnavn
     c                   endif
      *
      * Modul
      *
     c                   if        w_modn <> 0
     c                   eval      vvarlb_nmod = w_modn
     c     vvarlb_key    chain     vvarlb                             45
     c                   if        *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    w_modn
     c                   parm                    w_mtxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_vare <> *blank
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      w_vtxt = vvtek1
     c                   else
     c                   eval      jvarl1_vare = w_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      *in46 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_vtxt = jvtek1
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1lnav = *blank
     c                   eval      c1vtxt = *blank
     c                   eval      c1mtxt = *blank
      *
      *
      * Overgruppe
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      c1otxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      c1htxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c1utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Leverandør
      *
     c                   if        c1ldor <> 0
     c                   eval      jlevl1_ldor = c1ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      c1lnav = jlnavn
     c                   endif
     c                   endif
      *
      * Modul
      *
     c                   if        c1modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mtxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        c1vare <> *blank
     c                   eval      vvarl1_vare = c1vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      c1vtxt = vvtek1
     c                   else
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      c1vtxt = jvtek1
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på avrundings nøkkel
      *
     c     jav1lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1lr_ogrp
     c                   kfld                    jav1lr_hgrp
     c                   kfld                    jav1lr_ugrp
     c                   kfld                    jav1lr_ldor
     c                   kfld                    jav1lr_modn
     c                   kfld                    jav1lr_vare
      *
      * Key for oppdatering av avrundings nøkkel
      *
     c     jav1lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1lu_ogrp
     c                   kfld                    jav1lu_hgrp
     c                   kfld                    jav1lu_ugrp
     c                   kfld                    jav1lu_ldor
     c                   kfld                    jav1lu_modn
     c                   kfld                    jav1lu_vare
      *
      * Key for posisjonering på overgruppe
      *
     c     jav1l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1l1_ogrp
     c                   kfld                    jav1l1_hgrp
     c                   kfld                    jav1l1_ugrp
     c                   kfld                    jav1l1_ldor
     c                   kfld                    jav1l1_modn
     c                   kfld                    jav1l1_vare
      *
      * Key for posisjonering på hovedgruppe
      *
     c     jav1l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1l2_hgrp
     c                   kfld                    jav1l2_ugrp
      *
      * Key for posisjonering på undergruppe
      *
     c     jav1l3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1l3_ugrp
      *
      * Key for posisjonering på Leverandør
      *
     c     jav1l4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1l4_ldor
      *
      * Key for posisjonering på modulnummer
      *
     c     jav1l5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1l5_modn
      *
      * Key for posisjonering på varenummer
      *
     c     jav1l6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav1l6_vare
     c     jav2l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jav2l1_regl
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c**                 kfld                    w_firm
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlb_nmod
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     jav1l1_key    setll     jav1l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
