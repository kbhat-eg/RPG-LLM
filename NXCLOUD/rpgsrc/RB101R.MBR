     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : rb101R
      * Beskrivelse . . : Gammel/Ny momskode    Vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      *Ref Ver.   Dato Sig. Endringsbeskrivelse
      *--- ---- ------ ---  --------------------------------------------
7.xx  *K00 7.00 031120 sst  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frb00l1    if   e           k disk    rename(rb00pfr:rb00l1r)
     frb00l2    if   e           k disk    rename(rb00pfr:rb00l2r)
     frb00lr    if   e           k disk    rename(rb00pfr:rb00lrr)
     frb00lu    uf a e           k disk    rename(rb00pfr:rb00lur)
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
      *
     frb101d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d w_type          s             10
     d rb00l1_gkod     s              6
     d rb00l2_vtxt     s                   like(r00txt)
     d rb00lr_gkod     s              6
     d rb00lu_gkod     s              6
     d ra05l1_akod     s                   like(raekod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(r00fir)
      *
     d s_gkod          s              6
      *
     d p_firm          s                   like(raefir)
     d p_ekod          s                   like(raekod)
     d p_etxt          s                   like(raetxt)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2gkod <> *blank or
     c                             b2vtxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rb00l2_vtxt = b1vtxt
     c     rb00l2_key    setll     rb00l2
     c                   other
     c                   eval      rb00l1_gkod = b1gkod
     c     rb00l1_key    setll     rb00l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Bilagskoder
      *
     c                   if        b2gkod <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      rb00l1_gkod = b2gkod
     c     rb00l1_key    setll     rb00l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2vtxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rb00l2_vtxt = b2vtxt
     c     rb00l2_key    setll     rb00l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2gkod = *blank
     c                   eval      b2vtxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1gkod = b1gkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1gkod = b1gkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1gkod = b1gkod
     c                   eval      *in34 = '1'
     c                   exsr      xc2bld
     c                   eval      *in34 = '0'
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1gkod = b2gkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   eval      p_firm = l_firm
     c                   eval      p_ekod = *blank
     c                   eval      p_etxt = *blank
     c                   call      'RA505R'
     c                   parm                    p_firm
     c                   parm                    p_ekod
     c                   parm                    p_etxt
     c                   move      p_ekod        c1gkod
     c                   goto      c1taga
     c                   endif
     c                   if        c1gkod = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rb00lr_gkod = c1gkod
     c     rb00lr_key    chain     rb00lr
     c                   if        %found
     c                   eval      *in31 = '1'
     c                   exsr      xc1msg
     c                   goto      c1taga
     c                   endif
      *
     c                   move      c1gkod        ra05l1_akod
     c     ra05l1_key    chain     ra05l1
     c                   if        not %found
     c                   eval      *in32 = '1'
     c                   exsr      xc1msg
     c                   goto      c1taga
     c                   endif
      *
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1gkod = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2gkod = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rb00lr_gkod = c1gkod
     c     rb00lr_key    chain     rb00lr
      *
     c                   if        %found
     c                   eval      c2vtxt = r00txt
     c                   eval      c2nkod = r00nko
     c                   eval      c2eusr = r00usr
     c     *dmy          move      r00dat        c2edat
     c                   else
     c                   eval      c2nkod = *blank
     c                   eval      c2vtxt = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
      * Validering
     c                   move      c1gkod        ra05l1_akod
     c     ra05l1_key    chain     ra05l1
     c                   if        %found
     c                   eval      c2vtxt = raetxt
     c                   endif
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
     c                   if        c2nkod = *blank
     c                   eval      *in33 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      *
      * Oppdatering
      *
     c                   clear                   rb00lur
      *
     c                   eval      rb00lu_gkod = c1gkod
     c     rb00lu_key    chain     rb00lur
      *
     c                   eval      r00txt = c2vtxt
     c                   eval      r00nko = c2nkod
      *
     c                   time                    r00dat
     c                   time                    r00tim
     c                   eval      r00usr = l_user
      *
     c                   if        %found
     c                   update    rb00lur
     c                   else
     c                   eval      r00fir = w_firm
     c                   eval      r00typ = w_type
     c                   eval      r00kod = c1gkod
     c                   eval      r00nko = c2nkod
     c                   eval      r00txt = c2vtxt
     c                   write     rb00lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1vtxt = r00txt
     c                   eval      b1nkod = r00nko
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rb00lr_gkod = d1gkod
     c     rb00lr_key    chain     rb00lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rb00lu_gkod = d1gkod
     c     rb00lu_key    chain     rb00lur
      *
     c                   delete    rb00lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rb00l2
     c                   other
     c                   read      rb00l1
     c                   endsl
      *
     c                   if        %eof or
     c                             r00fir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *  Bare type 'MVAKODE'
     c                   if        r00typ <> w_type
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rb00l2_vtxt = r00txt
     c                   other
     c                   eval      rb00l1_gkod = r00kod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1gkod = r00kod
     c                   eval      b1nkod = r00nko
     c                   eval      b1vtxt = r00txt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     rb00l2_key    setgt     rb00l2
     c                   readp     rb00l2
     c                   other
     c     rb00l1_key    setgt     rb00l1
     c                   readp     rb00l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rb00l2
     c                   other
     c                   readp     rb00l1
     c                   endsl
      *
     c                   if        %eof or
     c                             r00fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rb00l2_vtxt = r00txt
     c                   other
     c                   eval      rb00l1_gkod = r00kod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rb00l2_key    setll     rb00l2
     c                   other
     c     rb00l1_key    setll     rb00l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på bilagskode
      *
     c     rb00l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_type
     c                   kfld                    rb00l1_gkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     rb00l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_type
     c                   kfld                    rb00l2_vtxt
      *
      * Key for les på bilagskode
      *
     c     rb00lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_type
     c                   kfld                    rb00lr_gkod
      *
      * Key for oppdatering av gammel/ny bilagskode
      *
     c     rb00lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_type
     c                   kfld                    rb00lu_gkod
      *
      *
      * Key for posisjonering på bilagskode
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_akod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_type = 'MVAKODE'
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rb00l1_gkod = *blank
     c     rb00l1_key    setll     rb00l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
