     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VC100R
      * Beskrivelse . . : Varesortiment-hode, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270599 HKO  Nytt program
      *       210404 HKO  Lagt inn sortimentstype
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.11  *       050110 bof  Test om dato er <> *loval
6.20  *       041110 bof  Utvidet med lager på sortiment
6.21  *       151110 bof  Utvidet med fra og tildato på sortiment
6.22  *       261110 bof  Utvidet med utskrift av kontroll-liste
6.23  *       031111 bof  Det er bestemt at fra/til dato skal være uke
6.24  *       191113 bsa  Justert i skjermfil pga jacada problemer
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.31  *       161116 bof  Utvidet med inlr
7.xx  * 7.00  240216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       190220 bsa  Ny type varesortiment - prisfokusvarer 'F'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
     fvshel2    if   e           k disk    rename(vshepfr:vshel2r)
     fvshelr    if   e           k disk    rename(vshepfr:vshelrr)
     fvshelu    uf a e           k disk    rename(vshepfr:vshelur)
     fvsdtlr    if   e           k disk    rename(vsdtpfr:vsdtlrr)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
      *
     fvc100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vshel1_vsor     s                   like(vcvsor)
     d vshel2_besk     s                   like(vcbesk)
     d vshelr_vsor     s                   like(vcvsor)
6.20 d vshelr_lage     s                   like(vclage)
     d vshelu_vsor     s                   like(vcvsor)
6.20 d vshelu_lage     s                   like(vclage)
     d vsdtlr_vsor     s                   like(vdvsor)
6.20 d vsdtlr_lage     s                   like(vdlage)
     d vsdtlu_vsor     s                   like(vdvsor)
6.20 d vsdtlu_lage     s                   like(vdlage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
6.20 d w_retk          s              1
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vcfirm)
     d w_fdat          s                   like(vcodat)
     d w_tdat          s                   like(vcodat)
6.31 d w_inlr          s              1    inz(*off)
      *
     d s_vsor          s                   like(vcvsor)
6.20 d s_lage          s                   like(vclage)
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkj
     c                   call      'VD600R'
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vsor <> *blank or
     c                             b2besk <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vshel2_besk = b1besk
     c     vshel2_key    setll     vshel2
     c                   other
     c                   eval      vshel1_vsor = b1vsor
     c     vshel1_key    setll     vshel1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på sortiment
      *
     c                   if        b2vsor <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      vshel1_vsor = b2vsor
     c     vshel1_key    setll     vshel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på beskrivelse
      *
     c                   if        b2besk <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vshel2_besk = b2besk
     c     vshel2_key    setll     vshel2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vsor = *blank
     c                   eval      b2besk = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vsor = b1vsor
6.20 c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1vsor = b1vsor
6.20 c                   eval      k1lage = b1lage
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1vsor = b1vsor
6.20 c                   eval      d1lage = b1lage
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vsor = b1vsor
6.20 c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av sortiment-linje
      *
     c                   if        b1valg = 7
     c                   call      'VD100R'
     c                   parm                    w_firm
     c                   parm                    b1vsor
6.20 c                   parm                    b1lage
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vsor = b2vsor
6.20 c                   eval      c1lage = 0
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1vsor = *blank
     c                   goto      end_c1bld
     c                   endif
      *
6.20 c                   if        c1lage <> 0
6.20 c                   call      'RS710R'
6.20 c                   parm                    w_retk
6.20 c                   parm                    c1lage
6.20 c                   parm                    c1ltxt
6.20 c                   if        w_retk <> *blank
6.20 c                   eval      *in32 = *on
6.20 c                   goto      c1taga
6.20 c                   endif
6.20 c                   endif
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vshelr_vsor = c1vsor
6.20 c                   eval      vshelr_lage = c1lage
     c     vshelr_key    chain     vshelr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1vsor = *blank
6.20 c                   eval      c1lage = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2vsor = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vshelr_vsor = c1vsor
6.20 c                   eval      vshelr_lage = c1lage
     c     vshelr_key    chain     vshelr                             90
      *
     c                   if        *in90 = *off
6.20  * finner lagertekst
6.20 c                   call      'RS710R'
6.20 c                   parm                    w_retk
6.20 c                   parm                    c1lage
6.20 c                   parm                    c1ltxt
     c                   if        w_retk <> *blank
     c                   eval      c1ltxt = *blank
     c                   endif
     c                   eval      c2besk = vcbesk
     c                   eval      c2type = vctype
6.23 c                   eval      c2fuke = vcfuke
6.23 c                   eval      c2tuke = vctuke
6.23 c                   if        c2fuke <> 0 and
6.23 c                             c2tuke <> 0
6.23 c                   exsr      finn_dato
6.23 c     *dmy          move      w_fdat        c2fdat
6.23 c     *dmy          move      w_tdat        c2tdat
     c                   else
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
6.23 c                   endif
     c                   eval      c2eusr = vceusr
6.10 c                   if        vcodat <> *loval
     c     *dmy          move      vcodat        c2odat
6.10 c                   else
6.10 c                   eval      c2odat = 0
6.10 c                   endif
6.10 c                   if        vcedat <> *loval
     c     *dmy          move      vcedat        c2edat
6.10 c                   else
6.10 c                   eval      c2edat = 0
6.10 c                   endif
     c                   else
     c                   eval      c2besk = *blank
     c                   eval      c2type = *blank
6.21 c                   eval      c2fdat = 0
6.21 c                   eval      c2tdat = 0
6.23 c                   eval      c2fuke = 0
6.23 c                   eval      c2tuke = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1vsor = k1vsor
6.20 c                   eval      c1lage = k1lage
     c                   eval      vshelr_vsor = k1vsor
6.20 c                   eval      vshelr_lage = k1lage
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2besk = *blank                              Betingelse 1
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
6.23  *
6.23 c                   if        c2fuke <> 0 and
6.23 c                             c2fuke > 53
6.23 c                   eval      *in34 = *on
6.23 c                   goto      c2taga
6.23 c                   endif
6.23 c                   if        c2tuke <> 0 and
6.23 c                             c2tuke > 53
6.23 c                   eval      *in35 = *on
6.23 c                   goto      c2taga
6.23 c                   endif
      *
      * Oppdatering av sortiment-hode
      *
     c                   clear                   vshelur
      *
     c                   eval      vshelu_vsor = c1vsor
6.20 c                   eval      vshelu_lage = c1lage
     c     vshelu_key    chain     vshelur                            90
      *
     c                   eval      vcbesk = c2besk
     c                   eval      vctype = c2type
6.23 c*                  if        c2fdat <> 0
6.23 c*    *dmy          move      c2fdat        vcfdat
6.23 c*                  else
6.23 c*                  eval      vcfdat = *loval
6.23 c*                  endif
6.23 c*                  if        c2tdat <> 0
6.23 c*    *dmy          move      c2tdat        vctdat
6.23 c*                  else
6.23 c*                  eval      vctdat = *loval
6.23 c*                  endif
6.23 c                   eval      vcfuke = c2fuke
6.23 c                   eval      vctuke = c2tuke
      *
     c                   time                    vcedat
     c                   time                    vcetim
     c                   eval      vceusr = l_user
      *
     c                   if        *in90 = *off
     c                   update    vshelur
     c                   else
     c                   eval      vcfirm = w_firm
     c                   eval      vcvsor = c1vsor
6.20 c                   eval      vclage = c1lage
     c                   time                    vcodat
6.10 c                   time                    vcotim
6.10 c                   eval      vcousr = l_user
     c                   write     vshelur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1besk = vcbesk
     c                   eval      b1type = vctype
     c                   select
     c                   when      b1type = 'H'
     c                   eval      b1tbes = 'Handleliste Nettbutikk'
     c                   when      b1type = 'V'
     c                   eval      b1tbes = 'Vareliste'
     c                   other
     c                   eval      b1tbes = *blank
     c                   endsl
     c                   endif
      *
      * Vareliste
      * Handleliste Nettbutikk
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vshelr_vsor = d1vsor
6.20 c                   eval      vshelr_lage = d1lage
     c     vshelr_key    chain     vshelr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter sortiment-hode
      *
     c                   eval      vshelu_vsor = d1vsor
6.20 c                   eval      vshelu_lage = d1lage
     c     vshelu_key    chain     vshelur                            90
     c                   delete    vshelur
      *
      * Sletter tilhørende sortiment-linjer
      *
     c                   eval      vsdtlu_vsor = vshelu_vsor
6.20 c                   eval      vsdtlu_lage = vshelu_lage
     c     vsdtlu_key    setll     vsdtlu
     c     vsdtlu_key    reade     vsdtlur                                91
     c                   dow       *in91 = *off
     c                   delete    vsdtlur
     c     vsdtlu_key    reade     vsdtlur                                91
     c                   enddo
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_vsor = k1vsor
6.20 c                   eval      s_lage = k1lage
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vshelr_vsor = k1vsor
6.20 c                   eval      vshelr_lage = k1lage
     c     vshelr_key    chain     vshelr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1vsor = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vshelr_vsor = k1vsor
6.20 c                   eval      vshelr_lage = k1lage
     c     vshelr_key    chain     vshelr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
6.20  *
6.20 c                   if        k1lage <> 0
6.20 c                   call      'RS710R'
6.20 c                   parm                    w_retk
6.20 c                   parm                    k1lage
6.20 c                   parm                    c1ltxt
6.20 c                   if        w_retk <> *blank
6.20 c                   eval      *in33 = *on
6.20 c                   goto      k1taga
6.20 c                   endif
6.20 c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1vsor = s_vsor
6.20 c                   eval      c1lage = s_lage
     c                   exsr      xc2bld
      *
      * Dersom sortiment er opprettet, kopieres sortimentet
      *
     c                   eval      vshelr_vsor = k1vsor
6.20 c                   eval      vshelr_lage = k1lage
     c     vshelr_key    chain     vshelr                             90
     c                   if        *in90 = *off
     c                   eval      vsdtlr_vsor = s_vsor
6.20 c                   eval      vsdtlr_lage = s_lage
     c     vsdtlr_key    setll     vsdtlr
     c     vsdtlr_key    reade     vsdtlr                                 91
     c                   dow       *in91 = *off
     c                   eval      vdvsor = k1vsor
6.20 c                   eval      vdlage = k1lage
6.10 c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
6.10 c                   time                    vdedat
6.10 c                   time                    vdetim
6.10 c                   eval      vdeusr = l_user
     c                   write     vsdtlur
     c     vsdtlr_key    reade     vsdtlr                                 91
     c                   enddo
     c                   endif
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vshel2
     c                   other
     c                   read      vshel1
     c                   endsl
      *
     c                   if        %eof or                                      EOF
     c                             vcfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vshel2_besk = vcbesk
     c                   other
     c                   eval      vshel1_vsor = vcvsor
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1vsor = vcvsor
     c                   eval      b1lage = vclage
     c                   eval      b1besk = vcbesk
     c                   eval      b1type = vctype
      *
     c                   select
     c                   when      b1type = 'H'
     c                   eval      b1tbes = 'Handleliste Nettbutikk'
     c                   when      b1type = 'V'
     c                   eval      b1tbes = 'Vareliste'
     c                   other
     c                   eval      b1tbes = *blank
     c                   endsl
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vshel2_key    setgt     vshel2
     c                   readp     vshel2
     c                   other
     c     vshel1_key    setgt     vshel1
     c                   readp     vshel1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vshel2
     c                   other
     c                   readp     vshel1
     c                   endsl
      *
     c                   if        %eof or
     c                             vcfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vshel2_besk = vcbesk
     c                   other
     c                   eval      vshel1_vsor = vcvsor
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vshel2_key    setll     vshel2
     c                   other
     c     vshel1_key    setll     vshel1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for finne dato (mandag i fra uke - søndag til uke)
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     finn_dato     begsr
6.23  *
6.23 c                   call      'VC701R  '
6.23 c                   parm                    c2fuke
6.23 c                   parm                    c2tuke
6.23 c                   parm                    w_fdat
6.23 c                   parm                    w_tdat
6.31 c                   parm                    w_inlr
6.23  *
6.23 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på sortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
      *
      * Key for posisjonering på beskrivelse
      *
     c     vshel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel2_besk
      *
      * Key for les på sortiment-hode
      *
     c     vshelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshelr_vsor
6.20 c                   kfld                    vshelr_lage
      *
      * Key for oppdatering av sortiment-hode
      *
     c     vshelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshelu_vsor
6.20 c                   kfld                    vshelu_lage
      *
      * Key for les på sortiment-linje
      *
     c     vsdtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtlr_vsor
6.20 c                   kfld                    vsdtlr_lage
      *
      * Key for oppdatering av sortiment-linje
      *
     c     vsdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtlu_vsor
6.20 c                   kfld                    vsdtlu_lage
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'VC100R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     *in40 = *on;
7.01   endif;
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vshel1_vsor = *blank
     c     vshel1_key    setll     vshel1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
