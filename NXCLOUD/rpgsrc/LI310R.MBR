     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LI310R                                       *
      *  Beskrivelse . . : Oppdatere varemottak på 2DP                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Ref   Dato   Ref  Endringsbeskrivelse                          *
      *  ----  ------      ---------------------------------------------*
7.00  *  K000  230420 Bsa  Nytt program                                 *
7.xx  *  K000  140520 Bsa  Innfører logging pga feilsøk                 *
7.01  *  K000  170920 Bsa  Bruker antall fra bestilt antall på pakk-    *
7.01  *  K000              seddelen. Da unngår vi at avvikende enheter  *
7.01  *  K000              på pakkseddelen, "ødelegger" forventet lager-*
7.01  *  K000              beholdning. Spes. på trelast vil det kunne   *
7.01  *  K000              være tildels store avvik her.                *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     flb2diu    uf   e           k disk    rename(lb2dst:lb2diur)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     flbhoi2    if   e           k disk    rename(lbhost:lbhoi2r)
     flpkoi2    uf   e           k disk    rename(lpkost:lpkoi2r)
     flpkoir    if   e           k disk    rename(lpkost:lpkoirr)
     flpkoiu    uf   e           k disk    rename(lpkost:lpkoiur)
     flpkli2    uf   e           k disk    rename(lpklst:lpkli2r)
     flbhoiu    uf   e           k disk    rename(lbhost:lbhoiur)
     flbhoi3    uf   e           k disk    rename(lbhost:lbhoi3r)
     flbliir    if   e           k disk    rename(lblist:lbliirr)
     flbliiu    uf   e           k disk    rename(lblist:lbliiur)
     flblii3    uf   e           k disk    rename(lblist:lblii3r)
     flphoi1    if   e           k disk    rename(lphost:lphoi1r)
     flphoiu    uf   e           k disk    rename(lphost:lphoiur)
     flpkliu    uf   e           k disk    rename(lpklst:lpkliur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
      *
     flplolu    uf a e           k disk    rename(lplopfr:lplolur)
     flplol1    if   e           k disk    rename(lplopfr:lplol1r)
     flplllu    uf a e           k disk    rename(lpllpfr:lplllur)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
      *
      * Definering av array's
      *
     d bnr             s              8  0 dim(20)
     d bsu             s              2  0 dim(20)
     d oty             s              2    dim(20)
     d ssc             s             18  0 dim(20)
      *
      *
      * Definering av variable i parametre
      *
     d p_firm          s                   like(phfirm)
     d p_sscc          s             18
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_paid          s                   like(popaid)
     d p_pver          s                   like(pblpve)
     d p_aarr          s                   like(poaarr)
      *
      * Definering av variabler i nøkler
      *
     d lpkoiu_type     s                   like(potype)
     d lpkoiu_mnum     s                   like(pomnum)
     d lpkoiu_aarr     s                   like(poaarr)
     d lpkoiu_paid     s                   like(popaid)
     d lpkoiu_pver     s                   like(popver)
     d lpkoiu_fsni     s                   like(pofsni)
     d lpkoiu_fniv     s                   like(pofniv)
     d lpkoiu_sscc     s                   like(posscc)
      *
     d lb2diu_lssc     s                   like(p2lssc)
     d lb2diu_lnum     s                   like(p2lnum)
     d lb2diu_lsuf     s                   like(p2lsuf)
     d lb2diu_llin     s                   like(p2llin)
     d lb2diu_lpid     s                   like(p2lpid)
     d lb2diu_lpve     s                   like(p2lpve)
     d lb2diu_laar     s                   like(p2laar)
      *
     d lpkoi2_type     s                   like(potype)
     d lpkoi2_sscc     s                   like(posscc)
     d lpkoi2_aarr     s                   like(poaarr)
     d lpkoi2_paid     s                   like(popaid)
      *
     d lpkoir_type     s                   like(phtype)
     d lpkoir_aarr     s                   like(phaarr)
     d lpkoir_mnum     s                   like(phmnum)
     d lpkoir_paid     s                   like(phpaid)
     d lpkoir_pver     s                   like(phpver)
      *
     d lbhoiu_hssc     s                   like(pbhssc)
     d lbhoiu_hnum     s                   like(pbhnum)
     d lbhoiu_hsuf     s                   like(pbhsuf)
      *
     d lbhoi3_hpid     s                   like(pbhpid)
     d lbhoi3_hpve     s                   like(pbhpve)
     d lbhoi3_hssc     s                   like(pbhssc)
     d lbhoi3_haar     s                   like(pbhaar)
     d lbhoi3_hnum     s                   like(pbhnum)
     d lbhoi3_hsuf     s                   like(pbhsuf)
      *
     d lblii3_lpid     s                   like(pblpid)
     d lblii3_lpve     s                   like(pblpve)
     d lblii3_lssc     s                   like(pblssc)
      *
     d lbhoi2_hpid     s                   like(pbhpid)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
      *
     d lplolu_numm     s                   like(lkponr)
     d lplolu_suff     s                   like(lkpsuf)
      *
     d lphoi1_type     s                   like(phtype)
     d lphoi1_aarr     s                   like(phaarr)
     d lphoi1_mnum     s                   like(phmnum)
     d lphoi1_paid     s                   like(phpaid)
     d lphoi1_pver     s                   like(phpver)
      *
     d lbliir_lssc     s                   like(pblssc)
      *
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
      *
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d votyl1_otyp     s                   like(vaotyp)
      *
     d lplllu_lonr     s                   like(lklonr)
     d lplllu_lsuf     s                   like(lklsuf)
     d lplllu_llin     s                   like(lkllin)
      *
     d lpkliu_type     s                   like(potype)
     d lpkliu_mnum     s                   like(pomnum)
     d lpkliu_aarr     s                   like(poaarr)
     d lpkliu_paid     s                   like(popaid)
     d lpkliu_pver     s                   like(popver)
     d lpkliu_fsni     s                   like(pofsni)
     d lpkliu_fniv     s                   like(pofniv)
      *
     d lplol1_lonr     s                   like(lkponr)
     d lplol1_lsuf     s                   like(lkpsuf)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_valg          s              1
     d w_ipor          s              1
     d w_bsuf          s              2  0
     d w_mnum          s                   like(pbhmnu)
     d w_sscc          s                   like(pbhssc)
7.xx d w_jnav          s             15
7.xx d w_jkey          s             41
7.xx d w_jtxt          s             35
7.xx d w_jstat         s              2  0
7.xx d w_jtext         s            200
7.xx d w_debg          s              1    inz(*off)
      *
     d x               s              2  0
      *
     d b_alle          s              1    inz(*off)
     d b_text          s              1    inz(*off)
     d b_flin          s              1    inz(*off)
      *
     d                UDS
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_firm               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler plukking av EDI pakksedler fra 2DP                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      x = 0
     c                   eval      bnr = *zeros
     c                   eval      bsu = *zeros
     c                   eval      oty = *blanks
     c                   eval      ssc = *zeros
      *
      * Legg ut info i LBLI om at det er scannet. Flagg statuskode.
      *
     c                   eval      lbhoi3_hpid = p_paid
     c                   eval      lbhoi3_hpve = p_pver
     c                   eval      lbhoi3_hssc = w_sscc
     c     lbhoi3_key2   setll     lbhoi3
     c     lbhoi3_key2   reade     lbhoi3
     c                   dow       not %eof
     c                   eval      pbhsta = '50'
     c                   update    lbhoi3r
     c     lbhoi3_key2   reade     lbhoi3
     c                   enddo
      *
      *
     c                   eval      lblii3_lpid = p_paid
     c                   eval      lblii3_lpve = P_pver
     c                   eval      lblii3_lssc = w_sscc
     c     lblii3_key    setll     lblii3
     c     lblii3_key    reade     lblii3
     c                   dow       not %eof
     c                   eval      lpkoiu_fsni = pblfsn
     c                   eval      lpkoiu_fniv = pblfni
     c                   eval      pblsta = '50'
     c                   update    lblii3r
      *
     c     lblii3_key    reade     lblii3
     c                   enddo
      *
      * Flagg status i LPKO scannet. Flagg statuskode.
      *
     c                   eval      lpkoiu_type = *blanks
     c                   eval      lpkoiu_mnum = w_mnum
     c                   eval      lpkoiu_aarr = p_aarr
     c                   eval      lpkoiu_paid = p_paid
     c                   eval      lpkoiu_pver = p_pver
     c                   eval      lpkoiu_sscc = w_sscc
     c     lpkoiu_key    chain     lpkoiu
     c                   if        %found
     c                   eval      postat = '50'
     c                   update    lpkoiur
     c                   endif
      *
      * Flagg status i LPKL scannet. Flagg statuskode.
      *
     c                   eval      lpkliu_type = *blanks
     c                   eval      lpkliu_mnum = w_mnum
     c                   eval      lpkliu_aarr = p_aarr
     c                   eval      lpkliu_paid = p_paid
     c                   eval      lpkliu_pver = p_pver
     c                   eval      lpkliu_fsni = pofsni
     c                   eval      lpkliu_fniv = pofniv
     c     lpkliu_key    setll     lpkliu
     c     lpkliu_key    reade     lpkliu
     c                   dow       not %eof
     c                   if        plsscc = w_sscc
     c                   eval      plstat = '50'
     c                   endif
     c                   update    lpkliur
     c     lpkliu_key    reade     lpkliu
     c                   enddo
      *
      * Da kjører vi oppdatering
      *
      * Finner om det er en bestillingsreferanse - minst en av dem må være gyldig
      *
     c                   eval      lbhoi2_hpid = p_paid
     c     lbhoi2_key    setll     lbhoi2
     c     lbhoi2_key    reade     lbhoi2
     c                   dow       not %eof
     c                   if        pbhnum <> 0     and
     c                             pbhaar = p_aarr and
     c                             pbhsta = '50'
      *
      *  Sjekk om bestilling er gyldig
      *
     c                   eval      lohel1_numm = pbhnum
     c                   eval      lohel1_suff = pbhsuf
     c     lohel1_key    chain     lohel1
     c                   if        %found
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
      *
     c                   if        %found
     c                   if        %lookup(pbhssc:ssc) = 0
     c                   eval      x = x +1
     c                   eval      bnr(x) = pbhnum
     c                   eval      bsu(x) = pbhsuf
     c                   eval      ssc(x) = pbhssc
     c                   eval      oty(x) = vaonxt
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c     lbhoi2_key    reade     lbhoi2
     c                   enddo
      *
      * Sett sperre på EDI pakkseddelen, så den ikke kan plukkes flere ganger.
      *
     c     lphoi1_key    chain     lphoiur
     c                   if        %found
     c                   eval      phstat = '30'
     c                   time                    phedat
     c                   time                    phetim
     c                   eval      pheusr = l_user
     c                   update    lphoiur
     c                   endif
      *
      * Så looper vi de bestillingene som er gyldige, og skal plukkes/delplukkes.
      *
     c     1             do        20            x
     c                   if        ssc(x) <> 0
     c                   eval      lohel1_numm = bnr(x)
     c                   eval      lohel1_suff = bsu(x)
     c                   eval      lohelu_numm = bnr(x)
     c                   eval      lohelu_suff = bsu(x)
     c                   eval      lonumm = bnr(x)
     c                   eval      losuff = bsu(x)
      *
      *  Oppdater BESTILLING-HODE-REGISTER, Bestillingen er til behandling
      *
     c                   call      'LO709R'
     c                   parm                    lonumm
     c                   parm                    losuff
      *
      * Sett "sperrekode" på bestilling
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lokode = 3
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
      * Kjør utplukk avhengig av hvor vi kommer fra
      *
     c                   exsr      scan_li210
      *
      *  Kaller opp utskrift-program for å sette inn alternativ
      *  og om direktevalg skal benyttes
7.xx  *
7.xx c                   if        w_debg = *on
7.xx c                   eval      w_jstat = 10
7.xx c                   eval      w_jtext = 'EDI Pakkseddel-LI311-call'
7.xx c                   call      'AB705R'
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jstat
7.xx c                   parm                    w_jtext
7.xx c                   endif
      *
     c                   eval      w_valg = '1'
      *
     c                   call      'LI311R'
     c                   parm                    w_ipor
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
     c                   parm                    w_valg
      *
      * Sette tilbake status
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lokode = 0
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
     c                   endif
     c                   enddo
      *
      * Så looper vi de bestillingene som er behqndlet og setter statuser
      *
7.xx  *
7.xx c                   if        w_debg = *on
7.xx c                   eval      w_jstat = 10
7.xx c                   eval      w_jtext = 'EDI Pakkseddel-status 90'
7.xx c                   call      'AB705R'
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jstat
7.xx c                   parm                    w_jtext
7.xx c                   endif
7.xx  *
     c     1             do        20            x
     c                   if        ssc(x) <> 0
     c                   eval      lbhoiu_hssc = ssc(x)
     c                   eval      lbhoiu_hnum = bnr(x)
     c                   eval      lbhoiu_hsuf = bsu(x)
     c                   eval      lpkoi2_sscc = ssc(x)
      *
      * Setter statusen til ferdig varemottatt på knytning bestilling SSCC kode  - hode
      *
     c     lbhoiu_key    chain     lbhoiu
     c                   if        %found
     c                   eval      pbhsta = '90'
     c                   time                    pbheda
     c                   time                    pbheti
     c                   eval      pbheus = l_user
     c                   update    lbhoiur
     c                   endif
      *
      * Setter statusen til ferdig varemottatt på knytning bestilling SSCC kode - linje
      *
     c     lbhoiu_key    setll     lbliiu
     c     lbhoiu_key    reade     lbliiu
     c                   dow       not %eof
     c                   eval      pblsta = '90'
     c                   time                    pbleda
     c                   time                    pbleti
     c                   eval      pbleus = l_user
     c                   update    lbliiur
     c     lbhoiu_key    reade     lbliiu
     c                   enddo
      *
      * Setter statusen til ferdig varemottatt på pakkseddel - kolli
      *
     c     lpkoi2_key    setll     lpkoi2
     c     lpkoi2_key    reade     lpkoi2
     c                   dow       not %eof
     c                   eval      postat = '90'
     c                   time                    poedat
     c                   time                    poetim
     c                   eval      poeusr = l_user
     c                   update    lpkoi2r
      *
      * Sett sperre på alle underliggende kollilinjer, så de ikke kan plukkes fra andre.
      *
     c     lpkoi2_key    setll     lpkli2
     c     lpkoi2_key    reade     lpkli2
     c                   dow       not %eof
     c                   eval      plstat = '90'
     c                   time                    pledat
     c                   time                    pletim
     c                   eval      pleusr = l_user
     c                   update    lpkli2r
     c     lpkoi2_key    reade     lpkli2
     c                   enddo
      *
     c     lpkoi2_key    reade     lpkoi2
     c                   enddo
      *
     c                   endif
     c                   enddo
      *
      * Sjekk om hele pakkkseddelen er scannet ?? Forutsetter at alle kolli er oppdatert
      *
     c                   eval      b_alle = *on
      *
     c     lpkoir_key    setll     lpkoir
     c     lpkoir_key    reade     lpkoir
     c                   dow       not %eof
     c                   if        postat <> '90'
     c                   eval      b_alle = *off
     c                   endif
     c     lpkoir_key    reade     lpkoir
     c                   enddo
      * Hvis alle har status 90, er alle scannet
     c                   if        b_alle = *on
     c     lphoi1_key    chain     lphoiur
     c                   if        %found
     c                   eval      phstat = '90'
     c                   time                    phedat
     c                   time                    phetim
     c                   eval      pheusr = l_user
     c                   update    lphoiur
     c                   endif
     c                   endif
      *
      * Slå sammen alle evnt IP'er
      * NB kun hvis alle kolli er scannet
      *
      * Så looper vi de bestillingene som er behandlet og setter statuser
      *
     c                   if        b_alle = *on
7.xx  *
7.xx c                   if        w_debg = *on
7.xx c                   eval      w_jstat = 10
7.xx c                   eval      w_jtext = 'EDI Pakkseddel-en IP'
7.xx c                   call      'AB705R'
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jstat
7.xx c                   parm                    w_jtext
7.xx c                   endif
7.xx  *
      *
     c     1             do        20            x
     c                   if        bnr(x) <> 0
      * Basisordre
     c                   eval      w_bsuf = 0
     c                   eval      lohelr_numm = bnr(x)
     c                   eval      lohelr_suff = 00
     c     lohelr_key    setll     lohelr
     c     lohelr_key2   reade     lohelr
     c                   dow       not %eof
     c                   if        losuff > 0 and
     c                             w_bsuf = 0
     c                   eval      w_bsuf = losuff
     c                   endif
     c     lohelr_key2   reade     lohelr
     c                   enddo
      *
     c                   eval      lohelr_numm = bnr(x)
     c                   eval      lohelr_suff = 99
      *
      * Les fra siste suffiks og slå sammen med basisbestillingen
      *
      *
     c     lohelr_key    setll     lohelr
     c     lohelr_key2   readpe    lohelr
     c                   dow       not %eof
     c
     c                   if        losuff <> bsu(x) and
     c                             losuff > 1
      *
7.xx c                   if        w_debg = *on
7.xx c                   eval      w_jstat = 10
7.xx c                   eval      w_jtext = 'EDI Pakkseddel-LI306-call. Slår -
7.xx c                                       sammen alle til en IP'
7.xx c                   call      'AB705R'
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jstat
7.xx c                   parm                    w_jtext
7.xx c                   endif
      *
      * Så kaller vi program for å slå sammen IP'er
     c                   call      'LI306R'
     c                   parm                    lohelr_numm
     c                   parm                    w_bsuf
     c                   parm                    losuff
     c                   endif
      *
     c     lohelr_key2   readpe    lohelr
     c                   enddo
      *
     c                   endif
     c                   enddo
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
7.xx c                   if        w_debg = *on
7.xx  * Avslutt debug - dette programmet
7.xx c                   call      'AB710R'
7.xx c                   parm                    w_jkey
7.xx c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for utplukk fra scanning av kolli                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     scan_li210    begsr
      *
      * Les scannede kollilinjer
      *
      *
     c                   eval      lbliir_lssc = ssc(x)
     c     lbliir_key    setll     lbliir
     c     lbliir_key    reade     lbliir
      *
     c                   dow       not %eof
      *
     c                   eval      lb2diu_lssc = pblssc
     c                   eval      lb2diu_lnum = pblnum
     c                   eval      lb2diu_lsuf = pblsuf
     c                   eval      lb2diu_llin = pbllin
     c                   eval      lb2diu_lpid = pblpid
     c                   eval      lb2diu_lpve = pblpve
     c                   eval      lb2diu_laar = pblaar
     c     lb2diu_key    chain     lb2diu
     c                   if        not %found
     c                   goto      ny_paklin
     c                   else
     c                   eval      p2stat = '90'
     c                   update    lb2diur
     c                   endif
      *
     c                   eval      lodtl1_numm = lohel1_numm
     c                   eval      lodtl1_suff = lohel1_suff
     c                   eval      lodtl1_line = pbllin
     c     lodtl1_key    chain     lodtl1
     c                   if        %found
      *
      * Leser innmeldt og skriver plukklinje
      *
      *  Tekstlinje plukket?
      *
     c                   if        ldvare = *blank
     c                   eval      pblpan = 1
     c                   endif
      *
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = lohelu_numm
     c                   eval      lklsuf = lohelu_suff
     c                   eval      lkllin = ldline
7.01 c**                 eval      lklant = pblpan
7.01 c                   eval      lklant = pblban
     c                   eval      lklkod = '1'
      *
     c                   if        ldvare = *blank
     c                   eval      lklant = 0
     c                   endif
      *
     c                   write     lplllur
      *
      * Sjekk om dette er første varelinje, da skal evnt tekstlinjer over plukkes samtidig.
      *
     c                   eval      b_flin = *on
     c                   eval      b_text = *on
     c                   eval      lodtlr_numm = lohel1_numm
     c                   eval      lodtlr_suff = lohel1_suff
     c                   eval      lodtlr_line = pbllin
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key2   readpe    lodtlr
      *
     c                   dow       not %eof
     c                   if        ldline < pbllin
     c                   if        ldltyp <> 'TX'
     c                   eval      b_flin = *off
     c                   endif
     c                   endif
      *
     c     lodtlr_key2   readpe    lodtlr
     c                   enddo
      * Tekstlinjer over skal følge første varelinje
     c                   if        b_flin = *on
     c                   eval      lodtlr_numm = lohel1_numm
     c                   eval      lodtlr_suff = lohel1_suff
     c                   eval      lodtlr_line = pbllin
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key2   readpe    lodtlr
      *
     c                   dow       not %eof
     c                   if        ldline < pbllin
     c                   if        ldltyp = 'TX'
      * Skriv tekstlinje som skal plukkes
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = lohelu_numm
     c                   eval      lklsuf = lohelu_suff
     c                   eval      lkllin = ldline
     c                   eval      lklkod = '1'
     c                   eval      lklant = 0
     c                   write     lplllur
     c                   endif
     c                   endif
      *
     c     lodtlr_key2   readpe    lodtlr
     c                   enddo
      *
     c                   endif
      *
      * Sjekk om neste linje(r) er tekstlinjer. Da skal disse også plukkes
      *
     c                   eval      b_text = *on
     c                   eval      lodtlr_numm = lohel1_numm
     c                   eval      lodtlr_suff = lohel1_suff
     c                   eval      lodtlr_line = pbllin
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key2   reade     lodtlr
      *
     c                   dow       not %eof
     c                   if        ldline > pbllin
     c                   if        b_text = *on and
     c                             ldltyp = 'TX'
      * Skriv tekstlinje som skal plukkes
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = lohelu_numm
     c                   eval      lklsuf = lohelu_suff
     c                   eval      lkllin = ldline
     c                   eval      lklkod = '1'
     c                   eval      lklant = 0
     c                   write     lplllur
     c                   else
     c                   eval      b_text = *off
     c                   endif
     c                   endif
      *
     c     lodtlr_key2   reade     lodtlr
     c                   enddo
      *
     c                   endif
      *
     c     ny_paklin     tag
      *
     c     lbliir_key    reade     lbliir
     c                   enddo
      *
      * Skriv plukk heading
      *
     c                   eval      lkpfir = w_firm
     c                   eval      lkponr = lohelu_numm
     c                   eval      lkpsuf = lohelu_suff
     c                   eval      lkpoty = oty(x)
     c                   eval      lkplag = lolage
     c                   eval      lkpwsd = l_wsid
     c                   eval      lkpusr = l_user
     c                   eval      lkpdir = 1
     c                   eval      lkpres = 1
      *
     c                   write     lplolur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_sscc
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_paid
     c                   parm                    p_pver
     c                   parm                    p_aarr
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for posisjonering av pakkseddel kolli
      *
     c     lpkoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoiu_type
     c                   kfld                    lpkoiu_mnum
     c                   kfld                    lpkoiu_aarr
     c                   kfld                    lpkoiu_paid
     c                   kfld                    lpkoiu_pver
     c                   kfld                    lpkoiu_fsni
     c                   kfld                    lpkoiu_fniv
     c                   kfld                    lpkoiu_sscc
      *
      *  Key for les av bestillingshode pr. kolli
      *
     c     lbhoi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoi2_hpid
      *
      *  Key for les av bestillingshode pr. kolli
      *
     c     lbhoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoiu_hssc
     c                   kfld                    lbhoiu_hnum
     c                   kfld                    lbhoiu_hsuf
      *
      *  Key for les av bestillingshode pr. kolli
      *
     c     lbhoi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoi3_hpid
     c                   kfld                    lbhoi3_hpve
     c                   kfld                    lbhoi3_hssc
     c                   kfld                    lbhoi3_haar
     c                   kfld                    lbhoi3_hnum
     c                   kfld                    lbhoi3_hsuf
      *
      *  Key for posisjonering av bestillinger på kolli
      *
     c     lbhoi3_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoi3_hpid
     c                   kfld                    lbhoi3_hpve
     c                   kfld                    lbhoi3_hssc
      *
      *  Key for les av bestillingshode pr. kolli linjer
      *
     c     lbliir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbliir_lssc
      *
      *  Key for posisjonering av bestillinger på kollilinjer
      *
     c     lblii3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lblii3_lpid
     c                   kfld                    lblii3_lpve
     c                   kfld                    lblii3_lssc
      *
      *  Key for les av bestillingshode plukket fra 2DP
      *
     c     lb2diu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lb2diu_lssc
     c                   kfld                    lb2diu_lnum
     c                   kfld                    lb2diu_lsuf
     c                   kfld                    lb2diu_llin
     c                   kfld                    lb2diu_lpid
     c                   kfld                    lb2diu_lpve
     c                   kfld                    lb2diu_laar
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for file : Kollihode
      *
     c     lpkoi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoi2_type
     c                   kfld                    lpkoi2_sscc
     c                   kfld                    lpkoi2_aarr
     c                   kfld                    lpkoi2_paid
      *
      *  Key for file : Kollihoder
      *
     c     lpkoir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoir_type
     c                   kfld                    lpkoir_mnum
     c                   kfld                    lpkoir_aarr
     c                   kfld                    lpkoir_paid
     c                   kfld                    lpkoir_pver
      *
      *  Key for file : Pakkseddelhode
      *
     c     lphoi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lphoi1_type
     c                   kfld                    lphoi1_mnum
     c                   kfld                    lphoi1_aarr
     c                   kfld                    lphoi1_paid
     c                   kfld                    lphoi1_pver
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     lplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplol1_lonr
     c                   kfld                    lplol1_lsuf
      *
      * Key for les STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les av bestillingshode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for les av bestillingshode
      *
     c     lohelr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
      *
      * Key for les av bestillingslinjer
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      * Key for les av bestillingslinjer
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      * Key for les av bestillingslinjer
      *
     c     lodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
      *
      * Key for les/oppd. av utplukksfil/hode
      *
     c     lplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplolu_numm
     c                   kfld                    lplolu_suff
      *
      * Key for les av plukkede linjer
      *
     c     lplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplllu_lonr
     c                   kfld                    lplllu_lsuf
     c                   kfld                    lplllu_llin
      *
      *  Key for posisjonering av pakkseddel kolli linjer
      *
     c     lpkliu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkliu_type
     c                   kfld                    lpkliu_mnum
     c                   kfld                    lpkliu_aarr
     c                   kfld                    lpkliu_paid
     c                   kfld                    lpkliu_pver
     c                   kfld                    lpkliu_fsni
     c                   kfld                    lpkliu_fniv
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_sscc = %int(p_sscc)
      *
     c                   eval      lpkoi2_type = *blanks
     c                   eval      lpkoi2_aarr = p_aarr
     c                   eval      lpkoi2_paid = p_paid
      *
     c                   eval      lpkoir_type = *blanks
     c                   eval      lpkoir_aarr = p_aarr
     c                   eval      lpkoir_paid = p_paid
     c                   eval      lpkoir_pver = p_pver
      *
     c                   eval      lphoi1_type = *blanks
     c                   eval      lphoi1_aarr = p_aarr
     c                   eval      lphoi1_paid = p_paid
     c                   eval      lphoi1_pver = p_pver
      *
     c                   eval      lbhoi3_hpid = p_paid
     c                   eval      lbhoi3_hpve = p_pver
     c                   eval      lbhoi3_hssc = w_sscc
     c                   eval      lbhoi3_haar = p_aarr
     c                   eval      lbhoi3_hnum = p_numm
     c                   eval      lbhoi3_hsuf = p_suff
     c     lbhoi3_key    chain     lbhoi3
      *
     c                   eval      w_mnum = pbhmnu
     c                   eval      lpkoir_mnum = w_mnum
     c                   eval      lphoi1_mnum = w_mnum
      *
      *  Nullstiller felter som alltid skal være blanke/null
      *
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
7.xx  *
7.xx  * Logging av EDI Pakkseddel mottak
7.xx  *
7.xx c                   if        w_debg = *on
7.xx c                   eval      w_jnav = 'EDI Pakkseddel motttak'
7.xx c                   eval      w_jtxt = 'Scanning av SSCC koder på PDA-310'
7.xx c                   eval      w_jkey = *blanks
7.xx c                   call      'AB700R'
7.xx c                   parm                    w_jnav
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jtxt
7.xx  *
7.xx  * Vi logger at utskriftprogrammet har startet
7.xx  *
7.xx c                   eval      w_jstat = 10
7.xx c                   eval      w_jtext = 'EDI Pakkseddel-LI310-inzsr'
7.xx c                   call      'AB705R'
7.xx c                   parm                    w_jkey
7.xx c                   parm                    w_jstat
7.xx c                   parm                    w_jtext
7.xx  *
7.xx c                   endif
      *
     c                   endsr
