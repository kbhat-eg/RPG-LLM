     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV862R
      * Beskrivelse . . : Oppdater fra NOBB, modul
      *
      *                   Oppdaterer/oppretter modul fra
      *                   NOBB 80 (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV
6.30  * 6.30  211118 bkv  Utvidet med trelast sortiment
8.01  *       060522 bof  Hvis modulnr = 0, så hopp over.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnlmopf    uf   e           k disk    infsr(FileErr)
     fjmodlu    uf a e           k disk    rename(jmodpfr:jmodlur)
     f                                     infsr(FileErr)
     fjmstlu    uf a e           k disk    rename(jmstpfr:jmstlur)
     f                                     infsr(FileErr)
     fjmsdlu    uf a e           k disk    rename(jmsdpfr:jmsdlur)
     f                                     infsr(FileErr)
     fjmtxlu    uf a e           k disk    rename(jmtxpfr:jmtxlur)
     f                                     infsr(FileErr)
     fjvarlg    uf   e           k disk    rename(jvarpfr:jvarlgr)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
6.30 fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)

      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_stat          s              1
      * Brukes som parametre til jobblogging
     d p_jnav          s             15
     d p_jbid          s             41
     d p_jtxt          s             35
     d p_jsts          s              2  0
     d p_jmld          s            200
     d w_meld          s            100
      *
      * Job log teller
      *
     d                 ds
     d d_job_log                     50
     d  w_jmstt                       5    overlay(d_job_log:2)
     d  w_jmst                        5  0 overlay(w_jmstt:1)
     d  w_jmsdt                       5    overlay(d_job_log:7)
     d  w_jmsd                        5  0 overlay(w_jmsdt:1)
     d  w_jmtxt                       5    overlay(d_job_log:12)
     d  w_jmtx                        5  0 overlay(w_jmtxt:1)
     d  w_jmodt                       5    overlay(d_job_log:17)
     d  w_jmod                        5  0 overlay(w_jmodt:1)

      * parametre
     d p_mrec          s            250
     d p_skrc          s            100    Dim(5)
     d p_strc          s            100    Dim(5)
     d p_ftxt1         s          30000
     d p_job_log       s             50
     d p_inlr          s              1

      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      *  Dataelementer modul
      *
     d                 ds
     d d_mrec                       250
     d  d_modn                        8    overlay(d_mrec:1)
     d   d_modn_num                   8  0 overlay(d_modn:1)
     d  d_stat                        1    overlay(d_mrec:9)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_mte1                       35    overlay(d_mrec:10)
     d  d_mte2                       35    overlay(d_mrec:45)
     d  d_merk                       35    overlay(d_mrec:80)
     d  d_ldor                        6    overlay(d_mrec:115)
     d   d_ldor_num                   6  0 overlay(d_ldor:1)
     d  d_prod                        6    overlay(d_mrec:121)
     d   d_prod_num                   6  0 overlay(d_prod:1)
     d  d_vgnr                        7    overlay(d_mrec:127)
     d   d_vgnr_num                   7  0 overlay(d_vgnr:1)
     d   d_ogrp                       2    overlay(d_vgnr:1)
     d   d_ogrp_num                   2  0 overlay(d_ogrp:1)
     d   d_hgrp                       2    overlay(d_vgnr:3)
     d   d_hgrp_num                   2  0 overlay(d_hgrp:1)
     d   d_ugrp                       3    overlay(d_vgnr:5)
     d   d_ugrp_num                   3  0 overlay(d_ugrp:1)
     d  d_imnr                       15    overlay(d_mrec:134)
     d  d_mtyp                       35    overlay(d_mrec:149)
     d  d_crea                       10    overlay(d_mrec:184)
     d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
     d  d_upda                       10    overlay(d_mrec:194)
     d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
     d  d_dele                       10    overlay(d_mrec:204)
     d   d_dele_iso                    d   overlay(d_dele:1) datfmt(*iso)
      *
      *  Dataelementer stikkord
      *
     d                 ds
     d d_skrc                       100
     d  d_sord                       35    overlay(d_skrc:1)
     d  d_screa                      10    overlay(d_skrc:36)
     d   d_screa_iso                   d   overlay(d_screa:1) datfmt(*iso)
     d  d_supda                      10    overlay(d_skrc:46)
     d   d_supda_iso                   d   overlay(d_supda:1) datfmt(*iso)
     d  d_sdele                      10    overlay(d_skrc:56)
     d   d_sdele_iso                   d   overlay(d_sdele:1) datfmt(*iso)
      *
      *  Dataelementer standard
      *
     d                 ds
     d d_strc                       100
     d  d_sstd                       35    overlay(d_strc:1)
     d  d_dcrea                      10    overlay(d_strc:36)
     d   d_dcrea_iso                   d   overlay(d_dcrea:1) datfmt(*iso)
     d  d_dupda                      10    overlay(d_strc:46)
     d   d_dupda_iso                   d   overlay(d_dupda:1) datfmt(*iso)
     d  d_ddele                      10    overlay(d_strc:56)
     d   d_ddele_iso                   d   overlay(d_ddele:1) datfmt(*iso)
      *
      *  Dataelementer fritekst
      *
     d d_ftxt1         s          30000

     d                 ds
     d d_ftrc                        80
     d  d_ftxt                       78    overlay(d_ftrc:1)

      *
      * Definering av variable i nøkler
      *
     d jmodlu_modn     s                   like(jumodn)
     d jmstlu_smod     s                   like(jusmod)
     d jmsdlu_umod     s                   like(juumod)
     d jmtxlu_tmod     s                   like(jutmod)
     d jvarlg_modn     s                   like(jvmodn)
6.30 d jvkhl1_kvnr     s                   like(jvkvnr)

      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * Definering av variable
      *
     d n               s              2  0
     d w_str_i         s            800
     d w_line          s              3  0
     d w_str_78        s             78
     d w_posi          s              4  0
     d b_blnk          s              1    inz(*off)
     d b_frtx          s              1    inz(*off)
     d oppdat_nobb     s              1  0

      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0

6.30  * Prototype for externt program oppdatere logistikk vare
6.30 D P_OppLogi       PR                  ExtPgm('JK101R')

      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c                   eval      d_job_log = p_job_log
     c                   if        p_inlr = *on

     c                   eval      d_mrec = p_mrec
     c                   eval      d_ftxt1 = p_ftxt1
      *
     c                   exsr      skr_modu
      *
     c                   exsr      beh_stikkord
      *
     c                   exsr      beh_standard
      *
     c                   exsr      beh_fritekst
     c                   endif
      *
      * retunerer jobb status
     c                   eval      p_job_log = d_job_log

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     C
     c                   return

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_modu      begsr
      *
      *
     c                   endsr


      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modul
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_modu      begsr
      *
     c                   if        %TRIM(d_modn) = ''
     c                   leavesr
     c                   endif
8.01 c                   if        d_modn_num = 0
8.01 c                   leavesr
8.01 c                   endif
      *
     c                   clear                   jmodlur
      *
     c                   eval      jmodlu_modn = d_modn_num
     c     jmodlu_key    chain     jmodlur
     c                   eval      jumte1 = d_mte1
     c                   eval      jumte2 = d_mte2
     c                   eval      jumerk = d_merk
     c                   eval      jumint = d_imnr
     c                   eval      juldor = d_ldor_num
     c                   eval      juprod = d_prod_num
     c                   eval      juogrp = d_ogrp_num
     c                   eval      juhgrp = d_hgrp_num
     c                   eval      juugrp = d_ugrp_num
     c                   eval      junoda = d_crea_iso
     c                   eval      juneda = d_upda_iso
     c                   eval      junsda = d_dele_iso
     c                   eval      jueusr = l_user
      *
     c                   time                    juedat
     c                   time                    juetim
      *
     c                   if        %found(jmodlu)
     c                   update    jmodlur
     c                   else
     c                   eval      jumodn = d_modn_num
     c                   time                    juodat
     c                   eval      w_jmod = w_jmod + 1
     c                   write     jmodlur
     c                   endif
      *
      * Oppdaterer alle varer med endret modulinformasjon
      *
     c                   eval      jvarlg_modn = d_modn_num
     c     jvarlg_key    setll     jvarlg
     c     jvarlg_key    reade     jvarlgr
     c                   dow       not %eof
      *
6.30  // sjekk om det er logistikkvare
6.30   oppdat_nobb = 1;
6.30   jvkhl1_kvnr = jvvare;
6.30   chain jvkhl1_key jvkhl1;
6.30   if (%found(jvkhl1) and (jvknup = 0));
6.30     oppdat_nobb = 0;
6.30   endif;
      *
     c                   if        (jvldor <> juldor or
     c                             jvprod <> juprod or
     c                             jvogrp <> juogrp or
     c                             jvhgrp <> juhgrp or
     c                             jvugrp <> juugrp) and
     c                             oppdat_nobb = 1
     c                   eval      jvldor = juldor
     c                   eval      jvprod = juprod
     c                   eval      jvogrp = juogrp
     c                   eval      jvhgrp = juhgrp
     c                   eval      jvugrp = juugrp
     c                   eval      jveusr = l_user
     c                   time                    jvedat
     c                   time                    jvetim
     c                   update    jvarlgr
     c                   else
     c                   unlock    jvarlg
     c                   endif
      *
     c     jvarlg_key    reade     jvarlgr
     c                   enddo
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av stikkord-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_stikkord  begsr

     c                   if        %TRIM(d_modn) = ''
     c                   leavesr
     c                   endif

     c                   eval      jmstlu_smod = d_modn_num
     c     jmstlu_key    setll     jmstlu
     c     jmstlu_key    reade     jmstlur
     c                   dow       not %eof
     c                   delete    jmstlur
     c     jmstlu_key    reade     jmstlur
     c                   enddo

     c                   for       n = 1 to 5
     c                   eval      d_skrc = p_skrc(n)
     c                   if        %TRIM(d_sord) = ''
     c                   leave
     c                   endif
     c                   exsr      oppd_stikkord
     c                   endfor
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modulstikkord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_stikkord begsr
      *
     c                   clear                   jmstlur
      *
     c                   eval      jusmod = d_modn_num
     c                   eval      jusord = d_sord
     c                   eval      jusnod = d_screa_iso
     c                   eval      jusned = d_supda_iso
     c                   eval      jusnsd = d_sdele_iso
     c                   eval      juseus = l_user
      *
     c                   time                    jusoda
     c                   time                    juseda
     c                   time                    juseti
      *
     c                   eval      w_jmst = w_jmst + 1
     c                   write     jmstlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av standard-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_standard  begsr
      *
     c                   if        %TRIM(d_modn) = ''
     c                   leavesr
     c                   endif

      * Slett alle gamle standardbetegnelser
     c                   eval      jmsdlu_umod = d_modn_num
     c     jmsdlu_key    setll     jmsdlu
     c     jmsdlu_key    reade     jmsdlur
     c                   dow       not %eof
     c                   delete    jmsdlur
     c     jmsdlu_key    reade     jmsdlur
     c                   enddo

     c                   for       n = 1 to 5
     c                   eval      d_strc = p_strc(n)
     c                   if        %TRIM(d_sstd) = ''
     c                   leave
     c                   endif
     c                   exsr      oppd_standard
     c                   endfor

     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modulstandard
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_standard begsr
      *
     c                   clear                   jmsdlur
      *
     c                   eval      juumod = d_modn_num
     c                   eval      juustd = d_sstd
     c                   eval      juueus = l_user
     c                   eval      juunod = d_dcrea_iso
     c                   eval      juuned = d_dupda_iso
      *
     c                   time                    juuoda
     c                   time                    juueda
     c                   time                    juueti
      *
     c                   eval      w_jmsd = w_jmsd + 1
     c                   write     jmsdlur
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av fritekst-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_fritekst  begsr
      *
     c                   eval      d_ftrc = *blank
     c                   eval      w_line = *zero
     c                   eval      b_blnk = *off
     c                   eval      b_frtx = *off
      * Sletter 'gammel' fritekst
     c                   eval      jmtxlu_tmod = d_modn_num
     c     jmtxlu_key    setll     jmtxlu
     c     jmtxlu_key    reade     jmtxlur
     c                   dow       not %eof
     c                   delete    jmtxlur
     c     jmtxlu_key    reade     jmtxlur
     c                   enddo
      *
      *  Enkeltelement fritekst herfra - slpittes på setninger på 80 posisjoner
      *
      * Splitter opp teksten i strenger på 78 posisjoner
     c                   dow       b_frtx = *off
      *
     c                   eval      w_posi = 78
     c                   eval      w_str_78 = *blank
     c                   movel     d_ftxt1       w_str_78
     c                   eval      b_blnk = *off
      * Vi begynner å lese bakfra
     c                   dow       b_blnk = *off
     c                   if        %subst(w_str_78:w_posi:1) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_posi  = w_posi - 1
     c                   endif
     c                   if        w_posi < 2
     c                   eval      b_blnk = *on
     c                   eval      w_posi = 78
     c                   endif
     c                   enddo
      *
     c                   if        w_posi > 0
     c                   eval      w_str_i = %subst(w_str_78:1:w_posi)
     c                   exsr      uttr_teks
     c                   exsr      oppd_fritekst
     c                   endif
     c                   eval      d_ftxt1 = %subst(d_ftxt1:w_posi)
     c                   if        %trim(d_ftxt1) = *blanks
     c                   eval      b_frtx = *on
     c                   endif
      *
     c                   enddo
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av fritekstelement : Txt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_teks     begsr
      *
      *                  eval      w_str_i = *blank
      *                  eval      w_str_i = %trim(d_ftxt1)
     c                   eval      d_ftxt = %trim(w_str_i)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modulfritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_fritekst begsr
      *
     c                   clear                   jmtxlur
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      jutmod = d_modn_num
     c                   eval      jutlin = w_line
     c                   eval      juttxt = d_ftxt
     c                   eval      juteus = l_user
     c                   eval      jutnod = d_crea_iso
     c                   eval      jutned = d_upda_iso
     c                   eval      jutnsd = d_dele_iso
      *
     c                   time                    jutoda
     c                   time                    juteda
     c                   time                    juteti
      *
     c                   eval      w_jmtx = w_jmtx + 1
     c                   write     jmtxlur
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_mrec
     c                   parm                    p_skrc
     c                   parm                    p_strc
     c                   parm                    p_ftxt1
     c                   parm                    p_job_log
     c                   parm                    p_inlr
      *
      * Key for oppdatering av modul
      *
     c     jmodlu_key    klist
     c                   kfld                    jmodlu_modn
      *
      * Key for oppdatering av modul-stikkord
      *
     c     jmstlu_key    klist
     c                   kfld                    jmstlu_smod
      *
      * Key for oppdatering av modul-standard
      *
     c     jmsdlu_key    klist
     c                   kfld                    jmsdlu_umod
      *
      * Key for oppdatering av modul-tekst
      *
     c     jmtxlu_key    klist
     c                   kfld                    jmtxlu_tmod
      *
      * Key for oppdatering av vare
      *
     c     jvarlg_key    klist
     c                   kfld                    jvarlg_modn
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
6.30  *
6.30  * Key for les av logistikkvare
6.30  *
6.30 c     jvkhl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    jvkhl1_kvnr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr

