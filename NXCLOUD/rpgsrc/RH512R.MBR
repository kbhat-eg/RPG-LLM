     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH512R
      * Beskrivelse . . : Hovedbok - poster, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080402 KOB  Nytt program
T5.01 * V5.00 130303 HFL  Henter ikke tekst når avd = 0
      * V5.20 261003 KOB  Lagt inn spørring på bilag
      * V5.41 280805 KOB  Lagt inn ny funksjon for å vise arkivpost
      * V5.42 041006 HFL  Viser valutakode og beløp.
      * V5.50 100607 KOB  Lagt inn konkatenering av lev.fakturanr og bilagstekst
      *                   og visning av lev.fakturanr i detaljbildet.
      * V5.50 100607 KOB  Fjernet visning av investeringsavgift  - flyttet felt
T5.50 * V5.50 140607 HFL  Notatark på postnivå.
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  *       021210 sst  ENdret prosjekt til 6 alfa
6.21  *  6.20 070611 bsa  Hvis ikke faktura, startes arkivet
6.22  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.22  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.23  * R6.20     051212  Endret metode for oppslag mot arkiv            bsa  *
6.24  * R6.20     040113  Endret logikk rundt refn                       bsa  *
6.25  * R6.20     270213  Endret logikk rundt refn                       bsa  *
6.26  * R6.10     260313  Henter dokumenttype basert på bilagskode       bsa  *
7.xx  * 7.xx  030516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhtrl1    if   e           k disk    rename(rhtrpfr:rhtrl1r)
     frhtrl2    if   e           k disk    rename(rhtrpfr:rhtrl2r)
     frhtrl3    if   e           k disk    rename(rhtrpfr:rhtrl3r)
     frhtrl4    if   e           k disk    rename(rhtrpfr:rhtrl4r)
T5.50frklal2    if   e           k disk    rename(rklapfr:rklal2r)
T5.50frklal3    if   e           k disk    rename(rklapfr:rklal3r)
      *
     frh512d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rhtrl1_kont     s                   like(rbkont)
     d rhtrl1_avde     s                   like(rbavde)
     d rhtrl1_spes     s                   like(rbspes)
     d rhtrl1_rper     s                   like(rbrper)
     d rhtrl1_bdat     s                   like(rbbdat)
     d rhtrl1_tist     s                   like(rbtist)
      *
     d rhtrl2_avde     s                   like(rbavde)
     d rhtrl3_spes     s                   like(rbspes)
     d rhtrl4_biln     s                   like(rbbiln)
      *
T5.50d rklal2_syst     s                   like(rxsyst)
T5.50d rklal2_pros     s                   like(rxpros)
T5.50d rklal2_akti     s                   like(rxakti)
T5.50d rklal2_kont     s                   like(rxkont)
T5.50d rklal2_spes     s                   like(rxspes)
T5.50d rklal2_avde     s                   like(rxavde)
      *
T5.50d rklal3_syst     s                   like(rxsyst)
T5.50d rklal3_pros     s                   like(rxpros)
T5.50d rklal3_akti     s                   like(rxakti)
T5.50d rklal3_kont     s                   like(rxkont)
T5.50d rklal3_spes     s                   like(rxspes)
T5.50d rklal3_avde     s                   like(rxavde)
T5.50d rklal3_tist     s                   like(rxtist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d sbraar          s                   like(rbraar)
     d sbrper          s                   like(rbrper)
      *
6.23 d w_type          s             50
     d w_seqe          s              1
     d w_sude          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rbfirm)
     d w_raar          s                   like(rbraar)
     d w_rper          s                   like(rbrper)
     d w_kont          s              6  0
pdf  d*w_modu          s             10    inz('MSA     ')
pdf  d w_modu          s             10    inz('PROA    ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
6.23 d w_refn          s             50
6.26 d w_dtyp          s             50
6.26 d w_year          s              1
6.26 d w_ok            s              1
      *
      * Definering av parametre Arcive
      *
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
      *
     d p_stat          s              1
     d p_kont          s                   like(rbkont)
     d p_avde          s                   like(rbavde)
     d p_spes          s                   like(rbspes)
     d p_akod          s                   like(rbbilk)
     d p_tx20          s                   like(rbtext)
     d p_text          s                   like(rhtext)
     d p_bkod          s                   like(rbspes)
     d p_btxt          s             30
     d p_gkod          s                   like(rbavde)
     d p_gtxt          s             20
      *
     d s_kont          s                   like(rbkont)
     d s_avde          s                   like(rbavde)
     d s_spes          s                   like(rbspes)
      *
T5.50 * Parameter - program RF020R
6.20 i/COPY QCPYLESRC,RF020PAR
6.20 d*                ds
6.20 d*rxulda                       175
6.20 d* rxstat                        1    overlay(rxulda)
6.20 d* rxfirm                        3  0 overlay(rxulda:2)
6.20 d* rxsyst                        1    overlay(rxulda:5)
6.20 d* rxpros                        6  0 overlay(rxulda:6)
6.20 d* rxakti                        3    overlay(rxulda:12)
6.20 d* rxf001                        3    overlay(rxulda:15)
6.20 d* rxkont                        6  0 overlay(rxulda:18)
6.20 d* rxspes                        4  0 overlay(rxulda:24)
6.20 d* rxavde                        4  0 overlay(rxulda:28)
6.20 d* rxlinj                        4  0 overlay(rxulda:32)
6.20 d* rxtgrp                        3    overlay(rxulda:36)
6.20 d* rxtext                       78    overlay(rxulda:39)
6.20 d* rxf002                       13    overlay(rxulda:117)
6.20 d* rxvalg                        1  0 overlay(rxulda:130)
6.20 d* rxtist                         z   overlay(rxulda:131)
6.20 d* rxbiln                        6  0 overlay(rxulda:158)
6.20 d* rxrper                        2  0 overlay(rxulda:164)
6.20 d* rxraar                        4  0 overlay(rxulda:166)
6.20 d* rxonr                         6  0 overlay(rxulda:170)
T5.50d rxupar          s                   like(rxulda)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2WIN  - C2 Skjermbilde for endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
T5.50 * Finnes notatark på kontoen?
      *
T5.50c                   eval      rklal2_syst = 'H'
T5.50c                   eval      rklal2_pros = *zeros
T5.50c                   eval      rklal2_akti = *blanks
T5.50c                   eval      rklal2_kont = b1kont
T5.50c                   eval      rklal2_spes = b1spes
T5.50c                   eval      rklal2_avde = b1avde
T5.50c     rklal2_key    setll     rklal2                                 94
      *
T5.50c                   if        *in94 = *on
T5.50c                   eval      C2notb = 'NOTATARK'
T5.50c                   endif
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
T5.50c                   when      *inkb
T5.50c                   move      *loval        rxtist
T5.50c                   eval      rxbiln = *zero
T5.50c                   eval      rxrper = *zero
T5.50c                   eval      rxraar = *zero
T5.50c                   eval      rxonr  = *zero
T5.50c                   exsr      notat
T5.50c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
      *
      * Spørring
      *
     c                   when      *inkd = *on
      *
     c                   if        w_afld = 'B2KONT'
      *                                                                 .
     c                   call      'RH500R'
T400 c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_kont <> *zero
     c                   move      p_kont        b2kont
     c                   move      p_spes        b2spes
     c                   move      p_avde        b2avde
     c                   goto      b2taga
     c                   endif
      *                                                                 .
     c                   endif
      *
     c                   if        w_afld = 'B2SPES'
      *
     c                   call      'RA528R'
     c                   parm                    w_firm
     c                   parm                    p_bkod
     c                   parm                    p_btxt
      *                                                                 .
     c                   if        p_bkod <> *zero
     c                   move      p_bkod        b2spes
     c                   goto      b2taga
     c                   endif
      *                                                                 .
     c                   endif
      *
     c                   if        w_afld = 'B2AVDE'
      *
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *                                                                 .
     c                   if        p_gkod <> *zero
     c                   move      p_gkod        b2avde
     c                   goto      b2taga
     c                   endif
      *                                                                 .
     c                   endif
      *
     c                   goto      b2taga
      *
     c                   endsl
      *
      * Sjekk om år/periode er endret
      *
     c                   if        b2raar <> w_raar or
     c                             b2rper <> w_rper
     c                   eval      w_raar = b2raar
     c                   eval      w_rper = b2rper
     c                   eval      sbrper = b2rper
     c                   exsr      forny
     c                   endif
      *
      * Slå av indikator for ib/ub
      *
     c                   if        b2avde <> 0 or
     c                             b2spes <> 0 or
     c                             b2biln <> 0
     c                   eval      b2sumi = *zeros
     c                   eval      b2sumu = *zeros
     c                   eval      *in31 = *on
     c                   endif
      *
      * Slå på indikator for ib/ub
      *
     c                   if        b2kont <> 0
     c                   eval      b2sumi = *zeros
     c                   eval      b2sumu = *zeros
     c                   eval      *in31 = *off
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kont <> 0 or
     c                             b2avde <> 0 or
     c                             b2spes <> 0 or
     c                             b2biln <> 0
     c                   eval      b1kont = b2kont
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
      *                                                                 .
     c                   when      w_seqe = ' '
      *                                                                 .
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1
     c                   endif
     c                   exsr      beregn_ib_ub
      *
     c                   when      w_seqe = 'K'
     c                   eval      rhtrl1_kont = b1kont
     c     rhtrl1_key    setll     rhtrl1
     c                   exsr      beregn_ib_ub
      *
     c                   when      w_seqe = 'A'
     c                   eval      rhtrl2_avde = b1avde
     c     rhtrl2_key    setll     rhtrl2
      *
     c                   when      w_seqe = 'B'
     c                   eval      rhtrl3_spes = b1spes
     c     rhtrl3_key    setll     rhtrl3
      *
     c                   when      w_seqe = 'N'
     c                   eval      rhtrl4_biln = b1biln
     c     rhtrl4_key    setll     rhtrl4
      *
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   eval      *in15 = *off
      *
      * Konto
      *
     c                   if        b2kont <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      rhtrl1_kont = b2kont
     c     rhtrl1_key    setll     rhtrl1
      *                                                                 .
     c                   exsr      beregn_ib_ub
      *                                                                 .
     c                   goto      end_pos
     c                   endif
      *
      * Avdeling
      *
     c                   if        b2avde <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      rhtrl2_avde = b2avde
     c     rhtrl2_key    setll     rhtrl2
     c                   goto      end_pos
     c                   endif
      *
      * Kostnadsbærer
      *
     c                   if        b2spes <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      rhtrl3_spes = b2spes
     c     rhtrl3_key    setll     rhtrl3
     c                   goto      end_pos
     c                   endif
      *
      * Bilagsnummer
      *
     c                   if        b2biln <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      rhtrl4_biln = b2biln
     c     rhtrl4_key    setll     rhtrl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kont = 0
     c                   eval      b2avde = 0
     c                   eval      b2spes = 0
     c                   eval      b2biln = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      rhtrl1_kont = b1kont
     c                   eval      rhtrl1_spes = b1spes
     c                   eval      rhtrl1_avde = b1avde
     c                   eval      rhtrl1_rper = b1rper
     c                   eval      rhtrl1_tist = b1tist
     c                   eval      rhtrl1_bdat = b1bdat
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis bilag
      *
     c                   if        b1valg = 7
      *
     c                   eval      w_kont = b1kont
      *
     c                   call      'RF512R'
     c                   parm                    w_kont
     c                   parm                    b1biln
     c                   parm                    b1bdat
     c                   parm                    b1rper
     c                   parm                    b1raar
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis sumposter
      *
     c                   if        b1valg = 8
     c                   exsr      hent_text
      *
     c                   call      'RH514R'
     c                   parm                    b1raar
     c                   parm                    b1kont
     c                   parm                    b1spes
     c                   parm                    b1avde
     c                   parm                    rhtext
     c                   parm                    b1biln
     c                   parm                    b1text
     c                   parm                    b1bdat
     c                   parm                    b1neto
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
6.26  *
6.26  * Hent dokumenttype basert på bilagskoden
6.26  *
6.26 c                   eval      w_dtyp = *blanks
6.26 c                   eval      w_year = *blanks
6.26 c                   eval      w_ok = '0'
6.26  *
6.26 c                   call      'AP632R'
6.26 c                   parm                    w_firm
6.26 c                   parm                    b1bilk
6.26 c                   parm                    w_dtyp
6.26 c                   parm                    w_year
6.26 c                   parm                    w_ok
6.26  *
6.26 c                   if        w_ok = '1'
6.26 c                   eval      w_type = w_dtyp
6.26 c                   if        w_year = 'J'
6.26 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.26 c                   else
6.26 c                   eval      w_refn = %trim(%char(b1biln))
6.26 c                   endif
6.26 c                   else
6.26  * Gammel logikk hvis den ikke finnes
      *
6.25 c                   if        b1bilk = 1
pdf  c                   eval      w_type = 'FAKTURA'
6.24 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.25 c                   elseif    b1bilk = 2
6.23 c                   eval      w_type = 'INNGÅENDE%20FAKTURA'
6.24 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.25 c                   else
6.25 c                   eval      w_type = 'DIVERSEBILAG'
6.25 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
pdf  c                   endif
6.26 c                   endif
      *
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '10'
     c                   eval      w_aara = %char(b1raar)
     c                   eval      w_bila = %char(b1biln)
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.23 c                   parm                    w_refn
6.23 c*                  parm                    w_aara
6.23 c*                  parm                    w_bila
6.23 c*                  parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c     rhtrl1_key1   chain     rhtrl1
      *
     c                   if        %found
      *
      * Hente konto-tekster
      *
     c                   exsr      hent_text
      *
     c     *dmy          move      rbbdat        c2bdat
     c                   eval      c2bilk = rbbilk
     c                   eval      c2text = rbtext
     c                   eval      c2amva = rbamva
     c                   eval      c2pmva = rbpmva
     c                   endif
      *
     c                   eval      w_arow = 4
     c                   eval      w_acol = 15
      *
     c     c2taga        tag
      *
T5.50 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.50c                   eval      rklal3_syst = 'H'
T5.50c                   eval      rklal3_pros = *zeros
T5.50c                   eval      rklal3_akti = *blanks
T5.50c                   eval      rklal3_kont = b1kont
T5.50c                   eval      rklal3_spes = b1spes
T5.50c                   eval      rklal3_avde = b1avde
T5.50c                   eval      rklal3_tist = b1tist
T5.50c     rklal3_key    setll     rklal3                                 73
      *
     c                   exfmt     c2win
      *
T5.50c                   eval      *in73 = *off
      *
T5.50 * Notatblokk bilag
T5.50c                   if        *inkb
T5.50c                   eval      rxtist = rbtist
T5.50c                   eval      rxbiln = rbbiln
T5.50c                   eval      rxrper = rbrper
T5.50c                   eval      rxraar = rbraar
T5.50c                   eval      rxonr  = *zero
T5.50c                   exsr      notat
T5.50c                   goto      end_c2bld
T5.50c                   endif
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
      *                                                                 .
      * Les posteringer i kontorekkefølge (Opprinnelig konto)           .
      *                                                                 .
     c                   when      w_seqe = ' '
      *                                                                 .
     c                   if        w_sude = 'S'
     c     rhtrl1_key    reade     rhtrl1
     c                   else
     c     rhtrl1_keyh   reade     rhtrl1
     c                   endif
      *                                                               . .
     c                   when      w_seqe = 'K'
     c     rhtrl1_key    reade     rhtrl1
      *                                                               . .
     c                   when      w_seqe = 'A'
     c     rhtrl2_key    reade     rhtrl2
      *                                                               . .
     c                   when      w_seqe = 'B'
     c     rhtrl3_key    reade     rhtrl3
      *                                                               . .
     c                   when      w_seqe = 'N'
     c     rhtrl4_key    reade     rhtrl4
      *                                                               . .
     c                   endsl
      *
     c                   if        %eof or
     c                             rbfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Hvis periode oppgitt - ta bare med periodens poster
      *
     c                   if        w_rper <> 0 and
     c                             rbrper <> w_rper
     c                   goto      crt_neste
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rhtrl2_avde = rbavde
     c                   other
     c                   eval      rhtrl1_kont = rbkont
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = rbkont
     c                   eval      b1avde = rbavde
     c                   eval      b1spes = rbspes
     c                   eval      b1biln = rbbiln
     c                   eval      b1bdat = rbbdat
     c     *dmy          move      rbbdat        b1bida
     c                   eval      b1bilk = rbbilk
      *
     c                   if        rblfak <> *blank
     c                   eval      b1text = %trim(rblfak)+'/'+%trim(rbtext)
     c                   else
     c                   eval      b1text = rbtext
     c                   endif
      *
     c                   eval      b1mvak = rbmvak
     c                   eval      b1neto = rbneto
     c                   eval      b1raar = rbraar
     c                   eval      b1rper = rbrper
     c                   eval      b1tist = rbtist
      *
     c                   if        rbautk = '3'
     c                   eval      b1autk = '*'
     c                   else
     c                   eval      b1autk = ' '
     c                   endif
      *
T5.50 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.50c                   eval      rklal3_syst = 'H'
T5.50c                   eval      rklal3_pros = *zeros
T5.50c                   eval      rklal3_akti = *blanks
T5.50c                   eval      rklal3_kont = b1kont
T5.50c                   eval      rklal3_spes = b1spes
T5.50c                   eval      rklal3_avde = b1avde
T5.50c                   eval      rklal3_tist = b1tist
T5.50c     rklal3_key    setll     rklal3                                 73
      *
     c                   write     b1sfl
      *
T5.50c                   eval      *in73 = *off
     c     crt_neste     tag
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
      *                                                                 .
     c                   when      w_seqe = ' ' and
     c                             %eof(rhtrl1)
      *                                                                 .
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setgt     rhtrl1
     c                   else
     c     rhtrl1_keyh   setgt     rhtrl1
     c                   endif
      *                                                                 .
     c                   when      w_seqe = 'K' and
     c                             %eof(rhtrl1)
     c     rhtrl1_key    setgt     rhtrl1
     c                   readpe    rhtrl1
      *                                                                 .
     c                   when      w_seqe = 'A' and
     c                             %eof(rhtrl2)
     c     rhtrl2_key    setgt     rhtrl2
     c                   readpe    rhtrl2
      *                                                                 .
     c                   when      w_seqe = 'B' and
     c                             %eof(rhtrl3)
     c     rhtrl3_key    setgt     rhtrl3
     c                   readpe    rhtrl3
      *                                                                 .
     c                   when      w_seqe = 'N' and
     c                             %eof(rhtrl4)
     c     rhtrl4_key    setgt     rhtrl4
     c                   readpe    rhtrl4
      *                                                                 .
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
      *
     c                   when      w_seqe = ' '
      *                                                                 .
     c                   if        w_sude = 'S'
     c     rhtrl1_key    readpe    rhtrl1
     c                   else
     c     rhtrl1_keyh   readpe    rhtrl1
     c                   endif
      *                                                                 .
     c                   when      w_seqe = 'K'
     c     rhtrl1_key    readpe    rhtrl1
      *                                                                 .
     c                   when      w_seqe = 'A'
     c     rhtrl2_key    readpe    rhtrl2
      *                                                                 .
     c                   when      w_seqe = 'B'
     c     rhtrl3_key    readpe    rhtrl3
      *                                                                 .
     c                   when      w_seqe = 'N'
     c     rhtrl4_key    readpe    rhtrl4
      *                                                                 .
     c                   endsl
      *
     c                   if        %eof or
     c                             rbfirm <> w_firm
     c                   leave
     c                   endif
      *
      * Hvis periode oppgitt - ta bare med periodens poster
      *
     c                   if        w_rper <> 0 and
     c                             rbrper <> w_rper
     c                   goto      bck_neste
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
      *                                                                 .
     c                   when      w_seqe = ' '
     c                   eval      rhtrl1_kont = rbkont
      *                                                                 .
     c                   when      w_seqe = 'K'
     c                   eval      rhtrl1_kont = rbkont
      *                                                                 .
     c                   when      w_seqe = 'A'
     c                   eval      rhtrl2_avde = rbavde
      *                                                                 .
     c                   when      w_seqe = 'B'
     c                   eval      rhtrl3_spes = rbspes
      *                                                                 .
     c                   when      w_seqe = 'N'
     c                   eval      rhtrl4_biln = rbbiln
      *                                                                 .
     c                   endsl
      *
     c     bck_neste     tag
      *
     c                   enddo
      *
     c                   select
      *
     c                   when      w_seqe = ' ' and
     c                             %eof(rhtrl1)
      *                                                                 .
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1
     c                   endif
      *                                                                 .
     c                   when      w_seqe = 'K' and
     c                             %eof(rhtrl1)
     c     rhtrl1_key    setll     rhtrl1
      *                                                                 .
     c                   when      w_seqe = 'A' and
     c                             %eof(rhtrl2)
     c     rhtrl2_key    setll     rhtrl2
      *                                                                 .
     c                   when      w_seqe = 'B' and
     c                             %eof(rhtrl3)
     c     rhtrl3_key    setll     rhtrl3
      *                                                                 .
     c                   when      w_seqe = 'N' and
     c                             %eof(rhtrl4)
     c     rhtrl4_key    setll     rhtrl4
      *                                                                 .
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hente inn kontonavn fra kontoplanen                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hent_text     begsr
      *
      * Konto-spes-avd
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
     c                   parm                    b1kont
     c                   parm                    b1spes
     c                   parm                    b1avde
     c                   parm                    rhtext
      *
      * Hent avdeling
      *
T5.01c                   if        b1avde <> 0
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    b1avde
     c                   parm                    ragtxt
T5.01c                   else
T5.01c                   eval      ragtxt = *blank
T5.01c                   endif
      *
      * Hent kostnadsbærer
      *
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    b1spes
     c                   parm                    rebtxt
      *
     c                   if        p_stat = 'N'
     c                   eval      rebtxt = *blank
     c                   endif
      *
     c                   endsr
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Beregn inngående og utgående balanse                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beregn_ib_ub  begsr
      *
      * Les posteringer i kontorekkefølge
      *
     c                   eval      *in31  = *off
     c                   eval      b2sumi = *zeros
     c                   eval      b2sumu = *zeros
      *
     c                   if        w_seqe = ' ' or
     c                             w_seqe = 'K'
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1                                 91
     c     rhtrl1_key    reade     rhtrl1                                 91
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1                                 91
     c     rhtrl1_keyh   reade     rhtrl1                                 91
     c                   endif
      *
     c                   dow       *in91 = *off
      *
      * Beregn IB
      *
     c                   if        sbrper =  *zero
     c                             and rbrper <  01
     c                   add       rbneto        b2sumi
     c                   endif
      *
     c                   if        sbrper <> *zero
     c                             and rbrper <  sbrper
     c                   add       rbneto        b2sumi
     c                   endif
      *
      * Beregn UB
      *
     c                   if        sbrper =  *zero
     c                   add       rbneto        b2sumu
     c                   endif
      *
     c                   if        sbrper <> *zero
     c                             and rbrper <= sbrper
     c                   add       rbneto        b2sumu
     c                   endif
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    reade     rhtrl1                                 91
     c                   else
     c     rhtrl1_keyh   reade     rhtrl1                                 91
     c                   endif
      *
     c                   enddo
      *
      * Sett tilbake startpunkt
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1                                 91
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1                                 91
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
T5.50 * Subrutine for oppkall av notatark
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
T5.50c     notat         begsr
      *
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'H'
E5.50c                   eval      rxpros = *blank
     c                   eval      rxakti = ' '
     c                   eval      rxkont = rbkont
     c                   eval      rxspes = rbspes
     c                   eval      rxavde = rbavde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   eval      rxvalg = *blank
     c                   move      rxulda        rxupar
      *
     c                   call      'RF020R'
     c                   parm                    rxupar
      *
     c                   move      rxupar        rxulda
      *
T5.50c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_sude
     c                   parm                    w_raar
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
      *
      * Key for posisjonering på konto
      *
     c     rhtrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl1_kont
      *
      * Key for posisjonering på hele konto
      *
     c     rhtrl1_keyh   klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl1_kont
     c                   kfld                    rhtrl1_spes
     c                   kfld                    rhtrl1_avde
      *
      *
      * Key for posisjonering på avdeling
      *
     c     rhtrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl2_avde
      *
      * Key for posisjonering på kostnadsbærer
      *
     c     rhtrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl3_spes
      *
      * Key for startpunkt bilagsnummer
      *
     c     rhtrl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl4_biln
      *
      * Key for oppdatering av bilag
      *
     c     rhtrl1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl1_kont
     c                   kfld                    rhtrl1_spes
     c                   kfld                    rhtrl1_avde
     c                   kfld                    rhtrl1_rper
     c                   kfld                    rhtrl1_bdat
     c                   kfld                    rhtrl1_tist
      *
T5.50 * Key for notatblokk kontonivå
      *
T5.50c     rklal2_key    klist
T5.50c                   kfld                    w_firm
T5.50c                   kfld                    rklal2_syst
T5.50c                   kfld                    rklal2_pros
T5.50c                   kfld                    rklal2_akti
T5.50c                   kfld                    rklal2_kont
      *
T5.50 * Key for notatblokk postnivå
      *
T5.50c     rklal3_key    klist
T5.50c                   kfld                    w_firm
T5.50c                   kfld                    rklal3_syst
T5.50c                   kfld                    rklal3_pros
T5.50c                   kfld                    rklal3_akti
T5.50c                   kfld                    rklal3_kont
T5.50c                   kfld                    rklal3_spes
T5.50c                   kfld                    rklal3_avde
T5.50c                   kfld                    rklal3_tist
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.22 c                   eval      b2firm = w_firm
     c                   eval      w_seqe = ' '
     c                   eval      b2raar = w_raar
E5.40c                   eval      b2rper = 0
T5.40c                   eval      w_rper = 0
     c                   eval      sbrper = 0
      *
     c                   eval      *in22 = *on
     c                   eval      *in31 = *off
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rhtrl1_kont = p_kont
     c                   eval      rhtrl1_spes = p_spes
     c                   eval      rhtrl1_avde = p_avde
      *
     c                   exsr      beregn_ib_ub
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1
     c                   endif
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
