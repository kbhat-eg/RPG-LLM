     H/TITLE  ASSHOP: Oppretter standardverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BASTDR
      * Beskrivelse . . : Oppretter standardverdier
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     FAFORLU    UF A E           K DISK    RENAME(AFORPFR:AFORLUR)
     FANUMLU    UF A E           K DISK    RENAME(ANUMPFR:ANUMLUR)
     FVOTYLU    UF A E           K DISK    RENAME(VOTYPFR:VOTYLUR)
     FBSTSLU    UF A E           K DISK    RENAME(BSTSPFR:BSTSLUR)
      *
     D RUT1            S             80    DIM(14) CTDATA PERRCD(1)              Rutiner-1
     D RUT2            S             80    DIM(14) CTDATA PERRCD(1)              Rutiner-2
     D NUM             S             80    DIM(6) CTDATA PERRCD(1)               Nummer-koder
     D STS1            S             80    DIM(1) CTDATA PERRCD(1)               Butikk-koder-1
     D STS2            S             80    DIM(1) CTDATA PERRCD(1)               Butikk-koder-2
     D OTY             S             80    DIM(4) CTDATA PERRCD(1)               Ordre-typer
      *
     D                 DS
     D  A2REC1                 1     80
     D  A2RUTI                 1     10
     D  A2PROG                11     20
     D  A2TXT1                21     60
     D  A2FORM                61     70
     D  A2OUTQ                71     80
      *
     D                 DS
     D  A2REC2                 1     80
     D  A2ACPI                 1      2  0
     D  A2ALIN                 3      5  0
     D  A2OFLW                 6      8  0
     D  A2AKOP                 9     11  0
     D  A2HLDQ                12     12
     D  A2PRTY                13     13
     D  A2INST                14     14  0
     D  A2PRFI                15     15  0
     D  A2BGIR                16     16  0
     D* A2PGIR                17     17  0
     D* A2JOUR                18     18  0
     D  A2MVAK                19     19  0
     D  A2OCRK                20     20  0
      *
     D                 DS
     D  A3REC                  1     80
     D  A3FELL                 1      6
     D  A3TYPE                 7      8
     D  A3FAST                 9     18
     D  A3TXT1                19     48
     D  A3WRAP                49     49
     D  A3ÅRNU                50     50
     D  A3AUTO                51     51
     D  A3NFOM                52     57  0
     D  A3NTOM                58     63  0
     D  A3SIST                64     69  0
     D  A3TXT2                70     80
      *
      *
     D                 DS
     D  B0REC1                 1     80
     D  B0AAVS                 1      6
     D  B0ADET                 7     12
     D  B0AORD                13     18
     D  B0ALST                19     24
     D  B0ABNR                25     30
     D  B0AVAR                31     47
     D  B0AKUN                48     53  0
     D  B0AOPK                54     59  0
     D  B0AOPS                60     61  0
     D  B0ASAK                62     67  0
     D  B0ASAS                68     69  0
     D  B0AMVA                70     70  0
     D  B0ABIL                71     71  0
      *
     D                 DS
     D  B0REC2                 1     80
     D  B0AOKD                 1      2
     D  B0AOKK                 3      4
     D  B0AOCD                 5      6
     D  B0AOCK                 7      8
     D  B0AOFD                 9     10
     D  B0AOFK                11     12
     D  B0AOBD                13     14
     D  B0AOBK                15     16
     D  B0AOLD                17     18
     D  B0ALTY                19     20
     D  B0ATTY                21     22
     D  B0ABIN                23     24  0
     D  B0ABKA                25     26  0
     D  B0ABAV                27     28  0
      *
     D                 DS
     D  F2REC                  1     80
     D  F2OTYP                 1      2
     D  F2OTXT                 3     32
     D  F2OAKK                33     33  0
     D  F2OBOO                34     34  0
     D  F2OSKA                35     35  0
     D  F2OSAM                36     36  0
     D  F2OLAG                37     37  0
     D  F2OOKO                38     38  0
     D  F2OSTA                39     39  0
     D  F2OPRO                40     40  0
     D  F2OBUT                41     41  0
     D  F2OKRE                42     42
     D  F2OBIL                43     44
     D  F2ONXT                45     46
     D  F2ORUT                47     56
     D  F2ORUB                57     66
      *
     D                UDS
     D  l_user               911    920
     D  DSSFIR               944    946  0
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard RUTINER                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C                   MOVE      *ZERO         I
     C     I             DOUEQ     14
     C                   ADD       1             I
     C                   MOVE      RUT1(I)       A2REC1
     C                   MOVE      RUT2(I)       A2REC2
      *
     C                   MOVE      A2RUTI        RUTNUM
     C     RUTKEY        CHAIN     AFORLUR                            90
      *
     C                   MOVE      A2TXT1        AFTXT1
     C                   MOVE      *BLANK        AFTXT2
     C                   MOVE      *BLANK        AFMLD1
     C                   MOVE      *BLANK        AFMLD2
     C                   MOVE      *BLANK        AFMLD3
     C                   MOVE      A2FORM        AFFORM
     C                   MOVE      A2OUTQ        AFOUTQ
     C                   MOVE      A2ACPI        AFACPI
     C                   MOVE      A2ALIN        AFALIN
     C                   MOVE      A2OFLW        AFOFLW
     C                   MOVE      A2AKOP        AFAKOP
     C                   MOVE      A2HLDQ        AFHLDQ
     C                   MOVE      A2PRTY        AFPRTY
     C                   MOVE      A2INST        AFINST
     C                   MOVE      A2PRFI        AFPRFI
     C                   MOVE      A2BGIR        AFBGIR
     C***                MOVE      A2PGIR        AFPGIR
     C***                MOVE      A2JOUR        AFJOUR
     C                   MOVE      A2MVAK        AFMVAK
     C                   MOVE      A2OCRK        AFOCRK
     C                   MOVE      A2PROG        AFPROG
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
     C                   UPDATE    AFORLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      RUTNUM        AFRUTI
     C                   WRITE     AFORLUR
     C                   END
      *
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard NUMMER-SERIER                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         I
     C     I             DOUEQ     6
     C                   ADD       1             I
     C                   MOVE      NUM(I)        A3REC
      *
     C                   MOVE      A3FELL        NUMFEL
     C                   MOVE      A3TYPE        NUMTYP
     C                   MOVE      A3FAST        NUMFAS
     C     NUMKEY        CHAIN     ANUMLUR                            90
      *
     C                   MOVE      A3TXT1        ANTXT1
     C                   MOVEL     A3TXT2        ANTXT2
     C                   MOVE      A3WRAP        ANWRAP
     C                   MOVE      A3ÅRNU        ANÅRNU
     C                   MOVE      A3AUTO        ANAUTO
     C   90              MOVE      A3NFOM        ANNFOM
     C   90              MOVE      A3NTOM        ANNTOM
     C   90              MOVE      A3SIST        ANSIST
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
     C                   UPDATE    ANUMLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      NUMFIR        ANFIRM
     C                   MOVE      NUMFEL        ANFELL
     C                   MOVE      NUMTYP        ANTYPE
     C                   MOVE      NUMFAS        ANFAST
     C                   WRITE     ANUMLUR
     C                   END
      *
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard ORDRETYPER                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         I
     C     I             DOUEQ     4
     C                   ADD       1             I
     C                   MOVE      OTY(I)        F2REC
      *
     C                   MOVE      F2OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYLUR                            90
      *
     C                   MOVE      F2OTXT        VAOTXT
     C                   MOVE      F2OAKK        VAOAKK
     C                   MOVE      F2OBOO        VAOBOO
     C                   MOVE      F2OSKA        VAOSKA
     C                   MOVE      F2OSAM        VAOSAM
     C                   MOVE      F2OLAG        VAOLAG
     C                   MOVE      F2OOKO        VAOOKO
     C                   MOVE      F2OSTA        VAOSTA
     C                   MOVE      F2OPRO        VAOPRO
     C                   MOVE      F2OBUT        VAOBUT
     C                   MOVE      F2OKRE        VAOKRE
     C                   MOVE      F2OBIL        VAOBIL
     C                   MOVE      F2ONXT        VAONXT
     C                   MOVE      F2ORUT        VAORUT
     C                   MOVE      F2ORUB        VAORUB
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
     C                   UPDATE    VOTYLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      OTYFIR        VAOFIR
     C                   MOVE      OTYTYP        VAOTYP
     C                   WRITE     VOTYLUR
     C                   END
      *
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard BUTIKK-KODER                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      STS1(01)      B0REC1
     C                   MOVE      STS2(01)      B0REC2
      *
     C     STSKEY        CHAIN     BSTSLUR                            90
      *
     C                   move      *blank        w_alf1            1
     C                   MOVE      B0AAVS        BAAAVS
     C                   MOVE      B0ADET        BAADET
     C                   MOVE      B0AORD        BAAORD
     C                   MOVE      B0ALST        BAALST
     C                   MOVE      B0ABNR        BAABNR
     C                   MOVE      B0AOPK        BAAOPK
     C                   MOVE      B0AOPS        BAAOPS
     C                   MOVE      B0AMVA        BAAMVA
     C                   MOVE      B0ABIL        BAABIL
      *
     C                   MOVE      B0AOKD        BAAOKD
     C                   MOVE      B0AOKK        BAAOKK
     C                   MOVE      B0AOCD        BAAOCD
     C                   MOVE      B0AOCK        BAAOCK
     C                   MOVE      B0AOFD        BAAOFD
     C                   MOVE      B0AOFK        BAAOFK
     C                   MOVE      B0AOBD        BAAOBD
     C                   MOVE      B0AOBK        BAAOBK
     C                   MOVE      B0AOLD        BAAOLD
     C                   MOVE      B0ALTY        BAALTY
     C                   MOVE      B0ATTY        BAATTY
     C                   MOVE      B0ABIN        BAABIN
     C                   MOVE      B0ABKA        BAABKA
     C                   MOVE      B0ABAV        BAABAV
      *
     C                   MOVE      WPKUND        BAAKUN
     C                   MOVE      WPVARE        BAAVAR
     C                   MOVE      WPKONT        BAAOPK
     C                   MOVE      WPSPES        BAAOPS
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
6.10 c                   time                    baaeda
6.10 c                   time                    baaeti
6.10 c                   eval      baaeus = l_user
     C                   UPDATE    BSTSLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      STSFIR        BAAFIR
6.10 c                   time                    baaoda
6.10 c                   time                    baaoti
6.10 c                   eval      baaous = l_user
6.10 c                   time                    baaeda
6.10 c                   time                    baaeti
6.10 c                   eval      baaeus = l_user
     C                   WRITE     BSTSLUR
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         I                 2 0
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    w_alf1        WPVALG
     C     *LIKE         DEFINE    BAAKUN        WPKUND
     C     *LIKE         DEFINE    BAAVAR        WPVARE
     C     *LIKE         DEFINE    BAAOPK        WPKONT
     C     *LIKE         DEFINE    BAAOPS        WPSPES
      *
      *  Redefinering av nøkler
      *
      *
     C     *LIKE         DEFINE    AFRUTI        RUTNUM
      *
     C     *LIKE         DEFINE    ANFIRM        NUMFIR
     C     *LIKE         DEFINE    ANFELL        NUMFEL
     C     *LIKE         DEFINE    ANTYPE        NUMTYP
     C     *LIKE         DEFINE    ANFAST        NUMFAS
      *
     C     *LIKE         DEFINE    BAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
      *  Key for file : RUTINE-REGISTER
      *
     C     RUTKEY        KLIST
     C                   KFLD                    RUTNUM
      *
      *  Key for file : NUMMER-REGISTER
      *
     C     NUMKEY        KLIST
     C                   KFLD                    NUMFIR
     C                   KFLD                    NUMFEL
     C                   KFLD                    NUMTYP
     C                   KFLD                    NUMFAS
      *
      *  Key for file : BUTIKK-KODE-REGISTER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPVALG
     C                   PARM                    WPKUND
     C                   PARM                    WPVARE
     C                   PARM                    WPKONT
     C                   PARM                    WPSPES
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        NUMFIR
     C                   MOVE      DSSFIR        STSFIR
     C                   MOVE      DSSFIR        OTYFIR
      *
     CSR                 ENDSR
      *
**     R U T I N E R  (1)
BPLUKK    BO925C    Plukkliste                              PAKK      *JOB
BPAKK     BO930C    Pakkseddel                              PAKK      *JOB
BPAKKNEG  BO930C    Pakkseddel retur                        PAKK      *JOB
BFAKT     BO940C    Faktura                                 FAKT      *JOB
BKRED     BO940C    Kreditnota                              FAKT      *JOB
BKONTA4   BO950C    Kontant                                 KONT      *JOB
BKONTA4R  BO950C    Kontant retur                           KONT      *JOB
BKONT     BO950C    Kontant                                 KONT      *JOB
BRETUR    BO950C    Kontant retur                           KONT      *JOB
BINNBET   BO960C    Innbetaling                             KONT      *JOB
BUTBET    BO960C    Utbetaling                              KONT      *JOB
BO124     BO124C    Kontroll/Avstemmingsliste T             0001      *JOB
BO125     BO125C    Kontroll/Avstemmingsliste D             0001      *JOB
BO126     BO126C    Dekningsbidragsliste                    0001      *JOB
**     R U T I N E R  (2)
10072068001000000000
10072068001000000000
10072068001000000000
10072068001000010101
10072068001000010100
10072068001000000010
10072068001000000010
10008000001000000010
10008000001000000010
10008000001000000010
10008000001000000010
10048044001000000000
10048044001000000000
10048044001000000000
**     N U M M E R - K O D E R
BAVSTE            Avstemming,  fellesserie      10A200000999999000000Butikk
BDETAL            Detaljer,    fellesserie      10A200000999999000000Butikk
BLISTE            Listenummer                   10A200000999999000000Butikk
BBILAG            Bilagsnummer                  10A200000099999000000Butikk
BBILAGF           Bilagsnummer, Faktura         10A600000699999600000Butikk
BBILAGK           Bilagsnummer, Kreditnota      10A800000899999800000Butikk
**     B U T I K K   S T A T U S   K O D E R  (I)
BAVSTEBDETALORDRE BLISTEBBILAG               00000000000000000000000010
**     B U T I K K   S T A T U S   K O D E R  (II)
CDCKP PNF K IBUBPL  TX063001
**     O R D R E - T Y P E R
CDKontant Debet                 400011100 01            BKONT
CKKontant Kredit                400011100-03            BRETUR
IBInnbetaling                   500001000 06            BINNBET
UBUtbetaling                    500001000-06            BUTBET
