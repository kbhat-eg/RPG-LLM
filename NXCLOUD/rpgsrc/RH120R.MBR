     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH120R
      * Beskrivelse . . : Budsjett, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040302 KOB  Nytt program
      *       301203 KOB  Fjernet periode 13 pga plassmangel i skjermbildet
T5.30 * V5.30 130704 HFL  Rettet feil v/ F9-skift mellom budsj./alt. budsj.
T5.30 * V5.30 100505 HFL  Valg 8 viser poster på vedkommende konto.
T5.40 * V5.40 040805 HFL  Utvidet parameterliste for RF020R
T5.40 * V5.41 110905 KOB  Fjernet periode ved call RH512R
T5.40 * V5.41 151105 KOB  Økt antall posisjoner i budsjettert beløp
T5.40 * V5501 040307 KOB  Lagt inn periode = 12 som startverdi
T5.50 * V5.50 190607 HFL  Korrigert blaing bakover.
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
6.30  * V6.30     040719  Utvidet saldofelter                            BSA
7.xx  * 7.xx  120516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhsal2    if   e           k disk    rename(rhsapfr:rhsal2r)
     frhsal3    if   e           k disk    rename(rhsapfr:rhsal3r)
     frhsalr    if   e           k disk    rename(rhsapfr:rhsalrr)
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
     fra13l1    uf a e           k disk    rename(ra13pfr:ra13l1r)
T5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
      *
     frh120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
     d txt             s             10    dim(3) ctdata perrcd(1)
6.30 d isa             s             15  2 dim(14)
     d wrk             s             11  0 dim(13)
     d mfo             s              4  2 dim(13)
     d wfo             s             11  6 dim(13)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rhsal1_kont     s                   like(rikont)
     d rhsal2_avde     s                   like(riavde)
     d rhsal3_spes     s                   like(rispes)
     d rhsalr_kont     s                   like(rikont)
     d rhsalr_avde     s                   like(riavde)
     d rhsalr_spes     s                   like(rispes)
     d rhsalr_raar     s                   like(riraar)
     d rhsalr_type     s                   like(ritype)
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_spes     s                   like(rhspes)
     d rhovlr_avde     s                   like(rhavde)
     d ra13l1_mkod     s                   like(ramkod)
      *
T5.40d rklal2_syst     s                   like(rxsyst)
T5.40d rklal2_pros     s                   like(rxpros)
T5.40d rklal2_akti     s                   like(rxakti)
T5.40d rklal2_kont     s                   like(rxkont)
T5.40d rklal2_spes     s                   like(rxspes)
T5.40d rklal2_avde     s                   like(rxavde)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rifirm)
     d w_raar          s                   like(riraar)
     d w_rper          s              2  0
     d w_type          s                   like(ritype)
     d w_sabe          s              1  0
      *
     d wk1hlp          s              5
     d wk1typ          s              1  0
     d wk2hlp          s              5
     d wk2typ          s              1  0
     d wk3hlp          s              5
     d wk3typ          s              1  0
     d wk4hlp          s              5
     d wk4typ          s              1  0
      *
     d work0           s             13  0
     d work2           s             11  0
     d w_sude          s              1
     d w2rper          s              2  0
      *
     d p_kont          s                   like(rikont)
     d p_spes          s                   like(rispes)
     d p_avde          s                   like(riavde)
     d p_stat          s              1
     d p_text          s                   like(rhtext)
     d p_bkod          s                   like(rispes)
     d p_btxt          s             30
     d p_gkod          s                   like(riavde)
     d p_gtxt          s             20
     d p_mkod          s                   like(rhbnØk)
     d p_mtxt          s             30
      *
     d s_kont          s                   like(rikont)
     d s_spes          s                   like(rispes)
     d s_avde          s                   like(riavde)
     d s_raar          s                   like(riraar)
      *
     d akkbel          s             13  0
     d akkpro          s             11  6
     d n               s              2  0
     d werror          s              1  0
      *
      * Parameter - program RF020R
     i/COPY QCPYLESRC,RF020PAR
     d*                ds
E5.40d*rxulda                       175
     d* rxstat                        1    overlay(rxulda)
     d* rxfirm                        3  0 overlay(rxulda:2)
     d* rxsyst                        1    overlay(rxulda:5)
E5.40d* rxpros                        6  0 overlay(rxulda:6)
     d* rxakti                        3    overlay(rxulda:12)
     d* rxf001                        3    overlay(rxulda:15)
     d* rxkont                        6  0 overlay(rxulda:18)
     d* rxspes                        4  0 overlay(rxulda:24)
     d* rxavde                        4  0 overlay(rxulda:28)
     d* rxlinj                        4  0 overlay(rxulda:32)
     d* rxtgrp                        3    overlay(rxulda:36)
     d* rxtext                       78    overlay(rxulda:39)
E5.40d* rxf002                       13    overlay(rxulda:117)
E5.40d* rxvalg                        1  0 overlay(rxulda:130)
T5.40d* rxtist                         z   overlay(rxulda:131)
T5.40d* rxbiln                        6  0 overlay(rxulda:158)
T5.40d* rxrper                        2  0 overlay(rxulda:164)
T5.40d* rxraar                        4  0 overlay(rxulda:166)
T5.40d* rxonr                         6  0 overlay(rxulda:170)
     d rxupar          s                   like(rxulda)
      *
     d saavde          s                   like(riavde)
     d safmet          s                   like(c2fmet)
     d safnØk          s                   like(c2fnØk)
     d sapros          s                   like(c2pros)
     d sakont          s                   like(rikont)
     d sarper          s                   like(c2rper)
     d saspes          s                   like(rispes)
     d satbel          s                   like(c2tbel)
     d satext          s                   like(b1text)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Redefinering til array's
      *
     irhsal1r
     i              risa01                      isa(1)
     i              risa02                      isa(2)
     i              risa03                      isa(3)
     i              risa04                      isa(4)
     i              risa05                      isa(5)
     i              risa06                      isa(6)
     i              risa07                      isa(7)
     i              risa08                      isa(8)
     i              risa09                      isa(9)
     i              risa10                      isa(10)
     i              risa11                      isa(11)
     i              risa12                      isa(12)
     i              risa13                      isa(13)
     i              risa00                      isa(14)
      *
     irhsal2r
     i              risa01                      isa(1)
     i              risa02                      isa(2)
     i              risa03                      isa(3)
     i              risa04                      isa(4)
     i              risa05                      isa(5)
     i              risa06                      isa(6)
     i              risa07                      isa(7)
     i              risa08                      isa(8)
     i              risa09                      isa(9)
     i              risa10                      isa(10)
     i              risa11                      isa(11)
     i              risa12                      isa(12)
     i              risa13                      isa(13)
     i              risa00                      isa(14)
      *
     irhsal3r
     i              risa01                      isa(1)
     i              risa02                      isa(2)
     i              risa03                      isa(3)
     i              risa04                      isa(4)
     i              risa05                      isa(5)
     i              risa06                      isa(6)
     i              risa07                      isa(7)
     i              risa08                      isa(8)
     i              risa09                      isa(9)
     i              risa10                      isa(10)
     i              risa11                      isa(11)
     i              risa12                      isa(12)
     i              risa13                      isa(13)
     i              risa00                      isa(14)
      *
     irhsalrr
     i              risa01                      isa(1)
     i              risa02                      isa(2)
     i              risa03                      isa(3)
     i              risa04                      isa(4)
     i              risa05                      isa(5)
     i              risa06                      isa(6)
     i              risa07                      isa(7)
     i              risa08                      isa(8)
     i              risa09                      isa(9)
     i              risa10                      isa(10)
     i              risa11                      isa(11)
     i              risa12                      isa(12)
     i              risa13                      isa(13)
     i              risa00                      isa(14)
      *
      /eject
      *
     c     dummy         tag
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      * V1WIN  - V1 Vindu for kolonnestyring
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
      * F3/F12
     c                   when      *inkc or *inkl
     c                   goto      avslutt
      * F4
     c                   when      *inkd = *on
     c                   if        w_afld = 'B2KONT'
      *                                                                 .
     c                   call      'RH500R'
T400 c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_kont <> *zero
     c                   move      p_kont        b2kont
     c                   move      p_spes        b2spes
     c                   move      p_avde        b2avde
     c                   goto      b2taga
     c                   endif
      *                                                                 .
     c                   endif
      *
     c                   if        w_afld = 'B2SPES'
      *
     c                   call      'RA528R'
     c                   parm                    w_firm
     c                   parm                    p_bkod
     c                   parm                    p_btxt
      *                                                                 .
     c                   if        p_bkod <> *zero
     c                   move      p_bkod        b2spes
     c                   goto      b2taga
     c                   endif
      *                                                                 .
     c                   endif
      *
     c                   if        w_afld = 'B2AVDE'
      *
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *                                                                 .
     c                   if        p_gkod <> *zero
     c                   move      p_gkod        b2avde
     c                   goto      b2taga
     c                   endif
      *                                                                 .
     c                   endif
      * F5
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
      * F6
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
      * F7
      * Call rutine for overføring av saldo/budsjett
      *
     c                   when      *inkg
     c                   call      'RH125C'
     c                   goto      b2tagb
      * F8
      * Call rutine for omberegning av saldo/budsjett
      *
     c                   when      *inkh
     c                   call      'RH127C'
     c                   goto      b2tagb
      * F9
     c                   when      *inki
     c                   if        w_type = 3
     c                   eval      w_type = 4
     c                   eval      v1inh3 = 'A'
     c                   eval      v1inh4 = 'A'
     c                   movel     txt(3)        b2sabu
     c                   else
     c                   eval      w_type = 3
T5.30c                   eval      v1inh3 = 'B'
T5.30c                   eval      v1inh4 = 'B'
     c                   movel     txt(2)        b2sabu
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
      * Roll f
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
      * Roll b
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
      *
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
      *
     c                   endsl
      *
      * Endring i Periode/År ?
      *
     c                   if        b2raar <> w_raar or
     c                             b2rper <> w_rper
     c                   exsr      forny
     c                   eval      w_raar = b2raar
     c                   eval      w_rper = b2rper
     c                   eval      v1aar4 = b2raar
     c                   eval      v1aar3 = b2raar - 1
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kont <> 0 or
     c                             b2spes <> 0 or
     c                             b2avde <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
S5.50c*                  if        d_fcrn <> 0
T5.50c                   if        w_sfrn <> 0
S5.50c*    d_fcrn        chain     b1sfl
T5.50c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhsal1_kont = b1kont
     c     rhsal1_key    setll     rhsal1
     c                   when      w_seqe = 'B'
     c                   eval      rhsal2_avde = b1avde
     c     rhsal2_key    setll     rhsal2
     c                   when      w_seqe = 'A'
     c                   eval      rhsal3_spes = b1spes
     c     rhsal3_key    setll     rhsal3
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Konto
      *
     c                   if        b2kont <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      rhsal1_kont = b2kont
     c     rhsal1_key    setll     rhsal1
     c                   goto      end_pos
     c                   endif
      *
      * Kostnadsbærer
      *
     c                   if        b2spes <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      rhsal3_spes = b2spes
     c     rhsal3_key    setll     rhsal3
     c                   goto      end_pos
     c                   endif
      *
      * Avdeling
      *
     c                   if        b2avde <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      rhsal2_avde = b2avde
     c     rhsal2_key    setll     rhsal2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kont = 0
     c                   eval      b2spes = 0
     c                   eval      b2avde = 0
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kont = b1kont
     c                   eval      k1spes = b1spes
     c                   eval      k1avde = b1avde
     c                   eval      k1raar = b1raar
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kont = b1kont
     c                   eval      d1spes = b1spes
     c                   eval      d1avde = b1avde
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *                                                                 .
      * Call program for spørring                                       .
      * på hovedbokstransaksjoner                                       .
      *                                                                 .
     c                   if        b1valg = 8
     c                   move      b2raar        w_raar
     c                   move      b2rper        w_rper
     c                   move      b1kont        p_kont
     c                   move      b1spes        p_spes
     c                   move      b1avde        p_avde
      *                                                                 .
E5.30c                   call      'RH512R'
     c                   parm                    w_sude
     c                   parm                    w_raar
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
      *                                                                 .
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1kont = b2kont
     c                   eval      c1spes = b2spes
     c                   eval      c1avde = b2avde
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Call program for kontoplan
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C1KONT'
      *                                                                 .
     c                   call      'RH500R'
T400 c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_kont <> *zero
     c                   move      p_kont        c1kont
     c                   move      p_spes        c1spes
     c                   move      p_avde        c1avde
     c                   goto      c1taga
     c                   endif
      *                                                                 .
     c                   endif
      *
      * Call program for kostnadsbærer
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C1SPES'
      *
     c                   call      'RA528R'
     c                   parm                    w_firm
     c                   parm                    p_bkod
     c                   parm                    p_btxt
      *                                                                 .
     c                   if        p_bkod <> *zero
     c                   move      p_bkod        c1spes
     c                   goto      c1taga
     c                   endif
      *                                                                 .
     c                   endif
      *
      * Call program for avdeling
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C1AVDE'
      *
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *                                                                 .
     c                   if        p_gkod <> *zero
     c                   move      p_gkod        c1avde
     c                   goto      c1taga
     c                   endif
      *                                                                 .
     c                   endif
      *
     c                   if        c1kont = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rhsalr_kont = c1kont
     c                   eval      rhsalr_spes = c1spes
     c                   eval      rhsalr_avde = c1avde
     c                   eval      rhsalr_raar = b2raar
     c                   eval      rhsalr_type = w_type
     c     rhsalr_key    chain     rhsalr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1kont = 0
     c                   eval      c1spes = 0
     c                   eval      c1avde = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kont = 0
     c                   eval      b2spes = 0
     c                   eval      b2avde = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
      * Hent budsjettnøkkel
      * Konto-spes-avd
      *
     c                   eval      rhovlr_kont = c1kont
     c                   eval      rhovlr_spes = c1spes
     c                   eval      rhovlr_avde = c1avde
     c     rhovlr_key    chain     rhovlrr
      *
     c                   if        not%found
     c                   eval      rhovlr_avde = *zeros
     c     rhovlr_key    chain     rhovlrr
     c                   endif
      *
     c                   if        not%found
     c                   eval      rhovlr_spes = *zeros
     c     rhovlr_key    chain     rhovlrr
     c                   endif
      *
     c                   if        b1valg = 2 or b1valg = 3
     c                   eval      c2fnØk = 0
     c                   else
     c                   eval      c2fnØk = rhbnØk
     c                   endif
      *
      * Nullstilling
      *
     c                   eval      safnØk = 0
     c                   eval      safmet = 0
     c                   eval      sapros = 0
     c                   eval      sarper = 0
     c                   eval      satbel = 0
     c                   eval      c2fmet = *zero
     c                   eval      c2rper = *zero
     c                   eval      c2tbel = *zero
     c                   eval      c2pros = *zero
     c                   eval      c2kont = 0
     c                   eval      c2spes = 0
     c                   eval      c2avde = 0
      *
     c                   if        b1valg = 3
     c                   eval      c1kont = k1kont
     c                   eval      c1spes = k1spes
     c                   eval      c1avde = k1avde
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exsr      xc2les
      *
     c     c2tagb        tag
      *
T5.40 * Finnes notatark på kontoen?
      *
T5.40c                   eval      rklal2_syst = 'H'
T5.40c                   eval      rklal2_pros = *blank
T5.40c                   eval      rklal2_akti = *blanks
T5.40c                   eval      rklal2_kont = c1kont
T5.40c                   eval      rklal2_spes = c1spes
T5.40c                   eval      rklal2_avde = c1avde
T5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.40c                   if        *in94 = *on
T5.40c                   eval      C2notb = 'NOTATARK'
T5.40c                   endif
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Call program for budsjett-nøkkler
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2FNØK'
     c                   call      'RA513R'
     c                   parm                    w_firm
     c                   parm                    p_mkod
     c                   parm                    p_mtxt
      *                                                                 .
     c                   eval      c2fnØk = p_mkod
     c                   goto      c2taga
     c                   endif
      *
      * Endre kolonne
      *
     c                   if        *inki = *on
     c                   exsr      xv1win
     c                   goto      c2taga
     c                   endif
      *
      * Forrige år
      *
     c                   if        *inke = *on
     c                   eval      v1aar1 = v1aar1 - 1
     c                   eval      v1aar2 = v1aar2 - 1
     c                   eval      v1aar3 = v1aar3 - 1
     c                   goto      c2taga
     c                   endif
      *
      * Neste år
      *
     c                   if        *inkf = *on
     c                   eval      v1aar1 = v1aar1 + 1
     c                   eval      v1aar2 = v1aar2 + 1
     c                   eval      v1aar3 = v1aar3 + 1
     c                   goto      c2taga
     c                   endif
      *
      * Saldo/bevegelse
      *
     c                   if        *inkg = *on
     c                   if        w_sabe = 1
     c                   eval      w_sabe = 2
     c                   goto      c2taga
     c                   else
     c                   eval      w_sabe = 1
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Call til kontoplanen
      *
     c                   if        *inkh = *on
     c                   eval      p_kont = c1kont
     c                   eval      p_spes = c1spes
     c                   eval      p_avde = c1avde
      *
     c                   call      'RH500R'
     c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
     c                   goto      c2taga
     c                   endif
      *
      * Call til program for
      * hovedboksposteringer
      *
     c                   if        *inkj = *on
     c                   eval      w_raar = b2raar
     c                   eval      w_rper = b2rper
     c                   eval      p_kont = c1kont
     c                   eval      p_spes = c1spes
     c                   eval      p_avde = c1avde
      *
     c                   call      'RH510R'
     c                   parm                    w_sude
     c                   parm                    w_raar
     c                   parm                    w_rper
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
      *
     c                   goto      c2taga
     c                   endif
      *
      * Oppkall av notatark.
      *
     c                   if        *inkb = *on
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'H'
E5.40c                   eval      rxpros = *blank
     c                   eval      rxakti = *blanks
     c                   eval      rxkont = c1kont
     c                   eval      rxspes = c1spes
     c                   eval      rxavde = c1avde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   move      b1valg        rxvalg
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
     c                   move      rxulda        rxupar
     c                   call      'RF020R'
     c                   parm                    rxupar
     c                   move      rxupar        rxulda
     c                   goto      c2taga
     c                   endif
      *
      * Kopiere kolonne 3
      * til     kolonne 4
      *
     c                   if        *inkm = *on
     c                   eval      c2k401 = c2k301
     c                   eval      c2k402 = c2k302
     c                   eval      c2k403 = c2k303
     c                   eval      c2k404 = c2k304
     c                   eval      c2k405 = c2k305
     c                   eval      c2k406 = c2k306
     c                   eval      c2k407 = c2k307
     c                   eval      c2k408 = c2k308
     c                   eval      c2k409 = c2k309
     c                   eval      c2k410 = c2k310
     c                   eval      c2k411 = c2k311
     c                   eval      c2k412 = c2k312
     c***                eval      c2k413 = c2k313
     c                   eval      c2k414 = c2k314
     c                   goto      c2tagb
     c                   endif
      *
      * Kan ikke benytte både fordelings-nøkkel og fordelings-metode
      *
     c                   if        c2fnØk <> *zero and
     c                             c2fmet <> *zero
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Når fordelings-metode benyttes, må periode registreres
      *
     c                   if        c2fmet <> *zero and
     c                             c2rper = *zero
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Når fordelings-nøkkel benyttes, må beløp registreres
      *
     c                   if        c2fnØk <> *zero and
     c                             c2tbel = *zero
     c                   eval      *in34 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Når fordelings-metode benyttes, må beløp registreres
      *
     c                   if        c2fmet <> *zero and
     c                             c2tbel = *zero
     c                   eval      *in35 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Når %-sats er registrert må konto registreres
      *
     c                   if        c2pros <> *zero  and
     c                             c2kont = *zero
     c                   eval      *in36 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Er budsjettnøkkel endret ?
      *
     c                   if        c2fnØk <> *zeros and
     c                             c2fnØk <> safnØk
      *
     c                   if        c2fnØk <> 99
     c                   eval      ra13l1_mkod = c2fnØk
     c     ra13l1_key    chain     ra13l1                             61
      *
     c                   if        *in61  = *on
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   else
     c                   eval      mfo(01) = ramp01
     c                   eval      mfo(02) = ramp02
     c                   eval      mfo(03) = ramp03
     c                   eval      mfo(04) = ramp04
     c                   eval      mfo(05) = ramp05
     c                   eval      mfo(06) = ramp06
     c                   eval      mfo(07) = ramp07
     c                   eval      mfo(08) = ramp08
     c                   eval      mfo(09) = ramp09
     c                   eval      mfo(10) = ramp10
     c                   eval      mfo(11) = ramp11
     c                   eval      mfo(12) = ramp12
     c                   eval      mfo(13) = ramp13
     c                   endif
     c                   endif
      *
     c                   exsr      beregn
     c                   eval      safnØk = c2fnØk
     c                   eval      safmet = c2fmet
     c                   eval      sapros = c2pros
     c                   eval      sarper = c2rper
     c                   eval      satbel = c2tbel
     c                   goto      c2tagb
     c                   endif
      *
      * Er fordelingsmetode endret ?
      *
     c                   if        c2fmet <> safmet
     c                   exsr      beregn
     c                   eval      safmet = c2fmet
     c                   eval      safnØk = c2fnØk
     c                   eval      sapros = c2pros
     c                   eval      sarper = c2rper
     c                   eval      satbel = c2tbel
     c                   goto      c2tagb
     c                   endif
      *
      * Er %-sats endret ?
      *
     c                   if        c2pros <> sapros
     c                   exsr      beregn
     c                   eval      sapros = c2pros
     c                   eval      safnØk = c2fnØk
     c                   eval      safmet = c2fmet
     c                   eval      sarper = c2rper
     c                   eval      satbel = c2tbel
     c                   goto      c2tagb
     c                   endif
      *
      * Er regnskapsperiode endret ?
      *
     c                   if        c2rper <> sarper
     c                   exsr      beregn
     c                   eval      sarper = c2rper
     c                   eval      safnØk = c2fnØk
     c                   eval      safmet = c2fmet
     c                   eval      sapros = c2pros
     c                   eval      satbel = c2tbel
     c                   goto      c2tagb
     c                   endif
      *
      * Er budsjettbeløp endret ?
      *
     c                   if        c2tbel <> satbel and
     c                             satbel <> 0
     c                   exsr      beregn
     c                   eval      satbel = c2tbel
     c                   eval      safnØk = c2fnØk
     c                   eval      safmet = c2fmet
     c                   eval      sapros = c2pros
     c                   eval      sarper = c2rper
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rhsalur
      *
     c                   eval      rhsalu_kont = c1kont
     c                   eval      rhsalu_spes = c1spes
     c                   eval      rhsalu_avde = c1avde
     c                   eval      rhsalu_raar = v1aar4
     c                   eval      rhsalu_type = w_type
     c     rhsalu_key    chain     rhsalur
      *
     c                   eval      risa00 = c2k400
     c                   eval      risa01 = c2k401
     c                   eval      risa02 = c2k402
     c                   eval      risa03 = c2k403
     c                   eval      risa04 = c2k404
     c                   eval      risa05 = c2k405
     c                   eval      risa06 = c2k406
     c                   eval      risa07 = c2k407
     c                   eval      risa08 = c2k408
     c                   eval      risa09 = c2k409
     c                   eval      risa10 = c2k410
     c                   eval      risa11 = c2k411
     c                   eval      risa12 = c2k412
     c***                eval      risa13 = c2k413
      *
6.10 c                   time                    riedat
6.10 c                   time                    rietim
6.10 c                   eval      rieusr = l_user
      *
     c                   if        %found
     c                   update    rhsalur
     c                   else
     c                   eval      rifirm = w_firm
     c                   eval      rikont = c1kont
     c                   eval      rispes = c1spes
     c                   eval      riavde = c1avde
     c                   eval      riraar = v1aar4
     c                   eval      ritype = w_type
6.10 c                   time                    riodat
6.10 c                   time                    riotim
6.10 c                   eval      riousr = l_user
     c                   write     rhsalur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1avde = riavde
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rhsalr_kont = d1kont
     c                   eval      rhsalr_spes = d1spes
     c                   eval      rhsalr_avde = d1avde
     c                   eval      rhsalr_raar = b2raar
     c                   eval      rhsalr_type = w_type
     c     rhsalr_key    chain     rhsalr
      *                                                                 .
      * Hente inn kontotekst                                            .
      *                                                                 .
     c                   eval      p_kont = d1kont
     c                   eval      p_spes = d1spes
     c                   eval      p_avde = d1avde
      *                                                                 .
     c                   exsr      xkonto
      *                                                                 .
     c                   eval      d1text = p_text
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rhsalu_kont = d1kont
     c                   eval      rhsalu_spes = d1spes
     c                   eval      rhsalu_avde = d1avde
     c                   eval      rhsalu_raar = w_raar
     c                   eval      rhsalu_type = w_type
     c     rhsalu_key    chain     rhsalur
      *
     c                   delete    rhsalur
      *
     c     end_d1win     endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kont = k1kont
     c                   eval      s_spes = k1spes
     c                   eval      s_avde = k1avde
     c                   eval      s_raar = k1raar
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rhsalr_kont = k1kont
     c                   eval      rhsalr_spes = k1spes
     c                   eval      rhsalr_avde = k1avde
     c                   eval      rhsalr_raar = k1raar
     c                   eval      rhsalr_type = w_type
     c     rhsalr_key    chain     rhsalr
      *
      * Legg ut budsjett fra gammel konto
      *
     c                   if        %found
     c                   eval      k1sa01 = isa(1)
     c                   eval      k1sa02 = isa(2)
     c                   eval      k1sa03 = isa(3)
     c                   eval      k1sa04 = isa(4)
     c                   eval      k1sa05 = isa(5)
     c                   eval      k1sa06 = isa(6)
     c                   eval      k1sa07 = isa(7)
     c                   eval      k1sa08 = isa(8)
     c                   eval      k1sa09 = isa(9)
     c                   eval      k1sa10 = isa(10)
     c                   eval      k1sa11 = isa(11)
     c                   eval      k1sa12 = isa(12)
     c                   eval      k1sa13 = isa(13)
     c                   xfoot     isa           k1sa14
     c                   endif
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1kont = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rhsalr_kont = k1kont
     c                   eval      rhsalr_spes = k1spes
     c                   eval      rhsalr_avde = k1avde
     c                   eval      rhsalr_raar = k1raar
     c                   eval      v1aar4 = k1raar
     c                   eval      v1aar3 = k1raar - 1
     c                   eval      rhsalr_type = w_type
     c     rhsalr_key    chain     rhsalr
     c                   if        %found
     c                   exsr      xc1msg
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kont = s_kont
     c                   eval      c1spes = s_spes
     c                   eval      c1avde = s_avde
      *
      * Legg ut budsjett i vedlikeholdsbildet
      *
     c                   eval      c2k401 = k1sa01
     c                   eval      c2k402 = k1sa02
     c                   eval      c2k403 = k1sa03
     c                   eval      c2k404 = k1sa04
     c                   eval      c2k405 = k1sa05
     c                   eval      c2k406 = k1sa06
     c                   eval      c2k407 = k1sa07
     c                   eval      c2k408 = k1sa08
     c                   eval      c2k409 = k1sa09
     c                   eval      c2k410 = k1sa10
     c                   eval      c2k411 = k1sa11
     c                   eval      c2k412 = k1sa12
     c***                eval      c2k413 = k1sa13
     c                   eval      c2k414 = k1sa14
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   read      rhsal1                                 15
     c                   when      w_seqe = 'A'
     c                   read      rhsal2                                 15
     c                   when      w_seqe = 'B'
     c                   read      rhsal3                                 15
     c                   endsl
      *
     c                   if        %eof or
     c                             rifirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *                                                                 .
      * Aktuelt år ?                                                    .
      *                                                                 .
     c                   if        riraar <> b2raar
     c                   iter
     c                   endif
      *                                                                 .
      * Aktuell record-type ?                                           .
      *                                                                 .
     c                   if        ritype <> w_type
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = rikont
     c                   eval      b1spes = rispes
     c                   eval      b1avde = riavde
     c                   eval      b1raar = riraar
      *
      * Hente inn kontotekst
      *
     c                   eval      p_kont = rikont
     c                   eval      p_spes = rispes
     c                   eval      p_avde = riavde
      *
     c                   exsr      xkonto
      *
     c                   eval      b1text = p_text
      *
      * Summere opp budsjett
      *
     c                   eval      b1sbel = isa(14)
     c     1             do        b2rper        n
     c                   eval      b1sbel = b1sbel + isa(n)
     c                   enddo
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'K'
     c     rhsal1_key    setgt     rhsal1
     c                   readp     rhsal1
     c                   when      w_seqe = 'A'
     c     rhsal2_key    setgt     rhsal2
     c                   readp     rhsal2
     c                   when      w_seqe = 'B'
     c     rhsal3_key    setgt     rhsal3
     c                   readp     rhsal3
     c                   endsl
     c                   endif
      *
E5.50 * Leser tilbake en side
      *
     c                   eval      b_eof  = *off
     c                   eval      w_stel = 0
      *
S5.50c*                  dou       w_stel = c_sfil + 1
T5.50c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   readp     rhsal1
     c                   when      w_seqe = 'A'
     c                   readp     rhsal2
     c                   when      w_seqe = 'B'
     c                   readp     rhsal3
     c                   endsl
      *
     c                   if        %eof or
     c                             rifirm <> w_firm
     c                   eval      b_eof = *on
     c                   leave
     c                   endif
      *                                                                 .
      * Aktuelt år ?                                                    .
      *                                                                 .
     c                   if        riraar <> b2raar
     c                   iter
     c                   endif
      *                                                                 .
      * Aktuell record-type ?                                           .
      *                                                                 .
     c                   if        ritype <> w_type
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
S5.50c*                  if        w_stel <= c_sfil
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhsal1_kont = rikont
     c                   when      w_seqe = 'A'
     c                   eval      rhsal2_avde = riavde
     c                   when      w_seqe = 'B'
     c                   eval      rhsal3_spes = rispes
     c                   endsl
S5.50c*                  endif
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'K'
     c     rhsal1_key    setll     rhsal1
     c                   when      w_seqe = 'A'
     c     rhsal2_key    setll     rhsal2
     c                   when      w_seqe = 'B'
     c     rhsal3_key    setll     rhsal3
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Sjekk om konto finnes i kontoplanen                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xkonto        begsr
      *
     c                   eval      werror = 0
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in83 = *off
     c                   eval      *in84 = *off
     c                   eval      *in85 = *off
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
     c                   if        p_stat <> *blank
     c                   eval      werror = 1
      *                                                                 .
      * Avdeling finnes ikke i koderegisteret                           .
      *                                                                 .
     c                   if        p_stat = 'A'
     c                   eval      *in81 = *on
     c                   endif
      *                                                                 .
      * Avdeling utfylt men konto mangler                               .
      *                                                                 .
     c                   if        p_stat = 'M'
     c                   eval      *in82 = *on
     c                   endif
      *                                                                 .
      * Spesifikasjon utfylt men konto mangler
      *                                                                 .
     c                   if        p_stat = 'M'
     c                   eval      *in83 = *on
     c                   endif
      *                                                                 .
      * Konto mangler i kontoplan                                       .
      *                                                                 .
     c                   if        p_stat = 'N'
     c                   eval      *in84 = *on
     c                   endif
      *                                                                 .
      * Konto er sperret for bruk                                       .
      *                                                                 .
     c                   if        p_stat = 'S'
     c                   eval      *in85 = *on
     c                   endif
      *                                                                 .
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * V1WIN Behandle bilde for endring av kolonne                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xv1win        begsr
      *
     c                   exfmt     v1win
      *
     c                   eval      b2raar = v1aar4
      *
     c     *inkc         cabeq     *on           v1tag1
     c     *inkl         cabeq     *on           v1tag1
      *
     c     v1tag1        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XC2LES Leser file og flytter felter til C2BLD                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2les        begsr
      *
      * Hente inn kontotekst
      *
     c                   eval      p_kont = c1kont
     c                   eval      p_spes = c1spes
     c                   eval      p_avde = c1avde
      *
     c                   exsr      xkonto
      *
     c                   eval      c2text = p_text
      *
      * Legger opp heading over fire kolonner
      *
     c                   select
     c                   when      v1inh1 = 'R'
     c                   movel     txt(1)        c2hed1
     c                   move      1             wk1typ
      *
     c                   when      v1inh1 = 'B'
     c                   movel     txt(2)        c2hed1
     c                   move      3             wk1typ
      *
     c                   when      v1inh1 = 'A'
     c                   movel     txt(3)        c2hed1
     c                   move      4             wk1typ
     c                   endsl
      *
     c                   select
     c                   when      v1inh2 = 'R'
     c                   movel     txt(1)        c2hed2
     c                   move      1             wk2typ
      *
     c                   when      v1inh2 = 'B'
     c                   movel     txt(2)        c2hed2
     c                   move      3             wk2typ
      *
     c                   when      v1inh2 = 'A'
     c                   movel     txt(3)        c2hed2
     c                   move      4             wk2typ
     c                   endsl
      *
     c                   select
     c                   when      v1inh3 = 'R'
     c                   movel     txt(1)        c2hed3
     c                   move      1             wk3typ
      *
     c                   when      v1inh3 = 'B'
     c                   movel     txt(2)        c2hed3
     c                   move      3             wk3typ
      *
     c                   when      v1inh3 = 'A'
     c                   movel     txt(3)        c2hed3
     c                   move      4             wk3typ
     c                   endsl
      *
     c                   select
     c                   when      v1inh4 = 'R'
     c                   movel     txt(1)        c2hed4
     c                   move      1             wk4typ
      *
     c                   when      v1inh4 = 'B'
     c                   movel     txt(2)        c2hed4
     c                   move      3             wk4typ
      *
     c                   when      v1inh4 = 'A'
     c                   movel     txt(3)        c2hed4
     c                   move      4             wk4typ
     c                   endsl
      *
      * Legger opp årstall i de fire kolonner
      *
     c                   movel     v1aar1        wk1hlp
     c                   movel     v1aar2        wk2hlp
     c                   movel     v1aar3        wk3hlp
     c                   movel     v1aar4        wk4hlp
     c                   move      wk1hlp        c2hed1
     c                   move      wk2hlp        c2hed2
     c                   move      wk3hlp        c2hed3
     c                   move      wk4hlp        c2hed4
      *
      * Henter data inn fra saldomatrise og plasserer i skjermbildet
      *
     c                   eval      rhsalr_kont = c1kont
     c                   eval      rhsalr_spes = c1spes
     c                   eval      rhsalr_avde = c1avde
      *
      * Kolonne 1
      *
     c                   eval      rhsalr_raar = v1aar1
     c                   eval      rhsalr_type = wk1typ
     c     rhsalr_key    chain     rhsalr
      *
     c                   if        not%found
     c                   eval      isa = 0
     c                   endif
      *
     c                   if        w_sabe = 1
     c                   eval      c2k100 = isa(14)
     c                   eval      c2k101 = isa(1)
     c                   eval      c2k102 = isa(2)
     c                   eval      c2k103 = isa(3)
     c                   eval      c2k104 = isa(4)
     c                   eval      c2k105 = isa(5)
     c                   eval      c2k106 = isa(6)
     c                   eval      c2k107 = isa(7)
     c                   eval      c2k108 = isa(8)
     c                   eval      c2k109 = isa(9)
     c                   eval      c2k110 = isa(10)
     c                   eval      c2k111 = isa(11)
     c                   eval      c2k112 = isa(12)
     c***                eval      c2k113 = isa(13)
     c                   xfoot     isa           c2k114
     c                   endif
      *
     c                   if        w_sabe = 2
     c                   eval      c2k100 = isa(14)
     c                   eval      c2k101 = c2k100 + isa(1)
     c                   eval      c2k102 = c2k101 + isa(2)
     c                   eval      c2k103 = c2k102 + isa(3)
     c                   eval      c2k104 = c2k103 + isa(4)
     c                   eval      c2k105 = c2k104 + isa(5)
     c                   eval      c2k106 = c2k105 + isa(6)
     c                   eval      c2k107 = c2k106 + isa(7)
     c                   eval      c2k108 = c2k107 + isa(8)
     c                   eval      c2k109 = c2k108 + isa(9)
     c                   eval      c2k110 = c2k109 + isa(10)
     c                   eval      c2k111 = c2k110 + isa(11)
     c                   eval      c2k112 = c2k111 + isa(12)
     c***                eval      c2k113 = c2k112 + isa(13)
     c***                eval      c2k114 = c2k113
     c                   eval      c2k114 = c2k112 + isa(13)
     c                   endif
      *
      * Kolonne 2
      *
     c                   eval      rhsalr_raar = v1aar2
     c                   eval      rhsalr_type = wk2typ
     c     rhsalr_key    chain     rhsalr
      *
     c                   if        not%found
     c                   eval      isa = 0
     c                   endif
      *
     c                   if        w_sabe = 1
     c                   eval      c2k200 = isa(14)
     c                   eval      c2k201 = isa(1)
     c                   eval      c2k202 = isa(2)
     c                   eval      c2k203 = isa(3)
     c                   eval      c2k204 = isa(4)
     c                   eval      c2k205 = isa(5)
     c                   eval      c2k206 = isa(6)
     c                   eval      c2k207 = isa(7)
     c                   eval      c2k208 = isa(8)
     c                   eval      c2k209 = isa(9)
     c                   eval      c2k210 = isa(10)
     c                   eval      c2k211 = isa(11)
     c                   eval      c2k212 = isa(12)
     c***                eval      c2k213 = isa(13)
     c                   xfoot     isa           c2k214
     c                   endif
      *
     c                   if        w_sabe = 2
     c                   eval      c2k200 = isa(14)
     c                   eval      c2k201 = c2k200 + isa(1)
     c                   eval      c2k202 = c2k201 + isa(2)
     c                   eval      c2k203 = c2k202 + isa(3)
     c                   eval      c2k204 = c2k203 + isa(4)
     c                   eval      c2k205 = c2k204 + isa(5)
     c                   eval      c2k206 = c2k205 + isa(6)
     c                   eval      c2k207 = c2k206 + isa(7)
     c                   eval      c2k208 = c2k207 + isa(8)
     c                   eval      c2k209 = c2k208 + isa(9)
     c                   eval      c2k210 = c2k209 + isa(10)
     c                   eval      c2k211 = c2k210 + isa(11)
     c                   eval      c2k212 = c2k211 + isa(12)
     c***                eval      c2k213 = c2k212 + isa(13)
     c***                eval      c2k214 = c2k213
     c                   eval      c2k214 = c2k212 + isa(13)
     c                   endif
      *
      * Kolonne 3
      *
     c                   eval      rhsalr_raar = v1aar3
     c                   eval      rhsalr_type = wk3typ
     c     rhsalr_key    chain     rhsalr
      *
     c                   if        not%found
     c                   eval      isa = 0
     c                   endif
      *
     c                   if        w_sabe = 1
     c                   eval      c2k300 = isa(14)
     c                   eval      c2k301 = isa(1)
     c                   eval      c2k302 = isa(2)
     c                   eval      c2k303 = isa(3)
     c                   eval      c2k304 = isa(4)
     c                   eval      c2k305 = isa(5)
     c                   eval      c2k306 = isa(6)
     c                   eval      c2k307 = isa(7)
     c                   eval      c2k308 = isa(8)
     c                   eval      c2k309 = isa(9)
     c                   eval      c2k310 = isa(10)
     c                   eval      c2k311 = isa(11)
     c                   eval      c2k312 = isa(12)
     c***                eval      c2k313 = isa(13)
     c                   xfoot     isa           c2k314
     c                   endif
      *
     c                   if        w_sabe = 2
     c                   eval      c2k300 = isa(14)
     c                   eval      c2k301 = c2k300 + isa(1)
     c                   eval      c2k302 = c2k301 + isa(2)
     c                   eval      c2k303 = c2k302 + isa(3)
     c                   eval      c2k304 = c2k303 + isa(4)
     c                   eval      c2k305 = c2k304 + isa(5)
     c                   eval      c2k306 = c2k305 + isa(6)
     c                   eval      c2k307 = c2k306 + isa(7)
     c                   eval      c2k308 = c2k307 + isa(8)
     c                   eval      c2k309 = c2k308 + isa(9)
     c                   eval      c2k310 = c2k309 + isa(10)
     c                   eval      c2k311 = c2k310 + isa(11)
     c                   eval      c2k312 = c2k311 + isa(12)
     c***                eval      c2k313 = c2k312 + isa(13)
     c***                eval      c2k314 = c2k313
     c                   eval      c2k314 = c2k312 + isa(13)
     c                   endif
      *
      * Kolonne 4
      *
     c                   eval      rhsalr_raar = v1aar4
     c                   eval      rhsalr_type = wk4typ
     c     rhsalr_key    chain     rhsalr
      *
     c                   if        not%found
     c                   eval      isa = 0
     c                   endif
      *
     c                   if        w_sabe = 1 and
     c                             b1valg <> 3
     c                   eval      c2k400 = isa(14)
     c                   eval      c2k401 = isa(1)
     c                   eval      c2k402 = isa(2)
     c                   eval      c2k403 = isa(3)
     c                   eval      c2k404 = isa(4)
     c                   eval      c2k405 = isa(5)
     c                   eval      c2k406 = isa(6)
     c                   eval      c2k407 = isa(7)
     c                   eval      c2k408 = isa(8)
     c                   eval      c2k409 = isa(9)
     c                   eval      c2k410 = isa(10)
     c                   eval      c2k411 = isa(11)
     c                   eval      c2k412 = isa(12)
     c***                eval      c2k413 = isa(13)
     c                   xfoot     isa           c2k414
     c                   endif
      *
     c                   if        w_sabe = 2 and
     c                             %found
     c                   eval      c2k400 = isa(14)
     c                   eval      c2k401 = c2k400 + isa(1)
     c                   eval      c2k402 = c2k401 + isa(2)
     c                   eval      c2k403 = c2k402 + isa(3)
     c                   eval      c2k404 = c2k403 + isa(4)
     c                   eval      c2k405 = c2k404 + isa(5)
     c                   eval      c2k406 = c2k405 + isa(6)
     c                   eval      c2k407 = c2k406 + isa(7)
     c                   eval      c2k408 = c2k407 + isa(8)
     c                   eval      c2k409 = c2k408 + isa(9)
     c                   eval      c2k410 = c2k409 + isa(10)
     c                   eval      c2k411 = c2k410 + isa(11)
     c                   eval      c2k412 = c2k411 + isa(12)
     c***                eval      c2k413 = c2k412 + isa(13)
     c***                eval      c2k414 = c2k413
     c                   eval      c2k414 = c2k412 + isa(13)
     c                   endif
      *
      * Hente inn budsjett-nøkkel og beregn
      *
     c                   eval      mfo = *zero
      *
     c                   if        c2fnØk <> 99 and
     c                             c2fnØk <> *zero
     c                   eval      ra13l1_mkod = c2fnØk
     c     ra13l1_key    chain     ra13l1                             61
      *
     c                   if        %found
     c                   eval      c2mp01 = ramp01
     c                   eval      c2mp02 = ramp02
     c                   eval      c2mp03 = ramp03
     c                   eval      c2mp04 = ramp04
     c                   eval      c2mp05 = ramp05
     c                   eval      c2mp06 = ramp06
     c                   eval      c2mp07 = ramp07
     c                   eval      c2mp08 = ramp08
     c                   eval      c2mp09 = ramp09
     c                   eval      c2mp10 = ramp10
     c                   eval      c2mp11 = ramp11
     c                   eval      c2mp12 = ramp12
     c***                eval      c2mp13 = ramp13
      *
     c                   eval      mfo(01) = ramp01
     c                   eval      mfo(02) = ramp02
     c                   eval      mfo(03) = ramp03
     c                   eval      mfo(04) = ramp04
     c                   eval      mfo(05) = ramp05
     c                   eval      mfo(06) = ramp06
     c                   eval      mfo(07) = ramp07
     c                   eval      mfo(08) = ramp08
     c                   eval      mfo(09) = ramp09
     c                   eval      mfo(10) = ramp10
     c                   eval      mfo(11) = ramp11
     c                   eval      mfo(12) = ramp12
     c***                eval      mfo(13) = ramp13
     c                   xfoot     mfo           c2mpxf
     c                   endif
     c                   endif
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Beregner nytt budsjett                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beregn        begsr
      *
     c                   if        c2fnØk <> *zero
      *                                                                 .
     c     1             do        12            n
     c                   if        c2fnØk = 99
     c                   eval      wfo(n) = 100 / 12
     c                   else
     c                   eval      wfo(n) = mfo(n)
     c                   endif
     c                   enddo
      *                                                                 .
     c                   endif
      *                                                                 .
     c                   if        c2fnØk = 99
     c                   eval      c2mp01 = 100 / 12
     c                   eval      c2mp02 = 100 / 12
     c                   eval      c2mp03 = 100 / 12
     c                   eval      c2mp04 = 100 / 12
     c                   eval      c2mp05 = 100 / 12
     c                   eval      c2mp06 = 100 / 12
     c                   eval      c2mp07 = 100 / 12
     c                   eval      c2mp08 = 100 / 12
     c                   eval      c2mp09 = 100 / 12
     c                   eval      c2mp10 = 100 / 12
     c                   eval      c2mp11 = 100 / 12
     c                   eval      c2mp12 = 100 / 12
     c***                eval      c2mp13 = 0
     c                   eval      c2mpxf = 100
     c                   endif
      *
     c                   if        c2fnØk <> *zero
      *                                                                 .
     c                   eval      akkpro = *zero
     c                   eval      akkbel = *zero
      *                                                                 .
     c     1             do        13            n
     c                   eval      akkpro = akkpro + wfo(n)
     c                   eval(h)   work2 = akkpro * c2tbel /100
     c                   eval      wrk(n) = work2 - akkbel
     c                   eval      akkbel = work2
     c                   enddo
      *                                                                 .
     c                   eval      c2k400 = *zero
     c                   move      wrk(1)        c2k401
     c                   move      wrk(2)        c2k402
     c                   move      wrk(3)        c2k403
     c                   move      wrk(4)        c2k404
     c                   move      wrk(5)        c2k405
     c                   move      wrk(6)        c2k406
     c                   move      wrk(7)        c2k407
     c                   move      wrk(8)        c2k408
     c                   move      wrk(9)        c2k409
     c                   move      wrk(10)       c2k410
     c                   move      wrk(11)       c2k411
     c                   move      wrk(12)       c2k412
     c***                move      wrk(13)       c2k413
     c                   xfoot     wrk           c2k414
     c                   endif
      *
      * Beregne etter fordelingsmetode
      * 1 - 2 - 3 - 4 - 6 - 9
      *
     c                   if        c2fmet <> *zero
     c                   eval      wfo = 0
     c                   endif
      *
     c                   if        c2fmet = 1
     c     c2rper        do        12            n
     c                   eval      wfo(n) = 1
     c                   enddo
     c                   endif
      *
     c                   if        c2fmet = 2
     c     c2rper        do        12            n
     c                   eval      wfo(n) = 1
     c                   enddo     2
     c                   endif
      *
     c                   if        c2fmet = 3
     c     c2rper        do        12            n
     c                   eval      wfo(n) = 1
     c                   enddo     3
     c                   endif
      *
     c                   if        c2fmet = 4
     c     c2rper        do        12            n
     c                   eval      wfo(n) = 1
     c                   enddo     4
     c                   endif
      *
     c                   if        c2fmet = 6
     c     c2rper        do        12            n
     c                   eval      wfo(n) = 1
     c                   enddo     6
     c                   endif
      *
     c                   if        c2fmet <> *zero
     c                   if        c2fmet <> 9
     c                   eval      c2k401 = c2tbel * wfo(1)
     c                   eval      c2k402 = c2tbel * wfo(2)
     c                   eval      c2k403 = c2tbel * wfo(3)
     c                   eval      c2k404 = c2tbel * wfo(4)
     c                   eval      c2k405 = c2tbel * wfo(5)
     c                   eval      c2k406 = c2tbel * wfo(6)
     c                   eval      c2k407 = c2tbel * wfo(7)
     c                   eval      c2k408 = c2tbel * wfo(8)
     c                   eval      c2k409 = c2tbel * wfo(9)
     c                   eval      c2k410 = c2tbel * wfo(10)
     c                   eval      c2k411 = c2tbel * wfo(11)
     c                   eval      c2k412 = c2tbel * wfo(12)
     c***                eval      c2k413 = c2tbel * wfo(13)
      *                                                                 .
     c                   else
      *                                                                 .
     c                   eval      w2rper = 13 - c2rper
      *                                                                 .
     c     c2rper        do        12            n
     c                   eval(h)   wfo(n) = 100 / w2rper
     c                   enddo
      *                                                                 .
     c                   eval      akkpro = *zero
     c                   eval      akkbel = *zero
      *                                                                 .
     c     1             do        13            n
     c                   eval      akkpro = akkpro + wfo(n)
     c                   eval      work0 = akkpro * c2tbel
     c                   eval(h)   work2 = work0 / 100
     c                   eval      wrk(n) = work2 - akkbel
     c                   eval      akkbel = work2
     c                   enddo
      *                                                                 .
     c                   eval      c2k400 = *zero
     c                   move      wrk(1)        c2k401
     c                   move      wrk(2)        c2k402
     c                   move      wrk(3)        c2k403
     c                   move      wrk(4)        c2k404
     c                   move      wrk(5)        c2k405
     c                   move      wrk(6)        c2k406
     c                   move      wrk(7)        c2k407
     c                   move      wrk(8)        c2k408
     c                   move      wrk(9)        c2k409
     c                   move      wrk(10)       c2k410
     c                   move      wrk(11)       c2k411
     c                   move      wrk(12)       c2k412
     c***                move      wrk(13)       c2k413
     c                   xfoot     wrk           c2k414
     c                   endif
      *                                                                 .
     c                   eval      c2k414 = c2k400
     c                   eval      c2k414 = c2k414 + c2k401
     c                   eval      c2k414 = c2k414 + c2k402
     c                   eval      c2k414 = c2k414 + c2k403
     c                   eval      c2k414 = c2k414 + c2k404
     c                   eval      c2k414 = c2k414 + c2k405
     c                   eval      c2k414 = c2k414 + c2k406
     c                   eval      c2k414 = c2k414 + c2k407
     c                   eval      c2k414 = c2k414 + c2k408
     c                   eval      c2k414 = c2k414 + c2k409
     c                   eval      c2k414 = c2k414 + c2k410
     c                   eval      c2k414 = c2k414 + c2k411
     c                   eval      c2k414 = c2k414 + c2k412
     c***                eval      c2k414 = c2k414 + c2k413
     c                   endif
      *
      * Beregn i  % av annen konto
      *
     c                   if        c2kont > 0
      *
     c                   eval      rhsalr_kont = c2kont
     c                   eval      rhsalr_spes = c2spes
     c                   eval      rhsalr_avde = c2avde
     c                   eval      rhsalr_type = w_type
     c                   eval      rhsalr_raar = b2raar
     c     rhsalr_key    chain     rhsalr
      *
     c                   if        %found
     c                   eval(r)   c2k400 = isa(14) * c2pros / 100
     c                   eval(r)   c2k401 = isa(01) * c2pros / 100
     c                   eval(r)   c2k402 = isa(02) * c2pros / 100
     c                   eval(r)   c2k403 = isa(03) * c2pros / 100
     c                   eval(r)   c2k404 = isa(04) * c2pros / 100
     c                   eval(r)   c2k405 = isa(05) * c2pros / 100
     c                   eval(r)   c2k406 = isa(06) * c2pros / 100
     c                   eval(r)   c2k407 = isa(07) * c2pros / 100
     c                   eval(r)   c2k408 = isa(08) * c2pros / 100
     c                   eval(r)   c2k409 = isa(09) * c2pros / 100
     c                   eval(r)   c2k410 = isa(10) * c2pros / 100
     c                   eval(r)   c2k411 = isa(11) * c2pros / 100
     c                   eval(r)   c2k412 = isa(12) * c2pros / 100
     c***                eval(r)   c2k413 = isa(13) * c2pros / 100
      *                                                                 .
     c                   eval      c2k414 = c2k400
     c                   eval      c2k414 = c2k414 + c2k401
     c                   eval      c2k414 = c2k414 + c2k402
     c                   eval      c2k414 = c2k414 + c2k403
     c                   eval      c2k414 = c2k414 + c2k404
     c                   eval      c2k414 = c2k414 + c2k405
     c                   eval      c2k414 = c2k414 + c2k406
     c                   eval      c2k414 = c2k414 + c2k407
     c                   eval      c2k414 = c2k414 + c2k408
     c                   eval      c2k414 = c2k414 + c2k409
     c                   eval      c2k414 = c2k414 + c2k410
     c                   eval      c2k414 = c2k414 + c2k411
     c                   eval      c2k414 = c2k414 + c2k412
     c***                eval      c2k414 = c2k414 + c2k413
     c                   endif
     c                   endif
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på konto
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
      *
      * Key for posisjonering på avdeling
      *
     c     rhsal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal2_avde
      *
      * Key for posisjonering på kostnadsbærer
      *
     c     rhsal3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal3_spes
      *
      * Key for les på konto
      *
     c     rhsalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalr_kont
     c                   kfld                    rhsalr_spes
     c                   kfld                    rhsalr_avde
     c                   kfld                    rhsalr_raar
     c                   kfld                    rhsalr_type
      *
      * Key for oppdatering av konto
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for kontoplan
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Key for budsjettnøkkel
      *
     c     ra13l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra13l1_mkod
      *
T5.40 * Key for notatark
      *
T5.40c     rklal2_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rklal2_syst
T5.40c                   kfld                    rklal2_pros
T5.40c                   kfld                    rklal2_akti
T5.40c                   kfld                    rklal2_kont
T5.40c                   kfld                    rklal2_spes
T5.40c                   kfld                    rklal2_avde
      *
     c                   eval      wk1typ = *zero
     c                   eval      wk2typ = *zero
     c                   eval      wk3typ = *zero
     c                   eval      wk4typ = *zero
     c                   eval      werror = *zero
     c                   eval      n = *zero
     c                   eval      wk1hlp = *blank
     c                   eval      wk2hlp = *blank
     c                   eval      wk3hlp = *blank
     c                   eval      wk4hlp = *blank
      *
      * Parametre til Notatark.
      *
     c                   eval      rxulda = *zeros
      *
      * Legge inn standard valg for kolonner
      *
     c                   eval      v1inh1 = 'R'
     c                   eval      v1inh2 = 'R'
     c                   eval      v1inh3 = 'B'
     c                   eval      v1inh4 = 'B'
      *
      * Legge inn kode for saldo/bevegelse
      *
     c                   eval      w_sabe = 1
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Legge inn heading
      *
     c                   movel     txt(2)        b2sabu
      *
      * Legge inn år/periode
      *
     c                   move      *year         b2raar
     c                   move      12            b2rper
     c                   move      *year         w_raar
     c                   move      12            w_rper
      *
     c                   eval      v1aar1 = b2raar - 1
     c                   eval      v1aar2 = b2raar
     c                   eval      v1aar3 = b2raar - 1
     c                   eval      v1aar4 = b2raar
      *
     c                   eval      w_type = 3
     c                   eval      w_seqe = 'K'
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rhsal1_kont = 0
     c     rhsal1_key    setll     rhsal1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      /eject
**
 Regnskap                  001
 Budsjett                  002
 Alt.budsj.                003
