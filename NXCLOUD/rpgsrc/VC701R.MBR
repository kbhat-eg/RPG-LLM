     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VC701R
      * Beskrivelse . . : Sortiment, hente fra til dato ut fra uke
      *
      *                   Input:  Fra uke, til uke
      *                   Output: Fra dato, til dato
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       031111 bof  Nytt program
6.21  *       300415 bof  tatt i bruk p_INLR
6.22  *       121016 bsa  Endret program for datohåndtering
6.23  *       161116 bof  Feilretting hvis gjeldende uke > fra uke
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
      *
      * Definering av pamametre
      *
     d p_fuke          s                   like(vcfuke)
     d p_tuke          s                   like(vcfuke)
     d p_fdat          s                   like(vcfdat)
     d p_tdat          s                   like(vcfdat)
     d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vcfirm)
     d w_fdaf          s                   like(vcfdat)
     d w_fdat          s                   like(vctdat)
     d w_tdaf          s                   like(vcfdat)
     d w_tdat          s                   like(vctdat)
     d w_fuke          s                   like(vcfuke)
     d w_tuke          s                   like(vctuke)
     d w_udat          s                   like(vctdat)
     d w_uuke          s                   like(vctuke)
     d w_faar          s              4  0
     d w_taar          s              4  0
     d w_uaar          s              4  0
     d w_stat          s              1
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
6.20 c                   eval      w_fuke = p_fuke
6.20 c                   eval      w_tuke = p_tuke
6.20 c                   eval      p_fdat = *loval
6.20 c                   eval      p_tdat = *loval
     c                   time                    w_udat
      *
6.22 c**                 call      'AX711R '
6.22 c                   call      'AS711R '
     c                   parm                    w_udat
     c                   parm                    w_uaar
     c                   parm                    w_uuke
     c                   parm                    w_stat
6.21 c                   parm                    p_inlr
6.23  *
6.23 c                   eval      w_faar = w_uaar
6.23 c                   eval      w_taar = w_uaar
      *
      * hvis fra uke > til uke -  fra til år er i forskjellige år.
      *
     c                   if        w_fuke > w_tuke
      * fra:år   til:år+1
     c                   if        w_uuke <=  w_fuke
     c                   if        w_uuke >   w_tuke
     c                   eval      w_faar = w_uaar
     c                   eval      w_taar = w_uaar + 1
     c                   else
     c                   eval      w_faar = w_uaar - 1
     c                   eval      w_taar = w_uaar
     c                   endif
     c                   else
     c                   eval      w_faar = w_uaar
     c                   eval      w_taar = w_uaar + 1
     c                   endif
      *
     c                   else
6.23  *
6.23  * Fra  til år i samme år
6.23  *
6.23  * Hvis nåværende uke > fra uke --> år + 1
6.23  *
6.23 c                   if        w_uuke > w_fuke
6.23 c                   eval      w_faar = w_faar + 1
6.23 c                   eval      w_taar = w_taar + 1
6.23 c                   else
     c                   eval      w_faar = w_uaar
     c                   eval      w_taar = w_uaar
6.23 c                   endif
     c                   endif
      *  fra dato
6.22 c**                 call      'AX710R '
6.22 c                   call      'AS710R '
     c                   parm                    w_faar
     c                   parm                    w_fuke
     c                   parm                    w_fdaf
     c                   parm                    w_fdat
     c                   parm                    w_stat
6.21 c                   parm                    p_inlr
      *  til dato
6.22 c**                 call      'AX710R '
6.22 c                   call      'AS710R '
     c                   parm                    w_taar
     c                   parm                    w_tuke
     c                   parm                    w_tdaf
     c                   parm                    w_tdat
     c                   parm                    w_stat
6.21 c                   parm                    p_inlr
      *
     c                   eval      p_fdat = w_fdaf
     c                   eval      p_tdat = w_tdat
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
6.21  *
6.22 c**                 call      'AX711R '
6.22 c                   call      'AS711R '
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_uaar
6.21 c                   parm                    w_uuke
6.21 c                   parm                    w_stat
6.21 c                   parm                    p_inlr
6.22 c**                 call      'AX710R '
6.22 c                   call      'AS710R '
6.21 c                   parm                    w_faar
6.21 c                   parm                    w_fuke
6.21 c                   parm                    w_fdaf
6.21 c                   parm                    w_fdat
6.21 c                   parm                    w_stat
6.21 c                   parm                    p_inlr
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_fuke
5.70 c                   parm                    p_tuke
     c                   parm                    p_fdat
     c                   parm                    p_tdat
6.21 c                   parm                    p_inlr
      *
     c                   endsr
