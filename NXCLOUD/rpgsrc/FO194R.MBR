     h option(*nodebugio) datedit(*ymd)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Program . . . . : FO194R
      * Beskrivelse . . : Fakturabehandling, gen. av ClearPark-fakturaformat
      *                   -> Europay / VISA fellesformat
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280604 GRO  Nytt program
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffntrl1    if   e           k disk    rename(fntrpfr:fntrl1r)
     ffntrl2    if   e           k disk    rename(fntrpfr:fntrl2r)
     ffntrl3    if   e           k disk    rename(fntrpfr:fntrl3r)
     ffmkal1    if   e           k disk    rename(fmkapfr:fmkal1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsohel3    if   e           k disk    rename(sohepfr:sohel3r)
     fsodtl1    if   e           k disk    rename(sodtpfr:sodtl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     ffncppf    o  a e           k disk    rename(fncppfr:fncppfr)
     ffntrlu    uf   e           k disk    rename(fntrpfr:fntrlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d                 ds
      *
      * Definering av datastruktur for parameter
      *
6.nu d d_parm                        33
     d  d_aarr                        4  0 overlay(d_parm)
     d  d_otyp                        2    overlay(d_parm:5)
6.nu d  d_ffnr                        8  0 overlay(d_parm:7)
6.nu d  d_tfnr                        8  0 overlay(d_parm:15)
6.nu d  d_prog                       10    overlay(d_parm:23)
6.nu d  d_btch                        1    overlay(d_parm:33)
      *
      * Definering av outputrecord
      *
     d d_orec          s            300
      *
      * Definering av variabler i nøkler
      *
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel3_aarr     s                   like(soaarr)
     d sohel3_numm     s                   like(sonumm)
     d sodtl1_aarr     s                   like(sdaarr)
     d sodtl1_binr     s                   like(sdbinr)
     d rkunl1_kund     s                   like(rkkund)
     d vltyl1_ltyp     s                   like(sdltyp)
     d votyl1_otyp     s                   like(vaotyp)
     d fntrl2_kftp     s                   like(fnkftp)
     d fntrl3_kftp     s                   like(fnkftp)
     d fntrl3_binr     s                   like(fnbinr)
     d fntrlu_numm     s                   like(fnnumm)
     d fntrlu_suff     s                   like(fnsuff)
     d fmkal1_kund     s                   like(fmkund)
     d fmkal1_kpro     s                   like(fmkpro)
     d fmkal1_kftp     s                   like(fmkftp)
     d fmkal1_ktsq     s                   like(fmktsq)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_numf          s             20
     d w_posm          s              4  0
     d w_fsum          s             15
     d w_tmva          s             13  2
     d w_fmva          s             15
     d w_trab          s             13  2
     d w_frab          s             15
     d w_npri          s             13  2
     d w_lbto          s             13  2
     d w_raba          s              5  2
     d w_kref          s             17
     d w_fuor          s              9
     d w_vlin          s              3  0
     d w_vlix          s              3
     d w_dadr          s            120
     d w_dadu          s             50
     d w_kzip          s              5
     d w_kcny          s              3
     d w_ldat          s             10
     d w_wdat          s             10
     d w_vtek          s             26
     d w_antx          s             11  2
     d w_anta          s             14
     d w_enhp          s             11
     d w_mvap          s              8
     d w_mvkk          s             13  2
     d w_mvak          s             16
     d w_rkrt          s             11  2
     d w_rabk          s             13
     d w_lsum          s             15
     d w_nprx          s             13  2
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_mapi          s             25
     d w_filn          s             25
      *
      * Definering av meldingselementer
      *
     d w_fakh          s            300
     d w_fakl          s            300
     d w_reci          s              2
6.nu d w_binr          s              8
     d w_krnr          s             16
     d w_expd          s              4
      *
     d b_omkj          s              1    inz(*off)
     d w_crrt          s              1    inz(X'13')
      *
     d s_binr          s                   like(sobinr)
     d s_numm          s                   like(sonumm)
      *
      * Definering av parametre
6.nu d p_parm          s             33
     d p_mapi          s             25
     d p_filn          s             25
      *
     d p_firm          s                   like(sofirm)
     d p_aarr          s                   like(soaarr)
     d p_binr          s                   like(sobinr)
6.nu d p_numm          s                   like(sonumm)
     d p_suff          s                   like(sosuff)
      *
     d p_stat          s              1
     d p_ekod          s              1
     d p_etxt          s             30
     d p_dato          s               d   datfmt(*iso)
     d p_mvap          s              6  2
     d p_mvat          s              5  4
     d p_mvaf          s             10  9
      *
     d c_apos          c                   x'7D'
      *
      * Meldingskonstanter
      *
     d c_fsep          c                   ','
     d c_fquo          c                   '"'
     d c_zero          c                   '0.00'
     d c_zeroh         c                   '0'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser firmadata ifm utstederdata
      *
     c     afir_key      chain     afirl1
      *
      * Hvis online m/parameter : Sett indikator for omkjøring
      *
     c                   if        d_btch <> 'Y' and
     c                             (d_ffnr <> *zero or
     c                             d_tfnr <> *zero)
     c                   eval      b_omkj = *on
     c                   endif
      *
     c                   if        b_omkj = *off
     c                   exsr      sett_binr
     c                   endif
      *
     c                   time                    w_udat
     c                   time                    w_utim
      *
     c                   eval      fntrl3_kftp = *zero
     c                   eval      fntrl3_binr = *zero
     c     fntrl3_key    setll     fntrl3
     c                   read      fntrl3
      *
     c                   dow       not %eof
      *
      * VISA Purchasing - trans.type 01
      * Europay Purchasing - trans.type 02
      *
     c                   if        fnkftp <> 1 and
     c                             fnkftp <> 2
     c                   goto      neste_tr
     c                   endif
      *
      * Sjekk om kjørt tidligere
      *
     c                   if        b_omkj = *off
     c                   if        fnjour <> *zero or
     c                             fnkdat <> *loval
     c                   goto      neste_tr
     c                   endif
     c                   endif
      *
      * Ordre ikke knyttet til faktura
      *
     c                   if        fnbinr = *zero
     c                   goto      neste_tr
     c                   endif
      *
     c                   eval      fmkal1_kund = fnkund
     c                   eval      fmkal1_kpro = fnkpro
     c                   eval      fmkal1_kftp = fnkftp
     c                   eval      fmkal1_ktsq = fnktsq
     c     fmkal1_key    chain     fmkal1r
      *
     c                   if        %found(fmkal1)
     c                   eval      sohel1_aarr = fnaarr
     c                   eval      sohel1_binr = fnbinr
     c     sohel1_key    setgt     sohel1
     c                   readp     sohel1r
      *
     c                   if        not %eof(sohel1)
      *
     c                   if        sobinr <> s_binr
      *
     c                   if        b_omkj = *off
     c                   exsr      fakt_hode
     c                   exsr      fakt_vlin
     c                   exsr      oppd_trans
     c                   endif
      *
     c                   if        b_omkj = *on
     c                   if        sobinr >= d_ffnr and
     c                             sobinr <= d_tfnr
     c                   exsr      fakt_hode
     c                   exsr      fakt_vlin
     c                   exsr      oppd_trans
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   if        s_binr = sobinr
     c                   if        s_binr <> *zero
     c                   exsr      oppd_trans
     c                   endif
     c                   endif
      *
     c                   eval      s_binr = sobinr
     c                   endif
      *
     c                   endif
      *
     c     neste_tr      tag
      *
     c                   read      fntrl3
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av fakturahode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fakt_hode     begsr
      *
     c                   eval      w_fakh = *blank
      *
      * Record Indicator
      *
     c                   eval      votyl1_otyp = sootyp
     c     votyl1_key    chain     votyl1r
      *
     c                   if        %found and
     c                             vaokre = '-'
     c                   eval      w_reci = 'C3'
     c                   else
     c                   eval      w_reci = 'C1'
     c                   endif
      *
     c                   if        sofsum < *zero
     c                   eval      w_reci = 'C3'
     c                   endif
      *
     c                   eval      w_fakh = c_fquo + w_reci +
     c                                      c_fquo + c_fsep
      * + "tomt"/reserved felt
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      c_fquo + c_fsep
      *
      * Invoice Number
      *
     c                   move      sobinr        w_binr
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      w_binr + c_fquo + c_fsep
      *
      * Card Number
      *
     c                   move      fmkrnr        w_krnr
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      w_krnr + c_fquo + c_fsep
      *
      * Expiration Date
      *
     c                   move      fmexpd        w_expd
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %subst(w_expd:3:2) +
     c                                      %subst(w_expd:1:2) +
     c                                      c_fquo + c_fsep
      *
      * Total Invoice Amount
      *
     c                   eval      w_fsum = %editc(sofsum : 'M')
     c                   eval      w_numf = w_fsum
     c                   exsr      sjekk_minus
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo + c_fsep
      * + "tomt"/reserved felt
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      c_fquo + c_fsep
      *
      * Freight Amount
      *
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      c_zero + c_fquo + c_fsep
      *
      * Freight Tax Rate
      *
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      c_zero + c_fquo + c_fsep
      *
      * Freight Tax Amount
      *
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      c_zero + c_fquo + c_fsep
      *
      * Total VAT Amount
      *
     c                   exsr      finn_tbel
     c                   eval      w_fmva = %editc(w_tmva : 'M')
     c                   eval      w_numf = w_fmva
     c                   exsr      sjekk_minus
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo + c_fsep
      *
      * Discount Amount (hodepost; skal ikke inneholde rab.sum)
      *
     c*                  if        w_trab <> *zero
     c*                  eval      w_frab = %editc(w_trab : 'M')
     c*                  eval      w_numf = w_frab
     c*                  exsr      sjekk_minus
     c*                  eval      w_fakh = %trim(w_fakh) + c_fquo +
     c*                                     %trim(w_numf) +
     c*                                     c_fquo + c_fsep
     c*                  else
     c*                  eval      w_fakh = %trim(w_fakh) + c_fquo +
     c*                                     c_zero + c_fquo + c_fsep
     c*                  endif
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      c_zero + c_fquo + c_fsep
      *
      * Customer Reference
      *
     c                   if        sodref = *blank
     c                   eval      w_kref = 'x'
     c                   else
     c                   eval      w_kref = %trim(%subst(sodref:1:17))
     c                   endif
      *
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_kref) +
     c                                      c_fquo + c_fsep
      *
      * Customer VAT Number
      *
     c                   move      aafftn        w_fuor
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      w_fuor + c_fquo + c_fsep
      *
      * Merchant KID
      *
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(sokidd) +
     c                                      c_fquo + c_fsep
      *
      * Delivery addr.fields
      *
     c                   eval      w_dadr = *blank
     c                   eval      w_kzip = *blank
     c                   eval      w_kcny = *blank
      *
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1
      *
     c                   if        sonavn <> *blank or
     c                             soadr1 <> *blank or
     c                             soadr2 <> *blank or
     c                             sosted <> *blank
     c                   if        sonavn <> *blank
     c                   eval      w_dadr = %trim(sonavn)
     c                   endif
     c                   if        soadr1 <> *blank
     c                   if        w_dadr <> *blank
     c                   eval      w_dadr = %trim(w_dadr) + ', ' +
     c                                      %trim(soadr1)
     c                   else
     c                   eval      w_dadr = %trim(soadr1)
     c                   endif
     c                   endif
     c                   if        soadr2 <> *blank
     c                   if        w_dadr <> *blank
     c                   eval      w_dadr = %trim(w_dadr) + ', ' +
     c                                      %trim(soadr2)
     c                   else
     c                   eval      w_dadr = %trim(soadr2)
     c                   endif
     c                   endif
     c                   if        sosted <> *blank
     c                   if        sosted <> *blank and w_dadr <> *blank
     c                   eval      w_dadr = %trim(w_dadr) + ', ' +
     c                                      %trim(sosted)
     c                   else
     c                   eval      w_dadr = %trim(sosted)
     c                   endif
     c                   endif
     c                   else
     c                   eval      w_dadr = %trim(rknavn)
     c                   if        rkgate <> *blank
     c                   if        w_dadr <> *blank
     c                   eval      w_dadr = %trim(w_dadr) + ', ' +
     c                                       %trim(rkgate)
     c                   else
     c                   eval      w_dadr = %trim(rkgate)
     c                   endif
     c                   endif
     c                   if        rksted <> *blank
     c                   if        w_dadr <> *blank
     c                   eval      w_dadr = %trim(w_dadr) + ', ' +
     c                                      %trim(rksted)
     c                   else
     c                   eval      w_dadr = %trim(rksted)
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        sosted <> *blank
     c                   eval      w_kzip = %subst(sosted:1:5)
     c                   else
     c                   eval      w_kzip = %subst(rksted:1:5)
     c                   endif
      *
     c                   if        rkland <> *blank
     c                   eval      w_kcny = rkland
     c                   else
     c                   eval      w_kcny = 'NOR'
     c                   endif
      *
      * Delivery ZIP code
      *
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_kzip) +
     c                                      c_fquo + c_fsep
      *
      * Delivery Country Code
      *
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_kcny) +
     c                                      c_fquo + c_fsep
      *
      * Delivery Date
      *
     c                   movel     soldat        w_wdat
     c                   eval      w_ldat = *blank
     c                   eval      w_ldat = %subst(w_wdat:9:2) + '-' +
     c                                      %subst(w_wdat:6:2) + '-' +
     c                                      %subst(w_wdat:1:4)
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_ldat) +
     c                                      c_fquo + c_fsep
      *
      * Invoice Date
      *
     c                   movel     sofkda        w_wdat
     c                   eval      w_ldat = *blank
     c                   eval      w_ldat = %subst(w_wdat:9:2) + '-' +
     c                                      %subst(w_wdat:6:2) + '-' +
     c                                      %subst(w_wdat:1:4)
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_ldat) +
     c                                      c_fquo + c_fsep
      *
      * Number of Line items
      *
     c                   eval      w_vlix = %editc(w_vlin : 'M')
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_vlix) +
     c                                      c_fquo + c_fsep
      *
      * Delivery address
      *
     c                   eval      w_dadu = %subst(w_dadr:1:50)
     c                   eval      w_fakh = %trim(w_fakh) + c_fquo +
     c                                      %trim(w_dadu) +
     c                                      c_fquo
      *
     c                   if        w_vlin <> *zero
     c                   eval      d_orec = w_fakh
     c                   exsr      write_outp
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av fakturalinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fakt_vlin     begsr
      *
     c                   eval      sodtl1_aarr = soaarr
     c                   eval      sodtl1_binr = sobinr
      *
     c     sodtl1_key    setll     sodtl1
     c     sodtl1_key    reade     sodtl1
      *
     c                   dow       not %eof
      *
     c                   eval      vltyl1_ltyp = sdltyp
     c     vltyl1_key    chain     vltyl1r
      *
     c                   if        valktx <> 1
      *
     c                   eval      w_fakl = *blank
      *
      * Record Indicator
      *
     c                   eval      w_reci = 'P0'
     c                   eval      w_fakl = c_fquo + w_reci +
     c                                      c_fquo + c_fsep
      *
      * Product Code
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(sdvare) + c_fquo + c_fsep
      *
      * LID Description
      *
     c                   eval      w_vtek = sdtek1
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_vtek) +
     c                                      c_fquo + c_fsep
      *
      * Commodity Code
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      '0000' + c_fquo + c_fsep
      *
      * Quantity
      *
     c                   eval(h)   w_antx = sdanta
     c                   eval      w_anta = %editc(w_antx : 'M')
     c                   eval      w_numf = w_anta
     c                   exsr      sjekk_minus
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo + c_fsep
      *
      * Unit Cost
      *
     c                   eval      w_enhp = %editc(sdsapr : 'M')
     c                   eval      w_numf = w_enhp
     c                   exsr      sjekk_minus
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo + c_fsep
      *
      * Unit Of Measure
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(sdenh1) +
     c                                      c_fquo + c_fsep
      *
      * Kaller program i økonomisystemet / MVA-oppslag
      *
     c                   select
     c                   when      sdkmva = 1
     c                   eval      p_ekod = '4'
     c                   when      sdkmva = 2
     c                   eval      p_ekod = 'H'
     c                   other
     c                   eval      p_ekod = '3'
     c                   endsl
      *
     c                   eval      p_dato = sofkda
      *
     c                   call      'RS755R'
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_etxt
     c                   parm                    p_dato
     c                   parm                    p_mvap
     c                   parm                    p_mvat
     c                   parm                    p_mvaf
      *
      * VAT Rate
      *
     c                   if        p_mvap <> *zero
     c                   eval      w_mvap = %editc(p_mvap : 'M')
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_mvap) +
     c                                      c_fquo + c_fsep
     c                   else
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      c_zeroh +
     c                                      c_fquo + c_fsep
     c                   endif
      *
      * VAT Amount
      *
     c                   eval(h)   w_raba = sdrab1 +
     c                                      ((100 - sdrab1) * sdrab2 / 100)
     c                   eval(h)   w_npri = sdsalg - (sdsalg * w_raba / 100)
     c                   eval      w_nprx = sdsalg - sdrkr1 - sdrkr2 -
     c                                      sdrkro
     c                   eval      w_mvkk = ((w_nprx / 100) * p_mvap)
     c                   eval      w_mvak = %editw(w_mvkk : '          0 .  -')
     c                   eval      w_numf = w_mvak
     c                   exsr      sjekk_minus
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo + c_fsep
      *
      * Discount Amount
      *
     c                   eval      w_rkrt = sdrkr1 + sdrkr2 + sdrkro
     c                   eval      w_rabk = %editc(w_rkrt : 'M')
     c                   eval      w_numf = w_rabk
     c                   exsr      sjekk_minus
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo + c_fsep
      *
      * Cost Center
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      c_fquo + c_fsep
      *
      * Cost Type
      *
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      c_fquo + c_fsep
      *
      * Line item sub total
      *
     c                   eval      w_nprx = sdsalg - sdrkr1 - sdrkr2 -
     c                                      sdrkro
     c                   eval      w_lsum = %editc(w_nprx : 'M')
     c                   eval      w_numf = w_lsum
     c                   exsr      sjekk_minus
      *
     c                   if        fmkftp = 1
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo + c_fsep
     c                   else
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(w_numf) +
     c                                      c_fquo
     c                   endif
      *
      * LID reference (Teller)  test-> Sender med NOBB-nr. som LID ref...
      *
     c                   if        fmkftp = 1
     c                   if        %subst(sdvare:1:1) = '1'
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      %trim(sdvare) +
     c                                      c_fquo
     c                   else
     c                   eval      w_fakl = %trim(w_fakl) + c_fquo +
     c                                      c_fquo
     c                   endif
     c                   endif
      *
     c                   eval      d_orec = w_fakl
     c                   exsr      write_outp
      *
     c                   endif
      *
     c     sodtl1_key    reade     sodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for manipulering til ledende minustegn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_minus   begsr
      *
     c                   eval      w_posm = 0
     c                   eval      w_posm = %scan('-':w_numf)
      *
     c                   if        w_posm > 0
     c                   eval      w_numf = %subst(w_numf:1:w_posm - 1)
     c                   eval      w_numf = '-' + %trim(w_numf)
     c                   endif
      *..Skal ikke ha minustegn her overhodet....
     c                   eval      w_numf = %xlate('-':' ':w_numf)
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av fysisk record - m/konv. av bokstaver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     write_outp    begsr
      *
     c                   eval      fncpln = d_orec
     c     'Æ':X'46'     xlate     fncpln        fncpln
     c     'æ':X'A0'     xlate     fncpln        fncpln
     c     'Ø':X'77'     xlate     fncpln        fncpln
     c     'ø':X'90'     xlate     fncpln        fncpln
     c     'Å':X'2A'     xlate     fncpln        fncpln
     c     'å':X'EF'     xlate     fncpln        fncpln
      *
     c                   write     fncppfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for summering av momstotal/rabatt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_tbel     begsr
      *
     c                   eval      sodtl1_aarr = soaarr
     c                   eval      sodtl1_binr = sobinr
     c                   eval      w_tmva = *zero
     c                   eval      w_trab = *zero
     c                   eval      w_vlin = *zero
      *
     c     sodtl1_key    setll     sodtl1
     c     sodtl1_key    reade     sodtl1
      *
     c                   dow       not %eof
      *
     c                   eval      vltyl1_ltyp = sdltyp
     c     vltyl1_key    chain     vltyl1r
      *
     c                   if        valktx <> 1
     c                   eval(h)   w_raba = sdrab1 +
     c                                      ((100 - sdrab1) * sdrab2 / 100)
     c                   eval(h)   w_npri = sdsalg - (sdsalg * w_raba / 100)
     c                   eval      w_nprx = sdsalg - sdrkr1 - sdrkr2 -
     c                                      sdrkro
      *
      * Kaller program i økonomisystemet / MVA-oppslag
      *
     c                   select
     c                   when      sdkmva = 1
     c                   eval      p_ekod = '4'
     c                   when      sdkmva = 2
     c                   eval      p_ekod = 'H'
     c                   other
     c                   eval      p_ekod = '3'
     c                   endsl
      *
     c                   eval      p_dato = sofkda
      *
     c                   call      'RS755R'
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_etxt
     c                   parm                    p_dato
     c                   parm                    p_mvap
     c                   parm                    p_mvat
     c                   parm                    p_mvaf
      *
     c                   eval      w_tmva = w_tmva +
     c                                      ((w_nprx / 100) * p_mvap)
     c                   eval      w_trab = w_trab + sdrkr1 +
     c                                      sdrkr2 + sdrkro
     c                   eval      w_vlin = w_vlin + 1
     c                   endif
      *
     c     sodtl1_key    reade     sodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av trans.reg.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_trans    begsr
      *
     c                   eval      fntrlu_numm = fnnumm
     c                   eval      fntrlu_suff = fnsuff
     c     fntrlu_key    chain     fntrlu
     c                   if        %found(fntrlu)
     c                   eval      fnkdat = w_udat
     c                   eval      fnktim = w_utim
     c                   eval      fnjour = 1
     c                   update    fntrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av fakt.nr. på trans.post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sett_binr     begsr
      *
     c                   eval      fntrl2_kftp = *zero
     c     fntrl2_key    setll     fntrl2
     c                   read      fntrl2
      *
     c                   dow       not %eof(fntrl2)
      *
      * VISA Purchasing - trans.type 01
      * Europay Purchasing - trans.type 02
      *
     c                   if        fnkftp <> 1 and
     c                             fnkftp <> 2
     c                   goto      neste_t2
     c                   endif
      *
      * Sjekk om oppdat. tidligere
      *
     c                   if        fnbinr <> *zero
     c                   goto      neste_t2
     c                   endif
      *
     c                   eval      sohel3_aarr = fnaarr
     c                   eval      sohel3_numm = fnnumm
     c     sohel3_key    setll     sohel3
     c     sohel3_key    reade     sohel3r
      *
     c                   dow       not %eof(sohel3)
     c                   if        sosuff = fnsuff
     c                   eval      fntrlu_numm = fnnumm
     c                   eval      fntrlu_suff = fnsuff
     c     fntrlu_key    chain     fntrlu
     c                   if        %found(fntrlu)
     c                   eval      fnbinr = sobinr
     c                   update    fntrlur
     c                   endif
     c                   endif
     c     sohel3_key    reade     sohel3r
     c                   enddo
      *
     c     neste_t2      tag
     c                   read      fntrl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_parm
     c                   parm                    p_mapi
     c                   parm                    p_filn
      *
     c                   eval      d_aarr = *zero
     c                   eval      d_otyp = *blank
     c                   eval      d_ffnr = *zero
     c                   eval      d_tfnr = *zero
     c                   eval      d_parm = p_parm
     c                   eval      w_mapi = p_mapi
     c                   eval      w_filn = p_filn
      *
     c                   eval      w_firm = l_firm
      *
      *
      * Key for les av kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel3_aarr
     c                   kfld                    sohel3_numm
      *
      * Key for les av fakturalinjer
      *
     c     sodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtl1_aarr
     c                   kfld                    sodtl1_binr
      *
      * Key for linjetyper
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for firma
      *
     c     afir_key      klist
     c                   kfld                    w_firm
      *
      * Key for les av kort/fakt.avtalereg.
      *
     c     fmkal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkal1_kund
     c                   kfld                    fmkal1_kpro
     c                   kfld                    fmkal1_kftp
     c                   kfld                    fmkal1_ktsq
      *
      * Key for kort/fakt trans - type
      *
     c     fntrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fntrl2_kftp
      *
      * Key for kort/fakt trans - type/binr
      *
     c     fntrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fntrl3_kftp
     c                   kfld                    fntrl3_binr
      *
      * Key for oppdat. av kort/fakt trans - type
      *
     c     fntrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fntrlu_numm
     c                   kfld                    fntrlu_suff
      *
     c                   endsr
