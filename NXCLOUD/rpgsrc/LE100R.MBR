     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LE100R
      * Beskrivelse . . : Vedlikehold av telle-liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
6.11  *       140612 bof  Fjernet mulighet til å endre enhet
6.20  *       080410 bø   Utvidet med timestamp for bruk videre
6.21  *       101013 amd  Liten justering i skjermbilde fra bruk av
6.21  *                   Editword til bruk av Editcode. Ingen endring
6.21  *                   i RPG programmet
6.31  *       301215 ask  Endret uf til if i filspesifikasjon på LTELL2
7.xx  * 7.00  190516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       280116 ask  Linjer fra syklisk telling kan ikke oppadteres
7.02  * 7.01  011222 mst  7.01 styres av switch xxx.xxx.xxx
8.01  * 800   211024 ask  Lagt inn mulighet for å kalle opp pgm med batchid
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     fltellr    if   e           k disk    rename(ltelpfr:ltellrr)
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
6.31 fltell2    if   e           k disk    rename(ltelpfr:ltell2r)
     fltellu    uf   e           k disk    rename(ltelpfr:ltellur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fle100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Local Data Area
      *
8.01 d lda            uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
7.02 d l_filg                931    933
8.01 d l_batc               1006   1015
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
8.01  *
8.01  * Definering av variable i parameter
8.01  *
8.01 d p_batc          s                   like(lcbatc)
      *
      * Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d ltellr_batc     s                   like(lebatc)
     d ltellr_numm     s                   like(lenumm)
     d ltellr_suff     s                   like(lesuff)
     d ltellr_line     s                   like(leline)
     d ltell1_batc     s                   like(lebatc)
     d ltell1_numm     s                   like(lenumm)
     d ltell1_suff     s                   like(lesuff)
     d ltell1_line     s                   like(leline)
     d ltell2_batc     s                   like(lebatc)
     d ltell2_numm     s                   like(lenumm)
     d ltell2_suff     s                   like(lesuff)
     d ltell2_vare     s                   like(levare)
     d ltell2_lage     s                   like(lelage)
     d ltellu_batc     s                   like(lebatc)
     d ltellu_numm     s                   like(lenumm)
     d ltellu_suff     s                   like(lesuff)
     d ltellu_line     s                   like(leline)
     d vvarl1_vare     s                   like(vvvare)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(lefirm)
     d w_batc          s                   like(lebatc)
     d w_retu          s              1
     d w_vare          s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
7.01 d w_mld1          s             50
7.01 d w_mld2          s             50
7.01 d w_svar          s              1
7.01 d w_in12          s              1  0
      *
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
7.02   dcl-s co402_verdi1  char(1);
7.02   dcl-s co402_verdi2  char(2048);
7.02   DCL-PR CO402R EXTPGM('CO402R');
7.02     p_filgrp            char(3)     const;
7.02     p_firm              packed(3:0) const;
7.02     p_lager             packed(2:0) const;
7.02     p_nokkel            char(50)    const;
7.02     p_verdi1            char(1);
7.02     p_verdi2            char(2048);
7.02   END-PR;
7.02 d u_702           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2WIN  - C2 Vindu for endring av linje
      * B4WIN  - B4 Vindu for slette
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller opp subprogram for inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.01 c                   if        l_batc <> ' '
8.01 c                   eval      w_batc = l_batc
8.01 c                   else
     c                   eval      w_retu = 'R'
     c                   call      'LC500R'
     c                   parm                    w_retu
     c                   parm                    w_batc
      *
     c                   if        w_retu <> *blank
     c                   goto      end_pgm
     c                   endif
8.01 c                   endif
      *
     c                   eval      lbchl1_batc = w_batc
     c                   eval      ltellr_batc = w_batc
     c                   eval      ltell1_batc = w_batc
     c                   eval      ltell2_batc = w_batc
     c                   eval      ltellu_batc = w_batc
      *
      *  Hent opplysninger fra BATCH-REGISTER
      *
     c     lbchl1_key    chain     lbchl1
     c                   if        %found
     c                   eval      ltellr_numm = lcnumm
     c                   eval      ltell1_numm = lcnumm
     c                   eval      ltell2_numm = lcnumm
     c                   eval      ltellu_numm = lcnumm
     c                   else
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      w_seqe = ' '
     c     ltell1_key    setll     ltell1
     c                   exsr      crt_subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2line <> 0 or
     c                             b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   eval      ltell1_line = b1line
     c     ltell1_key    setll     ltell1
     c                   other
     c                   eval      ltell2_vare = b1vare
     c     ltell2_key    setll     ltell2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på linjenummer
      *
     c                   if        b2line <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ltell1_line = b2line
     c     ltell1_key    setll     ltell1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      ltell2_vare = b2vare
     c     ltell2_key    setll     ltell2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2line = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   exsr      xc2win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   exsr      xb4win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Fortløpende oppdatering
      *
     c                   if        b1valg = 7
     c                   exsr      xc7win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endringsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
7.02 c                   if        u_702 = *off
7.01  * Linjer fra syklisk telling kan ikke oppdateres - må slettes og telles på nytt
7.01  *
7.01 c                   if        lcstel = 1
7.01 c                   eval      w_mld1 = 'Ikke tillatt å endre linjer fra'
7.01 c                   eval      w_mld2 = 'syklisk telling - må slettes'
7.01 c                   eval      w_svar = ' '
7.01 c                   call      'AA007R'
7.01 c                   parm                    w_mld1
7.01 c                   parm                    w_mld2
7.01 c                   parm                    w_svar
7.01 c                   parm                    w_in12
7.01 c                   goto      end_c2win
7.01 c                   endif
7.01  *
7.02 c                   endif
     c                   eval      ltellr_line = b1line
     c     ltellr_key    chain     ltellr
     c                   if        not %found
     c                   goto      end_c2win
     c                   endif
      *
     c                   eval      c2tek1 = letek1
     c                   eval      c2antt = leantt
     c                   eval      c2enh1 = leenh1
     c                   eval      c2spny = lespny
     c                   eval      c2kpny = lekpny
      *
     c     xc2_ut        tag
     c                   exfmt     c2win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_c2win
     c                   endif
      *
      *  Tester gyldige verdier
      *
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      xc2_ut
     c                   endif
      *
      *  Endrer valgt linje i telleliste
      *
     c                   eval      ltellu_line = b1line
     c     ltellu_key    chain     ltellur
     c                   if        %found
     c                   eval      leantt = c2antt
6.11 c*                  eval      leenh1 = c2enh1
     c                   eval      lespny = c2spny
     c                   eval      lekpny = c2kpny
6.10 c                   time                    leedat
6.10 c                   time                    leetim
6.20 c                   time                    letims
6.10 c                   eval      leeusr = l_user
     c                   update    ltellur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb4win        begsr
      *
     c                   eval      b4line = b1line
     c                   eval      b4tek1 = b1tek1
      *
     c                   exfmt     b4win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_b4win
     c                   endif
      *
      *  Sletter valgt linje i telleliste
      *
     c                   eval      ltellu_line = b1line
     c     ltellu_key    chain     ltellur
     c                   if        %found
     c                   delete    ltellur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_b4win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sekvensiell oppdatering etter linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc7win        begsr
      *
7.01  * Linjer fra syklisk telling kan ikke oppdateres - må slettes og telles på nytt
7.01  *
7.01 c                   if        lcstel = 1
7.01 c                   eval      w_mld1 = 'Ikke tillatt å endre linjer fra'
7.01 c                   eval      w_mld2 = 'syklisk telling - må slettes'
7.01 c                   eval      w_svar = ' '
7.01 c                   call      'AA007R'
7.01 c                   parm                    w_mld1
7.01 c                   parm                    w_mld2
7.01 c                   parm                    w_svar
7.01 c                   parm                    w_in12
7.01 c                   goto      end_c7win
7.01 c                   endif
7.01  *
     c                   eval      ltellr_line = b1line
     c     ltellr_key    setll     ltellr
     c                   read      ltellr
      *
     c                   dow       not %eof and
     c                             lebatc = w_batc or
     c                             lenumm = lcnumm
      *
     c                   eval      c2tek1 = letek1
     c                   eval      c2antt = leantt
     c                   eval      c2enh1 = leenh1
     c                   eval      c2spny = lespny
     c                   eval      c2kpny = lekpny
      *
     c     xc7_ut        tag
     c                   exfmt     c2win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_c7win
     c                   endif
      *
      *  Tester gyldige verdier
      *
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      xc7_ut
     c                   endif
      *
      *  Endrer valgt linje i telleliste
      *
     c                   eval      ltellu_line = leline
     c     ltellu_key    chain     ltellur
     c                   if        %found
     c                   eval      leantt = c2antt
6.11 c*                  eval      leenh1 = c2enh1
     c                   eval      lespny = c2spny
     c                   eval      lekpny = c2kpny
6.10 c                   time                    leedat
6.10 c                   time                    leetim
6.10 c                   eval      leeusr = l_user
6.20 c                   time                    letims
     c                   update    ltellur
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   read      ltellr
     c                   enddo
      *
     c     end_c7win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   read      ltell1
     c                   other
     c                   read      ltell2
     c                   endsl
      *
     c                   if        %eof or
     c                             lefirm <> w_firm or
     c                             lebatc <> w_batc or
     c                             lenumm <> lcnumm or
     c                             lesuff <> 0
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ltell1_line = leline
     c                   other
     c                   eval      ltell2_vare = levare
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = leline
     c                   movel     levare        b1vare
     c                   eval      b1lage = lelage
     c                   eval      b1tek1 = letek1
     c                   eval(h)   b1antt = leantt
     c                   eval      b1enh1 = leenh1
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = ' '
     c     ltell1_key    setgt     ltell1
     c                   readp     ltell1
     c                   other
     c     ltell2_key    setgt     ltell2
     c                   readp     ltell2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   readp     ltell1
     c                   other
     c                   readp     ltell2
     c                   endsl
      *
     c                   if        %eof or
     c                             lefirm <> w_firm or
     c                             lebatc <> w_batc or
     c                             lenumm <> lcnumm or
     c                             lesuff <> 0
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   eval      ltell1_line = leline
     c                   other
     c                   eval      ltell2_vare = levare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = ' '
     c     ltell1_key    setll     ltell1
     c                   other
     c     ltell2_key    setll     ltell2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll av gyldig enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_enh     begsr
      *
     c                   eval      b_feil = *off
     c                   movel     levare        vvenlr_vare
     c                   eval      vvenlr_enhe = c2enh1
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   endif
      *
     c     sjekk_end     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   endif
      *
     c                   eval      b2vare = w_vare
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.01  *
8.01  * Definisjon av local data area for output
8.01  *
8.01 c     *dtaara       define    *lda          lda
      *
      *  Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Key for les av TELLELISTE
      *
     c     ltellr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltellr_batc
     c                   kfld                    ltellr_numm
     c                   kfld                    ltellr_suff
     c                   kfld                    ltellr_line
      *
      *  Key for posisjonering av TELLELISTE pr. linje
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
     c                   kfld                    ltell1_numm
     c                   kfld                    ltell1_suff
     c                   kfld                    ltell1_line
      *
      *  Key for posisjonering av TELLELISTE pr. vare/lager
      *
     c     ltell2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell2_batc
     c                   kfld                    ltell2_numm
     c                   kfld                    ltell2_suff
     c                   kfld                    ltell2_vare
     c                   kfld                    ltell2_lage
      *
      *  Key for oppdatering av TELLE
      *
     c     ltellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltellu_batc
     c                   kfld                    ltellu_numm
     c                   kfld                    ltellu_suff
     c                   kfld                    ltellu_line
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      ltellr_suff = 0
     c                   eval      ltellr_line = 0
     c                   eval      ltell1_suff = 0
     c                   eval      ltell1_line = 0
     c                   eval      ltell2_suff = 0
     c                   eval      ltell2_vare = *blank
     c                   eval      ltell2_lage = 0
      *
     c                   eval      *in22 = *on
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'LE100R_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
      *
     c                   endsr
      *
