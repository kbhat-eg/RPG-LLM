     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FX893R
      * Beskrivelse . . : FIX: Oppdaterer EAN-nummer på vareEnhet ut fra
      *                        vareEan
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100709 bof  Nytt program
      *       270510      De som ikke ligger med riktig enhet på veanpf blir
      *                   ikke slettet, men enhet fjernes.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fveanlu    uf   e           k disk    rename(veanpfr:veanlur)
     fvveplu    uf a e           k disk    rename(vveppfr:vveplur)
     flwexpf    uf a e           k disk    rename(lexcpfr:lwexpfr)
      *
      *  Datastruktur - excel-fil
      *
     d                 ds
     d d_ex                         512
     d  d_vare                       15    overlay(d_ex:1)
     d                                1    overlay(d_ex:16)  inz(X'05')
     d  d_tek1                       35    overlay(d_ex:17)
     d                                1    overlay(d_ex:53) inz(X'05')
     d  d_enhe                        3    overlay(d_ex:54)
     d                                1    overlay(d_ex:57) inz(X'05')
     d  d_besk                       35    overlay(d_ex:58)
     d                                1    overlay(d_ex:93) inz(X'05')
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av datastruktur
      *
      * Definering av variable i nøkler
      *
5.60 d vvenl1_vare     s                   like(vevare)
5.60 d vvenl1_enhe     s                   like(veenhe)
5.60 d vveplu_pvar     s                   like(vepvar)
5.60 d vveplu_penh     s                   like(vepenh)
5.60 d vveplu_pldo     s                   like(vepldo)
5.60 d veanl2_firm     s                   like(vxfirm)
5.60 d veanlu_firm     s                   like(vxfirm)
5.60 d veanlu_eann     s                   like(vxeann)
5.60 d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vxfirm)
     d b_oppd          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      veanl2_firm = 0
     c     veanl2_key    setll     veanl2
     c                   read      veanl2
     c                   dow       not %eof(veanl2)
     c                   eval      w_firm = vxfirm
     c                   if        vxenhe <> *blank
     c                   exsr      behandle
     c                   endif
      *
     c                   read      veanl2
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker VA om ean finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandle      begsr
      *
     c                   eval      vvarl1_vare = vxvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   if        %found(vvarl1) and
     c                             vvldor <> 0
      *
     c                   eval      vvenl1_vare = vxvare
     c                   eval      vvenl1_enhe = vxenhe
     c     vvenl1_key    chain     vvenl1
      *
     c                   if        %found(vvenl1)
      *
     c                   clear                   vveplur
     c                   eval      b_oppd = *off
     c                   eval      vveplu_pvar = vxvare
     c                   eval      vveplu_penh = vxenhe
     c                   eval      vveplu_pldo = vvldor
     c     vveplu_key    chain     vveplu
     c                   if        not %found(vveplu)
     c                   eval      b_oppd = *on
     c                   eval      vepfir = w_firm
     c                   eval      vepvar = vveplu_pvar
     c                   eval      vepenh = vveplu_penh
     c                   eval      vepldo = vveplu_pldo
     c                   eval      vepgti = vxeann
6.16 c                   eval      vepeus = l_user
6.16 c                   time                    vepeti
6.16 c                   time                    vepeda
6.16 c                   eval      vepous = l_user
6.16 c                   time                    vepoti
6.16 c                   time                    vepoda
     c                   write     vveplur
     c                   endif
      *
     c                   endif
      *
     c                   if        b_oppd = *on
     c                   eval      veanlu_eann = vxeann
     c     veanlu_key    chain     veanlu
     c                   if        %found(veanlu)
     c                   delete    veanlur
     c                   endif
     c                   else
     c                   eval      veanlu_eann = vxeann
     c     veanlu_key    chain     veanlu
     c                   if        %found(veanlu)
     c                   eval      veenhe = *blank
     c                   update    veanlur
     c                   endif
     c                   endif
      *
     c*                  if        b_oppd = *off
     c*                  eval      d_vare = vxvare
     c*                  eval      d_tek1 = vvtek1
     c*                  eval      d_enhe = vxenhe
     c*                  eval      d_besk = 'Ugyldig enhet - fjernet'
     c*                  eval      exclin   = d_ex
     c*                  write     lwexpfr
     c*                  endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Key for les av ean-register
      *
     c     veanl2_key    klist
     c                   kfld                    veanl2_firm
      *
      * Key for oppdatering av EAN
      *
     c     veanlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlu_eann
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppdatering av vare-enhet-prisgiver
      *
     c     vveplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vveplu_pvar
     c                   kfld                    vveplu_penh
     c                   kfld                    vveplu_pldo
      *
      *
      * Henter firma fra LDA
      *
      *
     c                   endsr
