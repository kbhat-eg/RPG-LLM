     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASOFAK
      *  Program . . . . : NN860R
      *  Beskrivelse . . : Mottak av EDI-best. (Type : Ordre)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *    6.10    250609  Oppdatert med sporingsinformasjon       BSA
6.11  *    6.10   290609   Utivdet eannr til 14 og brukt std. oppslag  BOF
6.nu  * 6.30     170614    Endring i hht. nummerutvidelse          bof
6.30  * 6.30 bkv 180515    Endret LVAR pga økt fra 15 til 20 tegn
6.31  * 6.30 bof 151018    Utvidet med trelast-sortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnwebpf    if   e           k disk
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frkunl5    if   e           k disk    rename(rkunpfr:rkunl5r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fnohelu    uf a e           k disk    rename(nohepfr:nohelur)
     fnodtlu    uf a e           k disk    rename(nodtpfr:nodtlur)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d rkunl5_eank     s                   like(rkeank)
     d nohelu_fsys     s                   like(nofsys)
     d nohelu_tell     s                   like(notell)
     d nodtlu_fsys     s                   like(ndfsys)
     d nodtlu_tell     s                   like(ndtell)
     d nodtlu_line     s                   like(ndline)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(rkfirm)
     d w_vare          s                   like(vvvare)
     d w_bes_alf       s              6
     d w_bes_inn       s             35
     d w_besa          s              6
     d w_besn          s              6
     d w_rect          s              4
     d w_srec          s              4
     d w_laks          s              3
     d w_lenh          s              3
     d w_stat          s              1
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_dato_6        s              6  0
     d w_dato_alf      s              6
     d w_dato_8        s              8  0
     d w_time_6        s              6  0
     d w_time_alf      s              4
     d w_pris          s             15  3
     d w_numm_15       s             15  0
     d w_numm_alf      s             13
     d w_anta          s              9  3
     d w_pos1          s              2  0
     d w_pos2          s              2  0
     d w_atel          s              1  0
     d w_numtst        s             15    inz('000000000000000')
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_type          s              2
6.nu d w_mnum          s              8  0
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.31 d w_lv_vare       s                   like(vvvare)
      *
     d b_bhod          s              1    inz(*off)
     d b_blin          s              1    inz(*off)
     d b_oskr          s              1    inz(*off)
      *
     d c_fsys          c                   'EDI'
     d c_digi          c                   '0123456789'
     d c_char          c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ ab+
     d                                      cdefghijklmnopqrstuvwxyzæøå.,/:-'
      *
      *  Definering av datastrukturer for melding inn
      *
     d                 ds
     d d_ed00                        80
     d  d_id00                        4    overlay(d_ed00)
     d  d_decs00                      1    overlay(d_ed00:5)
     d  d_send00                     13    overlay(d_ed00:6)
     d  d_mott00                     13    overlay(d_ed00:19)
     d  d_gdat00                      6    overlay(d_ed00:32)
     d    d_gdax00                    6  0 overlay(d_gdat00:1)
     d  d_gtim00                      4    overlay(d_ed00:38)
     d    d_gtix00                    4  0 overlay(d_gtim00:1)
     d  d_test00                      1    overlay(d_ed00:42)
     d  d_kva100                      3    overlay(d_ed00:43)
     d  d_mdat00                     12    overlay(d_ed00:46)
     d  d_for100                      3    overlay(d_ed00:58)
     d  d_kva200                      3    overlay(d_ed00:61)
     d  d_levd00                      8    overlay(d_ed00:64)
     d    d_lvda00                    8  0 overlay(d_levd00:1)
     d  d_for200                      3    overlay(d_ed00:69)
      *
     d                 ds
     d d_ed01                        80
     d  d_id01                        4    overlay(d_ed01)
     d  d_mref01                     10    overlay(d_ed01:5)
     d  d_mtyp01                      6    overlay(d_ed01:15)
     d  d_mver01                      3    overlay(d_ed01:21)
     d  d_mrel01                      3    overlay(d_ed01:24)
     d  d_mkon01                      2    overlay(d_ed01:27)
     d  d_mfor01                      6    overlay(d_ed01:29)
     d  d_mkod01                      3    overlay(d_ed01:35)
     d  d_benr01                     35    overlay(d_ed01:38)
     d  d_resp01                      3    overlay(d_ed01:73)
      *
     d                 ds
     d d_ed02                        80
     d  d_id02                        4    overlay(d_ed02)
     d  d_ftxq02                      3    overlay(d_ed02:5)
     d  d_ftxt02                     70    overlay(d_ed02:8)
      *
     d                 ds
     d d_ed03                        80
     d  d_id03                        4    overlay(d_ed03)
     d  d_refq03                      3    overlay(d_ed03:5)
     d  d_refr03                     35    overlay(d_ed03:8)
      *
     d                 ds
     d d_ed04                        80
     d  d_id04                        4    overlay(d_ed04)
     d  d_parq04                      3    overlay(d_ed04:5)
     d  d_part04                     13    overlay(d_ed04:8)
     d    d_pean04                   13  0 overlay(d_part04:1)
     d  d_porg04                      3    overlay(d_ed04:21)
      *
     d                 ds
     d d_ed05                        80
     d  d_id05                        4    overlay(d_ed05)
     d  d_pnav05                     30    overlay(d_ed05:5)
     d  d_pad105                     30    overlay(d_ed05:35)
      *
     d                 ds
     d d_ed06                        80
     d  d_id06                        4    overlay(d_ed06)
     d  d_pad206                     30    overlay(d_ed06:5)
     d  d_ppnu06                      4    overlay(d_ed06:35)
     d  d_ppst06                     30    overlay(d_ed06:39)
      *
     d                 ds
     d d_ed07                        80
     d  d_id07                        4    overlay(d_ed07)
     d  d_refq07                      3    overlay(d_ed07:5)
     d  d_fore07                     14    overlay(d_ed07:8)
     d  d_ctfu07                      3    overlay(d_ed07:22)
     d  d_kont07                     30    overlay(d_ed07:25)
      *
     d                 ds
     d d_ed08                        80
     d  d_id08                        4    overlay(d_ed08)
     d  d_komm08                     70    overlay(d_ed08:5)
     d  d_komq08                      3    overlay(d_ed08:75)
      *
     d                 ds
     d d_ed09                        80
     d  d_id09                        4    overlay(d_ed09)
     d  d_trsq09                      3    overlay(d_ed09:5)
     d  d_trko09                      3    overlay(d_ed09:8)
     d  d_fmko09                      8    overlay(d_ed09:11)
     d  d_lebq09                      3    overlay(d_ed09:19)
     d  d_levb09                      3    overlay(d_ed09:22)
     d  d_levq09                      3    overlay(d_ed09:25)
     d  d_lskq09                      3    overlay(d_ed09:28)
     d  d_lbst09                     13    overlay(d_ed09:31)
     d  d_lsko09                      3    overlay(d_ed09:44)
      *
     d                 ds
     d d_ed10                        80
     d  d_id10                        4    overlay(d_ed10)
     d  d_vlin10                      6    overlay(d_ed10:5)
     d    d_vlix10                    6  0 overlay(d_vlin10:1)
     d  d_eanr10                     15    overlay(d_ed10:11)
     d    d_eanx10                   13  0 overlay(d_eanr10:1)
     d  d_eank10                      3    overlay(d_ed10:26)
     d  d_piq110                      3    overlay(d_ed10:29)
     d  d_alt110                     15    overlay(d_ed10:32)
     d    d_altx10                    8  0 overlay(d_alt110:1)
     d  d_alk110                      3    overlay(d_ed10:47)
     d  d_piq210                      3    overlay(d_ed10:50)
     d  d_alt210                     15    overlay(d_ed10:53)
     d  d_alk210                      3    overlay(d_ed10:68)
      *
     d                 ds
     d d_ed11                        80
     d  d_id11                        4    overlay(d_ed11)
     d  d_txk111                      3    overlay(d_ed11:5)
     d  d_txt111                     35    overlay(d_ed11:8)
     d  d_txt211                     35    overlay(d_ed11:43)
      *
     d                 ds
     d d_ed12                        80
     d  d_id12                        4    overlay(d_ed12)
     d  d_kvaq12                      3    overlay(d_ed12:5)
     d  d_kvan12                     15    overlay(d_ed12:8)
     d    d_kvah12                   11  0 overlay(d_kvan12:1)
     d    d_kvad12                    3  3 overlay(d_kvan12:13)
     d  d_benh12                      3    overlay(d_ed12:23)
      *
     d                 ds
     d d_ed13                        80
     d  d_id13                        4    overlay(d_ed13)
     d  d_lftq13                      3    overlay(d_ed13:5)
     d  d_lftx13                     70    overlay(d_ed13:8)
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hoved program styrerutine - les av innposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_oskr = *off
      *
     c                   read      nwebpf
      *
     c                   dow       not %eof
     c                   eval      w_rect = %subst(ordlin:1:4)
      *
     c                   select
     c                   when      w_rect = 'ED00'
     c                   exsr      ed00
     c                   when      w_rect = 'ED01'
     c                   exsr      ed01
     c                   when      w_rect = 'ED02'
     c                   exsr      ed02
     c                   when      w_rect = 'ED03'
     c                   exsr      ed03
     c                   when      w_rect = 'ED04'
     c                   exsr      ed04
     c                   when      w_rect = 'ED05'
     c                   exsr      ed05
     c                   when      w_rect = 'ED06'
     c                   exsr      ed06
     c                   when      w_rect = 'ED07'
     c                   exsr      ed07
     c                   when      w_rect = 'ED08'
     c                   exsr      ed08
     c                   when      w_rect = 'ED09'
     c                   exsr      ed09
     c                   when      w_rect = 'ED10'
     c                   exsr      ed10
     c                   when      w_rect = 'ED11'
     c                   exsr      ed11
     c                   when      w_rect = 'ED12'
     c                   exsr      ed12
     c                   when      w_rect = 'ED13'
     c                   exsr      ed13
     c                   endsl
      *
     c                   read      nwebpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   if        b_blin = *on
     c                   exsr      skriv_lbest
     c                   endif
      *
     c                   if        b_oskr = *on
     c                   exsr      send_melding
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED00  - Meldingsinit.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed00          begsr
      *
     c                   exsr      hent_mnum
      *
     c                   exsr      null_hbest
     c                   eval      d_ed00 = ordlin
      *
     c                   if        d_lvda00 <> 0
     c     *ymd          move      d_lvda00      noldat
     c                   endif
      *
     c                   eval      b_bhod = *on
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED01  - Meldingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed01          begsr
      *
     c                   eval      d_ed01 = ordlin
      *
     c                   eval      nonref = %trim(d_benr01)
      *
     c                   eval      b_bhod = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED02 - Fritekst/hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed02          begsr
      *
     c                   eval      d_ed02 = ordlin
      *
     c                   if        d_ftxt02 <> *blank
     c                   eval      nomeld = %trim(nomeld) + %trim(d_ftxt02)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED03 - Referanser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed03          begsr
      *
     c                   eval      d_ed03 = ordlin
      *
     c                   if        d_refq03 = 'CR '
     c                   if        d_refr03 <> *blank
     c                   eval      norekv = *blank
     c                   eval      norekv = %trim(d_refr03)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED04 - Partsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed04          begsr
      *
     c                   eval      d_ed04 = ordlin
      *
     c     ' ':'0'       xlate     d_part04      d_part04
      *
     c                   if        d_parq04 = 'BY '
     c                   eval      rkunl5_eank = d_pean04
     c     rkunl5_key    chain     rkunl5
      *
     c                   if        %found
     c                   eval      nokund = rkkund
     c                   eval      nonavn = rknavn
     c                   eval      noknav = rknavn
     c                   eval      noadr1 = rkgate
     c                   eval      noadr2 = rkadr2
     c                   movel     rkponr        noponr
     c                   eval      nosted = rksted
     c                   else
     c                   eval      noknav = d_part04
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED05 - Mottager navn/adr 1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed05          begsr
      *
     c                   eval      d_ed05 = ordlin
      *
     c                   if        d_parq04 = 'DP '
     c                   if        d_pnav05 <> *blank
     c                   eval      nonavn = %trim(d_pnav05)
     c                   eval      noadr1 = %trim(d_pad105)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED06 -  Mottager adr 2/postdata
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed06          begsr
      *
     c                   eval      d_ed06 = ordlin
      *
     c                   if        d_parq04 = 'DP '
     c                   if        d_ppst06 <> *blank
     c                   eval      noadr2 = %trim(d_pad206)
     c                   eval      noponr = %trim(d_ppnu06)
     c                   eval      nosted = %trim(d_ppst06)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED07 - Foretaksnr m.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed07          begsr
      *
      *
     c                   eval      d_ed07 = ordlin
      *
     c                   if        d_parq04 = 'BY '
     c                   if        d_ctfu07 = 'PD '
     c                   eval      nodref = d_kont07
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED08 - Komm. adresse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed08          begsr
      *
     c                   eval      d_ed08 = ordlin
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED09 - Transport m.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed09          begsr
      *
     c                   eval      d_ed09 = ordlin
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED10 - Varelinjer/vareidentifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed10          begsr
      *
      * Er ordrehode skrevet?
      *
     c                   if        b_bhod = *on
     c                   exsr      skriv_hbest
     c                   endif
      *
      * Er forrige linje skrevet?
      *
     c                   if        b_blin = *on
     c                   exsr      skriv_lbest
     c                   endif
      *
     c                   exsr      null_lbest
      *
     c                   eval      ndnref = nonref
     c                   eval      ndfsys = nofsys
     c                   eval      ndtell = notell
      *
     c                   eval      d_ed10 = ordlin
     c     ' ':'0'       xlate     d_vlin10      d_vlin10
     c                   eval      ndline = d_vlix10
      *
     c     ' ':'0'       xlate     d_eanr10      d_eanr10
      *
     c     ' ':'0'       xlate     d_alt110      d_alt110
     c     c_digi        check     d_alt110                               80
     c                   if        *in80 = *on
     c                   eval      d_altx10 = 0
     c                   endif
      *
     c                   exsr      finn_vare
      *
     c                   eval      b_blin = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED11 - Varelinjer/varetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed11          begsr
      *
     c                   eval      d_ed11 = ordlin
      *
     c                   eval      ndtek1 = d_txt111
     c                   eval      ndtek2 = d_txt211
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED12 - Varelinjer/kvantumsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed12          begsr
      *
     c                   eval      d_ed12 = ordlin
     c     ' ':'0'       xlate     d_kvan12      d_kvan12
      *
     c                   eval      ndanta = d_kvah12 + d_kvad12
     c                   eval      ndenhe = d_benh12
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED13 - Varelinjer/fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed13          begsr
      *
     c                   eval      d_ed13 = ordlin
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vare     begsr
      *
      * Sjekk nobbnr
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvlnob = :d_altx10
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      ndvare = w_lv_vare
6.31 c                   goto      end_vare
6.31 c                   endif

      *
     c                   eval      vvarl3_nobb = d_altx10
     c     vvarl3_key    chain     vvarl3
     c                   if        %found
     c                   eval      ndvare = vvvare
     c                   goto      end_vare
     c                   endif
      *
      * Sjekk eannn
      *
     c                   eval      w_eann = d_eanx10
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
     c                   eval      ndvare = w_eava
     c                   goto      end_vare
     c                   endif
      *
      * Sjekk lev.varenr
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c                   movel     d_alt210      vvarl4_lvar
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :vvarl4_lvar
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      ndvare = w_lv_vare
6.31 c                   goto      end_vare
6.31 c                   endif
     c*                  eval      vvarl4_lvar = d_alt210
     c*
     c                   movel     d_alt210      vvarl4_lvar
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   eval      ndvare = vvvare
     c                   goto      end_vare
     c                   endif
      *
     c     end_vare      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive ordre hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_hbest   begsr
      *
     c                   eval      nohelu_fsys = c_fsys
     c                   eval      nohelu_tell = notell
      *
     c     nohelu_key    chain     nohelur
     c                   if        not %found
6.10 c                   time                    noodat
6.10 c                   time                    nootim
6.10 c                   eval      noousr = l_user
6.10 c                   time                    noedat
6.10 c                   time                    noetim
6.10 c                   eval      noeusr = l_user
     c                   write     nohelur
     c                   endif
      *
     c                   eval      b_bhod = *off
     c                   eval      b_oskr = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive ordre linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_lbest   begsr
      *
      * Er hode skrevet?
      *
     c                   if        b_bhod = *on
     c                   exsr      skriv_hbest
     c                   endif
      *
     c                   eval      ndldat = noldat
      *
     c                   eval      nodtlu_fsys = c_fsys
     c                   eval      nodtlu_tell = notell
     c                   eval      nodtlu_line = ndline
      *
     c     nodtlu_key    chain     nodtlur
     c                   if        not %found
6.10 c                   time                    ndodat
6.10 c                   time                    ndotim
6.10 c                   eval      ndousr = l_user
6.10 c                   time                    ndedat
6.10 c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
     c                   write     nodtlur
     c                   else
6.10 c                   time                    ndedat
6.10 c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
     c                   update    nodtlur
     c                   endif
      *
     c                   eval      b_blin = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille ordre hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_hbest    begsr
      *
     c                   clear                   d_ed01
     c                   clear                   d_ed02
     c                   clear                   d_ed03
     c                   clear                   d_ed04
     c                   clear                   d_ed05
     c                   clear                   d_ed06
     c                   clear                   d_ed07
     c                   clear                   d_ed08
     c                   clear                   d_ed09
      *
     c                   eval      nofirm = w_firm
     c                   eval      nonref = *blank
     c                   eval      nolage = 0
     c                   eval      nouser = *blank
     c                   eval      nokund = 0
     c                   eval      nokpro = 0
     c                   eval      nolety = 0
     c                   eval      noldat = *loval
     c                   eval      nonumm = 0
     c                   eval      nomeld = *blank
      *
     c                   time                    nobdat
     c                   time                    noodat
     c                   time                    nootim
     c                   time                    noedat
     c                   time                    noetim
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille ordre linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_lbest    begsr
      *
     c                   clear                   d_ed10
     c                   clear                   d_ed11
     c                   clear                   d_ed12
     c                   clear                   d_ed13
      *
     c                   eval      ndfirm = w_firm
     c                   eval      ndanta = 0
     c                   eval      ndntpr = 0
     c                   eval      ndlety = 0
     c                   eval      ndldat = *loval
      *
     c                   time                    ndodat
     c                   time                    ndedat
     c                   time                    ndetim
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente/oppdatere meldingsnummer for EDI/ordre inn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_mnum     begsr
      *
     c                   eval      w_fell = 'FEDIIN'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_mnum = 0
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_mnum
      *
     c                   eval      nofsys = c_fsys
     c                   eval      notell = w_mnum
      *
     c     end_mnum      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sende melding til ordrebehandlere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     send_melding  begsr
      *
     c     fusrl1_key    setll     fusrl1
     c     fusrl1_key    reade     fusrl1
     c                   dow       not %eof
      *
     c                   if        fbko10 = 1
     c                   call      'NN821C'
     c                   parm                    fbuser
     c                   endif
      *
     c     fusrl1_key    reade     fusrl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for lesing av ordrehode/forsyst.
      *
     c     nohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nohelu_fsys
     c                   kfld                    nohelu_tell
      *
      * Key for lesing av ordrelinje/forsyst.
      *
     c     nodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nodtlu_fsys
     c                   kfld                    nodtlu_tell
     c                   kfld                    nodtlu_line
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for posisjonering på leverandørens-varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for oppslag på kunde-register (EAN)
      *
     c     rkunl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl5_eank
      *
      * Key for oppslag på ofak-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
      *
      *  Legger inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      *  Legger inn "current" dato og tid
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
