     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VL700R
      * Beskrivelse . . : Lager, henter lagerbeholdning for enhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201098 AMD  Nytt program
      *       071201 HKO  Tatt med "i bestilling"
      *       300502 HKO  Enhet som input-parameter samt returnerer fortegn
      *                   på antall i ordre/pakkseddel
      *       131103 ASK  Lagt inn test for å unngå div. by zero ved   tegn
      *                   ugyldige enheter.
5.60  *       050808 bof  Tar lagerenhet fra vareregister
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
5.60 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_lage          s                   like(vllage)
     d p_enhe          s                   like(vlenhl)
     d p_behl          s                   like(vlbehl)
     d p_bbes          s                   like(vlbbes)
     d p_pakk          s                   like(vlpakk)
     d p_disp          s                   like(vlpakk)
      *
      * Definering av variable i nøkler
      *
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vvenl1_vare     s                   like(vevare)
5.60 d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
     d w_omre          s                   like(veomre)
     d w_omrl          s                   like(veomre)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn saldo og disponibelt fra lagerregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer beholdning
      *
     c                   eval      p_behl = 0
     c                   eval      p_bbes = 0
     c                   eval      p_pakk = 0
     c                   eval      p_disp = 0
5.60 c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1r
      *
      * Henter beholding
      * Dersom valgt enhet er forskjellig fra lagerenhet, omregnes antall
      * til valgt enhet
      *
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1r
     c                   if        %found
      *
     c                   eval      p_behl = vlbehl
     c                   eval      p_bbes = vlbbes
     c                   eval      p_pakk = (vlpakk + vloord) * -1
     c                   eval      p_disp = p_behl + p_bbes + p_pakk
      *
5.60 c                   if        p_enhe <> vvenhl
     c                   eval      vvenl1_vare = p_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   select
     c                   when      veenhe = p_enhe
     c                   eval      w_omre = veomre
5.60 c                   when      veenhe = vvenhl
     c                   eval      w_omrl = veomre
     c                   endsl
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omre <> 0
     c                   eval(h)   p_behl = p_behl * w_omrl / w_omre
     c                   eval(h)   p_bbes = p_bbes * w_omrl / w_omre
     c                   eval(h)   p_pakk = p_pakk * w_omrl / w_omre
     c                   eval(h)   p_disp = p_disp * w_omrl / w_omre
     c                   else
     c                   eval      p_behl = 0
     c                   eval      p_bbes = 0
     c                   eval      p_pakk = 0
     c                   eval      p_disp = 0
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_enhe
     c                   parm                    p_behl
     c                   parm                    p_bbes
     c                   parm                    p_pakk
     c                   parm                    p_disp
      *
      * Key for oppslag på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
5.60  *
5.60  * Key for oppslag på vare
5.60  *
5.60 c     vvarl1_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
