     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL402R
      * Beskrivelse . . : Leverandører, sletting av posteringer - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa1pf    if   e           k disk    rename(raa1pfr:raa1pfr)
     frl402d    cf   e             workstn
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        64
     d  d_raar                        4  0 overlay(d_list)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_firm          s              3  0
      *
     d w_mld1          c                   'Vennligst vent .........     '
     d w_mld2          c                   'Sletting pågår .........     '
     d wactli          s              2  0
     d wactpo          s              3  0
     d wmld01          s             30
     d wmld02          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Send meldingsbildet
      *
     c                   eval      wactli = 4
     c                   eval      wactpo = 44
     c                   eval      wmld01 = w_mld1
     c                   eval      wmld02 = w_mld2
      *
     c                   call      'AA006C'
     c                   parm                    wactli
     c                   parm                    wactpo
     c                   parm                    wmld01
     c                   parm                    wmld02
      *
      * Kaller sletteprogram
      *
     c                   call      'RL403R'
     c                   parm                    d_list

     c*                  goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
      *
      * Regnskapsår
      *
     c                   if        b1raar = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1raar <> 0 and
     c                             b1raar > *year
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1raar <> 0 and
     c                             b1raar >= ra1aar
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til DS-parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      d_raar = b1raar
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Initiering av skjermbilde
      *
     c                   eval      w_firm = l_firm
     c                   eval      b1firm = l_firm
     c                   eval      b1navn = l_fnav
     c                   eval      wactli = 3
     c                   eval      wactpo = 2
      *
      * Finn statusopplysninger for firma
      *
     c     raa1pf_key    chain     raa1pfr                            61
      *
      * Key for statusopplysninger - koderegister
      *
     c     raa1pf_key    klist
     C                   kfld                    l_firm
      *
     c                   endsr
