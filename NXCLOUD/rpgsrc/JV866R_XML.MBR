     h option(*nodebugio) datedit(*dmy)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV866R
      * Beskrivelse . . : Oppdater fra NOBB, pris
      *
      *                   Oppdaterer/oppretter pris fra
      *                   NOBB 80 (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV
6.30  * R6.30 090414 BKV  Hvis pris kommer med til dato = null, skal til dato settes til null
      *                   Koden var slik at det ikke var mulig å aktivere en pris igjen
6.31  * R6.30 230414 BKV  Støtte for NIB nr
6.32  * R6.30 250414 BKV  Bedre logging
6.33  * R6.30 150714 BKV  Mulighet for flere priser
6.34  * R6.30 040914 BKV  Trim ved sjekk om oppdatering
6.35  * R6.30 150515 BKV  d_lvar økt til 20
6.36  * R6.30 070116 bof  Sjekk om det finnes gamle priser uten tildato
7.01  *K00    121020 bof  Hvis egen reg, så skal ikke til dato settes
8.01  *       231122 bof  Switchstyre om tildato skal settes av program eller at tildato fra
8.01  *                   nobb gjelder.   NOBBPRIS_TILDATO = hvis på skal program sette tildato
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     f                                     infsr(FileErr)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
     fjjltlu    uf a e           k disk    rename(jjltpfr:jjltlur)
     f                                     infsr(FileErr)
6.36 fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
6.36 f                                     infsr(FileErr)
      *
      * Definering av variable i nøkler
      *
6.31 d jvarl3_nobb     s                   like(jvnobb)
6.31 d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
6.33 d jvprlu_gdat     s                   like(jxgdat)
6.36 d jvprl1_vare     s                   like(jxvare)
6.36 d jvprl1_ldor     s                   like(jxldor)
      *
      * d/copy QRPGLESRC,jv867r_xml
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_vfel          s              1    inz(*off)
      *
      * Definering av tabell med meldinger
     d a_meld          s            100    dim(1) ctdata perrcd(1)
      *
      *  Dataelementer pris
      *
     d                 ds
     d d_prec                       200
     d  d_stat                        1    overlay(d_prec:2)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_ldor                        6    overlay(d_prec:3)
     d   d_ldor_num                   6  0 overlay(d_ldor:1)
6.35 d  d_lvar                       20    overlay(d_prec:9)
6.35 d  d_fdat                       10    overlay(d_prec:29)
6.35 d   d_fdat_iso                    d   overlay(d_fdat:1) datfmt(*iso)
6.35 d  d_tdat                       10    overlay(d_prec:39)
6.35 d   d_tdat_iso                    d   overlay(d_tdat:1) datfmt(*iso)
6.35 d  d_nobp                       12    overlay(d_prec:49)
6.35 d   d_nobp_num                   9  2 overlay(d_nobp:1)
6.35 d  d_valu                        3    overlay(d_prec:61)
6.35 d  d_eann                       13    overlay(d_prec:64)
6.35 d   d_eann_num                  13  0 overlay(d_eann:1)
6.35 d  d_noda                       10    overlay(d_prec:77)
6.35 d   d_noda_iso                    d   overlay(d_noda:1) datfmt(*iso)
6.35 d  d_neda                       10    overlay(d_prec:87)
6.35 d  d_odat                       10    overlay(d_prec:97)
6.35 d  d_edat                       10    overlay(d_prec:107)
6.35 d  d_etim                       10    overlay(d_prec:117)
6.35 d  d_nobb                        8    overlay(d_prec:127)
6.35 d   d_nobb_num                   8  0 overlay(d_nobb:1)
6.35 d  d_crea                       10    overlay(d_prec:135)
6.35 d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
6.35 d  d_upda                       10    overlay(d_prec:145)
6.35 d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
      *
      * job_log
      *
6.32 d d_job_log                     70
     d  w_ojprt                       5    overlay(d_job_log:2)
     d  w_ojpr                        5  0 overlay(w_ojprt:1)
     d  w_jvprt                       5    overlay(d_job_log:7)
     d  w_jvpr                        5  0 overlay(w_jvprt:1)
     d  w_jbid                       41    overlay(d_job_log:12)
6.32 d  w_avprt                       5    overlay(d_job_log:53)
6.32 d  w_avpr                        5  0 overlay(w_avprt:1)
6.32 d  w_lnr                         5  0 overlay(d_job_log:58)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_stat          s              1
     d p_prec          s            200
     d p_job_log       s             70
     d p_inlr          s              1
      *
      * Brukes som parametre til jobblogging
      *
     d p_jsts          s              2  0
     d p_jmld          s            200
     d w_meld          s            100
     d w_ttxt          s                   like(jjttxt)
     d w_ttyp          s                   like(jjttyp)
     d w_tnob          s                   like(jjtnob)
     d w_tldo          s                   like(jjtldo)
6.36 d w_gdat          s                   like(jxgdat)
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
8.01 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * Definering av variable
      *
     d n               s              2  0
      *
8.01 d u_801           s              1    inz(*off)
8.01  * Definering av eksterne prg
8.01  * BKV
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   DCL-PR CO402R EXTPGM('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
8.01     p_verdi2            char(2048);
8.01   END-PR;
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c                   eval      d_job_log = p_job_log
     c                   if        p_inlr = *on
     c                   eval      d_prec = p_prec
      * Sjekk om nobbnummer finnes fra før
     c                   if        b_vfel = *off
     c                   exsr      les_vare
     c                   endif
      * Legg inn pris, hvis varen finnes
     c                   if        b_vfel = *off
     c                   exsr      skr_pris
     c                   endif
     c                   endif
      *
      * retunerer jobb status
     c                   eval      p_job_log = d_job_log
     c                   eval      b_vfel = *off
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   if        p_inlr <> *on
      * Avslutter søketekster
     c                   eval      b_inlr = *off
      *
     c                   eval      jvvare = *zero
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr

     c                   eval      *inlr = *on
     c                   endif
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   call      'AB705R'
     c                   parm                    w_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_pris      begsr
      *
     c                   if        %Trim(d_ldor) = ''
     c                   leavesr
     c                   endif
      *
6.36 c                   eval      w_gdat = *loval
      *
     c                   clear                   jvprlur
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = d_ldor_num
6.33 c                   eval      jvprlu_gdat = d_fdat_iso
     c     jvprlu_key    chain     jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = d_ldor_num
     c                   eval      jxeusr = l_user
      *
      *  sjekker lev.varenr
     c                   if        jxldor = jvldor and
     c                             d_lvar = *blank
     c                   eval      d_lvar = jvlvar
     c                   endif
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   if        %found
     c                   if        d_fdat_iso >= jxgdat
6.34 c                   if        %trim(d_lvar) <> %trim(jxlvar) or
     c                             d_nobp_num <> jxpris or
6.34 c                             %trim(d_valu) <> %trim(jxvalu) or
     c                             d_fdat_iso <> jxgdat or
6.30 c                             d_tdat_iso <> jxtdat
     c                   eval      jxlvar = d_lvar
     c                   eval      jxgpri = jxpris
     c                   eval      jxpris = d_nobp_num
     c                   eval      jxvalu = d_valu
     c                   eval      jxgdat = d_fdat_iso
6.36 c                   eval      w_gdat = jxgdat
6.30 c                   eval      jxtdat = d_tdat_iso
     c                   eval      jxnoda = d_crea_iso
     c                   eval      jxneda = d_upda_iso
     c                   eval      w_ojpr = w_ojpr + 1
     c                   update    jvprlur
6.32 c                   else
6.36 c                   eval      w_gdat = jxgdat
6.32 c                   eval      w_avpr = w_avpr + 1
     c                   endif
6.32 c                   else
6.36 c                   eval      w_gdat = jxgdat
6.32 c                   eval      w_avpr = w_avpr + 1
     c                   endif
     c                   else
     c                   eval      jxlvar = d_lvar
     c                   eval      jxgpri = 0
     c                   eval      jxpris = d_nobp_num
     c                   eval      jxvalu = d_valu
     c                   eval      jxgdat = d_fdat_iso
6.36 c                   eval      w_gdat = jxgdat
     c                   eval      jxtdat = d_tdat_iso
     c                   eval      jxcrdo = 0
     c                   eval      jxnoda = d_crea_iso
     c                   eval      jxneda = d_upda_iso
     c                   time                    jxodat
     c                   eval      w_jvpr = w_jvpr + 1
     c                   write     jvprlur
     c                   endif
6.36  *
6.36  * Sjekker om prisgiver har gammel pris, som ikke har til dato, sette til dato
6.36  * like fra dato på den som er skrevet.
7.01  * skal gjelde for ikke egenreg. priser
8.01 c                   if        u_801 = *on
6.36 c                   eval      jvprl1_vare = jxvare
6.36 c                   eval      jvprl1_ldor = jxldor
6.36 c     jvprl1_key    setll     jvprl1
6.36 c     jvprl1_key    reade     jvprl1
6.36 c                   dow       not  %eof(jvprl1)
6.36 c                   if        jxgdat < w_gdat and
7.01 c                             jxtdat = *loval and
7.01 c                             jxrsts = 0
6.36 c                   eval      jvprlu_vare = jxvare
6.36 c                   eval      jvprlu_ldor = jxldor
6.36 c                   eval      jvprlu_gdat = jxgdat
6.36 c     jvprlu_key    chain     jvprlur
6.36 c                   if        %found(jvprlu)
6.36 c                   eval      jxtdat = w_gdat
6.36 c                   time                    jxedat
6.36 c                   time                    jxetim
6.36 c                   eval      jxeusr = l_user
6.36 c                   update    jvprlur
6.36 c                   endif
6.36 c                   endif
6.36 c     jvprl1_key    reade     jvprl1
6.36 c                   enddo
8.01 c                   endif
      *
      * Oppdaterer/oppretter søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for les av tilhørende varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
6.31 c                   eval      jvarl3_nobb = d_nobb_num
      *
6.31 c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   eval      b_vfel = *on
      *
      * Vi skriver informasjon ut til jobbstatusfil
     c                   eval      w_meld = *blanks
     c                   eval      w_meld = %trim(a_meld(1)) + ' ' + d_nobb
     c                   eval      p_jmld = %trim(w_meld)
     c                   eval      p_jsts = 20
      *
     c                   call      'AB705R'
     c                   parm                    w_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      * logger endring
6.32 c                   eval      w_ttyp = 'E'
     c                   eval      w_ttxt = %trim(a_meld(1))
     c                   eval      w_tnob = d_nobb_num
     c                   eval      w_tldo = d_ldor_num
     c                   exsr      logg_utg
      *
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å logge problem pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     logg_utg      begsr
      *
     c                   eval       w_lnr = w_lnr  + 1
     c                   clear                   jjltlur
     c                   eval      jjtfir = l_firm
     c                   eval      jjtjid = w_jbid
     c                   eval      jjtlin = w_lnr
     c                   eval      jjtsta = 10
     c                   eval      jjttyp = w_ttyp
     c                   eval      jjtnob = w_tnob
     c                   eval      jjtldo = w_tldo
     c                   eval      jjttxt = w_ttxt
     c                   time                    jjtoda
     c                   time                    jjtoti
     c                   eval      jjtous = l_user
     c                   write     jjltlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_prec
     c                   parm                    p_job_log
     c                   parm                    p_inlr
      *
      * Key for posisjonering på Nobb-nummer
      *
6.31 c     jvarl3_key    klist
6.31 c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
6.33 c                   kfld                    jvprlu_gdat
6.36  *
6.36  * Key for les av vare-pris
6.36  *
6.36 c     jvprl1_key    klist
6.36 c                   kfld                    jvprl1_vare
6.36 c                   kfld                    jvprl1_ldor
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c     jstsl1_key    chain     jstsl1

8.01  *
8.01  * Endring 7.2m
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'NOBBPRIS_TILDATO':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_801 = *on;
8.01   endif;
      *
     c                   endsr
**
NOBBNummer er ikke opprettet i JVARPF. NOBBnr. :
