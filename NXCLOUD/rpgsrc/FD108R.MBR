     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD108R
      * Beskrivelse . . : Ordre-linje, hurtigoppretting av varelinje. NB Synker
      *                   mot bestilling.
      *
7.0x  * Spesialversjon. : Innkjøpspris legges over som kostpris på ordren
7.0x  * Tilpasset for . : Skattum
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.20  080411 BSA  Nytt program
6.21  * 6.20  190511 bof  Utvidet VL710R med utg.dato og l.dor
6.22  * 6.20  160312 bsa  Hvis kost/salgspris er 0, brukes info fra bestilling
6.23  * 6.20  071015 bsa  Må finne siste ledig linjenummer, hvis ordrelinje
6.23  *                   ikke finnes fra før.
6.nu  * R6.30 030614 bsa  Endringer grunnet nummerutvidelser
6.30  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.01  * K000  190121 bsa  Legger innkjøpspris på bestilling ut som kostpris på ordren
      *
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *PRG2   310524 bsa  Legger på GTIN nummer ved opprettelse av varelinjer.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
6.23 ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
6.23 f                                     prefix(fx:2)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
6.21 flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(fdfirm)
6.nu d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
     d p_ltyp          s                   like(fdltyp)
     d p_lety          s                   like(fdlety)
     d p_vare          s                   like(fdvare)
     d p_anta          s                   like(fdanta)
     d p_enhe          s                   like(fdenh1)
     d p_ldat          s                   like(fdldat)
     d p_vtyp          s                   like(vvvtyp)
6.nu d p_bnum          s                   like(ldnumm)
     d p_bsuf          s                   like(ldsuff)
     d p_blin          s                   like(ldline)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
      *
      * Definering av datastrukturer
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
      *
      * Definering av variable
      *
     d w_stat          s              1
      *
     d w_firm          s                   like(vvfirm)
6.nu d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_line          s                   like(fdline)
6.23 d w_slin          s                   like(fdline)
      *
     d w_raba          s                   like(fdsums)
     d w_sumr          s                   like(fdsums)
     d w_sums          s                   like(fdsums)
5.70 d w_prgr          s                   like(fdprgr)
6.30 d w_numa          s              8
5.02 d w_lina          s              4
6.21 d w_udat          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
8.02 d w_gtin          s                   like(fdeann)
8.02 d w_levr          s              6  0
      *
6.21 d b_inlr          s              1    inz(*off)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.01   END-PR;
      *
7.01 d u_701           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom vare eller antall ikke er utfylt
      *
     c                   if        p_vare = *blank or
     c                             p_anta = 0
     c                   goto      avslutt
     c                   endif
      *
      * Avbryter dersom enhet ikke finnes på vare
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Avbryter dersom ordre-linje finnes fra før
      *
6.23 c                   if        w_line <> 0
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
     c                   goto      avslutt
     c                   endif
6.23 c                   else
6.23 c                   exsr      last_line
6.23 c                   endif
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
5.70  *
5.70  * Kaller program for henting av Varetype fra lager/vare
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    folage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
5.70  *
5.70  * Kaller program for henting av prisgruppe fra lager/avdeling
5.70  *
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    folage
5.70 c                   parm                    foavde
5.70 c                   parm                    w_prgr
      *
      * Initierer ordre-linje
      *
     c                   clear                   fodtlur
      *
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = w_numm
     c                   eval      fdsuff = w_suff
     c                   eval      fdline = w_line
E    c                   eval      fdltyp = p_ltyp
     c                   eval      fdvare = p_vare
     c                   eval      fdanta = p_anta
     c                   eval      fdenh1 = p_enhe
     c                   eval      fdtek1 = vvtek1
     c                   eval      fdtek2 = vvtek2
     c                   eval      fdlety = p_lety
     c                   eval      fdpasl = fopasl
     c                   eval      fdldat = p_ldat
8.02  *
8.02 c                   exsr      hent_GTIN
      *
      * Henter inn priser og rabatter
      *
     c                   eval      hienhe = fdenh1
     c                   eval      hilety = fdlety
     c                   eval      hidekn = fdpasl
      *
     c                   exsr      hent_pris
      *
     c                   eval      fdsapr = hosapr
7.01 c                   if        u_701 = *off
     c                   if        hokopr = 0
     c                   eval      fdkopr = ldkpny
     c                   eval      fdkopr = ldipny
     c                   else
     c                   eval      fdkopr = hokopr
     c                   endif
7.01 c                   else
7.01 c                   eval      fdkopr = ldipny
7.01 c                   endif
6.22 c                   eval      fdinpr = ldipny
     c                   eval      fdkpri = hokpri
     c                   eval      fdrab1 = horab1
     c                   eval      fdrab2 = horab2
     c                   eval      fdpasl = hodekn
     c                   eval      fdkamp = hokamp
     c                   eval      fdalme = hoalme
     c                   eval      fdomva = hoomva
     c                   eval      fdbrty = hobrty
     c                   eval      fdbrr1 = hobrr1
     c                   eval      fdbrr2 = hobrr2
     c                   eval      fdpriv = 0
5.70 c                   eval      fdprgr = hoprgr
      *
      * Beregning av totaler
      *
     c                   eval      w_sums = fdsapr * fdanta
     c                   eval      w_sumr = 0
      *
      * Rabatt
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_raba = (w_sums * forabp) / 100
     c                   eval      w_sumr = w_raba
     c                   endif
      *
     c                   if        fdrab1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdrab2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
      * Kostpris
      *
     c                   if        fdkopr = 0
     c                   eval      fdkopr = fdsapr * hopros
     c                   endif
      *
      * Legger inn totaler
      *
     c                   eval      fdsums = w_sums
     c                   eval      fdsumr = w_sumr
     c                   eval      fdsumk = fdkopr * fdanta
      *
      * Legger inn faste opplysninger fra vare og ordre
      *
     c                   eval      fdotyp = footyp
     c                   eval      fdlage = folage
     c                   eval      fdkakk = 0
     c                   eval      fdkgeb = 0
     c                   eval      fdlass = 0
      *
     c                   eval      fdkund = fokund
     c                   if        folkun <> 0
     c                   eval      fdkund = folkun
     c                   endif
      *
     c                   eval      fdbenr = 0
5.51 c                   eval      fdbsuf = 0
5.51 c                   eval      fdblin = 0
     c                   eval      fdavde = 0
     c                   eval      fdkont = 0
     c                   eval      fdspes = 0
6.20 c**                 eval      fdproj = 0
6.20 c                   eval      fdproj = *blank
     c                   eval      fdakti = *blank
6.31 c                   if        w_lv_nobb > 0
6.31 c                   eval      fdnobb = w_lv_nobb
6.31 c                   eval      fdlvar = w_lv_llva
6.31 c                   else
     c                   eval      fdnobb = vvnobb
     c                   eval      fdlvar = vvlvar
6.31 c                   endif
     c                   eval      fdldor = vvldor
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
      * Legg inn referanse til bestilling
     c                   eval      fdbenr = p_bnum
     c                   eval      fdbsuf = p_bsuf
     c                   eval      fdblin = p_blin
      *
      * Oversetter varetekst til store bokstaver dersom dette er valgt
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     fdtek1        fdtek1
     c                   endif
      *
      * Skriver til ordre-linje
      *
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
      * Oppdatering av lager
      *
     c                   exsr      oppdat_lager
      *
      * Oppdater bestillingslinje med ordreref
      *
     c     lodtlu_key    chain     lodtlur
     c                   if        %found
     c                   eval      ldornu = p_numm
     c                   eval      ldorsu = p_suff
6.23 c**                 eval      ldorli = p_line
6.23 c                   eval      ldorli = w_line
     c                   update    lodtlur
     c                   endif
      *
      * Legger inn kode som sier at minst en linje er registrert
      *
     c                   eval      p_kode = *on
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Henter siste linjenummer på ordren.
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     last_line     begsr
6.23  *
6.23 c                   eval      w_slin = 9999
6.23 c     fodtlr_key    setgt     fodtlr
6.23 c     fodtlr_key2   readpe    fodtlr
6.23  *
6.23 c                   dow       not %eof
6.23 c                   eval      w_line = fxline + 10
6.23 c                   leave
6.23 c                   enddo
6.23  *
6.23 c                   endsr
6.23  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter opplysninger om varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
      *
     c                   eval      hikund = fokund
     c                   if        folkun <> 0
     c                   eval      hikund = folkun
     c                   endif
      *
     c                   eval      hikpro = fokpro
     c                   eval      hivare = vvvare
     c                   eval      hidato = fobdat
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = fopask
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiprgr = w_prgr
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_lager  begsr
      *
     c                   if        faalag = 1 and
5.70 c                             p_vtyp = 'L'
     c                   else
     c                   goto      end_lager
     c                   endif
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
     c                   eval      hllage = folage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = footyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
3.12 c                   move      fdlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c     end_lager     endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Henter oGTIN informasjon om vare/enhet
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     hent_GTIN     begsr
8.02  *
8.02 c                   eval      w_gtin = 0
8.02 c                   eval      w_levr = 0
8.02 c                   eval      w_stat = *blanks
8.02  *
8.02 c                   call      'VE713R'
8.02 c                   parm                    w_firm
8.02 c                   parm                    w_levr
8.02 c                   parm                    p_vare
8.02 c                   parm                    p_enhe
8.02 c                   parm                    w_gtin
8.02 c                   parm                    w_stat
8.02  *
8.02 c                   eval      fdeann = w_gtin
8.02  *
8.02 c                   endsr
8.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_vare
     c                   parm                    p_anta
     c                   parm                    p_enhe
     c                   parm                    p_ldat
6.nu c                   parm                    p_bnum
     c                   parm                    p_bsuf
     c                   parm                    p_blin
      *
      * Key for oppslag på status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for oppdatering av bestilling
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
6.23  *
6.23  * Key for oppslag på ordre-linje
6.23  *
6.23 c     fodtlr_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    w_numm
6.23 c                   kfld                    w_suff
6.23 c                   kfld                    w_slin
6.23  *
6.23  * Key for oppslag på ordre-linje
6.23  *
6.23 c     fodtlr_key2   klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    w_numm
6.23 c                   kfld                    w_suff
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
6.nu c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
     c                   eval      lodtlu_numm = p_bnum
     c                   eval      lodtlu_suff = p_bsuf
     c                   eval      lodtlu_line = p_blin
      *
      * Legger inn type for oppdatering av lager (R=Registrering)
      *
5.01 c                   eval      hltype = ' '
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * Hent opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1r                            90
6.22  *
6.22  * Hent inn bestillingslinje, slik at vi har info tilgjengelig
6.22  *
6.22 c     lodtlu_key    chain     lodtl1r
7.01  *
7.01  * Endring 7.01 Legger innkjøpspris på bestilling ut som
7.01  * kostpris på ordren.
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FD108R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
     c                   endsr
