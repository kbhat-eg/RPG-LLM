     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU720R
      * Beskrivelse . . : Butikk, eksport av varegruppe
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160304 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fra26l1    if   e           k disk    rename(ra26pfr:ra26l1r)
      *
     fbovgst    uf a e           k disk    rename(bovgst:bovgstr)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vgofir)
      *
      * Definering av variabler i nøkler
      *
     d vhgrl1_hogr     s                   like(vghogr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d ra26l1_zsak     s                   like(razsak)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgofir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer fil
      *
     c                   clear                   bovgstr
      *
     c                   eval      firm = w_firm
      *
      * Behandler overgruppe, hovedgruppe og undergruppe
      *
     c     vogrl1_key    setll     vogrl1
     c     vogrl1_key    reade     vogrl1
     c                   dow       %eof = *off
      *
     c                   eval      vvarl8_ogrp = vgoogr
     c     vvarl8_key1   chain     vvarl8
     c                   if        not %found
     c                   goto      neste_ogrp
     c                   endif
      *
     c                   eval      ogrp = vgoogr
     c                   eval      hgrp = 0
     c                   eval      ugrp = 0
     c                   eval      besk = vgotxt
     c                   eval      pran = *blank
     c                   eval      dekn = 0
     c                   eval      iavr = 0
     c                   eval      ilag = 0
     c                   eval      odat = vgoeda
     c                   eval      edat = vgoeda
     c                   eval      etim = vgoeti
     c                   eval      eusr = vgoeus
      *
     c                   if        vgopra <> *blank
     c                   eval      ra26l1_zsak = vgopra
     c     ra26l1_key    chain     ra26l1
     c                   if        %found
     c                   eval      pran = raztxt
     c                   endif
     c                   endif
      *
     c                   write     bovgstr
      *
     c                   eval      vhgrl1_hogr = vgoogr
     c     vhgrl1_key    setll     vhgrl1
     c     vhgrl1_key    reade     vhgrl1
     c                   dow       %eof = *off
      *
     c                   eval      vvarl8_hgrp = vghhgr
     c     vvarl8_key2   chain     vvarl8
     c                   if        not %found
     c                   goto      neste_hgrp
     c                   endif
      *
     c                   eval      ogrp = vghogr
     c                   eval      hgrp = vghhgr
     c                   eval      ugrp = 0
     c                   eval      besk = vghtxt
     c                   eval      pran = *blank
     c                   eval      dekn = 0
     c                   eval      iavr = 0
     c                   eval      ilag = 0
     c                   eval      odat = vgheda
     c                   eval      edat = vgheda
     c                   eval      etim = vgheti
     c                   eval      eusr = vgheus
      *
     c                   if        vghpra <> *blank
     c                   eval      ra26l1_zsak = vghpra
     c     ra26l1_key    chain     ra26l1
     c                   if        %found
     c                   eval      pran = raztxt
     c                   endif
     c                   endif
      *
     c                   write     bovgstr
      *
     c                   eval      vugrl1_uogr = vghogr
     c                   eval      vugrl1_uhgr = vghhgr
     c     vugrl1_key    setll     vugrl1
     c     vugrl1_key    reade     vugrl1
     c                   dow       %eof = *off
      *
     c                   eval      vvarl8_ugrp = vguugr
     c     vvarl8_key3   chain     vvarl8
     c                   if        not %found
     c                   goto      neste_ugrp
     c                   endif
      *
     c                   eval      ogrp = vguogr
     c                   eval      hgrp = vguhgr
     c                   eval      ugrp = vguugr
     c                   eval      besk = vgutxt
     c                   eval      pran = *blank
     c                   eval      dekn = vgudek
     c                   eval      iavr = vguavr
     c                   eval      ilag = vgulag
     c                   eval      odat = vgueda
     c                   eval      edat = vgueda
     c                   eval      etim = vgueti
     c                   eval      eusr = vgueus
      *
     c                   if        vgupra <> *blank
     c                   eval      ra26l1_zsak = vgupra
     c     ra26l1_key    chain     ra26l1
     c                   if        %found
     c                   eval      pran = raztxt
     c                   endif
     c                   endif
      *
     c                   write     bovgstr
      *
     c     neste_ugrp    tag
     c     vugrl1_key    reade     vugrl1
     c                   enddo
      *
     c     neste_hgrp    tag
     c     vhgrl1_key    reade     vhgrl1
     c                   enddo
      *
     c     neste_ogrp    tag
     c     vogrl1_key    reade     vogrl1
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for les på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
      *
      * Key for les på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
      *
      * Key for oppslag på vare
      *
     c     vvarl8_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
      *
     c     vvarl8_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
      *
     c     vvarl8_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      * Key for oppslag på produktansvarlig
      *
     c     ra26l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra26l1_zsak
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
