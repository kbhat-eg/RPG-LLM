     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AZ702R
      * Beskrivelse . . : Felles, sender mail
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280203 MHA  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av pamametre
      *
     d o_str           s               o   class(*JAVA:'java.lang.String')
     d o_msnd          s               o   class(*JAVA:'asp.mail.SendMail')
      *
     d p_vedl          s             50a
     d p_serv          s             50a
     d p_from          s             50a
     d p_to            s             50a
     d p_inif          s             50a
     d p_subj          s            200a
     d p_text          s            200a
     d p_status        s               N
      *
     d mkstr           pr              o   extproc(*JAVA:
     d                                      'java.lang.String':
     d                                      *CONSTRUCTOR)
     d                                     class(*JAVA:'java.lang.String')
     d   bytes                      200a   CONST VARYING
      *
     d crtmailobj      pr              o   extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      *CONSTRUCTOR)
      *
     d setvedl         pr                  extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'setFile')
     d   fil                           o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d setrecv         pr                  extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'addTo')
     d   recv                          o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d setfrom         pr                  extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'setFrom')
     d   from                          o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d setsubj         pr                  extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'setSubject')
     d   subj                          o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d setserver       pr                  extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'setHost')
     d   server                        o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d settext         pr                  extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'addBody')
     d   txtl                          o   class(*JAVA:
     d                                      'java.lang.String')
      *
     d sendmail        pr              N   extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'sendMail')
      *
     d setupfile       pr              N   extproc(*JAVA:
     d                                      'asp.mail.SendMail':
     d                                      'setupFromFile')
     d   sfile                         o   class(*JAVA:
     d                                      'java.lang.String')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      o_msnd = crtmailobj
      *
     c                   if        p_serv <> *blank and
     c                             %trim(p_serv) <> '*BLANK' and
     c                             %trim(p_serv) <> '*NONE'
     c                   eval      o_str = mkstr(%trim(p_serv))
     c                   callp     setserver(o_msnd:o_str)
     c                   endif
      *
     c                   if        p_to <> *blank and
     c                             %trim(p_to) <> '*BLANK' and
     c                             %trim(p_to) <> '*NONE'
     c                   eval      o_str = mkstr(%trim(p_to))
     c                   callp     setrecv(o_msnd:o_str)
     c                   endif
      *
     c                   if        p_from <> *blank and
     c                             %trim(p_from) <> '*BLANK' and
     c                             %trim(p_from) <> '*NONE'
     c                   eval      o_str = mkstr(%trim(p_from))
     c                   callp     setfrom(o_msnd:o_str)
     c                   endif
      *
     c                   if        p_subj <> *blank and
     c                             %trim(p_subj) <> '*BLANK' and
     c                             %trim(p_subj) <> '*NONE'
     c                   eval      o_str = mkstr(%trim(p_subj))
     c                   callp     setsubj(o_msnd:o_str)
     c                   endif
      *
     c                   if        p_text <> *blank and
     c                             %trim(p_text) <> '*BLANK' and
     c                             %trim(p_text) <> '*NONE'
     c                   eval      o_str = mkstr(%trim(p_text))
     c                   callp     settext(o_msnd:o_str)
     c                   endif
      *
     c                   if        p_vedl <> *blank and
     c                             %trim(p_vedl) <> '*BLANK' and
     c                             %trim(p_vedl) <> '*NONE'
     c                   eval      o_str = mkstr(%trim(p_vedl))
     c                   callp     setvedl(o_msnd:o_str)
     c                   endif
      *
     c                   if        p_inif <> *blank
     c                             and %trim(p_inif) <> '*BLANK' and
     c                             %trim(p_inif) <> '*NONE'
     c                   eval      o_str = mkstr(%trim(p_inif))
     c                   eval      p_status = setupfile(o_msnd:o_str)
     c                   endif
      *
     c                   eval      p_status = sendmail(o_msnd)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_serv
     c                   parm                    p_to
     c                   parm                    p_from
     c                   parm                    p_subj
     c                   parm                    p_text
     c                   parm                    p_vedl
     c                   parm                    p_inif
     c                   parm                    p_status
      *
     c                   endsr
