      /TITLE  ASOFAK: Rydder i ordrerelaterte registre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO415R
      * Beskrivelse . . : Sjekker om det ligger ordrerelaterte
      *                   info på et bestemt ordrenr
      *                   - LWPLPF
      *                   - FOTXPF
      *                   - FODTPF
6.31  *                   - FVLSPF
      *                   sletter i såfall dette
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160512 bof  Nytt program
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.31  * R6.30 120617 bof  Utvidet med FVLSPF
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwpllu    uf   e           k disk    rename(lwplpfr:lwpllur)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     ffotxlu    uf   e           k disk    rename(fotxpfr:fotxlur)
6.31 ffvlslu    uf   e           k disk    rename(fvlspfr:fvlslur)
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
6.nu d p_numm          s                   like(fdnumm)
      *
      * Definering av variabler i nøkler
      *
     d lwpllu_numm     s                   like(mknumm)
     d fodtlu_numm     s                   like(fdnumm)
     d fotxlu_numm     s                   like(ftnumm)
6.31 d fvlslu_numm     s                   like(fvnumm)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_test          s              1  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser ordrelinjer for å finne om linjer som kan slettes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * LWPPF Plukk RT
      *
     c     lwpllu_key    setll     lwpllu
     c     lwpllu_key    reade     lwpllu
     c                   dow       not %eof(lwpllu)
     c                   delete    lwpllur
     c     lwpllu_key    reade     lwpllu
     c                   enddo
      *
      * fodtPF ordre detaljer
      *
     c     fodtlu_key    setll     fodtlu
     c     fodtlu_key    reade     fodtlu
     c                   dow       not %eof(fodtlu)
     c                   delete    fodtlur
     c     fodtlu_key    reade     fodtlu
     c                   enddo
      *
      * fotxPF ordre tekst
      *
     c     fotxlu_key    setll     fotxlu
     c     fotxlu_key    reade     fotxlu
     c                   dow       not %eof(fotxlu)
     c                   delete    fotxlur
     c     fotxlu_key    reade     fotxlu
     c                   enddo
6.31  *
6.31  * fvlsPF lengde spesifikasjoner
6.31  *
6.31 c     fvlslu_key    setll     fvlslu
6.31 c     fvlslu_key    reade     fvlslu
6.31 c                   dow       not %eof(fvlslu)
6.31 c                   delete    fvlslur
6.31 c     fvlslu_key    reade     fvlslu
6.31 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : Ordre-linje-register
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
      *
      * Key for file : Ordre-tekst-register
      *
     c     fotxlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxlu_numm
      *
      * Key for file : plukking rt
      *
     c     lwpllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpllu_numm
6.31  *
6.31  * Key for file : lengde spesifikasjonen
6.31  *
6.31 c     fvlslu_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    fvlslu_numm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
      *
      * Legger inn firmanummer
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn ordrenummer og suffix i nøkklene
      *
     c                   eval      fotxlu_numm = p_numm
     c                   eval      fodtlu_numm = p_numm
     c                   eval      lwpllu_numm = p_numm
6.31 c                   eval      fvlslu_numm = p_numm
      *
     c                   endsr
