     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VA770R
      * Beskrivelse . . : Enhet, Omregning av antall til ny enhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100204 AMD  Nytt program
6.20  *       091111 bsa  Legger inn sjekk mot vareadministrasjon (skaffevarer)
6.20  *                   hvis den ikke finnes i VVEN.
6.31  * 6.30  260114 bof  Utvidet nøkkel i JVPKPF
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.31 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.20 fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vefirm)
     d p_vare          s                   like(vevare)
     d p_enh1          s                   like(veenhe)
     d p_enh2          s                   like(veenhe)
     d p_ant1          s             11  3
     d p_ant2          s             11  3
      *
      * Definering av variable i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
6.31 d vvarl1_vare     s                   like(vvvare)
6.20 d jvpkl2_vare     s                   like(jwvare)
6.31 d jvpkl2_ldor     s                   like(jwldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vefirm)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
6.32 d w_dato          s               d   datfmt(*iso)
      *
6.32 d b_pakn          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Omregning fra enhet 1 til enhet 2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer ny verdi for antall
      *
     c                   eval      p_ant2 = 0
      *
      * Omregner antall til valgt enhet
      *
     c                   eval      vvenl1_vare = p_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
      *
     c                   if        veenhe = p_enh1
     c                   eval      w_omr1 = veomre
     c                   endif
      *
     c                   if        veenhe = p_enh2
     c                   eval      w_omr2 = veomre
     c                   endif
      *
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omr2 <> 0
     c                   eval(h)   p_ant2 = p_ant1 * w_omr1 / w_omr2
     c                   endif
6.20  * Hvis faktor ikke er funnet, sjekk på nytt mot vareadministrasjon
6.20 c                   if        p_ant2 = 0
6.20 c                   eval      jvpkl2_vare = p_vare
6.31 c                   eval      jvpkl2_ldor = vvnlev
6.20 c     jvpkl2_key    setll     jvpkl2
6.20 c     jvpkl2_key    reade     jvpkl2
6.20 c                   dow       not %eof
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
6.20  *
6.20 c                   if        jwenhe = p_enh1
6.20 c                   eval      w_omr1 = jwofak
6.20 c                   endif
6.20  *
6.20 c                   if        jwenhe = p_enh2
6.20 c                   eval      w_omr2 = jwofak
6.20 c                   endif
 *
6.32 c                   endif
6.20  *
6.20 c     jvpkl2_key    reade     jvpkl2
6.20 c                   enddo
6.20  *
6.20 c                   if        w_omr2 <> 0
6.20 c                   eval(h)   p_ant2 = p_ant1 * w_omr1 / w_omr2
6.20 c                   endif
6.20  *
6.20 c                   endif
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32 c                   time                    w_dato
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enh1
     c                   parm                    p_ant1
     c                   parm                    p_enh2
     c                   parm                    p_ant2
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
6.31  *
6.31  * Key for oppslag på vare
6.31  *
6.31 c     vvarl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vvarl1_vare
6.20  *
6.20  * Key for oppslag på pakningsregister
6.20  *
6.20 c     jvpkl2_key    klist
6.20 c                   kfld                    jvpkl2_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.31  *
6.31  * Leser vare
6.31  *
6.31 c                   eval      vvarl1_vare = p_vare
6.31 c     vvarl1_key    chain     vvarl1
      *
     c                   endsr
