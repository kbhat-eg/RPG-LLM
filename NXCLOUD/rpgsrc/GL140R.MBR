      /TITLE  GL101R Renummerering av linjenummer på utbetalingsforslag
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASTELE                                        *
      * Program . . . . : GL140R                                        *
      * Beskrivelse . . : Test på om forslaget inneholder feilretur.    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign *
      * --------- ------  ----------------------------------------------*
      *           180504  Nytt program.                            RHO  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Overflow i rapport.                                  *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    31    = Brukes også til meldinger i rapport.                 *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgubflu    if   e           k disk    rename(gabfpfr:gabfplu)
      *
      /eject
      *
      * Datastruktur for å "pakke ut" parameterliste:
      *
     d                 ds
     d d_lis2                        18
     d  d_firm                        3  0 overlay(d_lis2)
     d  d_bfnr                        3  0 overlay(d_lis2:4)
     d  d_bfli                        5  0 overlay(d_lis2:7)
     d  d_koin                        1    overlay(d_lis2:12)
     d  d_kout                        1    overlay(d_lis2:13)
     d  d_anfe                        5  0 overlay(d_lis2:14)
      *
     d wplist          s             18
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Finner antall feil i forslaget:
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Posisjonerer på GUBFLU (GUBFPF) med firma og forslagsnr
      *
     c     gubfl2_key    setll     gubflu
     c     gubfl2_key    reade     gubflu                                 60
      *
     c                   dow       *in60 = *off
      *
      * Tester på om linje er merket med "F" (feilretur):
      *
     c                   if        gareko = 'F'
     c                   eval      d_anfe = d_anfe + 1
     c                   eval      d_kout = 'F'
     c                   endif
      * Leser neste post.
      *
     c     gubfl2_key    reade     gubflu                                 60
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Finner Neste linjenummer for feil dersom F7 er valgt i GL104R:
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Posisjonerer på GUBFLU (GUBFPF) med firma og forslagsnr
      *
     c                   if        d_koin = '7'
      *
     c     gubfl3_key    setll     gubflu
     c     gubfl2_key    reade     gubflu                                 60
      *
      * Forutsetter at dersom d_bfli inneholder en verdi, så er dette
      * linjenummer for en feilretur, og allerede lest en gang. Legger
      * derfor inn et "dummy" les for å lese NESTE feilretur:
      *
     c                   if        d_bfli <> 0
     c     gubfl2_key    reade     gubflu                                 60
     c                   endif
      *
     c                   dow       *in60 = *off
      *
      * Tester på om linje er merket med "F" (feilretur):
      *
     c                   if        gareko = 'F'
     c                   eval      d_bfli = gabfli
     c                   leave
     c                   endif
      *
      * Leser neste post.
      *
     c     gubfl2_key    reade     gubflu                                 60
      *
     c                   enddo
     c                   endif
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   movel     d_lis2        wplist
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    wplist
      *
      * "Pakker ut" parameterliste:
      *
     c                   movel     wplist        d_lis2
     c                   eval      d_anfe = 0
      *
      * Key til GUBFLU (GUBFPF):
      *
     c     gubfl2_key    klist
     c                   kfld                    d_firm
     c                   kfld                    d_bfnr
      *
      * Key til GUBFLU (GUBFPF):
      *
     c     gubfl3_key    klist
     c                   kfld                    d_firm
     c                   kfld                    d_bfnr
     c                   kfld                    d_bfli
      *
     c                   endsr
