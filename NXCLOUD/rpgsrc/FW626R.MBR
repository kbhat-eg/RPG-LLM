     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FW626R
      * Beskrivelse . . : Oppdatering av varer/priser, kontr./journal
      *                   Programmet overfører endrede varer fra leverandør
      *                   til vareregisteret
      *                   Skriver ut kontroll- eller journalliste.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121199 HKO  Nytt program
      *       070200 HKO  Byttet rekkefølge ved sletting av nyere priser
      *       250300 HKO  Fjernet oppdatering av dimmensjon, og lagt inn
      *                   oppdatering av høyde, bredde og lengde
      *       211003 HKO  Utvidet med cross-docking informasjon
      *       290304 GRO  Lagt til leveringstype
      *       040805 HKO  Flyttet vekt og voluminformasjon fra vare til
      *                   vare-enhet.
      *       260407 HKO  Rettet feil ved oppdatering av eksisterende pris
5.70  * PRGR  041108 BOF  Utivdet med prisgruppe - forutsetter at priser
      *                   inn via denne innlesing alltid har prgr = blank
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  080709 bof  Oppdaterer ean-nr på vare-enhet
6.12  * 6.10  240909 bof  Oppdaterer ean-nr på vare-enhet-prisgiver
6.20  * 6.20  030411 bof  Utvidet vvprpf med prisgiver
6.30  * 6.30  280514 BKV  LVAR økt fra 15 til 20
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflval3    if   e           k disk    rename(flvapfr:flval3r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
     fvvenlu    uf   e           k disk    rename(vvenpfr:vvenlur)
6.12 fvveplu    uf a e           k disk    rename(vveppfr:vveplur)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
     fflvalu    uf   e           k disk    rename(flvapfr:flvalur)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.12 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
      *
     ffw626p    o    e             printer
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        96
     d  d_firm                        3  0 overlay(d_list)
     d  d_otyp                        1  0 overlay(d_list:4)
     d  d_ldor                        6  0 overlay(d_list:5)
     d  d_ogrp                        2  0 overlay(d_list:11)
     d  d_hgrp                        2  0 overlay(d_list:13)
     d  d_ugrp                        3  0 overlay(d_list:15)
6.30 d  d_lvaf                       20    overlay(d_list:18)
6.30 d  d_lvat                       20    overlay(d_list:38)
6.30 d  d_dato                         d   overlay(d_list:58) datfmt(*iso)
6.30 d  d_ipro                        5  2 overlay(d_list:68)
6.30 d  d_kpro                        5  2 overlay(d_list:73)
6.30 d  d_spro                        5  2 overlay(d_list:78)
6.30 d  d_inio                        1  0 overlay(d_list:83)
6.30 d  d_koio                        1  0 overlay(d_list:84)
6.30 d  d_saio                        1  0 overlay(d_list:85)
6.30 d  d_nypo                        1  0 overlay(d_list:86)
6.30 d  d_ltyo                        1  0 overlay(d_list:87)
6.30 d  d_oppd                        1  0 overlay(d_list:88)
      *
      * Definering av parametre
      *
     d p_list          s             96
      *
      * Definering av variabler i nøkler
      *
     d vvarl5_lvar     s                   like(vvlvar)
     d vvarlu_vare     s                   like(vvvare)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvprlu_vare     s                   like(vpvare)
6.20 d vvprlu_ldor     s                   like(vpldor)
5.70 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
     d vvenlu_vare     s                   like(vevare)
     d vvenlu_enhe     s                   like(veenhe)
6.12 d vveplu_pvar     s                   like(vepvar)
6.12 d vveplu_penh     s                   like(vepenh)
6.12 d vveplu_pldo     s                   like(vepldo)
6.11 d veanlu_eann     s                   like(vxeann)
     d flvalu_line     s                   like(fwline)
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
6.12 d vvepl2_pgti     s                   like(vepgti)
      *
      * Definering av variable
      *
     d w_indi          s              2  0
     d w_stat          s              1  0
     d w_udat          s               d   datfmt(*iso)
     d w_dato          s               d   datfmt(*iso)
     d w_timestamp     s             12  0
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
     d w_kvar          s              1  0
     d w_saom          s             12  6
     d w_koom          s             12  6
     d w_inom          s             12  6
6.11 d w_eann          s             14  0
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
     d w_lety_gml      s                   like(vplety)
     d w_enhe_gml      s                   like(vpenhe)
      *
6.11 d b_dupp          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Behandling av kontrolliste/journalliste, og utskrift av detaljlinjer
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
     c                   if        d_oppd = 2
     c                   eval      a1otxt = 'Journalliste'
     c                   else
     c                   eval      a1otxt = 'Kontrolliste'
     c                   endif
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2otyp = d_otyp
     c                   eval      a2ldor = d_ldor
     c                   eval      a2ogrp = d_ogrp
     c                   eval      a2hgrp = d_hgrp
     c                   eval      a2ugrp = d_ugrp
     c                   eval      a2lvaf = d_lvaf
     c                   eval      a2lvat = d_lvat
     c                   eval      a2ipro = d_ipro
     c                   eval      a2kpro = d_kpro
     c                   eval      a2spro = d_spro
     c                   eval      a2inio = d_inio
     c                   eval      a2koio = d_koio
     c                   eval      a2saio = d_saio
     c                   eval      a2nypo = d_nypo
     c                   eval      a2ltyo = d_ltyo
      *
     c                   eval      rlevl1_levr = d_ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2lnav = rlnavn
     c                   endif
      *
     c                   if        d_ogrp <> 0
     c                   eval      vogrl1_oogr = d_ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        d_hgrp <> 0
     c                   eval      vhgrl1_hogr = d_ogrp
     c                   eval      vhgrl1_hhgr = d_hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        d_ugrp <> 0
     c                   eval      vugrl1_uogr = d_ogrp
     c                   eval      vugrl1_uhgr = d_hgrp
     c                   eval      vugrl1_uugr = d_ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off
     c                   eval      a2utxt = vgutxt
     c                   endif
     c                   endif
      *
     c     *dmy          move      d_dato        a2dato
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster som matcher utplukkskriteriene
      * Behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c     flval3_key    setll     flval3
     c     flval3_key    reade     flval3                                 90
     c                   dow       *in90 = *off
      *
      * Sjekker om varene skal være med på utplukket
      *
     c                   if        d_lvaf <> *blank and
     c                             fwlvar <  d_lvaf
     c                   goto      les_neste
     c                   endif
      *
     c                   if        d_lvat <> *blank and
     c                             fwlvar >  d_lvat
     c                   goto      les_neste
     c                   endif
      *
     c                   if        fwhkod = 1
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
     c                   if        d_ogrp <> 0 and
     c                             d_ogrp <> vvogrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        d_hgrp <> 0 and
     c                             d_hgrp <> vvhgrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        d_ugrp <> 0 and
     c                             d_ugrp <> vvugrp
     c                   goto      les_neste
     c                   endif
      *
      * Kaller sub-program for validering av vare, og retur av statuskode
      *
     c                   call      'FW113R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    fwline
     c                   parm                    fwlvar
     c                   parm                    fwtek1
     c                   parm                    fweann
     c                   parm                    fwnobb
     c                   parm                    fwenhe
     c                   parm                    fwinpr
     c                   parm                    fwsapr
     c                   parm                    fwtvar
     c                   parm                    w_kvar
     c                   parm                    w_stat
     c                   parm                    w_indi
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Oppdaterer vareregister dersom journalliste
      *
     c                   if        d_oppd = 2
     c                   exsr      oppdater
     c                   endif
      *
      * Leser neste rad
      *
     c     les_neste     tag
      *
     c     flval3_key    reade     flval3                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
      * Flytter felter til kvitteringsvariable
      *
     c                   eval      d1stat = w_stat
6.30 c*                  eval      d1tek1 = fwtek1
6.30 c                   movel     fwtek1        d1tek1
     c                   eval      d1lvar = fwlvar
     c                   eval      d1vare = vvvare
     c                   eval      d1nobb = fwnobb
     c                   eval      d1dato = 0
     c                   eval      d1enhe = fwenhe
     c                   eval      d1ingm = 0
     c                   eval      d1inpr = 0
     c                   eval      d1kogm = 0
     c                   eval      d1kopr = 0
     c                   eval      d1sagm = 0
     c                   eval      d1sapr = 0
     c                   eval      d1saim = 0
     c                   eval      d1dekn = 0
     c                   eval      d1endr = 0
     c                   eval      d1kpri = vvkpri
     c                   eval      d1vtyp = vvvtyp
     c                   eval      d1kjus = vvkjus
      *
     c                   if        fwdato <> *loval
     c     *dmy          move      fwdato        d1dato
     c                   endif
      *
      * Finner eksisterende priser i vareregisteret på oppgitt enhet og
      * leveringstype 0
      *
     c                   eval      vvprl1_vare = vvvare
6.20 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = *blank
     c                   eval      vvprl1_enhe = fwenhe
     c*                  eval      vvprl1_lety = 0
     c                   eval      vvprl1_lety = fwlety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   if        w_udat >= vppdat
     c                   eval      d1sagm = vpsapr
     c                   eval      d1kogm = vpkopr
     c                   eval      d1ingm = vpinpr
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
      * Henter/beregner nye priser
      *
     c                   eval      d1sapr = d1sagm
     c                   eval      d1inpr = d1ingm
     c                   eval      d1kopr = d1kogm
      *
     c                   if        d_saio = 0
     c                   eval(h)   d1sapr = fwsapr + (fwsapr * d_spro / 100)
     c                   endif
      *
     c                   if        d_inio = 0
     c                   eval(h)   d1inpr = fwinpr + (fwinpr * d_ipro / 100)
     c                   endif
      *
     c                   if        d_koio = 0 and
     c                             d_inio = 0
     c                   eval(h)   d1kopr = d1inpr + (d1inpr * d_kpro / 100)
     c                   endif
      *
      * Finner salgspris inkl. mva
      *
     c                   if        d1sapr <> 0
     c                   eval      d1saim = d1sapr * w_mvat
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d1saim
     c                   endif
      *
      * Finner prosentvis endring av salgspris
      *
     c                   if        d1sagm <> 0
     c                   select
     c                   when      (100 * d1sapr / d1sagm) - 100 < -999,99
     c                   eval      d1endr = -999,99
     c                   when      (100 * d1sapr / d1sagm) - 100 > 999,99
     c                   eval      d1endr = 999,99
     c                   other
     c                   eval(h)   d1endr = (100 * d1sapr / d1sagm) - 100
     c                   endsl
     c                   endif
      *
      * Finner dekningsgrad (DG)
      *
     c                   if        d1sapr <> 0
     c                   select
     c                   when      (100 - (d1kopr * 100 / d1sapr)) < -999,99
     c                   eval      d1dekn = -999,99
     c                   when      (100 - (d1kopr * 100 / d1sapr)) > 999,99
     c                   eval      d1dekn = 999,99
     c                   other
     c                   eval(h)   d1dekn = 100 - (d1kopr * 100 / d1sapr)
     c                   endsl
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av vareregister dersom journalliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   if        w_stat <> 0
     c                   goto      end_oppd
     c                   endif
      *
      * Finner prisendrings-faktorer
      *
     c                   eval      w_saom = 1
     c                   eval      w_koom = 1
     c                   eval      w_inom = 1
      *
     c                   if        d1sagm <> 0 or
     c                             d1kogm <> 0 or
     c                             d1ingm <> 0
      *
     c                   if        d_saio = 0 and
     c                             d1sapr <> 0 and
     c                             d1sagm <> 0
     c                   eval(h)   w_saom = d1sapr / d1sagm
     c                   endif
      *
     c                   if        d_koio = 0 and
     c                             d1kopr <> 0 and
     c                             d1kogm <> 0
     c                   eval(h)   w_koom = d1kopr / d1kogm
     c                   endif
      *
     c                   if        d_inio = 0 and
     c                             d1inpr <> 0 and
     c                             d1ingm <> 0
     c                   eval(h)   w_inom = d1inpr / d1ingm
     c                   endif
      *
     c                   endif
      *
      * Oppdaterer priser på alle enheter og leveringstyper med
      * prisendringsfaktor
      * Dersom det finnes priser nyere enn -eller lik prisdato, oppdateres
      * disse med endringen. Ellers opprettes nye priser.
      *
     c                   if        d_ltyo = 0
     c                   if        w_saom <> 1 or
     c                             w_koom <> 1 or
     c                             w_inom <> 1
      *
     c                   eval      w_enhe_gml = *blank
     c                   eval      w_lety_gml = -1
      *
     c                   eval      vvprl1_vare = vvvare
     c     vvprl1_key2   setll     vvprl1
     c     vvprl1_key2   reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   if        w_udat >= vppdat
      *
     c                   if        w_enhe_gml <> vpenhe or
     c                             w_lety_gml <> vplety
     c                   exsr      oppdat_pris
     c                   endif
      *
     c                   eval      w_enhe_gml = vpenhe
     c                   eval      w_lety_gml = vplety
      *
     c                   endif
      *
     c     vvprl1_key2   reade     vvprl1                                 91
     c                   enddo
      *
     c                   endif
     c                   endif
      *
     c                   if        d_ltyo = 1
      *
     c                   eval      vvprlu_vare = vvvare
6.20 c                   eval      vvprlu_ldor = vvldor
5.70 c                   eval      vvprlu_prgr = *blank
     c                   eval      vvprlu_enhe = fwenhe
     c                   eval      vvprlu_lety = fwlety
     c                   eval      vvprlu_pdat = d_dato
     c     vvprlu_key    chain     vvprlur
      *
     c                   eval      vpsapr = d1sapr
     c                   eval      vpkopr = d1kopr
     c                   eval      vpinpr = d1inpr
      *
     c                   eval      vppdat = d_dato
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        %found(vvprlu)
     c                   update    vvprlur
     c                   else
     c                   eval      vpvare = vvvare
     c                   eval      vpldor = vvldor
     c                   eval      vpprgr = *blank
     c                   eval      vpenhe = fwenhe
     c                   eval      vplety = fwlety
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   endif
      *
     c                   endif
      *
      * Legger inn data og oppdaterer vareregister
      *
     c                   eval      vvarlu_vare = vvvare
     c     vvarlu_key    chain     vvarlur                            91
      *
     c                   eval      vvldor = fwldor
     c                   eval      vvlvar = fwlvar
      *
     c                   if        fwbmen <> 0
     c                   eval      vvbmen = fwbmen
     c                   endif
      *
      * Endring av vareinformasjon
      *
     c                   if        d_otyp = 2
     c                   eval      vvtek1 = fwtek1
     c                   eval      vvtek2 = fwtek2
     c                   eval      vvnobb = fwnobb
     c                   eval      vvcrdo = fwcrdo
     c                   eval      vveusr = l_user
     c                   time                    vvedat
     c                   time                    vvetim
     c                   endif
      *
     c                   update    vvarlur
      *
      * Endring av enhet
      *
     c                   if        d_otyp = 2
      *
6.11 c                   clear                   vvenlur
      *
     c                   eval      vvenlu_vare = vvvare
     c                   eval      vvenlu_enhe = fwenhe
     c     vvenlu_key    chain     vvenlur
     c                   eval      vevekt = fwvekt
     c                   eval      vevolu = fwvolu
      *
     c                   if        fwleng <> 0
     c                   eval      veleng = fwleng
     c                   endif
      *
     c                   if        fwbred <> 0
     c                   eval      vebred = fwbred
     c                   endif
      *
     c                   if        fwhoyd <> 0
     c                   eval      vehoyd = fwhoyd
     c                   endif
      *
     c                   if        vevolu = 0 and
     c                             veleng <> 0 and
     c                             vebred <> 0 and
     c                             vehoyd <> 0
     c                   eval(h)   vevolu = (veleng / 1000) *
     c                                      (vebred / 1000) *
     c                                      (vehoyd / 1000)
     c                   endif
      *
     c                   eval      veeusr = l_user
     c                   time                    veedat
     c                   time                    veetim
      *
     c                   update    vvenlur
      *
     c                   endif
      *
6.12 c                   eval      w_eann = fweann
6.12 c                   exsr      sjekk_ean
6.12 c                   if        b_dupp = *off
6.12 c                   eval      vveplu_pvar = vvvare
6.12 c                   eval      vveplu_penh = fwenhe
6.12 c                   eval      vveplu_pldo = w_ldor
6.12 c     vveplu_key    chain     vveplur
6.12 c                   eval      vepgti = w_eann
6.12 c                   if        %found(vveplu)
6.12 c                   eval      vepeus = l_user
6.12 c                   time                    vepeti
6.12 c                   time                    vepeda
6.12 c                   update    vveplur
6.12 c                   else
6.12 c                   eval      vepvar = vveplu_pvar
6.12 c                   eval      vepenh = vveplu_penh
6.12 c                   eval      vepldo = vveplu_pldo
6.12 c                   eval      vepeus = l_user
6.12 c                   time                    vepeti
6.12 c                   time                    vepeda
6.12 c                   eval      vepous = l_user
6.12 c                   time                    vepoti
6.12 c                   time                    vepoda
6.12 c                   write     vveplur
6.12 c                   endif
6.12 c                   endif
      *
6.11  * Hvis den finnes på ean-reg. så slettes den - skal
6.11  * ligge på vareenhet.
      *
     c                   if        fweann >= 1000000000000 and
     c                             fweann <= 9999999999999
6.11  *
6.11 c                   eval      veanlu_eann = fweann
6.11 c     veanlu_key    chain     veanlu                             91
6.11 c                   if        %found(veanlu)
6.11 c                   delete    veanlur
6.11 c                   endif
      *
     c                   endif
      *
      * Kaller program for oppdatering av søketekster
      *
     c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    vvvare
      *
      * Legger varen på Hold
      *
     c                   eval      flvalu_line = fwline
     c     flvalu_key    chain     flvalur                            91
     c                   if        *in91 = *off
     c                   eval      fwhkod = 1
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   update    flvalur
     c                   endif
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av priser på enhet og leveringstype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_pris   begsr
      *
      * Avbryter dersom kun leveringstype 0 skal oppdateres, og leverings-
      * type er forskjellig fra 0
      * -> endret til :
      * Avbryter dersom lev.type fra inputfile og prisrec. forskjellig -
      * og "kun leveringstype"-parameter er satt til 1=Ja
      *
     c                   if        d_ltyo = 1 and
     c*                            vplety <> 0
     c                             vplety <> fwlety
     c                   goto      end_pris
     c                   endif
      *
      * Oppretter/oppdaterer vare-pris
      *
     c                   eval      vvprlu_vare = vpvare
6.20 c                   eval      vvprlu_ldor = vpldor
5.70 c                   eval      vvprlu_prgr = *blank
     c                   eval      vvprlu_enhe = vpenhe
     c                   eval      vvprlu_lety = vplety
     c                   eval      vvprlu_pdat = d_dato
     c     vvprlu_key    chain     vvprlur                            92
      *
     c                   if        vpenhe = fwenhe and
     c                             vplety = 0
     c                   eval      vpsapr = d1sapr
     c                   eval      vpkopr = d1kopr
     c                   eval      vpinpr = d1inpr
     c                   else
     c                   eval(h)   vpsapr = vpsapr * w_saom
     c                   eval(h)   vpkopr = vpkopr * w_koom
     c                   eval(h)   vpinpr = vpinpr * w_inom
     c                   endif
      *
     c                   eval      vppdat = d_dato
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        *in92 = *off
     c                   update    vvprlur
     c                   else
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   endif
      *
      * Sletter priser nyere enn prisdato
      *
     c                   if        d_nypo = 0
      *
     c     d_dato        adddur    1:*d          w_dato
      *
     c                   eval      vvprlu_vare = vpvare
6.20 c                   eval      vvprlu_ldor = vpldor
5.70 c                   eval      vvprlu_prgr = *blank
     c                   eval      vvprlu_enhe = vpenhe
     c                   eval      vvprlu_lety = vplety
     c                   eval      vvprlu_pdat = w_dato
     c     vvprlu_key    setll     vvprlur
     c     vvprlu_key2   reade     vvprlur                                92
     c                   dow       *in92 = *off
     c                   delete    vvprlur
     c     vvprlu_key2   reade     vvprlur                                92
     c                   enddo
      *
     c                   endif
      *
     c     end_pris      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  * Subrutine for å sjekke om ean-nr ligger inne fra før
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     sjekk_ean     begsr
6.11 c                   eval      b_dupp = *off
6.11  *
6.11 c                   eval      vvepl2_pgti = w_eann
6.11 c     vvepl2_key    chain     vvepl2
6.11 c                   if        %found(vvepl2)
6.11 c                   eval      b_dupp = *on
6.11 c                   endif
6.11  *
6.11 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på leverandør vareregister
      *
     c     flval3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
      *
      * Key for oppslag på vareregister
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppdatering av vareregister
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
     c     vvprl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
6.20 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
     c     vvprlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
6.20 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
      *
      * Key for oppdatering av vare-enhet
      *
     c     vvenlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlu_vare
     c                   kfld                    vvenlu_enhe
      *
      * Key for oppslag på EAN-register
      *
6.11 c     veanlu_key    klist
     c                   kfld                    w_firm
6.11 c                   kfld                    veanlu_eann
      *
      * Key for oppdatering av leverandør vareregister
      *
     c     flvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flvalu_line
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
6.12  *
6.12  * Key for oppslag på vare-enhet   med ean-nr
6.12  *
6.12 c     vvepl2_key    klist
6.12 c                   kfld                    w_firm
6.11 c                   kfld                    vvepl2_pgti
6.12  *
6.12  * Key for oppdatering av EVR-vare-enhet-prisgiver
6.12  *
6.12 c     vveplu_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vveplu_pvar
6.12 c                   kfld                    vveplu_penh
6.12 c                   kfld                    vveplu_pldo
      *
      *
      * Kopierer parameter til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma og leverandør fra parameter
      *
     c                   eval      w_firm = d_firm
     c                   eval      w_ldor = d_ldor
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter mva-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    d_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   endsr
