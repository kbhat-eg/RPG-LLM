     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LO612R
      * Beskrivelse . . : Inntasting av parameter for varsel-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  * R6.20 260116 bkv  ny parameter
7.xx  * 7.00  090316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flo612d    cf   e             workstn
      *
      * Definering av parametere
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
     d w_navn          s                   like(rlnavn)
6.20 d w_lagr          s              2  0 inz(0)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1 Behandler valg for utskrift av varsel-liste til leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
     c                   eval      a1ldor = 0
     c                   eval      a1dagr = 0
      *
     c     a1taga        tag
      *
     c                   exfmt     a1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      a1taga
     c                   endsl
      *
      * Validering
      *
      * Er det gyldig leverandørnummer ?
      *
     c                   if        a1ldor <> 0
     c                   eval      rlevl1_levr = a1ldor
     c     rlevl1_key    chain     rlevl1r
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      * Er dato gyldig ?
      *
     c     *dmy          test(d)                 a1dato                 32
     c                   if        *in32
     c                   goto      a1taga
     c                   endif
      *
      * Er kode for dato gyldig ?
      *
     c                   if        a1onbe <> 1 and
     c                             a1onbe <> 2
     c                   eval      *in33 = *on
     c                   goto      a1taga
     c                   endif
      *
      * Call av program for utskrift
      *
     c                   call      'LO614C'
     c                   parm                    a1ldor
     c                   parm                    a1dato
     c                   parm                    a1dagr
     c                   parm                    a1onbe
6.20 c                   parm                    w_lagr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Spørring på leverandørnummer
      *
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    a1ldor
     c                   parm                    w_navn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Flytter over firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Legg inn dagens dato
      *
     c                   time                    w_udat
     c     *dmy          move      w_udat        a1dato
      *
      * Legg inn ønsket dato som default
      *
     c                   eval      a1onbe = 1
      *
     c                   endsr
