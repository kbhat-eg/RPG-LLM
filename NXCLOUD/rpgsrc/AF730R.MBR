      /define LoggingIsOn
     HDFTACTGRP(*NO)
     h option(*nodebugio : *srcstmt) datedit(*dmy)
     HDECEDIT(',')
      /free
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : AF730R
      * Beskrivelse . . : Sjekk og sender mail hvis det er for gamle filer i
      *                   FTP kataloger
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120718 BKV  Nytt program
      *
      *
       //dcl-s  til_mail        char(50) INZ('support@eg.no');
       dcl-s  til_mail        char(50) INZ('nexstepdrift@eg.no');
       //dcl-s  til_mail        char(50) INZ('bjkva@eg.no');
       dcl-s  fra_mail        char(50) INZ('noreply@egcloud.no');
       dcl-s  dirlist_lib     char(8)  INZ('EG_DRIFT');
       dcl-s  dirlist_pre     char(7)  INZ('DIRLIST');
       dcl-s  dirlist_tabell  char(17) ;
       dcl-s  ant_dager       int(10)  INZ(10);
       dcl-s  antall_dager    char(2);   // char variant av ant_dager
       dcl-s  sjekk_type_BUSY     char(4) inz('BUSY');
       dcl-s  sjekk_type_BYGGDOK  char(7) inz('BYGGDOK');
       dcl-s  sjekk_type_COBUILDER2  char(10) inz('COBUILDER2');
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Prototype grensesnitt-definisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
       dcl-pr P_AF732C extpgm('AF732C');
         p_filgr       char(6);
         p_from        char(50);
         p_to          char(50);
         p_subj        char(250);
         p_inif        char(100);
       end-pr;
      *
       dcl-pr P_AF731C extpgm('AF731C');
         p_path        char(50);
         p_lib         char(8);
         p_pre         char(7);
         p_error       char(10);
       end-pr;
      *
       dcl-pr AF730R  extpgm('AF730R');
       end-pr;
      *
      * Arbeidsvariabler
       dcl-s finished        ind;
      *
       DCL-S  RS_Filgrupper SQLTYPE(RESULT_SET_LOCATOR);
       dcl-ds dsFilgrupper Qualified Dim(500);
         Miljo      char(10) inz('');
         Filgruppe  char(10) inz('');
         Lvr        char(10) inz('');
       end-ds;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * CALL PGM(AF730R) PARM()
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-pi AF730R;
       end-pi;
      *
       Exec SQL
         Set Option DatFmt=*ISO;
      *
       dcl-s status  packed(1:0);
       dcl-s sjekk_type char(100) ;
      *
       antall_dager = %char(ant_dager);
       dirlist_tabell  = dirlist_lib + '.'+ dirlist_pre + 'O';
      *
       /if defined(LoggingIsOn)
       JobLog ('>>> START AF730R');
       /endif
       status = FinnFilGr();


       if (status = 1);
         sjekk_type = sjekk_type_BUSY;
         status = SjekkFilGr(sjekk_type);
         sjekk_type = sjekk_type_BYGGDOK;
         status = SjekkFilGr(sjekk_type);
         sjekk_type = sjekk_type_COBUILDER2;
         status = SjekkFilGr(sjekk_type);
       ENDIF;
      *
       /if defined(LoggingIsOn)
       JobLog ('>>> STOPP AF730R');
       /endif
      *
       *inlr = *on;
       return;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk FTP kataloger for alle filgrupper
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc SjekkFilGr;
         dcl-pi *N packed(1:0);
           sjekk_type        char(50);
         end-pi;

         dcl-s ok  packed(1:0);
         dcl-s count  int(10);
         dcl-s ant  packed(9:0);

         dcl-s sqlCommand char(2048);
         dcl-s hva      char(10);
         dcl-s path     char(512);
         dcl-s filename char(64);
         dcl-s errortxt char(10);

         dcl-s  filgr  char(6);
         dcl-s  subj char(250);
         dcl-s  inif char(100) ;
         dcl-s filnavn_pattern char(50);
         dcl-s oppdatert_dato date(*iso);
         dcl-s oppdatert_dato_sjekk date(*iso);

         //  oppdatert_dato_sjekk = %date() - %days(ant_dager);

         ok = 0;

         for count = 1 to 500 ;

           if (%trim(dsFilgrupper(count).Miljo) = '') ;
             ok = 1;
             return ok;
           ENDIF;

           /if defined(LoggingIsOn)
           JobLog ('> Sjekk. milj√∏=/' + %trim(dsFilgrupper(count).Miljo) +
                   '/ filgruppe=/' + %trim(dsFilgrupper(count).Filgruppe) +
                   '/ type=/' + %trim(sjekk_type) +
                   '/'
                       );
           /endif

           if (sjekk_type = sjekk_type_BYGGDOK);
             sqlCommand = 'SELECT AFNAVN, A.AFLOCD,A.AFFILN,A.AFKDAT' +
               ' , IFNULL((SELECT MAX(B.FWBEDA) - 1 DAYS FROM ' +
                   %trim(dsFilgrupper(count).Filgruppe) +
             '.FBDOPF B WHERE B.FWBFIR = A.AFFIRM), CURRENT_DATE - 1000 DAYS)'+
                    ' as dato '  +
                ' FROM ' + %trim(dsFilgrupper(count).Filgruppe) + '.AFTPPF A +
               WHERE RTRIM(A.AFNAVN) = RTRIM('''+sjekk_type+''') ';
           elseif (sjekk_type = sjekk_type_BUSY);
             sqlCommand = 'SELECT AFNAVN, AFLOCD,AFFILN,AFKDAT' +
                ' , CURRENT_DATE '  +
                ' FROM ' + %trim(dsFilgrupper(count).Filgruppe) + '.AFTPPF +
               WHERE  AFSTAT=''B''  ';
           else;
             sqlCommand = 'SELECT AFNAVN, AFLOCD,AFFILN,AFKDAT' +
                ' , CURRENT_DATE - '  + antall_dager  + ' DAYS '  +
                ' FROM ' + %trim(dsFilgrupper(count).Filgruppe) + '.AFTPPF +
               WHERE RTRIM(AFNAVN) = RTRIM('''+sjekk_type+''') ';
           endif;

           exec SQL
             PREPARE SQL_STMT FROM :sqlCommand;
           if sqlcod < 0;
             iter;
           endif;

           exec SQL
             DECLARE STMT CURSOR FOR SQL_STMT;

           exec SQL
             OPEN STMT using :sjekk_type;
           if sqlcod < 0;
             iter;
           endif;

           finished = *off;

           Dou finished;

             exec SQL
             fetch STMT into :hva, :path, :filename, :oppdatert_dato,
                     :oppdatert_dato_sjekk;

             if sqlcod = 100;
               finished = *on;
               Leave;
             endif;

             if sqlcod < 0;
               finished = *on;
               Leave;
             Endif;

             filgr = %trim(dsFilgrupper(count).Filgruppe);
             subj = 'AF730R: ' + filgr +
                ' : ' + %trim(path) + ' : ' + %trim(filename);

             if (sjekk_type = sjekk_type_BUSY);

               inif = hva + ' : Sjekk BUSY status , sist oppdatert : ' +
                       %char(oppdatert_dato)  ;
               CallP P_AF732C ( filgr : til_mail : fra_mail : subj : inif);

             else;  // ikke busy sjekk

               if (oppdatert_dato < oppdatert_dato_sjekk);
                 inif = hva + ' : Mappen er sist oppdatert : ' + %trim(path) +
                   ' , dato: ' + %char(oppdatert_dato)  ;
                 CallP P_AF732C ( filgr : til_mail : fra_mail : subj : inif);
                 /if defined(LoggingIsOn)
                 JobLog ('> MAIL 1. filgruppe=/' +
                        %trim(dsFilgrupper(count).Filgruppe) + '/' );
               /endif
               endif;

               CallP P_AF731C (path :  dirlist_lib : dirlist_pre : errortxt);
               if (errortxt <> *blanks);
                 inif = hva + ' : Feil i katalogsjekk: ' + %trim(path)
                          + ' , feil: ' + errortxt  ;
                 CallP P_AF732C ( filgr : til_mail : fra_mail : subj : inif);
                 /if defined(LoggingIsOn)
                 JobLog ('> MAIL 2. filgruppe=/' +
                        %trim(dsFilgrupper(count).Filgruppe) + '/' );
               /endif
               else;

                 filnavn_pattern = %ScanRpl('*' : '%' : filename);
                 ant = SjekkKatalog(filnavn_pattern);
                 if (ant > 0);
                 inif = hva + ' : Sjekk innhold: ' + %trim(path) + ' , ANTALL: '
                                    + %char(ant)  ;
                   CallP P_AF732C ( filgr : til_mail : fra_mail : subj : inif);
                   /if defined(LoggingIsOn)
                   JobLog ('> MAIL 3. filgruppe=/' +
                          %trim(dsFilgrupper(count).Filgruppe) + '/' );
                 /endif
                 endif;

               endif;

             endif;


           Enddo;

           exec sql Close STMT;

         ENDFOR;

         return ok;
       end-proc;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finn alle Filgrupper
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc SjekkKatalog;
         dcl-pi *N packed(9:0);
           filnavn_pattern        char(50);
         end-pi;

         dcl-s antall  packed(8:0) INZ(0);

         dcl-s sqlCommand char(2048);

         filnavn_pattern = %trim(filnavn_pattern);

         if (%check('%' : filnavn_pattern) > 0);
           sqlCommand = ' +
             SELECT COUNT(*) FROM '+dirlist_tabell +'  +
               WHERE QEZOBJNAM LIKE RTRIM('''+filnavn_pattern+''') +
                AND QEZOBJTYPE=''*STMF'' +
                AND  QEZCRTTIM < CURRENT_TIMESTAMP - '+ antall_dager +' DAYS +
             ';
         else;
           sqlCommand = ' +
             SELECT COUNT(*) FROM '+dirlist_tabell +'  +
               WHERE QEZOBJNAM = RTRIM('''+filnavn_pattern+''') +
                AND QEZOBJTYPE=''*STMF'' +
                AND  QEZCRTTIM < CURRENT_TIMESTAMP - '+ antall_dager +' DAYS +
             ';
         endif;

         exec SQL
           PREPARE SQL_STMT_FC FROM :sqlCommand;
         if sqlcod <> 0;
           return antall;
         endif;

         exec SQL
           DECLARE STMT_FC CURSOR FOR SQL_STMT_FC;

         exec SQL
           OPEN STMT_FC;
         if sqlcod <> 0;
           return antall;
         endif;

         exec SQL
           fetch STMT_FC into :antall;

         exec SQL
           CLOSE STMT_FC;

         return antall;
       end-proc;

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finn alle Filgrupper
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       dcl-proc FinnFilGr;
         dcl-pi *N packed(1:0);
         end-pi;

         dcl-s status  packed(1:0);

         status = 0;

         exec SQL
           call ADB.SP_ALLE_AKTIVE_FILGRP;

         Exec SQL
           ASSOCIATE RESULT SET LOCATORS
           (:RS_Filgrupper)
           WITH PROCEDURE ADB.SP_ALLE_AKTIVE_FILGRP;

         Exec SQL
           ALLOCATE Filgrupper CURSOR
           FOR RESULT SET :RS_Filgrupper;

         Exec SQL
           FETCH Filgrupper FOR 500 ROWS INTO :dsFilgrupper;

         Exec SQL
           close Filgrupper;

         status = 1;

         return status;
       end-proc;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Jobblogg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      /if defined(LoggingIsOn)
       dcl-proc JobLog;

         dcl-pi JobLog;
           inMessage  varchar(132)    const;
         end-pi JobLog;

         dcl-pr  Qp0zLprintf   int(10)  extproc('Qp0zLprintf');
           inMessage  pointer  options(*string: *nopass) value;
         end-pr  Qp0zLprintf;

         dcl-c  EOL           const(x'25');

         Qp0zLprintf (%trimr(inMessage) + EOL);

       end-proc JobLog;
      /endif
      *
      /end-free
      *
