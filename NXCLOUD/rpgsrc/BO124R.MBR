     h datedit(*dmy)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : BO124R
      * Beskrivelse . . : Utskrift av kontroll/avstem. totaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.30  * R5.30 131004 HFL  I bonghode og linje er bongnr. 12A.
pdf   * R6.10 071010 BSA  Innført PDF utskrift. Innebærer primært å finne
pdf   *                   generert spool, og kall av AP620R med dette som
pdf   *                   parametre.
6.20  * R6.20 031212 BSA  Utvider parameterliste for å berike arkivtagger
6.21  * R6.20 041013 BSA  Endret logikk rundt refn
6.31  * R6.30 091214 ASK  Lagt inn spesifikasjon av kortbetaling pr. kort
6.32  * R6.30 161115 bsa  Lagt ut mer info på kvitteringsliste og arkiv.
6.33  * R6.20 251115 BSA  Fjernet '-' i type dokument, da dette skaper trøbbel
6.33  *                   for arkivklienten.
6.34  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.35  * R6.30 220916 bof  Justert på ledetekst i printfilen
6.nu  * 6.30  171019 bkv  Nummerserie rettelse ldlavs
7.01  * 7.00  190320 bsa  Ligger her pga utvidelser i BOKOST
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
     fbpsola    if   e           k disk    rename(bpsopfr:bpsolar)
     fbpsolb    if   e           k disk    rename(bpsopfr:bpsolbr)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
6.31 fbobli1    uf a e           k disk    rename(boblst:bobli1r)
6.31 fbokoi1    if   e           k disk    rename(bokost:bokoi1r)
     fbo124p    o    e             printer
pdf  f                                     usropn
6.31  *
6.31  * Definering av arrays
6.31  *
6.31 d a_ktyp          s              3    dim(100)
6.31 d a_ksal          s              9  2 dim(100)
      *
      * Definering av datastrukturer
      *
     d                 ds
     d ldprec                       256
     d  ldotal                        4    overlay(ldprec)
     d  ldot01                        2    overlay(ldprec:5)
     d  ldot02                        2    overlay(ldprec:7)
     d  ldot03                        2    overlay(ldprec:9)
     d  ldot04                        2    overlay(ldprec:11)
     d  ldot05                        2    overlay(ldprec:13)
     d  ldot06                        2    overlay(ldprec:15)
     d  ldot07                        2    overlay(ldprec:17)
     d  ldot08                        2    overlay(ldprec:19)
     d  ldot09                        2    overlay(ldprec:21)
     d  ldot10                        2    overlay(ldprec:23)
     d  ldwsgr                        6    overlay(ldprec:25)
     d  ldwsal                       10    overlay(ldprec:31)
     d  ldws01                       10    overlay(ldprec:41)
     d  ldws02                       10    overlay(ldprec:51)
     d  ldws03                       10    overlay(ldprec:61)
     d  ldws04                       10    overlay(ldprec:71)
     d  ldws05                       10    overlay(ldprec:81)
     d  ldws06                       10    overlay(ldprec:91)
     d  ldws07                       10    overlay(ldprec:101)
     d  ldws08                       10    overlay(ldprec:111)
     d  ldws09                       10    overlay(ldprec:121)
     d  ldws10                       10    overlay(ldprec:131)
     d  ldavst                        1  0 overlay(ldprec:142)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:143)
     d  ldperi                        6  0 overlay(ldprec:153)
     d  lddekn                        1  0 overlay(ldprec:159)
     d  ldvdag                        2  0 overlay(ldprec:160)
     d  lddeta                        1  0 overlay(ldprec:162)
     d  ldtota                        1  0 overlay(ldprec:163)
     d  ldfrad                       10d   datfmt(*iso)
     d                                     overlay(ldprec:164)
     d  ldtild                       10d   datfmt(*iso)
     d                                     overlay(ldprec:174)
     d  ldaarr                        4  0 overlay(ldprec:184)
6.nu d  ldlavs                        8  0 overlay(ldprec:188)
6.nu d  ldldet                        6  0 overlay(ldprec:196)
6.nu d  ldtime                        6  0 overlay(ldprec:202)
6.nu d  ldkred                        1  0 overlay(ldprec:208)
      *
      * Definering av local data area
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_utqm                585    594
pdf  d l_bach                595    635
pdf  d l_arch                636    636  0
pdf  d l_skje                637    637  0
pdf  d l_land                638    638  0
pdf  d l_exlp                639    639  0
pdf  d l_mail                640    640  0
     d l_ruti                800    809
pdf  D l_outq                810    819
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_parm          s            256
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(bofirm)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_wsid     s                   like(bowsid)
5.30 d bohel1_numm     s                   like(bobong)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_wsid     s                   like(bdwsid)
5.30 d bodtl1_bong     s                   like(bdbong)
     d votyl1_otyp     s                   like(vaotyp)
     d bpsolb_oaar     s                   like(bsoaar)
     d bpsolb_oakk     s                   like(bsoakk)
     d bpsolb_ooty     s                   like(bsooty)
     d bpsolb_owsd     s                   like(bsowsd)
     d bpsolb_onum     s                   like(bsonum)
     d bpsolb_olin     s                   like(bsolin)
     d bpsola_oaar     s                   like(bsoaar)
     d bpsola_owsd     s                   like(bsowsd)
     d bpsola_oakk     s                   like(bsoakk)
     d bpsola_ooty     s                   like(bsooty)
     d bpsola_onum     s                   like(bsonum)
     d bpsola_olin     s                   like(bsolin)
6.31 d bobli1_kass     s                   like(bjkass)
6.31 d bobli1_aarr     s                   like(bjaarr)
6.31 d bobli1_bong     s                   like(bjbong)
6.31 d bokoi1_ktyp     s                   like(byktyp)
     d aforl1_ruti     s                   like(afruti)
      *
      * Definering av variable
      *
     d sawsid          s                   like(bowsid)
     d wltell          s              3  0
     d wltest          s              3  0
6.31 d w_nest          s              3  0 inz(1)
6.31 d w_indx          s              3  0
     d wsonum          s                   like(bsonum)
     d wtestb          s              1  0
     d w_gkor          s                   like(bobknt)
pdf  d p_btyp          s            100
6.21 d w_year          s              4  0
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.20 d p_tagg0         s             20
6.20 d p_tagg0val      s             50
6.20 d p_tagg1         s             20
6.20 d p_tagg1val      s             50
6.20 d p_tagg2         s             20
6.20 d p_tagg2val      s             50
6.20 d p_tagg3         s             20
6.20 d p_tagg3val      s             50
6.20 d p_tagg4         s             20
6.20 d p_tagg4val      s             50
6.20 d p_tagg5         s             20
6.20 d p_tagg5val      s             50
6.20 d p_tagg6         s             20
6.20 d p_tagg6val      s             50
6.20 d p_tagg7         s             20
6.20 d p_tagg7val      s             50
6.20 d p_tagg8         s             20
6.20 d p_tagg8val      s             50
6.20 d p_tagg9         s             20
6.20 d p_tagg9val      s             50
6.20 d p_refn          s             50
6.20 d p_dspl          s            100
      *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Nullstilling av verdier m.m.  : Utskrift pr skjerm/kasse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Nullstiller totaler for utskrift
      *
     c                   eval      d1tots = *zero
     c                   eval      d1totg = *zero
     c                   eval      d1totb = *zero
     c                   eval      d1otyp = *blank
      *
     c                   eval      wltell = *zero
     c                   eval      wwtots = *zero
     c                   eval      wwtotg = *zero
     c                   eval      wwtotb = *zero
     c                   eval      wwtotk = *zero
      *
     c                   eval      bpsola_oaar = *zero
     c                   eval      bpsola_owsd = *blank
5.30 c                   eval      bpsola_onum = *blank
     c     bpsola_key    setll     bpsolar
      *
      * Skriver heading for første liste
      *
     c                   exsr      skhead
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Lesing av records             : Utskrift pr skjerm/kasse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     astart        tag
      *
      * Leser en record fra PLUKK-S-LINJE-REGISTTER, Skjerm/Kasse
      *
     c                   read      bpsolar                                90
     c     *in90         cabeq     *on           atotal
      *
      * Sjekk,  Er dette en headinglinje, hvis ikke hent neste
      *
     c     bsorct        cabne     'H'           astart
      *
      * Hent opplysninger fra BONG-HODE-REGISTER
      *
     c                   eval      w_firm = bsofir
     c                   eval      bohel1_aarr = bsoaar
     c                   eval      bohel1_wsid = bsowsd
     c                   eval      bohel1_numm = bsonum
     c     behel1_key    chain     bohel1r                            92
     c     *in92         cabeq     *on           atotal
      *
      * Sjekk,  Er det allerede kjørt Heading-Avstemmingsliste på denne
      *
     c     bokavs        cabeq     1             astart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1DET Behandle detail         : Utskrift av skjerm/kasse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Tester om kredit omsetning er valgt utelatt.
      *
     c                   if        ldkred = 1 and
     c                             vaoakk < 3
     c                   goto      astart
     c                   endif
      *
      * Sjekk   Er dette en SUMMERING AV BELØP
      *
B001 c                   if        d1otyp <> *blank
B002 c                   if        d1otyp <> bootyp
 002 c                             or sawsid <> bsowsd
 002  *
 002 c                   add       1             wltell
 002 c                   write     d1det                                30
B003 c                   if        *in30 = *on
 003 c                   exsr      skhead
E003 c                   endif
 002  *
 002  * Nullstiller skrevne totaler
 002  *
 002 c                   eval      d1tots = *zero
 002 c                   eval      d1totg = *zero
 002 c                   eval      d1totb = *zero
 002 c                   eval      d1totk = *zero
 002  *
E002 c                   endif
E001 c                   endif
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r                            92
     c                   eval      d1otxt = vaotxt
      *
      * Snur fortegn på beløp dersom negativ ordre-type
      *
B001 c                   if        vaokre = '-'
 001 c                   mult      -1            botots
 001 c                   mult      -1            bototb
E001 c                   endif
      *
      * Summering av beløp, håndterer kontant uttak
      *
     c                   eval      d1tots = d1tots + botots
     c                   eval      wwtots = wwtots + botots
      *
B001 c                   if        vaoakk >= 4
     c                   exsr      gkort
     c                   eval      d1totg = d1totg + w_gkor
     c                   eval      d1totb = d1totb + bobkrt
     c                   eval      d1totk = d1totk + bobknt
     c                   eval      wwtotg = wwtotg + w_gkor
     c                   eval      wwtotb = wwtotb + bobkrt
     c                   eval      wwtotk = wwtotk + bobknt
E001 c                   endif
      *
     c                   eval      d1otyp = bootyp
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1DET Behandle detail D1      : Utskrift av skjerm/kasse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Sjekk   Er dette en ny arbeidstatsjon  (hvis Ja skriv tekst)
      *
B001 c                   if        sawsid <> bsowsd
 001  *
 001 c                   add       2             wltell
 001 c                   write     b1det                                30
B002 c                   if        *in30 = *on
 002 c                   exsr      skhead
E002 c                   endif
 001  *
 001 c                   eval      sawsid = bowsid
E001 c                   endif
      *
     c                   goto      astart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * T1TOT Behandle total          : Utskrift av skjerm/kasse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     atotal        tag
      *
      * Skriver ut totaler for siste ordre-type
      *
     c                   add       1             wltell
     c                   write     d1det                                30
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     c     l_alin        sub       20            wltest
      *
      * Trekker ut antall
      * linjer skrevet
      *
     c                   sub       wltell        wltest
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     c                   sub       15            wltest
      *
      * Skrive ny heading ?
      *
B001 c                   if        wltest < 0
 001 c                   exsr      skhead
E001 c                   endif
      *
      * Skrive totalsum
      *
     c                   write     t1tot                                30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppstart av utskrift, Legger inn verdier i Start f.o.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Nullstiller skrevne totaler
      *
     c                   eval      d1tots = *zero
     c                   eval      d1totg = *zero
     c                   eval      d1totb = *zero
     c                   eval      d1totk = *zero
     c                   eval      d1otyp = *blank
      *
     c                   eval      wltell = *zero
     c                   eval      wwtots = *zero
     c                   eval      wwtotg = *zero
     c                   eval      wwtotb = *zero
     c                   eval      wwtotk = *zero
      *
     c                   eval      bpsolb_oaar = *zero
     c                   eval      bpsolb_oakk = *zero
     c                   eval      bpsolb_ooty = *blank
     c                   eval      bpsolb_owsd = *blank
5.30 c                   eval      bpsolb_onum = *blank
     c                   eval      bpsolb_olin = *zero
     c     bpsolb_key    setll     bpsolbr
      *
      * Skriver heading for andre liste
      *
     c                   exsr      skhead
      *
     c                   write     aspace
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Lesing av records og utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bstart        tag
      *
      * Leser en record fra PLUKK-SB-LINJE-REGISTER
      *
     c                   read      bpsolbr                                90
     c     *in90         cabeq     *on           btotal
      *
      * Sjekk,  Er dette en headinglinje, hvis ikke hent neste
      *
     c     bsorct        cabne     'H'           bstart
      *
      * Hent opplysninger fra BONG-HODE-REGISTER
      *
     c                   eval      w_firm = bsofir
     c                   eval      bohel1_aarr = bsoaar
     c                   eval      bohel1_wsid = bsowsd
     c                   eval      bohel1_numm = bsonum
     c     behel1_key    chain     bohel1r                            92
     c     *in92         cabeq     *on           btotal
      *
      * Sjekk,  Er det allerede kjørt Heading-Avstemmingsliste på denne
      *
     c     bokavs        cabeq     1             bstart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1DET Behandle detail         : Utskrift pr ordretype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Tester om kredit omsetning er valgt utelatt.
      *
     c                   if        ldkred = 1 and
     c                             vaoakk < 3
     c                   goto      bstart
     c                   endif
6.31  *
6.31  * Summer opp kortbetalinger fra denne bongen
6.31  *
6.31 c                   exsr      sum_kort
      *
      * Sjekk   Er dette en SUMMERING AV BELØP
      *
B001 c                   if        d1otyp <> *blank
B002 c                   if        d1otyp <> bootyp
 002  *
 002 c                   add       1             wltell
 002 c                   write     d1det                                30
B003 c                   if        *in30 = *on
 003 c                   exsr      skhead
E003 c                   endif
 002  *
 002  * Nullstiller skrevne totaler
 002  *
 002 c                   eval      d1tots = *zero
 002 c                   eval      d1totg = *zero
 002 c                   eval      d1totb = *zero
 002 c                   eval      d1totk = *zero
 002  *
E002 c                   endif
E001 c                   endif
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r                            92
     c                   eval      d1otxt = vaotxt
      *
      * Snur fortegn på beløp dersom negativ ordre-type
      *
B001 c                   if        vaokre = '-'
 001 c                   mult      -1            botots
 001 c                   mult      -1            bototb
E001 c                   endif
      *
      * Summering av beløp, håndterer kontant uttak
      *
     c                   eval      d1tots = d1tots + botots
     c                   eval      wwtots = wwtots + botots
      *
B001 c                   if        vaoakk >= 4
     c                   exsr      gkort
     c                   eval      d1totg = d1totg + w_gkor
     c                   eval      d1totb = d1totb + bobkrt
     c                   eval      d1totk = d1totk + bobknt
     c                   eval      wwtotg = wwtotg + w_gkor
     c                   eval      wwtotb = wwtotb + bobkrt
     c                   eval      wwtotk = wwtotk + bobknt
E001 c                   endif
      *
     c                   eval      d1otyp = bootyp
      *
     c                   goto      bstart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * T1TOT Behandle total T1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     btotal        tag
      *
      * Skriver ut totaler for siste ordre-type
      *
     c                   add       1             wltell
     c                   write     d1det                                30
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     c     l_alin        sub       20            wltest
      *
      * Trekker ut antall
      * linjer skrevet
      *
     c                   sub       wltell        wltest
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     c                   sub       15            wltest
      *
      * Skrive ny heading ?
      *
B001 c                   if        wltest < 0
 001 c                   exsr      skhead
E001 c                   endif
      *
      * Skrive totalsum m/kortspesifikasjon
      *
     c                   write     t1tot                                30
6.31  *
6.31 c                   write     t1khe                                30
6.31 c                   eval      w_indx = 1
6.31  *
6.31 c                   dow       w_indx < w_nest
6.31 c                   eval      wwkkod = a_ktyp(w_indx)
6.31  *
6.31 c                   eval      bokoi1_ktyp = a_ktyp(w_indx)
6.31 c     bokoi1_key    chain     bokoi1
6.31 c                   if        %found
6.31 c                   eval      wwktek = bykttx
6.31 c                   else
6.31 c                   eval      wwktek = 'Ukjent korttype'
6.31 c                   endif
6.31  *
6.31 c                   eval      wwkbel = a_ksal(w_indx)
6.31 c                   write     t1kli                                30
6.31 c                   eval      w_indx = w_indx + 1
6.31 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppstart av utskrift, Legger inn verdier i Start f.o.m.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hopper ut dersom varelinjer ikke er med
      *
     c     lddeta        cabeq     0             xslutt
      *
      * Nullstiller skrevne totaler
      *
     c                   eval      wtestb = 1
      *
     c                   eval      d2antb = *zero
     c                   eval      d2antl = *zero
     c                   eval      d2otyp = *blank
      *
     c                   eval      bpsolb_oaar = *zero
     c                   eval      bpsolb_oakk = *zero
     c                   eval      bpsolb_ooty = *blank
     c                   eval      bpsolb_owsd = *blank
5.30 c                   eval      bpsolb_onum = *blank
     c                   eval      bpsolb_olin = *zero
     c     bpsolb_key    setll     bpsolbr
      *
      * Skriver heading for andre liste
      *
     c                   exsr      skhead
      *
     c                   write     aspace
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Lesing av records og utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     cstart        tag
      *
      * Leser en record fra PLUKK-SB-LINJE-REGISTER
      *
     c                   read      bpsolbr                                90
     c     *in90         cabeq     *on           ctotal
      *
      * Sjekk,  Er dette en headinglinje, hvis JA hent neste
      *
     c     bsorct        cabeq     'H'           cstart
      *
      * Hent opplysninger fra BONG-HODE-REGISTER
      *
     c                   eval      w_firm = bsofir
     c                   eval      bohel1_aarr = bsoaar
     c                   eval      bohel1_wsid = bsowsd
     c                   eval      bohel1_numm = bsonum
     c     behel1_key    chain     bohel1r                            92
     c     *in92         cabeq     *on           ctotal
      *
      * Sjekk,  Er det allerede kjørt Heading-Avstemmingsliste på denne
      *
     c     bokavs        cabeq     1             cstart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D2DET Behandle detail         : Utskrift pr ordretype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Tester om kredit omsetning er valgt utelatt.
      *
     c                   if        ldkred = 1 and
     c                             vaoakk < 3
     c                   goto      cstart
     c                   endif
      *
      * Sjekk   Er dette en SUMMERING AV ANTALL
      *
B001 c                   if        d2otyp <> *blank
B002 c                   if        d2otyp <> bootyp
 002  *
 002 c                   add       1             wltell
 002 c                   write     d2det                                30
B003 c                   if        *in30 = *on
 003 c                   exsr      skhead
E003 c                   endif
 002  *
 002  * Nullstiller skrevne totaler
 002  *
 002 c                   eval      d2antb = *zero
 002 c                   eval      d2antl = *zero
 002  *
E002 c                   endif
E001 c                   endif
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r                            92
     c                   eval      d2otxt = vaotxt
      *
      * Snur fortegn på beløp dersom negativ ordre-type
      *
     c                   add       1             d2antl
      *
B001 c                   if        wsonum <> bsonum
 001 c                   add       1             d2antb
 001 c                   eval      wsonum = bsonum
E001 c                   endif
      *
     c                   eval      d2otyp = bootyp
      *
     c                   goto      cstart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C1TOT Behandle total
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ctotal        tag
      *
      * Skriver ut totaler for siste ordre-type
      *
     c                   write     d2det                                30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
pdf   * Generer xml for videre utskrift
pdf  c                   if        l_poff = '1'
pdf  c                   exsr      spoolnbr
6.34 c                   close     bo124p
6.34 c                   exsr      xml_create
pdf  c                   exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  c                   call      'AB710R'
pdf  c                   parm                    l_bach
pdf  c                   endif
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * SKHEAD    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skhead        begsr
      *
      * Sjekker om omsetning er eks. eller inkl. kreditsalg
      *
     c                   if        ldkred = 1
     c                   eval      *in42 = *on
     c                   else
     c                   eval      *in42 = *off
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr1                               30
      *
B001 c                   if        wtestb = *zero
 001 c                   write     a1hdr2                               30
X001 c                   else
 001 c                   write     a2hdr2                               30
E001 c                   endif
      *
      * Nullstiller hjelpeteller for linjer
      *
     c                   z-add     0             wltell
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * GKORT     Summerer evt. betalinger med gavekort på bong
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     gkort         begsr
      *
      * Leser bong linjer og leter etter gavekortbetalinger
      *
     c                   eval      w_gkor = 0
     c                   eval      bodtl1_aarr = boaarr
     c                   eval      bodtl1_wsid = bowsid
     c                   eval      bodtl1_bong = bobong
     c     bodtl1_key    setll     bodtl1
     c     bodtl1_key    reade     bodtl1
      *
     c                   dow       not %eof
     c                   if        bdvare = baagva and
     c                             bdanta < 0
     c                   eval      w_gkor = w_gkor + (bdsums * -1)
     c                   endif
      *
     c     bodtl1_key    reade     bodtl1
     c                   enddo
      *
     c                   endsr
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *    Summerer opp kortbetalinger pr. korttype
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sum_kort      begsr
6.31  *
6.31 c                   eval      bobli1_aarr = bsoaar
6.31 c                   eval      bobli1_kass = bsowsd
6.31 c                   eval      bobli1_bong = bsonum
6.31 c     bobli1_key    setll     bobli1
6.31  *
6.31 c     bobli1_key    reade     bobli1
6.31 c                   dow       not %eof
6.31 c                   eval      w_indx = 1
6.31  *
6.31 c                   dow       w_indx < w_nest
6.31 c                   if        a_ktyp(w_indx) = bjtype
6.31 c                   eval      a_ksal(w_indx) =
6.31 c                             a_ksal(w_indx) + bjsumb
6.31 c                   goto      les_kort
6.31 c                   endif
6.31 c                   eval      w_indx = w_indx + 1
6.31 c                   enddo
6.31  *
6.31 c                   eval      a_ktyp(w_nest) = bjtype
6.31 c                   eval      a_ksal(w_nest) = bjsumb
6.31 c                   eval      w_nest = w_nest + 1
6.31  *
6.31 c     les_kort      tag
6.31 c     bobli1_key    reade     bobli1
6.31 c                   enddo
6.31  *
6.31 c                   endsr
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.33 c**                 eval      p_btyp = 'DAGSOPPGJØR-TOTALER'
6.33 c                   eval      p_btyp = 'DAGSOPPGJØR TOTALER'
6.20 c                   eval      p_dspl = 'Avstemmingsliste - '+%char(a1list)+
6.32 c                                      ' Periode - ' + %char(a1peri) +
6.32 c                                      '  Gruppe ' + %trim(a1wsgr)
6.21 c**                 eval      p_refn = 'BO124-'+%char(a1list)
6.21 c                   eval      p_refn = %char(w_year)+%char(a1list)
6.20 c                   eval      p_tagg0 = 'NUMMER'
6.20 c                   eval      p_tagg0val = %char(a1list)
6.20 c                   eval      p_tagg1 = 'PERIODE'
6.20 c                   eval      p_tagg1val = %char(a1peri)
6.20 c                   eval      p_tagg2 = 'TOTALSUM'
6.20 c                   eval      p_tagg2val = %char(wwtotk)
6.20 c                   eval      p_tagg3 = *blanks
6.20 c                   eval      p_tagg3val = *blanks
6.20 c                   eval      p_tagg4 = *blanks
6.20 c                   eval      p_tagg4val = *blanks
6.20 c                   eval      p_tagg5 = *blanks
6.20 c                   eval      p_tagg5val = *blanks
6.20 c                   eval      p_tagg6 = *blanks
6.20 c                   eval      p_tagg6val = *blanks
6.20 c                   eval      p_tagg7 = *blanks
6.20 c                   eval      p_tagg7val = *blanks
6.20 c                   eval      p_tagg8 = *blanks
6.20 c                   eval      p_tagg8val = *blanks
6.20 c                   eval      p_tagg9 = *blanks
6.20 c                   eval      p_tagg9val = *blanks
pdf   *
6.20 c**                 call      'AP620R'
6.20 c                   call      'AP627R'
pdf  c                   PARM                    splfname
pdf  c                   PARM                    jobname
pdf  c                   PARM                    username
pdf  c                   PARM                    jobnbr
pdf  c                   PARM                    splfnbr
pdf  c                   PARM                    splfname2
pdf  c                   PARM                    jobname2
pdf  c                   PARM                    username2
pdf  c                   PARM                    jobnbr2
pdf  c                   PARM                    splfnbr2
pdf  c                   PARM                    splfname3
pdf  c                   PARM                    jobname3
pdf  c                   PARM                    username3
pdf  c                   PARM                    jobnbr3
pdf  c                   PARM                    splfnbr3
6.20 c                   parm                    p_tagg0
6.20 c                   parm                    p_tagg0val
6.20 c                   parm                    p_tagg1
6.20 c                   parm                    p_tagg1val
6.20 c                   parm                    p_tagg2
6.20 c                   parm                    p_tagg2val
6.20 c                   parm                    p_tagg3
6.20 c                   parm                    p_tagg3val
6.20 c                   parm                    p_tagg4
6.20 c                   parm                    p_tagg4val
6.20 c                   parm                    p_tagg5
6.20 c                   parm                    p_tagg5val
6.20 c                   parm                    p_tagg6
6.20 c                   parm                    p_tagg6val
6.20 c                   parm                    p_tagg7
6.20 c                   parm                    p_tagg7val
6.20 c                   parm                    p_tagg8
6.20 c                   parm                    p_tagg8val
6.20 c                   parm                    p_tagg9
6.20 c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   parm                    p_btyp
6.20 c                   parm                    p_refn
6.20 c                   parm                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wltell = *zero
     c                   eval      wltest = *zero
     c                   eval      wtestb = *zero
      *
      * Key for file : RUTINE-REGISTER
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : BONG-HODE-REGISTER
      *
     c     behel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohel1_aarr
     c                   kfld                    bohel1_wsid
     c                   kfld                    bohel1_numm
      *
      * Key for file : BONG-LINJE-REGISTER
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_bong
      *
      * Key for file : BUTIKK-PLUKK-S-LINJE-REGISTER
      *
     c     bpsola_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bpsola_oaar
     c                   kfld                    bpsola_owsd
     c                   kfld                    bpsola_oakk
     c                   kfld                    bpsola_ooty
     c                   kfld                    bpsola_onum
     c                   kfld                    bpsola_olin
      *
      * Key for file : BUTIKK-PLUKK-SB-LINJE-REGISTER
      *
     c     bpsolb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bpsolb_oaar
     c                   kfld                    bpsolb_oakk
     c                   kfld                    bpsolb_ooty
     c                   kfld                    bpsolb_owsd
     c                   kfld                    bpsolb_onum
     c                   kfld                    bpsolb_olin
6.31  *
6.31  * Key for les av Betalingstranser
6.31  *
6.31 c     bobli1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    bobli1_kass
6.31 c                   kfld                    bobli1_aarr
6.31 c                   kfld                    bobli1_bong
6.31  *
6.31  * Key for les av korttype
6.31  *
6.31 c     bokoi1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    bokoi1_ktyp
      *
      * Key for file : BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      a1firm = l_firm
      *
      * Behandling av parameter-felter
      *
     c                   eval      ldprec = p_parm
      *
      * Avstemmingsliste/Kontroll-liste   41=ON=Avstemmingsliste
      *
     c                   move      *off          *in41
B001 c                   if        ldavst = 1
 001 c                   move      *on           *in41
E001 c                   endif
      *
      * Legger inn opplysninger i utfelter
      *
     c                   eval      a1wsid = l_wsid
     c                   eval      a1user = l_user
     c                   eval      a1fnav = l_fnav
      *
     c                   eval      a1date = udate
6.21 c                   eval      w_year = *year
     c                   time                    a1time
      *
     c                   eval      a1peri = ldperi
     c                   eval      a1list = ldlavs
6.32 c                   eval      a1wsgr = ldwsgr
      *
      * Hent opplysninger fra BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    chain     bstsl1r                            90
      *
      * Hent opplysninger fra RUTINE-REGISTER
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r                            90
B001 c                   if        *in90 = *off
 001 c                   eval      t1mld1 = afmld1
E001 c                   endif
      *
pdf  c                   open      bo124p
      *
     c                   endsr
