     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AF732R
      * Beskrivelse . . : Send xml epost uten filgruppe
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120718 BKV  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     d p_rec1          s             50
     d p_send          s             50
     d p_subj          s            250
     d p_txt1          s            100
      *
     d p_lr            s              1
     d p_str_in        s            512
     d p_str_out       s            512
     d p_filg          s             10
     d p_filgin        s              3
     d p_xmlf          s            256
      *
     d w_subj          s            512
     d w_txt1          s            512
     d wptist          s               z
     d w_send          s             50
      *
      * Definering av variabler i nøkler
      *
      *
     d w_path          s             50
     d w_xmlp          s             50
     d w_jkey          s             41
     d w_tims          s               z
     d w_mtxt          s           1024
     d w_dtaq          s              1
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_user                911    920
      *
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a
     d Out_path        s            256a
      *
     d crlf            c                   const(X'0d25')
      *
     d                 ds
     d w_dato                        26
     d  w_date                       10    overlay(w_dato:1)
     d  w_ski1                        1    overlay(w_dato:11)
     d  w_time                        2    overlay(w_dato:12)
     d  w_ski2                        1    overlay(w_dato:14)
     d  w_minu                        2    overlay(w_dato:15)
     d  w_ski3                        1    overlay(w_dato:17)
     d  w_seku                        9    overlay(w_dato:18)
      *
      *
      /copy prototypeb
      /copy usec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis vi ikke har path til xml eller IFS avslutter vi uten videre
     C/Exec SQL
     C+  Set Option DatFmt=*ISO
     C/End-Exec
     c                   if        XML_path = *blanks or
     c                             OUT_path = *blanks
     c                   goto      avslutt
     c                   endif
      * Hent inn mal
     c                   exsr      xml_env
      *
      * Scann av strenger
      *
     c                   eval      w_subj = *blanks
     c                   eval      w_txt1 = *blanks
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_subj        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_subj
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_txt1
      *
     c                   eval      w_mtxt = w_txt1
      *
      * Skriv mail
      *
      /Free
           // Skriv mailkommand
               WrtSection('MailCommandStart');
               if %Trim(p_rec1)<> *blanks;
               UpdHTMLVar('Receivers':     %trim(p_rec1));
               WrtSection('MailReceiver');
               endif;

               UpdHTMLVar('CC':                     ' ');
               UpdHTMLVar('BCC':                 w_send);
               UpdHTMLVar('Sender':              w_send);
               UpdHTMLVar('Subject':             w_subj);
               UpdHTMLVar('Message':             w_mtxt);
               UpdHTMLVar('SMTPServer':             ' ');
               UPDHTMLVar('SMTPUser':               ' ');
               UPDHTMLVar('SMTPPw':                 ' ');
               WrtSection('MailCommandEnd');

      /End-free
      *
      * Skriv xml til IFS
      *
     c                   exsr      xml_end
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av mal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_env       begsr
      *
      * Hent inn xml templaten
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
     c                   callp     ClrHtmlBuffer
      *
      /Free
           if (l_user = *blanks);
             l_user = 'ASPASP';
           endif;
           // Finne nytt batch nummer
               UpdHTMLVar('BatchNo':                  w_jkey);
               UpdHTMLVar('Batchtype':               'MAIL');
               UpdHTMLVar('Username':                l_user);
               UpdHTMLVar('Batchtime':                w_dato);
               WrtSection('Topp');

           // Finne credentials
               UpdHTMLVar('Schema':             'ADB'+p_filgin);
               UpdHTMLVar('Companyno':         %char(0));
               WrtSection('Credential');
      /End-free
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_end       begsr
      *
      /Free
           // Skriv siste tagg
               WrtSection('Footer');

      /End-free
      *
      * Lag navn til xml fila
      *
     c                   eval      w_path = %trim(Out_path)
     c                   eval      XML_Output = %trim(w_path)+p_filgin+'/'+
     c                             'Mail_' + %trim(w_jkey) + '.xml'
      *
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
      * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
     c                   if        w_dtaq = '1'
     c                   eval      p_xmlf = xml_output
     c                   eval      p_filg = 'ADB'+p_filgin
     c                   call      'AP629C'
     c                   parm                    p_filg
     c                   parm                    p_xmlf
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_filgin
     c                   parm                    p_rec1
     c                   parm                    p_send
     c                   parm                    p_subj
     c                   parm                    p_txt1
      *
     c                   eval                    w_jkey = *blanks
     c                   time                    wptist
     c     *iso0         move      wptist        w_jkey
     c*                  eval                    ClockTicks = GetTime
     c*                  move      ClockTicks    w_jkey
     c                   eval                    w_jkey = 'DRIFT' +
     c                                                    %TRIM(w_jkey)
      *
      * oppslag på AFPS
      *
     c                   eval      w_send = *blanks
     c/exec sql
     c+                  declare sok_s cursor for
     c+                  select afudss
     c+                  from   afpspf
     c+                  where  AFUFIL = :p_filgin and
     c+                         AFUIDE = 'DFTEMAIL  '
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  open sok_s
     c/end-exec
     c/exec sql
     c+                  fetch next
     c+                  from  sok_s
     c+                  into  :w_send
     c/end-exec
     c/exec sql
     c+                  close sok_s
     c/end-exec
      *
     c                   if        (w_send = *blanks)
     c                   eval      w_send = p_send
     c                   endif
      *
     c                   eval      XML_path = *blanks
     c/exec sql
     c+                  declare sok_m cursor for
     c+                  select afudss
     c+                  from   afpspf
     c+                  where  AFUFIL = :p_filgin and
     c+                         AFUIDE = 'MAILMAL   '
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  open sok_m
     c/end-exec
     c/exec sql
     c+                  fetch next
     c+                  from  sok_m
     c+                  into  :XML_path
     c/end-exec
     c/exec sql
     c+                  close sok_m
     c/end-exec
      *
     c                   if        (XML_path = *blanks)
     c                   eval      XML_path =
     c                              '/home/asp/print/630/maler/MAIL.XML'
     c
     c                   ENDIF
      *
     c                   eval      OUT_path = *blanks
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select afudss
     c+                  from   afpspf
     c+                  where  AFUFIL = :p_filgin and
     c+                         AFUIDE = 'XMLPATH   '
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :OUT_path
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
      *
     c                   eval      w_dtaq = *blanks
     c/exec sql
     c+                  declare sok_d cursor for
     c+                  select afudss
     c+                  from   afpspf
     c+                  where  AFUFIL = :p_filgin and
     c+                         AFUIDE = 'DATAQ     '
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  open sok_d
     c/end-exec
     c/exec sql
     c+                  fetch next
     c+                  from  sok_d
     c+                  into  :w_dtaq
     c/end-exec
     c/exec sql
     c+                  close sok_d
     c/end-exec
      *
     c                   time                    w_tims
     c                   movel     w_tims        w_dato
     c                   eval      w_ski1 = 'T'
     c                   eval      w_ski2 = ':'
     c                   eval      w_ski3 = ':'
      *
     c                   endsr
