      /TITLE  ASTELE Innlesing av OCR-returer:
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL124R                                        *
      * Beskrivelse . . : Div. kontroller v/innlesing av OCR-returer.   *
      *                                                                 *
      *                   Programmet blir også brukt i andre sammen-    *
      *                   henger i ASTELE for å sende ut informasjons-  *
      *                   meldinger.                                    *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           020395  Nytt program mars-95                     RH   *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    60-69 = Fileindikatorer chain etc.                           *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgfaspf    if   e           k disk
     fgl124d    cf   e             workstn
      *
      *
      * Henter firmanavn.
      *
     d                uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_arow          s              2  0
     d w_acol          s              3  0
     d w_in03          s              1  0
     d w_kode          s              1  0
     d w_valg          s              1
      *
      /eject
      *
      * Sender angrebilde 1. gangen program kalles opp:
      *
     c                   if        w_valg = *blanks                             .....
      *                                                        .
     c                   eval      w_arow = w_arow + 2                              .
     c                   eval      w_acol = w_acol - 4                              .
     c                   exsr      xinitl                                           .
     c                   exfmt     a1win                                            .
      *                                                        .
     c                   if        *inkc = *on                                  ... .
     c                   eval      w_in03 = 1                                     . .
     c                   endif                                                  ... .
      *                                                        .
     c                   if        *inki = *on                                  ... .
     c                   eval      w_kode = 1                                     . .
     c                   endif                                                  ... .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Sender spm. om det skal leses inn flere disketter:
      *
     c                   if        w_valg = '1'                                 .....
      *                                                        .
     c                   eval      w_arow = w_arow + 2                              .
     c                   eval      w_acol = w_acol - 4                              .
     c                   exsr      xinitl                                           .
     c                   exfmt     a2win                                            .
      *                                                        .
     c                   eval      w_valg = *blanks                                 .
     c                   if        a2valg = 1                                   ... .
     c                   movel     'J'           w_valg                           . .
     c                   endif                                                  ... .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Diskett i diskettleser inneholder ikke OCR-returer:
      *
     c                   if        w_valg = '2'                                 .....
      *                                                        .
     c                   eval      w_arow = w_arow + 2                              .
     c                   eval      w_acol = w_acol - 4                              .
     c                   exsr      xinitl                                           .
     c                   exfmt     a4win                                            .
      *                                                        .
     c                   if        *inkc = *on                                  ... .
     c                   eval      w_in03 = 1                                     . .
     c                   endif                                                  ... .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Sender infobilde om at diskett leses inn:
      *
     c                   if        w_valg <> *blanks                            .....
     c                             and w_valg = '3'                                 .
      *                                                        .
     c                   eval      w_arow = w_arow + 2                              .
     c                   eval      w_acol = w_acol - 4                              .
     c                   exsr      xinitl                                           .
     c                   write     a3win                                            .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Program er allerede i bruk av en annen jobb:
      *
     c                   if        w_valg = '4'                                 .....
      *                                                        .
     c                   eval      w_arow = w_arow + 2                              .
     c                   eval      w_acol = w_acol - 6                              .
     c                   exsr      xinitl                                           .
     c                   exfmt     a5win                                            .
      *                                                        .
     c                   if        *inkc = *on                                  ... .
     c                   eval      w_in03 = 1                                     . .
     c                   endif                                                  ... .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Programmet brukes her v/CALL i fra GL105C for å teste på
      * kode for om samlefile skal benyttes.  Koden finnes på
      * file GFASPF:
      *
     c                   if        w_valg = '5'                                 .....
      *                                                        .
      * Henter opp faste opplysninger.                         .
      *                                                        .
     c     l_firm        chain     gfaspf                             60            .
      *                                                        .
     c                   if        *in60 = *off                                 ... .
     c                             and gefsam = 'N'                               . .
     c                   eval      w_kode = 1                                     . .
     c                   endif                                                  ... .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Programmet brukes her v/CALL i fra GL105C.  Det sendes
      * melding dersom firma HAR kode "N", d.v.s. firma skal
      * kjøre overføring, OG SAMTIDIG at program GL106C allerede
      * er i bruk av en annen jobb:
      *
     c                   if        w_valg = '6'                                 .....
      *                                                        .
     c                   eval      w_arow = w_arow + 2                            . .
     c                   eval      w_acol = 35                                    . .
     c                   exsr      xinitl                                           .
     c                   exfmt     a6win                                          . .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Programmet for å hente returdata kan ikke være aktiv ved
      * innlesing av OCR-disketter:
      *
     c                   if        w_valg = '7'                                 .....
      *                                                        .
     c                   eval      w_arow = w_arow + 2                            . .
     c                   eval      w_acol = w_acol - 4                            . .
     c                   exsr      xinitl                                           .
     c                   exfmt     a7win                                          . .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
      * Programmet for å lese OCR-disketter kan ikke være aktiv når
      * overføring kjøres:
      *
     c                   if        w_valg = '8'                                 .....
      *                                                        .
     c                   eval      w_arow = w_arow + 2                            . .
     c                   eval      w_acol = w_acol - 4                            . .
     c                   exsr      xinitl                                           .
     c                   exfmt     a8win                                          . .
      *                                                        .
     c                   goto      xslutt                                           .
      *                                                        .
     c                   endif                                                  .....
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiere linjeteller på nytt dersom >= 15:        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xinitl        begsr
      *
      * Parameterliste returnerer om program skal avsluttes ELLER
      * om det skal leses inn flere disketter
      *
     c                   if        w_arow >= 15                                 ...
     c                   eval      w_arow = 4                                     .
     c                   eval      w_acol = 37                                    .
     c                   endif                                                  ...
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      *
      * Parameterliste returnerer om program skal avsluttes ELLER
      * om det skal leses inn flere disketter
      *
     c     *entry        plist
     c                   parm                    w_in03
     c                   parm                    w_valg
     c                   parm                    w_arow
     c                   parm                    w_acol
     c                   parm                    w_kode
      *
     c                   eval      w_in03 = 0
     c                   eval      w_kode = 0
      *
     c                   endsr
      *
