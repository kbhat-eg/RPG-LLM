     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA112R
      * Beskrivelse . . : Landkoder, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
      * V5.30 290304 KOB  Lagt inn prefix for telefon
7.xx  * 7.00  020316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 6.30  271217 bsa  Legger inn adgangskontroll i programmet
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra12l1    if   e           k disk    rename(ra12pfr:ra12l1r)
     fra12l2    if   e           k disk    rename(ra12pfr:ra12l2r)
     fra12lr    if   e           k disk    rename(ra12pfr:ra12lrr)
     fra12lu    uf a e           k disk    rename(ra12pfr:ra12lur)
      *
     fra112d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra12l1_lkod     s                   like(ralkod)
     d ra12l2_ltxt     s                   like(raltxt)
     d ra12lr_lkod     s                   like(ralkod)
     d ra12lu_lkod     s                   like(ralkod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(ralfir)
7.01 d w_tilgko        s             10
7.01 d w_rutine        s             10
7.01 d w_adv           s              3
7.01 d w_status        s             10
      *
     d s_lkod          s                   like(ralkod)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
7.01 d u_s701          s              1    inz(*off)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
7.01  *
7.01  * Sjekk om denne bruker har tilgang til denne F-tast
7.01  *
7.01 c                   if        u_s701 = *on
7.01 c                   if        *in16 = *on
7.01 c                   select
7.01 c                   when      *inkf
7.01 c                   eval      w_adv    = 'F06'
7.01 c                   other
7.01 c                   goto      f_tast_ok
7.01 c                   endsl
7.01 c                   eval      w_tilgko = l_user
7.01 c                   eval      w_rutine = 'RA112R'
7.01 c                   eval      w_status = *blank
7.01 c                   call      'AD005R'
7.01 c                   parm                    w_tilgko
7.01 c                   parm                    w_rutine
7.01 c                   parm                    w_adv
7.01 c                   parm                    w_status
7.01 c                   if        w_status = 'Sperret'
7.01 c                   goto      b2tagb
7.01 c                   endif
7.01 c                   endif
7.01 c     F_tast_ok     tag
7.01 c                   endif
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lkod <> *blank or
     c                             b2ltxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra12l2_ltxt = b1ltxt
     c     ra12l2_key    setll     ra12l2
     c                   other
     c                   eval      ra12l1_lkod = b1lkod
     c     ra12l1_key    setll     ra12l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Land
      *
     c                   if        b2lkod <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      ra12l1_lkod = b2lkod
     c     ra12l1_key    setll     ra12l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2ltxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra12l2_ltxt = b2ltxt
     c     ra12l2_key    setll     ra12l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lkod = *blank
     c                   eval      b2ltxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
7.01  *
7.01  * Sjekk om denne bruker har tilgang til dette valg
7.01  *
7.01 c                   if        u_s701 = *on
7.01 c                   if        b1valg <> 0
7.01 c                   move      b1valg        w_adv
7.01 c                   eval      w_tilgko = l_user
7.01 c                   eval      w_rutine = 'RA112R'
7.01 c                   eval      w_status = *blank
7.01 c                   call      'AD005R'
7.01 c                   parm                    w_tilgko
7.01 c                   parm                    w_rutine
7.01 c                   parm                    w_adv
7.01 c                   parm                    w_status
7.01 c                   if        w_status = 'Sperret'
7.01 c                   eval      b1valg   = *zero
7.01 c                   update    b1sfl
7.01 c                   iter
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1lkod = b1lkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1lkod = b1lkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1lkod = b1lkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1lkod = b1lkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1lkod = b2lkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1lkod = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1lkod = *blank
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra12lr_lkod = c1lkod
     c     ra12lr_key    chain     ra12lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1lkod = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2lkod = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra12lr_lkod = c1lkod
     c     ra12lr_key    chain     ra12lr
      *
     c                   if        %found
     c                   eval      c2ltxt = raltxt
     c                   eval      c2lspr = ralspr
     c                   eval      c2lpfx = ralpfx
     c                   eval      c2eusr = ralusr
     c     *dmy          move      raldat        c2edat
     c                   else
     c                   eval      c2ltxt = *blank
     c                   eval      c2lspr = *zeros
     c                   eval      c2lpfx = *zeros
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1lkod = k1lkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2ltxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra12lur
      *
     c                   eval      ra12lu_lkod = c1lkod
     c     ra12lu_key    chain     ra12lur
      *
     c                   eval      raltxt = c2ltxt
     c                   eval      ralspr = c2lspr
     c                   eval      ralpfx = c2lpfx
      *
     c                   time                    raldat
     c                   time                    raltim
     c                   eval      ralusr = l_user
      *
     c                   if        %found
     c                   update    ra12lur
     c                   else
     c                   eval      ralfir = w_firm
     c                   eval      ralkod = c1lkod
     c                   write     ra12lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ltxt = raltxt
     c                   eval      b1lspr = ralspr
     c                   eval      b1lpfx = ralpfx
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra12lr_lkod = d1lkod
     c     ra12lr_key    chain     ra12lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra12lu_lkod = d1lkod
     c     ra12lu_key    chain     ra12lur
      *
     c                   delete    ra12lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_lkod = k1lkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra12lr_lkod = k1lkod
     c     ra12lr_key    chain     ra12lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1lkod = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra12lr_lkod = k1lkod
     c     ra12lr_key    chain     ra12lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1lkod = s_lkod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra12l2
     c                   other
     c                   read      ra12l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ralfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra12l2_ltxt = raltxt
     c                   other
     c                   eval      ra12l1_lkod = ralkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1lkod = ralkod
     c                   eval      b1ltxt = raltxt
     c                   eval      b1lspr = ralspr
     c                   eval      b1lpfx = ralpfx
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra12l2_key    setgt     ra12l2
     c                   readp     ra12l2
     c                   other
     c     ra12l1_key    setgt     ra12l1
     c                   readp     ra12l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra12l2
     c                   other
     c                   readp     ra12l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ralfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra12l2_ltxt = raltxt
     c                   other
     c                   eval      ra12l1_lkod = ralkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra12l2_key    setll     ra12l2
     c                   other
     c     ra12l1_key    setll     ra12l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på land
      *
     c     ra12l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra12l1_lkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra12l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra12l2_ltxt
      *
      * Key for les på land
      *
     c     ra12lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra12lr_lkod
      *
      * Key for oppdatering av land
      *
     c     ra12lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra12lu_lkod
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'OKO_TILGANGSKONTROLL':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
7.01  *
7.01  * Sjekk om denne bruker har tilgang til denne rutina
7.01  *
7.01 c                   if        u_s701 = *on
7.01 c                   eval      w_adv    = *blank
7.01 c                   eval      w_tilgko = l_user
7.01 c                   eval      w_rutine = 'RA112R'
7.01 c                   eval      w_status = *blank
7.01 c                   call      'AD005R'
7.01 c                   parm                    w_tilgko
7.01 c                   parm                    w_rutine
7.01 c                   parm                    w_adv
7.01 c                   parm                    w_status
7.01 c                   if        w_status = 'Sperret'
7.01 c                   goto      avslutt
7.01 c                   endif
7.01 c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra12l1_lkod = *blank
     c     ra12l1_key    setll     ra12l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
