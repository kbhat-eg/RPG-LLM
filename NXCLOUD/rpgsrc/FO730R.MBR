     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO730R
      * Beskrivelse . . : Endring/Oppdatering av transer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290499 AMD  Skrevet om til ny standard
      *       161203 HKO  Tar hensyn til bruksrett-rabatt
      *                   Endret parameter ved kall til VP900R
      *                   Skrevet om logikk
      *       160404 HKO  Rettet feil med at gammel rabatt-sats ble benyttet
      *                   ved rekalkulering av endret rabattkategori
      *       120804 HKO  Endret parameter ved kall til VP900R
      *                   Rekalkulerer også skaffevarer/VA-varer
      *       041104 HKO  Endret parameter ved kall til VP900R
5.60  * R5.60 091107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
5.70  * PRGR  301008 bof  Utvidet med prisgruppe
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.1a  * R6.10 070513 bof  EHF krever avrunding av FDSUMS
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
7.01  * K000  100720 bsa  Må ta høyde for returgebyr
      *
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  * K000  070722 bsa  Hvis varetype D, skal linja ikke rekalkuleres.
8.02  *                   Disse linjene er priset manuelt, og da blir
8.02  *                   prisen satt til 0.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.01 ffod2l1    if   e           k disk    rename(fod2pfr:fod2l1r)
      *
      * Definering av local data area
      *
     d                uds
6.10 d l_user                911    920
7.01 d l_filg                931    933
      *
      * Definering av parametere
      *
     d p_enor          s              1
     d p_firm          s                   like(fofirm)
6.NU d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
6.nu d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
5.70 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variable i nøkler
      *
     d fodtlu_line     s                   like(fdline)
7.01 d fod2l1_line     s                   like(fdline)
     d vltyl1_ltyp     s                   like(valtyp)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
5.60 d w_suma          s             11  2
     d w_sumk          s             11  2
     d w_totk          s             11  2
     d w_totr          s             11  2
     d w_tots          s             11  2
5.60 d w_tota          s             11  2
7.01 d w_retb          s             11  2
      *
     d w_firm          s                   like(fofirm)
6.NU d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_kpro          s                   like(fokpro)
     d w_kund          s                   like(fokund)
     d w_rkat          s                   like(forkat)
     d w_raba          s                   like(fdsums)
     d w_sumr          s                   like(fdsums)
     d w_sums          s                   like(fdsums)
5.70 d w_prgr          s                   like(fdprgr)
      *
     d s_anta          s                   like(fdanta)
     d s_kopr          s                   like(fdkopr)
     d s_kpri          s                   like(fdkpri)
     d s_pasl          s                   like(fdpasl)
     d s_rab1          s                   like(fdrab1)
     d s_rab2          s                   like(fdrab2)
     d s_sapr          s                   like(fdsapr)
     d s_kamp          s                   like(fdkamp)
     d s_alme          s                   like(fdalme)
     d s_brty          s                   like(fdbrty)
     d s_brr1          s                   like(fdbrr1)
     d s_brr2          s                   like(fdbrr2)
     d s_omva          s                   like(fdomva)
      *
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
7.01 d u_rgeb          s              1    inz(*off)                            returgebyr
8.02 d b_evar          s              1    inz(*off)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent opplysninger fra ordre-hode, og avbryter dersom hode
      * ikke finnes
      *
     c     fohel1_key    chain     fohel1r
      *
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
7.01 c                   eval      w_retb = 0
      *
      * Leser og oppdaterer ordre-linjer
      *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
      *
      * Behandler ikke tekstlinjer
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1
      *
     c                   if        valktx <> 0
     c                   goto      neste_linje
     c                   endif
      *
5.70 c                   exsr      hent_prisgr
      *
      * Hent opplysninger fra vare
      *
8.02 c                   eval      b_evar = *on
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
8.02 c                   if        not %found
8.02 c                   eval      b_evar = *off
8.02 c                   endif
      *
     c                   eval      s_sapr = fdsapr
     c                   eval      s_kopr = fdkopr
     c                   eval      s_kpri = fdkpri
     c                   eval      s_rab1 = fdrab1
     c                   eval      s_rab2 = fdrab2
     c                   eval      s_pasl = fdpasl
     c                   eval      s_kamp = fdkamp
     c                   eval      s_alme = fdalme
     c                   eval      s_brty = fdbrty
     c                   eval      s_brr1 = fdbrr1
     c                   eval      s_brr2 = fdbrr2
     c                   eval      s_omva = fdomva
      *
      * Beregning av priser, summer og totaler
      *
      * Statuskode = 0  gir beregning av nye summer og totaler
      * Statuskode = 1  gir henting av nye priser/rabatter samt beregning
      *
     c                   if        p_enor <> *blank
     c***>                         fdkpri <> 'F'
8.02  *
8.02  * Hvis varetype D og ligger i EVR, ikke hent pris på nytt
8.02  *
8.02 c                   if        b_evar = *on and
8.02 c                             vvvtyp = 'D'
8.02 c                   goto      no_pris
8.02 c                   endif
      *
      * Legger også inn koden på hver enkelt linje
      *
     c                   move      fopasl        fdpasl
      *
     c                   exsr      hent_pris
      *
     c                   if        s_kopr = 0
     c                   eval      s_kopr = s_sapr * hopros
     c                   endif
      *
8.02 c     no_pris       tag
      *
     c                   endif
      *
      * Beregner totaler for den enkelte varelinje
      *
     c                   eval      s_anta = fdanta
     c                   if        valkre = '-'
     c                   eval      s_anta = s_anta * -1
     c                   endif
      *
      * Beregning av totaler
      *
6.1a c*                  eval      w_sums = s_sapr * s_anta
6.1a c                   eval(h)   w_sums = s_sapr * s_anta
     c                   eval      w_sumr = 0
5.60 c                   eval      w_suma = 0
      *
      * Rabatt
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_raba = (w_sums * forabp) / 100
     c                   eval      w_sumr = w_raba
     c                   endif
      *
     c                   if        s_rab1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * s_rab1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        s_rab2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * s_rab2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        s_brr1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * s_brr1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
5.60 c                   eval      w_suma = w_suma + w_raba
     c                   endif
      *
     c                   if        s_brr2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * s_brr2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
5.60 c                   eval      w_suma = w_suma + w_raba
     c                   endif
      *
      * Kostpris
      *
     c                   eval      w_sumk = s_kopr * s_anta
7.01  *
7.01  * Returgebyr
7.01  *
7.01 c                   if        u_rgeb = *on
7.01 c                   eval      fod2l1_line = fdline
7.01 c     fod2l1_key    chain     fod2l1r
7.01 c                   if        %found and
7.01 c                             fd2ber = 1 and
7.01 c                             fd2sat > 0
7.01 c                   eval(h)   w_retb = (w_sums - w_sumr) * fd2sat / 100
7.01 c                   eval      w_sumr = w_sumr + w_retb
7.01 c                   endif
7.01 c                   endif
      *
      * Oppdatering av ordre-linje med nye opplysninger
      *
     c                   eval      fodtlu_line = fdline
     c     fodtlu_key    chain     fodtlur
     c                   if        %found
      *
     c                   eval      fdsapr = s_sapr
     c                   eval      fdkopr = s_kopr
     c                   eval      fdkpri = s_kpri
     c                   eval      fdrab1 = s_rab1
     c                   eval      fdrab2 = s_rab2
     c                   eval      fdpasl = s_pasl
     c                   eval      fdkamp = s_kamp
     c                   eval      fdalme = s_alme
     c                   eval      fdbrty = s_brty
     c                   eval      fdbrr1 = s_brr1
     c                   eval      fdbrr2 = s_brr2
     c                   eval      fdomva = s_omva
      *
     c                   eval      fdsums = w_sums
     c                   eval      fdsumr = w_sumr
     c                   eval      fdsumk = w_sumk
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
      *
     c                   update    fodtlur
      *
     c                   endif
      *
      * Akkumulerer totaler for oppdatering av ordre-hode-register
      *
     c                   eval      w_tots = w_tots + w_sums
     c                   eval      w_totr = w_totr + w_sumr
     c                   eval      w_totk = w_totk + w_sumk
5.60 c                   eval      w_tota = w_tota + w_suma
      *
     c     neste_linje   tag
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
      * Oppdaterer hode
      *
     c                   exsr      oppdat_hode
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer hode-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_hode   begsr
      *
      * Beregner justeringsbeløp
      *
     c                   eval      w_sald = fototr - fotots + w_tots -
     c                                      w_totr
5.60 c                   eval      w_sal2 = w_tota - fobrra
      *
      * Kaller opp subrutine for oppdatering av memosaldo
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    footyp
     c                   parm                    fokund
     c                   parm                    w_sald
5.60 c                   parm                    w_sal2
      *
      * Oppdater Ordre-hode
      *
     c     fohelu_key    chain     fohelur
      *
     c                   eval      fotots = w_tots
     c                   eval      fototr = w_totr
     c                   eval      fototk = w_totk
5.60 c                   eval      fobrra = w_tota
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
      *
     c                   update    fohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter opplysninger om varen fra pris/vare subrutinen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = forkat
      *
     c                   eval      hikund = fokund
     c                   if        folkun <> 0
     c                   eval      hikund = folkun
     c                   endif
      *
     c                   eval      hikpro = fokpro
     c                   eval      hivare = fdvare
     c                   eval      hienhe = fdenh1
     c                   eval      hilety = fdlety
     c                   eval      hidato = fobdat
     c                   eval      hitime = *loval
     c                   eval      hinumm = fopord
     c                   eval      hikode = fopask
     c                   eval      hidekn = fdpasl
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = fdpriv
     c                   eval      hiskaf = *off
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
      *
      * Dersom rabattkategori er overstyrt, skal kunde og kunde-
      * prosjekt null-stilles, slik at priser hentes på nytt i
      * henhold til den nye rabattkategorien.
      *
     c                   eval      w_kund = fokund
     c                   eval      w_kpro = fokpro
     c                   eval      w_rkat = 0
      *
     c                   call      'FL710R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    w_rkat
      *
     c                   if        w_rkat <> forkat
     c                   eval      hikund = 0
     c                   eval      hikpro = 0
     c                   endif
      *
      * Behandling dersom varen er en skaffevare (VA-vare)
      *
     c                   if        fdlvre = 1
     c                   eval      hiskaf = *on
     c                   eval      hildor = fdldor
     c                   eval      hisapr = fdsapr
     c                   eval      hikopr = fdkopr
     c                   endif
      *
      * Kaller prisberegningsprogram
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   eval      s_kamp = hokamp
     c                   eval      s_omva = hoomva
      *
     c                   if        hosapr > 0.01 and
     c                             fdkpri <> 'F'
     c                   eval      s_kpri = hokpri
     c                   eval      s_sapr = hosapr
     c                   eval      s_kopr = hokopr
     c                   eval      s_rab1 = horab1
     c                   eval      s_rab2 = horab2
     c                   eval      s_pasl = hodekn
     c                   eval      s_alme = hoalme
     c                   eval      s_brty = hobrty
     c                   eval      s_brr1 = hobrr1
     c                   eval      s_brr2 = hobrr2
     c                   endif
      *
     c                   endsr
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  * Henter prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL712R   '
5.70 c                   parm                    w_firm
5.70 c                   parm                    fdlage
5.70 c                   parm                    fdavde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_enor
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    fodtlu_line
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
7.01  *
7.01  * Key for oppslag på ordrelinje SIDETABELL
7.01  *
7.01 c     fod2l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    w_numm
7.01 c                   kfld                    w_suff
7.01 c                   kfld                    fod2l1_line
      *
      *
      * Henter fra prameter
      *
     c                   eval      w_firm = p_firm
6.NU c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
7.01  *
7.01  * Test om returgrebyr er av eller på
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'AKTIVER_RETURGEBYR':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_rgeb = *on;
7.01   endif;
      *
     c                   endsr
