     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV101R
      * Beskrivelse . . : Vare, vedlikehold - bilde 1
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190499 HKO  Nytt program
      *       050100 HKO  Lagt inn funksjonstast for endring av basispriser
      *       220200 HKO  Lagt inn Notat
      *       220300 HKO  Takler DG > 99,99
      *                   Tildeler neste ledige linjenummer ved oppretting
      *                   av nye enheter
      *       250300 HKO  Endret lengde på volum, nettovekt og bruttovekt
      *                   Lagt inn vedlikehold av abc-kode, høyde, lengde
      *                   og bredde
      *       130900 HKO  Sperret for spørring på statistikk- og lager-
      *                   enhet ved endring av vare
      *       190900 HKO  Sperret for at det kan legges inn salgspris på
      *                   0,01 på lager- og skaffe-vare
      *       061000 HKO  Kopierer ikke over nobb-nummer ved kopiering av
      *                   vare
      *       031100 HKO  Legger inn lagerenhet ved opprettelse av vare
      *                   i lager
      *       130102 HKO  Kaller program for oppretting og sletting av
      *                   lager.
      *       040203 HKO  Legger ut alternativ varegruppe på nye varer
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *       251103 HKO  Lagt inn vedlikehold av utgår-dato og viser
      *                   endret dato, endret av og Nobb utgår-dato
      *       191203 HKO  Lagt inn omregningsfaktor til nominell
      *       301104 HKO  Lagt inn prissammenligningsenhet
      *       020805 HKO  Fjernet linjenr fra vare-enhet
      *                   Utvidet vare-enhet med opprettet dato
      *       040805 HKO  Fjernet vedlikehold av lengde, bredde, høyde,
      *                   nettovekt, bruttovekt og volum
      *       130905 HKO  Fjernet ROLLDOWN fra DDS'en
      *       121105 HKO  Fjernet test på at salgspris må være oppgitt
      *                   på rene innkjøpsenheter
      *       041005 HKO  Endret parameter ved kall av LL765R
5.70  * PRGR  241008 bof  Utvidet med prisgruppe /varetype på lager
5.70  * PRGR  050509 bof  For brukere m.spes.tilgang skal man kunne
      *                   bruke alle prisgrupper
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  191109 bof  Utvidet med kjedesortiment
6.12  * 6.10  191109 bof  Kaller feil program for GTIN nummer
6.13  * 6.10  060110 bof  Tatt bort alle lager-relaterte felter
6.14  * 6.10  231110 bof  Hindre endre varetype fra L-->S hvis L på lager
6.20  * 6.20  310311 bof  Kall til vp505r vare-pris-lev.nr
6.21  * 6.20  310311 bof  Utvidet vare-pris med leverandør
6.22  * 6.20  011111 bof  HVis 1 lager så skal skal vedlikehold være åpent
6.22  *                   samt at hvis man endrer,så endres lagersamtidig
6.23  * 6.20  050112 bof  Sjekke at v.type kun endres fra L->S vv D-T vv.
6.30  * 6.30  080814 bof  Viser erstattet av nobbnr.
6.31  * 6.30  100917 bof  Hvis man endrer lev.nr, endr.også pris og enh.pgi
6.32  * 6.30  120918 bof  Utvidet med trelast-sortiment
      *
7.01  * 6.30  050917 sst  Merke vare dersom den skal utgå også i lager
7.02  * 7.00  271119 bkv  Skal ikke vise prisgruppe blank pris hvis pris med prisgr. Gunvald
7.03  * 7.00  170220 bof  Hindre for store beløp vises i subfile
7.04  * 7.00  280320 bkv  Lagt inn tulle subfile option for å lurer newlook
7.05  * 7.00  230421 bof  Utvidet subfilen til 36 linjer
7.06  * 6.30  270616 bsa  Lagt inn F tast for visning av VA info.
7.07  * 6.30  050917 sst  Merke vare dersom den skal utgå også i lager
      *
8.01  *       290422 bof  Utvidet subfilen til 72 linjer
8.02  *       191222 bof  Endret logikk for test på *in55
8.03  *       210223 bof  Feilretting vedr. gjenoppretting av vare og test på pris
8.04  *       110423 bof  Må godkjenne at det ikke finnes priser, men at man reg.nye
8.05  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenl5    if   e           k disk    rename(vvenpfr:vvenl5r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
7.02 fvvprlr    if   e           k disk    rename(vvprpfr:vvprlrr)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvvtyl1    if   e           k disk    rename(vvtypfr:vvtyl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fanotl1    if   e           k disk    rename(anotpfr:anotl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
5.70 fra30lr    if   e           k disk    rename(ra30pfr:ra30lrr)
6.22 fra10lr    if   e           k disk    rename(ra10pfr:ra10lrr)
     fvvarlu    uf a e           k disk    rename(vvarpfr:vvarlur)
     fvvenlu    uf a e           k disk    rename(vvenpfr:vvenlur)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
     fvvaslu    uf   e           k disk    rename(vvaspfr:vvaslur)
6.31 fvvepl3    uf   e           k disk    rename(vveppfr:vvepl3r)
6.30 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
7.01 fvlagl1    uf   e           k disk    rename(vlagpfr:vlagl1r)
      *
     fvv101d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_valg          s              1  0
     d p_varekopi      s                   like(vvvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d vvarl3_nobb     s                   like(vvnobb)
     d vvenl1_enhe     s                   like(veenhe)
6.21 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
7.02 d vvprlr_prgr     s                   like(vpprgr)
7.02 d vvprlr_ldor     s                   like(vpldor)
7.02 d vvprlr_enhe     s                   like(vpenhe)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vvtyl1_vtyp     s                   like(vavtyp)
     d venhl1_eenh     s                   like(vaeenh)
     d anotl1_syst     s                   like(ausyst)
     d anotl1_noid     s                   like(aunoid)
     d vltpl1_lety     s                   like(vylety)
     d rlevl1_levr     s                   like(rllevr)
     d vvenlu_enhe     s                   like(veenhe)
6.21 d vvprlu_ldor     s                   like(vpldor)
5.70 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
5.70 d ra30lr_dkod     s                   like(redkod)
6.22 d ra10lr_jkod     s                   like(rajkod)
6.30 d jvarl1_vare     s                   like(jvvare)
6.31 d vvepl3_pldo     s                   like(vepldo)
7.01 d vlagl1_firm     s                   like(vlfirm)
7.01 d vlagl1_vare     s                   like(vlvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_feil          s              1    inz(*off)
     d b_ny_vare       s              1    inz(*off)
     d b_pris          s              1    inz(*off)
     d b_enhs          s              1    inz(*off)
     d b_enhl          s              1    inz(*off)
     d b_enhe          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
     d b_lagr          s              1    inz(*off)
6.22 d b_flla          s              1    inz(*off)
     d b_endr_vtyp     s              1    inz(*off)
     d b_endr_pris     s              1    inz(*off)
7.02 d b_prgr          s              1    inz(*off)
      *
     d w_valg          s              1  0
     d w_syst          s              4
     d w_udat          s               d   datfmt(*iso)
     d w_pdat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
5.70 d w_prgr          s                   like(vpprgr)
5.70 d w_ko13          s              1  0
5.70 d w_kode          s              2  0
6.22 d w_late          s              3  0
6.22 d w_lage          s                   like(rajkod)
      *
     d w_sapr          s                   like(vpsapr)
     d w_kopr          s                   like(vpkopr)
     d w_inpr          s                   like(vpinpr)
     d w_bupr          s                   like(vpbupr)
     d w_omre          s                   like(veomre)
      *
     d w_tek1          s                   like(vvtek1)
     d w_kbon          s                   like(vvkbon)
     d w_kord          s                   like(vvkord)
     d w_kenh          s                   like(vvkenh)
     d w_kmva          s                   like(vvkmva)
     d w_kres          s                   like(vvkres)
     d w_kpme          s                   like(vvkpme)
     d w_keti          s                   like(vvketi)
     d w_kpdi          s                   like(vvkpdi)
     d w_kjus          s                   like(vvkjus)
     d w_desi          s                   like(vvdesi)
     d w_kstr          s                   like(vvkstr)
     d w_kabc          s                   like(vvkabc)
     d w_ahgr          s                   like(vvahgr)
     d w_augr          s                   like(vvaugr)
     d w_avar          s                   like(vvavar)
     d w_kfri          s                   like(vvkfri)
     d w_fako          s                   like(vvfako)
     d w_dekn          s                   like(vvdekn)
     d w_omrn          s                   like(vvomrn)
     d w_godj          s                   like(vvgodj)
     d w_khev          s                   like(vvkhev)
6.11 d w_sort          s                   like(vvsort)
      *
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
     d w_ttxt          s                   like(vavtxt)
     d w_etxt          s                   like(vaetxt)
     d w_ltxt          s                   like(vybesk)
7.06 d w_lety          s              1  0
7.06 d w_prig          s                   like(vpprgr)
8.02 d w_anta          s              4  0
      *
     d s_ogrp          s                   like(vvogrp)
     d s_hgrp          s                   like(vvhgrp)
     d s_ugrp          s                   like(vvugrp)
     d s_ldor          s                   like(vvldor)
     d s_enhe          s                   like(vvenhe)
     d s_vtyp          s                   like(vvvtyp)
     d s_lety          s                   like(vplety)
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
7.02 d u_702           s              1    inz(*off)
      *
      * Konstanter
      *
8.02 d c_sfil          s              2  0 inz(72)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b2tag         tag
      *
      * Initierering av skjermbildet
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      b_ny_vare = *off
      *
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c     vvasl1_key    chain     vvasl1
     c                   endif
      *
     c                   if        %found
     c                   exsr      endr_vare
     c                   else
     c                   exsr      ny_vare
     c                   endif
      *
6.30  * henter info fra VA
6.30 c                   eval      jvarl1_vare = vvvare
6.30 c     jvarl1_key    chain     jvarl1
6.30 c                   if        %found(jvarl1)
6.30 c                   eval      b2enob = jvenob
6.30 c                   else
6.30 c                   eval      b2enob = 0
6.30 c                   endif
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
     c     b2tagb        tag
      *
      * Sjekker notat
      *
     c                   eval      *in80 = *off
      *
     c                   if        w_vare <> *blank
     c                   eval      anotl1_syst = 'VVAR'
     c                   eval      anotl1_noid = w_vare
     c     anotl1_key    chain     anotl1                             91
     c                   if        *in91 = *off
     c                   eval      *in80 = *on
     c                   endif
     c                   endif
      *
      * Viser subfile
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *in10
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F13=Bilde 2
      *
     c                   if        *inkm = *on
     c                   call      'VV102R'
     c                   parm                    p_firm
     c                   parm                    w_vare
     c                   parm                    p_valg
     c                   parm                    w_tek1
     c                   parm                    w_kbon
     c                   parm                    w_kord
     c                   parm                    w_kenh
     c                   parm                    w_kmva
     c                   parm                    w_kres
     c                   parm                    w_kpme
     c                   parm                    w_keti
     c                   parm                    w_kpdi
     c                   parm                    w_kjus
     c                   parm                    w_desi
     c                   parm                    w_kstr
     c                   parm                    w_kabc
     c                   parm                    w_ahgr
     c                   parm                    w_augr
     c                   parm                    w_avar
     c                   parm                    w_kfri
     c                   parm                    w_fako
     c                   parm                    w_dekn
     c                   parm                    w_omrn
     c                   parm                    w_godj
     c                   parm                    w_khev
6.11 c                   parm                    w_sort
     c                   goto      b2tagb
     c                   endif
      *
      * Validering av input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   endif
      *
6.14  * Dersom varen endrer varetype fra lagervare, og varen har aktiv
6.14  * lagerbeholdning, eller har varetype = 'L'  får bruker melding om dette
      *
     c                   if        faalag = 1 and
     c                             b_ny_vare = *off and
     c                             s_vtyp <> b2vtyp and
     c                             s_vtyp =  'L'
     c                   eval      b_inlr = *off
6.22  * hvis et lager - så sjekkes kun dette
6.22 c                   if        b_flla = *off
6.22 c                   call      'LL766R'
6.22 c                   parm                    w_firm
6.22 c                   parm                    w_vare
6.22 c                   parm                    w_lage
6.22 c                   parm                    b_inlr
6.22 c                   parm                    b_lagr
6.22 c                   else
6.14 c                   call      'LL767R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b_inlr
     c                   parm                    b_lagr
6.22 c                   endif
     c                   if        b_lagr = *on
     c                   exsr      xb2msg
     c                   if        b_endr_vtyp = *off
     c                   eval      w_virn = 1
     c                   goto      b2tagb
     c                   endif
     c                   endif
     c                   endif
      *
      * Oppdater vare
      *
     c                   exsr      oppdater
      *
      * Sender bildet opp på nytt dersom variable som påvirker disse er
      * endret
      *
     c                   if        b2ogrp <> s_ogrp or
     c                             b2hgrp <> s_hgrp or
     c                             b2ugrp <> s_ugrp or
     c                             b2ldor <> s_ldor or
     c                             b2enhe <> s_enhe or
     c                             b_endr_pris = *on
     c                   goto      b2tag
     c                   endif
      *
      * F2=Notat
      *
     c                   if        *inkb = *on
     c                   eval      w_syst = 'VVAR'
     c                   call      'AU100R'
     c                   parm                    w_firm
     c                   parm                    w_syst
     c                   parm                    vvvare
     c                   goto      b2tag
     c                   endif
7.06  *
7.06  * F10=Vis VA info
7.06  *
7.06 c                   if        *inkj = *on
7.06 c                   exsr      vis_VA
7.06 c                   goto      b2tag
7.06 c                   endif
      *
      * F14=Enheter
      *
     c                   if        *inkn = *on
     c                   call      'VE110R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tag
     c                   endif
      *
      * F15=Priser
      *
     c                   if        *inkp = *on
6.20 c                   call      'VP505R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tag
     c                   endif
      *
      * F16=Vedlikehold av søketekster
      *
     c                   if        *inkq = *on
     c                   call      'VS100R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tag
     c                   endif
      *
      * F17=Vedlikehold av EAN-nummer
      *
     c                   if        *inkr = *on
6.12 c                   call      'VE515R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tag
     c                   endif
      *
      * F18=Tilbudspriser
      *
     c                   if        *inks = *on
     c                   call      'VT110R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tag
     c                   endif
      *
      * F19=Prisjustering
      *
     c                   if        *inkt = *on
     c                   call      'VV105R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tag
     c                   endif
      *
      * F20=Endring av priser på basisenhet
      *
     c                   if        *inku = *on
     c                   call      'VV106R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_prgr
     c                   parm                    b2enhe
     c                   goto      b2tag
     c                   endif
6.32  *
6.32  * F21=Overgang til vare-leverandør
6.32  *
6.32 c                   if        *inkv = *on
6.32 c                   call      'VV130R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    w_vare
6.32 c                   goto      b2tag
6.32 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av eksisterende vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_vare     begsr
      *
     c                   eval      *in30 = *on
      *
      * Henter vareinformasjon
      *
     c                   eval      b2vare = vvvare
     c                   eval      b2tek1 = vvtek1
     c                   eval      b2tek2 = vvtek2
     c                   eval      b2ogrp = vvogrp
     c                   eval      b2hgrp = vvhgrp
     c                   eval      b2ugrp = vvugrp
     c                   eval      b2ldor = vvldor
     c                   eval      b2lvar = vvlvar
     c                   eval      b2nmod = vvnmod
     c                   eval      b2enps = vvenps
     c                   eval      b2enhs = vvenhs
     c                   eval      b2enhl = vvenhl
     c                   eval      b2enhe = vvenhe
     c                   eval      b2kpri = vvkpri
     c                   eval      b2vtyp = vvvtyp
     c                   eval      b2nobb = vvnobb
     c                   eval      b2nlev = vvnlev
     c                   eval      b2nuda = 0
     c                   eval      b2udat = 0
     c                   eval      b2eusr = vveusr
      *
     c                   if        vvnuda <> *loval
     c     *dmy          move      vvnuda        b2nuda
     c                   endif
      *
     c                   if        vvudat <> *loval
     c     *dmy          move      vvudat        b2udat
     c                   endif
      *
     c     *dmy          move      vvodat        b2odat
     c     *dmy          move      vvedat        b2edat
7.01  *
7.01  *   Sjekk om vare er utgått på alle lager
7.01  *
7.01 c                   if        u_701 = *on
7.01 c                   eval      vlagl1_firm = vvfirm
7.01 c                   eval      vlagl1_vare = vvvare
7.01 c                   eval      b2utgl = 'J'
7.01 c     vlagl1_key    setll     vlagl1
7.01 c     vlagl1_key    reade     vlagl1
7.01 c                   dow       not %eof(vlagl1)
7.01 c                   if        vludat = *loval
7.01 c                   eval      b2utgl = *blank
7.01 c                   endif
7.01 c                   eval      vlvare = *blank
7.01 c     vlagl1_key    reade     vlagl1
7.01 c                   enddo
7.01 c                   if        vlvare <> *blank
7.01 c                   unlock    vlagl1
7.01 c                   endif
7.01 c                   endif
      *
      * Henter verdier til bilde-2
      *
     c                   eval      w_tek1 = vvtek1
     c                   eval      w_kbon = vvkbon
     c                   eval      w_kord = vvkord
     c                   eval      w_kenh = vvkenh
     c                   eval      w_kmva = vvkmva
     c                   eval      w_kres = vvkres
     c                   eval      w_kpme = vvkpme
     c                   eval      w_keti = vvketi
     c                   eval      w_kpdi = vvkpdi
     c                   eval      w_kjus = vvkjus
     c                   eval      w_desi = vvdesi
     c                   eval      w_kstr = vvkstr
     c                   eval      w_kabc = vvkabc
     c                   eval      w_ahgr = vvahgr
     c                   eval      w_augr = vvaugr
     c                   eval      w_avar = vvavar
     c                   eval      w_kfri = vvkfri
     c                   eval      w_fako = vvfako
     c                   eval      w_dekn = vvdekn
     c                   eval      w_omrn = vvomrn
     c                   eval      w_godj = vvgodj
     c                   eval      w_khev = vvkhev
6.11 c                   eval      w_sort = vvsort
      *
      * Henter beskrivelse av felter i bildet
      *
     c                   exsr      hent_kontr
      *
      * Henter vare-pris informasjon
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Tar vare på gamle verdien av variable som kontroll-informasjon i
      * bildet
      *
     c                   eval      s_ogrp = b2ogrp
     c                   eval      s_hgrp = b2hgrp
     c                   eval      s_ugrp = b2ugrp
     c                   eval      s_ldor = b2ldor
     c                   eval      s_enhe = b2enhe
     c                   eval      s_vtyp = b2vtyp
      *
      * Dersom kopiering av vare, settes varenummer til varenummer det skal
      * kopieres til, og opprettelsesdato/endringsdato lik udate
      *
     c                   if        p_valg = 3
     c                   eval      b_ny_vare = *on
     c                   eval      b2vare = p_varekopi
     c                   eval      vvvare = p_varekopi
     c                   eval      w_vare = p_varekopi
     c                   eval      w_valg = 1
     c                   eval      b2nobb = 0
     c                   eval      b2udat = 0
     c                   eval      b2eusr = l_user
     c     *dmy          move      w_udat        b2odat
     c     *dmy          move      w_udat        b2edat
     c                   else
     c                   eval      b2vare = p_vare
     c                   endif
8.03 c                   if        p_valg = 1
8.03 c                   eval      b_ny_vare = *on
8.03 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
      * Setter default-verdier
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b_ny_vare = *on
      *
     c                   eval      b2vare = p_vare
     c                   eval      b2tek1 = *blank
     c                   eval      b2tek2 = *blank
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2lvar = *blank
     c                   eval      b2nmod = 0
     c                   eval      b2enps = *blank
     c                   eval      b2enhs = *blank
     c                   eval      b2enhl = *blank
     c                   eval      b2enhe = *blank
     c                   eval      b2kpri = *blank
     c                   eval      b2vtyp = 'L'
     c                   eval      b2nobb = 0
     c                   eval      b2nlev = 0
     c                   eval      b2nuda = 0
     c                   eval      b2udat = 0
     c                   eval      b2eusr = l_user
      *
     c                   eval      b2utxt = *blank
     c                   eval      b2lnav = *blank
      *
     c     *dmy          move      w_udat        b2odat
     c     *dmy          move      w_udat        b2edat
      *
     c                   eval      w_desi = 2
      *
      * Oppretter subfile for vare-pris
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile med priser og reg.linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller subfile med priser
      * Henter nyeste pris innenfor leveringstype og enhet
      *
     c     vvenl1_key2   setll     vvenl1
     c     vvenl1_key2   reade     vvenl1
     c                   dow       not %eof
      *
7.02 c                   if        u_702 = *on
7.02 c                   exsr      sjekk_prisgr
7.02 c                   endif
      *
     c                   eval      s_lety = -1
      *
5.70 c                   if        w_prgr <> *blank
5.70  * 1. prøve med prisgruppe   med fast prisgruppe
5.70 c                   if        w_ko13 = 0
6.21 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = veenhe
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1
     c                   dow       not %eof and
     c                             w_srrn < c_sfil
      *
     c                   if        vplety <> s_lety
      *
7.03  * alle beløp må være max 999999,99
7.03 c                   if        vpsapr < 1000000 and
7.03 c                             vpkopr < 1000000 and
7.03 c                             vpbupr < 1000000
7.03  *
7.02 c                   if        u_702 = *on
7.02 c                   if        vpprgr = *blank
7.02 c                   exsr      sjekk_prisgr
7.02 c                   if        b_prgr = *on
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
5.70 c                   eval      b1prgr = vpprgr
     c                   eval      b1enhe = vpenhe
     c                   eval      b1lety = vplety
     c                   eval      b1sapr = vpsapr
     c                   eval      b1kopr = vpkopr
     c                   eval      b1inpr = vpinpr
     c                   eval      b1bupr = vpbupr
     c                   eval      b1omre = veomre
     c                   eval      b1dekn = 0
      *
     c     *dmy          move      vppdat        b1pdat
      *
     c                   if        w_valg <> 3
5.70 c                   eval      b1prgr_gml = b1prgr
     c                   eval      b1enhe_gml = b1enhe
     c                   eval      b1lety_gml = b1lety
     c                   eval      b1pdat_gml = b1pdat
     c                   eval      b1sapr_gml = b1sapr
     c                   eval      b1kopr_gml = b1kopr
     c                   eval      b1inpr_gml = b1inpr
     c                   eval      b1bupr_gml = b1bupr
     c                   endif
      *
     c                   if        vplety = 0 and
     c                             vpenhe = vvenhe
     c                   eval      w_sapr = vpsapr
     c                   eval      w_kopr = vpkopr
     c                   eval      w_inpr = vpinpr
     c                   eval      w_bupr = vpbupr
     c                   endif
      *
     c                   exsr      kalk_dekn
      *
     c                   write     b1sfl
      *
7.03 c                   endif
     c                   endif
      *
     c                   eval      s_lety = vplety
      *
     c     vvprl1_key    reade     vvprl1
     c                   enddo
     c                   else
5.70  * 1. prøve med prisgruppe   med alle prisgrupper
     c     ra30lr_key2   setll     ra30lr
     c     ra30lr_key2   reade     ra30lr
     c                   dow       not %eof(ra30lr)
     c                   eval      s_lety = -1
      *
6.21 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = redkod
     c                   eval      vvprl1_enhe = veenhe
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1
     c                   dow       not %eof and
     c                             w_srrn < c_sfil
      *
     c                   if        vplety <> s_lety
7.03  * alle beløp må være max 999999,99
7.03 c                   if        vpsapr < 1000000 and
7.03 c                             vpkopr < 1000000 and
7.03 c                             vpbupr < 1000000
7.03  *
     c                   eval      w_srrn = w_srrn + 1
      *
5.70 c                   eval      b1prgr = vpprgr
     c                   eval      b1enhe = vpenhe
     c                   eval      b1lety = vplety
     c                   eval      b1sapr = vpsapr
     c                   eval      b1kopr = vpkopr
     c                   eval      b1inpr = vpinpr
     c                   eval      b1bupr = vpbupr
     c                   eval      b1omre = veomre
     c                   eval      b1dekn = 0
      *
     c     *dmy          move      vppdat        b1pdat
      *
     c                   if        w_valg <> 3
5.70 c                   eval      b1prgr_gml = b1prgr
     c                   eval      b1enhe_gml = b1enhe
     c                   eval      b1lety_gml = b1lety
     c                   eval      b1pdat_gml = b1pdat
     c                   eval      b1sapr_gml = b1sapr
     c                   eval      b1kopr_gml = b1kopr
     c                   eval      b1inpr_gml = b1inpr
     c                   eval      b1bupr_gml = b1bupr
     c                   endif
      *
     c                   if        vplety = 0 and
     c                             vpenhe = vvenhe
     c                   eval      w_sapr = vpsapr
     c                   eval      w_kopr = vpkopr
     c                   eval      w_inpr = vpinpr
     c                   eval      w_bupr = vpbupr
     c                   endif
      *
     c                   exsr      kalk_dekn
      *
     c                   write     b1sfl
      *
7.03 c                   endif
     c                   endif
      *
     c                   eval      s_lety = vplety
      *
     c     vvprl1_key    reade     vvprl1
     c                   enddo
     c     ra30lr_key2   reade     ra30lr
     c                   enddo
     c                   endif
      *
5.70 c                   endif
      *
      *
5.70  * 2. prisgruppe lik blank
5.70  *
7.02 c                   if        b_prgr = *off or u_702 = *off
      *
5.70 c                   eval      s_lety = -1
6.21 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = veenhe
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1
5.70 c                   dow       not %eof and
5.70 c                             w_srrn < c_sfil
5.70  *
5.70 c                   if        vplety <> s_lety
7.03  * alle beløp må være max 999999,99
7.03 c                   if        vpsapr < 1000000 and
7.03 c                             vpkopr < 1000000 and
7.03 c                             vpbupr < 1000000
7.03  *
7.02 c                   if        u_702 = *on
7.02 c                   if        vpprgr = *blank
7.02 c                   exsr      sjekk_prisgr
7.02 c                   if        b_prgr = *on
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
7.02 c                   endif
5.70  *
5.70 c                   eval      w_srrn = w_srrn + 1
5.70  *
5.70 c                   eval      b1prgr = vpprgr
5.70 c                   eval      b1enhe = vpenhe
5.70 c                   eval      b1lety = vplety
5.70 c                   eval      b1sapr = vpsapr
5.70 c                   eval      b1kopr = vpkopr
5.70 c                   eval      b1inpr = vpinpr
5.70 c                   eval      b1bupr = vpbupr
5.70 c                   eval      b1omre = veomre
5.70 c                   eval      b1dekn = 0
5.70  *
5.70 c     *dmy          move      vppdat        b1pdat
5.70  *
5.70 c                   if        w_valg <> 3
5.70 c                   eval      b1prgr_gml = b1prgr
5.70 c                   eval      b1enhe_gml = b1enhe
5.70 c                   eval      b1lety_gml = b1lety
5.70 c                   eval      b1pdat_gml = b1pdat
5.70 c                   eval      b1sapr_gml = b1sapr
5.70 c                   eval      b1kopr_gml = b1kopr
5.70 c                   eval      b1inpr_gml = b1inpr
5.70 c                   eval      b1bupr_gml = b1bupr
5.70 c                   endif
5.70  *
5.70 c                   if        vplety = 0 and
5.70 c                             vpenhe = vvenhe
5.70 c                   eval      w_sapr = vpsapr
5.70 c                   eval      w_kopr = vpkopr
5.70 c                   eval      w_inpr = vpinpr
5.70 c                   eval      w_bupr = vpbupr
5.70 c                   endif
5.70  *
5.70 c                   exsr      kalk_dekn
5.70  *
5.70 c                   write     b1sfl
5.70  *
7.03 c                   endif
5.70 c                   endif
5.70  *
5.70 c                   eval      s_lety = vplety
5.70  *
5.70 c     vvprl1_key    reade     vvprl1
5.70 c                   enddo
5.70  *
7.02 c                   endif                                                  b_prgr = *off or u..
5.70  *
5.70 c     vvenl1_key2   reade     vvenl1
5.70 c                   enddo
      *
      * Fyller opp subfile med blanke registreringslinjer
      *
     c                   dow       w_srrn < c_sfil
      *
     c                   eval      w_srrn = w_srrn + 1
      *
5.70 c                   eval      b1prgr = *blank
     c                   eval      b1enhe = *blank
     c                   eval      b1lety = 0
     c                   eval      b1pdat = 0
     c                   eval      b1sapr = 0
     c                   eval      b1kopr = 0
     c                   eval      b1inpr = 0
     c                   eval      b1bupr = 0
     c                   eval      b1omre = 0
     c                   eval      b1dekn = 0
      *
5.70 c                   eval      b1prgr_gml = *blank
     c                   eval      b1enhe_gml = *blank
     c                   eval      b1lety_gml = 0
     c                   eval      b1pdat_gml = 0
     c                   eval      b1sapr_gml = 0
     c                   eval      b1kopr_gml = 0
     c                   eval      b1inpr_gml = 0
     c                   eval      b1bupr_gml = 0
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   eval      *in15 = *on
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av kontroll-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kontr    begsr
      *
     c                   eval      b2utxt = *blank
     c                   eval      b2lnav = *blank
      *
      * Varegruppe-beskrivelse
      *
     c                   eval      vugrl1_uogr = b2ogrp
     c                   eval      vugrl1_uhgr = b2hgrp
     c                   eval      vugrl1_uugr = b2ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      b2utxt = vgutxt
     c                   endif
      *
      * Leverandørnavn
      *
     c                   eval      rlevl1_levr = b2ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b2lnav = rlnavn
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulering av dekningsgrad
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_dekn     begsr
      *
     c                   eval      b1dekn = 0
      *
      * Avbryter dersom salgspris = 0
      *
     c                   if        b1sapr = 0
     c                   leavesr
     c                   endif
      *
      * Beregner DG utfra kostpris på vare
      *
     c                   if        b1kopr <> 0
     c                   select
     c                   when      (100 - (b1kopr * 100 / b1sapr)) < -99,99
     c                   eval      b1dekn = -99,99
     c                   when      (100 - (b1kopr * 100 / b1sapr)) > 99,99
     c                   eval      b1dekn = 99,99
     c                   other
     c                   eval(h)   b1dekn = 100 - (b1kopr * 100 / b1sapr)
     c                   endsl
     c                   leavesr
     c                   endif
      *
      * Dersom kostpris = 0 hentes DG fra varen eller undergruppe
      *
     c                   if        w_dekn > 99,99
     c                   eval      b1dekn = 99,99
     c                   else
     c                   eval      b1dekn = w_dekn
     c                   endif
      *
     c                   if        b1dekn = 0
     c                   if        vgudek > 99,99
     c                   eval      b1dekn = 99,99
     c                   else
     c                   eval      b1dekn = vgudek
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_utxt
     c                   eval      b2utxt = w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   eval      b2utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_utxt
     c                   eval      b2utxt = w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2UGRP'
     c                   eval      w_utxt = b2utxt
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   eval      b2utxt = w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2LDOR'
     c                   eval      w_lnav = b2lnav
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   eval      b2lnav = w_lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    b2vtyp
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2ENPS'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enps
     c                   parm                    w_etxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2ENHS' and
     c                             *in30 = *off
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enhs
     c                   parm                    w_etxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2ENHL' and
     c                             *in30 = *off
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enhl
     c                   parm                    w_etxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B2ENHE'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   leavesr
     c                   endif
      *
      * Spørring i subfile
      *
     c                   if        w_afld = 'B1ENHE'
     c     w_curn        chain     b1sfl                              91
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b1enhe
     c                   parm                    w_etxt
     c                   update    b1sfl
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1LETY'
     c     w_curn        chain     b1sfl                              91
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1lety
     c                   parm                    w_ltxt
     c                   update    b1sfl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Behandler subfile control
      *
      * Varetekst
      *
     c                   eval      b2tek1 = %trim(b2tek1)
     c                   eval      b2tek2 = %trim(b2tek2)
      *
     c                   if        b2tek1 = *blank
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Varegruppe
      *
     c                   eval      vugrl1_uogr = b2ogrp
     c                   eval      vugrl1_uhgr = b2hgrp
     c                   eval      vugrl1_uugr = b2ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b2utxt = vgutxt
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      rlevl1_levr = b2ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *on
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b2lnav = rlnavn
     c                   endif
      *
      * NOBB-nummer
      *
     c                   if        b2nobb <> 0
      *
     c                   if        b2nobb < 10000000
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vvarl3_nobb = b2nobb
     c     vvarl3_key    setll     vvarl3
     c     vvarl3_key    reade     vvarl3                                 91
     c                   dow       *in91 = *off
     c                   if        vvvare <> w_vare
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c     vvarl3_key    reade     vvarl3                                 91
     c                   enddo
      *
     c                   endif
      *
      * Varetype
      *
     c                   if        b2vtyp = *blank
     c                   eval      *in40  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vvtyl1_vtyp = b2vtyp
     c     vvtyl1_key    chain     vvtyl1                             91
     c                   if        *in91 = *on
     c                   eval      *in41  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.23  *
6.23  * sjekker om man prøver å bytte annet L-S vv eller D-T vv.
6.23 c                   if        s_vtyp <> b2vtyp
6.23  *
6.23 c                   if        s_vtyp <> *blank and
6.23 c                             s_vtyp = 'S'  and
6.23 c                             b2vtyp <>'L'
     c                   exsr      xb3msg
6.23 c                   eval      *in42 =   *on
6.23 c                   eval      b_feil = *on
6.23 c                   leavesr
6.23 c                   endif
6.23 c                   if        s_vtyp <> *blank and
6.23 c                             s_vtyp = 'L'  and
6.23 c                             b2vtyp <>'S'
     c                   exsr      xb3msg
6.23 c                   eval      *in42 =   *on
6.23 c                   eval      b_feil = *on
6.23 c                   leavesr
6.23 c                   endif
6.23 c                   if        s_vtyp <> *blank and
6.23 c                             s_vtyp = 'D'  and
6.23 c                             b2vtyp <>'T'
     c                   exsr      xb3msg
6.23 c                   eval      *in42 =   *on
6.23 c                   eval      b_feil = *on
6.23 c                   leavesr
6.23 c                   endif
6.23 c                   if        s_vtyp <> *blank and
6.23 c                             s_vtyp = 'T'  and
6.23 c                             b2vtyp <>'D'
     c                   exsr      xb3msg
6.23 c                   eval      *in42 =   *on
6.23 c                   eval      b_feil = *on
6.23 c                   leavesr
6.23 c                   endif
6.23  *
6.23 c                   endif
      *
      * Priskode
      *
     c                   if        b2kpri <> *blank and
     c                             b2kpri <> 'N' and
     c                             b2kpri <> 'B'
     c                   eval      *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Statistikkenhet, lagerenhet og basisenhet
      * Enhetene kreves kun utfylt dersom varen er en lager eller skaffevare
      * Dersom ny vare, og statistikkenhet eller lagerenhet ikke er utfylt,
      * hentes disse fra undergruppe eller hovedgruppe
      *
     c                   if        b2vtyp = 'L' or
     c                             b2vtyp = 'S'
      *
     c                   if        b2enhe = *blank
     c                   eval      *in46 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b_ny_vare = *on
      *
     c                   if        b2enhs = *blank
     c                   eval      b2enhs = vguens
     c                   endif
      *
     c                   if        b2enhl = *blank
     c                   eval      b2enhl = vguenl
     c                   endif
      *
     c                   if        b2enhs = *blank or
     c                             b2enhl = *blank
     c                   eval      vhgrl1_hogr = b2ogrp
     c                   eval      vhgrl1_hhgr = b2hgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   if        b2enhs = *blank
     c                   eval      b2enhs = vghens
     c                   endif
     c                   if        b2enhl = *blank
     c                   eval      b2enhl = vghenl
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   if        b2enhs = *blank or
     c                             b2enhl = *blank
     c                   eval      *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
     c                   if        b2enps <> *blank
     c                   eval      venhl1_eenh = b2enps
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in51 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b2enhs <> *blank
     c                   eval      venhl1_eenh = b2enhs
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in47 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b2enhl <> *blank
     c                   eval      venhl1_eenh = b2enhl
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in48 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b2enhe <> *blank
     c                   eval      venhl1_eenh = b2enhe
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in49 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b2udat <> 0
     c     *dmy          test(d)                 b2udat                 50
     c                   if        *in50 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Priser
      *
     c                   exsr      sjekk_pris
     c                   if        b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av innhold i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_pris    begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b_pris = *off
     c                   eval      b_enhs = *off
     c                   eval      b_enhl = *off
     c                   eval      b_enhe = *off
      *
     c                   eval      w_sapr = 0
     c                   eval      w_kopr = 0
     c                   eval      w_inpr = 0
     c                   eval      w_bupr = 0
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w = w_srrn_w + 1
     c     w_srrn_w      chain     b1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Utelater blanke og slettede pris-linjer
      *
5.70 c                   if        b1prgr = *blank and
5.70 c                             b1enhe = *blank and
     c                             b1lety = 0 and
     c                             b1pdat = 0 and
     c                             b1sapr = 0 and
     c                             b1kopr = 0 and
     c                             b1inpr = 0 and
     c                             b1bupr = 0
     c                   iter
     c                   endif
      *
     c                   eval      b_pris = *on
5.70  *
5.70  * prisgruppe
5.70  *
5.70 c                   if        b1prgr <> *blank
5.70 c                   if        w_ko13 = 0
5.70 c                   if        b1prgr <> w_prgr
5.70 c                   eval      *in62 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   else
5.70 c                   eval      ra30lr_dkod = b1prgr
5.70 c     ra30lr_key    chain     ra30lr
5.70 c                   if        not  %found(ra30lr)
5.70 c                   eval      *in62 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   endif
5.70 c                   endif
      *
      * Enhet og salgspris
      *
     c                   if        b1enhe <> *blank
     c                   eval      venhl1_eenh = b1enhe
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in61 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b2vtyp = 'L' or
     c                             b2vtyp = 'S'
     c                   if        b1enhe = *blank
     c                   eval      *in60 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   if        b1sapr = 0 or
     c                             b1sapr = 0,01
     c                   eval      vvenl1_enhe = b1enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        %found and
     c                             vesaen > 0 or
     c                             not %found
     c                   eval      *in60 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        b1lety <> 0
     c                   eval      vltpl1_lety = b1lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *on
     c                   eval      *in63 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Dato
      *
     c                   if        b1pdat = 0
     c     *dmy          move      w_udat        b1pdat
     c                   endif
      *
     c     *dmy          test(d)                 b1pdat                 67
     c                   if        *in67 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Statistikkenhet
      *
     c                   if        b1enhe = b2enhs
     c                   eval      b_enhs = *on
     c                   endif
      *
      * Lagerenhet
      *
     c                   if        b1enhe = b2enhl
     c                   eval      b_enhl = *on
     c                   endif
      *
      * Basisenhet
      *
     c                   if        b1lety = 0 and
     c                             b1enhe = b2enhe
      *
     c                   eval      w_sapr = b1sapr
     c                   eval      w_kopr = b1kopr
     c                   eval      w_inpr = b1inpr
     c                   eval      w_bupr = b1bupr
      *
     c                   eval      b_enhe = *on
      *
     c                   endif
      *
     c                   enddo
      *
      * Oppretter pris-linje dersom vare ikke er lager-/skaffevare, og
      * pris-linje ikke er registrert
      *
     c                   if        b2vtyp <> 'L' and
     c                             b2vtyp <> 'S' and
     c                             b_pris = *off
     c                   if        b2enhs = *blank and
     c                             b2enhl = *blank and
     c                             b2enhe = *blank
     c                   eval      b_enhs = *on
     c                   eval      b_enhl = *on
     c                   eval      b_enhe = *on
     c                   eval      w_srrn_w = 1
     c     w_srrn_w      chain     b1sfl
     c     *dmy          move      w_udat        b1pdat
     c                   update    b1sfl
     c                   endif
     c                   endif
      *
      * Pris-linje må finnes for statistikkenhet, lagerenhet og
      * basisenhet
8.02  * sjekk dette med sql - kun når varen eksisterer
8.04 c                   if        b_ny_vare = *off and
8.04 c                             b_pris = *off
8.02 c                   eval      w_anta = 0
8.02 c/exec sql
8.02 c+                  SELECT COUNT(*)
8.02 c+                  into :w_anta
8.02 c+                  FROM vvprpf
8.02 c+                  where vpfirm = :w_firm and
8.02 c+                        vpvare = :vvvare and
8.02 c+                        vpenhe = :vvenhe
8.02 c+                  fetch first 1 rows only
8.02 c/end-exec
8.02 c                   if        w_anta = 0
8.02 c                   eval      *in55  = *on
8.02 c                   eval      b_feil = *on
8.02 c                   leavesr
8.02 c                   endif
8.02 c                   eval      w_anta = 0
8.02 c/exec sql
8.02 c+                  SELECT COUNT(*)
8.02 c+                  into :w_anta
8.02 c+                  FROM vvprpf
8.02 c+                  where vpfirm = :w_firm and
8.02 c+                        vpvare = :vvvare and
8.02 c+                        vpenhe = :vvenhl
8.02 c+                  fetch first 1 rows only
8.02 c/end-exec
8.02 c                   if        w_anta = 0
8.02 c                   eval      *in55  = *on
8.02 c                   eval      b_feil = *on
8.02 c                   leavesr
8.02 c                   endif
8.02 c                   eval      w_anta = 0
8.02 c/exec sql
8.02 c+                  SELECT COUNT(*)
8.02 c+                  into :w_anta
8.02 c+                  FROM vvprpf
8.02 c+                  where vpfirm = :w_firm and
8.02 c+                        vpvare = :vvvare and
8.02 c+                        vpenhe = :vvenhs
8.02 c+                  fetch first 1 rows only
8.02 c/end-exec
8.02 c                   if        w_anta = 0
8.02 c                   eval      *in55  = *on
8.02 c                   eval      b_feil = *on
8.02 c                   leavesr
8.02 c                   endif
8.02  *
8.02 c                   else
8.02 c                   if        b_enhs = *off or
8.02 c                             b_enhl = *off or
8.02 c                             b_enhe = *off
8.02 c                   eval      *in55  = *on
8.02 c                   eval      b_feil = *on
8.02 c                   leavesr
8.02 c                   endif
8.02 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bekrefte bytting av varetype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb2msg        begsr
      *
     c                   eval      b_endr_vtyp = *off
      *
     c     b2tagm        tag
      *
     c                   exfmt     b2msg
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   leavesr
     c                   when      *inkm
     c                   call      'LL502R'
     c                   parm                    w_firm
6.14 c                   parm                    w_vare
     c                   goto      b2tagm
6.14 c*                  when      *inkx
6.14 c*                  eval      b_endr_vtyp = *on
6.14 c*                  leavesr
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise en feil melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb3msg        begsr
      *
     c                   exfmt     b3msg
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Oppdatering av vareregister
      *
     c                   clear                   vvarlur
     c     vvarlu_key    chain     vvarlur                            90
      *
      * Flytter verdier til database-felter
      *
     c                   eval      s_enhe = vvenhe
      *
     c                   eval      vvtek1 = b2tek1
     c                   eval      vvtek2 = b2tek2
     c                   eval      vvogrp = b2ogrp
     c                   eval      vvhgrp = b2hgrp
     c                   eval      vvugrp = b2ugrp
     c                   eval      vvkpri = b2kpri
     c                   eval      vvvtyp = b2vtyp
     c                   eval      vvldor = b2ldor
     c                   eval      vvlvar = b2lvar
     c                   eval      vvnmod = b2nmod
     c                   eval      vvenps = b2enps
     c                   eval      vvenhs = b2enhs
     c                   eval      vvenhl = b2enhl
     c                   eval      vvenhe = b2enhe
     c                   eval      vvnobb = b2nobb
     c                   eval      vvnlev = b2nlev
     c                   eval      vvudat = *loval
     c                   eval      vveusr = l_user
      *
     c                   time                    vvedat
     c                   time                    vvetim
      *
     c                   if        b2udat <> 0
     c     *dmy          move      b2udat        vvudat
     c                   endif
      *
      * Bilde-2
      *
     c                   eval      vvkbon = w_kbon
     c                   eval      vvkord = w_kord
     c                   eval      vvkenh = w_kenh
     c                   eval      vvkmva = w_kmva
     c                   eval      vvkres = w_kres
     c                   eval      vvkpme = w_kpme
     c                   eval      vvketi = w_keti
     c                   eval      vvkpdi = w_kpdi
     c                   eval      vvkjus = w_kjus
     c                   eval      vvdesi = w_desi
     c                   eval      vvkstr = w_kstr
     c                   eval      vvkabc = w_kabc
     c                   eval      vvahgr = w_ahgr
     c                   eval      vvaugr = w_augr
     c                   eval      vvavar = w_avar
     c                   eval      vvkfri = w_kfri
     c                   eval      vvfako = w_fako
     c                   eval      vvdekn = w_dekn
     c                   eval      vvomrn = w_omrn
     c                   eval      vvgodj = w_godj
     c                   eval      vvkhev = w_khev
6.11 c                   eval      vvsort = w_sort
      *
      * Dersom ny vare, og alternativ varegruppe ikke er utfylt, hentes
      * denne fra varegruppeknytning
      *
     c                   if        *in90 = *on and
     c                             vvahgr = 0 and
     c                             vvaugr = 0
     c                   call      'VK700R'
     c                   parm                    w_firm
     c                   parm                    vvogrp
     c                   parm                    vvhgrp
     c                   parm                    vvugrp
     c                   parm                    vvldor
     c                   parm                    vvahgr
     c                   parm                    vvaugr
     c                   endif
      *
      * Oppdaterer vare
      *
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   if        *in90 = *off
     c                   update    vvarlur
     c                   else
     c                   eval      vvfirm = w_firm
     c                   eval      vvvare = w_vare
     c                   time                    vvodat
6.10 c                   time                    vvotim
6.10 c                   eval      vvousr = l_user
     c                   write     vvarlur
     c                   endif
6.31  *
6.31  * hvis endring av varenr må nøkkel oppdateres på vare-pris og
6.31  * vare-enhet prisgiver.
6.31 c                   if        b2ldor <> s_ldor
6.31 c                   exsr      endr_ldor
6.31 c                   endif
      *
      * Dersom vare finnens fra før, og basisenhet er endret må omregnings-
      * faktor volum endres på alle enheter
      *
     c                   if        *in90 = *off and
     c                             vvenhe <> s_enhe
     c                   exsr      oppdat_omre
     c                   endif
      *
      * Oppdaterer vare-pris-register
      *
     c                   exsr      oppdat_pris
      *
      * Oppdaterer vare-enhet-register
      *
     c                   exsr      oppdat_enhet
      *
      * Oppdaterer register over slettede vare
      *
     c     vvasl1_key    chain     vvaslur                            91
     c                   if        *in91 = *off
     c                   delete    vvaslur
     c                   endif
      *
      * Oppdaterer vare-søketekst-register
      *
     c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    vvvare
6.22  *
6.22  * Hvis varetype er endret og det finnet kun et lager, så skal også varetype på
6.22  * lager endres.
6.22 c                   if        b_flla = *off and
6.22 c                             faalag = 1 and
6.22 c                             (b2vtyp = 'L' or
6.22 c                              b2vtyp = 'S')
6.22 c                   if        b_ny_vare = *off and
6.22 c                             b2vtyp <> s_vtyp
6.22 c                   call      'LL768R'
6.22 c                   parm                    w_firm
6.22 c                   parm                    vvvare
6.22 c                   parm                    w_lage
6.22 c                   parm                    vvvtyp
6.22 c                   parm                    b_inlr
6.22 c                   endif
6.22 c                   endif
      *
      *
5.70  * Oppretter vare i alle lager  dersom det er en ny lager eller skaffevare
5.70  * Ingen endring på lager hvis endring på varens varetype
      *
     c                   if        faalag = 1 and
5.70 c                             (b2vtyp = 'L' or
5.70 c                              b2vtyp = 'S')
5.70 c                   if        b_ny_vare = *on
5.70 c*                            b_ny_vare = *off and
5.70 c*                            b2vtyp <> s_vtyp
     c                   call      'LL760R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   endif
     c                   endif
      *
      * Sletter vare fra lager dersom endret vare, og varetype endres
5.70  * fra lagervare - men kun dersom det er endret til annet enn S eller L
      *
     c                   if        b_ny_vare = *off and
     c                             s_vtyp <> b2vtyp and
5.70 c                             b2vtyp <> 'L' and
5.70 c                             b2vtyp <> 'S'
     c                   call      'LL762R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   endif
7.01  *
7.01  * Merker varen i alle lager m/utgått dersom 'Utgår lager' er satt
7.01  *
7.01 c                   if        u_701 = *on
7.01 c                   if        b2utgl = 'J'
7.01 c                             and vvudat <> *loval
7.01 c                   eval      vlagl1_firm = vvfirm
7.01 c                   eval      vlagl1_vare = vvvare
7.01 c     vlagl1_key    setll     vlagl1
7.01 c     vlagl1_key    reade     vlagl1
7.01 c                   dow       not %eof(vlagl1)
7.01 c                   if        vludat = *loval
7.01 c                   eval      vludat = vvudat
7.01 c                   eval      vleusr = l_user
7.01 c                   time                    vledat
7.01 c                   time                    vletim
7.01 c                   endif
7.01 c                   update    vlagl1r
7.01 c                   eval      vlvare = *blank
7.01 c     vlagl1_key    reade     vlagl1
7.01 c                   enddo
7.01 c                   if        vlvare <> *blank
7.01 c                   unlock    vlagl1
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av omregningsfaktorer på enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_omre   begsr
      *
     c                   eval      vvenlu_enhe = vvenhe
     c     vvenlu_key    chain     vvenlur
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      w_omre = veomre
      *
     c                   eval      veomre = 1
6.10 c                   time                    veedat
6.10 c                   time                    veetim
6.10 c                   eval      veeusr = l_user
     c                   update    vvenlur
      *
     c     vvenl1_key2   setll     vvenl1
     c     vvenl1_key2   reade     vvenl1
     c                   dow       not %eof
      *
     c                   if        veenhe <> vvenhe
     c                   eval      vvenlu_enhe = veenhe
     c     vvenlu_key    chain     vvenlur
     c                   if        %found
     c                   eval(h)   veomre = veomre / w_omre
6.10 c                   time                    veedat
6.10 c                   time                    veetim
6.10 c                   eval      veeusr = l_user
     c                   update    vvenlur
     c                   endif
     c                   endif
      *
     c     vvenl1_key2   reade     vvenl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_pris   begsr
      *
     c                   eval      b_endr_pris = *off
      *
      * Leser alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w = w_srrn_w + 1
     c     w_srrn_w      chain     b1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
      * Avbryter dersom ingen endring
      *
     c                   if        b1enhe = b1enhe_gml and
5.70 c                             b1prgr = b1prgr_gml and
     c                             b1lety = b1lety_gml and
     c                             b1pdat = b1pdat_gml and
     c                             b1sapr = b1sapr_gml and
     c                             b1kopr = b1kopr_gml and
     c                             b1inpr = b1inpr_gml and
     c                             b1bupr = b1bupr_gml
     c                   iter
     c                   endif
      *
     c                   eval      b_endr_pris = *on
      *
      * Sletting av pris
      *
     c                   if        b1pdat_gml <> 0 or
     c                             b1sapr_gml <> 0 or
     c                             b1kopr_gml <> 0 or
     c                             b1inpr_gml <> 0 or
     c                             b1bupr_gml <> 0
5.70 c                   if        b1prgr = *blank and
5.70 c                             b1enhe = *blank and
     c                             b1lety = 0 and
     c                             b1pdat = 0 and
     c                             b1sapr = 0 and
     c                             b1kopr = 0 and
     c                             b1inpr = 0 and
     c                             b1bupr = 0
      *
6.21 c                   eval      vvprlu_ldor = vvldor
5.70 c                   eval      vvprlu_prgr = b1prgr_gml
     c                   eval      vvprlu_enhe = b1enhe_gml
     c                   eval      vvprlu_lety = b1lety_gml
     c     *dmy          move      b1pdat_gml    vvprlu_pdat
     c     vvprlu_key    chain     vvprlur                            90
     c                   if        *in90 = *off
     c                   delete    vvprlur
     c                   endif
      *
     c                   update    b1sfl
     c                   iter
      *
     c                   endif
     c                   endif
      *
      * Dersom dato er null settes dato lik dagens dato
      *
     c                   if        b1pdat = 0
     c     *dmy          move      w_udat        b1pdat
     c                   endif
      *
      * Endring av enhet eller leveringstype
      *
     c                   if        b1enhe <> b1enhe_gml or
5.70 c                             b1prgr <> b1prgr_gml or
     c                             b1lety <> b1lety_gml
     c                   if        b1pdat_gml <> 0 or
     c                             b1sapr_gml <> 0 or
     c                             b1kopr_gml <> 0 or
     c                             b1inpr_gml <> 0 or
     c                             b1bupr_gml <> 0
6.21 c                   eval      vvprlu_ldor = vvldor
5.70 c                   eval      vvprlu_prgr = b1prgr_gml
     c                   eval      vvprlu_enhe = b1enhe_gml
     c                   eval      vvprlu_lety = b1lety_gml
     c     *dmy          move      b1pdat_gml    vvprlu_pdat
     c     vvprlu_key    chain     vvprlur                            90
     c                   if        *in90 = *off
5.70 c                   eval      vpprgr = b1prgr
     c                   eval      vpenhe = b1enhe
     c                   eval      vplety = b1lety
6.10 c                   time                    vpedat
6.10 c                   time                    vpetim
6.10 c                   eval      vpeusr = l_user
     c                   update    vvprlur
     c                   endif
     c                   endif
     c                   endif
      *
      * Endring av dato uten å endre pris
      *
     c                   if        b1pdat_gml <> 0 and
     c                             b1pdat <> b1pdat_gml and
     c                             b1sapr =  b1sapr_gml and
     c                             b1kopr =  b1kopr_gml and
     c                             b1inpr =  b1inpr_gml and
     c                             b1bupr =  b1bupr_gml
6.21 c                   eval      vvprlu_ldor = vvldor
5.70 c                   eval      vvprlu_prgr = b1prgr
     c                   eval      vvprlu_enhe = b1enhe
     c                   eval      vvprlu_lety = b1lety
     c     *dmy          move      b1pdat_gml    vvprlu_pdat
     c     vvprlu_key    chain     vvprlur                            90
     c                   if        *in90 = *off
     c     *dmy          move      b1pdat        vppdat
6.10 c                   time                    vpedat
6.10 c                   time                    vpetim
6.10 c                   eval      vpeusr = l_user
     c                   update    vvprlur
     c                   endif
     c                   endif
      *
      * Ny pris / endret pris
      *
     c                   if        b1sapr <> b1sapr_gml
     c     *dmy          move      b1pdat        w_pdat
     c                   if        w_pdat < w_udat
     c     *dmy          move      w_udat        b1pdat
     c                   endif
     c                   endif
      *
6.21 c                   eval      vvprlu_ldor = vvldor
5.70 c                   eval      vvprlu_prgr = b1prgr
     c                   eval      vvprlu_enhe = b1enhe
     c                   eval      vvprlu_lety = b1lety
     c     *dmy          move      b1pdat        vvprlu_pdat
     c     vvprlu_key    chain     vvprlur                            90
      *
     c                   eval      vpfirm = w_firm
     c                   eval      vpvare = w_vare
6.21 c                   eval      vpldor = vvldor
5.70 c                   eval      vpprgr = b1prgr
     c                   eval      vpenhe = b1enhe
     c                   eval      vplety = b1lety
     c                   eval      vpsapr = b1sapr
     c                   eval      vpkopr = b1kopr
     c                   eval      vpinpr = b1inpr
     c                   eval      vpbupr = b1bupr
     c                   eval      vpeusr = l_user
      *
     c     *dmy          move      b1pdat        vppdat
     c                   time                    vpedat
6.10 c                   time                    vpetim
      *
     c                   if        *in90 = *off
     c                   update    vvprlur
     c                   else
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   endif
      *
     c                   update    b1sfl
     c                   iter
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av vare-enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_enhet  begsr
      *
      * Leser vare-pris, og oppretter enheter som mangler i vare-enhet
      * Dersom første enhet på vare settes rekkefølgenr til 1, ellers økes
      * rekkefølgenr med 1 i forhold til forrige rekkefølgenr
      *
     c     vvprl1_key2   setll     vvprl1
     c     vvprl1_key2   reade     vvprl1
     c                   dow       not %eof
      *
     c                   eval      vvenl1_enhe = vpenhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
      *
     c     vvenl5_key    chain     vvenl5
     c                   if        %found
     c                   if        vesaen < 9
     c                   eval      vesaen = vesaen + 1
     c                   else
     c                   eval      vesaen = 9
     c                   endif
     c                   else
     c                   eval      vesaen = 1
     c                   endif
      *
     c                   eval      vefirm = w_firm
     c                   eval      vevare = w_vare
     c                   eval      veenhe = vpenhe
     c                   eval      veomre = 1
     c                   eval      veinen = 0
     c                   eval      vepben = 0
     c                   eval      veeusr = l_user
      *
     c                   time                    veodat
     c                   time                    veedat
     c                   time                    veetim
      *
     c                   if        vpenhe = vvenhl
     c                   eval      veinen = 1
     c                   eval      vepben = 1
     c                   endif
      *
     c                   if        vpenhe <> vvenhe
     c                   select
     c                   when      vpinpr <> 0 and
     c                             w_inpr <> 0
     c                   eval(h)   veomre = vpinpr / w_inpr
     c                   when      vpkopr <> 0 and
     c                             w_kopr <> 0
     c                   eval(h)   veomre = vpkopr / w_kopr
     c                   when      vpsapr <> 0 and
     c                             w_sapr <> 0
     c                   eval(h)   veomre = vpsapr / w_sapr
     c                   endsl
     c                   endif
      *
6.10 c                   time                    veodat
6.10 c                   time                    veotim
6.10 c                   eval      veousr = l_user
6.10 c                   time                    veedat
6.10 c                   time                    veetim
6.10 c                   eval      veeusr = l_user
     c                   write     vvenlur
      *
     c                   endif
      *
     c     vvprl1_key2   reade     vvprl1
     c                   enddo
      *
      * Sletter enheter som er fjernet fra vare-pris
      *
     c     vvenl1_key2   setll     vvenl1
     c     vvenl1_key2   reade     vvenl1
     c                   dow       not %eof
      *
6.21 c                   eval      vvprl1_ldor = vvldor
5.70 c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = veenhe
     c     vvprl1_key    chain     vvprl1
     c                   if        not %found
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c     vvprl1_key    chain     vvprl1
5.70 c                   endif
5.70 c                   if        not %found
     c                   eval      vvenlu_enhe = veenhe
     c     vvenlu_key    chain     vvenlur
     c                   delete    vvenlur
     c                   endif
      *
     c     vvenl1_key2   reade     vvenl1
     c                   enddo
      *
     c                   endsr
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for oppdatere lev.nr på pris og vare-enh-prisgiver
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     endr_ldor     begsr
6.31  *
6.31 c                   eval      vvprlu_ldor = s_ldor
6.31 c     vvprlu_key2   setll     vvprlu
6.31 c     vvprlu_key2   reade     vvprlu
6.31 c                   dow       not %eof(vvprlu)
6.31 c                   eval      vpldor = vvldor
6.31 c                   time                    vpedat
6.31 c                   time                    vpetim
6.31 c                   eval      vpeusr = l_user
6.31 c                   update    vvprlur
6.31 c     vvprlu_key2   reade     vvprlu
6.31 c                   enddo
6.31  * vare-enhet-prisgiver
6.31 c                   eval      vvepl3_pldo = s_ldor
6.31 c     vvepl3_key    setll     vvepl3
6.31 c     vvepl3_key    reade     vvepl3
6.31 c                   dow       not %eof(vvepl3)
6.31 c                   eval      vepldo = vvldor
6.31 c                   time                    vepeda
6.31 c                   time                    vepeti
6.31 c                   eval      vepeus = l_user
6.31 c                   update    vvepl3r
6.31 c     vvepl3_key    reade     vvepl3
6.31 c                   enddo
6.31  *
6.31 c                   endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for å sjekke om prisgruppe finnes
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     sjekk_prisgr  begsr
7.02  *
7.02 c                   eval      b_prgr = *off
7.02 c                   eval      vvprlr_prgr = w_prgr
7.02 c                   eval      vvprlr_enhe = veenhe
7.02 c                   eval      vvprlr_ldor = vvldor
7.02 c     vvprlr_key2   chain     vvprlr
7.02 c                   if        %found(vvprlr)
7.02 c                   eval      b_prgr = *on
7.02 c                   endif
7.02 c                   endsr
      *
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  * Viser VA info
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  *
7.06 c     vis_VA        begsr
7.06  *
7.06 c                   eval      w_lety = 0
7.06 c                   eval      w_prig = *blanks
7.06  *
7.06 c                   call      'JV145R'
7.06 c                   parm                    w_firm
7.06 c                   parm                    vvvare
7.06 c                   parm                    w_prig
7.06 c                   parm                    w_lety
7.06 c                   parm                    w_udat
7.06 c                   parm                    vvldor
7.06  *
7.06 c                   endsr
7.06  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_valg
     c                   parm                    p_varekopi
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på vare m/NOBB-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for oppslag på slettet vare
      *
     c     vvasl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag/les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenl1_enhe
      *
     c     vvenl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-enhet
      *
     c     vvenl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.21 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
7.02  *
7.02 c     vvprlr_key2   klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    w_vare
7.02 c                   kfld                    vvprlr_ldor
7.02 c                   kfld                    vvprlr_prgr
7.02 c                   kfld                    vvprlr_enhe
      *
     c     vvprl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på varetype
      *
     c     vvtyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvtyl1_vtyp
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på notat
      *
     c     anotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anotl1_syst
     c                   kfld                    anotl1_noid
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på kode/status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av vare-enhet
      *
     c     vvenlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenlu_enhe
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.21 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
6.31  *
6.31  * Key for oppdatering av vare-pris
6.31  *
6.31 c     vvprlu_key2   klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    w_vare
6.31 c                   kfld                    vvprlu_ldor
6.31  *
6.31  * Key for oppdatering av vare-enhet-prisg
6.31  *
6.31 c     vvepl3_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    w_vare
6.31 c                   kfld                    vvepl3_pldo
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30lr_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30lr_dkod
5.70  * Key for les av alle prisgrupper
5.70  *
5.70 c     ra30lr_key2   klist
5.70 c                   kfld                    w_firm
6.22  *
6.22  * Key for oppslag på lager
6.22  *
6.22 c     ra10lr_key    klist
6.22 c                   kfld                    w_firm
6.30  *
6.30  * Key for oppslag på vare i VA
6.30  *
6.30 c     jvarl1_key    klist
6.30 c                   kfld                    jvarl1_vare
6.30  *
7.01  *
7.01  * Key for oppslag på lager
7.01  *
7.01 c     vlagl1_key    klist
7.01 c                   kfld                    vlagl1_firm
7.01 c                   kfld                    vlagl1_vare
      *
      *
      * Henter firma, vare og valg fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
     c                   eval      w_valg = p_valg
7.01  *
7.01  * Endring 7.01
7.01  *
7.01 c                   eval      *in44 = *off
7.01   CallP CO402R(l_filg:w_firm:0:'VV101R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01     *in44 = *on;
7.01   endif;
7.02  *
7.02  * Endring 7.01
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'IKKE_VIS_PG_BLANK':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
      *
      * Oppslag på kode/status
      *
     c     fstsl1_key    chain     fstsl1
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
5.70  *
5.70  * Henter tilgang på prisgruppe begrensning
5.70  *
5.70 c                   eval      w_kode = 13
5.70 c                   call      'FB700R   '
5.70 c                   parm                    w_kode
5.70 c                   parm                    w_ko13
6.22  *
6.22  * Sjekke om det kjøres lager eller at det kun er et lager
6.22  *
6.22 c                   eval      b_flla = *off
6.22 c                   eval      w_late = 0
6.22 c     ra10lr_key    setll     ra10lr
6.22 c     ra10lr_key    reade     ra10lr
6.22 c                   dow       not %eof(ra10lr)
6.22 c                   eval      w_lage = rajkod
6.22 c                   eval      w_late = w_late + 1
6.22 c     ra10lr_key    reade     ra10lr
6.22 c                   enddo
6.22 c                   if        w_late > 1
6.22 c                   eval      b_flla = *on
6.22 c                   endif
     c                   endsr
