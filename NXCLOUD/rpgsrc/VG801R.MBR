      /TITLE  Varegrupper - leser inn unspc
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASOFAK
      *  Program . . . . : VG801R
      *  Beskrivelse . . : Innlesing av unspc til varegrupper - bruker
      *                    feltet VGUFRI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           281111  Nytt program                             bof
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk
     fvugrlu    uf a e           k disk    rename(vugrpfr:vugrlur)
      *
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      *
     d                 ds
     d d_vgrp                         7
     d  d_ogrp                        2  0 overlay(d_vgrp:1)
     d  d_hgrp                        2  0 overlay(d_vgrp:3)
     d  d_ugrp                        3  0 overlay(d_vgrp:5)
      *
      *  Definering av variable i nøkler
      *
     d vugrlu_uogr     s                   like(vguogr)
     d vugrlu_uhgr     s                   like(vguhgr)
     d vugrlu_uugr     s                   like(vguugr)
      *
      *  Definering av variable
      *
     d w_firm          s                   like(vgufir)
     d w_ugrp          s              7
     d w_ufri          s                   like(vgufri)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese ekstern fil for å hente neste vare for behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Lese file
      *
     c                   read      lwexpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      w_ugrp = %subst(exclin:1:7)
     c                   eval      w_ufri = %trim(%subst(exclin:38:8))
     c                   if        %len(%trim(w_ugrp)) = 7      and
     c                             w_ufri <> *blank
     c                   move      w_ugrp        d_vgrp
     c                   eval      vugrlu_uogr = d_ogrp
     c                   eval      vugrlu_uhgr = d_hgrp
     c                   eval      vugrlu_uugr = d_ugrp
     c     vugrlu_key    chain     vugrlu
     c                   if        %found(vugrlu)
     c                   eval      vgufri = w_ufri
     c                   time                    vgueda
     c                   time                    vgueti
     c                   eval      vgueus = l_user
     c                   update    vugrlur
     c                   endif
      *
     c                   endif
      *
     c                   read      lwexpf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for oppdatering av LAGER
      *
     c     vugrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrlu_uogr
     c                   kfld                    vugrlu_uhgr
     c                   kfld                    vugrlu_uugr
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
