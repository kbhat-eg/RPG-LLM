     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP145R
      * Beskrivelse . . : Prosjekt, vedlikehold Budjsetterte varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240919 sst  Nytt program
7.00  *       240919 sst  Alltid hente lev.dato fra FODT/FOHE, SODT/FOHE
7.01  *       250919 sst  Bare hente vaerr dersom votypf/vaoakk <> 0
7.03  *       ?????? ???  ??????????????????????????????????????????????
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frpvalu    uf a e           k disk    rename(rpvapfr:rpvalur)
7.03 ffodtlp    if   e           k disk    rename(fodtpfr:fodtlpr)
7.03 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
7.03 fsodtlp    if   e           k disk    rename(sodtpfr:sodtlpr)
7.03 fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
7.03 fsidtlp    if   e           k disk    rename(sidtpfr:sidtlpr)
7.03 fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
7.01 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
      * Definering av Local data area
      *
     d                udS
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      * Definering av variabler i nøkler
      *
     d rpvalu_pros     s                   like(rpvpro)
     d rpvalu_upro     s                   like(rpvupr)
     d rpvalu_akti     s                   like(rpvakt)
     d rpvalu_vare     s                   like(rpvvar)
     d rpvalu_lass     s                   like(rpvlas)
     d fodtlp_pros     s                   like(fdproj)
     d fodtlp_vare     s                   like(fdvare)
     d fohel1_numm     s                   like(fonumm)
7.03 d fohel1_suff     s                   like(fosuff)
     d sodtlp_pros     s                   like(sdproj)
     d sodtlp_vare     s                   like(sdvare)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
7.03 d sohel1_suff     s                   like(sosuff)
     d sidtlp_pros     s                   like(slproj)
     d sidtlp_vare     s                   like(slvare)
     d sihel1_aarr     s                   like(shaarr)
     d sihel1_binr     s                   like(shbinr)
     d sihel1_numm     s                   like(shnumm)
7.03 d sihel1_suff     s                   like(shsuff)
7.01 d votyl1_firm     s                   like(vaofir)
7.01 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
      *
     d w_firm          s              3  0
     d w_vare          s                     like(rpvvar)
     d w_lass          s                     like(rpvlas)
      *
     d p_pros          s              6
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Oppdatere RPVAPF   fra FODTPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *
     c                   eval      fodtlp_pros = p_pros
     c                   eval      fodtlp_vare = *blank
     c     fodtlp_key    setll     fodtlp
     c     fodtlp_key    reade     fodtlp
      *
     c                   dow       not %eof(fodtlp)
     c                             and fdproj = p_pros
      *
     c                   if        fdvare <> *blank
      *
     c                   if        fonumm <> fdnumm
     c                             or fosuff <> fdsuff
     c                   eval      fohel1_numm = fdnumm
     c                   eval      fohel1_suff = fdsuff
     c     fohel1_key    chain     fohel1
7.01 c                   eval      votyl1_otyp = footyp
7.01 c     votyl1_key    chain     votyl1
7.01 c                   if        not %found(votyl1)
7.01 c                   eval      vaoakk = 0
7.01 c                   endif
     c                   endif
      *
     c                   if        %found(fohel1)
7.01 c                             and vaoakk <> 0
     c                   eval      rpvalu_pros = fdproj
     c                   eval      rpvalu_upro = foupro
     c                   eval      rpvalu_akti = fdakti
     c                   eval      rpvalu_vare = fdvare
     c                   eval      rpvalu_lass = fdlass
     c     rpvalu_key    chain     rpvalu
     c                   if        not %found(rpvalu)
     c                   eval      rpvfir = fdfirm
     c                   eval      rpvpro = fdproj
     c                   eval      rpvupr = foupro
     c                   eval      rpvakt = fdakti
     c                   eval      rpvprk = fokpro
     c                   eval      rpvkto = fdkont
     c                   eval      rpvvar = fdvare
     c                   eval      rpvlas = fdlass
     c                   eval      rpvtxt = fdtek1
     c                   eval      rpvenh = fdenh1
     c                   eval      rpvens = *blank
     c                   eval      rpvlda = fdldat
7.00 c                   if        fdldat = *loval
7.00 c                   eval      rpvlda = foldat
7.00 c                   endif
     c                   eval      rpvabu =*zero
     c                   eval      rpvabj = *zero
     c                   eval      rpvabs = *zero
     c                   eval      rpvkpr = fdkopr
     c                   eval      rpvkpb = *zero
     c                   eval      rpvaop = *zero
     c                   eval      rpvapp = *zero
     c                   eval      rpvafp = *zero
     c                   eval      rpvous = fdoous
     c                   eval      rpveus = fdeusr
     c                   eval      rpvoda = fdodat
     c                   eval      rpvoti = fdotim
     c                   eval      rpveda = fdedat
     c                   eval      rpveti = fdetim
     c                   write     rpvalur
7.00 c                   else
7.00 c                   eval      rpvlda = fdldat
7.00 c                   if        fdldat = *loval
7.00 c                   eval      rpvlda = foldat
7.00 c                   endif
7.00 c                   update    rpvalur
     c                   endif
     c                   eval      w_vare = fdvare
     c                   eval      w_lass = fdlass
     c                   endif
     c                   endif
     c     fodtlp_key    reade     fodtlp
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Oppdatere RPVAPF   fra SODTPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *
     c                   eval      sodtlp_pros = p_pros
     c                   eval      sodtlp_vare = *blank
     c     sodtlp_key    setll     sodtlp
     c     sodtlp_key    reade     sodtlp
      *
     c                   dow       not %eof(sodtlp)
     c                             and sdproj = p_pros
      *
     c                   if        sdvare <> *blank
      *
     c                   if        sonumm <> sdnumm
     c                             or sosuff <> sdsuff
     c                   eval      sohel1_aarr = sdaarr
     c                   eval      sohel1_binr = sdbinr
     c                   eval      sohel1_numm = sdnumm
     c                   eval      sohel1_suff = sdsuff
     c     sohel1_key    chain     sohel1
     c                   endif
      *
     c                   if        %found(sohel1)
     c                   eval      rpvalu_pros = sdproj
     c                   eval      rpvalu_upro = soupro
     c                   eval      rpvalu_akti = sdakti
     c                   eval      rpvalu_vare = sdvare
     c                   eval      rpvalu_lass = *zero
     c     rpvalu_key    chain     rpvalu
     c                   if        not %found(rpvalu)
     c                   eval      rpvfir = sdfirm
     c                   eval      rpvpro = sdproj
     c                   eval      rpvupr = soupro
     c                   eval      rpvakt = soakti
     c                   eval      rpvprk = sokpro
     c                   eval      rpvkto = sdkont
     c                   eval      rpvvar = sdvare
     c                   eval      rpvlas = *zero
     c                   eval      rpvtxt = sdtek1
     c                   eval      rpvenh = sdenh1
     c                   eval      rpvens = *blank
     c                   eval      rpvlda = sdldat
7.00 c                   if        sdldat = *loval
7.00 c                   eval      rpvlda = soldat
7.00 c                   endif
     c                   eval      rpvabu = *zero
     c                   eval      rpvabj = *zero
     c                   eval      rpvabs = *zero
     c                   eval      rpvkpr = sdkopr
     c                   eval      rpvkpb = *zero
     c                   eval      rpvaop = *zero
     c                   eval      rpvapp = *zero
     c                   eval      rpvafp = *zero
     c                   eval      rpvous = sdousr
     c                   eval      rpveus = sdeusr
     c                   eval      rpvoda = sdodat
     c                   eval      rpvoti = sdotim
     c                   eval      rpveda = sdedat
     c                   eval      rpveti = sdetim
     c                   write     rpvalur
7.00 c                   else
7.00 c                   eval      rpvlda = sdldat
7.00 c                   if        sdldat = *loval
7.00 c                   eval      rpvlda = soldat
7.00 c                   endif
7.00 c                   update    rpvalur
     c                   endif
     c                   eval      w_vare = sdvare
     c                   eval      w_lass = *zero
     c                   endif
     c                   endif
     c     sodtlp_key    reade     sodtlp
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Oppdatere RPVAPF   fra SIDTPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *
     c                   eval      sidtlp_pros = p_pros
     c                   eval      sidtlp_vare = *blank
     c     sidtlp_key    setll     sidtlp
     c     sidtlp_key    reade     sidtlp
      *
     c                   dow       not %eof(sidtlp)
     c                             and slproj = p_pros
      *
     c                   if        slvare <> *blank
      *
     c                   if        shnumm <> slnumm
     c                             or shsuff <> slsuff
     c                   eval      sihel1_aarr = slaarr
     c                   eval      sihel1_binr = slbinr
     c                   eval      sihel1_numm = slnumm
     c                   eval      sihel1_suff = slsuff
     c     sihel1_key    chain     sihel1
     c                   endif
      *
     c                   if        %found(sihel1)
     c                   eval      rpvalu_pros = slproj
     c                   eval      rpvalu_upro = slupro
     c                   eval      rpvalu_akti = slakti
     c                   eval      rpvalu_vare = slvare
     c                   eval      rpvalu_lass = *zero
     c     rpvalu_key    chain     rpvalu
     c                   if        not %found(rpvalu)
     c                   eval      rpvfir = slfirm
     c                   eval      rpvpro = slproj
     c                   eval      rpvupr = slupro
     c                   eval      rpvakt = slakti
     c                   eval      rpvprk = *zero
     c                   eval      rpvkto = slkont
     c                   eval      rpvvar = slvare
     c                   eval      rpvlas = *zero
     c                   eval      rpvtxt = sltek1
     c                   eval      rpvenh = slenh1
     c                   eval      rpvens = *blank
     c                   eval      rpvlda = shldat
     c                   eval      rpvabu = *zero
     c                   eval      rpvabj = *zero
     c                   eval      rpvabs = *zero
     c                   eval      rpvkpr = slkpny
     c                   eval      rpvkpb = *zero
     c                   eval      rpvaop = *zero
     c                   eval      rpvapp = *zero
     c                   eval      rpvafp = *zero
     c                   eval      rpvous = slousr
     c                   eval      rpveus = sleusr
     c                   eval      rpvoda = slodat
     c                   eval      rpvoti = slotim
     c                   eval      rpveda = sledat
     c                   eval      rpveti = sletim
     c                   write     rpvalur
     c                   endif
     c                   eval      w_vare = slvare
     c                   eval      w_lass = *zero
     c                   endif
     c                   endif
     c     sidtlp_key    reade     sidtlp
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_pros
      *
      * Key for posisjonering på prosjekt
      *
     c     rpvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rpvalu_pros
     c                   kfld                    rpvalu_upro
     c                   kfld                    rpvalu_akti
     c                   kfld                    rpvalu_vare
     c                   kfld                    rpvalu_lass
      *
     c     fodtlp_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlp_pros
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
     c                   kfld                    sohel1_suff
      *
     c     sodtlp_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtlp_pros
      *
     c     sihel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel1_aarr
     c                   kfld                    sihel1_binr
     c                   kfld                    sihel1_numm
     c                   kfld                    sihel1_suff
      *
     c     sidtlp_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sidtlp_pros
7.01  *
7.01 c     votyl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    votyl1_otyp
      *
     c                   eval      w_firm  = l_firm
      *
     c                   endsr
