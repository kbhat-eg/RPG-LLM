     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RM110R
      * Beskrivelse . . : Almenning, vedlikehold av kundeinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160303 KOB  Nytt program
      * V5.50 090607 KOB  Lagt inn vedlikehold av beløpsgrense almenning
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter for utvalgte OKO filer.
6.11  * V6.10 070510 BSA  Endret sporing av kundeendringer
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.xx  120516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
     frkunl3    if   e           k disk    rename(rkunpfr:rkunl3r)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     frksopf    if   e           k disk
     fapospf    if   e           k disk
     frmkul1    if   e           k disk    rename(rmkupfr:rmkul1r)
     frmkulu    uf a e           k disk    rename(rmkupfr:rmkulur)
      *
     frm110d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rkunlr_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
     d rkunlu_kund     s                   like(rkkund)
     d rkunl2_alfa     s                   like(rkalfa)
     d rkunl3_ponr     s                   like(rkponr)
     d rmkul1_kund     s                   like(rmkund)
     d rmkulu_kund     s                   like(rmkund)
     d apospf_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d b_open_sok      s              1    inz(*off)
      *
     d w_udat_iso      s               d   datfmt(*iso)
     d w_udat_alf      s              8
     d w_udat          s              8  0
     d w_valg          s              1  0
     d w_retk          s              1
     d w_4knr          s              6  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d s_stek          s             35
      *
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
     d w_navn          s                   like(rknavn)
     d w_pass          s              1  0
      *
     d p_stat          s              1
     d p_biln          s              6  0
     d p_rest          s             11  2
     d p_kund          s                   like(rkkund)
     d p_navn          s                   like(rknavn)
     d p_egnr          s              5  0
     d p_ebnr          s              5  0
     d p_alme          s              4  0
     d p_anav          s             30
      *
     d w_hipad         s             15a
      *
      * Definering av konstanter
      *
     d s_kund          s                   like(rkkund)
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F5=Skift mellom aktive/alle
      *
     c                   if        *inke = *on
     c                   if        w_pass  = 1
     c                   eval      w_pass  = 0
     c                   else
     c                   eval      w_pass  = 1
     c                   endif
     c                   exsr      forny
     c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kund <> 0 or
     c                             b2alfa <> *blank or
     c                             b2ponr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'A'
     c     rkunl2_key    setll     rkunl2
     c                   when      w_seqe = 'P'
     c     rkunl3_key    setll     rkunl3
     c                   other
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søk i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    setll     rkunl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på alfafelt
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      rkunl2_alfa = b2alfa
     c     rkunl2_key    setll     rkunl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på postnummer
      *
     c                   if        b2ponr <> 0
     c                   eval      w_seqe = 'P'
     c                   eval      rkunl3_ponr = b2ponr
     c     rkunl3_key    setll     rkunl3
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kund = 0
     c                   eval      b2alfa = *blank
     c                   eval      b2ponr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc3bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Behandle eiendommer
      *
     c                   if        b1valg = 7
     c                   eval      p_kund = b1kund
     c                   eval      p_navn = b1navn
     c                   exsr      beh_eiendom
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
      *
     c                   eval      w_kund = b1kund
     c                   call      'RK502R'
     c                   parm                    w_kund
     c                   parm                    w_valg
      *
     c                   eval      b2kund = *zeros
     c                   eval      b2alfa = *blank
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c3bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *on
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select rksfir,
     c+                         rkstek,
     c+                         rkskun
     c+                  from   rksopf
     c+                  where  rksfir = :w_firm and
     c+                         rkstek like :w_ste1
     c+                  order by rksfir, rkskun
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :rksfir,
     c+                        :rkstek,
     c+                        :rkskun
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'A'
     c                   read      rkunl2                                 15
     c                   when      w_seqe = 'P'
     c                   read      rkunl3                                 15
     c                   other
     c                   read      rkunl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      rkunl1_kund = rkskun
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        rkfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
      * Utelat passive kunder
      *
     c                   if        w_pass = 0 and
     c                             rkpass = 'J'
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1kund = rkkund
     c                   eval      b1navn = rknavn
     c                   eval      b1sted = rksted
     c                   eval      b1tlfn = rktlfn
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setgt     rkunl2
     c                   readp     rkunl2
     c                   when      w_seqe = 'P' and
     c                             %eof(rkunl3)
     c     rkunl3_key    setgt     rkunl3
     c                   readp     rkunl3
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setgt     rkunl1
     c                   readp     rkunl1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     rkunl2
     c                   when      w_seqe = 'P'
     c                   readp     rkunl3
     c                   other
     c                   readp     rkunl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rkfirm <> w_firm
     c                   leave
     c                   endif
      *
      * Utelat passive kunder
      *
     c                   if        w_pass = 0 and
     c                             rkpass = 'J'
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rkunl2_alfa = rkalfa
     c                   when      w_seqe = 'P'
     c                   eval      rkunl3_ponr = rkponr
     c                   other
     c                   eval      rkunl1_kund = rkkund
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setll     rkunl2
     c                   when      w_seqe = 'P' and
     c                             %eof(rkunl3)
     c     rkunl3_key    setll     rkunl3
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *       Vise eiendommer                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beh_eiendom   begsr
      *
     c                   call      'RM120R'
     c                   parm                    p_kund
     c                   parm                    p_navn
     c                   parm                    p_alme
     c                   parm                    p_anav
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
      *
     c                   eval      b1valg = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2BLD Behandle bilde for ny - endring - vise                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2bld        begsr
      *
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1
      *
     c                   if        %found
     c                   eval      c2kund = rkkund
     c                   eval      c2navn = rknavn
     c                   eval      c2emal = rkemal
     c                   eval      c2pers = rkpers
     c                   eval      c2tlfn = rktlfn
     c                   eval      c2tlfx = rktlfx
     c                   eval      c2gral = rkgral
      *
     c                   eval      rmkul1_kund = rkkund
     c     rmkul1_key    chain     rmkul1
      *
     c                   if        %found
      *
     c                   if        rmkfda = *loval
     c                   eval      c2kfda = 0
     c                   else
     c     *dmy          move      rmkfda        c2kfda
     c                   endif
      *
     c                   else
      *
     c                   eval      c2kfda = 0
     c                   endif
      *
     c                   else
      *
     c                   eval      c2pers = *blank
     c                   eval      c2tlfn = *blank
     c                   eval      c2tlfx = *blank
     c                   eval      c2emal = *blank
     c                   eval      c2gral = 0
      *
     c                   eval      c2kfda = 0
      *
     c                   if        *in30 = *on
     c                   endif
      *
     c                   endif
      *
     c     c2tagb        tag
      *
      * Posisjonere cursor
      *
     c                   if        *in32 = *off
     c                   move      *on           *in31
     c                   endif
      *
     c                   eval      wactli = 3
     c                   eval      wactpo = 9
     c                   exfmt     c2win
      *
     c     *inkc         cabeq     *on           c2tag1
     c     *inkl         cabeq     *on           c2tag1
     c     *in30         cabeq     *on           c2tag1
      *
      * Kontroll av skjermbildet
      *
      *
     c                   eval      rkunlu_kund = b1kund
     c     rkunlu_key    chain     rkunlur
      *
     c                   move      c2pers        rkpers
     c                   move      c2tlfn        rktlfn
     c                   move      c2tlfx        rktlfx
     c                   move      c2emal        rkemal
     c                   move      c2gral        rkgral
      *
     c                   if        %found
6.10 c                   time                    rkedat
6.10 c                   time                    rketim
6.10 c                   eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RM110R    '
     c                   update    rkunlur
     c                   endif
      *
      * Oppdater kundeinfo
      *
     c                   eval      rmkulu_kund = b1kund
     c     rmkulu_key    chain     rmkulur
      *
     c                   if        c2kfda = 0
     c                   eval      rmkfda = *loval
     c                   else
     c     *dmy          move      c2kfda        rmkfda
     c                   endif
      *
     c                   if        %found
     c                   update    rmkulur
     c                   else
     c                   eval      rmfirm = l_firm
     c                   eval      rmkund = rkkund
     c                   write     rmkulur
     c                   endif
      *
     c     c2tag1        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_alme
     c                   parm                    p_anav
      *
      * Key for oppslag på kunde
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      * Key for posisjonering på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på alfa
      *
     c     rkunl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl2_alfa
      *
      * Key for posisjonering på postnummer
      *
     c     rkunl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl3_ponr
      *
      * Key for oppdatering av kunderegister
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Key for les av kundeinformasjon
      *
     c     rmkul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmkul1_kund
      *
      * Key for oppdatering av kundeinformasjon
      *
     c     rmkulu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmkulu_kund
      *
      * Key for postnummer-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Henter dagens dato
      *
     c                   time                    w_udat_iso
     c     *iso0         move      w_udat_iso    w_udat_alf
     c                   move      w_udat_alf    w_udat
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      p_rest = 0
     c                   eval      w_pass = 0
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rkunl1_kund = 0
     c     rkunl1_key    setll     rkunl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
