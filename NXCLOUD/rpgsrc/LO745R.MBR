     h option(*nodebugio) datedit(*dmy) dftactgrp(*no)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LO745R
      * Beskrivelse . . : Automatisk overføring til leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato  Sign. Endringsbeskrivelse
      * --------- ------  --------------------------------------------------
      *    800    070222  ask  Nytt program basert på LO740R
8.01  *    800    100622  ask  Lagt inn kontroll mot sperret felt, mailsending
      *                        og nullstilling av kode fra LOTIST.
      *                        Byttet rekkefølgepå noen tester.
8.02  *    800    130622  ask  Lagt inn mailsending på ALLE med riktig
      *                        kode hvis de ikke har forsendelsesinfo.
8.03  *    800    201022  ask  Nytt valg fra leverandørditribusjon skal flagge
8.03  *                        loggkode 2, og gjøre sjekker tilsvarende EDI.
8.04  *    800    031122  ask  Lagt inn sending av kopi til RDS om det er aktuelt
8.05  *800        211122  ask  Endret nøkkelbegrep på eksdternt lager fra N(2 0) til A(20)
8.06  *800        201222  ask  Lagt inn loggkode 2 på bestilling selv om bestilling er sent på mail
      *                        Dette gjelder ved kopi til RDS for å håndtere EDI pakkseddel
8.07  *K000       100123  bsa  Send transportrequest hvis tilhørende ordre oppfyller
8.07  *K000                    krav til kjøring.
8.08  *K000       180123  bsa  Flagg om skaffetekst i transportlogg.
8.09  *800        180123  ask  Lagt inn sjekk på switc om kopi til eksternt lager
8.10  *K000       240123  bsa  Hvis vi ikke får treff i leverandørdistribusjon, sørg for at
8.10  *K000                    at alle verdier er blanke.
8.11  *K000       060223  bsa  Flagg om sjekk på loggkode i transportlogging
8.12  *K000       130525  bsa  Sjekk om dette er bestilling fra Blueridge, og oppdater
8.12  *K000                    bestillingshodet ihht mvakode på leverandøren.
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     flldilu    uf   e           k disk    rename(lldipfr:lldilur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     favall1    if   e           k disk    rename(avalpfr:avall1r)
8.07 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
8.12 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
      * Datastruktur
      *
     d                 ds
     d d_urec                        80
     d  d_firm                        3  0 overlay(d_urec)
     d  d_numm                        8  0 overlay(d_urec:4)
     d  d_suff                        2  0 overlay(d_urec:12)
     d  d_kode                        1    overlay(d_urec:14)
     d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:33)
     d  d_wsid                       10    overlay(d_urec:43)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
8.07 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d lldilu_ldor     s                   like(lpldor)
     d lldilu_lage     s                   like(lplage)
     d avall1_apgm     s                   like(avapgm)
8.07 d fohel1_numm     s                   like(fonumm)
8.07 d fohel1_suff     s                   like(fosuff)
8.12 d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable i parametre
      *
     d p_type          s              1
     d p_akod          s              2  0
     d p_aktk          s              2
     d p_luke          s              4  0
     d p_text          s             35
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_feil          s              1
     d w_stmp          s               z
     d w_stmp_alf      s             26
     d w_memb          s             10
     d w_lknr          s             10
     d w_numm_alf      s              6
     d w_leng          s              2  0
     d w_stat          s              1
     d w_parm          s             80
     d w_navn          s             10
     d w_osta          s              1
     d w_akko          s              1  0
     d w_lotk          s              1
8.02 d w_kode          s              1
8.01 d w_logk          s              1
     d w_tell          s              1  0
8.04 d w_rdsk          s              1
8.04 d w_lagd          s                   like(lolage)
8.04 d w_lmat          s                   like(lolmat)
8.05 d w_ekst          s             20
8.06 d w_wsid          s             10
8.07 d w_reqs          s              5
8.08 d w_skaf          s              1  0
8.11 d w_logg          s              1  0
      *
     d b_feil          s              1    inz(*off)
      *
8.07 d u_flat          s              1    inz(*on)
8.09 d rds_kop         s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_null          s              6    inz('000000')
8.07  *
8.07  * Definering av eksterne prg
8.07  *
8.07   dcl-s co402_verdi1  char(1);
8.07   dcl-s co402_verdi2  char(2048);
8.07   DCL-PR CO402R EXTPGM('CO402R');
8.07     p_filgrp            char(3)     const;
8.07     p_firm              packed(3:0) const;
8.07     p_lager             packed(2:0) const;
8.07     p_nokkel            char(50)    const;
8.07     p_verdi1            char(1);
8.07     p_verdi2            char(2048);
8.07   END-PR;
      *
     d sleep           pr            10u 0 extproc('sleep')
     d   Interval                    10u 0 value
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram - les bestillingsregister og finn aktuelle bestillinger for sending
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Les bestillinger
      *
     c                   eval      lohel1_numm = 0
     c                   eval      lohel1_suff = 0
     c     lohel1_key    setll     lohel1
      *
     c     les_best      tag
     c                   read      lohel1
     c                   if        %eof
     c                   goto      pgm_end
     c                   endif
      *
8.01 c                   eval      b_feil = *off
      *
      * Bestilling riktig status?
      *
8.01 c                   eval      w_akko = 0
       exec sql
             select vaoakk into :w_akko
              from votypf
                where vaofir = :w_firm and
                      vaotyp = :lootyp;

     c                   if        sqlcod < 0 or
     c                             w_akko <> 1
     c                   goto      les_best
     c                   endif
8.01  *
8.01  * Bestilling låst?
8.01  *
8.01 c                   if        lokspr > 0
8.01 c                   goto      les_best
8.01 c                   endif
      *
      * Bestilling med riktig kode i tilleggsregister?
      *
8.01 c                   eval      w_lotk = ' '
       exec sql
             select lotko1 into :w_lotk
              from lotist
                where lotfir = :w_firm and
                      lotnum = :lonumm and
                      lotsuf = :losuff;

     c                   if        sqlcod < 0 or
8.01 c                             sqlcod = 100 or
     c                             w_lotk <> p_type
     c                   goto      les_best
     c                   endif
      *
      * Bestilling allerede sendt?
      *
     c                   if        lobsen = 1
     c                   goto      les_best
     c                   endif
8.03  *
8.03  * Bestilling allerede sendt elektronisk?
8.03  *
8.03 c                   eval      w_logk = ' '
8.03   exec sql
8.03         select lmkode into :w_logk
8.03          from llogpf
8.01            where lmfirm = :w_firm and
8.03                  lmnumm = :lonumm and
8.03                  lmsuff = :losuff and
8.03                  lmkode = '2'
8.03              fetch first row only;
8.03
8.03 c                   if        sqlcod = 0
8.03 c                   goto      les_best
8.03 c                   endif
8.01  *
8.01  * Bestilling allerede sendt på mail?
8.01  *
8.01 c                   eval      w_logk = ' '
8.01   exec sql
8.01         select lmkode into :w_logk
8.01          from llogpf
8.01            where lmfirm = :w_firm and
8.01                  lmnumm = :lonumm and
8.01                  lmsuff = :losuff and
8.01                  lmkode = '5'
8.01              fetch first row only;
8.01
8.01 c                   if        sqlcod = 0
8.01 c                   goto      les_best
8.01 c                   endif
8.04  *
8.04  * Sjekk om kopi skal sendes til RDS
8.04  *
8.09 c                   if        rds_kop = *on
8.04 c                   exsr      sjk_rds_kop
8.09 c                   endif
      *
8.02  * Sjekk forsendelsesinfo - hvis ikke info,send på mail
      *
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      lldilr_lage = *blank
     c     lldilr_key    chain     lldilr
     c                   if        not %found
8.02 c                   exsr      send_mail
     c                   goto      les_best
     c                   endif
     c                   endif
8.03  *
8.03  * NGN_EDI - sett loggkode 2
8.03  *
8.03 c                   if        %trim(lpform) = 'NGN_EDI' and
8.03 c                             %trim(lptype) = 'NGN_EDI' and
8.03 c                             %trim(lpmott) = 'NGN_EDI'
8.03 c                   eval      w_kode = '2'
8.06 c                   eval      w_wsid = l_wsid
8.03 c                   exsr      overf_stat
8.03 c                   goto      les_best
8.03 c                   endif
8.03  *
      *
8.02  * Sjekk om sending pågår - overfør
8.02  *
     c                   exsr      sjekk_ovf
     c                   if        b_feil = *on
     c                   goto      les_best
     c                   endif
      *
     c                   exsr      send_best
     c                   if        b_feil = *on
     c                   goto      les_best
     c                   endif
      *
8.02 c                   eval      w_kode = '2'
8.06 c                   eval      w_wsid = l_wsid
     c                   exsr      overf_stat
      *
     c                   goto      les_best
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk om overføring allerede pågår for forsendelsestype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ovf     begsr
      *
     c                   eval      w_navn = *blanks
     c                   eval      w_osta = *blanks
     c                   eval      b_feil = *off
      * EDI via Logiq
     c                   if        %trim(lpform) = 'EDI' and
     c                             %trim(lptype) = 'FTP' and
     c                             %trim(lpmott) = 'NBS'
     c                   eval      w_navn = 'NBSEDISND'
     c                   endif
      * Webtrade
     c                   if        %trim(lpform) = 'WEBTRADE' and
     c                             %trim(lptype) = 'FTP'
     c                   eval      w_navn = 'JERNIABEST'
     c                   endif
      * FARVERINGEN
     c                   if        %trim(lpform) = 'FARVERING' and
     c                             %trim(lptype) = 'FTP'
     c                   eval      w_navn = 'FARVERING'
     c                   endif
      * EFONELFO / HEIDENREICH
     c                   if        %trim(lpform) = 'EFONELFO' and
     c                             %trim(lptype) = 'FTP'
     c                   eval      w_navn = 'HEIDENBEST'
     c                   endif
      * LØVENSKIOLD
     c                   if        %trim(lpform) = 'LOVENSKIOL' and
     c                             %trim(lptype) = 'FTP'
     c                   eval      w_navn = 'LOVENSBEST'
     c                   endif
      * NORGROS
     c                   if        %trim(lpform) = 'NORGROS' and
     c                             %trim(lptype) = 'FTP'
     c                   eval      w_navn = 'NBSEDISND'
     c                   endif
     c                   if        w_navn = '  '
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Overføring pågår ??
      *
     c                   eval      w_tell = 0
     c     ny1           tag
     c                   eval      w_osta = ' '
     c                   call      'AF712R'
     c                   parm                    w_navn
     c                   parm                    w_osta
     c                   if        w_osta <> *blanks
     c                   eval      w_tell = w_tell + 1
     c                   if        w_tell > 5
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
                         sleep(5);
     c                   goto      ny1
     c                   endif
      *
      * Overføring pågår alikevel - sjekk mot LWEDUT
      *
      *
     c                   eval      w_tell = 0
     c     ny2           tag
     c                   eval      w_osta = ' '
     c                   call      'AF713C'
     c                   parm                    w_osta
     c                   if        w_osta <> *blanks
     c                   eval      w_tell = w_tell + 1
     c                   if        w_tell > 5
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
                         sleep(5);
     c                   goto      ny2
     c                   endif
      *
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Send med riktig format og forsendelses metode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     send_best     begsr
      *
     c                   exsr      crt_memb
      *
      * EDI
      *
     c                   if        lpform = 'EDI      '
     c                   CALL      'LO760C'
     c                   parm                    w_memb
     c                   parm                    lonumm
     c                   parm                    losuff
      *
     c                   if        lptype = 'FTP      ' and
     c                             lpmott = 'NBS            '
     c                   CALL      'LO767C'
     c                   parm                    w_memb
     c                   parm                    w_feil
8.04  *
8.04 c                   if        w_rdsk = 'C'
8.04 c                   exsr      send_kopi
8.04 c                   endif
      *
      * Feilsjekk
      *
     c                   if        w_feil = 'L'
     c                   eval      b_feil = *on
     c                   goto      end_send
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * JERNIA/WEBTRADE
      *
     c                   if        lpform = 'WEBTRADE '
     c                   eval      p_akod = 0
     c                   eval      p_aktk = ' '
     c                   eval      p_luke = 0
     c                   CALL      'LO750C'
     c                   parm                    w_memb
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   parm                    lootyp
     c                   parm                    p_akod
     c                   parm                    p_aktk
     c                   parm                    p_luke
      *
     c                   if        lptype = 'FTP      '
     c                   move      lonumm        w_numm_alf
     c                   CALL      'LO752C'
     c                   parm                    w_numm_alf
     c                   parm                    w_memb
     c                   parm                    lootyp
     c                   parm                    w_feil
      *
      * Feilsjekk
      *
     c                   if        w_feil = '1'
     c                   eval      b_feil = *on
     c                   goto      end_send
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * FARVERINGEN
      *
     c                   if        lpform = 'FARVERING'
     c                   CALL      'LO770C'
     c                   parm                    w_memb
     c                   parm                    lonumm
     c                   parm                    losuff
      *
     c                   if        lptype = 'FTP      '
     c                   move      lonumm        w_numm_alf
     c                   eval      w_lknr = %subst(lplknr:1:5) + '     '
      *
     c                   CALL      'LO771C'
     c                   parm                    w_numm_alf
     c                   parm                    w_memb
     c                   parm                    w_lknr
     c                   parm                    w_feil
      *
      * Feilsjekk
      *
     c                   if        w_feil = '1'
     c                   eval      b_feil = *on
     c                   goto      end_send
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * EFONELFO / HEIDENREICH
      *
     c                   if        lpform = 'EFONELFO'
     c                   if        lptype = 'FTP      '
     c                   CALL      'LO775C'
     c                   parm                    w_memb
     c                   parm                    lonumm
     c                   parm                    losuff
      *
      * Feilsjekk
      *
     c                   if        w_feil = '1'
     c                   eval      b_feil = *on
     c                   goto      end_send
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * LØVENSKIOLD
      *
     c                   if        lpform = 'LOVENSKIOL'
     c                   if        lptype = 'FTP      '
     c                   CALL      'LO780C'
     c                   parm                    w_memb
     c                   parm                    lonumm
     c                   parm                    losuff
      *
      * Feilsjekk
      *
     c                   if        w_feil = '1'
     c                   eval      b_feil = *on
     c                   goto      end_send
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * NORGROS
      *
     c                   if        lpform = 'NORGROS   '
     c                   if        lptype = 'FTP      '
      *
     c                   CALL      'LO790C'
     c                   parm                    w_memb
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   parm                    p_text
      *
     c                   CALL      'LO763C'
     c                   parm                    w_memb
     c                   parm                    w_feil
      *
      * Feilsjekk
      *
     c                   if        w_feil = '1'
     c                   eval      b_feil = *on
     c                   goto      end_send
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * INTERNT FRA BESTILLING TIL ORDRE
      *
     c                   if        lpform = 'INTERN    '
     c                   if        lptype = 'INTERN   '
      *
      * Rediger intert kundenummer
      *
     c                   eval      lplknr = %trim(lplknr)
     c                   eval      w_leng = %len(%trim(lplknr))
     c                   if        w_leng < 6
     c                   eval      lplknr = %subst(c_null:1:6-w_leng) +
     c                                      %subst(lplknr:1:w_leng)
     c                   endif
      *
     c                   CALL      'NO124R'
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   parm                    lplknr
     c                   parm                    loavde
     c                   parm                    lolage
      *
      * Feilsjekk
      *
     c                   if        w_feil = '1'
     c                   eval      b_feil = *on
     c                   goto      end_send
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c     end_send      endsr
8.02  *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Sender mail til dem som ikke har elektroisk distribusjon
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     send_mail     begsr
8.02  *
8.02 c                   eval      w_stat = ' '
8.02  *
8.02 c                   call      'LO928C'
8.02 c                   parm                    lonumm
8.02 c                   parm                    losuff
8.02 c                   parm                    w_stat
8.02  *
8.02 c                   if        w_stat = ' '
8.06 c                   if        w_rdsk = 'C'
8.06 c                   exsr      send_kopi
8.02 c                   eval      w_kode = '2'
8.06 c                   eval      w_wsid = 'RDS EDI'
8.02 c                   exsr      overf_stat
8.06 c                   endif
8.06 c                   eval      w_kode = '5'
8.06 c                   eval      w_wsid = l_wsid
8.06 c                   exsr      overf_stat
8.02 c                   endif
8.02  *
8.02 c     end_mail      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kvittering av overført
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overf_stat    begsr
      *
      * Logger i loghistorikk
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_numm = lonumm
     c                   eval      d_suff = losuff
     c                   eval      d_kode = w_kode
     c                   eval      d_date = *loval
     c                   eval      d_time = *loval
     c                   eval      d_user = 'AUTO BEST'
8.06 c                   eval      d_wsid = w_wsid
     c                   eval      w_parm = d_urec
      *
      * Oppdaterer semndestatus på bestilling
      *
     c                   call      'LM900R'
     c                   parm                    w_stat
     c                   parm                    w_parm
8.12  *
8.12  * Hvis dette er en Blueridge bestilling, sjekk om den er utenlandsk og
8.12  * at mva koden på hodet er ihht denne.
      *
8.12 c                   if        p_type = 'B'
8.12 c                   eval      rlevl1_levr = loldor
8.12 c     rlevl1_key    chain     rlevl1
8.12 c                   if        %found and
8.12 c                             rlmkod > 0
8.12 c                   eval      lohelu_numm = lonumm
8.12 c                   eval      lohelu_suff = losuff
8.12 c     lohelu_key    chain     lohelur
8.12 c                   if        %found
8.12 c                   eval      lomkod = rlmkod
8.12 c                   update    lohelur
8.12 c                   endif
8.12 c                   endif
8.12 c                   endif
      *
     c                   eval      lohelu_numm = lonumm
     c                   eval      lohelu_suff = losuff
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lobsen = 1
     c                   time                    lobsda
     c                   eval      lobsav = 'AUTO BEST'
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
      * Oppdaterer logg på distribusjonsinformasjon
      *
     c                   eval      lldilu_ldor = lldilr_ldor
     c                   eval      lldilu_lage = lldilr_lage
     c     lldilu_key    chain     lldilu
     c                   if        %found
     c                   time                    lpsikl
     c                   time                    lpsida
     c                   eval      lpsibe = lonumm
     c                   update    lldilur
8.10 c                   else
8.10 c                   eval      lpform = *blanks
8.10 c                   eval      lptype = *blanks
8.10 c                   eval      lpmott = *blanks
     c                   endif
8.07  *
8.07  * Hvis flåtestyring, sjekk om vi skal sende transportrequest. NB Gjelder ikke
8.07  * ikke NGN_EDI, hvor disse går rett til transportformidler.
8.07  *
8.07 c                   if        u_flat = *on and
8.07 c                             loornr <> 0
8.07  *
8.07 c                   if        %trim(lpform) <> 'NGN_EDI' and
8.07 c                             %trim(lptype) <> 'NGN_EDI' and
8.07 c                             %trim(lpmott) <> 'NGN_EDI'
8.07  *
8.07 c                   eval      w_reqs = *blanks
8.08 c                   eval      w_skaf = 1
8.11 c                   eval      w_logg = 1
8.07 c                   call      'AP058R '
8.07 c                   parm                    fofirm
8.07 c                   parm                    loornr
8.07 c                   parm                    loorsu
8.08 c                   parm                    w_skaf
8.11 c                   parm                    w_logg
8.07 c                   parm                    w_reqs
8.07  *
8.07 c                   endif
8.07  *
8.07 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bygging av file-member
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_memb      begsr
      *
     c                   time                    w_stmp
      *
     c                   move      w_stmp        w_stmp_alf
     c                   eval      w_memb = 'W' +
     c                                      %subst(w_stmp_alf:12:2) +
     c                                      %subst(w_stmp_alf:15:2) +
     c                                      %subst(w_stmp_alf:18:2) +
     c                                      %subst(w_stmp_alf:21:3)
      *
     c                   endsr

8.04  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  // Subrutine for å sjekke om kopi skal sendes til eksternlager
8.04  // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04
8.04     begsr sjk_rds_kop;
8.04
8.04      w_lagd = 0;
8.04      w_lmat = 0;
8.05      w_ekst = ' ';
8.04
8.04  // Finn lager og leveringsmåte på ordre
8.04
8.04      if loornr > 0;
8.04       exec sql
8.04        select folage, folmat
8.04         into :w_lagd, :w_lmat
8.04        from fohepf
8.04         where fofirm = :w_firm and fonumm = :loornr and fosuff = :loorsu;
8.04      endif;
8.04
8.04
8.04  // Finn finn kobling til distribusjonslager
8.04
8.04      if w_lmat > 0 and
8.04         w_lagd > 0;
8.04       exec SQL
8.05        select rkrela into :w_ekst
8.04          from ra36st
8.04          where rkrfir = :w_firm
8.04            and rkrfsk = :w_lmat
8.04            and rkrsla = :w_lagd;
8.04      endif;
8.04
8.04      if sqlcod = 0 and
8.05         w_ekst <> ' ';
8.04       w_rdsk = 'C';
8.04      endif;
8.04
8.04
8.04     endsr;
8.04  *
8.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  * Sende kopi av bestilling til eksternlager
8.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  *
8.04 c     send_kopi     begsr
8.04  *
8.04 c                   exsr      crt_memb
8.04  *
8.04 c                   CALL      'LO768C'
8.04 c                   parm                    w_memb
8.04 c                   parm                    lonumm
8.04 c                   parm                    losuff
8.04  *
8.04 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_type
      *
      * Key for oppslag på leverandør distribusjon
      *
      *
      * Key for file : Firmastyrt validering
      *
     c     avall1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    avall1_apgm
      *
     c     lldilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      * Key for oppdatering av leverandør distribusjon
      *
     c     lldilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldilu_ldor
     c                   kfld                    lldilu_lage
      *
      * Key for les av bestilling
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppdatering på ordre/suffix
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
8.07  *
8.07  * Key for oppdatering på ordre/suffix
8.07  *
8.07 c     fohel1_key    klist
8.07 c                   kfld                    w_firm
8.07 c                   kfld                    fohel1_numm
8.07 c                   kfld                    fohel1_suff
8.12  *
8.12  * Key for file : Firmastyrt validering
8.12  *
8.12 c     rlevl1_key    klist
8.12 c                   kfld                    w_firm
8.12 c                   kfld                    rlevl1_levr
      *
     c                   eval      w_firm = l_firm
8.07  *
8.07  * Endring 8.08 - Koblet til ekstern flåtestyring
8.07  *
8.07   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.07                    co402_verdi1:co402_verdi2);
8.07   if co402_verdi1 = '1';
8.07     u_flat = *on;
8.07   endif;
8.09  *
8.09  * Automatisk kopi til ekstern lager?
8.09  *
8.09   CallP CO402R(l_filg:w_firm:0:'RDS_KOPI':
8.09                    co402_verdi1:co402_verdi2);
8.09   if co402_verdi1 = '1';
8.09     rds_kop = *on;
8.09   endif;
      *
     c                   endsr
