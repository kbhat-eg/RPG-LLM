     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : RY769R
      * Beskrivelse . . : Sletter priser for alle ukjente leverandører
      *
      * Programmet kjøres i forbindelse med overgang til automatisk
      * oppdatering fra NIB eller fra Nobb, og hensikten er å få fjernet
      * gamle priser
      *
      * 1. Slett alle leverandører som ikke er knyttet mot reskontrolev.
      * 2. Start innlesing fra NIB eller Nobb
      * 3. Kjør programmet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200307 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvprlu    uf   e           k disk    rename(jvprpfr:jvprlur)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
      * Definering av variable i nøkler
      *
     d jvprlu_vare     s                   like(jxvare)
     d jlevl1_ldor     s                   like(jlldor)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      jvprlu_vare = '10000000'
     c     jvprlu_key    setll     jvprlu
     c                   read      jvprlur
     c                   dow       not %eof
      *
     c                   eval      jlevl1_ldor = jxldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not %found
     c                   delete    jvprlur
     c                   endif
      *
     c                   read      jvprlur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
     c                   endsr
