     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV411R
      * Beskrivelse . . : Vare, sletting av utgåtte varer
      *
      *                   Sletter varer hvor varestatus er 4
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141003 HKO  Nytt program
      *       121105 HKO  Dersom det velges at varer som er overført EVR
      *                   ikke skal slettes, testes det også på at varen
      *                   ikke ligger i salgsstatistikken
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl9    if   e           k disk    rename(jvarpfr:jvarl9r)
     fjvarlc    if   e           k disk    rename(jvarpfr:jvarlcr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fsstal2    if   e           k disk    rename(sstapfr:sstal2r)
      *
      * Definering av parametre
      *
     d p_list          s             20
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        20
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldor                        6  0 overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_valg                        1  0 overlay(d_list:17)
      *
      * Definering av variable i nøkler
      *
     d jvarl9_ldor     s                   like(jvldor)
     d jvarl9_ogrp     s                   like(jvogrp)
     d jvarl9_hgrp     s                   like(jvhgrp)
     d jvarl9_ugrp     s                   like(jvugrp)
     d jvarlc_ogrp     s                   like(jvogrp)
     d jvarlc_hgrp     s                   like(jvhgrp)
     d jvarlc_ugrp     s                   like(jvugrp)
     d jvarl1_vare     s                   like(jvvare)
     d vvarl1_vare     s                   like(vvvare)
     d sstal2_vare     s                   like(sfvare)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   time                    w_udat
      *
      * Kaller subrutiner for sletting av varer
      *
     c                   select
     c                   when      d_ldor <> 0
     c                   exsr      slett_ldor
     c                   when      d_ogrp <> 0
     c                   exsr      slett_vgrp
     c                   other
     c                   exsr      slett_alle
     c                   endsl
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av varer for leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_ldor    begsr
      *
     c                   eval      jvarl9_ldor = d_ldor
     c                   eval      jvarl9_ogrp = d_ogrp
     c                   eval      jvarl9_hgrp = d_hgrp
     c                   eval      jvarl9_ugrp = d_ugrp
      *
     c                   select
     c                   when      d_ugrp <> 0
     c     jvarl9_key1   setll     jvarl9
     c     jvarl9_key1   reade     jvarl9
     c                   when      d_hgrp <> 0
     c     jvarl9_key2   setll     jvarl9
     c     jvarl9_key2   reade     jvarl9
     c                   when      d_ogrp <> 0
     c     jvarl9_key3   setll     jvarl9
     c     jvarl9_key3   reade     jvarl9
     c                   other
     c     jvarl9_key4   setll     jvarl9
     c     jvarl9_key4   reade     jvarl9
     c                   endsl
      *
     c                   dow       not %eof
      *
     c                   exsr      slett_vare
      *
     c                   select
     c                   when      d_ugrp <> 0
     c     jvarl9_key1   reade     jvarl9
     c                   when      d_hgrp <> 0
     c     jvarl9_key2   reade     jvarl9
     c                   when      d_ogrp <> 0
     c     jvarl9_key3   reade     jvarl9
     c                   other
     c     jvarl9_key4   reade     jvarl9
     c                   endsl
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av varer for vareguppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_vgrp    begsr
      *
     c                   eval      jvarlc_ogrp = d_ogrp
     c                   eval      jvarlc_hgrp = d_hgrp
     c                   eval      jvarlc_ugrp = d_ugrp
      *
     c                   select
     c                   when      d_ugrp <> 0
     c     jvarlc_key1   setll     jvarlc
     c     jvarlc_key1   reade     jvarlc
     c                   when      d_hgrp <> 0
     c     jvarlc_key2   setll     jvarlc
     c     jvarlc_key2   reade     jvarlc
     c                   other
     c     jvarlc_key3   setll     jvarlc
     c     jvarlc_key3   reade     jvarlc
     c                   endsl
      *
     c                   dow       not %eof
      *
     c                   exsr      slett_vare
      *
     c                   select
     c                   when      d_ugrp <> 0
     c     jvarlc_key1   reade     jvarlc
     c                   when      d_hgrp <> 0
     c     jvarlc_key2   reade     jvarlc
     c                   other
     c     jvarlc_key3   reade     jvarlc
     c                   endsl
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av alle varer med statuskode 4
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_alle    begsr
      *
     c                   eval      jvarl1_vare = *blank
      *
     c     jvarl1_key    setll     jvarl1
     c                   read      jvarl1
     c                   dow       not %eof
      *
     c                   exsr      slett_vare
      *
     c                   read      jvarl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_vare    begsr
      *
     c                   if        jvstat <> 4
     c                   leavesr
     c                   endif
      *
     c                   if        d_valg = 1
     c                   eval      vvarl1_vare = jvvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   eval      sstal2_vare = jvvare
     c     sstal2_key    chain     sstal2
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV400R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på vare m/leverandør
      *
     c     jvarl9_key1   klist
     c                   kfld                    jvarl9_ldor
     c                   kfld                    jvarl9_ogrp
     c                   kfld                    jvarl9_hgrp
     c                   kfld                    jvarl9_ugrp
      *
     c     jvarl9_key2   klist
     c                   kfld                    jvarl9_ldor
     c                   kfld                    jvarl9_ogrp
     c                   kfld                    jvarl9_hgrp
      *
     c     jvarl9_key3   klist
     c                   kfld                    jvarl9_ldor
     c                   kfld                    jvarl9_ogrp
      *
     c     jvarl9_key4   klist
     c                   kfld                    jvarl9_ldor
      *
      * Key for les på vare m/varegruppe
      *
     c     jvarlc_key1   klist
     c                   kfld                    jvarlc_ogrp
     c                   kfld                    jvarlc_hgrp
     c                   kfld                    jvarlc_ugrp
      *
     c     jvarlc_key2   klist
     c                   kfld                    jvarlc_ogrp
     c                   kfld                    jvarlc_hgrp
      *
     c     jvarlc_key3   klist
     c                   kfld                    jvarlc_ogrp
      *
      * Key for les på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare (EVR)
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på salgsstatistikk
      *
     c     sstal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal2_vare
      *
      *
      * Henter parametre og firma
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
