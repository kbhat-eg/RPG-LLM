     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD751R
      * Beskrivelse . . : Varesortiment-linje, oppretter sortiment
      *
      *                   Leser vareregister og oppretter totalsortiment
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151101 HKO  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  051110 bof  Utvidet med lager på sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl9    if   e           k disk    rename(vvarpfr:vvarl9r)
      *
     fvshelu    uf a e           k disk    rename(vshepfr:vshelur)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
      *
      * Definering av datastruktur - parameterliste inn
      *
     d                 ds
     d d_list                        48
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_vsor                       10    overlay(d_list:4)
     d  d_besk                       35    overlay(d_list:14)
      *
      * Definering av parametre
      *
     d p_list          s             48
      *
      * Definering av variable i nøkler
      *
     d vshelu_vsor     s                   like(vcvsor)
     d vsdtlu_vsor     s                   like(vdvsor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vdfirm)
     d w_gvar          s                   like(vvvare)
     d w_vare          s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Oppretter/oppdaterer sortiment-hode
      *
6.20 c                   clear                   vshelur
     c                   eval      vshelu_vsor = d_vsor
     c     vshelu_key    chain     vshelur                            90
     c                   eval      vcvsor = d_vsor
6.20 c                   eval      vclage = 0
     c                   eval      vcbesk = d_besk
     c                   eval      vceusr = l_user
     c                   time                    vcedat
     c                   time                    vcetim
     c                   if        *in90 = *off
     c                   update    vshelur
     c                   else
     c                   eval      vcfirm = w_firm
6.10 c                   time                    vcodat
6.10 c                   time                    vcotim
6.10 c                   eval      vcousr = l_user
     c                   write     vshelur
     c                   endif
      *
      * Sletter eksisterende sortiment-linje
      *
     c                   eval      vsdtlu_vsor = d_vsor
     c     vsdtlu_key    setll     vsdtlur
     c     vsdtlu_key    reade     vsdtlur                                90
     c                   dow       *in90 = *off
     c                   delete    vsdtlur
     c     vsdtlu_key    reade     vsdtlur                                90
     c                   enddo
      *
      * Leser vareregister, og oppretter sortiment
      * Dersom varen har byttet varernummer via matching, opprettes nytt
      * varenummer i sortimentet
      *
     c     vvarl9_key    setll     vvarl9
     c     vvarl9_key    reade     vvarl9                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vvvare < '10000000'
     c                   eval      w_gvar = vvvare
     c                   eval      w_vare = *blank
     c                   call      'FO711C'
     c                   parm                    w_firm
     c                   parm                    w_gvar
     c                   parm                    w_vare
     c                   if        w_vare <> *blank
     c                   eval      vvvare = w_vare
     c                   endif
     c                   endif
      *
     c                   eval      vdfirm = w_firm
     c                   eval      vdvsor = d_vsor
6.20 c                   eval      vdlage = 0
     c                   eval      vdldor = vvldor
     c                   eval      vdogrp = vvogrp
     c                   eval      vdhgrp = vvhgrp
     c                   eval      vdugrp = vvugrp
     c                   eval      vdmodn = vvnmod
     c                   eval      vdvare = vvvare
      *
     c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
     c                   eval      vdeusr = l_user
      *
     c                   write     vsdtlur
      *
     c     vvarl9_key    reade     vvarl9                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på vare
      *
     c     vvarl9_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av varesortiment-hode
      *
     c     vshelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshelu_vsor
      *
      * Key for oppdatering av varesortiment-linje
      *
     c     vsdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtlu_vsor
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
