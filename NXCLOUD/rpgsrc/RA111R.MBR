     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA111R
      * Beskrivelse . . : Kundekategorier, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
      * V5.00 120702 KOB  Lagt inn vedlikehold av kreditgrense
T5.00 * V5.00 150702 HFL  Mulighet for F4 på kostnadsbærer.
T5.00 * V5.00 130902 HFL  Prisgruppe tatt bort, ikke i bruk noe sted.
T5.00 * R5.00 220802 HFL  Firma i parameter til RH500R
T5.00 * R5.20 071103 KOB  Lagt inn ny kreditgrense for butikksalg
T5.00 * R5.40 260505 KOB  Lagt inn nye defaultverdier for kategori
T5.00 * R5.41 070905 KOB  Lagt inn nye felt for krav til registrering
T5.00 * R5.41 190506 KOB  Lagt inn blanking av tekstfelt
6.20  *       111113 NHO  Kan ikke slette kundekategori dersom den
6.20  *                   ligger i kunderegisteret
6.20  *                   *in40 styrer feilmelding
6.30  *       110515 bof  Endret fax til mobilnr (gjenbruk av felt RAKFAX)
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  020316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *  6.30 030718 bsa  Lagt inn felt for kundegruppe.
7.02  *  6.30 240918 bsa  Lagt inn felt for krav til prosjekt.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 fra32l1    if   e           k disk    rename(ra32pfr:ra32l1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     fra11l2    if   e           k disk    rename(ra11pfr:ra11l2r)
     fra11lr    if   e           k disk    rename(ra11pfr:ra11lrr)
     fra11lu    uf a e           k disk    rename(ra11pfr:ra11lur)
      *
     fra111d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
7.01 d ra32l1_fkod     s                   like(refkod)
     d ra11l1_kkod     s                   like(rakkod)
     d ra11l2_ktxt     s                   like(raktxt)
     d ra11lr_kkod     s                   like(rakkod)
     d ra11lu_kkod     s                   like(rakkod)
      *
      * Definering av variable
      *
6.20 d rkkatg          s              4  0
6.20 d rkfirm          s              3  0
6.20 d w_kkod          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rakfir)
      *
     d p_stat          s              1
     d p_kont          s                   like(rakkto)
     d p_spes          s                   like(raksps)
     d p_avde          s                   like(rakavd)
     d p_text          s             30
     d p_tx20          s             20
     d p_bkod          s              1  0
     d p_ckod          s              2  0
7.01 d p_kugr          s             10
      *
     d s_kkod          s                   like(rakkod)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kkod <> 0 or
     c                             b2ktxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra11l2_ktxt = b1ktxt
     c     ra11l2_key    setll     ra11l2
     c                   other
     c                   eval      ra11l1_kkod = b1kkod
     c     ra11l1_key    setll     ra11l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kundekategori
      *
     c                   if        b2kkod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ra11l1_kkod = b2kkod
     c     ra11l1_key    setll     ra11l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2ktxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra11l2_ktxt = b2ktxt
     c     ra11l2_key    setll     ra11l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kkod = 0
     c                   eval      b2ktxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kkod = b1kkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kkod = b1kkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kkod = b1kkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kkod = b1kkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1kkod = b2kkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1kkod = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra11lr_kkod = c1kkod
     c     ra11lr_key    chain     ra11lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1kkod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kkod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra11lr_kkod = c1kkod
     c     ra11lr_key    chain     ra11lr
      *
     c                   if        %found
     c                   eval      c2ktxt = raktxt
     c                   eval      c2kkto = rakkto
     c                   eval      c2ksps = raksps
     c                   eval      c2kavd = rakavd
     c                   eval      c2kkat = rakkat
     c                   eval      c2kavp = rakavp
     c                   eval      c2kgrn = rakgrn
     c                   eval      c2bgrn = rabgrn
     c                   eval      c2kbeb = rakbeb
     c                   eval      c2kbem = rakbem
     c                   eval      c2kppa = rakppa
     c                   eval      c2knto = raknto
     c                   eval      c2ksam = raksam
     c                   eval      c2ksek = raksek
     c                   eval      c2kbok = rakbok
     c                   eval      c2korg = rakorg
     c                   eval      c2kpnr = rakpnr
7.01 c                   eval      c2kugr = rakugr
7.02 c                   eval      c2kkpr = rakkpr
      *
     c                   eval      c2kkpe = rakkpe
     c                   eval      c2ktlf = raktlf
     c                   eval      c2kfax = rakfax
     c                   eval      c2kema = rakema
     c                   eval      c2btxt = *blank
     c                   eval      c2ctxt = *blank
      *
     c                   eval      c2eusr = rakusr
     c     *dmy          move      rakdat        c2edat
      *
      * Hent betalingsmåte
      *
     c                   if        rakbem <> *zero
     c                   move      rakbem        p_bkod
     c                   call      'RS702R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2btxt
     c                   endif
      *
      * Hent betalingsbetingelse
      *
     c                   if        rakbeb <> *zero
     c                   move      rakbeb        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        c2ctxt
     c                   endif
      *
     c                   else
      *
     c                   eval      c2ktxt = *blank
     c                   eval      c2kkto = *zeros
     c                   eval      c2ksps = *zeros
     c                   eval      c2kavd = *zeros
     c                   eval      c2kkat = *blank
     c                   eval      c2kavp = *zeros
     c                   eval      c2kgrn = *zeros
     c                   eval      c2bgrn = *zeros
     c                   eval      c2kbeb = *zeros
     c                   eval      c2kbem = *zeros
     c                   eval      c2kppa = *zeros
     c                   eval      c2knto = *zeros
     c                   eval      c2ksam = *zeros
     c                   eval      c2ksek = *zeros
7.01 c                   eval      c2kugr = *blanks
7.02 c                   eval      c2kkpr = *blanks
     c                   eval      c2kbok = *zeros
     c                   eval      c2korg = *blank
     c                   eval      c2kpnr = *blank
     c                   eval      c2kkpe = *blank
     c                   eval      c2ktlf = *blank
     c                   eval      c2kfax = *blank
     c                   eval      c2kema = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1kkod = k1kkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      *
      * Call program for avdeling
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2KAVD'                                         .
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_avde                                      .
     c                   parm                    p_tx20                                      .
      *                                                                 .
     c                   if        p_avde <> *zero                                           .
     c                   eval      c2kavd = p_avde                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *
      * Call program for konto
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2KKTO'                                         .
      *                                                                 .
     c                   call      'RH500R'                                      konto       .
T5.00c                   parm                    w_firm                           info       .
     c                   parm                    p_kont                           info       .
     c                   parm                    p_spes                           info       .
     c                   parm                    p_avde                           info       .
     c                   parm                    p_text                           info       .
      *                                                                 .
     c                   if        p_kont <> *zero                                           .
     c                   eval      c2kkto = p_kont                                           .
     c                   eval      c2ksps = p_spes                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   if        c2kavd = *zero                                            .
     c                   eval      c2kavd = p_avde                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
T5.00 * Call program for kostnadsbærer
      *
T5.00c                   if        *inkd = *on and                                       .....
T5.00c                             w_afld = 'C2KSPS'                                         .
T5.00c                   call      'RA528R'                                                  .
T5.00c                   parm                    w_firm                                      .
T5.00c                   parm                    p_spes                                      .
T5.00c                   parm                    p_tx20                                      .
      *                                                                 .
T5.00c                   if        p_spes <> *zero                                           .
T5.00c                   eval      c2ksps = p_spes                                           .
T5.00c                   endif                                                               .
      *                                                                 .
T5.00c                   goto      c2taga                                                    .
T5.00c                   endif                                                               .
      *
      * Call program for BETALINGSMÅTE
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2KBEM'
     c                   call      'RA502R'
     c                   parm                    w_firm
     c                   parm                    p_bkod
     c                   parm                    p_tx20
      *
     c                   if        p_bkod <> *zero
     c                   move      p_bkod        c2kbem
     c                   move      p_tx20        c2btxt
     c                   endif
      *
     c                   goto      c2taga
     c                   endif
      *
      * Call program for BETALINGSBETINGELSE
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2KBEB'
     c                   call      'RA503R'
     c                   parm                    w_firm
     c                   parm                    p_ckod
     c                   parm                    p_tx20
      *
     c                   if        p_ckod <> *zero
     c                   move      p_ckod        c2kbeb
     c                   move      p_tx20        c2ctxt
     c                   endif
      *
     c                   goto      c2taga
     c                   endif
7.01  *
7.01  * Call program for Kundegruppe
7.01  *
7.01 c                   if        *inkd = *on and
7.01 c                             w_afld = 'C2KUGR'
7.01 c                   call      'RA535R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    p_kugr
7.01 c                   parm                    p_tx20
7.01  *
7.01 c                   if        p_kugr <> *blanks
7.01 c                   eval      c2kugr = p_kugr
7.01 c                   endif
7.01  *
7.01 c                   goto      c2taga
7.01 c                   endif
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2ktxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Sjekke konto/avd/bærer
      *
     c                   if        c2kkto <> *zero or
     c                             c2ksps <> *zero or
     c                             c2kavd <> *zero
      *
     c                   move      c2kkto        p_kont
     c                   move      c2ksps        p_spes
     c                   move      c2kavd        p_avde
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
      * Avdeling finnes ikke
      *
     c                   if        p_stat = 'A'
     c                   move      *on           *in34
     c                   goto      c2taga
     c                   endif
      *
      * Konto finnes ikke
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in35
     c                   goto      c2taga
     c                   endif
      *
      * Er konto sperret?
      *
     c                   if        p_stat = 'S'
     c                   move      *on           *in36
     c                   goto      c2taga
     c                   endif
      *
      * Hvis avdeling er utfylt, må konto også utfylles
      *
     c                   if        p_stat = 'M'
     c                   move      *on           *in33
     c                   goto      c2taga
     c                   endif
      *
      * Hvis kostnadsbærer er utfylt, må konto også utfylles
      *
     c                   if        p_stat = 'K'
     c                   move      *on           *in37
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Sjekke om betalingsmåte er gyldig
      *
     c                   if        c2kbem <> *zero
     c                   move      c2kbem        p_bkod
     c                   call      'RS702R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in39
     c                   goto      c2taga
     c                   else
     c                   move      p_tx20        c2btxt
     c                   endif
     c                   endif
      *
      * Sjekke om betalingsbetingelse er gyldig
      *
     c                   if        c2kbeb <> *zero
     c                   move      c2kbeb        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in38
     c                   goto      c2taga
     c                   else
     c                   move      p_tx20        c2ctxt
     c                   endif
     c                   endif
7.01  *
7.01  * Sjekke om kundegruppe er gyldig
7.01  *
7.01 c                   if        c2kugr <> *blanks
7.01 c                   eval      ra32l1_fkod = c2kugr
7.01 c     ra32l1_key    chain     ra32l1
7.01 c                   if        not %found
7.01 c                   eval      *in40 = *on
7.01 c                   goto      c2taga
7.01 c                   endif
7.01 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra11lur
      *
     c                   eval      ra11lu_kkod = c1kkod
     c     ra11lu_key    chain     ra11lur
      *
     c                   eval      raktxt = c2ktxt
     c                   eval      rakkto = c2kkto
     c                   eval      raksps = c2ksps
     c                   eval      rakavd = c2kavd
     c                   eval      rakkat = c2kkat
     c                   eval      rakavp = c2kavp
     c                   eval      rakgrn = c2kgrn
     c                   eval      rabgrn = c2bgrn
     c                   eval      rakbeb = c2kbeb
     c                   eval      rakbem = c2kbem
     c                   eval      rakppa = c2kppa
     c                   eval      raknto = c2knto
     c                   eval      raksam = c2ksam
     c                   eval      raksek = c2ksek
7.01 c                   eval      rakugr = c2kugr
7.02 c                   eval      rakkpr = c2kkpr
     c                   eval      rakbok = c2kbok
     c                   eval      rakorg = c2korg
     c                   eval      rakpnr = c2kpnr
     c                   eval      rakkpe = c2kkpe
     c                   eval      raktlf = c2ktlf
     c                   eval      rakfax = c2kfax
     c                   eval      rakema = c2kema
      *
     c                   time                    rakdat
     c                   time                    raktim
     c                   eval      rakusr = l_user
      *
     c                   if        %found
     c                   update    ra11lur
     c                   else
     c                   eval      rakfir = w_firm
     c                   eval      rakkod = c1kkod
     c                   write     ra11lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ktxt = raktxt
     c                   eval      b1kkat = rakkat
     c                   eval      b1kavd = rakavd
     c                   eval      b1kkto = rakkto
     c                   eval      b1ksps = raksps
     c                   eval      b1kavp = rakavp
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra11lr_kkod = d1kkod
     c     ra11lr_key    chain     ra11lr
      *
6.20 c     d1tagb        tag
6.20  *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
6.20  * Sjekker om kundekategori finnes i kundefilen
6.20 c                   eval      w_kkod = d1kkod
6.20 c                   exsr      les_hist
6.20 c                   if        *in40 = '1'
6.20 c                   exsr      xc1msg
6.20 c                   eval      *in40 = '0'
6.20 c                   if        b_anul = *on
6.20 c                   goto      d1tagb
6.20 c                   endif
6.20 c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra11lu_kkod = d1kkod
     c     ra11lu_key    chain     ra11lur
      *
     c                   delete    ra11lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kkod = k1kkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra11lr_kkod = k1kkod
     c     ra11lr_key    chain     ra11lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1kkod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra11lr_kkod = k1kkod
     c     ra11lr_key    chain     ra11lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kkod = s_kkod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra11l2
     c                   other
     c                   read      ra11l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rakfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra11l2_ktxt = raktxt
     c                   other
     c                   eval      ra11l1_kkod = rakkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kkod = rakkod
     c                   eval      b1ktxt = raktxt
     c                   eval      b1kkat = rakkat
     c                   eval      b1kavd = rakavd
     c                   eval      b1kkto = rakkto
     c                   eval      b1ksps = raksps
     c                   eval      b1kavp = rakavp
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra11l2_key    setgt     ra11l2
     c                   readp     ra11l2
     c                   other
     c     ra11l1_key    setgt     ra11l1
     c                   readp     ra11l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra11l2
     c                   other
     c                   readp     ra11l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rakfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra11l2_ktxt = raktxt
     c                   other
     c                   eval      ra11l1_kkod = rakkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra11l2_key    setll     ra11l2
     c                   other
     c     ra11l1_key    setll     ra11l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra11l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l2_ktxt
      *
      * Key for les på kundekategori
      *
     c     ra11lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11lr_kkod
      *
      * Key for oppdatering av kundekategori
      *
     c     ra11lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11lu_kkod
7.01  *
7.01  * Key for posisjonering på kundegruppe
7.01  *
7.01 c     ra32l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    ra32l1_fkod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra11l1_kkod = 0
     c     ra11l1_key    setll     ra11l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *-----------------------------------------------------------------------
6.20  ******************************************************
6.20  * Start endring, bruker sql for å danne riktig nøkkel
6.20  ******************************************************
     c     les_hist      begsr
      *
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select rkfirm,
     c+                         rkkatg
     c+                  from   rkunpf
     c+                     where rkfirm = :w_firm and
     c+                           rkkatg = :w_kkod
      *
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c     fetch_vare    tag
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :rkfirm,
     c+                        :rkkatg
     c/end-exec
      *
      *
      * Tester på firmakode/valgt kundekat
     c                   if        rkfirm <> l_firm or
     c                             rkkatg <> d1kkod
     c                   goto      slutt1
     c                   endif
      * Tester på firmakode/valgt kundekat
     c                   if        rkfirm = l_firm and
     c                             rkkatg <> d1kkod
     c                   goto      slutt1
     c                   endif
      *
      * Gir feilmelding dersom finnes i kunderegister
     c                   if        rkfirm = l_firm and
     c                             rkkatg <> d1kkod
     c                   goto      fetch_vare
     c                   else
     c                   eval      *in40 = '1'
     c                   goto      slutt1
     c                   endif
      *
     c     slutt1        tag
      *
     c                   endsr
6.20  * Slutt endring
      *-----------------------------------------------------------------------
