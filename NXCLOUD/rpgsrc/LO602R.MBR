      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO602R                                       *
      *  Beskrivelse . . : Utskrift/Utplukk fra bestillings-register    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *  6.10      180609  Oppdatert med sporingsinformasjon       BSA
6.nu  *  6.30      120614  Gjennomgang mtp. nummerutvidelse        bof  *
6.01  *  6.30      100919  Rettet feil i msgf ref                  bof  *
7.00  *  K00       220420  Utvidet skjermbile                      bsa  *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     FLO602D    CF   E             WORKSTN
      *
      *
      *
      *
     FVOTYL1    IF   E           K DISK
     F                                     RENAME(VOTYPFR:VOTYL1R)
      *
     FLOHELU    UF   E           K DISK
     F                                     RENAME(LOHEPFR:LOHELUR)
     FLOHEL1    IF   E           K DISK
     F                                     RENAME(LOHEPFR:LOHEL1R)
     FLOHELO    IF   E           K DISK
     F                                     RENAME(LOHEPFR:LOHELOR)
     FLPLOLU    UF A E           K DISK
     F                                     RENAME(LPLOPFR:LPLOLUR)
      *
      *
     D                 DS
6.nu D  TILKEY                 1     15
     D  TILFIR                 1      3  0
     D  TILOTY                 4      5
6.nu D  TILNUM                 6     13  0
6.nu D  TILSUF                14     15  0
     D                 DS
6.nu D  LESKEY                 1     15
     D  LESFIR                 1      3  0
     D  LESOTY                 4      5
6.nu D  LESNUM                 6     13  0
6.nu D  LESSUF                14     15  0
      *
     D                UDS
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D0 Behandler inntasting av ordretyper samt utvalgsmetode       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     D0TAGA        TAG
      *
      *  Send Alt
      *
     C                   WRITE     D0BLD
      *
      *  Bare data
      *
     C     D0TAGB        TAG
     C                   EXFMT     D0BLD
      *
      *  F3=Avslutt program
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      *  Sjekk,  finnes ordretype(fra) i ORDRETYPE-REGISTER
      *
     C                   MOVE      D0FOTY        WWOTYP
     C                   MOVE      D0FOTY        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   GOTO      D0TAGB
     C                   END
      *
      *  Sjekk,  finnes ordretype(til) i ORDRETYPE-REGISTER
      *
     C     D0TOTY        IFNE      *BLANK
     C                   MOVE      D0TOTY        WWOTYP
     C                   MOVE      D0TOTY        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN32
     C                   GOTO      D0TAGB
     C                   ENDIF
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler spesialhandtering av noen ordretyper, samt valg      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     D0TAG5        TAG
      *
      *  Ved akkumulator-kode 0 og 1 skal subrutine hentes
      *  for å få tak i riktig valg (BESTFORSLAG har 0) (BEST har 0)
      *
     C     VAOAKK        IFEQ      0                                            BEST.FORSLAG
     C                   EXSR      XE2WIN
     C     *INKC         CABEQ     *ON           D0TAGA
     C                   END
      *
     C     VAOAKK        IFEQ      1                                            BESTILLING
     C                   EXSR      XE3WIN
     C     *INKC         CABEQ     *ON           D0TAGA
     C                   END
      *
      *  Finner utvalgsmetode og hopper til riktig subrutine
      *
     C     D0SVAR        CABEQ     1             D1TAGA                         FRA/TIL
     C     D0SVAR        CABEQ     2             D2TAGA                         DATO SELEKSJON
     C     D0SVAR        CABEQ     3             D3TAGA                         TILFELDIG
      *
     C                   GOTO      D0TAGB                                       FEIL
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandler plukking av bestillingsnummer fra/til             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     D1TAGA        TAG
      *
      *  Send Alt
      *
     C                   WRITE     D1BLD
      *
      *  Bare data
      *
     C     D1TAGB        TAG
     C                   EXFMT     D1BLD
      *
      *  F3=Avslutt program
      *
     C     *INKC         CABEQ     *ON           D0TAGA
      *
      *  Sjekk,  Bestillingsnummer fra må være utfylt
      *
     C     D1FONR        IFEQ      *ZERO
     C                   MOVE      *ON           *IN33
     C                   GOTO      D1TAGB
     C                   ENDIF
      *
      *  Sjekk,  Bestillingsnummer til må være større enn bestillngsnummer fra
      *          eller bestillingsnummer til må være blank
      *
     C     D1TONR        IFNE      *ZERO
     C     D1TONR        IFLT      D1FONR
     C                   MOVE      *ON           *IN34
     C                   GOTO      D1TAGB
     C                   ENDIF
     C                   ENDIF
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler utplukk av bestilling fra/til                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      D0FOTY        HOTOTY
     C                   MOVE      D1FONR        HOTNUM
     C                   MOVE      *ZERO         HOTSUF
     C     HOTKEY        SETLL     LOHELO
      *
     C                   MOVE      D0FOTY        TILOTY
     C                   MOVE      D1FONR        TILNUM
     C                   MOVE      99            TILSUF
      *
     C     D1TONR        IFNE      *ZERO
     C                   MOVE      D1TONR        TILNUM
     C                   END
      *
      *  Starter utplukk av bestillingsnummer
      *
     C     D1TAG1        TAG
     C                   READ      LOHELO                                 90
     C     *IN90         CABEQ     *ON           XSLUTT
      *
     C                   MOVE      LOFIRM        LESFIR
     C                   MOVE      LOOTYP        LESOTY
     C                   MOVE      LONUMM        LESNUM
     C                   MOVE      LOSUFF        LESSUF
     C     LESKEY        CABGT     TILKEY        XSLUTT
      *
     C                   EXSR      OPPORD
      *
     C                   GOTO      D1TAG1
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D2 Behandler plukking av bestillingsnummer Datoseleksjon       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     D2TAGA        TAG
      *
      *  Send Alt
      *
     C                   WRITE     D2BLD
      *
      *  Bare data
      *
     C     D2TAGB        TAG
     C                   EXFMT     D2BLD
      *
      *  F3=Avslutt program
      *
     C     *INKC         CABEQ     *ON           D0TAGA
      *
      *  Er t.o.m. dato en gyldig dato
      *
     C     D2BDAT        IFNE      *ZERO
      *
     c     *dmy          test(d)                 d2bdat                 32
     c                   if        *in32 = *on
     c                   goto      d2tagb
     c                   endif
      *
      *  Snu dato før test
      *
     c     *dmy          move      d2bdat        wwbdat
      *
     C                   ENDIF
      *
      *  Kontroll om fra
      *  er større enn til
      *  dersom til er oppgitt
      *
     C     D2TONR        IFNE      *ZERO
     C     D2FONR        IFGT      D2TONR
     C                   MOVE      *ON           *IN33
     C                   GOTO      D2TAGB
     C                   ENDIF
     C                   ENDIF
      *
      *  Dersom til ikke er
      *  oppgitt, legges fra
      *  ut i til
      *
     C     D2TONR        IFEQ      *ZERO
     C                   Z-ADD     D2FONR        D2TONR
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler plukking av bestillingsnummer ytfra Datoseleksjon    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C                   MOVE      D0FOTY        HOTOTY
     C                   MOVE      *ZERO         HOTNUM
     C                   MOVE      *ZERO         HOTSUF
     C     HOTKEY        SETLL     LOHELO
      *
      *  Starter utplukk av bestillingsnummer
      *
     C     D2TAG1        TAG
     C                   READ      LOHELO                                 90
     C     *IN90         CABEQ     *ON           XSLUTT
     C     LOOTYP        CABNE     HOTOTY        XSLUTT
      *
      *  Sjekk på dato dersom oppgitt
      *
     C     WWBDAT        IFne      *loval
     C     LOBDAT        IFGT      WWBDAT
     C                   GOTO      D2TAG1
     C                   ENDIF
     C                   ENDIF
      *
      *  Sjekk på beløpsgrense dersom oppgitt
      *
     C     D2BELG        IFGT      *ZERO
     C     LOTOTI        SUB       LOTOTR        WWBELO
      *
     C     D2BELK        IFNE      'U'
     C     WWBELO        IFLT      D2BELG
     C                   GOTO      D2TAG1
     C                   ENDIF
     C                   ENDIF
      *
     C     D2BELK        IFEQ      'U'
     C     WWBELO        IFGT      D2BELG
     C                   GOTO      D2TAG1
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
      *
      *  Sjekk på bestillingnummer fra-til dersom oppgitt
      *
     C     D2FONR        IFGT      *ZERO
     C     LONUMM        IFLT      D2FONR
     C                   GOTO      D2TAG1
     C                   ENDIF
     C                   ENDIF
      *
     C     D2FONR        IFGT      *ZERO
     C     LONUMM        IFGT      D2TONR
     C                   GOTO      D2TAG1
     C                   ENDIF
     C                   ENDIF
      *
     C                   GOTO      D2TAG1
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D3 Behandler plukking av bestillingsnummer tilfeldige          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     D3TAGA        TAG
      *
     C                   MOVE      *ZERO         D3UONR
     C                   MOVE      *ZERO         D3USUF
     C                   MOVE      *ZERO         D3IONR
     C                   MOVE      *ZERO         D3ISUF
      *
      *  Send Alt
      *
     C                   WRITE     D3BLD
      *
      *  Bare data
      *
     C     D3TAGB        TAG
     C                   EXFMT     D3BLD
      *
      *  F3=Avslutt program
      *
     C     *INKC         CABEQ     *ON           D0TAGA
      *
      *  Ingen bestillingsnummer er tastet, avslutt tilfeldig registrering
      *
     C     D3IONR        IFEQ      *ZERO
     C     D3ISUF        IFEQ      *ZERO
     C                   GOTO      XSLUTT
     C                   ENDIF
     C                   ENDIF
      *
      *  Sjekk, Finnes bestillingsnummer i BESTILLING-HODE-REGISTER
      *
     C                   MOVE      D3IONR        HEDNUM
     C                   MOVE      D3ISUF        HEDSUF
     C     HEDKEY        CHAIN     LOHEL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   GOTO      D3TAGB
     C                   ENDIF
      *
      *  Sjekk, Er ordretype på bestilling lik med
      *        med ordretype fra i parameter
      *
     C     LOOTYP        IFNE      D0FOTY
     C                   MOVE      *ON           *IN32
     C                   GOTO      D3TAGB
     C                   ENDIF
      *
      *  Behandler utplukk
      *
     C                   EXSR      OPPORD
      *
      *  Legger inn for å vise siste utplukkede bestillingsnummer
      *
     C                   MOVE      HEDNUM        D3UONR
     C                   MOVE      HEDSUF        D3USUF
      *
      *  Blanker felter for å registrere nytt bestillingsnummer
      *
     C                   MOVE      *ZERO         D3IONR
     C                   MOVE      *ZERO         D3ISUF
      *
     C                   GOTO      D3TAGB
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2BLD Behandle bilde for BESTILLINGSFORSLAG                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XE2WIN        BEGSR
      *
     C                   MOVE      *ZERO         E2PALT
      *
     C                   WRITE     E2BLD
     C                   EXFMT     E2BLD
      *
     C     *INKC         CABEQ     *ON           XE2SLT
      *
     C                   MOVE      E2PALT        WWPALT
      *
     C     XE2SLT        ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E3BLD Behandle bilde for BESTILLING                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XE3WIN        BEGSR
      *
     C                   MOVE      *ZERO         E3PALT
      *
     C                   WRITE     E3BLD
     C                   EXFMT     E3BLD
      *
     C     *INKC         CABEQ     *ON           XE3SLT
      *
     C                   MOVE      E3PALT        WWPALT
      *
     C     XE3SLT        ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler oppdatering av registre etter kontroll-utvalg        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     OPPORD        BEGSR
      *
      *  Sjekk, er denne bestilllin allerede under behandling
      *
     C     LOKODE        CABNE     *ZERO         OPPEND
      *
      *  Sjekk, har denne bestillingen en ordretype som er valgt (fra)
      *
     C     LOOTYP        CABNE     D0FOTY        OPPEND
      *
      *  Oppdaterer BEST.-HODE-REGISTER med merke, bestillingen er til behandl.
      *
     C                   CALL      'LO709R'
     C                   PARM                    LONUMM
     C                   PARM                    LOSUFF
      *
     C                   MOVE      LONUMM        HEDNUM
     C                   MOVE      LOSUFF        HEDSUF
     C     HEDKEY        CHAIN     LOHELUR                            94
     C     *IN94         IFEQ      *OFF
     C     D0TOTY        IFEQ      *BLANK
     C                   MOVE      2             LOKODE
     C                   ELSE
     C                   MOVE      3             LOKODE
     C                   END
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = dsuser
     C                   UPDATE    LOHELUR
     C                   END
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER med ny bestilling
      *
     C                   MOVE      LONUMM        PLONUM
     C                   MOVE      LOSUFF        PLOSUF
     C     PLOKEY        CHAIN     LPLOLUR                            96
     C     *IN96         IFEQ      *ON
     C                   MOVE      PLOFIR        LKPFIR
     C                   MOVE      PLONUM        LKPONR
     C                   MOVE      PLOSUF        LKPSUF
     C                   MOVE      WWOTYP        LKPOTY
     C                   MOVE      DSWSID        LKPWSD
     C                   MOVE      DSUSER        LKPUSR
     C                   MOVE      WWPALT        LKPALT
     C                   MOVE      *ZERO         LKPDIR
     C                   MOVE      *ZERO         LKPRES
     C                   MOVE      D0PTXT        LKPTXT
     C                   MOVE      LOLAGE        LKPLAG
     C                   MOVE      *ZERO         LKPKOL
     C                   MOVE      *ZERO         LKPNET
     C                   MOVE      *ZERO         LKPBRU
     C                   MOVE      *BLANK        LKPLMT
     C                   MOVE      *ZERO         LKPFPR
     C                   MOVE      *BLANK        LKPFTE
     C                   MOVE      *ZERO         LKPEPR
     C                   MOVE      *BLANK        LKPETE
     C                   WRITE     LPLOLUR
     C                   END
      *
     C     OPPEND        ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   move      *blank        wpftor            1
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    LOBDAT        WWBDAT
     C     *LIKE         DEFINE    LOOTYP        WWOTYP
     C     *LIKE         DEFINE    LKPALT        WWPALT
     C     *LIKE         DEFINE    LOTOTI        WWBELO
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    LOFIRM        WPFIRM
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    LOFIRM        HOTFIR
     C     *LIKE         DEFINE    LOOTYP        HOTOTY
     C     *LIKE         DEFINE    LONUMM        HOTNUM
     C     *LIKE         DEFINE    LOSUFF        HOTSUF
      *
     C     *LIKE         DEFINE    LKPFIR        PLOFIR
     C     *LIKE         DEFINE    LKPONR        PLONUM
     C     *LIKE         DEFINE    LKPSUF        PLOSUF
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      * Key for file : BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : BESTILLING-HODE-ORDRETYPE-REGISTER
      *
     C     HOTKEY        KLIST
     C                   KFLD                    HOTFIR
     C                   KFLD                    HOTOTY
     C                   KFLD                    HOTNUM
     C                   KFLD                    HOTSUF
      *
      * Key for file : UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        KLIST
     C                   KFLD                    PLOFIR
     C                   KFLD                    PLONUM
     C                   KFLD                    PLOSUF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPFTOR
     C                   PARM                    WPFIRM
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      WPFIRM        OTYFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        HOTFIR
     C                   MOVE      WPFIRM        PLOFIR
     C                   MOVE      WPFIRM        TILFIR
      *
     CSR                 ENDSR
