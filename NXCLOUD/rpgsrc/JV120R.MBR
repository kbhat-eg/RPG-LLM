     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV120R
      * Beskrivelse . . : Overføring nye varer til EVR, parameterprog
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020796 HKO  Nytt program
      *       101202 HKO  Lagt inn grossistløsning
      *       141003 HKO  Kaller programmer med leverandør som parameter
      *       140605 HKO  Lagt inn valg av leveringstype
      *       130905 HKO  Overstyring til skaffevare
5.70  * PRGR  171108 bof  Utvidet med prisgruppe
6.10  *       030909 bof  Utvidet med sortimentskode
6.11  *       090909 bof  Hvis lev. er med iparm. sett til prisgiver
6.30  *       140714 bof  Utvidet med prisendringsdato + parm. vis utgåtte
6.31  *       280814 bof  Ny feilmelding hvis p.giv eller lev. sperr/pass.
6.32  * 6.30  211014 bsa  Justert i skjermbildet pga jacada
7.xx  * 7.00  040316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
6.10 fjsorl1    if   e           k disk    rename(jsorpfr:jsorl1r)
      *
     fjv120d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_ldor_alf      s              6
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vltpl1_lety     s                   like(vylety)
5.70 d ra30l1_dkod     s                   like(redkod)
6.10 d jsorl1_osor     s                   like(jsosor)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
6.30 d w_udat          s                   like(jledat)
     d w_dato          s               d   datfmt(*iso)
6.31 d w_retk          s              1
6.31 d w_levr          s                   like(jlenum)
6.31 d w_navn          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
6.30  *
6.30 c     *dmy          move      w_udat        b1dato
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
6.30 c                   eval      w_dato = *loval
6.30  *
6.30 c                   if        b1dato <> 0
6.30 c     *dmy          move      b1dato        w_dato
6.30 c                   endif
      *
      * Kaller overføringsprogram
      *
     c                   call      'JV121R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1pgiv
     c                   parm                    b1lety
     c                   parm                    b1skaf
5.70 c                   parm                    b1prgr
6.10 c                   parm                    b1osor
6.30 c                   parm                    w_dato
6.30 c                   parm                    b1utgp
      *
     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'JL500R'
     c                   parm                    b1ldor
     c                   parm                    b1navn
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Prisgivende leverandør
      *
     c                   if        w_afld = 'B1PGIV'
     c                   call      'JL500R'
     c                   parm                    b1pgiv
     c                   parm                    b1pnav
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1lety
     c                   parm                    b1lbes
     c                   leavesr
     c                   endif
4.70  *
4.70  * Prisgruppe
4.70  *
4.70 c                   if        w_afld = 'B1PRGR'
4.70 c                   call      'RA530R'
4.70 c                   parm                    w_firm
4.70 c                   parm                    b1prgr
4.70 c                   parm                    b1ptxt
4.70 c                   leavesr
4.70 c                   endif
6.10  *
6.10  * Sortimentskode
6.10  *
6.10 c                   if        w_afld = 'B1OSOR'
6.10 c                   call      'JS510R'
6.10 c                   parm                    b1osor
6.10 c                   parm                    b1stxt
6.10 c                   leavesr
6.10 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1navn = *blank
     c                   eval      b1utxt = *blank
     c                   eval      b1pnav = *blank
     c                   eval      b1lbes = *blank
5.70 c                   eval      b1ptxt = *blank
6.10 c                   eval      b1stxt = *blank
6.30  *
6.30  * Prisendringsdato
6.30  *
6.30 c                   if        b1dato <> 0
6.30  *
6.30 c     *dmy          test (d)                b1dato                 43
6.30 c                   if        *in43 = *on
6.30 c                   eval      b_feil = *on
6.30 c                   leavesr
6.30 c                   endif
6.30 c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldor = 0 and
     c                             b1pgiv = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.31 c                   eval      w_retk = *blank
6.31 c                   eval      w_levr = jlenum
6.31 c                   call      'RS760R  '
6.31 c                   parm                    w_retk
6.31 c                   parm                    w_levr
6.31 c                   parm                    w_navn
6.31  *
6.31 c                   if        w_retk = 'P' or
6.31 c                             w_retk = 'S'
6.31 c                   eval      *in41  = *on
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
     c                   eval      b1navn = jlnavn
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0 and
     c                             b1hgrp = 0 and
     c                             b1ugrp = 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0 and
     c                             b1ugrp = 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Prisgivende leverandør
      *
     c                   if        b1pgiv <> 0
     c                   eval      jlevl1_ldor = b1pgiv
     c     jlevl1_key    chain     jlevl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.31 c                   eval      w_retk = *blank
6.31 c                   eval      w_levr = jlenum
6.31 c                   call      'RS760R  '
6.31 c                   parm                    w_retk
6.31 c                   parm                    w_levr
6.31 c                   parm                    w_navn
6.31  *
6.31 c                   if        w_retk = 'P' or
6.31 c                             w_retk = 'S'
6.31 c                   eval      *in40  = *on
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
     c                   eval      b1pnav = jlnavn
     c                   endif
      *
      * Leveringstype
      *
     c                   eval      vltpl1_lety = b1lety
     c     vltpl1_key    chain     vltpl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1lbes = vybesk
5.70  *
5.70  * prisgruppe
5.70  *
5.70 c                   eval      b1ptxt = *blank
5.70 c                   if        b1prgr <> *blank
5.70 c                   eval      ra30l1_dkod = b1prgr
5.70 c     ra30l1_key    chain     ra30l1                             38
5.70 c                   if        *in38 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   eval      b1ptxt = redtxt
5.70 c                   endif
6.10  *
6.10  * Sortimentskode
6.10  *
6.10 c                   eval      b1stxt = *blank
6.10 c                   if        b1osor <> *blank
6.10 c                   eval      jsorl1_osor = b1osor
6.10 c     jsorl1_key    chain     jsorl1                             39
6.10 c                   if        *in39 = *on
6.10 c                   eval      b_feil = *on
6.10 c                   leavesr
6.10 c                   endif
6.10 c                   eval      b1stxt = jsosor
6.10 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_ldor_alf
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
5.70  *
6.10  * Key for oppslag på sortimentskode
6.10  *
6.10 c     jsorl1_key    klist
6.10 c                   kfld                    jsorl1_osor
      *
      *
      * Henter firma fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
6.11 c                   move      p_ldor_alf    b1pgiv
      *
6.11 c                   if        b1pgiv <> 0
6.11 c                   eval      jlevl1_ldor = b1pgiv
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
6.11 c                   eval      b1pnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   eval      vltpl1_lety = b1lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      b1lbes = vybesk
     c                   endif
      *
6.11 c                   eval      *in22 = *on
6.10 c                   eval      b1osor = *blank
      *
6.30 c                   time                    w_udat
      *
     c                   endsr
