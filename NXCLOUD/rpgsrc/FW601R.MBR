      /TITLE  ASOFAK Lev.vare, utskrift av varer
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FW601R
      * Beskrivelse . . : Utskrift av varer fra leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
5.70  * PRGR  211108 BOF  utvidet med prisgruppe
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.30  * 6.30  290514 BKV  LVAR økt fra 15 til 20
6.31  * 6.30  090117 BKV  fix på 6.30
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflval3    if   e           k disk    rename(flvapfr:flval3r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     ffw601p    o    e             printer
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
5.70 d l_lage               1001   1002  0
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
6.30 d d_list                        74
     d  d_firm                        3  0 overlay(d_list)
     d  d_rtyp                        1    overlay(d_list:4)
     d  d_ldor                        6  0 overlay(d_list:5)
6.30 d  d_lvaf                       20    overlay(d_list:11)
6.30 d  d_lvat                       20    overlay(d_list:31)
6.30 d  d_dato                         d   overlay(d_list:51) datfmt(*iso)
6.30 d  d_oper                        2    overlay(d_list:61)
6.30 d  d_endr                        5  2 overlay(d_list:63)
6.30 d  d_ekod                        1    overlay(d_list:68)
      *
      * Definering av parametre
      *
6.31 d p_list          s             74
      *
      * Definering av variabler i nøkler
      *
     d vvarl5_lvar     s                   like(vvlvar)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_vare_funnet   s              1    inz(*off)
6.21 d b_inlr          s              1    inz(*off)
      *
     d w_indi          s              2  0
     d w_udat          s               d   datfmt(*iso)
     d w_lety          s              1  0
     d w_inlr          s              1
     d w_timestamp     s             12  0
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
     d w_endr          s              5  2
     d w_inen          s              5  2
     d w_saen          s              5  2
     d w_kvar          s              1  0
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
     d w_sagm          s                   like(fwsapr)
     d w_bugm          s                   like(fwsapr)
     d w_ingm          s                   like(fwinpr)
     d w_kogm          s                   like(fwinpr)
5.70 d w_prgr          s              2
5.70 d w_lage          s              2  0
6.21 d w_pldo          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
5.70 d p_vtyp          s                   like(vvvtyp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Behandling av kontrolliste/journalliste, og utskrift av deltajlinjer
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2rtyp = d_rtyp
     c                   eval      a2ldor = d_ldor
     c                   eval      a2lvaf = d_lvaf
     c                   eval      a2lvat = d_lvat
     c                   eval      a2oper = d_oper
     c                   eval      a2endr = d_endr
     c                   eval      a2ekod = d_ekod
      *
     c                   eval      rlevl1_levr = d_ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      a2lnav = rlnavn
     c                   endif
      *
     c                   if        d_dato <> *loval
     c     *dmy          move      d_dato        a2dato
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster som matcher utplukkskriteriene
      * Behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c     flval3_key    setll     flval3
     c     flval3_key    reade     flval3                                 90
      *
     c                   dow       *in90 = *off
      *
      * Sjekker om varene skal være med på utplukket
      *
     c                   if        d_lvaf <> *blank and
     c                             fwlvar <  d_lvaf
     c                   goto      les_neste
     c                   endif
      *
     c                   if        d_lvat <> *blank and
     c                             fwlvar >  d_lvat
     c                   goto      les_neste
     c                   endif
      *
     c                   if        d_dato <> *loval
     c                   select
     c                   when      (d_oper = *blank or
     c                              d_oper = 'EQ') and
     c                             fwdato <> d_dato
     c                   goto      les_neste
     c                   when      d_oper = 'GT' and
     c                             fwdato <= d_dato
     c                   goto      les_neste
     c                   when      d_oper = 'LT' and
     c                             fwdato >= d_dato
     c                   goto      les_neste
     c                   endsl
     c                   endif
      *
     c                   if        d_ekod <> *blank and
     c                             fwekod <> d_ekod
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5                             91
     c                   if        *in91 = *off
     c                   eval      b_vare_funnet = *on
     c                   else
     c                   eval      b_vare_funnet = *off
     c                   endif
      *
     c                   if        d_rtyp = '1' and
     c                             b_vare_funnet = *off or
     c                             d_rtyp = '2' and
     c                             b_vare_funnet = *on
     c                   goto      les_neste
     c                   endif
      *
      * Finner gammel salgspris og %-vis endring, og tester på %-vis endring
      *
     c                   eval      w_ingm = 0
     c                   eval      w_sagm = 0
     c                   eval      w_endr = 0
     c                   eval      w_inen = 0
     c                   eval      w_saen = 0
      *
     c                   if        b_vare_funnet = *on
      *
      * Finner eksisterende priser i vareregisteret
      *
     c                   eval      w_lety = 0
     c                   eval      w_inlr = *blank
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_prgr
     c                   parm                    fwenhe
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    w_inlr
     c                   parm                    w_sagm
     c                   parm                    w_bugm
     c                   parm                    w_kogm
     c                   parm                    w_ingm
      *
      * Finner prosentvis endring av innkjøpspris og salgspris
      *
     c                   if        w_ingm <> 0
     c                   if        (100 * fwinpr / w_ingm) - 100 > 999,99
     c                   eval      w_inen = 999,99
     c                   else
     c                   eval(h)   w_inen = (100 * fwinpr / w_ingm) - 100
     c                   endif
     c                   endif
      *
     c                   if        w_sagm <> 0
     c                   if        (100 * fwsapr / w_sagm) - 100 > 999,99
     c                   eval      w_saen = 999,99
     c                   else
     c                   eval(h)   w_saen = (100 * fwsapr / w_sagm) - 100
     c                   endif
     c                   endif
      *
     c                   if        %abs(w_saen) > %abs(w_inen)
     c                   eval      w_endr = %abs(w_saen)
     c                   else
     c                   eval      w_endr = %abs(w_inen)
     c                   endif
      *
     c                   if        d_endr <> 0 and
     c                             w_endr < d_endr
     c                   goto      les_neste
     c                   endif
      *
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Leser neste rad
      *
     c     les_neste     tag
      *
     c     flval3_key    reade     flval3                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
      * Flytter felter til kvitteringsvariable
      *
     c                   eval      d1tek1 = fwtek1
     c                   eval      d1tek2 = fwtek2
     c                   eval      d1lvar = fwlvar
     c                   eval      d1ekod = fwekod
     c                   eval      d1vare = *blank
     c                   eval      d1eann = fweann
     c                   eval      d1nobb = fwnobb
     c                   eval      d1dato = 0
     c                   eval      d1enhe = fwenhe
     c                   eval      d1inpr = fwinpr
     c                   eval      d1inen = w_inen
     c                   eval      d1sagm = w_sagm
     c                   eval      d1sapr = fwsapr
     c                   eval      d1saim = 0
     c                   eval      d1dekn = 0
     c                   eval      d1saen = w_saen
     c                   eval      d1kpri = *blank
     c                   eval      d1vtyp = *blank
     c                   eval      d1kjus = 0
      *
      * Vareinformasjon
      *
     c                   if        b_vare_funnet = *on
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_pldo
6.21 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
     *
     c                   eval      d1vare = vvvare
     c                   eval      d1kpri = vvkpri
5.70 c                   eval      d1vtyp = p_vtyp
     c                   eval      d1kjus = vvkjus
     c                   endif
      *
      * Kaller sub-program for validering av vare, og retur av statuskode
      *
     c                   call      'FW113R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    fwline
     c                   parm                    fwlvar
     c                   parm                    fwtek1
     c                   parm                    fweann
     c                   parm                    fwnobb
     c                   parm                    fwenhe
     c                   parm                    fwinpr
     c                   parm                    fwsapr
     c                   parm                    fwtvar
     c                   parm                    w_kvar
     c                   parm                    d1stat
     c                   parm                    w_indi
      *
      * Prisdato
      *
     c                   if        fwdato <> *loval
     c     *dmy          move      fwdato        d1dato
     c                   endif
      *
      * Finner salgspris inkl. mva
      *
     c                   if        d1sapr <> 0
     c                   eval      d1saim = d1sapr * w_mvat
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    d1vare
     c                   parm                    w_kode
     c                   parm                    d1saim
     c                   endif
      *
      * Finner dekningsgrad (DG)
      *
     c                   if        d1sapr <> 0
     c                   select
     c                   when      (100 - (d1inpr * 100 / d1sapr)) < -999,99
     c                   eval      d1dekn = -999,99
     c                   when      (100 - (d1inpr * 100 / d1sapr)) > 999,99
     c                   eval      d1dekn = 999,99
     c                   other
     c                   eval(h)   d1dekn = 100 - (d1inpr * 100 / d1sapr)
     c                   endsl
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på leverandør vareregister
      *
     c     flval3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
      *
      * Key for oppslag på vareregister
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Kopierer parameter til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma og leverandør fra parameter
      *
     c                   eval      w_firm = d_firm
     c                   eval      w_ldor = d_ldor
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter mva-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R'
5.70 c                   parm                    w_prgr
      *
5.70 c                   eval      w_lage = l_lage
      *
     c                   endsr
