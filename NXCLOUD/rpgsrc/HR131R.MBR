     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR131R
      * Beskrivelse . . : Radioterminal trans, varemottak
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *              ASK  Nytt program
      *       201204 HKO  Lagt inn ny logikk for scanning
      *       020805 HKO  Endret les mot vare-enhet for å finne innkjøps-
      *                   enheter og salgenheter
      *       060307 bø   Hvis enhet på ean - skal denne foreslås.
      *       011107 bø   Hvis vare ikke finnes ved scanning - legg i JVSCAPF
      *       181207 bø   Skal takle skaffevarer (Varer i VA)               F
      *       090108 bø   Teste om antall skal være større enn 1000
5.61  *       080109 bø   Kaller mot cl ved scanne-sjekk
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  250909 bof  Fjernet mulighet til å registrere ean herfra
6.13  * 6.10  021009 bof  Bestillbar enhet pr. leverandør - ekstra test
6.14  * 6.10  100512 amd  Etter samtale med BOF skal endringen merket
6.14  *                   6.13 tas bort igjen, da enhet skal beholdes
6.22  * 6.20  061210 bof  Gir melding hvis skaffevare
6.23  * 6.20  190511 bø   Utvidet vl710r med div. info
6.24  * 6.20  120112 bsa  Utvidet vp753r med kampanje
6.30  * 6.30  270113 bof  Omskriving i forb. endring pakning VA
6.31  * 6.30  070214 bsa  Flagger om det er en erstatningsvare
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.nu  * 6.30  100614 bof  Gjennomgått mtp. nummerutvidelse
6.33  * 6.30  290515 bsa  Flagger om det er en erstatningsvare. NB Kun
6.33  *                   hvis det er BM_MODUL
6.34  * 6.30  300516 bof  Hvis scanning gir en enhet, bruk denne
6.35  * 6.30  260916 nho  Dersom tekstlinje lå inne som siste linje så
      *                   fikk skanning av vare TX som linjetype
6.36  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhrtrl2    if   e           k disk    rename(hrtrpfr:hrtrl2r)
     fhrtrlr    if   e           k disk    rename(hrtrpfr:hrtrlrr)
     fhrtrlu    uf a e           k disk    rename(hrtrpfr:hrtrlur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
6.11 fvvenlu    uf   e           k disk    rename(vvenpfr:vvenlur)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvenl4    if   e           k disk    rename(vvenpfr:vvenl4r)
6.13 **vepl3    if   e           k disk    rename(vveppfr:vvepl3r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flwvalu    uf a e           k disk    rename(lwvapfr:lwvalur)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     flplolu    uf a e           k disk    rename(lplopfr:lplolur)
     flplllu    uf a e           k disk    rename(lpllpfr:lplllur)
6.31 fvverl1    if   e           k disk    rename(vverpfr:vverl1r)
6.36 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fhr131d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(ldfirm)
     d p_kode          s                   like(hrkode)
     d p_tims          s                   like(hrtims)
     d p_numm          s                   like(ldnumm)
     d p_suff          s                   like(ldsuff)
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Definering av variabler i nøkler
      *
     d hrtrlr_vare     s                   like(hrvare)
     d hrtrlr_enhe     s                   like(hrenhe)
     d hrtrlu_vare     s                   like(hrvare)
     d hrtrlu_enhe     s                   like(hrenhe)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
6.11 d vvenlu_vare     s                   like(vevare)
6.11 d vvenlu_enhe     s                   like(veenhe)
     d vvenl1_vare     s                   like(vevare)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenl4_vare     s                   like(vevare)
     d vvenl4_inen     s                   like(veinen)
6.13 d*vvepl3_pvar     s                   like(vepvar)
6.13 d*vvepl3_pldo     s                   like(vepldo)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlv_vare     s                   like(ldvare)
     d lodtlv_numm     s                   like(ldnumm)
     d lodtlv_suff     s                   like(ldsuff)
     d lwvalu_numm     s                   like(mvnumm)
     d lwvalu_suff     s                   like(mvsuff)
     d lwvalu_line     s                   like(mvline)
     d votyl1_otyp     s                   like(vaotyp)
     d lplolu_ponr     s                   like(lkponr)
     d lplolu_psuf     s                   like(lkpsuf)
     d lplllu_lonr     s                   like(lklonr)
     d lplllu_lsuf     s                   like(lklsuf)
     d lplllu_llin     s                   like(lkllin)
     d jvarl1_vare     s                   like(jvvare)
     d jvpkl1_vare     s                   like(jwvare)
6.30 d jvpkl1_ldor     s                   like(jwldor)
     d jvpkl1_pakn     s                   like(jwpakn)
6.21 d vverl1_envr     s                   like(vvenvr)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_scan          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_till          s              1    inz(*off)
     d b_nean          s              1    inz(*off)
     d b_eaok          s              1    inz(*off)
     d b_pluk          s              1    inz(*off)
     d b_rest          s              1    inz(*off)
     d b_vava          s              1    inz(*off)
     d b_vaok          s              1    inz(*off)
6.22 d b_skaf          s              1    inz(*off)
6.32 d b_pakn          s              1    inz(*off)
6.33 d b_bmpp          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_scan          s             26
5.61 d w_sctx          s             30
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_vars          s             15
6.11 d w_eans          s             14
     d w_kod2          s              1
     d w_rkod          s              1
     d w_lety          s              1  0
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_stek          s             35
     d w_dirk          s              1  0
     d w_stat          s              1
     d w_outq          s             10
     d x               s              1  0
      *
     d w_firm          s                   like(hrfirm)
     d w_kode          s                   like(hrkode)
     d w_tims          s                   like(hrtims)
     d w_enhe          s                   like(hrenhe)
     d w_anta          s                   like(hranta)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_eann          s                   like(hreann)
     d w_dato          s                   like(hrdato)
     d w_time          s                   like(hrtime)
     d w_anta_kalk     s                   like(hranta)
     d w_vare          s                   like(vvvare)
     d w_line          s                   like(ldline)
     d w_rest          s                   like(rlrest)
     d w_tek1          s                   like(vvtek1)
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.11 d w_xenhe         s                   like(vvenhe)
     d p_vare          s                   like(vvvare)
6.22 d w_vtyp          s                   like(vvvtyp)
6.23 d w_utda          s                   like(vvudat)
6.23 d w_ldor          s                   like(vvldor)
6.23 d b_inlr          s              1    inz(*off)
6.36 d w_lv_nobb       s                   like(vvlnob)
6.36 d w_lv_modn       s                   like(vvlmod)
6.36 d w_lv_lldo       s                   like(vvlnle)
6.36 d w_lv_llva       s                   like(vvllva)
6.36 d w_lv_vare       s                   like(vvlvnr)
6.24 d w_kamp          s              4
6.33 d w_disp          s              1
6.33 d w_modu          s             10
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(6)
     d c_digi          c                   '0123456789 '
     d c_null          c                   '0000000000000'
6.33 d c_modu          c                   'BM_MODUL  '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for registrering
      * D1BLD  - D1 Skjermbilde for slette (Delete)
      * E1BLD  - E1 Skjermbilde for avslutting av varemottak
      * F1BLD  - F1 Skjermbilde for melding om vare ikke funnet
      * G1BLD  - F1 Skjermbilde for melding om vare ikke på bestilling
      *
      *
6.36 C/Exec SQL
6.36 C+  Set Option DatFmt=*ISO
6.36 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   endsl
      *
      * F4=Spørring på vare
      *
     c                   if        *inkd
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    b2sca1
     c                   parm                    w_stek
     c                   eval      *in22 = *on
     c                   if        b2sca1 = *blank
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av scanning
      *
     c                   if        b2sca1 <> *blank or
     c                             b2sca2 <> *blank
     c                   exsr      scanning
     c                   eval      *in22 = *on
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      * Behandler valg i avslutningsbilde
      *
     c                   exsr      slutt_mottak
     c                   if        b_oppr = *off
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
     c
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
6.11  * 4) num + lengde <= 14:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
     c                   eval      b_feil = *off
     c                   eval      b_eaok = *off
     c                   eval      b_nean = *off
     c                   eval      b_scan = *on
     c                   eval      w_eann = 0
      *
     c                   eval      c1enhe = *blank
     c                   eval      w_anta = 0
      *
     c                   eval      w_scan = %trim(b2sca1) + %trim(b2sca2)
      *
     c                   eval      w_leng = %len(%trim(w_scan))
      *
      * Sjekker om scannefelt inneholder alfa-verdi
      *
     c     c_digi        check     w_scan        w_posi
      *
      * Behandling dersom alfa
      *
     c                   if        w_posi > 0
      *
6.36  * sjekker om logistikk-vare
6.36 c                   eval      w_lv_vare = *blank
6.36 c/exec sql
6.36 c+ select vvlvnr
6.36 c+ into   :w_lv_vare
6.36 c+ from   vvlepf
6.36 c+ where  vvllva = :w_scan
6.36 c+ fetch first 1 rows only
6.36 c/end-exec
6.36 c                   if        w_lv_vare <> *blank
6.36 c                   movel     w_lv_vare     c1vare
6.36 c                   exsr      xc2bld
6.36 c                   goto      end_scan
6.36 c                   endif

     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      * hvis VA ok
     c                   exsr      sjekk_va
     c                   if        b_vaok = *on
     c                   eval      b_feil = *off
     c                   movel     p_vare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      w_xenhe = *blank
     c                   eval      d_e128 = w_scan
6.11 c                   eval      w_eann = d_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.34 c                   eval      c1enhe      = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   eval      w_anta = d_anta
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      * hvis VA ok
     c                   exsr      sjekk_va
     c                   if        b_vaok = *on
     c                   eval      b_feil = *off
     c                   movel     p_vare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vars = %subst(c_null:1:8-w_leng) +
     c                                      w_scan
      *
     c                   eval      vvarl1_vare = w_vars
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde <= 13
      *
6.11 c                   if        w_leng <= 14
      *
6.11 c                   eval      w_eans = %subst(c_null:1:14-w_leng) +
     c                                      w_scan
      *
     c                   eval      w_xenhe = *blank
     c                   movel     w_eans        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.34 c                   eval      c1enhe      = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.22 c                   eval      b_skaf  = *off
6.22 c                   exsr      sjk_skaff
6.22 c                   if        b_skaf  = *on
6.22 c                   goto      end_scan
6.22 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
6.36  * sjekker om logistikk-vare
6.36 c                   eval      w_lv_vare = *blank
6.36 c/exec sql
6.36 c+ select vvlvnr
6.36 c+ into   :w_lv_vare
6.36 c+ from   vvlepf
6.36 c+ where  vvllva = :w_scan
6.36 c+ fetch first 1 rows only
6.36 c/end-exec
6.36 c                   if        w_lv_vare <> *blank
6.36 c                   movel     w_lv_vare     c1vare
6.36 c                   exsr      xc2bld
6.36 c                   goto      end_scan
6.36 c                   endif
      *
     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      * hvis VA ok
     c                   exsr      sjekk_va
     c                   if        b_vaok = *on
     c                   eval      b_feil = *off
     c                   movel     p_vare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
      * Vare ikke identifisert - nytt ean nr?
      *
     c                   exsr      sjekk_ean
      *
     c                   if        b_eaok = *on
     c                   exsr      xf1bld
     c                   else
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      * hvis VA ok
     c                   exsr      sjekk_va
     c                   if        b_vaok = *on
     c                   eval      b_feil = *off
     c                   movel     p_vare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
     c                   endif
      *
     c                   if        b_nean = *on
     c                   exsr      xc2bld
     c                   endif
      *
      * Setter feil-indikator dersom vare ikke ble funnet
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      * hvis VA ok
     c                   exsr      sjekk_va
     c                   if        b_vaok = *on
     c                   eval      b_feil = *off
     c                   movel     p_vare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c     end_scan      tag
      *
     c                   eval      b_scan = *off
      *
     c                   if        b_feil = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
     c                   eval      *in22 = *off
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
     c                   eval      d1enhe = b1enhe
     c                   exsr      xd1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_enhe = c1enhe
     c                   eval      c2etik = 0
      *
      * Henter vareinformasjon
      *
     c                   if        b_vava = *on
      * hvis det er VA -vare
     c                   eval      jvarl1_vare = *blank
     c                   movel     c1vare        jvarl1_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   goto      end_c2bld
     c                   endif
     c                   eval      w_vare = jvvare
     c                   eval      w_tek1 = jvtek1
     c                   else
     c                   eval      vvarl1_vare = *blank
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      end_c2bld
     c                   endif
     c                   eval      w_vare = vvvare
     c                   eval      w_tek1 = vvtek1
     c                   endif
      *
      * Sjekker om vare er på bestilling
      *
     c                   eval      b_feil = *off
     c*                  eval      w_vare = vvvare
     c                   exsr      finn_linje
      *
     c                   if        b_feil = *on
     c                   if        b_vava = *off
     c                   exsr      xg1bld
     c                   if        b_till = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   goto      end_c2bld
     c                   endif
     c                   else
     c                   exsr      xi1bld
     c                   endif
     c                   eval      b_feil = *off
     c                   endif
      *
     c                   eval      w_tek1 = %trim(w_tek1)
     c                   eval      c2tek1 = %subst(w_tek1:1:19)
     c                   eval      c2tek2 = %subst(w_tek1:20:16)
      *
      * Henter tre innkjøpsenheter. Dersom innkjøpsenhet ikke ble funnet,
      * hentes tre salgsenheter
      *
     c                   if        b_vava = *on
     c                   exsr      va_enhe
     c                   else
6.13  *
6.13  * Prøver å først å finne bestillbare enheter pr. leverandør
6.13  *
6.13  *
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      c2enh1 = *blank
     c                   eval      c2enh2 = *blank
     c                   eval      c2enh3 = *blank
6.13  *
6.13 c*->                if        loldor <> 0
6.13 c*->                eval      vvepl3_pvar = vvvare
6.13 c*->                eval      vvepl3_pldo = loldor
6.13 c*->  vvepl3_key    setll     vvepl3
6.13 c*->  vvepl3_key    reade     vvepl3
6.13 c*->                dow       not %eof(vvepl3)
6.13 c*->                if        vepbes = 1
6.13 c*->                eval      x = x + 1
6.13  *
6.13 c*->                select
6.13 c*->                when      x = 1
6.13 c*->                eval      c2enh1 = vepenh
6.13 c*->                when      x = 2
6.13 c*->                eval      c2enh2 = vepenh
6.13 c*->                when      x = 3
6.13 c*->                eval      c2enh3 = vepenh
6.13 c*->                endsl
6.13 c*->                endif
6.13 c*->  vvepl3_key    reade     vvepl3
6.13 c*->                enddo
6.13 c*->                endif
      *
6.13 c*->                if        x = 0
      *
     c                   eval      vvenl4_vare = w_vare
6.34 c                   eval      vvenl4_inen = 0
     c     vvenl4_key    setll     vvenl4
     c     vvenl4_key2   reade     vvenl4
     c                   dow       not %eof and
     c                             x < 3
6.34 c                   if        (c1enhe <> *blank and
6.34 c                             c1enhe = veenhe) or
6.34 c                             veinen = 1
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = veenhe
     c                   when      x = 2
     c                   eval      c2enh2 = veenhe
     c                   when      x = 3
     c                   eval      c2enh3 = veenhe
     c                   endsl
      *
6.34 c                   endif
      *
     c     vvenl4_key2   reade     vvenl4
     c                   enddo
      *
6.13 c*->                endif
      *
     c                   if        x = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = veenhe
     c                   when      x = 2
     c                   eval      c2enh2 = veenhe
     c                   when      x = 3
     c                   eval      c2enh3 = veenhe
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endif
      *
     c                   endif
      *
     c                   if        w_xenhe <> *blank
     c                   select
     c                   when      w_xenhe = c2enh1
     c                   eval      *in80  = *on
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh2
     c                   eval      *in80  = *off
     c                   eval      *in81  = *on
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh3
     c                   eval      *in80  = *off
     c                   eval      *in81  = *off
     c                   eval      *in82  = *on
     c                   endsl
     c                   endif
      *
      * Antall
      *
     c                   eval      c2anta = w_anta
      *
      * Varemottak
      *
     c                   if        b_scan = *off
      *
     c                   eval      hrtrlr_vare = c1vare
     c                   eval      hrtrlr_enhe = c1enhe
     c     hrtrlr_key    chain     hrtrlr
     c                   if        %found
      *
     c                   eval      *in80 = *off
      *
     c                   select
     c                   when      hrenhe = c2enh1
     c                   eval      *in80 = *on
     c                   when      hrenhe = c2enh2
     c                   eval      *in81 = *on
     c                   when      hrenhe = c2enh3
     c                   eval      *in82 = *on
     c                   endsl
      *
     c                   eval      c2anta = hranta
      *
     c                   endif
     c                   endif
6.31  * Er dette en erstatningsvare ??
6.33 c                   if        b_bmpp = *on
6.31 c                   exsr      sjk_erst
6.33 c                   endif
      *
      * Viser bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc
     c                   goto      end_c2bld
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   select
     c                   when      *in80 = *on and
     c                             c2enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        c2enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
     c                   goto      c2tagb
     c                   endif
      *
      * F2=Kalkulator
      *
     c                   if        *inkb
     c                   call      'HR200R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_anta_kalk
     c                   goto      c2tagb
     c                   endif
      *
      * F5=Forny
      *
     c                   if        *inke
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      c2anta = 0
     c                   goto      c2tagb
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2anta <= 0
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Tester om antall skal være større en 1000
      *
     c                   if        c2anta > 1000
      *
     c                   exfmt     j1bld
      *
     c                   if        *inkc
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   hrtrlur
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      c1enhe = c2enh1
     c                   when      *in81 = *on
     c                   eval      c1enhe = c2enh2
     c                   when      *in82 = *on
     c                   eval      c1enhe = c2enh3
     c                   endsl
      *
     c                   if        b1valg = 2 and
     c                             w_enhe <> c1enhe
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = w_enhe
     c     hrtrlu_key    chain     hrtrlur
     c                   if        %found
     c                   delete    hrtrlur
     c                   endif
     c                   endif
      *
     c                   if        b_till = *on
     c                   exsr      endre_linje
     c                   eval      hrstat = '4'
     c                   eval      b_till = *off
     c                   endif
      *
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = c1enhe
     c     hrtrlu_key    chain     hrtrlur
      *
     c                   eval      hrvare = c1vare
     c                   eval      hrenhe = c1enhe
     c                   eval      hreann = w_eann
     c                   if        b_nean = *on
     c                   eval      hrstat = '3'
     c                   endif
      *
     c                   if        %found and
     c                             b_scan = *on
     c                   eval      hranta = hranta + c2anta
     c                   else
     c                   eval      hranta = c2anta
     c                   endif
      *
     c                   time                    hrdato
     c                   time                    hrtime
      *
     c                   if        %found
     c                   update    hrtrlur
     c                   else
     c                   eval      hrfirm = w_firm
     c                   eval      hrkode = w_kode
     c                   eval      hrtims = w_tims
     c                   write     hrtrlur
     c                   endif
      *
     c                   eval      hrstat = ' '
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1enhe = hrenhe
     c     hranta        mult      1             b1anta
     c                   endif
      *
      * Sjekker om etikettutskrift er valgt
      *
     c                   if        c2etik = 1
     c                   call      'FP250C'
     c                   parm                    w_vare
     c                   endif
      *
     c     end_c2bld     endsr
      *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å sjekke etter erstatningsvarer
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjk_erst      begsr
6.31  *
6.31 c                   eval      c2info = *blanks
6.31 c                   movel     c1vare        vverl1_envr
6.31 c     vverl1_key    chain     vverl1
6.31 c                   if        %found
6.31 c                   if        vvantb < 3 and
6.31 c                             w_udat >= vvfidt and
6.31 c                             w_udat <= vvsidt
6.31 c                   eval      c2info = 'Erstatningsvare'
6.31 c                   endif
6.31 c                   endif
6.31  *
6.31 c                   endsr
6.31  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (d1bld)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1bld        begsr
      *
     c                   eval      hrtrlr_vare = d1vare
     c                   eval      hrtrlr_enhe = d1enhe
     c     hrtrlr_key    chain     hrtrlr
      *
     c                   eval      d1anta = hranta
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      d1tek1 = %subst(vvtek1:1:19)
     c                   eval      d1tek2 = %subst(vvtek1:20:16)
     c                   endif
      *
     c                   exfmt     d1bld
      *
     c                   if        *inkc
     c                   goto      end_d1bld
     c                   endif
      *
     c                   if        hrstat = '4'
     c                   exsr      slett_linje
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      hrtrlu_vare = d1vare
     c                   eval      hrtrlu_enhe = d1enhe
     c     hrtrlu_key    chain     hrtrlur
      *
     c                   delete    hrtrlur
      *
     c     end_d1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare ikke funnet i vareregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1bld        begsr
      *
     c                   exfmt     f1bld
      *
     c                   if        *inkc
     c                   goto      end_f1bld
     c                   endif
      *
     c                   if        *inkd
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_stek
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
6.12 c*                  exsr      reg_ean
6.12 c*                  eval      b_nean = *on
     c                   endif
     c                   endif
      *
      *
     c     end_f1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare ikke funnet på bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xg1bld        begsr
      *
     c                   eval      b_till = *off
     c                   exfmt     g1bld
      *
     c                   if        *inkc or
     c                             g1till = 0
     c                   goto      end_g1bld
     c                   endif
      *
     c                   exsr      ny_linje
     c                   eval      b_till = *on
      *
      *
     c     end_g1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare funnet i VA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1bld        begsr
      *
     c                   eval      b_vaok = *off
     c                   eval      h1ok   = 0
     c                   exfmt     h1bld
      *
     c                   if        *inkc or
     c                             h1ok   = 0
     c                   goto      end_h1bld
     c                   endif
      *
     c                   eval      b_vaok = *on
      *
      *
     c     end_h1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for som forteller at skaffevarer ikke kan legges til best.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xi1bld        begsr
      *
     c                   eval      b_vaok = *off
     c                   exfmt     i1bld
      *
     c                   if        *inkc
     c                   goto      end_i1bld
     c                   endif
      *
     c     end_i1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     hrtrl2_key    reade     hrtrl2
      *
     c                   if        %eof
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1tek1 = *blank
     c                   eval      b1enhe = hrenhe
     c                   eval      b1anta = 0
     c                   eval      b1vare = hrvare
      *
     c     hranta        mult      1             b1anta
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
     c                   else
     c                   eval      jvarl1_vare = *blank
     c                   movel     hrvare        jvarl1_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b1tek1 = jvtek1
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   eval      *in31 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å avslutte varemottak
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt_mottak  begsr
      *
     c     hrtrl2_key    chain     hrtrl2
     c                   if        not %found
     c                   eval      b_oppr = *on
     c                   goto      end_mottak
     c                   endif
      *
      * Avslutningsbilde
      *
     c     e1taga        tag
      *
     c                   eval      e1valg = 0
     c                   exfmt     e1bld
      *
     c                   if        *inkc
     c                   goto      end_mottak
     c                   endif
      *
     c                   select
     c                   when      e1valg = 0
     c                   exsr      opprett
     c                   when      e1valg = 1
     c                   exsr      opprett
     c                   exsr      oppdater
     c                   other
     c                   goto      e1taga
     c                   endsl
      *
     c     end_mottak    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å legge inn rapportert antall i varemottaksfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
      * Leser varemottak fra radioterminal
      *
     c     hrtrlu_key2   setll     hrtrlu
     c     hrtrlu_key2   reade     hrtrlur
     c                   dow       not %eof
      *
     c                   eval      w_vare = *blank
     c                   movel     hrvare        w_vare
     c                   exsr      finn_linje
      *
      * Avvik fra bestillingsenhet?
      *
     c                   if        hrenhe <> ldenh1
     c                   exsr      omr_antall
     c                   eval      hranta = w_anta
     c                   eval      hrenhe = ldenh1
     c                   endif
      *
      * Oppdaterer varemottak arbeidsfil
      *
     c                   eval      lwvalu_line = w_line
     c     lwvalu_key    chain     lwvalur
      *
     c                   eval      mvrkod = rlrest
     c                   eval      mvekod = hrekod
     c                   eval      mvstat = hrstat
      *
     c                   if        %found
     c                   eval      mvlevr = mvlevr + hranta
     c                   update    lwvalur
     c                   else
     c                   eval      mvfirm = w_firm
     c                   eval      mvnumm = p_numm
     c                   eval      mvsuff = p_suff
     c                   eval      mvline = w_line
     c                   eval      mvvare = *blank
     c                   movel     hrvare        mvvare
     c                   eval      mvenhe = hrenhe
     c                   eval      mvlevr = hranta
     c                   write     lwvalur
     c                   endif
      *
     c                   delete    hrtrlur
      *
     c     hrtrlu_key2   reade     hrtrlur
     c                   enddo
      *
      * Viser avslutingsbildet
      *
     c                   eval      b_oppr = *on
      *
     c     end_opprett   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere varemottak
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Finn neste ordretype
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1r
      *
      * Leser bestillingslinjer
      *
     c                   eval      lodtl1_line = 0
     c     lodtl1_key    setll     lodtl1
     c                   read      lodtl1
      *
     c                   dow       not %eof  and
     c                             ldnumm = p_numm and
     c                             ldsuff = p_suff
      *
      * Leser innmeldt og skriver plukklinje
      *
     c                   eval      lwvalu_line = ldline
     c     lwvalu_key    chain     lwvalu
     c                   if        not %found
     c                   eval      mvlevr = 0
     c                   eval      mvrkod = w_rest
     c                   endif
      *
     c                   if        mvrkod = 1 or
     c                             mvrkod <> w_rest or
     c                             mvlevr > 0
     c                   eval      b_pluk = *on
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = p_numm
     c                   eval      lklsuf = p_suff
     c                   eval      lkllin = ldline
     c                   eval      lklant = mvlevr
      *
     c                   if        mvrkod = 0
     c                   eval      lklkod = '5'
     c                   else
     c                   eval      lklkod = *blank
     c                   endif
      *
     c                   write     lplllur
     c                   endif
      *
     c                   if        mvrkod = 1
     c                   eval      b_rest = *on
     c                   endif
     c                   if        mvrkod = 1 and
     c                             mvlevr <> ldanta
     c                   eval      b_rest = *on
     c                   endif
      *
      * Sletter linje fra innmeldingsfil
      *
     c                   if        %found (lwvalu)
     c                   delete    lwvalur
     c                   endif
      *
     c                   read      lodtl1
     c                   enddo
      *
      * Skriv plukk heading
      *
     c                   if        b_pluk = *on
     c                   eval      lkpfir = w_firm
     c                   eval      lkponr = p_numm
     c                   eval      lkpsuf = p_suff
     c                   eval      lkpoty = vaonxt
     c                   eval      lkplag = lolage
     c                   eval      lkpwsd = l_wsid
     c                   eval      lkpusr = l_user
     c                   eval      lkpdir = 1
     c                   eval      lkpres = 1
      *
     c                   write     lplolur
     c                   endif
      *
      * Skriv inngående pakkseddel
      *
     c                   eval      w_dirk = 1
     c                   eval      w_outq = *blank
      *
     c                   call      'LO604R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   parm                    vaonxt
     c                   parm                    w_dirk
     c                   parm                    w_outq
      *
      *  Kaller opp subprogram for å slette utvalg og blanke merking
      *
     c                   call      'LO402R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
      *
     c     end_oppdater  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne varelinje på varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_linje    begsr
      *
     c                   eval      lodtlv_vare = w_vare
     c     lodtlv_key    chain     lodtlv
     c                   if        %found
     c                   eval      w_line = ldline
     c                   else
     c                   eval      b_feil = *on
     c                   eval      w_line = 0
     c                   endif
      *
     c     end_linje     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om gyldig ean-nr er angitt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ean     begsr
      *
     c                   eval      w_rkod = '1'
     c                   call      'FÅ900R'
     c                   parm                    w_eann
     c                   parm                    w_rkod
      *
     c                   if        w_rkod = ' '
     c                   eval      b_eaok = *on
     c                   else
     c                   eval      w_eann = 0
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av ny bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_linje      begsr
      *
      * Finn siste linje på bestilling
      *
     c                   eval      lodtl1_line = 9999
     c     lodtl1_key    setll     lodtl1
     c                   readp     lodtl1
     c                   if        %eof
     c                   goto      end_ny_linje
     c                   endif
      *
      * Tilordner verdier til ny bestillingslinje
      *
     c                   eval      lodtlu_line = ldline + 10
     c     lodtlu_key    chain     lodtlu
     c                   if        %found
     c                   goto      end_ny_linje
     c                   endif
      *
     c                   eval      ldline = ldline + 10
     c                   eval      ldvare = vvvare
6.36 c                   if        w_lv_nobb > 0
6.36 c                   eval      ldlvar = w_lv_llva
6.36 c                   eval      ldnobb = w_lv_nobb
6.36 c                   else
     c                   eval      ldlvar = vvlvar
     c                   eval      ldnobb = vvnobb
6.36 c                   endif
     c                   eval      ldtek1 = vvtek1
     c                   eval      ldtek2 = vvtek2
     c                   eval      ldogrp = vvogrp
     c                   eval      ldhgrp = vvhgrp
     c                   eval      ldugrp = vvugrp
      *
     c                   eval      ldenh1 = *blank
     c                   eval      ldanta = 0
     c                   eval      ldkpny = 0
     c                   eval      ldipny = 0
     c                   eval      ldsumk = 0
     c                   eval      ldsumi = 0
6.35 c                   eval      ldltyp = *blank
      *
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
      *
     c     end_ny_linje  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av ny bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endre_linje   begsr
      *
     c                   eval      w_vare = vvvare
     c                   exsr      finn_linje
      *
     c                   eval      lodtlu_line = w_line
     c     lodtlu_key    chain     lodtlu
     c                   if        %found
     c                   eval      ldenh1 = c1enhe
     c                   eval      ldanta = c2anta
      *
     c                   time                    w_dato
     c                   eval      w_lety = 0
6.24 c                   eval      w_kamp = lokamp
     c                   call      'VP753R'
     c                   parm                    w_firm
     c                   parm                    ldvare
5.70 c                   parm                    w_prgr
     c                   parm                    ldenh1
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    ldkpny
     c                   parm                    ldipny
6.24 c                   parm                    w_kamp
      *
     c                   eval      ldsumk = ldkpny * ldanta
     c                   eval      ldsumi = ldipny * ldanta
      *
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlur
     c                   endif
      *
     c     end_endre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av ny bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_linje   begsr
      *
     c                   eval      w_vare = vvvare
     c                   exsr      finn_linje
      *
     c                   eval      lodtlu_line = w_line
     c     lodtlu_key    chain     lodtlu
     c                   if        %found
     c                   delete    lodtlur
     c                   endif
      *
     c     end_slett     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning til bestillings enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Finner omregningsfaktor volum for best.enhet og for registrert enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   movel     w_vare        vvenl1_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   if        hrenhe = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        ldenh1 = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl <> 0
     c                   eval      w_anta = hranta * w_omrl / w_omrs
     c                   else
     c                   eval      w_anta = 0
     c                   endif
      *
     c     end_omr       endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne enhet hvis det er vare fra VA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     va_enhe       begsr
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      c2enh1 = *blank
     c                   eval      c2enh2 = *blank
     c                   eval      c2enh3 = *blank
      *
     c                   eval      jvpkl1_vare = w_vare
6.30 c                   eval      jvpkl1_ldor = jvldor
     c                   eval      jvpkl1_pakn = '001'
     c     jvpkl1_key    setll     jvpkl1
     c                   read      jvpkl1
     c                   dow       not %eof and
     c                             x < 3    and
     c                             jwvare = w_vare
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = jwenhe
     c                   when      x = 2
     c                   eval      c2enh2 = jwenhe
     c                   when      x = 3
     c                   eval      c2enh3 = jwenhe
     c                   endsl
      *
6.32 c                   endif
      *
     c                   read      jvpkl1
     c                   enddo
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om den ligger i VA
      * for å kunne scanne skaffevarer v/varemottak
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_va      begsr
      *
      *  sjekker om varen ligger i VA - hvis ok og det godkjennes så
      *  settes feilkode av og den går videre
      *
5.61 c                   call      'LL875C '
     c                   parm                    w_scan
     c                   parm                    p_vare
     c                   parm                    b_vava
     c                   if        b_vava = *on
     c                   exsr      xh1bld
     c                   endif
      *
     c                   endsr
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *   Subrutine som sjekker om varen er skaffevare for lageret
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22 c     sjk_skaff     begsr
6.22  *
6.22 c                   call      'VL710R'
6.22 c                   parm                    w_firm
6.22 c                   parm                    vvvare
6.22 c                   parm                    lolage
6.22 c                   parm                    w_vtyp
6.23 c                   parm                    w_utda
6.23 c                   parm                    w_ldor
6.23 c                   parm                    b_inlr
6.36 c                   parm                    w_lv_nobb
6.36 c                   parm                    w_lv_modn
6.36 c                   parm                    w_lv_lldo
6.36 c                   parm                    w_lv_llva
6.22  *
6.22 c                   if        w_vtyp = 'S'
6.22 c                   exfmt     m1msg
6.22  *
6.22 c                   if        *inkc
6.22 c                   eval      b_skaf =*on
6.22 c                   leavesr
6.22 c                   endif
6.22 c                   endif
6.22  *
6.22 c                   endsr
      *
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_udat and
6.32 c                             jwtdat >  w_udat
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_udat
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_udat and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kode
     c                   parm                    p_tims
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for les på radioterminal-trans
      *
     c     hrtrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      * Key for oppslag på radioterminal-trans
      *
     c     hrtrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlr_vare
     c                   kfld                    hrtrlr_enhe
      *
      * Key for oppdatering av radioterminal-trans
      *
     c     hrtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlu_vare
     c                   kfld                    hrtrlu_enhe
      *
     c     hrtrlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
6.11  *
6.11  * Key for oppdatering av EAN-register på vare-enhet
6.11  *
6.11 c     vvenlu_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    vvenlu_vare
6.11 c                   kfld                    vvenlu_enhe
      *
      * Key for les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
     c     vvenl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
     c                   kfld                    vvenl4_inen
      *
     c     vvenl4_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
6.13  *
6.13  * Key for les på vare-enhet-prisgiver
6.13  *
6.13 c*->  vvepl3_key    klist
6.13 c*->                kfld                    w_firm
6.13 c*->                kfld                    vvepl3_pvar
6.13 c*->                kfld                    vvepl3_pldo
      *
      * Key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppdatering av bestillingslinje
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      * Key for les av bestillingslinje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      * Key for posisjonering av bestillingslinje på varenummer
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
     c                   kfld                    lodtlv_numm
     c                   kfld                    lodtlv_suff
      *
      *  Key for oppdatering av arbeidsregister
      *
     c     lwvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwvalu_numm
     c                   kfld                    lwvalu_suff
     c                   kfld                    lwvalu_line
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for UTPLUKK-PARAM
      *
     c     lplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplolu_ponr
     c                   kfld                    lplolu_psuf
      *
      *  Key for UTPLUKK-LINJE
      *
     c     lplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplllu_lonr
     c                   kfld                    lplllu_lsuf
     c                   kfld                    lplllu_llin
      *
      *  Key for les av vare i VA
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *  Key for les av enhet i VA
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
     c                   kfld                    jvpkl1_pakn
6.31  *
6.31  *  Key for les av ertstaningsvare
6.31  *
6.31 c     vverl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vverl1_envr
      *
      *
      * Henter firma og timestamp fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kode = p_kode
     c                   eval      w_tims = p_tims
      *
      * Legger inn bestillingsnummer i nøkler
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtlu_numm = p_numm
     c                   eval      lodtlu_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c                   eval      lwvalu_numm = p_numm
     c                   eval      lwvalu_suff = p_suff
     c                   eval      lodtlv_numm = p_numm
     c                   eval      lodtlv_suff = p_suff
      *
      * Leser bestillingshode
      *
     c     lohel1_key    chain     lohel1
      *
      * Hent restkode fra leverandør
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   eval      w_rest = 0
     c                   else
     c                   eval      w_rest = rlrest
     c                   endif
5.70  *
5.70  * Hent prisgruppe
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lolage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.61 c                   eval      w_sctx = 'HR131R Varemottak'
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
6.31 c                   time                    w_udat
6.33  * Sjekk om du har tilgang til alle felter
6.33 c                   eval      b_bmpp = *on
6.33 c                   eval      w_disp = '0'
6.33 c                   eval      w_stat = '0'
6.33 c                   eval      w_modu = c_modu
6.33 c                   call      'AX700R'
6.33 c                   parm                    w_modu
6.33 c                   parm                    w_disp
6.33 c                   parm                    w_stat
6.33  *
6.33 c                   if        w_stat = '1'
6.33 c                   eval      b_bmpp = *off
6.33 c                   endif
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
