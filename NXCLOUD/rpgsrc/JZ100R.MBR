     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JZ100R
      * Beskrivelse . . : Forpakning, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170899 HKO  Nytt program
      *       250803 HKO  Lagt inn styring av prisbok- og innkjøpsenheter
      *                   Lagt inn opprettet-dato
      *       290208 bof  Utvidet forpakning med modulnr.
6.31  *       010816 bof  Hvis modulnr, skal den finne riktig varegruppe.
6.32  *       230117 bof  Feilretting vedr. søk                        e.
7.xx  * 7.00  290716 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjforlr    if   e           k disk    rename(jforpfr:jforlrr)
     fjforlu    uf a e           k disk    rename(jforpfr:jforlur)
     fjforl1    if   e           k disk    rename(jforpfr:jforl1r)
     fjforl2    if   e           k disk    rename(jforpfr:jforl2r)
     fjforl3    if   e           k disk    rename(jforpfr:jforl3r)
     fjforl4    if   e           k disk    rename(jforpfr:jforl4r)
     fjforl5    if   e           k disk    rename(jforpfr:jforl5r)
     fjforl6    if   e           k disk    rename(jforpfr:jforl6r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.31 fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
      *
     fjz100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jforlr_ogrp     s                   like(jzogrp)
     d jforlr_hgrp     s                   like(jzhgrp)
     d jforlr_ugrp     s                   like(jzugrp)
     d jforlr_ldor     s                   like(jzldor)
     d jforlr_modn     s                   like(jzmodn)
     d jforlr_vare     s                   like(jzvare)
     d jforlu_ogrp     s                   like(jzogrp)
     d jforlu_hgrp     s                   like(jzhgrp)
     d jforlu_ugrp     s                   like(jzugrp)
     d jforlu_ldor     s                   like(jzldor)
     d jforlu_modn     s                   like(jzmodn)
     d jforlu_vare     s                   like(jzvare)
     d jforl1_ogrp     s                   like(jzogrp)
     d jforl1_hgrp     s                   like(jzhgrp)
     d jforl1_ugrp     s                   like(jzugrp)
     d jforl1_ldor     s                   like(jzldor)
     d jforl1_modn     s                   like(jzmodn)
     d jforl1_vare     s                   like(jzvare)
     d jforl2_hgrp     s                   like(jzhgrp)
     d jforl3_ugrp     s                   like(jzugrp)
     d jforl4_ldor     s                   like(jzldor)
     d jforl5_vare     s                   like(jzvare)
     d jforl6_modn     s                   like(jzmodn)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jlevl1_ldor     s                   like(jlldor)
6.31 d jmodl1_modn     s                   like(jumodn)
     d jvarl1_vare     s                   like(jvvare)
     d venhl1_eenh     s                   like(vaeenh)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(jzfirm)
     d w_ogrp          s                   like(jzogrp)
     d w_hgrp          s                   like(jzhgrp)
     d w_ugrp          s                   like(jzugrp)
     d w_ldor          s                   like(jzldor)
     d w_modn          s                   like(jzmodn)
     d w_vare          s                   like(jzvare)
      *
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(jlnavn)
     d w_tek1          s                   like(jvtek1)
6.31 d w_mte1          s                   like(jvtek1)
      *
     d s_ogrp          s                   like(jzogrp)
     d s_hgrp          s                   like(jzhgrp)
     d s_ugrp          s                   like(jzugrp)
     d s_ldor          s                   like(jzldor)
     d s_modn          s                   like(jzmodn)
     d s_vare          s                   like(jzvare)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2ldor <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jforl2_hgrp = b1hgrp
     c     jforl2_key    setll     jforl2
     c                   when      w_seqe = 'U'
     c                   eval      jforl3_ugrp = b1ugrp
     c     jforl3_key    setll     jforl3
     c                   when      w_seqe = 'L'
     c                   eval      jforl4_ldor = b1ldor
     c     jforl4_key    setll     jforl4
     c                   when      w_seqe = 'M'
     c                   eval      jforl6_modn = b1modn
     c     jforl4_key    setll     jforl4
     c                   when      w_seqe = 'V'
     c                   eval      jforl5_vare = b1vare
     c     jforl5_key    setll     jforl5
     c                   other
     c                   eval      jforl1_ogrp = b1ogrp
     c                   eval      jforl1_hgrp = b1hgrp
     c                   eval      jforl1_ugrp = b1ugrp
     c                   eval      jforl1_ldor = b1ldor
     c                   eval      jforl1_modn = b1modn
     c                   eval      jforl1_vare = b1vare
     c     jforl1_key    setll     jforl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jforl1_ogrp = b2ogrp
     c                   eval      jforl1_hgrp = b2hgrp
     c                   eval      jforl1_ugrp = b2ugrp
     c                   eval      jforl1_ldor = b2ldor
     c                   eval      jforl1_modn = b2modn
     c                   eval      jforl1_vare = b2vare
     c     jforl1_key    setll     jforl1
     c                   goto      end_pos
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      jforl2_hgrp = b2hgrp
     c     jforl2_key    setll     jforl2
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      jforl3_ugrp = b2ugrp
     c     jforl3_key    setll     jforl3
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      jforl4_ldor = b2ldor
     c     jforl4_key    setll     jforl4
     c                   goto      end_pos
     c                   endif
      *
      * Modulnr
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      jforl6_modn = b2modn
     c     jforl6_key    setll     jforl6
     c                   goto      end_pos
     c                   endif
      *
      * Vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      jforl5_vare = b2vare
     c     jforl5_key    setll     jforl5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1ldor = b1ldor
     c                   eval      k1modn = b1modn
     c                   eval      k1vare = b1vare
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1ldor = b1ldor
     c                   eval      d1modn = b1modn
     c                   eval      d1vare = b1vare
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ogrp = b2ogrp
     c                   eval      c1hgrp = b2hgrp
     c                   eval      c1ugrp = b2ugrp
     c                   eval      c1ldor = b2ldor
     c                   eval      c1modn = b2modn
     c                   eval      c1vare = b2vare
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1ogrp = 0 and
     c                             c1hgrp = 0 and
     c                             c1ugrp = 0 and
     c                             c1ldor = 0 and
     c                             c1modn = 0 and
     c                             c1vare = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_ldor = c1ldor
     c                   eval      w_modn = c1modn
     c                   eval      w_vare = c1vare
     c                   exsr      sjekk_input
     c                   eval      c1ogrp = w_ogrp
     c                   eval      c1hgrp = w_hgrp
     c                   eval      c1ugrp = w_ugrp
     c                   eval      c1ldor = w_ldor
     c                   eval      c1modn = w_modn
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
     c                   eval      jforlr_ogrp = c1ogrp
     c                   eval      jforlr_hgrp = c1hgrp
     c                   eval      jforlr_ugrp = c1ugrp
     c                   eval      jforlr_ldor = c1ldor
     c                   eval      jforlr_modn = c1modn
     c                   eval      jforlr_vare = c1vare
     c     jforlr_key    chain     jforlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ogrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1ldor = 0
     c                   eval      c1modn = 0
     c                   eval      c1vare = *blank
      *
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1lnav = *blank
     c                   eval      c1tek1 = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jforlr_ogrp = c1ogrp
     c                   eval      jforlr_hgrp = c1hgrp
     c                   eval      jforlr_ugrp = c1ugrp
     c                   eval      jforlr_ldor = c1ldor
     c                   eval      jforlr_modn = c1modn
     c                   eval      jforlr_vare = c1vare
     c     jforlr_key    chain     jforlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2enh1 = jzenh1
     c                   eval      c2enh2 = jzenh2
     c                   eval      c2enh3 = jzenh3
     c                   eval      c2eni1 = jzeni1
     c                   eval      c2eni2 = jzeni2
     c                   eval      c2eni3 = jzeni3
     c                   eval      c2enp1 = jzenp1
     c                   eval      c2enp2 = jzenp2
     c                   eval      c2enp3 = jzenp3
     c                   eval      c2eusr = jzeusr
     c     *dmy          move      jzodat        c2odat
     c     *dmy          move      jzedat        c2edat
     c                   else
     c                   eval      c2enh1 = *blank
     c                   eval      c2enh2 = *blank
     c                   eval      c2enh3 = *blank
     c                   eval      c2eni1 = *blank
     c                   eval      c2eni2 = *blank
     c                   eval      c2eni3 = *blank
     c                   eval      c2enp1 = *blank
     c                   eval      c2enp2 = *blank
     c                   eval      c2enp3 = *blank
     c                   eval      c2etx1 = *blank
     c                   eval      c2etx2 = *blank
     c                   eval      c2etx3 = *blank
     c                   eval      c2itx1 = *blank
     c                   eval      c2itx2 = *blank
     c                   eval      c2itx3 = *blank
     c                   eval      c2ptx1 = *blank
     c                   eval      c2ptx2 = *blank
     c                   eval      c2ptx3 = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1ldor = k1ldor
     c                   eval      c1modn = k1modn
     c                   eval      c1vare = k1vare
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Henter beskrivelse av enheter
      *
     c                   eval      c2etx1 = *blank
     c                   eval      c2etx2 = *blank
     c                   eval      c2etx3 = *blank
     c                   eval      c2itx1 = *blank
     c                   eval      c2itx2 = *blank
     c                   eval      c2itx3 = *blank
     c                   eval      c2ptx1 = *blank
     c                   eval      c2ptx2 = *blank
     c                   eval      c2ptx3 = *blank
      *
     c                   if        c2enh1 <> *blank
     c                   eval      venhl1_eenh = c2enh1
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2etx1 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2enh2 <> *blank
     c                   eval      venhl1_eenh = c2enh2
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2etx2 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2enh3 <> *blank
     c                   eval      venhl1_eenh = c2enh3
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2etx3 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2eni1 <> *blank
     c                   eval      venhl1_eenh = c2eni1
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2itx1 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2eni2 <> *blank
     c                   eval      venhl1_eenh = c2eni2
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2itx2 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2eni3 <> *blank
     c                   eval      venhl1_eenh = c2eni3
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2itx3 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2enp1 <> *blank
     c                   eval      venhl1_eenh = c2enp1
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2ptx1 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2enp2 <> *blank
     c                   eval      venhl1_eenh = c2enp2
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2ptx2 = vaetxt
     c                   endif
     c                   endif
      *
     c                   if        c2enp3 <> *blank
     c                   eval      venhl1_eenh = c2enp3
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2ptx3 = vaetxt
     c                   endif
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2ENH1'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enh1
     c                   parm                    c2etx1
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENH2'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enh2
     c                   parm                    c2etx2
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENH3'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enh3
     c                   parm                    c2etx3
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENI1'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2eni1
     c                   parm                    c2itx1
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENI2'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2eni2
     c                   parm                    c2itx2
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENI3'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2eni3
     c                   parm                    c2itx3
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENP1'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enp1
     c                   parm                    c2ptx1
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENP2'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enp2
     c                   parm                    c2ptx2
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ENP3'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enp3
     c                   parm                    c2ptx3
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
     c                   eval      c2etx1 = *blank
     c                   eval      c2etx2 = *blank
     c                   eval      c2etx3 = *blank
     c                   eval      c2itx1 = *blank
     c                   eval      c2itx2 = *blank
     c                   eval      c2itx3 = *blank
     c                   eval      c2ptx1 = *blank
     c                   eval      c2ptx2 = *blank
     c                   eval      c2ptx3 = *blank
      *
     c                   if        c2enh1 = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      venhl1_eenh = c2enh1
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2etx1 = vaetxt
      *
     c                   if        c2enh2 <> *blank
     c                   eval      venhl1_eenh = c2enh2
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2etx2 = vaetxt
     c                   endif
      *
     c                   if        c2enh3 <> *blank
     c                   eval      venhl1_eenh = c2enh3
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2etx3 = vaetxt
     c                   endif
      *
     c                   if        c2eni1 <> *blank
     c                   eval      venhl1_eenh = c2eni1
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in35 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2itx1 = vaetxt
     c                   endif
      *
     c                   if        c2eni2 <> *blank
     c                   eval      venhl1_eenh = c2eni2
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2itx2 = vaetxt
     c                   endif
      *
     c                   if        c2eni3 <> *blank
     c                   eval      venhl1_eenh = c2eni3
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2itx3 = vaetxt
     c                   endif
      *
     c                   if        c2enp1 <> *blank
     c                   if        c2enp1 <> c2enh1 and
     c                             c2enp1 <> c2enh2 and
     c                             c2enp1 <> c2enh3
     c                   eval      *in38 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      venhl1_eenh = c2enp1
     c     venhl1_key    chain     venhl1
     c                   eval      c2ptx1 = vaetxt
     c                   endif
      *
     c                   if        c2enp2 <> *blank
     c                   if        c2enp2 <> c2enh1 and
     c                             c2enp2 <> c2enh2 and
     c                             c2enp2 <> c2enh3
     c                   eval      *in39 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      venhl1_eenh = c2enp2
     c     venhl1_key    chain     venhl1
     c                   eval      c2ptx2 = vaetxt
     c                   endif
      *
     c                   if        c2enp3 <> *blank
     c                   if        c2enp3 <> c2enh1 and
     c                             c2enp3 <> c2enh2 and
     c                             c2enp3 <> c2enh3
     c                   eval      *in40 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      venhl1_eenh = c2enp3
     c     venhl1_key    chain     venhl1
     c                   eval      c2ptx3 = vaetxt
     c                   endif
      *
      * Oppdatering
      *
     c                   if        c2enh2 = c2enh1
     c                   eval      c2enh2 = *blank
     c                   endif
      *
     c                   if        c2enh3 = c2enh1 or
     c                             c2enh3 = c2enh2
     c                   eval      c2enh3 = *blank
     c                   endif
      *
     c                   if        c2enh2 = *blank and
     c                             c2enh3 <> *blank
     c                   eval      c2enh2 = c2enh3
     c                   eval      c2enh3 = *blank
     c                   endif
      *
     c                   if        c2eni2 = c2eni1
     c                   eval      c2eni2 = *blank
     c                   endif
      *
     c                   if        c2eni3 = c2eni1 or
     c                             c2eni3 = c2eni2
     c                   eval      c2eni3 = *blank
     c                   endif
      *
     c                   if        c2eni2 = *blank and
     c                             c2eni3 <> *blank
     c                   eval      c2eni2 = c2eni3
     c                   eval      c2eni3 = *blank
     c                   endif
      *
     c                   if        c2eni1 = *blank and
     c                             c2eni2 <> *blank
     c                   eval      c2eni1 = c2eni2
     c                   eval      c2eni2 = c2eni3
     c                   eval      c2eni3 = *blank
     c                   endif
      *
     c                   if        c2enp2 = c2enp1
     c                   eval      c2enp2 = *blank
     c                   endif
      *
     c                   if        c2enp3 = c2enp1 or
     c                             c2enp3 = c2enp2
     c                   eval      c2enp3 = *blank
     c                   endif
      *
     c                   if        c2enp2 = *blank and
     c                             c2enp3 <> *blank
     c                   eval      c2enp2 = c2enp3
     c                   eval      c2enp3 = *blank
     c                   endif
      *
     c                   if        c2enp1 = *blank and
     c                             c2enp2 <> *blank
     c                   eval      c2enp1 = c2enp2
     c                   eval      c2enp2 = c2enp3
     c                   eval      c2enp3 = *blank
     c                   endif
      *
     c                   clear                   jforlur
      *
     c                   eval      jforlu_ogrp = c1ogrp
     c                   eval      jforlu_hgrp = c1hgrp
     c                   eval      jforlu_ugrp = c1ugrp
     c                   eval      jforlu_ldor = c1ldor
     c                   eval      jforlu_modn = c1modn
     c                   eval      jforlu_vare = c1vare
     c     jforlu_key    chain     jforlur                            90
      *
     c                   eval      jzenh1 = c2enh1
     c                   eval      jzenh2 = c2enh2
     c                   eval      jzenh3 = c2enh3
     c                   eval      jzeni1 = c2eni1
     c                   eval      jzeni2 = c2eni2
     c                   eval      jzeni3 = c2eni3
     c                   eval      jzenp1 = c2enp1
     c                   eval      jzenp2 = c2enp2
     c                   eval      jzenp3 = c2enp3
     c                   eval      jzeusr = l_user
      *
     c                   time                    jzedat
     c                   time                    jzetim
      *
     c                   if        *in90 = *off
     c                   update    jforlur
     c                   else
     c                   eval      jzfirm = w_firm
     c                   eval      jzogrp = c1ogrp
     c                   eval      jzhgrp = c1hgrp
     c                   eval      jzugrp = c1ugrp
     c                   eval      jzldor = c1ldor
     c                   eval      jzmodn = c1modn
     c                   eval      jzvare = c1vare
     c                   time                    jzodat
     c                   write     jforlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1enh1 = c2enh1
     c                   eval      b1enh2 = c2enh2
     c                   eval      b1enh3 = c2enh3
     c                   eval      b1eni1 = c2eni1
     c                   eval      b1enp1 = c2enp1
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jforlr_ogrp = d1ogrp
     c                   eval      jforlr_hgrp = d1hgrp
     c                   eval      jforlr_ugrp = d1ugrp
     c                   eval      jforlr_ldor = d1ldor
     c                   eval      jforlr_modn = d1modn
     c                   eval      jforlr_vare = d1vare
     c     jforlr_key    chain     jforlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jforlu_ogrp = d1ogrp
     c                   eval      jforlu_hgrp = d1hgrp
     c                   eval      jforlu_ugrp = d1ugrp
     c                   eval      jforlu_ldor = d1ldor
     c                   eval      jforlu_modn = d1modn
     c                   eval      jforlu_vare = d1vare
     c     jforlu_key    chain     jforlur                            90
     c                   delete    jforlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_ldor = k1ldor
     c                   eval      s_modn = k1modn
     c                   eval      s_vare = k1vare
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jforlr_ogrp = k1ogrp
     c                   eval      jforlr_hgrp = k1hgrp
     c                   eval      jforlr_ugrp = k1ugrp
     c                   eval      jforlr_ldor = k1ldor
     c                   eval      jforlr_modn = k1modn
     c                   eval      jforlr_vare = k1vare
     c     jforlr_key    chain     jforlr                             90
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_ldor = k1ldor
     c                   eval      w_modn = k1modn
     c                   eval      w_vare = k1vare
     c                   exsr      sjekk_input
     c                   eval      k1ogrp = w_ogrp
     c                   eval      k1hgrp = w_hgrp
     c                   eval      k1ugrp = w_ugrp
     c                   eval      k1ldor = w_ldor
     c                   eval      k1modn = w_modn
     c                   if        b_feil = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jforlr_ogrp = k1ogrp
     c                   eval      jforlr_hgrp = k1hgrp
     c                   eval      jforlr_ugrp = k1ugrp
     c                   eval      jforlr_ldor = k1ldor
     c                   eval      jforlr_modn = k1modn
     c                   eval      jforlr_vare = k1vare
     c     jforlr_key    chain     jforlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1ldor = s_ldor
     c                   eval      c1modn = s_modn
     c                   eval      c1vare = s_vare
      *
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   read      jforl2                                 15
     c                   when      w_seqe = 'U'
     c                   read      jforl3                                 15
     c                   when      w_seqe = 'L'
     c                   read      jforl4                                 15
     c                   when      w_seqe = 'V'
     c                   read      jforl5                                 15
6.32 c                   when      w_seqe = 'M'
6.32 c                   read      jforl6                                 15
     c                   other
     c                   read      jforl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        jzfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jforl2_hgrp = jzhgrp
     c                   when      w_seqe = 'U'
     c                   eval      jforl3_ugrp = jzugrp
     c                   when      w_seqe = 'L'
     c                   eval      jforl4_ldor = jzldor
     c                   when      w_seqe = 'M'
     c                   eval      jforl6_modn = jzmodn
     c                   when      w_seqe = 'V'
     c                   eval      jforl5_vare = jzvare
     c                   other
     c                   eval      jforl1_ogrp = jzogrp
     c                   eval      jforl1_hgrp = jzhgrp
     c                   eval      jforl1_ugrp = jzugrp
     c                   eval      jforl1_ldor = jzldor
     c                   eval      jforl1_modn = jzmodn
     c                   eval      jforl1_vare = jzvare
     c                   endsl
      *
     c                   eval      b1ogrp = jzogrp
     c                   eval      b1hgrp = jzhgrp
     c                   eval      b1ugrp = jzugrp
     c                   eval      b1utxt = *blank
     c                   eval      b1ldor = jzldor
     c                   eval      b1modn = jzmodn
     c*                  eval      b1lnav = *blank
     c                   eval      b1vare = jzvare
     c                   eval      b1tek1 = *blank
     c                   eval      b1enh1 = jzenh1
     c                   eval      b1enh2 = jzenh2
     c                   eval      b1enh3 = jzenh3
     c                   eval      b1eni1 = jzeni1
     c                   eval      b1enp1 = jzenp1
      *
     c                   select
     c                   when      b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      b1utxt = vgutxt
     c                   endif
     c                   when      b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      b1utxt = vghtxt
     c                   endif
     c                   when      b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      b1utxt = vgotxt
     c                   endif
     c                   endsl
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c*                  eval      b1lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   if        b1vare <> *blank
     c                   eval      jvarl1_vare = b1vare
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off
     c                   eval      b1tek1 = jvtek1
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'H'
     c     jforl2_key    setgt     jforl2
     c                   readp     jforl2
     c                   when      w_seqe = 'U'
     c     jforl3_key    setgt     jforl3
     c                   readp     jforl3
     c                   when      w_seqe = 'L'
     c     jforl4_key    setgt     jforl4
     c                   readp     jforl4
     c                   when      w_seqe = 'V'
     c     jforl5_key    setgt     jforl5
     c                   readp     jforl5
6.32 c                   when      w_seqe = 'M'
6.32 c     jforl6_key    setgt     jforl6
6.32 c                   readp     jforl6
     c                   other
     c     jforl1_key    setgt     jforl1
     c                   readp     jforl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   readp     jforl2
     c                   when      w_seqe = 'U'
     c                   readp     jforl3
     c                   when      w_seqe = 'L'
     c                   readp     jforl4
     c                   when      w_seqe = 'V'
     c                   readp     jforl5
6.32 c                   when      w_seqe = 'M'
6.32 c                   readp     jforl6
     c                   other
     c                   readp     jforl1
     c                   endsl
      *
     c                   if        %eof or
     c                             jzfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jforl2_hgrp = jzhgrp
     c                   when      w_seqe = 'U'
     c                   eval      jforl3_ugrp = jzugrp
     c                   when      w_seqe = 'L'
     c                   eval      jforl4_ldor = jzldor
     c                   when      w_seqe = 'M'
     c                   eval      jforl6_modn = jzmodn
     c                   when      w_seqe = 'V'
     c                   eval      jforl5_vare = jzvare
     c                   other
     c                   eval      jforl1_ogrp = jzogrp
     c                   eval      jforl1_hgrp = jzhgrp
     c                   eval      jforl1_ugrp = jzugrp
     c                   eval      jforl1_ldor = jzldor
     c                   eval      jforl1_modn = jzmodn
     c                   eval      jforl1_vare = jzvare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'H'
     c     jforl2_key    setll     jforl2
     c                   when      w_seqe = 'U'
     c     jforl3_key    setll     jforl3
     c                   when      w_seqe = 'L'
     c     jforl4_key    setll     jforl4
     c                   when      w_seqe = 'V'
     c     jforl5_key    setll     jforl5
6.32 c                   when      w_seqe = 'M'
6.32 c     jforl6_key    setll     jforl6
     c                   other
     c     jforl1_key    setll     jforl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1otxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   eval      k1hgrp = 0
     c                   eval      k1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   eval      c1ugrp = 0
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   eval      k1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   eval      c1utxt = *blank
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'JL500R'
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'JL500R'
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'JL500R'
     c                   parm                    k1ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
6.32  *
6.32  * Modul
6.32  *
6.32 c                   if        w_afld = 'B2MODN'
6.32 c                   call      'JU500R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    b2modn
6.32 c                   parm                    w_mte1
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        w_afld = 'C1MODN'
6.32 c                   call      'JU500R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    c1modn
6.32 c                   parm                    w_mte1
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        w_afld = 'K1MODN'
6.32 c                   call      'JU500R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    k1modn
6.32 c                   parm                    w_mte1
6.32 c                   leavesr
6.32 c                   endif
6.32  *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1tek1
     c                   eval      c1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      k1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_lnav = *blank
     c                   eval      w_tek1 = *blank
      *
      * Sjekk nivå
      *
     c                   if        w_ogrp = 0 and
6.31 c                             w_modn = 0 and
     c                             w_vare = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.31  *
6.31  * Modul
6.31  *
6.31 c                   if        w_modn <> 0
6.31 c                   eval      jmodl1_modn = w_modn
6.31 c     jmodl1_key    chain     jmodl1                             39
6.31 c                   if        *in39 = *on
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   eval      w_ogrp = juogrp
6.31 c                   eval      w_hgrp = juhgrp
6.31 c                   eval      w_ugrp = juugrp
6.31 c                   eval      w_ldor = juldor
6.31 c                   eval      w_mte1 = jumte1
6.31 c                   endif
      *
      * Vare
      *
     c                   if        w_vare <> *blank
     c                   eval      jvarl1_vare = w_vare
     c     jvarl1_key    chain     jvarl1                             40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ogrp = jvogrp
     c                   eval      w_hgrp = jvhgrp
     c                   eval      w_ugrp = jvugrp
     c                   eval      w_ldor = jvldor
     c                   eval      w_modn = jvmodn
     c                   eval      w_tek1 = jvtek1
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0 and
     c                             w_ugrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_ldor <> 0
      *
     c                   if        w_ugrp = 0
     c                   eval      *in38  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      jlevl1_ldor = w_ldor
     c     jlevl1_key    chain     jlevl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_lnav = jlnavn
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1lnav = *blank
6.31 c                   eval      c1mnav = *blank
     c                   eval      c1tek1 = *blank
      *
      * Overgruppe
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1otxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1htxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Leverandør
      *
     c                   if        c1ldor <> 0
     c                   eval      jlevl1_ldor = c1ldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c1lnav = jlnavn
     c                   endif
     c                   endif
6.31  *
6.31  * Modulnr
6.31  *
6.31 c                   if        c1modn <> 0
6.31 c                   eval      jmodl1_modn = c1modn
6.31 c     jmodl1_key    chain     jmodl1
6.31 c                   if        %found
6.31 c                   eval      c1mnav = jumte1
6.31 c                   endif
6.31 c                   endif
      *
      * Varenummer
      *
     c                   if        c1vare <> *blank
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c1tek1 = jvtek1
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på forpakning
      *
     c     jforlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforlr_ogrp
     c                   kfld                    jforlr_hgrp
     c                   kfld                    jforlr_ugrp
     c                   kfld                    jforlr_ldor
     c                   kfld                    jforlr_modn
     c                   kfld                    jforlr_vare
      *
      * Key for oppdatering av forpakning
      *
     c     jforlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforlu_ogrp
     c                   kfld                    jforlu_hgrp
     c                   kfld                    jforlu_ugrp
     c                   kfld                    jforlu_ldor
     c                   kfld                    jforlu_modn
     c                   kfld                    jforlu_vare
      *
      * Key for posisjonering på overgruppe
      *
     c     jforl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl1_ogrp
     c                   kfld                    jforl1_hgrp
     c                   kfld                    jforl1_ugrp
     c                   kfld                    jforl1_ldor
     c                   kfld                    jforl1_modn
     c                   kfld                    jforl1_vare
      *
      * Key for posisjonering på hovedgruppe
      *
     c     jforl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl2_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     jforl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl3_ugrp
      *
      * Key for posisjonering på leverandør
      *
     c     jforl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl4_ldor
      *
      * Key for posisjonering på varenummer
      *
     c     jforl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl5_vare
      *
      * Key for posisjonering på modulnr
      *
     c     jforl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl6_modn
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på enhetskode
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
6.31  *
6.31  * Key for oppslag på modul
6.31  *
6.31 c     jmodl1_key    klist
6.31 c                   kfld                    jmodl1_modn
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     jforl1_key    setll     jforl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
