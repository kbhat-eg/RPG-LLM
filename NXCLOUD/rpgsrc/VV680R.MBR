     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV680R
      * Beskrivelse . . : Vare, sletting av varer uten salg - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141101 HKO  Nytt program
      *       090204 HKO  Lagt inn valg av telle-batch
      *                   Tvinger å sende jobben som bakgrunnsjobb
6.30  *       030914 bof  Nye parametre for valg ved sletting.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
      *
     fvv680d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av datastruktur - parameterliste ut
      *
     d                 ds
     d d_list                        80
     d  d_firm                        3  0 overlay(d_list: 1)
     d  d_kntr                        1  0 overlay(d_list: 4)
     d  d_batc                       10    overlay(d_list: 5)
     d  d_srt1                        1  0 overlay(d_list:15)
     d  d_srt2                        1  0 overlay(d_list:16)
     d  d_srt3                        1  0 overlay(d_list:17)
     d  d_srt4                        1  0 overlay(d_list:18)
6.30 d  d_anto                        2  0 overlay(d_list:19)
6.30 d  d_ants                        2  0 overlay(d_list:21)
6.30 d  d_utgv                        1  0 overlay(d_list:23)
6.30 d  d_dummy                       1    overlay(d_list:24) inz('*')
      *
      * Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_tmst          s               z
     d w_memb          s             10
     d w_tmst_20       s             20
     d w_tmst_12       s             12
     d w_tmst_9        s              9
6.31 d w_mld1          s             50
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
6.30 c                   eval      b1anto = 12
6.30 c                   eval      b1ants = 12
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tag
     c                   endsl
      *
      * Validering av input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tag
     c                   endif
      *
      * Finner unikt member-navn
      *
     c                   time                    w_tmst
     c     *iso0         move      w_tmst        w_tmst_20
     c                   move      w_tmst_20     w_tmst_12
     c                   movel     w_tmst_12     w_tmst_9
      *
     c                   movel     'W'           w_memb
     c                   move      w_tmst_9      w_memb
      *
      * Kaller slette-program
      *
     c                   if        b1srt1 = 0 and                                        .....
     c                             b1srt2 = 0 and                                            .
     c                             b1srt3 = 0 and                                            .
     c                             b1srt4 = 0                                                .
     c                   eval      b1srt1 = 1                                                .
     c                   eval      b1srt2 = 2                                                .
     c                   eval      b1srt3 = 3                                                .
     c                   endif                                                           .....
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_kntr = b1kntr
     c                   eval      d_batc = b1batc
6.30 c                   eval      d_anto = b1anto
6.30 c                   eval      d_ants = b1ants
6.30 c                   eval      d_utgv = b1utgv
     c                   eval      d_srt1 = b1srt1
     c                   eval      d_srt2 = b1srt2
     c                   eval      d_srt3 = b1srt3
     c                   eval      d_srt4 = b1srt4
      *
     c                   call      'VV680C'
     c                   parm                    d_list
     c                   parm                    w_memb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Batchnr
      *
     c                   if        w_afld = 'B1BATC'
     c                   call      'LC501R'
     c                   parm                    w_firm
     c                   parm                    b1batc
     c                   leavesr
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1batc <> *blank
     c                   eval      lbchl1_batc = b1batc
     c     lbchl1_key    chain     lbchl1
     c                   if        not %found
     c                   eval      *in35 = *on                                             . .
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                               . .
     c                   endif
     c                   endif
      *
6.30  * sjekk om både batch og sletting av utgåtte utfylt, ikke tillatt.
6.30  *
6.30 c                   if        b1batc <> *blank and
6.30 c                             b1utgv <> 0
6.31 c                   eval      w_mld1 = 'Kan ikke både fylle ut batch og -
6.31 c                             sletting av utgåtte'
6.31 c                   call      'AA005R'
6.31 c                   parm                    w_mld1
6.31 c                   eval      b_feil = *on
6.30 c                   goto      end_sjekk                                               . .
6.30 c                   endif
6.30  *
6.30  * sjekk om antall måneder > 12
6.30  *
6.30 c                   if        b1anto > 12
6.30 c                   eval      *in37 = *on                                             . .
6.30 c                   eval      b_feil = *on
6.30 c                   goto      end_sjekk                                               . .
6.30 c                   endif
6.30 c                   if        b1ants > 12
6.30 c                   eval      *in38 = *on                                             . .
6.30 c                   eval      b_feil = *on
6.30 c                   goto      end_sjekk                                               . .
6.30 c                   endif
      *
     c                   if        b1srt1 <> 0                                           .....
     c                   if        b1srt1 < 1 or                                       ..... .
     c                             b1srt1 > 3                                              . .
     c                   eval      *in31 = *on                                             . .
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                               . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *
     c                   if        b1srt2 <> 0                                           .....
     c                   if        b1srt2 < 1 or                                       ..... .
     c                             b1srt2 > 3                                              . .
     c                   eval      *in32 = *on                                             . .
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                               . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *
     c                   if        b1srt3 <> 0                                           .....
     c                   if        b1srt3 < 1 or                                       ..... .
     c                             b1srt3 > 3                                              . .
     c                   eval      *in33 = *on                                             . .
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                               . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *
     c                   if        b1srt4 <> 0                                           .....
     c                   if        b1srt4 < 1 or                                       ..... .
     c                             b1srt4 > 3                                              . .
     c                   eval      *in34 = *on                                             . .
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                               . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på batch
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      b1kntr = 1
     c                   eval      b1srt1 = 1
     c                   eval      b1srt2 = 2
     c                   eval      b1srt3 = 3
      *
     c                   endsr
