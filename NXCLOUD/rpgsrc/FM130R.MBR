     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FM130R
      * Beskrivelse . . : Fakturalogg, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100205 GRO  Nytt program
5.60  *       090610 bof  Rettet slik at det bare vises for valgt firma
6.11  *       030211 bof  Justert rettelse 5.60
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.NU  *                   NB justert også 11.12.15.
7.xx  * 7.00  290316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffnufl1    if   e           k disk    rename(fnufpfr:fnufl1r)
     ffnufl2    if   e           k disk    rename(fnufpfr:fnufl2r)
     ffnuflr    if   e           k disk    rename(fnufpfr:fnuflrr)
     ffnuflu    uf a e           k disk    rename(fnufpfr:fnuflur)
     ffmftl1    if   e           k disk    rename(fmftpfr:fmftl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffm130d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fnufl1_aarr     s                   like(fnuaar)
     d fnufl1_binr     s                   like(fnubin)
     d fnufl2_faty     s                   like(fnufat)
     d fnuflr_aarr     s                   like(fnuaar)
     d fnuflr_binr     s                   like(fnubin)
     d fnuflu_aarr     s                   like(fnuaar)
     d fnuflu_binr     s                   like(fnubin)
     d fmftl1_faty     s                   like(fmtfat)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_dato          s               d   datfmt(*iso)
     d w_kdat          s                   like(fnukda)
      *
      * Definering av parametre
      *
     d p_aarr          s                   like(fnuaar)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(15)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      ny_les
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     fnufl1_key    setll     fnufl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c2aarr = b2aarr
     c                   eval      c2binr = b1binr
     c                   eval      c2faty = b1faty
     c                   eval      c2kund = b1kund
     c                   eval      c2besk = b1besk
     c                   eval      c2knav = b1knav
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1aarr = b2aarr
     c                   eval      d1binr = b1binr
     c                   eval      d1faty = b1faty
     c                   eval      d1kund = b1kund
     c                   eval      d1besk = b1besk
     c                   eval      d1knav = b1knav
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c2aarr = b2aarr
     c                   eval      c2binr = b1binr
     c                   eval      c2faty = b1faty
     c                   eval      c2kund = b1kund
     c                   eval      c2besk = b1besk
     c                   eval      c2knav = b1knav
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      fnuflr_binr = b1binr
     c     fnuflr_key    chain     fnuflr
      *
     c                   if        %found
     c                   if        fnukda <> *loval
     c     *dmy          move      fnukda        c2kdat
     c                   else
     c                   eval      c2kdat = *zero
     c                   endif
      *
     c                   if        fnukti <> *loval
     c     *hms          move      fnukti        c2ktim
     c                   else
     c                   eval      c2ktim = *zero
     c                   endif
      *
     c                   eval      c2kwsi = fnukws
     c                   eval      c2tell = fnutel
      *
     c                   if        fnuoda <> *loval
     c     *dmy          move      fnuoda        c2odat
     c                   else
     c                   eval      c2odat = *zero
     c                   endif
      *
     c                   if        fnuoti <> *loval
     c     *hms          move      fnuoti        c2otim
     c                   else
     c                   eval      c2otim = *zero
     c                   endif
      *
     c                   eval      c2owsi = fnuwsi
      *
     c                   if        fnueda <> *loval
     c     *dmy          move      fnueda        c2edat
     c                   else
     c                   eval      c2edat = *zero
     c                   endif
      *
     c                   if        fnuoti <> *loval
     c     *hms          move      fnueti        c2etim
     c                   else
     c                   eval      c2etim = *zero
     c                   endif
      *
     c                   eval      c2eusr = fnueus
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        *inkj
     c                   call      'SO510R'
     c                   parm                    w_firm
     c                   parm                    c2aarr
     c                   parm                    c2kund
     c                   parm                    c2binr
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        *in30
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   if        *inkk
      *
     c                   eval      fnuflu_binr = c2binr
     c     fnuflu_key    chain     fnuflur
      *
     c                   eval      e1aarr = c2aarr
     c                   eval      e1binr = c2binr
     c                   eval      e1faty = c2faty
     c                   eval      e1kund = c2kund
     c                   eval      e1besk = c2besk
     c                   eval      e1knav = c2knav
      *
     c                   exfmt     e1win
      *
     c                   if        *inkc or *inkl
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        %found
     c                   eval      fnukda = *loval
     c                   eval      fnukti = *loval
     c                   eval      fnukws = *blank
     c                   time                    fnueda
     c                   time                    fnueti
     c                   eval      fnueus = l_user
     c                   update    fnuflur
     c                   endif
      *
     c                   eval      c2kdat = *zero
     c                   eval      c2ktim = *zero
     c                   eval      c2kwsi = *blank
     c                   goto      c2tagb
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      fnuflr_binr = d1binr
     c     fnuflr_key    chain     fnuflr
      *
     c                   eval      d1aarr = fnuaar
     c                   eval      d1faty = fnufat
     c                   eval      d1binr = fnubin
     c                   eval      d1kund = fnukun
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      fnuflu_binr = d1binr
     c     fnuflu_key    chain     fnuflur
     c                   delete    fnuflur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av filter-bildet (H1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c                   if        h1kund = *zero
     c                   eval      h1knav = *blank
     c                   endif
      *
     c     h1taga        tag
     c                   exfmt     h1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_h1win
     c                   endif
      *
     c                   if        *inkd
     c                   if        w_afld = 'H1KUND'
     C                   call      'RK500R'
     C                   parm                    w_firm
     C                   parm                    h1kund
     C                   parm                    h1knav
     c                   endif
      *
     c                   if        w_afld = 'H1FATY'
     C                   call      'FM510R'
     C                   parm                    w_firm
     C                   parm                    h1faty
     C                   parm                    h1besk
     c                   endif
      *
     c                   goto      h1taga
     c                   endif
      *
     c                   if        h1kdat <> 0
     c     *dmy          test(d)                 h1kdat                 31
     c                   if        *in31  = *on
     c                   goto      h1taga
     c                   endif
     c                   endif
      *
     c     end_h1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      fnufl1                                 15
      *
5.61 c                   if        *in15 or
6.10 c                             fnufir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        fnuaar <> b2aarr
     c                   iter
     c                   endif
      *
      *
      * Sjekk mot angitt filterverdier
      *
     c                   if        h1usen = '1'
     c                   if        fnukda <> *loval
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        h1faty <> *zero
     c                   if        fnufat <> h1faty
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        h1kund <> *zero
     c                   if        fnukun <> h1kund
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        h1binr <> *zero
     c                   if        fnubin <> h1binr
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        h1kdat <> *zero
     c     *dmy          move      h1kdat        w_kdat
     c                   if        fnukda = *loval
     c                   iter
     c                   endif
     c                   if        fnukda < w_kdat
     c                   iter
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1binr = fnubin
     c                   eval      b1faty = fnufat
     c                   eval      fmftl1_faty = fnufat
     c     fmftl1_key    chain     fmftl1
     c                   if        %found
     c                   eval      b1besk = fmtbes
     c                   else
     c                   eval      b1besk = *blank
     c                   endif
      *
     c                   eval      b1kund = fnukun
     c                   eval      rkunl1_kund = fnukun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b1knav = rknavn
     c                   else
     c                   eval      b1knav = *blank
     c                   endif
     c                   if        fnukda <> *loval
     c     *dmy          move      fnukda        b1kdat
     c                   else
     c                   eval      b1kdat = *zero
     c                   endif
     c                   if        fnukti <> *loval
     c     *hms          move      fnukti        b1ktim
     c                   else
     c                   eval      b1ktim = *zero
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for ny les (bytte av årstall)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_les        begsr
      *
     c                   exsr      clr_subfile
      *
     c                   eval      fnufl1_aarr = b2aarr
     c                   eval      fnufl1_binr = *zero
     c     fnufl1_key    setll     fnufl1
      *
     c                   exsr      crt_subfile
      *
     c                   eval      fnuflr_aarr = b2aarr
     c                   eval      fnuflu_aarr = b2aarr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_aarr
      *
      * Key for posisjonering på fakturalogg
      *
     c     fnufl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnufl1_aarr
     c                   kfld                    fnufl1_binr
      *
      * Key for posisjonering på fakturatype
      *
     c     fnufl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnufl2_faty
      *
      * Key for les på fakturalogg
      *
     c     fnuflr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnuflr_aarr
     c                   kfld                    fnuflr_binr
      *
      * Key for oppdatering av fakturatype
      *
     c     fnuflu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnuflu_aarr
     c                   kfld                    fnuflu_binr
      *
      * Key for les av fakturatype
      *
     c     fmftl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmftl1_faty
      *
      * Key for oppslag på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   eval      w_firm = l_firm
      *
     c                   if        p_aarr <> *zero
     c                   eval      b2aarr = p_aarr
     c                   else
     c                   time                    w_dato
     c                   eval      b2aarr = %subdt(w_dato:*years)
     c                   endif
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fnufl1_aarr = b2aarr
     c                   eval      fnufl1_binr = *zero
     c     fnufl1_key    setll     fnufl1
      *
     c                   exsr      crt_subfile
      *
     c                   eval      fnuflr_aarr = b2aarr
     c                   eval      fnuflu_aarr = b2aarr
      *
     c                   endsr
