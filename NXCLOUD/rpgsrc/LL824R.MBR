     H/TITLE  ASLAGR: Sletter varer i Lager-register, Oppdateringen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LL824R                                       *
      *  Beskrivelse . . : Sletter varer i Lager-register, Oppdateringen*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign *
      * --------- ------  ----------------------------------------------*
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *
     FVLAGL1    IF   E           K DISK
     F                                     RENAME(VLAGPFR:VLAGL1R)
     FVLAGLU    UF   E           K DISK
     F                                     RENAME(VLAGPFR:VLAGLUR)
      *
     FLBCHL1    IF   E           K DISK
     F                                     RENAME(LBCHPFR:LBCHL1R)
     FLTELL2    IF   E           K DISK
     F                                     RENAME(LTELPFR:LTELL2R)
      *
     D                 DS
      *                                                      Parameter-inn
     D  DSPREC                 1     64
     D  DSBATC                 1     10
     D  DSVAL1                11     11  0
     D  DSVAL2                12     12  0
      *
     D/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  Local Data Area                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                UDS
     D  DSFIRM               944    946  0
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utplukk, Setter inn riktige parameterverdier          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *BLANK        LAGVAR
     C                   MOVE      *ZERO         LAGNUM
     C     LAGKEY        SETLL     VLAGL1R
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Leser poster fra register for utplukks-test                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSTART        TAG
      *
     C                   MOVE      *OFF          *IN90
     C                   READ      VLAGL1R                                90
     C     *IN90         CABEQ     *ON           XSLUTT
      *
      *  Sjekk   Er det korrekt firma som leses,    Ellers hopp til Slutt
      *
     C     VLFIRM        CABNE     LAGFIR        XSLUTT
      *
      *  Sjekk   Finnes det beholdning         ,    Ellers hopp til Start
      *
     C     VLBEHL        CABNE     *ZERO         XSTART
      *
      *  Sjekk   Finnes tall for Ordre/Bestilling,  Ellers hopp til Start
      *
     C     VLPAKK        CABNE     *ZERO         XSTART
     C     VLOORD        CABNE     *ZERO         XSTART
     C     VLORES        CABNE     *ZERO         XSTART
     C     VLBBES        CABNE     *ZERO         XSTART
     C     VLBRES        CABNE     *ZERO         XSTART
      *
      *  Sjekk   Finnes tall for HIÅ           ,    Ellers hopp til Start
      *
     C     VLINNG        CABNE     *ZERO         XSTART
     C     VLUTTA        CABNE     *ZERO         XSTART
     C     VLJUST        CABNE     *ZERO         XSTART
     C     VLSALG        CABNE     *ZERO         XSTART
     C     VLRETU        CABNE     *ZERO         XSTART
     C     VLBEVE        CABNE     *ZERO         XSTART
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdater Telle-register med nye opplysninger fra vare-register *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Hent opplysninger fra LAGER-TELLE-REGISTER
      *
     C                   MOVE      VLVARE        TVAVAR
     C                   MOVE      VLLAGE        TVALAG
     C     TVAKEY        CHAIN     LTELL2                             92
      *
      *  Dersom record finnes i LAGER-TELLE-REGISTER, skal
      *  denne varen IKKE slettes fra LAGER-REGISTER
      *
     C     *IN92         CABEQ     *OFF          XSTART
      *
      *  Slettet records fra LAGER-REGISTER
      *
     C                   MOVE      VLVARE        LAGVAR
     C                   MOVE      VLLAGE        LAGNUM
     C     LAGKEY        CHAIN     VLAGLU                             94
     C     *IN94         IFEQ      *OFF
     C                   DELETE    VLAGLUR
     C                   END
      *
      *  Velg neste
      *
     C                   GOTO      XSTART
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
      *  Redefinering av felter
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VLFIRM        LAGFIR
     C     *LIKE         DEFINE    VLVARE        LAGVAR
     C     *LIKE         DEFINE    VLLAGE        LAGNUM
      *
     C     *LIKE         DEFINE    LCFIRM        BATFIR
     C     *LIKE         DEFINE    LCBATC        BATNUM
      *
     C     *LIKE         DEFINE    LEFIRM        TVAFIR
     C     *LIKE         DEFINE    LEBATC        TVABAT
     C     *LIKE         DEFINE    LENUMM        TVANUM
     C     *LIKE         DEFINE    LESUFF        TVASUF
     C     *LIKE         DEFINE    LEVARE        TVAVAR
     C     *LIKE         DEFINE    LELAGE        TVALAG
     C     *LIKE         DEFINE    LEDLOP        TVALOP
      *
      *  Key for file : LAGER-REGISTER
      *
     C     LAGKEY        KLIST
     C                   KFLD                    LAGFIR
     C                   KFLD                    LAGVAR
     C                   KFLD                    LAGNUM
      *
      *  Key for BATCH-REGISTER
      *
     C     BATKEY        KLIST
     C                   KFLD                    BATFIR
     C                   KFLD                    BATNUM
      *
      *  Key for LAGER-TELLE-VARE-REGISTER
      *
     C     TVAKEY        KLIST
     C                   KFLD                    TVAFIR
     C                   KFLD                    TVABAT
     C                   KFLD                    TVANUM
     C                   KFLD                    TVASUF
     C                   KFLD                    TVAVAR
     C                   KFLD                    TVALAG
     C                   KFLD                    TVALOP
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WWPARM           64
     C                   MOVE      WWPARM        DSPREC
      *
      *  Legge inn firmanummer i nøkklene
      *
     C                   MOVE      DSFIRM        LAGFIR
     C                   MOVE      DSFIRM        BATFIR
     C                   MOVE      DSFIRM        TVAFIR
      *
     C                   MOVE      DSBATC        BATNUM
     C                   MOVE      DSBATC        TVABAT
      *
     C                   MOVE      *ZERO         TVASUF
     C                   MOVE      *ZERO         TVALOP
      *
      *  Hent opplysninger fra BATCH-REGISTER
      *
     C     BATKEY        CHAIN     LBCHL1R                            90
     C                   MOVE      LCNUMM        TVANUM
      *
     CSR                 ENDSR
