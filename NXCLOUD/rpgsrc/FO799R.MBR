     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO799R
      * Beskrivelse . . : Sende melding som sms
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       271005 BOF  Nytt program
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunpf    if   e           k disk
     frlevpf    if   e           k disk
     ffkprlr    if   e           k disk    rename(fkprpfr:fkprl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fra29l1    if   e           k disk    rename(ra29pfr:ra29l1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     ffo799d    cf   e             workstn
      *
      * Definering av parametere
      *
     d p_kule          s              1
     d p_kund          s                   like(rkkund)
     d p_kpro          s                   like(flkpro)
     d p_navn          s                   like(rknavn)
     d p_emal          s                   like(rkemal)
     d p_ema2          s                   like(rkemal)
     d p_emne          s             50
     d p_pers          s             50
     d p_inif          s             40
     d p_in03          s              1
6.nu d p_numm          s              8  0
     d p_selg          s              4  0
      *
      * Definering av LDA
      *
     d                uds
     d l_ruti                800    809
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d rkunpf_kund     s                   like(rkkund)
     d rlevpf_levr     s                   like(rllevr)
     d fkprlr_kund     s                   like(flkund)
     d fkprlr_kpro     s                   like(flkpro)
     d ra09l1_ikod     s                   like(raikod)
     d ra29l1_ckod     s                   like(reckod)
     d aforl1_ruti     s                   like(afruti)
     d afirl1_ffir     s                   like(aaffir)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_firm          s                   like(rkfirm)
6.nu d w_numm          s              8
     d w_mobo          s                   like(rkmobn)
     d w_cadr          s                   like(recadr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c                   eval      b1kund = *zero
     c                   eval      b1navn = *blank
     c                   eval      b1pers = *blank
     c                   eval      b1emal = *blank
     c                   eval      b1mobn = *blank
     c                   eval      p_ema2 = *blank
      *
     c                   if        p_kule = 'K'
     c                   eval      rkunpf_kund = p_kund
     c     rkunpf_key    chain     rkunpfr
      *
     c                   if        %found
     c                   eval      b1kund = rkkund
     c                   eval      b1navn = rknavn
     c                   eval      b1mobn = rkmobn
     c                   eval      w_mobo = 'T '
     c                   endif
     c                   endif
      *
     c                   if        b1mobn = *blank
     c                   eval      fkprlr_kund = p_kund
     c                   eval      fkprlr_kpro = p_kpro
     c     fkprlr_key    chain     fkprlr
     c                   if        %found(fkprlr)
     c                   movel     rkmobn        b1mobn
     c                   endif
     c                   endif
     c
      *
      * Finne mobiloperatør
      *
     c                   eval      ra29l1_ckod = w_mobo
     c     ra29l1_key    chain     ra29l1r
     c                   if        %found
     c                   eval      b1ctxt = rectxt
     c                   eval      w_cadr = recadr
     c                   else
     c                   eval      b1ctxt = *blank
     c                   eval      w_cadr = *blank
     c                   endif
      *
      * sette sammen adresse
      *
     c                   eval      b1emal = %subst(b1mobn:1:2) +
     c                                      %subst(b1mobn:4:2) +
     c                                      %subst(b1mobn:7:2) +
     c                                      %subst(b1mobn:10:2) + '@' +
     c                                           (w_cadr)
      *
      * Finne e-mail-adresse for avsender
      *
     c                   eval      ra09l1_ikod = p_selg
     c     ra09l1_key    chain     ra09l1r
     c                   if        %found
     c                   eval      p_ema2 =  raiema
     c                   eval      b1pers = 'Vennlig hilsen: ' + raitxt
     c                   endif
      *
      * Finne informasjon fra rutineregister
      * og legge dette inn i parametere
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        %found
     c                   eval      p_inif = afhex1 + afhex2
     c                   endif
      *
      *  Henter inn firmaopplysninger
      *  og klargjør variabel for emne
      *  i skjermbildet
      *
     c                   eval      afirl1_ffir = w_firm
     c     afirl1_key    chain     afirl1r
     c                   eval      b1emne = %trim(aftxt1) + ' ' +
     c                                           (w_numm) + ' ' +
     c                                           ('fra')  + ' ' +
     c                                      %trim(aafnav)
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   eval      p_in03 = *on
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
      *                  if        *inkd = *on
      *                  exsr      spØrring
      *                  goto      b1taga
      *                  endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   exsr      flytt_par
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
      *                  if        w_afld = 'B1KUND'
      *                  eval      b1kpro = 0
      *                  call      'RK500R'
      *                  parm                    w_firm
      *                  parm                    b1kund
      *                  parm                    w_knav
      *                  goto      end_spØrr
      *                  endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Kontroller at det er
      * oppgitt e-mail-adresse
      *
     c                   if        b1emal = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      p_navn = b1navn
     c                   eval      p_pers = b1pers
     c                   eval      p_emne = b1emne
     c                   eval      p_emal = b1emal
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter-liste
      *
     c     *entry        plist
     c                   parm                    p_kule
     c                   parm                    p_kund
     c                   parm                    p_kpro
6.nu c                   parm                    p_numm
     c                   parm                    p_selg
     c                   parm                    p_navn
     c                   parm                    p_emal
     c                   parm                    p_emne
     c                   parm                    p_pers
     c                   parm                    p_ema2
     c                   parm                    p_inif
     c                   parm                    p_in03
      *
      * Key for oppslag på kunde
      *
     c     rkunpf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunpf_kund
      *
      * Key for oppslag på leverandør
      *
     c     rlevpf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevpf_levr
      *
      * Key for oppslag på kunde
      *
     c     fkprlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlr_kund
     c                   kfld                    fkprlr_kpro
      *
      * Key for oppslag på selgerregister
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på mobiloperatører
      *
     c     ra29l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra29l1_ckod
      *
      * Key for oppslag på rutineregister
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for oppslag på firma-register
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Flytt over ordrenummer til alfafelt
      *
6.nu c                   move      p_numm        w_numm
      *
     c                   endsr
      *
