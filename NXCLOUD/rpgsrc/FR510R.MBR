     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FR510R
      * Beskrivelse . . : Rabattmatrise, vise for kunde/kundeprosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280499 HKO  Nytt program
      *       170300 HKO  Lagt inn leverandør som nivå i rabattmatrisen
      *       040400 HKO  Viser utplukksparametre
      *       150503 HKO  Lagt inn kundekategori som nivå i rabattmatrisen
      *       161103 HKO  Erstattet rabattgruppe med varegruppe og modulnr
      *                   Lagt inn rabatt på bunnpris
      *                   Lagt inn rollback
5.70  * PRGR  231008 BOF  Utvidet med prisgruppe
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.41  * R6.40 110118 bof  Utvidet med påslag
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffrablr    if   e           k disk    rename(frabpfr:frablrr)
     ffrabl1    if   e           k disk    rename(frabpfr:frabl1r)
     ffrabl2    if   e           k disk    rename(frabpfr:frabl2r)
     ffrabl3    if   e           k disk    rename(frabpfr:frabl3r)
     ffrabl4    if   e           k disk    rename(frabpfr:frabl4r)
     ffrabl5    if   e           k disk    rename(frabpfr:frabl5r)
     ffrabl6    if   e           k disk    rename(frabpfr:frabl6r)
     ffrabl7    if   e           k disk    rename(frabpfr:frabl7r)
     ffrabl8    if   e           k disk    rename(frabpfr:frabl8r)
     ffrabl9    if   e           k disk    rename(frabpfr:frabl9r)
     ffrabla    if   e           k disk    rename(frabpfr:frablar)
     ffrablb    if   e           k disk    rename(frabpfr:frablbr)
     ffrablc    if   e           k disk    rename(frabpfr:frablcr)
5.70 ffrabld    if   e           k disk    rename(frabpfr:frabldr)
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
     fra11pf    if   e           k disk    rename(ra11pfr:ra11pfr)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
      *
     ffr510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(frfirm)
     d p_kund          s                   like(frkund)
     d p_kpro          s                   like(frkpro)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
5.70 d frablr_prgr     s                   like(frprgr)
     d frablr_rkat     s                   like(frrkat)
     d frablr_kkat     s                   like(frkkat)
     d frablr_kund     s                   like(frkund)
     d frablr_kpro     s                   like(frkpro)
     d frablr_ogrp     s                   like(frogrp)
     d frablr_hgrp     s                   like(frhgrp)
     d frablr_ugrp     s                   like(frugrp)
     d frablr_ldor     s                   like(frldor)
     d frablr_modn     s                   like(frmodn)
     d frablr_vare     s                   like(frvare)
     d frablr_enhe     s                   like(frenhe)
     d frablr_lety     s                   like(frlety)
5.70 d frabl1_prgr     s                   like(frprgr)
     d frabl1_rkat     s                   like(frrkat)
     d frabl1_kkat     s                   like(frkkat)
     d frabl1_kund     s                   like(frkund)
     d frabl1_kpro     s                   like(frkpro)
     d frabl1_ogrp     s                   like(frogrp)
     d frabl1_hgrp     s                   like(frhgrp)
     d frabl1_ugrp     s                   like(frugrp)
     d frabl1_ldor     s                   like(frldor)
     d frabl1_modn     s                   like(frmodn)
     d frabl1_vare     s                   like(frvare)
     d frabl1_enhe     s                   like(frenhe)
     d frabl1_lety     s                   like(frlety)
     d frabl2_kkat     s                   like(frkkat)
     d frabl3_kund     s                   like(frkund)
     d frabl3_kpro     s                   like(frkpro)
     d frabl3_ogrp     s                   like(frogrp)
     d frabl3_hgrp     s                   like(frhgrp)
     d frabl3_ugrp     s                   like(frugrp)
     d frabl3_ldor     s                   like(frldor)
     d frabl4_kpro     s                   like(frkpro)
     d frabl5_ogrp     s                   like(frogrp)
     d frabl5_hgrp     s                   like(frhgrp)
     d frabl5_ugrp     s                   like(frugrp)
     d frabl6_hgrp     s                   like(frhgrp)
     d frabl6_ugrp     s                   like(frugrp)
     d frabl7_ugrp     s                   like(frugrp)
     d frabl8_ldor     s                   like(frldor)
     d frabl9_modn     s                   like(frmodn)
     d frabla_vare     s                   like(frvare)
     d frabla_enhe     s                   like(frenhe)
5.70 d frabla_prgr     s                   like(frprgr)
     d frabla_lety     s                   like(frlety)
     d frablb_enhe     s                   like(frenhe)
     d frablc_lety     s                   like(frlety)
5.70 d frabld_rkat     s                   like(frrkat)
     d ra06pf_fkod     s                   like(rafkod)
     d ra11pf_kkod     s                   like(rakkod)
5.70 d ra30l1_dkod     s                   like(redkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d venhl1_enhe     s                   like(vaeenh)
     d vltpl1_lety     s                   like(vylety)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_mtxt          s             35
      *
     d w_firm          s                   like(frfirm)
     d w_rkat          s                   like(frrkat)
     d w_kkat          s                   like(frkkat)
     d w_kund          s                   like(frkund)
     d w_kpro          s                   like(frkpro)
     d w_rtxt          s                   like(raftxt)
     d w_ktxt          s                   like(raktxt)
5.70 d w_dtxt          s                   like(redtxt)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
     d w_gtxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
     d w_vare          s                   like(vvvare)
     d w_vtxt          s                   like(vvtek1)
     d w_etxt          s                   like(vaetxt)
     d w_ltxt          s                   like(vybesk)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Bytting mellom å vise ordinære rabatter/priser og datostyrte
      * rabatter/priser
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *off
     c                   eval      *in80 = *on
     c                   else
     c                   eval      *in80 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
5.70 c                   if        b2prgr <> *blank  or
5.70 c                             b2rkat <> 0 or
     c                             b2kkat <> 0 or
     c                             b2kund <> 0 or
     c                             b2kpro <> 0 or
     c                             b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2ldor <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank or
     c                             b2enhe <> *blank or
     c                             b2lety <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      frabl2_kkat = b1kkat
     c     frabl2_key    setll     frabl2
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      frabld_rkat = b1rkat
5.70 c     frabld_key    setll     frabld
     c                   when      w_seqe = 'K'
     c                   eval      frabl3_kund = b1kund
     c                   eval      frabl3_kpro = b1kpro
     c                   eval      frabl3_ogrp = b1ogrp
     c                   eval      frabl3_hgrp = b1hgrp
     c                   eval      frabl3_ugrp = b1ugrp
     c                   eval      frabl3_ldor = b1ldor
     c     frabl3_key    setll     frabl3
     c                   when      w_seqe = 'P'
     c                   eval      frabl4_kpro = b1kpro
     c     frabl4_key    setll     frabl4
     c                   when      w_seqe = 'O'
     c                   eval      frabl5_ogrp = b1ogrp
     c                   eval      frabl5_hgrp = b1hgrp
     c                   eval      frabl5_ugrp = b1ugrp
     c     frabl5_key    setll     frabl5
     c                   when      w_seqe = 'H'
     c                   eval      frabl6_hgrp = b1hgrp
     c                   eval      frabl6_ugrp = b1ugrp
     c     frabl6_key    setll     frabl6
     c                   when      w_seqe = 'U'
     c                   eval      frabl7_ugrp = b1ugrp
     c     frabl7_key    setll     frabl7
     c                   when      w_seqe = 'L'
     c                   eval      frabl8_ldor = b1ldor
     c     frabl8_key    setll     frabl8
     c                   when      w_seqe = 'M'
     c                   eval      frabl9_modn = b1modn
     c     frabl9_key    setll     frabl9
     c                   when      w_seqe = 'V'
     c                   eval      frabla_vare = b1vare
5.70 c                   eval      frabla_prgr = b1prgr
     c                   eval      frabla_enhe = b1enhe
     c                   eval      frabla_lety = b1lety
     c     frabla_key    setll     frabla
     c                   when      w_seqe = 'E'
     c                   eval      frablb_enhe = b1enhe
     c     frablb_key    setll     frablb
     c                   when      w_seqe = 'T'
     c                   eval      frablc_lety = b1lety
     c     frablc_key    setll     frablc
     c                   other
5.70 c                   eval      frabl1_prgr = b1prgr
     c                   eval      frabl1_rkat = b1rkat
     c                   eval      frabl1_kkat = b1kkat
     c                   eval      frabl1_kund = b1kund
     c                   eval      frabl1_kpro = b1kpro
     c                   eval      frabl1_ogrp = b1ogrp
     c                   eval      frabl1_hgrp = b1hgrp
     c                   eval      frabl1_ugrp = b1ugrp
     c                   eval      frabl1_ldor = b1ldor
     c                   eval      frabl1_modn = b1modn
     c                   eval      frabl1_vare = b1vare
     c                   eval      frabl1_enhe = b1enhe
     c                   eval      frabl1_lety = b1lety
     c     frabl1_key    setll     frabl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
5.70  * Posisjoner startverdi på prisgruppe
5.70  *
5.70 c                   if        b2prgr <> *blank
5.70 c                   eval      w_seqe = *blank
5.70 c                   eval      frabl1_prgr = b2prgr
5.70 c                   eval      frabl1_rkat = b2rkat
5.70 c                   eval      frabl1_kkat = b2kkat
5.70 c                   eval      frabl1_kund = b2kund
5.70 c                   eval      frabl1_kpro = b2kpro
5.70 c                   eval      frabl1_ogrp = b2ogrp
5.70 c                   eval      frabl1_hgrp = b2hgrp
5.70 c                   eval      frabl1_ugrp = b2ugrp
5.70 c                   eval      frabl1_ldor = b2ldor
5.70 c                   eval      frabl1_modn = b2modn
5.70 c                   eval      frabl1_vare = b2vare
5.70 c                   eval      frabl1_enhe = b2enhe
5.70 c                   eval      frabl1_lety = b2lety
5.70 c     frabl1_key    setll     frabl1
5.70 c                   goto      end_pos
5.70 c                   endif
      *
5.70  * Posisjoner startverdi på rabattkategori
5.70  *
5.70 c                   if        b2rkat <> 0
5.70 c                   eval      w_seqe = 'R'
5.70 c                   eval      frabld_rkat = b2rkat
5.70 c     frabld_key    setll     frabld
5.70 c                   goto      end_pos
5.70 c                   endif
      *
      * Posisjoner startverdi på kundekategori
      *
     c                   if        b2kkat <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      frabl2_kkat = b2kkat
     c     frabl2_key    setll     frabl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      frabl3_kund = b2kund
     c                   eval      frabl3_kpro = b2kpro
     c                   eval      frabl3_ogrp = b2ogrp
     c                   eval      frabl3_hgrp = b2hgrp
     c                   eval      frabl3_ugrp = b2ugrp
     c                   eval      frabl3_ldor = b2ldor
     c     frabl3_key    setll     frabl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      w_seqe = 'P'
     c                   eval      frabl4_kpro = b2kpro
     c     frabl4_key    setll     frabl4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      frabl5_ogrp = b2ogrp
     c                   eval      frabl5_hgrp = b2hgrp
     c                   eval      frabl5_ugrp = b2ugrp
     c     frabl5_key    setll     frabl5
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      frabl6_hgrp = b2hgrp
     c                   eval      frabl6_ugrp = b2ugrp
     c     frabl6_key    setll     frabl6
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      frabl7_ugrp = b2ugrp
     c     frabl7_key    setll     frabl7
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      frabl8_ldor = b2ldor
     c     frabl8_key    setll     frabl8
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på modulnr
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      frabl9_modn = b2modn
     c     frabl9_key    setll     frabl9
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      frabla_vare = b2vare
5.70 c                   eval      frabla_prgr = b2prgr
     c                   eval      frabla_enhe = b2enhe
     c                   eval      frabla_lety = b2lety
     c     frabla_key    setll     frabla
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på enhet
      *
     c                   if        b2enhe <> *blank
     c                   eval      w_seqe = 'E'
     c                   eval      frablb_enhe = b2enhe
     c     frablb_key    setll     frablb
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'T'
     c                   eval      frablc_lety = b2lety
     c     frablc_key    setll     frablc
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2rkat = 0
     c                   eval      b2kkat = 0
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
     c                   eval      b2enhe = *blank
     c                   eval      b2lety = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
5.70 c                   eval      c2prgr = b1prgr
     c                   eval      c2rkat = b1rkat
     c                   eval      c2kkat = b1kkat
     c                   eval      c2kund = b1kund
     c                   eval      c2kpro = b1kpro
     c                   eval      c2ogrp = b1ogrp
     c                   eval      c2hgrp = b1hgrp
     c                   eval      c2ugrp = b1ugrp
     c                   eval      c2ldor = b1ldor
     c                   eval      c2modn = b1modn
     c                   eval      c2vare = b1vare
     c                   eval      c2enhe = b1enhe
     c                   eval      c2lety = b1lety
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
5.70 c                   eval      frablr_prgr = c2prgr
     c                   eval      frablr_rkat = c2rkat
     c                   eval      frablr_kkat = c2kkat
     c                   eval      frablr_kund = c2kund
     c                   eval      frablr_kpro = c2kpro
     c                   eval      frablr_ogrp = c2ogrp
     c                   eval      frablr_hgrp = c2hgrp
     c                   eval      frablr_ugrp = c2ugrp
     c                   eval      frablr_ldor = c2ldor
     c                   eval      frablr_modn = c2modn
     c                   eval      frablr_vare = c2vare
     c                   eval      frablr_enhe = c2enhe
     c                   eval      frablr_lety = c2lety
     c     frablr_key    chain     frablr
      *
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2rab1 = frrab1
     c                   eval      c2ra1d = frra1d
     c                   eval      c2ra1f = 0
     c                   eval      c2ra1t = 0
     c                   eval      c2rab2 = frrab2
     c                   eval      c2ra2d = frra2d
     c                   eval      c2ra2f = 0
     c                   eval      c2ra2t = 0
     c                   eval      c2rabn = frrabn
     c                   eval      c2rand = frrand
     c                   eval      c2ranf = 0
     c                   eval      c2rant = 0
     c                   eval      c2rabb = frrabb
     c                   eval      c2rabd = frrabd
     c                   eval      c2rbuf = 0
     c                   eval      c2rbut = 0
     c                   eval      c2rabt = frrabt
     c                   eval      c2ratd = frratd
     c                   eval      c2ratf = 0
     c                   eval      c2ratt = 0
     c                   eval      c2dekn = frdekn
     c                   eval      c2dekd = frdekd
6.41 c                   eval      c2pslm = frpslm
     c                   eval      c2dekf = 0
     c                   eval      c2dekt = 0
     c                   eval      c2eusr = freusr
      *
     c                   if        frra1f <> *loval
     c     *dmy          move      frra1f        c2ra1f
     c                   endif
      *
     c                   if        frra1t <> *loval
     c     *dmy          move      frra1t        c2ra1t
     c                   endif
      *
     c                   if        frra2f <> *loval
     c     *dmy          move      frra2f        c2ra2f
     c                   endif
      *
     c                   if        frra2t <> *loval
     c     *dmy          move      frra2t        c2ra2t
     c                   endif
      *
     c                   if        frranf <> *loval
     c     *dmy          move      frranf        c2ranf
     c                   endif
      *
     c                   if        frrant <> *loval
     c     *dmy          move      frrant        c2rant
     c                   endif
      *
     c                   if        frrbuf <> *loval
     c     *dmy          move      frrbuf        c2rbuf
     c                   endif
      *
     c                   if        frrbut <> *loval
     c     *dmy          move      frrbut        c2rbut
     c                   endif
      *
     c                   if        frratf <> *loval
     c     *dmy          move      frratf        c2ratf
     c                   endif
      *
     c                   if        frratt <> *loval
     c     *dmy          move      frratt        c2ratt
     c                   endif
      *
     c                   if        frdekf <> *loval
     c     *dmy          move      frdekf        c2dekf
     c                   endif
      *
     c                   if        frdekt <> *loval
     c     *dmy          move      frdekt        c2dekt
     c                   endif
      *
     c     *dmy          move      frodat        c2odat
     c     *dmy          move      fredat        c2edat
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Viser bildet
      *
     c                   exfmt     c2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      frabl2
5.70 c                   when      w_seqe = 'R'
5.70 c                   read      frabld
     c                   when      w_seqe = 'K'
     c                   read      frabl3
     c                   when      w_seqe = 'P'
     c                   read      frabl4
     c                   when      w_seqe = 'O'
     c                   read      frabl5
     c                   when      w_seqe = 'H'
     c                   read      frabl6
     c                   when      w_seqe = 'U'
     c                   read      frabl7
     c                   when      w_seqe = 'L'
     c                   read      frabl8
     c                   when      w_seqe = 'M'
     c                   read      frabl9
     c                   when      w_seqe = 'V'
     c                   read      frabla
     c                   when      w_seqe = 'E'
     c                   read      frablb
     c                   when      w_seqe = 'T'
     c                   read      frablc
     c                   other
     c                   read      frabl1
     c                   endsl
      *
     c                   if        %eof or
     c                             frfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        w_kund <> 0
     c                   if        frrkat <> 0 and
     c                             frrkat <> w_rkat or
     c                             frkkat <> 0 and
     c                             frkkat <> w_kkat or
     c                             frkund <> 0 and
     c                             frkund <> w_kund
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             frkpro <> 0 and
     c                             frkpro <> w_kpro
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      frabl2_kkat = frkkat
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      frabld_rkat = frrkat
     c                   when      w_seqe = 'K'
     c                   eval      frabl3_kund = frkund
     c                   eval      frabl3_kpro = frkpro
     c                   eval      frabl3_ogrp = frogrp
     c                   eval      frabl3_hgrp = frhgrp
     c                   eval      frabl3_ugrp = frugrp
     c                   eval      frabl3_ldor = frldor
     c                   when      w_seqe = 'P'
     c                   eval      frabl4_kpro = frkpro
     c                   when      w_seqe = 'O'
     c                   eval      frabl5_ogrp = frogrp
     c                   eval      frabl5_hgrp = frhgrp
     c                   eval      frabl5_ugrp = frugrp
     c                   when      w_seqe = 'H'
     c                   eval      frabl6_hgrp = frhgrp
     c                   eval      frabl6_ugrp = frugrp
     c                   when      w_seqe = 'U'
     c                   eval      frabl7_ugrp = frugrp
     c                   when      w_seqe = 'L'
     c                   eval      frabl8_ldor = frldor
     c                   when      w_seqe = 'M'
     c                   eval      frabl9_modn = frmodn
     c                   when      w_seqe = 'V'
     c                   eval      frabla_vare = frvare
5.70 c                   eval      frabla_prgr = frprgr
     c                   eval      frabla_enhe = frenhe
     c                   eval      frabla_lety = frlety
     c                   when      w_seqe = 'E'
     c                   eval      frablb_enhe = frenhe
     c                   when      w_seqe = 'T'
     c                   eval      frablc_lety = frlety
     c                   other
5.70 c                   eval      frabl1_prgr = frprgr
     c                   eval      frabl1_rkat = frrkat
     c                   eval      frabl1_kkat = frkkat
     c                   eval      frabl1_kund = frkund
     c                   eval      frabl1_kpro = frkpro
     c                   eval      frabl1_ogrp = frogrp
     c                   eval      frabl1_hgrp = frhgrp
     c                   eval      frabl1_ugrp = frugrp
     c                   eval      frabl1_ldor = frldor
     c                   eval      frabl1_modn = frmodn
     c                   eval      frabl1_vare = frvare
     c                   eval      frabl1_enhe = frenhe
     c                   eval      frabl1_lety = frlety
     c                   endsl
      *
5.70 c                   eval      b1prgr = frprgr
     c                   eval      b1rkat = frrkat
     c                   eval      b1kkat = frkkat
     c                   eval      b1kund = frkund
     c                   eval      b1kpro = frkpro
     c                   eval      b1ldor = frldor
     c                   eval      b1ogrp = frogrp
     c                   eval      b1hgrp = frhgrp
     c                   eval      b1ugrp = frugrp
     c                   eval      b1modn = frmodn
     c                   eval      b1vare = frvare
     c                   eval      b1enhe = frenhe
     c                   eval      b1lety = frlety
      *
     c                   if        *in80 = *off
     c                   eval      b1rab1 = frrab1
     c                   eval      b1rabn = frrabn
     c                   eval      b1dekn = frdekn
     c                   else
     c                   eval      b1rab1 = frra1d
     c                   eval      b1rabn = frrand
     c                   eval      b1dekn = frdekd
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'A'
     c     frabl2_key    setgt     frabl2
     c                   readp     frabl2
5.70 c                   when      w_seqe = 'R'
5.70 c     frabld_key    setgt     frabld
5.70 c                   readp     frabld
     c                   when      w_seqe = 'K'
     c     frabl3_key    setgt     frabl3
     c                   readp     frabl3
     c                   when      w_seqe = 'P'
     c     frabl4_key    setgt     frabl4
     c                   readp     frabl4
     c                   when      w_seqe = 'O'
     c     frabl5_key    setgt     frabl5
     c                   readp     frabl5
     c                   when      w_seqe = 'H'
     c     frabl6_key    setgt     frabl6
     c                   readp     frabl6
     c                   when      w_seqe = 'U'
     c     frabl7_key    setgt     frabl7
     c                   readp     frabl7
     c                   when      w_seqe = 'L'
     c     frabl8_key    setgt     frabl8
     c                   readp     frabl8
     c                   when      w_seqe = 'M'
     c     frabl9_key    setgt     frabl9
     c                   readp     frabl9
     c                   when      w_seqe = 'V'
     c     frabla_key    setgt     frabla
     c                   readp     frabla
     c                   when      w_seqe = 'E'
     c     frablb_key    setgt     frablb
     c                   readp     frablb
     c                   when      w_seqe = 'T'
     c     frablc_key    setgt     frablc
     c                   readp     frablc
     c                   other
     c     frabl1_key    setgt     frabl1
     c                   readp     frabl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     frabl2
5.70 c                   when      w_seqe = 'R'
5.70 c                   readp     frabld
     c                   when      w_seqe = 'K'
     c                   readp     frabl3
     c                   when      w_seqe = 'P'
     c                   readp     frabl4
     c                   when      w_seqe = 'O'
     c                   readp     frabl5
     c                   when      w_seqe = 'H'
     c                   readp     frabl6
     c                   when      w_seqe = 'U'
     c                   readp     frabl7
     c                   when      w_seqe = 'L'
     c                   readp     frabl8
     c                   when      w_seqe = 'M'
     c                   readp     frabl9
     c                   when      w_seqe = 'V'
     c                   readp     frabla
     c                   when      w_seqe = 'E'
     c                   readp     frablb
     c                   when      w_seqe = 'T'
     c                   readp     frablc
     c                   other
     c                   readp     frabl1
     c                   endsl
      *
     c                   if        %eof or
     c                             frfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        w_kund <> 0
     c                   if        frrkat <> 0 and
     c                             frrkat <> w_rkat or
     c                             frkkat <> 0 and
     c                             frkkat <> w_kkat or
     c                             frkund <> 0 and
     c                             frkund <> w_kund
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             frkpro <> 0 and
     c                             frkpro <> w_kpro
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      frabl2_kkat = frkkat
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      frabld_rkat = frrkat
     c                   when      w_seqe = 'K'
     c                   eval      frabl3_kund = frkund
     c                   eval      frabl3_kpro = frkpro
     c                   eval      frabl3_ogrp = frogrp
     c                   eval      frabl3_hgrp = frhgrp
     c                   eval      frabl3_ugrp = frugrp
     c                   eval      frabl3_ldor = frldor
     c                   when      w_seqe = 'P'
     c                   eval      frabl4_kpro = frkpro
     c                   when      w_seqe = 'O'
     c                   eval      frabl5_ogrp = frogrp
     c                   eval      frabl5_hgrp = frhgrp
     c                   eval      frabl5_ugrp = frugrp
     c                   when      w_seqe = 'H'
     c                   eval      frabl6_hgrp = frhgrp
     c                   eval      frabl6_ugrp = frugrp
     c                   when      w_seqe = 'U'
     c                   eval      frabl7_ugrp = frugrp
     c                   when      w_seqe = 'L'
     c                   eval      frabl8_ldor = frldor
     c                   when      w_seqe = 'M'
     c                   eval      frabl9_modn = frmodn
     c                   when      w_seqe = 'V'
     c                   eval      frabla_vare = frvare
5.70 c                   eval      frabla_prgr = frprgr
     c                   eval      frabla_enhe = frenhe
     c                   eval      frabla_lety = frlety
     c                   when      w_seqe = 'E'
     c                   eval      frablb_enhe = frenhe
     c                   when      w_seqe = 'T'
     c                   eval      frablc_lety = frlety
     c                   other
5.70 c                   eval      frabl1_prgr = frprgr
     c                   eval      frabl1_rkat = frrkat
     c                   eval      frabl1_kkat = frkkat
     c                   eval      frabl1_kund = frkund
     c                   eval      frabl1_kpro = frkpro
     c                   eval      frabl1_ogrp = frogrp
     c                   eval      frabl1_hgrp = frhgrp
     c                   eval      frabl1_ugrp = frugrp
     c                   eval      frabl1_ldor = frldor
     c                   eval      frabl1_modn = frmodn
     c                   eval      frabl1_vare = frvare
     c                   eval      frabl1_enhe = frenhe
     c                   eval      frabl1_lety = frlety
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'A'
     c     frabl2_key    setll     frabl2
5.70 c                   when      w_seqe = 'R'
5.70 c     frabld_key    setll     frabld
     c                   when      w_seqe = 'K'
     c     frabl3_key    setll     frabl3
     c                   when      w_seqe = 'P'
     c     frabl4_key    setll     frabl4
     c                   when      w_seqe = 'O'
     c     frabl5_key    setll     frabl5
     c                   when      w_seqe = 'H'
     c     frabl6_key    setll     frabl6
     c                   when      w_seqe = 'U'
     c     frabl7_key    setll     frabl7
     c                   when      w_seqe = 'L'
     c     frabl8_key    setll     frabl8
     c                   when      w_seqe = 'M'
     c     frabl9_key    setll     frabl9
     c                   when      w_seqe = 'V'
     c     frabla_key    setll     frabla
     c                   when      w_seqe = 'E'
     c     frablb_key    setll     frablb
     c                   when      w_seqe = 'T'
     c     frablc_key    setll     frablc
     c                   other
     c     frabl1_key    setll     frabl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B2PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b2prgr
5.70 c                   parm                    w_dtxt
5.70 c                   leavesr
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B2RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b2rkat
     c                   parm                    w_rtxt
     c                   leavesr
     c                   endif
      *
      * Kundekategori
      *
     c                   if        w_afld = 'B2KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    b2kkat
     c                   parm                    w_ktxt
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B2KUND'
     c                   eval      b2kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
      * Modulnr
      *
     c                   if        w_afld = 'B2MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    w_mtxt
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
      * Enhet
      *
     c                   if        w_afld = 'B2ENHE'
     c                   if        b2vare <> *blank
     c                   eval      w_vare = b2vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_ltxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c2rtxt = *blank
     c                   eval      c2ktxt = *blank
     c                   eval      c2knav = *blank
     c                   eval      c2pnav = *blank
     c                   eval      c2gtxt = *blank
     c                   eval      c2lnav = *blank
     c                   eval      c2mtxt = *blank
     c                   eval      c2vtxt = *blank
     c                   eval      c2etxt = *blank
     c                   eval      c2ltxt = *blank
5.70 c                   eval      c2dtxt = *blank
5.70  *
5.70  *Prisgruppe
5.70  *
5.70 c                   if        c2prgr <> *blank
5.70 c                   eval      ra30l1_dkod = c2prgr
5.70 c     ra30l1_key    chain     ra30l1
5.70 c                   if        %found
5.70 c                   eval      c2dtxt = redtxt
5.70 c                   endif
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        c2rkat <> 0
     c                   eval      ra06pf_fkod = c2rkat
     c     ra06pf_key    chain     ra06pf
     c                   if        %found
     c                   eval      c2rtxt = raftxt
     c                   endif
     c                   endif
      *
      * Kundekategori
      *
     c                   if        c2kkat <> 0
     c                   eval      ra11pf_kkod = c2kkat
     c     ra11pf_key    chain     ra11pf                             91
     c                   if        *in91 = *off
     c                   eval      c2ktxt = raktxt
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        c2kund <> 0
     c                   eval      rkunl1_kund = c2kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2knav = rknavn
     c                   endif
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        c2kpro <> 0
     c                   eval      fkprl1_kund = c2kund
     c                   eval      fkprl1_kpro = c2kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      c2pnav = flnavn
     c                   endif
     c                   endif
      *
      * Overgruppe
      *
     c                   if        c2ogrp <> 0 and
     c                             c2hgrp = 0
     c                   eval      vogrl1_oogr = c2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      c2gtxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c2hgrp <> 0 and
     c                             c2ugrp = 0
     c                   eval      vhgrl1_hogr = c2ogrp
     c                   eval      vhgrl1_hhgr = c2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      c2gtxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c2ugrp <> 0
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c2gtxt = vgutxt
     c                   endif
     c                   endif
      *
      * Leverandør
      *
     c                   if        c2ldor <> 0
     c                   eval      rlevl1_levr = c2ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      c2lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Modul
      *
     c                   if        c2modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    c2modn
     c                   parm                    c2mtxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        c2vare <> *blank
     c                   eval      vvarl1_vare = c2vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      c2vtxt = vvtek1
     c                   endif
     c                   endif
      *
      * Enhet
      *
     c                   if        c2enhe <> *blank
     c                   eval      venhl1_enhe = c2enhe
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c2etxt = vaetxt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        c2lety <> 0
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      c2ltxt = vybesk
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Key for oppslag på rabattmatrise
      *
     c     frablr_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    frablr_prgr
     c                   kfld                    frablr_rkat
     c                   kfld                    frablr_kkat
     c                   kfld                    frablr_kund
     c                   kfld                    frablr_kpro
     c                   kfld                    frablr_ogrp
     c                   kfld                    frablr_hgrp
     c                   kfld                    frablr_ugrp
     c                   kfld                    frablr_ldor
     c                   kfld                    frablr_modn
     c                   kfld                    frablr_vare
     c                   kfld                    frablr_enhe
     c                   kfld                    frablr_lety
      *
      * Key for posisjonering på rabattkategori
      *
     c     frabl1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    frabl1_prgr
     c                   kfld                    frabl1_rkat
     c                   kfld                    frabl1_kkat
     c                   kfld                    frabl1_kund
     c                   kfld                    frabl1_kpro
     c                   kfld                    frabl1_ogrp
     c                   kfld                    frabl1_hgrp
     c                   kfld                    frabl1_ugrp
     c                   kfld                    frabl1_ldor
     c                   kfld                    frabl1_modn
     c                   kfld                    frabl1_vare
     c                   kfld                    frabl1_enhe
     c                   kfld                    frabl1_lety
      *
      * Key for posisjonering på kundekategori
      *
     c     frabl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl2_kkat
      *
      * Key for posisjonering på kunde
      *
     c     frabl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl3_kund
     c                   kfld                    frabl3_kpro
     c                   kfld                    frabl3_ogrp
     c                   kfld                    frabl3_hgrp
     c                   kfld                    frabl3_ugrp
     c                   kfld                    frabl3_ldor
      *
      * Key for posisjonering på kundeprosjekt
      *
     c     frabl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl4_kpro
      *
      * Key for posisjonering på overgruppe
      *
     c     frabl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl5_ogrp
     c                   kfld                    frabl5_hgrp
     c                   kfld                    frabl5_ugrp
      *
      * Key for posisjonering på hovedgruppe
      *
     c     frabl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl6_hgrp
     c                   kfld                    frabl6_ugrp
      *
      * Key for posisjonering på undergruppe
      *
     c     frabl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl7_ugrp
      *
      * Key for posisjonering på leverandør
      *
     c     frabl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl8_ldor
      *
      * Key for posisjonering på modulnr
      *
     c     frabl9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl9_modn
      *
      * Key for posisjonering på varenummer
      *
     c     frabla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabla_vare
5.70 c                   kfld                    frabla_prgr
     c                   kfld                    frabla_enhe
     c                   kfld                    frabla_lety
      *
      * Key for posisjonering på enhet
      *
     c     frablb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frablb_enhe
      *
      * Key for posisjonering på leveringstype
      *
     c     frablc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frablc_lety
5.70  *
5.70  * Key for posisjonering på rabatt kategori
5.70  *
5.70 c     frabld_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    frabld_rkat
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
      *
      * Key for oppslag på kundekategori
      *
     c     ra11pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11pf_kkod
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_enhe
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
     c                   eval      w_rkat = 0
     c                   eval      w_kkat = 0
      *
      * Henter overstyringer fra kunde
      *
     c                   if        w_kund <> 0
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      w_rkat = rkrabk
     c                   eval      w_kkat = rkkatg
     c                   endif
     c                   endif
      *
      * Henter overstyringer fra kundeprosjekt
      *
     c                   if        w_kpro <> 0
      *
     c                   eval      fkprl1_kund = w_kund
     c                   eval      fkprl1_kpro = w_kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
      *
     c                   if        florka <> 0
     c                   eval      w_rkat = florka
     c                   endif
      *
     c                   if        flokun <> 0
     c                   eval      w_kund = flokun
     c                   eval      w_kpro = 0
     c                   endif
      *
     c                   if        flokpr <> 0
     c                   eval      w_kpro = flokpr
     c                   endif
      *
     c                   if        flokun <> 0
     c                   eval      rkunl1_kund = flokun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      w_rkat = rkrabk
     c                   eval      w_kkat = rkkatg
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2rka1 = w_rkat
     c                   eval      b2kka1 = w_kkat
     c                   eval      b2kun1 = w_kund
     c                   eval      b2kpr1 = w_kpro
      *
     c                   if        b2rka1 <> 0
     c                   eval      ra06pf_fkod = b2rka1
     c     ra06pf_key    chain     ra06pf
     c                   if        %found
     c                   eval      b2rtxt = raftxt
     c                   endif
     c                   endif
      *
     c                   if        b2kka1 <> 0
     c                   eval      ra11pf_kkod = b2kka1
     c     ra11pf_key    chain     ra11pf
     c                   if        %found
     c                   eval      b2ktxt = raktxt
     c                   endif
     c                   endif
      *
     c                   if        b2kun1 <> 0
     c                   eval      rkunl1_kund = b2kun1
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b2knav = rknavn
     c                   endif
     c                   endif
      *
     c                   if        b2kpr1 <> 0
     c                   eval      fkprl1_kund = b2kun1
     c                   eval      fkprl1_kpro = b2kpr1
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      b2pnav = flnavn
     c                   endif
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     frabl1_key    setll     frabl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
