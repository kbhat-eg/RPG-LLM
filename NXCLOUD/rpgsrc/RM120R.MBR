     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RM120R
      * Beskrivelse . . : Eiendomsknytning, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170303 KOB  Nytt program
7.xx  * 7.00  130516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frmknl1    if   e           k disk    rename(rmknpfr:rmknl1r)
     frmeil2    if   e           k disk    rename(rmeipfr:rmeil2r)
     frmknlr    if   e           k disk    rename(rmknpfr:rmknlrr)
     frmknlu    uf a e           k disk    rename(rmknpfr:rmknlur)
      *
     frm120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rmknl1_xalm     s                   like(rmxalm)
     d rmknl1_xknr     s                   like(rmxknr)
     d rmknl1_xgnr     s                   like(rmxgnr)
     d rmknl1_xbnr     s                   like(rmxbnr)
     d rmknl2_xalm     s                   like(rmxalm)
     d rmeil2_enav     s                   like(rmenav)
     d rmknlr_xalm     s                   like(rmxalm)
     d rmknlr_xknr     s                   like(rmxknr)
     d rmknlr_xgnr     s                   like(rmxgnr)
     d rmknlr_xbnr     s                   like(rmxbnr)
     d rmknlu_xalm     s                   like(rmxalm)
     d rmknlu_xknr     s                   like(rmxknr)
     d rmknlu_xgnr     s                   like(rmxgnr)
     d rmknlu_xbnr     s                   like(rmxbnr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rmxfir)
      *
     d p_kund          s                   like(rmxknr)
     d p_navn          s             30
     d p_alme          s                   like(rmealm)
     d p_egnr          s              5  0
     d p_ebnr          s              5  0
     d p_enav          s             30
     d p_stat          s              1
     d p_anav          s             30
      *
     d s_xgnr          s                   like(rmxgnr)
     d s_xbnr          s                   like(rmxbnr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2xgnr <> 0 or
     c                             b2enav <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rmeil2_enav = b1enav
     c     rmeil2_key    setll     rmeil2
     c                   other
     c                   eval      rmknl1_xgnr = 0
     c                   eval      rmknl1_xbnr = 0
     c     rmknl1_key    setll     rmknl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rabattgruppe
      *
     c                   if        b2xgnr <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rmknl1_xgnr = b2xgnr
     c                   eval      rmknl1_xbnr = b2xbnr
     c     rmknl1_key    setll     rmknl1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2enav <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rmeil2_enav = b2enav
     c     rmeil2_key    setll     rmeil2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2xgnr = 0
     c                   eval      b2xbnr = 0
     c                   eval      b2enav = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1xgnr = b1xgnr
     c                   eval      c1xbnr = b1xbnr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1xgnr = b1xgnr
     c                   eval      k1xbnr = b1xbnr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1xgnr = b1xgnr
     c                   eval      d1xbnr = b1xbnr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1xgnr = b1xgnr
     c                   eval      c1xbnr = b1xbnr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1xgnr = b2xgnr
     c                   eval      c1xbnr = b2xbnr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C1XGNR' or
     c                             w_afld = 'C1XBNR'
      *
     c                   call      'RM510R'
     c                   parm                    p_alme
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
     c                   parm                    p_enav
      *
     c                   eval      c1xgnr = p_egnr
     c                   eval      c1xbnr = p_ebnr
     c                   goto      c1taga
     c                   endif
      *
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1xgnr = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1xgnr = *blank
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rmknlr_xalm = p_alme
     c                   eval      rmknlr_xgnr = c1xgnr
     c                   eval      rmknlr_xbnr = c1xbnr
     c     rmknlr_key    chain     rmknlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1xgnr = 0
     c                   eval      c1xbnr = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2xgnr = 0
     c                   eval      b2xbnr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rmknlr_xalm = p_alme
     c                   eval      rmknlr_xgnr = c1xgnr
     c                   eval      rmknlr_xbnr = c1xbnr
     c     rmknlr_key    chain     rmknlr
      *
     c                   if        %found
     c                   eval      c2enav = rmenav
      *
     c                   eval      p_egnr = c1xgnr
     c                   eval      p_ebnr = c1xbnr
      *
     c                   call      'RM720R'
     c                   parm                    p_stat
     c                   parm                    p_alme
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
     c                   parm                    p_enav
      *
     c                   eval      c2enav = p_enav
      *
     c                   eval      c2xeus = rmxeus
      *
     c                   if        rmxeda = *loval
     c                   move      0             c2xeda
     c                   else
     c     *dmy          move      rmxeda        c2xeda
     c                   endif
      *
     c                   else
      *
     c                   eval      p_egnr = c1xgnr
     c                   eval      p_ebnr = c1xbnr
      *
     c                   call      'RM720R'
     c                   parm                    p_stat
     c                   parm                    p_alme
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
     c                   parm                    p_enav
      *
     c                   if        p_stat <> ' '
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      c2enav = p_enav
     c                   eval      c2xeus = *blank
     c                   eval      c2xeda = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1xgnr = k1xgnr
     c                   eval      c1xbnr = k1xbnr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c***                if        c2text = *blank
     c***                eval      *in31 = *on
     c***                goto      c2taga
     c***                endif
      *
      * Oppdatering
      *
     c                   clear                   rmknlur
      *
     c                   eval      rmknlu_xalm = p_alme
     c                   eval      rmknlu_xgnr = c1xgnr
     c                   eval      rmknlu_xbnr = c1xbnr
     c     rmknlu_key    chain     rmknlur
      *
     c                   eval      rmenav = c2enav
      *
     c                   time                    rmxeda
     c                   time                    rmxeti
     c                   eval      rmxeus = l_user
      *
     c                   if        %found
     c                   update    rmknlur
     c                   else
     c                   eval      rmxfir = w_firm
     c                   eval      rmxalm = p_alme
     c                   eval      rmxknr = p_kund
     c                   eval      rmxgnr = c1xgnr
     c                   eval      rmxbnr = c1xbnr
     c                   write     rmknlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1enav = rmenav
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rmknlr_xgnr = d1xgnr
     c                   eval      rmknlr_xbnr = d1xbnr
     c     rmknlr_key    chain     rmknlr
      *
     c                   eval      p_egnr = d1xgnr
     c                   eval      p_ebnr = d1xbnr
      *
     c                   call      'RM720R'
     c                   parm                    p_stat
     c                   parm                    p_alme
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
     c                   parm                    p_enav
      *
     c                   eval      d1enav = p_enav
      *
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rmknlu_xgnr = d1xgnr
     c                   eval      rmknlu_xbnr = d1xbnr
     c     rmknlu_key    chain     rmknlur
      *
     c                   delete    rmknlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_xgnr = k1xgnr
     c                   eval      s_xbnr = k1xbnr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rmknlr_xgnr = k1xgnr
     c                   eval      rmknlr_xbnr = k1xbnr
     c     rmknlr_key    chain     rmknlr
      *
     c                   eval      p_egnr = k1xgnr
     c                   eval      p_ebnr = k1xbnr
      *
     c                   call      'RM720R'
     c                   parm                    p_stat
     c                   parm                    p_alme
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
     c                   parm                    p_enav
      *
     c                   eval      k1enav = p_enav
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1xgnr = 0 or
     c                             k1xbnr = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rmknlr_xgnr = k1xgnr
     c                   eval      rmknlr_xbnr = k1xbnr
     c     rmknlr_key    chain     rmknlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1xgnr = s_xgnr
     c                   eval      c1xbnr = s_xbnr
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rmeil2
     c                   other
     c                   read      rmknl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rmxfir <> w_firm or
     c                             rmxknr <> p_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rmeil2_enav = rmenav
     c                   other
     c                   eval      rmknl1_xgnr = rmxgnr
     c                   eval      rmknl1_xbnr = rmxbnr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1xgnr = rmxgnr
     c                   eval      b1xbnr = rmxbnr
      *
     c                   eval      p_egnr = rmxgnr
     c                   eval      p_ebnr = rmxbnr
      *
     c                   call      'RM720R'
     c                   parm                    p_stat
     c                   parm                    p_alme
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
     c                   parm                    p_enav
      *
     c                   eval      b1enav = p_enav
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     rmeil2_key    setgt     rmeil2
     c                   readp     rmeil2
     c                   other
     c     rmknl1_key    setgt     rmknl1
     c                   readp     rmknl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rmeil2
     c                   other
     c                   readp     rmknl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rmxfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rmeil2_enav = rmenav
     c                   other
     c                   eval      rmknl1_xgnr = rmxgnr
     c                   eval      rmknl1_xbnr = rmxbnr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rmeil2_key    setll     rmeil2
     c                   other
     c     rmknl1_key    setll     rmknl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_kund
     c                   parm                    p_navn
     c                   parm                    p_alme
     c                   parm                    p_anav
     c                   parm                    p_egnr
     c                   parm                    p_ebnr
      *
      * Key for posisjonering på programkode
      *
     c     rmknl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl1_xalm
     c                   kfld                    rmknl1_xknr
     c                   kfld                    rmknl1_xgnr
     c                   kfld                    rmknl1_xbnr
      *
      * Key for posisjonering på beskrivelse
      *
     c     rmeil2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl2_xalm
     c                   kfld                    rmeil2_enav
      *
      * Key for les på programkode
      *
     c     rmknlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknlr_xalm
     c                   kfld                    rmknlr_xknr
     c                   kfld                    rmknlr_xgnr
     c                   kfld                    rmknlr_xbnr
      *
      * Key for oppdatering av programkode
      *
     c     rmknlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknlu_xalm
     c                   kfld                    rmknlu_xknr
     c                   kfld                    rmknlu_xgnr
     c                   kfld                    rmknlu_xbnr
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      rmknl1_xknr = p_kund
     c                   eval      rmknl1_xalm = p_alme
     c                   eval      rmknlr_xknr = p_kund
     c                   eval      rmknlr_xalm = p_alme
     c                   eval      rmknlu_xknr = p_kund
     c                   eval      rmknlu_xalm = p_alme
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rmknl1_xgnr = 0
     c                   eval      rmknl1_xbnr = 0
     c     rmknl1_key    setll     rmknl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
