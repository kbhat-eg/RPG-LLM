      /TITLE  Klargjøring av kommunikasjonsfil for henting.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                   *
      * Program . . . . : GL702R                                          *
      * Beskrivelse . . : Klargjør for henting av banktranser.            *
      *                   Henter inn parametre.                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                   *
      * Spesialversjon. :                                                 *
      * Tilpasset for . :                                                 *
      *                                                                   *
      * Referanse  Dato   Endringsbeskrivelse                        Sign.*
      * --------- ------  ------------------------------------------------*
      *                                                                   *
      * 19091     190901  Lagt inn parameter på hvilken record som   RH   *
      *                   skal hentes i fra DIRPIE.                  RH   *
      *                                                                   *
      *                                                                   *
      *                                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                   *
      *  BRUK AV INDIKATORER                                              *
      *                                                                   *
      *       KC = F3  = Exit                                             *
      *                                                                   *
      *       LR = Avslutt program                                        *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer               *
      *    60-69 = Fileindikatorer chain etc.                             *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                       *
      *    80-89 = Arbeidsindikatorer ordinære                            *
      *    90-99 = Benyttes ved std. sub-rutiner                          *
      *                                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fdirpie    uf a e           k disk
     fdiruie    o  a e           k disk
     fgl702d    cf   e             workstn
      *
     d wfnut           s              1
     d wlogf           s             80
     d wlogn           s             80
     d wpbank          s              2
     d wpin03          s              1  0
     d wsend           s             80
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A1BLD  - A1 Hovedbilde for oppstart
      * A2WIN  - A2 Vindu med informasjon om klargjøring av fil
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1BLD Styrebilde - inntasting av konto,brukerid og passord.       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Definerer generelle felter
      *
     c                   biton     '123457'      wfnut                          Definere '
     c                   bitoff    '06'          wfnut
      *
      * Henter data fra paramete-fil og legger ut i skjermfelter
      *
19091c     wpbank        chain     dirpie                             60
     c                   if        *in60 = *on                                            .....
     c                   eval      a1sacc = *blanks                                           .
     c                   eval      a1suid = *blanks                                           .
     c                   eval      a1spwd = *blanks                                           .
     c                   eval      a1mmsg = *blanks                                           .
     c                   else                                                                 .
     c                   move      iesacc        a1sacc                                       .
     c                   move      iesuid        a1suid                                       .
     c                   move      iespwd        a1spwd                                       .
     c                   move      iemmsg        a1mmsg                                       .
     c                   move      iepart        a1part                                       .
     c                   endif                                                            .....
      *
     c     a1taga        tag
     c                   exfmt     a1bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  ...
     c                   eval      wpin03 = 1                                     .
     c                   goto      xslutt                                         .
     c                   endif                                                  ...
      *
      * Sjekke om logon konto utfylt
      *
     c                   if        a1sacc = *blanks                                       .....
     c                   move      *on           *in31                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om logon brukerid er utfylt
      *
     c                   if        a1suid = *blanks                                       .....
     c                   move      *on           *in32                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om logon passord er utfylt
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   if        a1spwd = *blanks                                           .
     c                   move      *on           *in33                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                                .
     c                   endif                                                            .....
      *
      * Sjekke om meldingsklasse er utfylt
      *
     c                   if        a1mmsg = *blanks                                       .....
     c                   move      *on           *in35                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Sjekke om kommunikasjonspartner er utfylt
      *
     c                   if        a1part = *blanks                                       .....
     c                   move      *on           *in36                                        .
     c                   goto      a1taga                                                     .
     c                   endif                                                            .....
      *
      * Redigerer data ut på parameter fil og oppdaterer
      *
     c                   write     a2win
      *
19091c     wpbank        chain     dirpie                             60
      *
     c                   move      a1sacc        iesacc
     c                   move      a1suid        iesuid
     c                   move      a1spwd        iespwd
     c                   move      a1mmsg        iemmsg
     c                   move      a1part        iepart
      *
     c                   if        *in60 = *off                                           .....
     c                   update    iedata                                                     .
     c                   else                                                                 .
19091c                   move      wpbank        iekey                                        .
     c                   write     iedata                                                     .
     c                   endif                                                            .....
      *
      * Logge inn på CICS og starte applikasjon  (NIT)
      *
     c                   if        a1part = 'NIT'                                         .....
     c                   movel(p)  'CESN'        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   movel(p)  a1suid        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   movel(p)  a1spwd        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   movel(p)  'EXPV'        wlogn                                        .
     c                   move      wlogn         ierec                                        .
     c                   write     ieutrec                                                    .
      *                                                                  .
     c                   endif                                                            .....
      *
      * Redigere logon
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   exsr      xlogn1                                                     .
     c                   else                                                                 .
     c                   exsr      xlogn2                                                     .
     c                   endif                                                            .....
      *
      * Redigere receive
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   exsr      xrecv1                                                     .
     c                   else                                                                 .
     c                   exsr      xrecv2                                                     .
     c                   endif                                                            .....
      *
      * Redigere logoff
      *
     c                   if        a1part = 'IBM'                                         .....
     c                   exsr      xlogf                                                      .
     c                   endif                                                            .....
      *
      * Kaller opp kommunikasjonsprogram
      *
     c                   close     diruie
     c                   call      'GL703R'
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av logon-streng IBM
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlogn1        begsr
     c                   movel(p)  'IELOGON'     wlogn
     c                   cat       ' ACCOU':0    wlogn
     c                   cat       'NT(':0       wlogn
     c                   cat       iesacc:0      wlogn
     c                   cat       ') USER':0    wlogn
     c                   cat       'ID(':0       wlogn
     c                   cat       iesuid:0      wlogn
     c                   cat       ') PASS':0    wlogn
     c                   cat       'WORD(':0     wlogn
     c                   cat       iespwd:0      wlogn
     c                   cat       ');':0        wlogn
     c                   move      wlogn         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av logon-streng NIT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlogn2        begsr
     c                   movel(p)  'IELOGON'     wlogn
     c                   cat       ' ACCOU':0    wlogn
     c                   cat       'NT(':0       wlogn
     c                   cat       iesacc:0      wlogn
     c                   cat       ') USER':0    wlogn
     c                   cat       'ID(':0       wlogn
     c                   cat       iesuid:0      wlogn
     c                   cat       ');':0        wlogn
     c                   move      wlogn         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av receive streng IBM
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xrecv1        begsr
     c                   movel(p)  'RECEIVE'     wsend
      *
     c                   if        iemmsg <> '*ALL'                                       .....
     c                   cat       ' ACCOU':0    wsend                                        .
     c                   cat       'NT(':0       wsend                                         .
     c                   cat       iesacc:0      wsend                                         .
     c                   cat       ') USER':0    wsend                                         .
     c                   cat       'ID(':0       wsend                                         .
     c                   cat       iesuid:0      wsend                                         .
     c                   cat       ') CLAS':0    wsend                                         .
     c                   cat       'S(':0        wsend                                         .
     c                   cat       wfnut:0       wsend                                         .
     c                   cat       iemmsg:0      wsend                                         .
     c                   cat       wfnut:0       wsend                                         .
     c                   cat       ') DLM(':0    wsend                                         .
     c                   cat       '¤¤¤);':0     wsend                                         .
     c                   else                                                                  .
     c                   cat       ' ACCOU':0    wsend                                         .
     c                   cat       'NT(':0       wsend                                         .
     c                   cat       iesacc:0      wsend                                         .
     c                   cat       ') USER':0    wsend                                         .
     c                   cat       'ID(':0       wsend                                         .
     c                   cat       iesuid:0      wsend                                         .
     c                   cat       ') DLM(':0    wsend                                         .
     c                   cat       '¤¤¤);':0     wsend                                     .....
     c                   endif
      *
     c                   move      wsend         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av receive streng NIT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xrecv2        begsr
     c                   movel(p)  'RECEIVE;'    wsend
      *
     c                   move      wsend         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for redigering/utlegg av logoff streng
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlogf         begsr
     c                   movel(p)  '/*LOGOFF'    wlogf
     c                   move      wlogf         ierec
      *
     c                   write     ieutrec
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    wpin03
     c                   parm                    wpbank
     c                   endsr
      *
