     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NW871R
      * Beskrivelse . . : WAP, API - nettbutikk-inngang
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211103 HKO  Nytt program
6.20  * 6.20  210813 bsa  Rekompilert grunnet endret ANBLPF
6.31  * 6.30  080219 bof  Rekompilert grunnet endret ANBLPF
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fnetwap    uf a e           k disk
      *
      * Definering av parametre
      *
     d p_fgrp          s              3
     d p_firm          s              3  0
     d p_avde          s              4  0
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                       499
      *
      * Datakø - inn (ordre-hode)
      *
     d                 ds
5.10 d d_hode                       499
     d  d_kund                        9  0 overlay(d_hode:  1)
     d  d_kpro                       12    overlay(d_hode: 10)
     d  d_refn                       15    overlay(d_hode: 22)
     d  d_ladr                       35    overlay(d_hode: 37)
     d  d_dref                       35    overlay(d_hode: 72)
     d  d_rekv                       20    overlay(d_hode:107)
     d  d_hlma                       20    overlay(d_hode:127)
     d  d_hlda                        8    overlay(d_hode:147)
     d  d_unav                       35    overlay(d_hode:155)
     d  d_umai                       50    overlay(d_hode:190)
     d  d_hmld                      256    overlay(d_hode:240)
5.10 d  d_frsp                        1    overlay(d_hode:496)
5.10 d  d_fsys                        3    overlay(d_hode:497)
      *
      * Datakø - inn (ordre-linje)
      *
     d                 ds
     d d_linj                       128
     d  d_vare                       16    overlay(d_linj: 1)
     d  d_enhe                        3    overlay(d_linj:17)
     d  d_anta                       11  3 overlay(d_linj:20)
     d  d_llma                       20    overlay(d_linj:31)
     d  d_llda                        8    overlay(d_linj:51)
     d  d_lmld                       70    overlay(d_linj:59)
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         16
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                 9    overlay(d_bgm: 5) inz('IMLWAPIMP')
     d  d_bgmfill2                    2    overlay(d_bgm:14) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:16) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - WAP-hode
      *
     d                 ds
     d d_wah                        157
     d  d_wahrcd                      3    overlay(d_wah:  1) inz('WAH')
     d  d_wahfill1                    1    overlay(d_wah:  4) inz(';')
     d  d_wahnum                      6  0 overlay(d_wah:  5)
     d  d_wahfill2                    1    overlay(d_wah: 11) inz(';')
     d  d_wahorg                      9  0 overlay(d_wah: 12)
     d  d_wahfill3                    1    overlay(d_wah: 21) inz(';')
     d  d_wahnam                     35    overlay(d_wah: 22)
     d  d_wahfill4                    1    overlay(d_wah: 57) inz(';')
     d  d_wahmai                    100    overlay(d_wah: 58)
      *
      * Recordbeskrivelse - WAP-linje
      *
     d                 ds
     d d_wal                        109
     d  d_walrcd                      3    overlay(d_wal:  1) inz('WAL')
     d  d_walfill1                    1    overlay(d_wal:  4) inz(';')
     d  d_walnum                      8    overlay(d_wal:  5)
     d  d_walfill2                    1    overlay(d_wal: 13) inz(';')
     d  d_walunt                      3    overlay(d_wal: 14)
     d  d_walfill3                    1    overlay(d_wal: 17) inz(';')
     d  d_walnbr                     12    overlay(d_wal: 18)
     d  d_walfill4                    1    overlay(d_wal: 30) inz(';')
     d  d_waldat                      8  0 overlay(d_wal: 31)
     d  d_walfill5                    1    overlay(d_wal: 39) inz(';')
     d  d_walmsg                     70    overlay(d_wal: 40)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variable i nøkler
      *
     d anbll3_lfgr     s                   like(aolfgr)
     d anbll3_lfir     s                   like(aolfir)
     d anbll3_lavd     s                   like(aolavd)
     d anbhl1_heie     s                   like(aoheie)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d b_buti          s              1    inz(*off)
     d b_frst          s              1    inz(*off)
      *
     d w_tmst          s               z
     d w_firm          s              3  0
     d w_tell          s             12  0
     d w_anta          s             11
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_frst = *on
      *
      * Henter informasjon fra nettbutikk
      * Dersom butikk ikke ble funnet avbrytes videre behandling
      *
     c                   eval      b_buti = *on
      *
     c                   eval      anbll3_lfgr = p_fgrp
     c                   eval      anbll3_lfir = p_firm
     c                   eval      anbll3_lavd = p_avde
     c     anbll3_key    chain     anbll3
     c                   if        not %found
     c                   eval      anbll3_lavd = 0
     c     anbll3_key    chain     anbll3
     c                   if        not %found
     c                   eval      b_buti = *off
     c                   endif
     c                   endif
      *
     c                   eval      anbhl1_heie = aoleie
     c     anbhl1_key    chain     anbhl1r
     c                   if        not %found
     c                   eval      b_buti = *off
     c                   endif
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank and
     c                             b_buti = *on
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Meldingshale
      *
     c                   if        b_buti = *on
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netwapr
      *
     c                   endif
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordreinngang
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        b_frst = *on
     c                   exsr      behandl_hode
     c                   else
     c                   exsr      behandl_linje
     c                   endif
      *
     c                   eval      b_frst = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_hode  begsr
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netwapr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netwapr
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netwapr
      *
      * WAP-hode
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_hode = d_inpu
      *
     c                   eval      d_wahnum = d_kund
     c                   eval      d_wahorg = 0
     c                   eval      d_wahnam = *blank
     c                   eval      d_wahmai = d_umai
      *
     c                   eval      rkunl1_kund = d_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      d_wahorg = rkftnr
     c                   eval      d_wahnam = rknavn
     c                   endif
      *
     c                   eval      data = d_wah
     c                   write     netwapr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordre-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_linje begsr
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_linj = d_inpu
      *
     c                   eval      d_walnum = d_vare
     c                   eval      d_walunt = %trim(d_enhe)
     c                   eval      d_walnbr = '00000000,000'
     c                   eval      d_waldat = 0
     c                   eval      d_walmsg = %trim(%trim(d_llma) + ' ' +
     c                                              %trim(d_lmld))
      *
     c                   move      d_anta        w_anta
     c                   eval      d_walnbr = %subst(w_anta:1:8) + ',' +
     c                                        %subst(w_anta:9:3)
      *
     c                   if        d_hlda <> *blank
     c                   move      d_hlda        d_waldat
     c                   endif
      *
     c                   eval      data = d_wal
     c                   write     netwapr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = '1'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_fgrp
     c                   parm                    p_firm
     c                   parm                    p_avde
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for oppslag på nettbutikk-linje
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_lfgr
     c                   kfld                    anbll3_lfir
     c                   kfld                    anbll3_lavd
      *
      * Key for oppslag på nettbutikk-hode
      *
     c     anbhl1_key    klist
     c                   kfld                    anbhl1_heie
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Hent firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
