      /TITLE  Hjelperegister for kontoutdrag
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK641R                                              *
      * Beskrivelse . . : Bygging av hjelpe-register for kontoutdrag          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
 6.20 * R6.20     090911  Rekompilert pga endring i RPARRKDS            *NHO
      *                                                                       *
      *                                                                       *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkw1pf    o  a e           k disk
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                 ds
     d wkwrec                  2    192
     d  wkfirm                        3  0 overlay(wkwrec)
     d  wkkund                        6  0 overlay(wkwrec:4)
     d  wknavn                       30    overlay(wkwrec:10)
      *
     d bruddx          s              1
     d w_alfa          s                   like(rkalfa)
     d n               s              2  0
     d tstkt           s              7  0
      *
     d w_firm          s                   like(rkfirm)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d/COPY QCPYLESRC,RPARRKDS
      /eject
      *
      * Leser kunde-register
      *
     c                   move      l_firm        w_firm
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1                                 60
      *
     c                   dow       *in60 = *off
      *
      * Seleksjon av ønsket avdeling
      *
     c                   if        rkavde < pavdf or
     c                             rkavde > pavdt and
     c                             pavdt > 0
     c                   goto      neste
     c                   endif
      *
      * Seleksjon av ønsket kategori
      *
     c                   if        rkkatg < pkatf or
     c                             rkkatg > pkatt and
     c                             pkatt > 0
     c                   goto      neste
     c                   endif
      *
      * Seleksjon av ønsket selger
      *
     c                   if        rkslgr < pself or
     c                             rkslgr > pselt and
     c                             pselt > 0
     c                   goto      neste
     c                   endif
      *
      * Kunden ligger innenfor fra - til
      *
     c                   if        rkkund >= pknrf and
     c                             rkkund <= pknrt
     c                   goto      skriv_kund
     c                   endif
      *
      * Sjekk om enkeltkunder er valgt
      *
     c                   if        tstkt = *zeros                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Kundenr. ligger utenfor fra - til
      * sjekk mot tabell
      *
     c                   eval      n = 1
     c                   dou       n > 10                                                .....
      *                                                                 .
     c                   if        kun(n) <> *zeros                                          .
      *                                                                 .
     c                   if        rkkund = kun(n)                                           .
     c                   goto      skriv_kund                                                .
     c                   endif                                                               .
      *                                                                 .
     c                   endif                                                               .
      *                                                                 .
     c                   eval      n = n + 1                                                 .
     c                   enddo                                                           .....
      *
      * Konto heller ikke i tabell -
      * les neste postering
      *
     c                   goto      neste
      *
     c     skriv_kund    tag
      *
     c                   eval      w_alfa = rkalfa                              Alfa-felt
      * - Sortering
     c                   if        psort = ' '
     c                   eval      w_alfa = *blank
     c                   endif
      *
      * - Skal det være brudd-sum?
      *
     c                   if        bruddx <> *blank
     c                   eval      rwkniv = *zeros
      *
      * - Avdelings-sum                                   Avdeling
      *
     c                   if        pavds = 'J'
     c                   eval      rwkniv = rkavde                              Brudd
     c                   endif
      *
      * - Kategori-sum                                    Kategori
      *
     c                   if        pkats = 'J'
     c                   eval      rwkniv = rkkatg                              Brudd
     c                   endif
      *
      * - Selger-sum                                      Selger
      *
     c                   if        psels = 'J'
     c                   eval      rwkniv = rkslgr                              Brudd 1
     c                   endif
     c                   endif
      *
      * Legg ut kunde
      *
     c                   eval      rwkfir = w_firm
     c                   eval      wknavn = rknavn                              Kundetekst
     c                   eval      wkkund = rkkund                              Kunde
     c                   move      wkwrec        rwkrec
      *
      * Legg ut sorteringsbegrep
      *
     c                   eval      rwksal = *zeros
     c                   eval      rwkalf = w_alfa
     c                   eval      rwkknr = rkkund
      *
     c                   write     rkw1pfr                                      Legger ut
      *
     c     neste         tag
      *
     c     rkunl1_key    reade     rkunl1                                 60
     c                   enddo                                                     fordeling
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *                                                      trans
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wkwrec = *zeros
      *
     c                   eval      w_firm = l_firm
      *
      * Skal det være brudd-sum
      *
     c                   if        pavds <> *blank
     c                   movel     'X'           bruddx
     c                   endif
      *
     c                   if        pkats <> *blank
     c                   movel     'X'           bruddx
     c                   endif
      *
     c                   if        psels <> *blank
     c                   movel     'X'           bruddx
     c                   endif
      *
      * Sjekk om kontovalg utfylt
      *
     c                   xfoot     kun           tstkt
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
