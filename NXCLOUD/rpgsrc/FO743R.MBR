     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO743R
      * Beskrivelse . . : Automatisk generering av bestillinger fra ordre
      *                   Sjekk om noen varer på ordre må bestilles
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150206 ASK  Nytt program
5.70  * PRGR  231008 sst  Prisgruppe
6.21  *       190511 bof  Utvidet vl710r med div. info
6.22  *       120115 bsa  Sjekker ikke tekstlinjer
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.3x fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
6.NU d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_best          s              1
5.70 d p_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(fdvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser ordrelinjer og sjekker om varer må bestilles
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Forutsetning: - Varen er en skaffevare
      *               - Varen er hentet fra vareadministrasjon
      *               - direkteleveranse er valgt
      *
     c                   eval      p_best = '0'
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
      *
     c                   dow       not %eof
6.22  * Sjekker ikke tekstlinjer
6.22 c                   if        fdvare <> *blanks
      *
      * Hent vareinfo
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
      *
      * Skal linje bestilles?
      *
     c                   if        fdbenr = 0
5.70  *
5.70  * Kaller program for henting av prisgruppe
5.70  *
     c                   eval      w_utda = *loval
     c                   eval      w_ldor = 0
5.70 c                   call      'VL710R'
5.70 c                   parm                    p_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    fdlage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.3x c                   parm                    w_lv_nobb
6.3x c                   parm                    w_lv_modn
6.3x c                   parm                    w_lv_lldo
6.3x c                   parm                    w_lv_llva
5.70 c                   if        p_vtyp = 'S' or
     c                             fdlety = 1 or
     c                             fdlvre = 1
     c                   eval      p_best = '1'
     c                   goto      avslutt
     c                   endif
     c                   endif
6.22 c                   endif
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : Ordre-linje-register
      *
     c     fodtl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
      *
      * Key for file : Vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    vvarl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_best
      *
     c                   endsr
