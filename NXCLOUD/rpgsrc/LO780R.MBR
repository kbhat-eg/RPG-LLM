     h option(*nodebugio) datedit(*ymd)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO780R
      *  Beskrivelse . . : Leverandørbestilling i Løvenskiold - format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            011102  Nytt program (utg.pkt LO770R)           GRO
6.10  *            251011  Takler eannr på 14 post                 bof
6.11  *            150413  Innfører eget program for innhenting av bsa
6.11  *                    GLN nummer.
6.12  *            150413  Utidet lev.varenr både 6 og 7 pos.      bof
6.20  *            270513  Fanger ikke opp den reelle delen av nr  bsa
6.nu  * 6.30  050614 bof  Utvidelser mtp. nummerutvidelse
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flwlopf    o    e           k disk
      *
      *  Definering av datastrukturer for Løvenskiold
      *
      *  Start-record
     d                 ds
     d d_srec                        80
     d  d_sidt                        3    overlay(d_srec)    inz('200')
     d  d_strm                        5    overlay(d_srec:5)  inz('00000')
     d  d_skun                        6    overlay(d_srec:10) inz('000000')
     d  d_sdat                        6    overlay(d_srec:16)
     d  d_stim                        6    overlay(d_srec:22)
     d  d_sbtm                        3    overlay(d_srec:28) inz('BTM')
      *
      *  Ordre-record
     d                 ds
     d d_orec                        80
     d  d_oidt                        3    overlay(d_orec)    inz('201')
     d  d_oref                       10    overlay(d_orec:5)
     d  d_ostr                        2    overlay(d_orec:20) inz('00')
     d  d_otxt                        5    overlay(d_orec:22) inz('ORDRE')
      *
      *  Varelinje-record #1 (EAN)
     d                 ds
     d d_vrc1                        80
     d  d_vid1                        3    overlay(d_vrc1)    inz('202')
     d  d_eakd                        2    overlay(d_vrc1:5)  inz('26')
     d  d_eanr                       13  0 overlay(d_vrc1:7)
     d  d_eant                        7    overlay(d_vrc1:20)
      *
      *  Varelinje-record #2 (Leverandørs varenr)
     d                 ds
     d d_vrc2                        80
     d  d_vid2                        3    overlay(d_vrc2)    inz('202')
     d  d_prkd                        2    overlay(d_vrc2:5)  inz('26')
6.12 d  d_prnr                        6    overlay(d_vrc2:7)
6.12 d  d_prn7                        7    overlay(d_vrc2:7)
     d  d_pant                        7    overlay(d_vrc2:20)
      *
      *  Varelinje-record #3 (NOBB)
     d                 ds
     d d_vrc3                        80
     d  d_vid3                        3    overlay(d_vrc3)    inz('202')
     d  d_nokd                        2    overlay(d_vrc3:5)  inz('26')
     d  d_nonr                        8  0 overlay(d_vrc3:7)
     d  d_nant                        7    overlay(d_vrc3:20)
      *
      *  Slutt-record
     d                 ds
     d d_slrc                        80
     d  d_slid                        3    overlay(d_slrc)    inz('299')
     d  d_sltr                        4    overlay(d_slrc:5)
     d  d_slku                        7    overlay(d_slrc:9)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d vvarl1_vare     s                   like(vvvare)
      *
      *  Definering av variable i parameter
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_filn          s             20
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
     d b_feil          s              1    inz(*off)
     d b_hode          s              1    inz(*off)
     d b_vnum          s              1    inz(*off)
     d w_time          s              6  0
     d w_date          s               d   datfmt(*eur)
     d w_timf          s               t   timfmt(*eur)
     d w_dat2          s             10
     d w_fptr          s              2  0 inz(20)
     d w_tptr          s              2  0 inz(7)
     d w_dcnt          s              2  0
     d w_ctst          s              1
     d w_anta          s              6  2
     d w_antx          s              7
     d w_vlen          s              2  0
     d w_prnr          s              6
     d w_vnix          s              1  0
     d w_filn          s             12
     d w_datx          s               d   datfmt(*iso)
     d w_wday          s             11p 0
     d w_wdax11        s             11
     d w_wdax1         s              1
     d w_timx          s              6
      *
      *  Konstanter
      *
     d c_mxch          c                   7
     d c_mxvl          c                   13
6.12 d c_digits        c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av startrecord
      *C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   time                    w_time
     c                   time                    w_date
     c                   time                    w_timf
      *
     c                   time                    w_datx
     c     w_datx        subdur    d'2002-12-01' w_wday:*d
     c                   div       7             w_wday
     c                   mvr                     w_wday
      *
     c                   if        w_wday > 0
     c                   move      w_wday        w_wdax11
     c                   else
     c                   eval      w_wday = w_wday + 7
     c                   move      w_wday        w_wdax11
     c                   endif
      *
     c                   eval      w_wdax1 = %subst(w_wdax11:11:1)
     c                   move      w_time        w_timx
      *
     c     lohel1_key    chain     lohel1r
      *
     c                   if        %found
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      lldilr_lage = *blank
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   endif
     c                   endif
      *
     c*                  dow       w_dcnt < c_mxch and
     c*                            w_fptr > 1 and
     c*                            b_feil = *off
     c*                  eval      w_ctst = %subst(lplknr:w_fptr:1)
     c*                  if        w_ctst >= '0' and
     c*                            w_ctst <= '9'
     c*                  eval      w_dcnt = w_dcnt + 1
     c*                  eval      %subst(d_skun:w_tptr:1) = w_ctst
     c*                  eval      w_tptr = w_tptr - 1
     c*                  endif
     c*                  eval      w_fptr = w_fptr - 1
     c*                  enddo
     c                   eval      d_strm = %subst(lplknr:1:5)
     c                   eval      d_skun = %subst(lplknr:6:6)
      *
     c                   move      w_date        w_dat2
     c                   eval      d_sdat = %subst(w_dat2:9:2) +
     c                                      %subst(w_dat2:4:2) +
     c                                      %subst(w_dat2:1:2)
     c                   move      w_time        d_stim
     c                   move      p_numm        d_oref
      *
     c                   if        b_feil = *off
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
      *
     c                   if        ldltyp <> laatty
     c                   exsr      finn_vnr
      *
     c                   if        b_feil = *off
     c                   if        b_hode = *off
     c                   eval      lovrec = d_srec
     c                   write     lwlopfr
     c                   eval      b_hode = *on
     c                   eval      lovrec = d_orec
     c                   write     lwlopfr
     c                   endif
      *
     c                   eval(h)   w_anta = ldanta
     c                   eval      w_antx = %editc(w_anta : 'M')
     c     ' ':'0'       xlate     w_antx        w_antx
      *
     c                   select
     c                   when      d_eanr <> 0
     c                   movel     w_antx        d_eant
     c                   eval      lovrec = d_vrc1
6.12 c                   when      d_prnr <> *blank
     c                   movel     w_antx        d_pant
     c                   eval      lovrec = d_vrc2
     c                   when      d_nonr <> 0
     c                   movel     w_antx        d_nant
     c                   eval      lovrec = d_vrc3
     c                   endsl
      *
     c                   write     lwlopfr
     c                   endif
     c                   endif
      *
     c     lodtl1_key    reade     lodtl1r
     c                   enddo
      *
     c                   eval      d_sltr = d_strm
     c                   eval      d_slku = d_skun
     c                   eval      lovrec = d_slrc
     c                   write     lwlopfr
     c                   endif
     c                   endif
      *
     c*                  eval      w_filn = 'B' + w_wdax1 + w_timx
     c                   eval      w_filn = 'xxx' + w_wdax1 + w_timx
      *  Testfilenavn (inkl. 'TT' i navn)
     c*                  eval      w_filn = 'xxxTT' + w_wdax1 +
     c*                                     %subst(w_timx:1:4)
     c                   eval      p_filn = w_filn
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne varenummer (EAN el LEV.VNR)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vnr      begsr
      *
     c                   eval      d_eanr = 0
6.12 c                   eval      d_prnr = *blank
     c                   eval      d_prn7 = *blank
     c                   eval      d_nonr = 0
      *
     c*                  eval      vvarl1_vare = ldvare
     c*    vvarl1_key    chain     vvarl1
     c*                  if        not %found
     c*                  eval      b_feil = *on
     c*                  leavesr
     c*                  endif
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    ldvare
6.11 c                   parm                    ldenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
     c                   if        w_east = '1' and
6.10 c                             w_eann <> 0  and
6.10 c                             w_eann < 10000000000000
     c                   eval      d_eanr = w_eann
     c                   leavesr
     c                   endif
      *
     c                   if        ldlvar <> *blank
6.12  *
6.12 c                   if        %len(%trim(ldlvar)) = 7
6.12  * lev.varenr = 7
6.20 c                   if        %check(c_digits : %trim(ldlvar)) = 0
6.12 c                   eval      d_prnr = *blank
6.12 c                   movel     ldlvar        d_prn7
6.12 c                   leavesr
6.12 c                   endif
6.12 c                   else
      * som før, varenr = 6
     c                   eval      b_vnum = *on
     c                   eval      w_vnix = 0
     c                   dow       w_vnix < 6 and
     c                             b_vnum = *on
     c                   eval      w_vnix = w_vnix + 1
     c                   if        %subst(ldlvar:w_vnix:1) < '0' or
     c                             %subst(ldlvar:w_vnix:1) > '9'
     c                   eval      b_vnum = *off
     c                   endif
     c                   enddo
     c                   if        b_vnum = *on
     c                   movel     ldlvar        w_prnr
     c                   move      w_prnr        d_prnr
     c                   leavesr
     c                   endif
6.12  *
6.12 c                   endif
     c                   endif
      *
     c                   if        ldnobb <> 0
     c                   move      ldnobb        d_nonr
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file les FIRMA-OPPLYSNINGER
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      *  Key for file les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_filn
     c*                  eval      p_numm = 906
     c*                  eval      p_suff = 0
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
      *
      *  Hent opplysninger fra FIRMA og LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c     afirl1_key    chain     afirl1r                            90
      *
     c                   endsr
