     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX829R
      * Beskrivelse . . : FIX: Oppdaterer alternativ varegruppe i statistikk
      *
      *                   Leser gjennom salgsstatistikk, og oppdaterer
      *                   alternativ varegruppe fra vare. Dersom vare ikke
      *                   finnes hentes varegruppe fra varegruppeknytning.
      *                   Behandler kun salg uten alternativ varegruppe
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070504 HKO  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstapf    uf   e           k disk    rename(sstapfr:sstapfr)
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvgkl1    if   e           k disk    rename(vvgkpfr:vvgkl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvgkl1_ogrp     s                   like(vkogrp)
     d vvgkl1_hgrp     s                   like(vkhgrp)
     d vvgkl1_ugrp     s                   like(vkugrp)
     d vvgkl1_ldor     s                   like(vkldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser hele salgsstatistikken for oppdatering
      *
     c                   read      sstapfr                                90
     c                   dow       *in90 = *off
      *
     c                   if        sffirm = w_firm and
     c                             sfvare <> *blank and
     c                             sfahgr = 0
     c                   exsr      hent_vgrp
     c                   update    sstapfr
     c                   endif
      *
     c                   read      sstapfr                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente alt. varegruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_vgrp     begsr
      *
     c                   eval      vvarl1_vare = sfvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c     vvarl1_key    chain     vvasl1
     c                   endif
     c                   if        %found and
     c                             vvahgr <> 0
     c                   eval      sfahgr = vvahgr
     c                   eval      sfaugr = vvaugr
     c                   endif
      *
     c                   if        sfahgr <> 0 or
     c                             sfogrp = 0
     c                   leavesr
     c                   endif
      *
      * Leser på varegruppeknytning
      * Leter først etter knytninger mot leverandør
      *
     c                   eval      vvgkl1_ogrp = sfogrp
     c                   eval      vvgkl1_hgrp = sfhgrp
     c                   eval      vvgkl1_ugrp = sfugrp
     c                   eval      vvgkl1_ldor = sfldor
      *
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = sfldor
     c                   eval      vvgkl1_ugrp = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = sfldor
     c                   eval      vvgkl1_hgrp = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        *in91 = *off
     c                   eval      sfahgr = vkahgr
     c                   eval      sfaugr = vkaugr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på varegruppeknytning
      *
     c     vvgkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl1_ogrp
     c                   kfld                    vvgkl1_hgrp
     c                   kfld                    vvgkl1_ugrp
     c                   kfld                    vvgkl1_ldor
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
