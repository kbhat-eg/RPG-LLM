      /TITLE  ASLAGR: Lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LE611R
      *  Beskrivelse . . : Utskrift av avvik-liste, plukker varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
5.40  *           090107  Justert sortering
5.70  *  PRGR     021208  Utvidet med prisgruppe                   bof
6.10  *  6.10     040810  Henter pris fra et annet prishentings-   BSA
6.10  *                   program.
6.11  *  6.10     060111  Sjekker om varetype = L,bare L skal på liste BOF
6.20  *  6.20     190111  Legger ut timestamp fra telling(syklisk) bof
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.30  * 6.30      241017  Skaffevarer skal ut på lista             nho
6.31  *           121118  Utvidet med trelast-sort                 bof
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flsorlu    uf a e           k disk    rename(lsorpfr:lsorlur)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_batc                       10    overlay(d_prec)
     d  d_fogr                        2  0 overlay(d_prec:11)
     d  d_togr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_thgr                        2  0 overlay(d_prec:17)
     d  d_fugr                        3  0 overlay(d_prec:19)
     d  d_tugr                        3  0 overlay(d_prec:22)
     d  d_fvar                       15    overlay(d_prec:25)
     d  d_tvar                       15    overlay(d_prec:40)
     d  d_flag                        2  0 overlay(d_prec:55)
     d  d_tlag                        2  0 overlay(d_prec:57)
     d  d_val1                        1  0 overlay(d_prec:59)
     d  d_val2                        1  0 overlay(d_prec:60)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lsorlu_lage     s                   like(lwlage)
     d lsorlu_lagr     s                   like(lwlagr)
     d lsorlu_numm     s                   like(lwnumm)
     d lsorlu_vare     s                   like(lwvare)
     d ltell1_batc     s                   like(lebatc)
     d ltell1_line     s                   like(leline)
     d ltell1_numm     s                   like(lenumm)
     d ltell1_suff     s                   like(lesuff)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_numm          s                   like(lenumm)
     d w_parm          s             64
     d w_firm          s                   like(vvfirm)
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
6.10 d w_inpr          s              9  2
6.11 d w_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.31 d w_lv_nobb       s                   like(vvnobb)
6.31 d w_lv_modn       s                   like(vvnmod)
6.31 d w_lv_lldo       s                   like(vvldor)
6.31 d w_lv_llva       s                   like(vvlvar)
      *
      * Definering av parametre
      *
     d p_dato          s               d   datfmt(*iso)
     d p_time          s               t   timfmt(*iso)
     d p_inlr          s              1
     d p_lety          s              1  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      d_prec = w_parm
      *
     c                   eval      ltell1_batc = d_batc
     c                   eval      ltell1_numm = *zero
     c                   eval      w_numm = 999999
      *
     c     ltell1_key    setll     ltell1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start         tag
      *
     c                   eval      *in90 = *off
      *
     c                   read      ltell1r                                90
      *
     c                   if        *in90 = *on
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er firmanummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        lefirm <> w_firm
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er batchnummer innenfor grensene, Ellers hopp til Slutt
      *
     c     lebatc        cabne     ltell1_batc   slutt
     c                   if        lebatc <> ltell1_batc
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er listenummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        lenumm > w_numm
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er listenummer en subliste        Hvis nei les neste post
      *
     c     lesuff        cabne     *zero         start
     c                   if        lesuff <> *zero
     c                   goto      start
     c                   endif
6.11  *
6.11  *  Sjekk   Er varen L på lageret som gjelder
6.11  *
6.11 c                   call      'VL710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    levare
6.11 c                   parm                    lelage
6.11 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
6.30  *
6.30 c                   if        w_vtyp = 'S'
6.30 c                   goto      okskaf
6.30 c                   endif
6.30  *
6.11 c                   if        w_vtyp <> 'L'
6.11 c                   goto      start
6.11 c                   endif
      *
6.30 c     okskaf        tag
6.30  *
      *  Sjekk   Er overgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        leogrp < d_fogr
     c                             or leogrp > d_togr
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er hovedgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        lehgrp < d_fhgr
     c                             or lehgrp > d_thgr
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er Undergruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        leugrp < d_fugr
     c                             or leugrp > d_tugr
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er Varenummer  innenfor grensene, Hvis nei les neste post
      *
     c                   if        levare < d_fvar
     c                             or levare > d_tvar
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er Lagernummer innenfor grensene  Hvis nei les neste post
      *
     c                   if        lelage < d_flag
     c                             or lelage > d_tlag
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Diversevarer skal ikke være med
      *
     c                   if        ledlop <> *zero
     c                   goto      start
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk opplysninger i Plukk-register
      *  Utfør de riktige valgene utfra hva som er tastet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vvarl1_vare = levare
     c     vvarl1_key    chain     vvarl1r                            90
      *
      *
     c                   eval      p_lety = 0
     c                   eval      p_inlr = ' '
     c                   move      udate         p_dato
5.70 c                   exsr      hent_prisgr
      *
      * Henter og beregner prisgrunnlag fra vareregisteret
      *
6.10 c**                 call      'VP700R'
6.10 c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    levare
5.70 c                   parm                    w_prgr
     c                   parm                    leenh1
     c                   parm                    p_lety
     c                   parm                    p_dato
6.10 c**                 parm                    p_time
     c                   parm                    p_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
6.10 c**                 parm                    w_tipr
     c                   parm                    w_kopr
6.10 c                   parm                    w_inpr
      *
     c                   if        lespny = *zero
     c                   eval      lespny = w_sapr
     c                   endif
     c                   if        lekpny = *zero
     c                   eval      lekpny = w_kopr
     c                   endif
     c                   if        letek1 = *blank
     c                   eval      letek1 = vvtek1
     c                   endif
      *
      *  Oppdater Sorterings-register med ny record
      *
     c                   eval      lsorlu_lage = lelage
     c                   eval      lsorlu_vare = levare
     c                   eval      lsorlu_numm = ledlop
     c     lsorlu_key    chain     lsorlur                            92
      *
      *  Danner sorteringsrekkefølge
      *
     c                   if        d_val1 = 1
     c                   eval      lwssum = *zero
5.40 c                   eval      lwogrp = leogrp
     c                   eval      lwhgrp = lehgrp
     c                   eval      lwugrp = leugrp
     c                   endif
      *
     c                   if        d_val1 = 3
     c                   eval      lwssum = *zero
5.40 c                   eval      lwogrp = *zero
     c                   eval      lwhgrp = *zero
     c                   eval      lwugrp = *zero
     c                   endif
      *
     c                   eval      lwspny = lespny
     c                   eval      lwkpny = lekpny
     c                   eval      lwantt = leantt
     c                   eval      lwantl = leantl
     c                   eval      lwtek1 = letek1
     c                   eval      lwenh1 = leenh1
6.20 c                   eval      lwtims = letims
      *
     c                   if        *in92 = *off
     c                   update    lsorlur
     c                   else
     c                   eval      lwfirm = w_firm
     c                   eval      lwlage = lsorlu_lage
     c                   eval      lwvare = lsorlu_vare
     c                   eval      lwnumm = lsorlu_numm
     c                   eval      lwlagr = lsorlu_lagr
     c                   write     lsorlur
     c                   endif
      *
      *  Velg neste Bestilling
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lelage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER-TELLE
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
     c                   kfld                    ltell1_numm
     c                   kfld                    ltell1_suff
     c                   kfld                    ltell1_line
      *
      *  Key for oppdatering LAGERUTSKRIFT-HJELPE
      *
     c     lsorlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lsorlu_lage
     c                   kfld                    lsorlu_vare
     c                   kfld                    lsorlu_numm
     c                   kfld                    lsorlu_lagr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
