     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FA933R
      * Beskrivelse . . : Oppslag mot 1881 via web-service
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070420 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
     d p_navn          s             30
     d p_adr1          s             30
     d p_adr2          s             30
     d p_sted          s             30
     d p_mobn          s             11
     d p_ponr          s              4  0
     d p_ftnr          s              9  0
      *
      * Definering av variable
      *
     d w_akey          s           4000
     d w_akeyut        s           4014
     d w_indata        s           2000
     d w_url           s            200
     d w_urlv          s            100
     d w_reqf          s            100
     d w_rspf          s            100
     d w_usrn          s             20
     d w_pass          s             50
     d w_reqt          s              6
     d w_enco          s             20
     d w_stat          s              1  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_start         s              4  0
     d w_slutt         s              4  0
     d w_lengd         s              4  0
     d w_party         s             10
     d w_adres         s             30
     d w_house         s              5
     d w_orgnr         s              9
     d IFS_Output1     s            256a   Varying
     d IFS_Output2     s            256a   Varying
     d IFS_Input       s            256a   Varying
     d API_Output2     s            256a   Varying
     d API_Input       s            256a   Varying
     d IFS_path        s            256a   Varying
     D Len             S             10I 0
      *
      /copy prototypeb
      *--------------------------------------------------------------------
      * Change Directory
      *
      * int chdir(const char *path)
      *--------------------------------------------------------------------
     D chdir           PR            10I 0 ExtProc('chdir')
     D   path                          *   Value Options(*string)

     D fd              S             10I 0
     D RW              C                   6
     D R               C                   4
     D OWNER           C                   64
     D GROUP           C                   8
      /copy usec
      *
      * Datastruktur for IFS fra 1881
      *
     d Kundeionfo      ds                  qualified
     d                                     dim(99)
     d   Posting_Date                10a
     d   Document_Type...
     d                               10a
     d   Document_No                  8a
     d   Customer_No                  6a
     d   Description                 26a
     d   Amount                      11a
     d   Remaining_Amount...
     d                               11a
     d   Due_Date                    10a
      *
      * Hovedprogram:
      *
      *  - slår opp med telefonnummer
      *  - mottar responsfil fra API
      *  - henter ut data fra responsfil
      *  - rydder på IFS
      *
      * Oppretter request file (dummy)
      *
     c                   eval      IFS_path = '/home/' +
     c                                        l_figr +
     c                                        '/1881/'
      *
     c                   eval      IFS_Output1 = '1881req' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request'
      *
     c                   if        chdir(%Trim(IFS_path)) >= 0
     c                   eval      fd = open(IFS_Output1 :
     c                               O_CREAT+O_WRONLY+O_CODEPAGE :
     c                               RW*OWNER + RW*GROUP + R : 1252  )
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   callp     close(fd)
      *
      * Oppretter headers file
      *
     c                   eval      IFS_Output2 = '1881req' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request.headers'
      *
     c                   if        chdir(%Trim(IFS_path)) >= 0
     c                   eval      fd = open(IFS_Output2 :
     c                               O_CREAT+O_WRONLY+O_CODEPAGE :
     c                               RW*OWNER + RW*GROUP + R : 1252  )
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   callp     close(fd)
      *
      * Åpnes igjen for å skrive inn APIKEY
      *
     c                   eval      w_akeyut = %triml('eg-apps-token=' + w_akey)
     c                   eval      fd = open(IFS_Output2 :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   eval      w_size = %size(w_akeyut)
     c                   callp     write(fd: %addr(w_akeyut): w_size)
     c                   callp     close(fd)
      *
      * Bygger navn på retur file
      *
     c                   eval      IFS_Input = '1881req' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
      *
      * Initierer og kaller webservice
      *
     c                   eval      API_Output2 = %triml(IFS_path + IFS_Output1)
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
     c                   eval      w_reqt = 'GET'
     c                   eval      w_usrn = '           '
     c                   eval      w_pass = '            '
     c                   eval      w_reqf = API_Output2
     c                   eval      w_rspf = API_Input
     c                   eval      w_enco = 'ISO-8859-1'
     c*                  eval      w_enco = 'UTF-8'
     c                   eval      w_stat = 0
      *
     c                   call      'AW702C'
     c                   parm                    w_url
     c                   parm                    w_reqt
     c                   parm                    w_reqf
     c                   parm                    w_rspf
     c                   parm                    w_usrn
     c                   parm                    w_pass
     c                   parm                    w_enco
     c                   parm                    w_stat
      *
      * Henter ut data fra returfil
      *
     c                   eval      IFS_Input = '1881req' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
     c                   eval      fd = open(API_Input :
     c                               O_RDONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   eval      len = read(fd: %addr(w_indata):
     c                                            %size(w_indata))
      /FREE

      *
      * Partstype
      *
           w_start = %scan('partyType' : w_indata);
           w_start = w_start + 12;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           w_party = %subst(w_indata:w_start : w_lengd);
      *
      * Navn
      *
           w_start = %scan('name' : w_indata);
           w_start = w_start + 7;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           p_navn = %subst(w_indata:w_start : w_lengd);
      *
      * Orgnummer for Company
      *
         if w_party = 'Company';
           w_start = %scan('organizationNumber' : w_indata);
           w_start = w_start + 21;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           w_orgnr = %subst(w_indata:w_start : w_lengd);
           p_ftnr  = %dec(w_orgnr:9:0);
         endif;
      *
      * Adresse og husnummer
      *
           w_start = %scan('streetAddress' : w_indata);
           w_start = w_start + 16;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           w_adres = %subst(w_indata:w_start : w_lengd);

           w_start = %scan('houseNumber' : w_indata);
           w_start = w_start + 14;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           w_house = %subst(w_indata:w_start : w_lengd);

           p_adr1 = %trim(w_adres) + ' ' + %trim(w_house);
      *
      * Postnummer og sted
      *
           w_start = %scan('postalCode' : w_indata);
           w_start = w_start + 13;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           p_ponr  = %dec(%subst(w_indata:w_start : w_lengd):4:0);

           w_start = %scan('city' : w_indata);
           w_start = w_start + 7;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           p_sted  = %subst(w_indata:w_start : w_lengd);

           p_sted = %char(p_ponr) + ' ' + %trim(p_sted);

      /END-FREE
      *
      * Rydder opp i filer på IFS
      *
     c                   eval      fd = open(IFS_Output2 :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   callp     unlink(IFS_Output1)
     c                   callp     unlink(IFS_Output2)
     c                   callp     unlink(IFS_Input)
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_navn = '1881 søk feilet'
     c                   eval      p_adr1 = *blank
     c                   eval      p_adr2 = *blank
     c                   eval      p_ponr = 0
     c                   eval      p_sted = *blank
     c                   eval      p_mobn = *blank
     c                   goto      avslutt
      *
     c                   endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_navn
     c                   parm                    p_adr1
     c                   parm                    p_adr2
     c                   parm                    p_sted
     c                   parm                    p_mobn
     c                   parm                    p_ponr
     c                   parm                    p_ftnr
      *
      * Henter inn løpenummer teller
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'TE1881'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    p_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      /Free

      *
      * Bygger URL
      *
      *  Sjekker først om link er lagt inn i AFPSPF
      *    - finnes den ikke, så bruk den faste produksjons linken
      *
         exec sql
          select afudss
          into   :w_urlv
          from   afpspf
          where  afuide = 'cldPartUrl';

         if w_urlv = ' ';
            w_urlv = 'https://party-enricher.cs.egapps.no/';
         endif;

         w_url = %trim(w_urlv) + 'api/partyenrich+
                 /enrichByPhoneNumber?enricherType=API1881&phoneNumber=' +
                 %char(p_mobn);

      *
      * Henter inn appkey fra COMPANY_SETTINGS
      *
       exec sql
        select aposkval
        into   :w_akey
        from   aposextnst
        where  aposfirm = :p_firm and
               apossyst = 'EGAPPS' and
               aposckey = 'CLOUD_COMMON_API_KEY';

      /End-Free
      *
     c                   endsr
