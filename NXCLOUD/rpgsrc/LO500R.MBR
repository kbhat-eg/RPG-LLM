     h option(*nodebugio  : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO500R
      * Beskrivelse . . : Spørring / søking etter bestilling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150699 ASK  Nytt program
5.34  *       011204 GRO  Endret overgang FO500R/LO500R mhp startverdi
5.34  *                   og indikator for kallende prog.
5.50  *       201005 KOB  Lagt inn selektering på bestillinger før dato
5.50  *                   og uten leverandørens ordrenummer
5.51  *       251005 BOF  Valg på matcing av varel. mot ordre
      *       071105 HKO  Initierer ikke *in22 dersom parmeter som input-
      *                   parameter
      *       150307 BOF  Lagt inn bla-alt på selger
5.60  *       061207 AMD  Lagt inn bla-alt på avdeling
5.60  * V5.60 230308 KOB  Bla-alt påselger fungerte ikke - sjekket mot ordreregisteret
      *
6.10  *       110412 bof  nullstilt en returkode før man sletter.
6.20  * V6.20 270410 bof  Hvis ordre er i bruk, så gis melding hvis valg = 2
6.21  * V6.20 030511 bsa  Sjekk om ordre er i bruk ved matching av ordre (55)
6.22  * V6.20 040112 bsa  Aksepterer bruk av ordre, hvis det er samme bruker
6.22  *                   Da er vi sansynligvis inne i en ordre/bestilling situasjon
6.23  * V6.20 130112 bsa  Hvis overgang til historikk, tar vi med oss evnt bestillingsnummer
6.24  * V6.20 180412 bof  Henter høyste loggkode, ikke siste
6.30  * V6.30 040214 bsa  Filter for å vise forsinkede bestillinger
6.31  * V6.30 160214 bof  Hvis validering på lolmat, så sjekk om best. har lev.måte
6.32  * V6.30 110414 bsa  Sjekker mot folmat og ikke lolmat.           har lev.måte
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.33  * V6.30 300914 bof  sjekk på bytt dato, rekkef. virkelig,bekreftet, ønsket
6.34  * 6.30  090715 ask  Brukerbegr. endre og slette pakksedler får egne koder.
6.34  *                   Endre/slette faktura kan bare gjøres av de som har kan fakturere
6.35  * 6.30  110915 bsa  Endret posisjonering av pop up. JIT GUI
6.36  * 6.30  280116 bof  Spørsmål om man skal beholde ordrereferanse
6.37  * 6.30  100816 bsa  Viser hvem som sperrer bestilling før fjerning av kode.
      *
7.xx  * 7.00  040216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  161018 bkv  Vasker vekk probl Newlook tegn
7.02  * 7.00  270319 bkv  Frisøk
7.03  * 6.30  240915 bsa  Endring av sjekk mot rettigheter for endring av ordre med
7.03  *                   behandlingskode 3. Justering av endring 6.34
7.04  * 6.30  210316 ask  Henter avdeling fra ausrpf og setter "startverdi" på filter
7.05  * 6.30  190516 bof  Farge Rød, best.knyttet til ordre m/levtype=1 Blå: ordre=kontantkunde
7.06  *       220619 bof  Skal ikke kunne fylle ut beholde ordre.ref
7.07  *       061119 bsa  Skal ikke kunne plukkes hvis elektronisk pakkseddel er mottatt.
7.08  *       181219 nho  Flytter riktig best.nr og navn ut dersom slett etter kopiering
7.09  * K000  220420 bsa  Utvider til 27x132 skjermbilde. Viser også vekt og volu.
7.10  * K000  300420 bsa  Ved valg 18=Send bestilling, skal vanlig utskrift trigges hvis
7.10  * K000              ikke alt er riktig satt opp på leverandørdistribusjon.
7.11  * K000  250620 bsa  Legger ut ordrenummer, forsendelsemåte og bekr.levdato
7.11  * K000              i skjermbilde.
7.12  * K000  180820 bof  Vaske feltet B2FSO2  for - (bindestrek)
7.13  * K000  120819 sst  Splitte IP i to IP m/samme nr men nytt suffix Blå: ord
      *
7.fb  * K000  140117 bof  Hvis depositumskrav og ikke betalt.
      *
8.01  * K000  120923 bsa  Sjekker om tilknyttet salgsordre er
8.01  * K000              dellevert. Hvis så er tilfelle, skal det
8.01  * K000              ikke være mulig å matche. (Valg 55).
8.02  * K000  261023 bsa  Fortsetter sjekk om dellevering. Endrer logikk til samme
8.02  * K000              som ved F3 fra LD100R.
8.03  * K000  021123 bsa  Fortsetter sjekk om dellevering.
8.04  * K000  020224 bsa  Fortsetter dellevering, sjekker på bestnr/ordre.
8.05  * K000  111124 bsa  Ved retur fra valg 16, skal den starte med samme
8.05  * K000              bestillingsnummer som er behandlet.
8.06  * K000  170125 bsa  Endrer sjekk mot dellevering. Må teste på linjer, da
8.06  * K000              en ordre kan være knyttet mot flere bestillinger.
8.07  * K000  270625 bsa  Feil logikk ved kalkulering av dellevring
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohela    if   e           k disk    rename(lohepfr:lohelar)
     floheld    if   e           k disk    rename(lohepfr:loheldr)
     flohell    if   e           k disk    rename(lohepfr:lohellr)
     flohelo    if   e           k disk    rename(lohepfr:lohelor)
     flohelv    if   e           k disk    rename(lohepfr:lohelvr)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
7.04 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
6.31 favall1    if   e           k disk    rename(avalpfr:avall1r)
7.05 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.07 flbhoi4    if   e           k disk    rename(lbhost:lbhoi4r)
7.10 flldilr    if   e           k disk    rename(lldipfr:lldilrr)
7.fb fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
8.06 flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
8.06 ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
      *
     flo500d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
6.nu d p_numm          s                   like(lonumm)
     d p_lpro          s              1
6.21 d p_stat          s              1
      *
      * Definering av array
      *
     d a_mld1          s             11    dim(3) ctdata perrcd(1)
6.20 d a_mld2          s             50    dim(2) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
6.21 d l_arch                636    636  0
6.21 d l_skje                637    637  0
6.21 d l_mail                640    640  0
     d l_wsid                901    910
     d l_user                911    920
7.00 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d lohelr_num2     s                   like(lonumm)
     d lohelr_suf2     s                   like(losuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohela_lena     s                   like(lolena)
     d loheld_bdat     s                   like(lobdat)
     d lohell_ldat     s                   like(loldat)
     d lohelo_otyp     s                   like(lootyp)
     d lohelv_navn     s                   like(lonavn)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d rlevl1_levr     s                   like(rllevr)
     d lusrl1_user     s                   like(lbuser)
7.04 d ausrl1_user     s                   like(abuser)
     d votyl1_otyp     s                   like(vaotyp)
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
6.31 d avall1_apgm     s                   like(avapgm)
7.05 d rkunl1_kund     s                   like(rkkund)
7.07 d lbhoi4_hnum     s                   like(pbhnum)
7.07 d lbhoi4_hsuf     s                   like(pbhsuf)
7.10 d lldilr_ldor     s                   like(lpldor)
7.10 d lldilr_lage     s                   like(lplage)
7.fb d sdepl1_kund     s                   like(skkund)
7.fb d sdepl1_numm     s                   like(sknumm)
7.fb d sdepl1_tell     s                   like(sktell)
8.06 d lodtlr_numm     s                   like(lonumm)
8.06 d lodtlr_suff     s                   like(losuff)
8.06 d lodtlr_line     s                   like(ldline)
8.06 d fodtlv_vare     s                   like(fdvare)
8.06 d fodtlv_numm     s                   like(fdnumm)
8.06 d fodtlv_suff     s                   like(fdsuff)
8.06 d fodtlv_enh1     s                   like(fdenh1)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_belp          s              9  0
     d w_salg          s             12
     d w_seqe          s              1
     d w_sjek          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
3.14 d b_exit          s              1    inz(*off)
6.34 d b_stop          s              1    inz(*off)
7.02 d b_open_sok      s              1    inz(*off)
7.02 d sqlquery        s           4096
7.02 d sqlwhere        s           2096
7.02 d sql_fri_s       s             50
7.02 d sql_fri_sl      s             50
7.02 d x               s              1  0
7.02 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
7.02 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
7.02 d w_ste1          s             35
7.02 d w_ste2          s             35
7.02 d w_ste3          s             35
7.02 d w_ste4          s             35
7.02 d w_ste5          s             35
7.02 d w_fsok          s                   like(b2fsok)
7.12 d w_fso2          s                   like(b2fso2)
      *
     d w_firm          s                   like(lofirm)
     d w_navn_gml      s                   like(lonavn)
     d w_lnav          s                   like(rlnavn)
     d w_ityp          s              2
     d w_ity1          s              1
     d w_itxt          s             35
5.60 d w_gtxt          s             20
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_moms          s              1    inz(*off)
      *
     d w_enor          s              1
     d w_ftor          s              1
     d w_retu          s              1
     d w_vare          s             15
     d w_ldor          s                   like(loldor)
     d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_numx          s                   like(lonumm)
     d w_sufx          s                   like(losuff)
     d w_otyp          s                   like(lootyp)
     d w_navn          s                   like(rlnavn)
     d w_pdir          s              1  0
     d w_outq          s             10
     d w_typd          s              4
     d w_kom1          s             30
     d w_kom2          s             30
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_kode          s              1
     d w_logk          s              1
     d w_val1          s              1  0
     d w_val2          s              2  0
     d w_mld1          s             50
     d w_mld2          s             50
7.07 d w_mld3          s             50
     d w_eror          s              1
     d w_okod          s              2  0
6.20 d w_retk          s              1
6.23 d w_ntyp          s              1
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
7.09 d w_vekt          s              9  3
7.09 d w_volu          s              9  3
7.09 d w_avvi          s              1
7.13 d w_tilgko        s             10
7.13 d w_felt          s             10
7.13 d w_rutine        s             10
7.13 d w_adv           s              3
7.13 d w_status        s             10
8.04 d w_btel          s              2  0
8.06 d w_test          s              2  0
8.06 d w               s              1  0
      *
T5.50d w_datr          s                   like(lobdat)
6.34 d s_otyp          s                   like(footyp)
7.fb d b_depo          s              1
      * Eksternt program
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.03 d u_703           s              1    inz(*off)
7.04 d u_704           s              1    inz(*off)
7.05 d u_705           s              1    inz(*off)
7.06 d u_706           s              1    inz(*off)
      *
      * Definering av konstanter
      *
7.09 d c_sfil          s              2  0 inz(14)
      *
6.31  * Firmastyrt validering av felt
6.31  *
6.31 d u_lmat          s              1    inz(*off)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene D1WIN og K1WIN
      *             er referert til dette.
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
      *
      * F1= Bla-alternativer
      *
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2taga
      *
      * F2= Velg ordre fra - til
      *
     c                   when      *inkb
     c                   call      'LO602R'
     c                   parm                    w_ftor
     c                   parm                    w_firm
     c                   goto      b2tagb
      *
      * F3= Avslutt (+ F12)
      *
     c                   when      *inkc or *inkl
     c                   eval      p_lpro = *blank
     c                   goto      avslutt
      *
      * F5= Forny
      *
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
      *
      * F6= Opprette ny ordre
      *
     c                   when      *inkf
     c                   eval      w_numm = 0
     c                   eval      w_suff = 0
     c                   call      'LO110R'
     c                   parm                    b1valg
6.nu c                   parm                    w_numm
     c                   parm                    w_suff
      *
     c                   if        w_numm <> 0
     c                   eval      b2numm = w_numm
     c                   exsr      posisjoner
     c                   endif
     c                   goto      b2tagb
      *
      * F7= Vise vare/bestillingslinjer
      *
     c                   when      *inkg
     c                   call      'LO526R'
     c                   parm                    w_ftor
     c                   parm                    w_firm
     c                   goto      b2tagb
      *
      * F9= Vise varenr/ordrelinjer
      *
     c                   when      *inki
     c                   call      'FO520R'
     c                   parm                    w_eror
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tagb
      *
      * F10= Vise ordrehistorikk
      *
     c                   when      *inkj
6.23 c                   eval      w_numm = 0
6.23 c                   eval      w_ntyp = '1'
     c                   exsr      historikk
     c                   goto      b2tagb
      *
      * F11= Spørr på bestilling
      *
     c                   when      *inkk
     c                   call      'LO512R'
     c                   goto      b2taga
      *
      * F15= Starter utskrift
      *
     c                   when      *inkp
     c                   call      'LO604R'
     c                   parm                    w_ftor
     c                   parm                    w_firm
6.nu c                   parm                    w_numx
     c                   parm                    w_sufx
     c                   parm                    w_otyp
     c                   parm                    w_pdir
     c                   parm                    w_outq
     c                   goto      b2tagb
      *
      * F16= Vise utplukk
      *
     c                   when      *inkq
     c                   call      'LO736R'
     c                   parm                    w_ftor
     c                   parm                    w_firm
6.nu c                   parm                    w_numx
     c                   parm                    w_sufx
     c                   goto      b2taga
7.02  *
7.02  * F21=Hent søk
7.02  *
7.02 c                   when      *inkv
7.02 c                   eval      b2fsok = w_fsok
7.02 c                   eval      *in22 = *on
7.02 c                   goto      b2tagb
      *
      * F22= Vise inkl/eks mva.
      *
     c                   when      *inkw
     c                   if        *in71   = *on
     c                   if        w_moms  = *off
     c                   eval      w_moms  = *on
     c                   eval      b2hed2  = 'Inkl. mva.'
     c                   else
     c                   eval      w_moms  = *off
     c                   eval      b2hed2  = 'Best.verdi'
     c                   endif
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
      *
      * F23= Vise 2
      *
     c                   when      *inkx
     c                   if        *in71  = *off
     c                   eval      *in71  = *on
     c                   eval      b2hedd = 'Best.dato'
7.11 c                   eval      b2hed3 = 'Bekr.ldat'
     c                   eval      b2hed2 = 'Best.verdi'
     c                   else
     c                   eval      *in71  = *off
7.11 c                   eval      b2hedd = 'Sendt dat'
7.11 c                   eval      b2hed3 = 'Ønsk.ldat'
7.11 c**                 eval      b2hed2 = '     Ordre'
7.11 c                   eval      b2hed2 = 'Best.verdi'
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
      *
      * Roll
      *
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
      *
      * Styr cursor
      *
     c                   when      *in21
     c                   eval      *in22  = *on
     c                   goto      b2taga
      *
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      xsjekk_inp
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0      or
     c                             b2dato <> 0      or
     c                             b2lena <> *blank or
7.02 c*                             b2navn <> *blank or
     c                             b2otyp <> *blank or
     c                             b2fsok <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_exit = *off
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01  /FREE
7.01
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 5 ;
7.01       w_len = %len(%trim(b1navn));
7.01       if (w_len > 0);
7.01       b1navn = %ScanRpl('.':'':b1navn:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1navn));
7.01       if (w_len > 0);
7.01       b1navn = %ScanRpl(':':'':b1navn:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1lena));
7.01       if (w_len > 0);
7.01       b1lena = %ScanRpl('.':'':b1lena:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1lena));
7.01       if (w_len > 0);
7.01       b1lena = %ScanRpl(':':'':b1lena:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01     endfor ;
7.01   endsr;
7.01  /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.12  * Newlook vask av data i B2FSO2
7.12  *  Fjerner tegn som gjør at Newlook feiltolker
7.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.12  *
7.12 c     vask2         begsr
7.12 c                   eval      w_fso2 = b2fso2
7.12 c     '-':' '       xlate     w_fso2        w_fso2
7.12 c                   eval      b2fso2 = w_fso2
7.12 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c                   eval      b2fsok = *blank
7.02 c                   eval      b2fso2 = *blank
7.02 c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'A'
     c     lohela_key    setll     lohela
     c                   when      w_seqe = 'D'
     c     loheld_key    setll     loheld
     c                   when      w_seqe = 'L'
     c     lohell_key    setll     lohell
     c                   when      w_seqe = 'O'
     c     lohelo_key    setll     lohelo
     c                   when      w_seqe = 'V'
     c     lohelv_key    setll     lohelv
     c                   other
     c     lohel1_key    setll     lohel1
     c                   endsl
      *
     c                   eval      *in22 = *off
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2numm <> 0
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = ' '
     c                   eval      lohel1_numm = b2numm
     c                   eval      lohel1_suff = b2suff
     c     lohel1_key    setll     lohel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordretype
      *
     c                   if        b2otyp <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'O'
     c                   eval      lohelo_otyp = b2otyp
     c     lohelo_key    setll     lohelo
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner på ordrenummer dersom ordretype er
      * valgt både som posisjonering og selektering
      *
     c                   if        b2otyp <> *blank
     c                   if        h1otyp <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = ' '
     c                   eval      lohel1_numm = *zero
     c                   eval      lohel1_suff = *zero
     c     lohel1_key    setll     lohel1
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner startverdi på levr.navn
      *
     c                   if        b2lena <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'A'
     c                   eval      lohela_lena = b2lena
     c     lohela_key    setll     lohela
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vareadresse
      *
7.02 c*                   if        b2navn <> *blank
7.02 c*                   eval      w_seqe = 'V'
7.02 c*                   eval      lohelv_navn = b2navn
7.02 c*     lohelv_key    setll     lohelv
7.02 c*                   goto      end_pos
7.02 c*                   endif
      *
      * Posisjoner startverdi på ordredato
      *
     c                   if        b2dato <> 0
7.02 c                   eval      b2fso2 = *blank
     c                   if        *in71  =  *on
     c                   eval      w_seqe = 'D'
      *
     c     *dmy          move      b2dato        loheld_bdat
     c     loheld_key    setll     loheld
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner startverdi på leveringsdato
      *
     c                   if        b2dato <> 0
7.02 c                   eval      b2fso2 = *blank
     c                   if        *in71  =  *off
     c                   eval      w_seqe = 'L'
      *
     c     *dmy          move      b2dato        lohell_ldat
     c     lohell_key    setll     lohell
     c                   goto      end_pos
     c                   endif
     c                   endif
7.02  *
7.02  * Posisjoner startverdi på frisøk
7.02  *
7.02 c                   if        b2fsok <> *blank
7.02 c                   eval      b2fso2 = b2fsok
7.12 c                   exsr      vask2
7.02 c                   eval      w_fsok = b2fsok
7.02 c                   eval      b_open_sok = *on
7.02 c                   eval      w_seqe = 'S'
7.02 c                   goto      end_pos
7.02 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
     c                   eval      b2suff = 0
     c                   eval      b2dato = 0
     c                   eval      b2otyp = *blank
     c                   eval      b2lena = *blank
7.02 c*                   eval      b2navn = *blank
7.02 c                   eval      b2fsok = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_exit = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      wvisrn = w_srrn
7.13  *
7.13  *   Har bruker tilgang til dette valg?
7.13  *
7.13 c                   eval      w_tilgko = l_user
7.13 c                   eval      w_rutine = 'LO500R'
7.13 c                   eval      w_status = *blank
7.13 c                   move      b1valg        w_adv
7.13 c                   call      'AD005R'
7.13 c                   parm                    w_tilgko
7.13 c                   parm                    w_rutine
7.13 c                   parm                    w_adv
7.13 c                   parm                    w_status
7.13 c                   if        w_status <> 'Ok'
7.13 c                   eval      b1valg = 0
7.13 c                   exsr      xfarge
7.13 c                   update    b1sfl
7.13 c                   iter
7.13 c                   endif
      *
      * Hvem behandler ordre ?
      *
     c                   if        b1valg = 8
     c                   call      'LO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette binding av ordre
      *
     c                   if        b1valg = 9
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c                   exsr      xb9win
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Er ordre ledig for bruk ?
      *
     c                   exsr      xsjekk_ord1
     c                   if        w_sjek = *on
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endring av ordre
      *
     c                   if        b1valg = 2
6.20  *
6.20 c                   eval      lohelr_numm = b1numm
6.20 c                   eval      lohelr_suff = b1suff
      *
6.34  * Endring av ordre, har bruker lov ?
6.34  *
6.34 c                   exsr      xsjekk_paks
6.34 c                   if        b_stop = *on
6.34 c                   eval      w_mld1 = 'Bruker kan ikke endre dokument'
6.34 c                   call      'AA005R'
6.34 c                   parm                    w_mld1
6.34 c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
6.34 c                   update    b1sfl
6.34 c                   iter
6.34 c                   endif
6.34  *
6.20 c     lohelr_key    chain     lohelr
6.20 c                   if        %found(lohelr) and
6.20 c                             loornr > 0
6.20 c                   eval      fohelr_numm = loornr
6.20 c                   eval      fohelr_suff = loorsu
6.20 c     fohelr_key    chain     fohelr                             90
6.20 c                   if        %found(fohelr) and
6.20 c                             fokode <> 0
6.20 c                   eval      w_mld1 = a_mld2(2)
6.22 c                   if        fokous <> l_user
6.20 c                   call      'AA005R'
6.20 c                   parm                    w_mld1
6.22 c                   endif
6.20 c                   endif
6.20 c                   endif
      *
     c                   eval      w_val2 = b1valg
     c                   eval      w_numm = b1numm
     c                   eval      w_suff = b1suff
     c                   call      'LO110R'
     c                   parm                    w_val2
6.nu c                   parm                    w_numm
     c                   parm                    w_suff
      *
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c     lohelr_key    chain     lohelr                             90
     c                   if        *in90  = *off
     c                   eval      b1salg = (lototi - lototr)
     c                   if        *in71
     c                   eval      w_salg = %editw(b1salg : '         ,  ')
     c                   move      w_salg        b1info
     c                   else
7.11 c**                 if        loornr <> 0
7.11 c**                 move      loornr        b1info
7.11 c**                 endif
     c                   endif
     c                   endif
      *
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere ordre
      *
     c                   if        b1valg = 3
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c                   exsr      xb3win
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette ordre
      *
     c                   if        b1valg = 4
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c                   exsr      xb4win
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise ordre
      *
     c                   if        b1valg = 5
     c                   eval      w_numm = b1numm
     c                   eval      w_suff = b1suff
      *
     c                   call      'LO505R'
     c                   parm                    w_firm
6.nu c                   parm                    w_numm
     c                   parm                    w_suff
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Velge ordre til utskrift
      *
     c                   if        b1valg = 6
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c                   exsr      xb6win
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   eval      b_forn = *on
     c***                update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise historikk på ordre
      *
     c                   if        b1valg = 10
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c     lohelr_key    chain     lohelr                             90
     c                   if        %found
6.23 c                   if        loornr <> 0
     c                   eval      w_numm = loornr
6.23 c                   eval      w_ntyp = '1'
6.23 c                   else
     c                   eval      w_numm = b1numm
6.23 c                   eval      w_ntyp = '2'
6.23 c                   endif
     c                   endif
     c                   exsr      historikk
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
7.13  *
7.13  * Splitte en IP i to
7.13  *   sven
7.13 c                   if        b1valg = 11
7.13  *
7.13 c                   eval      votyl1_otyp = b1otyp
7.13 c     votyl1_key    chain     votyl1r
7.13 c                   if        %found(votyl1)
7.13 c                             and (vaosys <> 1
7.13 c                               or vaoakk <> 2)
7.13 c                   eval      w_mld1 = 'Dette valget kan kun +
7.13 c                             gjøres på Inng.Pakkseddel'
7.13 c                   call      'AA005R'
7.13 c                   parm                    w_mld1
7.13 c                   eval      b1valg = 0
7.13 c                   exsr      xfarge
7.13 c                   update    b1sfl
7.13 c                   iter
7.13 c                   endif
7.13  *
7.13 c                   eval      lohelr_numm = b1numm
7.13 c                   eval      lohelr_suff = b1suff
7.13  *
7.13  *  Kaller opp subprogrammet for linjevalg
7.13  *
7.13 c                   call      'LO737R'
7.13 c                   parm                    w_retu
7.13 c                   parm                    w_firm
7.13 c                   parm                    lohelr_numm
7.13 c                   parm                    lohelr_suff
7.13 c                   eval      w_retu = *blank
7.13 c                   eval      b1valg = 0
7.13 c                   exsr      xfarge
7.13 c                   eval      b_forn = *on
7.13 c                   update    b1sfl
7.13 c                   iter
7.13 c                   endif
      *
      * Velge utplukk av ordre
      *
     c                   if        b1valg = 16
      *
      * Ordretype/beh.kode = 0
      * skal ikke kunne plukkes
      *
     c                   eval      votyl1_otyp = b1otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        vaoakk = 0
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
7.07  *
7.07  * Skal ikke kunne plukkes hvis elektronisk pakkseddel er mottat
7.07  * NB Kun hvis ordretypen er av type bestilling.
7.07  *
7.07 c                   if        vaoakk = 1
7.07 c                   eval      lbhoi4_hnum = b1numm
7.07 c                   eval      lbhoi4_hsuf = b1suff
7.07 c     lbhoi4_key    chain     lbhoi4
7.07 c                   if        %found
7.07 c                   eval      w_mld1 = *blanks
7.07 c                   eval      w_mld2 = *blanks
7.07 c                   eval      w_mld3 = *blanks
7.07 c                   eval      w_mld1 = 'Elektronisk pakkseddel er mottatt-
7.07 c                              på bestilling'
7.07 c                   eval      w_mld2 = 'Kan ikke varemottas manuelt'
7.07 c                   call      'AA031R'
7.07 c                   parm                    w_mld1
7.07 c                   parm                    w_mld2
7.07 c                   parm                    w_mld3
7.07 c                   eval      b1valg = 0
7.07 c                   update    b1sfl
7.07 c                   iter
7.07 c                   endif
7.07 c                   endif
      *
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c                   exsr      xb16wi
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   eval      b_forn = *on
8.05 c                   eval      b2fso2 = *blank
8.05 c                   eval      w_seqe = ' '
8.05 c                   eval      lohel1_numm = b1numm
8.05 c                   eval      lohel1_suff = b1suff
8.05 c     lohel1_key    setll     lohel1
     c***                update    b1sfl
     c                   iter
     c                   endif
      *
      * Velge overføring av bestilling til leverandør
      *
     c                   if        b1valg = 18
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c                   exsr      xb18wi
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   eval      b_forn = *on
     c***                update    b1sfl
     c                   iter
     c                   endif
      *
      * Velge oppdatering av tilhørende ordre
      *
     c                   if        b1valg = 26
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c                   exsr      xb26wi
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   eval      b_forn = *on
     c***                update    b1sfl
     c                   iter
     c                   endif
      *
      * Velge vise slettede bestillinger
      *
     c                   if        b1valg = 46
     c                   call      'LY500R'
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   eval      b_forn = *on
     c                   iter
     c                   endif
      *
      * Velge samkjøring av suffix
      *
     c                   if        b1valg = 36
     c                   call      'LO105R'
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   eval      b_forn = *on
     c***                update    b1sfl
     c                   iter
     c                   endif
      *
      * Velge overgang til ordresystemet
      *
     c                   if        b1valg = 50
     c                   if        p_lpro <> *blank
     c                   eval      p_lpro = *blank
     c                   eval      b_exit = *on
     c                   leavesr
     c                   else
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c     lohelr_key    chain     lohelr
     c                   eval      p_lpro = 'L'
     c                   call      'FO500R'
6.nu c                   parm                    loornr
     c                   parm                    p_lpro
     c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
     c                   eval      b_forn = *on
     c***                update    b1sfl
     c                   iter
     c                   endif
     c                   endif
5.51  *
5.51  * Velge matching mot ordre
5.51  *
5.51 c                   if        b1valg = 55
8.01  * Sjekk om tilhørende ordre er dellevert
8.01 c                   if        loornr <> 0
8.02 c                   eval      fohelr_numm = loornr
8.02 c                   eval      fohelr_suff = loorsu
8.02 c     fohelr_key    chain     fohelr
8.02 c                   if        not %found
8.02 c                   eval      w_mld1 = *blanks
8.02 c                   eval      w_mld2 = *blanks
8.02 c                   eval      w_mld3 = *blanks
8.02 c                   eval      w_mld1 = 'Ordren finnes ikke lenger'
8.02 c                   eval      w_mld2 = 'Ingen synkronisering kan foretas!'
8.02 c                   call      'AA031R'
8.02 c                   parm                    w_mld1
8.02 c                   parm                    w_mld2
8.02 c                   parm                    w_mld3
8.02 c                   goto      no_link
8.02 c                   endif
8.04  * Sjekker heller om det kun er en ordre med bestillingsnummer
8.02 c*                  eval      fohelr_numm = loornr
8.06 c*                  eval      fohelr_suff = 0
8.06 c*    fohelr_key    setll     fohelr
8.06 c*    fohelr_key2   reade     fohelr
8.06 c*                  dow       not %eof
8.06 c*                  if        fobenr = lonumm
8.06 c*                  eval      w_btel = w_btel + 1
8.06 c*                  endif
8.06 c*    fohelr_key2   reade     fohelr
8.06 c*                  enddo
8.06  *
8.06  * Ny logikk for delevering.
8.06  *
8.06 c                   exsr      sjk_dellev
8.06  *
8.04 c*                  if        fosuff > 0
8.06 c*                  if        w_btel <> 1
8.06 c                   if        w_btel = 1
8.01 c                   eval      w_mld1 = *blanks
8.01 c                   eval      w_mld2 = *blanks
8.01 c                   eval      w_mld3 = *blanks
8.01 c                   eval      w_mld1 = 'Ordren er dellevert'
8.01 c                   eval      w_mld2 = 'Ingen synkronisering kan foretas!'
8.01 c                   call      'AA031R'
8.01 c                   parm                    w_mld1
8.01 c                   parm                    w_mld2
8.01 c                   parm                    w_mld3
8.01 c                   goto      no_link
8.01 c                   endif
8.01 c                   endif
8.01  *
6.21 c                   eval      p_stat = '1'
6.21 c                   call      'FO711R'
6.21 c                   parm                    w_firm
6.nu c                   parm                    loornr
6.21 c                   parm                    loorsu
6.21 c                   parm                    p_stat
6.21  * Kall oppdatering av ordre hvis ikke sperret
6.21 c                   if        p_stat = '1'
5.51 c                   eval      w_numm = b1numm
5.51 c                   eval      w_suff = b1suff
5.51 c                   call      'LO580R'
5.51 c                   parm                    w_firm
6.nu c                   parm                    w_numm
5.51 c                   parm                    w_suff
6.21 c                   endif
6.21  *
8.01 c     no_link       tag
6.21  *
5.51 c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
5.51 c                   update    b1sfl
5.51 c                   iter
5.51 c                   endif
5.51  *
5.51  * Velge vise bestillings log
5.51  *
5.51 c                   if        b1valg = 60
5.51 c                   eval      w_numm = b1numm
5.51 c                   eval      w_suff = b1suff
5.51 c                   call      'LM500R'
5.51 c                   parm                    w_firm
6.nu c                   parm                    w_numm
5.51 c                   parm                    w_suff
5.51 c                   eval      b1valg = 0
7.05 c                   exsr      xfarge
5.51 c                   update    b1sfl
5.51 c                   iter
5.51 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - -
     c                   enddo
      * - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B9 Behandle bilde for sletting av behandlingskode og utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb9win        BEGSR
6.37  *
6.37  *  Henter inn opplysninger om hvem som holder bestilling
6.37  *
6.37 c                   call      'LO710R'
6.37 c                   parm                    w_firm
6.37 c                   parm                    b1numm
6.37 c                   parm                    b1suff
      *
      *  Henter inn opplysninger fra ORDRE-HODE-REGISTER
      *
     c     lohelr_key    chain     lohelrr                            90
     c                   eval      b9numm = lonumm
     c*                  eval      b9navn = lolena
     c                   eval      w_numm = lonumm
     c                   eval      w_suff = losuff
      *
      *  Send Alt
      *
     c     xb9tag        tag
     c                   write     b9win
     c                   exfmt     b9win
      *
      *  F12=Avslutt subrutinen
      *
     c                   if        *inkc
     c                   goto      xb9slt
     c                   endif
      *
      *  Har bruker lov til å slette binding til ordre ?
      *
     c                   if        lbko03 = 0
     c                   if        lokode = 1
     c                   eval      *in31  = *on
     c                   goto      xb9tag
     c                   endif
     c                   endif
      *
      *  Kaller opp subprogrammet for sletting av utplukk
      *  og blanking av behandlinskode i ORDRE-HODE-REGISTER
      *
     c                   call      'LO402R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    w_numm
     c                   parm                    w_suff
      *
      *  Beregner nye summer for ordre
      *
     c                   eval      w_enor = *blank
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
6.nu c                   parm                    w_numm
     c                   parm                    w_suff
      *
      *  Nullstiller kode i subfile
      *
     c                   eval      b1kode = *blank
      *
     c     xb9slt        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsjekk_inp    begsr
      *
     c                   eval      b_feil = *off
      *
      * Dato
      *
     c                   if        b2dato <> 0
     c     *dmy          test(d)                 b2dato                 34
     c                   if        *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk om ordren allerede er til behandling (subfile)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsjekk_ord1   begsr
      *
     c                   eval      w_sjek = *off
      *
      *  Sjekk, er denne ordren allerede til behandling
      *
     c                   if        b1kode <> *blank
     c                   if        b1valg <> 1
     c                   eval      w_sjek =  *on
     c                   endif
     c                   endif
      *
      *  Sjekk, er denne ordren allerede til behandling,
      *         Denne sjekken må utføres fordi subfilen
      *         ikke alltid er oppdatert
      *
     c                   if        w_sjek = *off
     c                   if        b1valg <> 1
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c     lohelr_key    chain     lohelrr                            90
     c                   if        *in90  =  *off
     c                   if        lokode <> 0
     c                   eval      w_sjek =  *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *  Sjekker om bruker har lov til å endre dokument
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     xsjekk_paks   begsr
6.34  *
6.34 c                   eval      b_stop = *off
6.34  *
6.34  *  Henter inn opplysninger fra ORDRE-HODE-REGISTER
6.34  *
6.34 c     lohelr_key    chain     lohelrr                            90
6.34  *
6.34  *  Henter inn opplysninger fra ORDRE-TYPE-REGISTER
6.34  *
6.34 c                   eval      votyl1_otyp = lootyp
6.34 c     votyl1_key    chain     votyl1r                            90
6.34  *
6.34  *  Sjekk,  Har bruker lov til å endre ?  (gjelder pakkseddel)
6.34  *
6.34 c                   if        vaoakk = 2
6.34 c                   if        lbko09 = 0
6.34 c                   eval      b_stop = *on
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  *  Sjekk,  Har bruker lov til å endre ?  (gjelder faktura)
6.34  *
6.34 c                   if        vaoakk = 3
7.03 c                   if        u_703 = *on
7.03 c                   if        lbko11 = 0
7.03 c                   eval      b_stop = *on
7.03 c                   endif
7.03 c                   else
6.34 c                   if        lbko10 = 0
6.34 c                   eval      b_stop = *on
6.34 c                   endif
7.03 c                   endif
6.34 c                   endif
3.14  *
3.14 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B3 Behandle bilde for kopiere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb3win        begsr
      *
      *  Henter inn opplysninger fra ORDRE-HODE-REGISTER
      *
     c     lohelr_key    chain     lohelrr                            90
     c                   eval      b3numm = lonumm
     c                   eval      b3navn = lonavn
6.31  *
6.31  * Ta vare på ordretype fra 'Fra-ordren'
6.31  *
6.31 c                   eval      s_otyp = lootyp
      *
      *  Nullstiller/Blanker felter som blir benyttet i bildet
      *
     c                   eval      b3nonr = 0
     c                   eval      b3otyp = *blank
     c                   eval      b3ldor = loldor
     c                   eval      b3krab = 0
     c                   eval      b3kcop = 0
6.36 c                   eval      b3beor = 0
7.06 c                   if        u_706 = *on
7.06 c                   eval      b3beor = 1
7.06 c                   endif
      *
      *  Send Alt
      *
     c                   write     b3win
      *
     c     b3tagb        tag
      *
      *  Bare data
      *
     c                   exfmt     b3win
      *
      *  F3=Avslutt subrutinen
      *
     c                   if        *inkc
     c                   goto      b3tag1
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd
     c                   if        wactre = 'B3WIN'
      *
      *  Spørring i leverandør-register
      *
     c                   if        wactfe = 'B3LDOR'
     C                   call      'RL500R'
     C                   parm                    w_firm
     C                   parm                    b3ldor
     C                   parm                    w_navn
     c                   goto      b3tagb
     c                   endif
      *
      *  Spørring i ordretype-register
      *
     c                   if        wactfe = 'B3OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b3otyp
     c                   goto      b3tagb
     c                   endif
      *
     c                   endif
     c                   goto      b3tagb
     c                   endif
      *
      *  Slå opp
      *  i filen
      *
     c                   eval      lohelr_num2 = b3nonr
     c                   eval      lohelr_suf2 = 0
     c     lohelr_ke2    chain     lohelrr                            90
      *
      *  Post finnes
      *  fra før
      *
     c                   if        *in90 = *off
     c                   eval      *in31 = *on
     c                   goto      b3tagb
     c                   endif
      *
      *  Sjekk,  Finnes ordretype i kode-register
      *
     c                   eval      votyl1_otyp = b3otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in32 = *on
     c                   goto      b3tagb
     c                   endif
      *
     c                   if        vaosys <> 1
     c                   eval      *in32 = *on
     c                   goto      b3tagb
     c                   endif
6.36  *
6.36  * hvis beholde ordretype, så må fra best.slettes
6.36  *
7.06 c                   if        u_706 = *off
6.36 c                   if        b3beor = 1 and
6.36 c                             b3kcop = 0
6.36 c                   eval      *in34 = *on
6.36 c                   goto      b3tagb
6.36 c                   endif
7.06 c                   endif
      *
      *  Sjekk,  Finnes vare-leverandør-nummer i leverandør-register
      *
     c                   eval      rlevl1_levr = b3ldor
     c     rlevl1_key    chain     rlevl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in33 = *on
     c                   goto      b3tagb
     c                   endif
      *
      *  Henter neste ledige ordrenummer, dersom ordrenummer til
      *                                   ikke er fylt ut
      *
     c     b3tagx        tag
     c                   if        b3nonr = 0
      *
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
     c                   eval      lohelr_num2 = w_numm
     c                   endif
      *
     c     lohelr_ke2    chain     lohelrr                            90
     c                   if        *in90  = *off
     c                   goto      b3tagx
     c                   endif
      *
      *  Kaller opp subprogram for kopiering av ordre
      *
     c                   call      'LO300R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    lohelr_numm
     c                   parm                    lohelr_suff
6.nu c                   parm                    lohelr_num2
     c                   parm                    b3otyp
     c                   parm                    b3ldor
     c                   parm                    b3krab
     c                   parm                    b3ktxt
6.36 c                   parm                    b3beor
      *
      *  Kaller opp subprogrammet for sletting av ordre
      *  dersom ordren skal slettes etter kopiering
      *
     c                   eval      b4kom1 = *blank
     c                   eval      b4kom2 = *blank
     c                   if        b3kcop = 1
7.08 c                   eval      b4numm = b1numm
7.08 c                   eval      b4navn = b1navn
     c                   write     b4win
     c     b3tagc        tag
     c                   exfmt     b4win
      *
      *  F3 Anuller sletting
      *
     c                   if        *inkc  = *on
     c                   goto      b3tag1
     c                   endif
6.34  *
6.34  *  Sjekk,  Har bruker lov til å slette ? (gjelder pakkseddel)
6.34  *
6.34 c                   eval      votyl1_otyp = s_otyp
6.34 c     votyl1_key    chain     votyl1r                            90
6.34 c                   if        vaoakk = 2
6.34 c                   if        lbkO04 = 0
6.34 c                   eval      *in34  = *on
6.34 c                   goto      b3tagc
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  *  Sjekk,  Har bruker lov til å slette ? (gjelder faktura)
6.34  *
6.34 c                   if        vaoakk = 3
7.03 c                   if        u_703 = *on
7.03 c                   if        lbkO11 = 0
7.03 c                   eval      *in32  = *on
7.03 c                   goto      b3tagc
7.03 c                   endif
7.03 c                   else
6.34 c                   if        lbkO10 = 0
6.34 c                   eval      *in32  = *on
6.34 c                   goto      b3tagc
6.34 c                   endif
7.03 c                   endif
6.34 c                   endif
      *
      *  Slettegrunn kan ikke være blank
      *
     c                   if        b4kom1 = *blank
     c                   eval      *in31  = *on
     c                   goto      b3tagc
     c                   endif
      *
      *  Kaller opp subprogrammet for sletting av ordre
      *
6.10 c                   eval      w_retu = *blank
     c                   eval      w_typd = '*KOP'
     c                   eval      w_kom1 = b4kom1
     c                   eval      w_kom2 = b4kom2
     c                   call      'LO400R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    lohelr_numm
     c                   parm                    lohelr_suff
     c                   parm                    w_typd
     c                   parm                    w_kom1
     c                   parm                    w_kom2
      *
      *  Oppdater subfile
      *
     c                   eval      b1lena = *blank
     c                   eval      b1lena = 'Slettet'
     c                   eval      b1navn = *blank
     c                   eval      b1info = *blank
      *
     c                   endif
      *
     c     b3tag1        tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B4 Behandle bilde for sletting av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb4win        begsr
      *
      *  Henter inn opplysninger fra ORDRE-HODE-REGISTER
      *
     c     lohelr_key    chain     lohelrr                            90
     c                   eval      b4numm = lonumm
     c                   eval      b4navn = lolena
      *
      *  Henter inn opplysninger fra ORDRE-TYPE-REGISTER
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1r                            90
      *
      *  Send Alt
      *
     c                   eval      b4kom1 = *blank
     c                   eval      b4kom2 = *blank
     c                   write     b4win
     c     b4tagc        tag
     c                   exfmt     b4win
      *
      *  F12=Avslutt subrutinen
      *
     c                   if        *inkc
     c                   goto      b4tag1
     c                   endif
      *
      *  Sjekk,  Har bruker lov til å slette ? (gjelder pakkseddel)
      *
     c                   if        vaoakk = 2
     c                   if        lbkO04 = 0
     c                   eval      *in32  = *on
     c                   goto      b4tagc
     c                   endif
     c                   endif
6.34  *
6.34  *  Sjekk,  Har bruker lov til å slette ? (gjelder faktura)
6.34  *
6.34 c                   if        vaoakk = 3
7.03 c                   if        u_703 = *on
7.03 c                   if        lbkO11 = 0
7.03 c                   eval      *in32  = *on
7.03 c                   goto      b4tagc
7.03 c                   endif
7.03 c                   else
6.34 c                   if        lbkO10 = 0
6.34 c                   eval      *in32  = *on
6.34 c                   goto      b4tagc
6.34 c                   endif
7.03 c                   endif
6.34 c                   endif
      *
      *  Slettegrunn kan ikke være blank
      *
     c                   if        b4kom1 = *blank
     c                   eval      *in31  =  *on
     c                   goto      b4tagc
     c                   endif
      *
      *
      *  Kaller opp subprogrammet for sletting av ordre
      *
     c                   eval      w_typd = '*STD'
     c                   eval      w_kom1 = b4kom1
     c                   eval      w_kom2 = b4kom2
     c                   call      'LO400R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    lohelr_numm
     c                   parm                    lohelr_suff
     c                   parm                    w_typd
     c                   parm                    w_kom1
     c                   parm                    w_kom2
      *
      *  Oppdater subfile
      *
     c                   eval      b1lena = *blank
     c                   eval      b1lena = '*Slettet'
     c                   eval      b1navn = *blank
     c                   eval      b1info = *blank
      *
     c     b4tag1        tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B6 Behandle bilde for utskrift av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb6win        begsr
      *
     c                   eval      w_val1 = 0
      *
      *  Kaller opp subprogrammet for utskriftsvalg
      *
     c                   call      'LO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    lohelr_numm
     c                   parm                    lohelr_suff
     c                   parm                    w_val1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B16 Behandle bilde for utplukk av en ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb16wi        begsr
      *
      *  Kaller opp subprogrammet for utplukksvalg
      *
     c                   call      'LO736R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    lohelr_numm
     c                   parm                    lohelr_suff
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B18 Behandle bilde for overføring til leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb18wi        begsr
      *
6.31  * hvis obl.levmåte på ordre, så sjekk om det er satt.
6.31  *
6.31 c                   if        u_lmat = *on
6.31 c     lohelr_key    chain     lohelr
6.31 c                   if        %found(lohelr) and
6.32 c**                           folmat = 0
6.32 c                             lolmat = 0
6.31 c                   exfmt     b18msg
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   endif
7.10  *
7.10  * Hvis leverandørdistribusjon ikke fines, skriv ut isteden.
7.10  *
7.10 c                   eval      lldilr_ldor = loldor
7.10 c                   move      lolage        lldilr_lage
7.10 c     lldilr_key    chain     lldilr
7.10 c                   if        not %found
7.10 c                   eval      lldilr_lage = *blank
7.10 c     lldilr_key    chain     lldilr
7.10  *
7.10 c                   if        not %found
7.10 c                   exsr      xb6win
7.10 c                   goto      end_send
7.10 c                   endif
7.10 c                   endif
      *
      *  Kaller opp subprogrammet for overføringskontroll
      *
6.20 c                   eval      w_retk = *blank
     c                   call      'LO740R'
6.nu c                   parm                    lohelr_numm
     c                   parm                    lohelr_suff
6.20 c                   parm                    w_retk
      *
7.10 c     end_send      endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B26 Behandle bilde for oppdatering av tilhørende ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb26wi        begsr
      *
      *  Sjekker om ordre er tilknyttet og finnes
      *
     c     lohelr_key    chain     lohelr
     c                   if        not %found or
     c                             loornr = 0
     c                   goto      xb26wi_ut
     c                   endif
      *
     c                   eval      fohelr_numm = loornr
     c                   eval      fohelr_suff = loorsu
     c     fohelr_key    chain     fohelr
     c                   if        not %found
     c                   goto      xb26wi_ut
     c                   endif
      *
     c                   eval      w_okod = 2
     c                   call      'FO100R'
     c                   parm                    w_okod
6.nu c                   parm                    loornr
     c                   parm                    loorsu
      *
     c     xb26wi_ut     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B28 Behandle bilde for tilbakestilling av plukket ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb28wi        begsr
      *
      *  Kaller opp subprogrammet for tilbakestilling
      *
     c                   call      'LOTBBR'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    lohelr_numm
     c                   parm                    lohelr_suff
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = laabrk
     c                   eval      w_type = b3otyp
     c                   eval      w_fast = l_wsid
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      h1taga
     c                   endsl
6.30  *
6.30  * Sjekk om gyldig kode
6.30  *
6.30 c                   if        h1dlay <> *blank and
6.30 c                             h1dlay <> '0'    and
6.30 c                             h1dlay <> '1'
6.30 c                   eval      *in31 = *on
6.30 c                   goto      h1taga
6.30 c                   endif
      *
      * Legg inn 'O' dersom over/under
      * er blank og beløp er oppgitt
      *
     c                   if        h1belp <> 0
     c                   if        h1valg =  *blank
     c                   eval      h1valg =  'O'
     c                   endif
     c                   endif
      *
     c     end_h1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14  = *on
     c                   write     b2ctl
     c                   eval      *in14  = *off
      *
     c                   eval      *in15  = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
7.02  * Initierer SQL for søk
7.02  *
7.02 c                   if        b_open_sok = *on
7.02  *
7.02 c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c/exec sql
7.02 c+ close STMT
7.02 c/end-exec
7.02 c
7.02 c                   eval      sqlwhere = ''
7.02 c                   if        b2fsok <> *blanks
7.02 c     lo:up         xlate     b2fsok        b2fsok
7.02 c                   call      'AS700R'
7.02 c                   parm                    b2fsok
7.02 c                   parm                    w_ste1
7.02 c                   parm                    w_ste2
7.02 c                   parm                    w_ste3
7.02 c                   parm                    w_ste4
7.02 c                   parm                    w_ste5
7.02 c
7.05 c                   eval      w_ste1 = %ScanRpl('%' : ' ' : w_ste1)
7.02 c     1             do        5             x
7.02 c                   select
7.02 c                   when      x=1
7.02 c                   if        w_ste1 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste1)+'%'''
7.02 c                   when      x=2
7.02 c                   if        %trim(w_ste2) = ''
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste2)+'%'''
7.02 c                   when      x=3
7.02 c                   if        w_ste3 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste3)+'%'''
7.02 c                   when      x=4
7.02 c                   if        w_ste4 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste4)+'%'''
7.02 c                   when      x=5
7.02 c                   if        w_ste5 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste5)+'%'''
7.02 c                   endsl
7.02 c
7.02 c     up:lo         xlate     sql_fri_s     sql_fri_sl
7.02 c                   eval      sql_fri_sl = %ScanRpl('%' : '' : sql_fri_sl)
7.02 c
7.02 c                   eval      sqlwhere  = %trim(sqlwhere) + ' +
7.02 c                             and ( +
7.02 c                                a.lolena like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.lonavn like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or b.rlnavn like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or b.rllevr like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.lonumm like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.loadr1 like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.loadr2 like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.losted like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.loornr like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or UPPER(a.lodref) like '+
7.02 c                                %trim(sql_fri_s)+ ' +
7.02 c                             or UPPER(a.lorekv) like '+
7.02 c                                %trim(sql_fri_s)+ ' +
7.02 c                              or lower(DAYNAME(a.lobdat)) +
7.02 c                                  = '+sql_fri_sl+ ' +
7.02 c                             or ((select count(*) from lodtpf +
7.02 c                             where ldfirm=a.lofirm and ldnumm= +
7.02 c                              a.lonumm and ldsuff = a.losuff and +
7.02 c                             ldtek1 like '+%trim(sql_fri_s)+ ' ) <> 0)   +
7.02 c                             or ((select count(*) from lodtpf +
7.02 c                             where ldfirm=a.lofirm and ldnumm= +
7.02 c                             a.lonumm and ldsuff = a.losuff and +
7.02 c                             ldvare LIKE '+%trim(sql_fri_s)+ ' ) <> 0)   +
7.02 c                               )'
7.02 c                   enddo
7.02 c                   endif
7.02 c
7.02 c                   eval      sqlquery  = 'select +
7.02 c                               a.lofirm +
7.02 c                              ,a.lonumm +
7.02 c                              ,a.losuff +
7.02 c                              ,a.loavde +
7.02 c                              ,a.lobdat +
7.02 c                              ,a.loinnk +
7.02 c                              ,a.lokode +
7.02 c                              ,a.loldat +
7.02 c                              ,a.loldor +
7.02 c                              ,a.lolena +
7.02 c                              ,a.lomoda +
7.02 c                              ,a.lonavn +
7.02 c                              ,a.loornr +
7.02 c                              ,a.lootyp +
7.02 c                              ,a.losorn +
7.02 c                              ,a.lototr +
7.02 c                              ,a.lototi +
7.11 c                              ,a.lolmat +
7.11 c                              ,a.lobsda +
7.02 c                              ,a.louser +
7.02 c                              ,a.lovida +
7.02 c                              ,a.loedat +
7.02 c                              ,a.loetim +
7.02 c                              ,a.loeusr +
7.02 c                               from   lohepf a +
7.02 c                               left outer join rlevpf b on b.rlfirm = +
7.02 c                                 a.lofirm and b.rllevr = a.loldor +
7.02 c                               '
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               where  a.lofirm = ? '
7.02 c                   eval      sqlquery  = %trim(sqlquery) + %trim(sqlwhere)
7.02 c
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               order by +
7.02 c                               a.lodate desc +
7.02 c                               , a.lotime desc +
7.02 c                               , a.lonumm +
7.02 c                               , a.losuff +
7.02 c                               '
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               for read only +
7.02 c                               '
7.02 c
7.02 c/exec sql
7.02 c+ PREPARE SQL_STMT FROM :sqlquery
7.02 c/end-exec
7.02 c/exec sql
7.02 c+ DECLARE STMT CURSOR FOR SQL_STMT
7.02 c/end-exec
7.02 c
7.02 c                   if        sqlcod = 100 or sqlstt = '02000'
7.02 c                   eval      *in15 = *on
7.02 c                   leavesr
7.02 c                   endif
7.02 c
7.02 c/exec sql
7.02 c+ OPEN STMT using :w_firm
7.02 c/end-exec
7.02 c
7.02 c                   endsl
7.02 c                   endif                                                  b_open_sok = *on
      *
     c                   dou       w_srrn = w_spge
      *
      * Velg logisk fil
      *
     c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c/exec sql
7.02 c+ fetch STMT
7.02 c+ into   :lofirm,:lonumm,:losuff,:loavde,:lobdat,:loinnk
7.02 c+       ,:lokode,:loldat,:loldor,:lolena,:lomoda,:lonavn
7.11 c+       ,:loornr,:lootyp,:losorn,:lototr,:lototi,:lolmat
7.11 c+       ,:lobsda,:louser,:lovida,:loedat,:loetim,:loeusr
7.02 c/end-exec
7.02 c                   if        sqlcod = 100 or sqlstt = '02000'
7.02 c                   eval      *in15 = *on
7.02 c                   endif
     c                   when      w_seqe = 'A'
     c                   read      lohela                                 15
     c                   when      w_seqe = 'D'
     c                   read      loheld                                 15
     c                   when      w_seqe = 'L'
     c                   read      lohell                                 15
     c                   when      w_seqe = 'O'
     c                   read      lohelo                                 15
     c                   when      w_seqe = 'V'
     c                   read      lohelv                                 15
     c                   other
     c                   read      lohel1                                 15
     c                   endsl
      *
      * End of file ?
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
      * Kommet til neste firma ?
      *
     c                   if        lofirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
      * Uaktuell ordretype ?
      *
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> lootyp
     c                   iter
     c                   endif
      *
      * Uaktuell bruker ?
      *
     c                   if        h1user <> *blank and
     c                             h1user <> louser
     c                   iter
     c                   endif
      *
      * Uaktuell leverandør ?
      *
     c                   if        h1ldor <> 0 and
     c                             h1ldor <> loldor
     c                   iter
     c                   endif
      *
      * Uaktuelt ordrenummer ?
      *
     c                   if        h1ornr <> 0 and
     c                             h1ornr <> loornr
     c                   iter
     c                   endif
5.60  *
5.60  * Uaktuell avdeling ?
5.60  *
5.60 c                   if        h1avde <> 0 and
5.60 c                             h1avde <> loavde
5.60 c                   iter
5.60 c                   endif
      *
      * Oppgitt beløpsgrense ?
      * Over   = O
      * eller <> U
      *
     c                   if        h1belp <> 0
     c                   eval      w_belp =  (lototi - lototr)
     c                   if        h1valg <> 'U'
     c                   if        w_belp <  h1belp
     c                   iter
     c                   endif
     c                   endif
     c                   endif
      *
      * Oppgitt beløpsgrense ?
      * Under =  U
      *
     c                   if        h1belp <> 0
     c                   eval      w_belp =  (lototi - lototr)
     c                   if        h1valg =  'U'
     c                   if        w_belp >  h1belp
     c                   iter
     c                   endif
     c                   endif
     c                   endif
      *
      * Velge ut på ordreanmerkning ?
      *
     c                   if        h1oanm = '**' and
     c                             lokode <> 1
     c                   iter
     c                   endif
      *
     c                   if        h1oanm = 'UT' and
     c                             lokode <> 2
     c                   iter
     c                   endif
      *
     c                   if        h1oanm = 'OK' and
     c                             lokode <> 3
     c                   iter
     c                   endif
      *
     c                   if        h1oanm = 'VE' and
     c                             lokode <> 5
     c                   iter
     c                   endif
      *
     c                   if        h1oanm = 'BU' and
     c                             lokode <> 7
     c                   iter
     c                   endif
      *
T5.50 * Bestillingsdato etter selekteringsdato og
T5.50 * leverandørens ordrenummer ikk er utfylt
T5.50 *
T5.50c                   if        h1datr <> 0
T5.50c     *dmy          move      h1datr        w_datr
T5.50 *
T5.50 *  Henter inn opplysninger fra ORDRE-TYPE-REGISTER
T5.50 *
T5.50c                   eval      votyl1_otyp = lootyp
T5.50c     votyl1_key    chain     votyl1r                            90
T5.50 *
T5.50c                   if        w_datr < lobdat or
T5.50c                             losorn <> ' '   or
T5.50c                             vaoakk <> 1
T5.50c                   iter
T5.50c                   endif
T5.50c                   endif
T5.50 *
T5.50 * Sjekker selgernr.
T5.50 *
T5.50c                   if        h1selg <> 0
E5.60c                   eval      lohelr_numm = lonumm
E5.60c                   eval      lohelr_suff = losuff
E5.50c     lohelr_key    chain     lohelr
E5.50c                   if        %found(lohelr)
E5.50c                   if        h1selg <> loinnk
T5.50c                   iter
T5.50c                   endif
T5.50c                   endif
T5.50c                   endif
6.30  *
6.30  * Sil ut bestillinger med forsinket leveranse
6.30  *
6.30 c                   if        h1dlay =  '1'
6.30 c                   if        lomoda =  *loval or
6.30 c                             lovida <> *loval or
6.30 c                             lomoda >  w_udat
6.30 c                   iter
6.30 c                   endif
6.30 c                   endif
7.05 c                   if        u_705 = *on
7.05 c                   eval      b1lety = 0
7.05 c                   eval      b1kres = 0
7.05 c                   if        loornr <> 0
7.05 c                   eval      fohelr_numm = loornr
7.05 c                   eval      fohelr_suff = loorsu
7.05 c     fohelr_key    chain     fohelr                             90
7.05 c                   if        %found(fohelr)
7.05 c                   eval      b1lety = folety
7.05 c                   eval      rkunl1_kund = fokund
7.05 c     rkunl1_key    chain     rkunl1
7.05 c                   eval      b1kres = rkkres
7.05 c                   endif
7.05 c                   endif
7.05 c                   exsr      xfarge
7.05 c                   endif
      *
      * Fyll opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1numm = lonumm
     c                   eval      b1suff = losuff
     c                   eval      b1otyp = lootyp
     c                   eval      b1navn = lonavn
     c                   eval      b1info = *blank
7.11 c                   eval      b1ornr = loornr
7.11 c                   eval      b1lmat = lolmat
7.09  *
7.09  * Kalkulerer vekt og volum på bestillingen
7.09  *
7.09 c                   eval      b1vekt = 0
7.09 c                   eval      b1volu = 0
7.09  *
7.09 c                   call      'LO786R'
7.09 c                   parm                    w_firm
7.09 c                   parm                    b1numm
7.09 c                   parm                    b1suff
7.09 c                   parm                    w_vekt
7.09 c                   parm                    w_volu
7.09 c                   parm                    w_avvi
7.09  *
7.09 c                   eval      b1vekt = w_vekt
7.09 c                   eval      b1volu = w_volu
      *
      * Henter logg status
      *
     c                   eval      w_logk = *blank
6.24 c                   call      'LM701R'
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   parm                    w_logk
     c                   eval      b1ityp = w_logk
      *
7.11 c                   if        *in71  =  *on
     c*7.11              eval      b1dato =  0
6.33 c*7.11              if        lovida <> *loval
6.33 c*7.11*dmy          move      lovida        b1dato
6.33 c*7.11              endif
6.33 c*7.11              if        lomoda <> *loval and
6.33 c*7.11                        b1dato = 0
6.33 c*7.11*dmy          move      lomoda        b1dato
6.33 c*7.11              endif
7.11 c     *dmy          move      lobdat        b1dato
7.11  *
7.11 c                   if        lomoda <> *loval
7.11 c     *dmy          move      lomoda        b1ldat
7.11 c                   else
7.11 c                   eval       b1ldat = 0
7.11 c                   endif
7.11 c                   else
7.11 c                   if        lobsda <> *loval
7.11 c     *dmy          move      lobsda        b1dato
7.11 c                   else
7.11 c                   eval       b1dato = 0
7.11 c                   endif
6.33 c                   if        loldat <> *loval and
7.11 c                             b1ldat = 0
7.11 c     *dmy          move      loldat        b1ldat
6.33 c                   endif
     c                   endif
      *
     c                   eval      b1kode = *blank
     c                   if        lokode <> 0
     c                   eval      b1kode = '**'
     c                   endif
     c                   if        lokode = 2
     c                   eval      b1kode = 'UT'
     c                   endif
     c                   if        lokode = 3
     c                   eval      b1kode = 'OK'
     c                   endif
     c                   if        lokode = 5
     c                   eval      b1kode = 'VE'
     c                   endif
     c                   if        lokode = 7
     c                   eval      b1kode = 'BU'
     c                   endif
      *
     c                   eval      b1lena      = *blank
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1r                            90
     c                   eval      b1lena = lolena
     c                   eval      b1ldor = loldor
      *
     c                   eval      b1salg = (lototi - lototr)
     c                   if        w_moms = '1'
     c                   eval      b1salg = (b1salg * w_mvat)
     c                   endif
7.11 c**                 if        *in71
     c                   eval      w_salg = %editw(b1salg : '         ,  ')
     c                   move      w_salg        b1info
7.11 c**                 else
7.11 c**                 if        loornr <> 0
7.11 c**                 move      loornr        b1info
7.11 c**                 endif
7.11 c**                 endif
7.fb  * sjekker depositum
7.fb c                   if        loornr <> 0
7.fb c                   exsr      sjekk_depo
7.fb c                   if        b_depo = *on
7.fb c                   eval      *in45  = *on
7.fb c                   endif
7.fb c                   endif
      *
7.01 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
7.02 c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12  = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13  = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12  = *off
     c                   eval      *in13  = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for lesing av historikk på ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     historikk     begsr
      *
     c                   call      'FO550R'
6.nu c                   parm                    w_numm
6.23 c                   parm                    w_ntyp
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        wactfe = 'H1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    h1ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
5.60  *
5.60  * Avdeling
5.60  *
5.60 c                   if        wactfe = 'H1AVDE'
5.60 c                   call      'RA507R'
5.60 c                   parm                    w_firm
5.60 c                   parm                    h1avde
5.60 c                   parm                    w_gtxt
5.60 c                   leavesr
5.60 c                   endif
      *
      * Ordre-type
      *
     c                   if        wactfe = 'H1OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    h1otyp
     c                   goto      end_spØrr
     c                   endif
      *
      * Ordre-info-type
      *
     c                   if        wactfe = 'H1ITYP'
     c                   call      'VA540R'
     c                   parm                    w_firm
     c                   parm                    w_ityp
     c                   parm                    w_itxt
     c                   eval      h1ityp = w_ityp
     c                   goto      end_spØrr
     c                   endif
5.60  *
5.60  * Selger
5.60  *
5.60 c                   if        wactfe = 'H1SELG'
5.60 c                   call      'RA509R'
5.60 c                   parm                    w_firm
5.60 c                   parm                    h1selg
5.60 c                   parm                    w_gtxt
     c                   goto      end_spØrr
5.60 c                   endif
      *
      *
     c     end_spØrr     endsr
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Setter opp farge på best.nr.
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05 c     xfarge        begsr
7.05  *
7.05 c                   if        u_705 = *off
7.05 c                   leavesr
7.05 c                   endif
7.05 c                   eval      *in45  = *off
7.05 c                   eval      *in46  = *off
7.05 c                   if        b1lety = 1
7.05 c                   eval      *in45  = *on
7.05 c                   endif
7.05 c                   if        b1kres = 2
7.05 c                   eval      *in46  = *on
7.05 c                   endif
7.05 c                   endsr
      *
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Subrutine for å sjekke om linje har ubetalt depositumskrav
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  *
7.fb c     sjekk_depo    begsr
7.fb c
7.fb c                   eval      fohelr_numm = loornr
7.fb c                   eval      fohelr_suff = loorsu
7.fb c     fohelr_key    chain     fohelr                             90
7.fb c                   if        %found(fohelr)
7.fb c                   eval      b_depo = *off
7.fb c                   eval      *in45  = *off
7.fb c                   eval      sdepl1_numm = fonumm
7.fb c                   eval      sdepl1_kund = fokund
7.fb c                   eval      sdepl1_tell = 0
7.fb c     sdepl1_key    chain     sdepl1r
7.fb c                   if        %found(sdepl1) and
7.fb c                             skdepo > skdepb
7.fb c                   eval      b_depo = *on
7.fb c                   endif
7.fb c                   endif
7.fb  *
7.fb c                   endsr
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  * Sjekk dellvering
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  *
8.06 c     sjk_dellev    begsr
8.06  *
8.06 c                   eval      w_btel = 0
8.06 c                   eval      w_test = 0
8.06 c                   eval      w = 0
8.06  *
8.06  * Les alle linjer på bestillingen. Må være likt suffiks på alle linjene.
8.06  * NB Tar også høyde for at vi innfører oppdateringer av suffikser mellom
8.06  *    bestilling/ordre.
8.06  *
8.06 c                   eval      lodtlr_numm = b1numm
8.06 c                   eval      lodtlr_suff = b1suff
8.06 c                   eval      lodtlr_line = 0
8.06 c     lodtlr_key    setll     lodtlr
8.06 c     lodtlr_key2   reade     lodtlr
8.06 c                   dow       not %eof
8.06 c                   if        w = 0
8.06 c                   eval      w_test = ldorsu
8.06 c                   eval      w = 1
8.06 c                   endif
8.06  *
8.06 c                   if        w_test <> ldorsu
8.06 c                   eval      w_btel = 1
8.06 c                   leave
8.06 c                   endif
8.06  *
8.06 c     lodtlr_key2   reade     lodtlr
8.06 c                   enddo
8.06  *
8.06  * Finn reelt suffiks på ordren, og da må dette være likt på alle linjer.
8.06  *
8.06 c                   if        w_btel = 0
8.06 c                   eval      w_test = 0
8.06 c                   eval      w = 0
8.06 c                   eval      lodtlr_line = 0
8.06 c     lodtlr_key    setll     lodtlr
8.06 c     lodtlr_key2   reade     lodtlr
8.06 c                   dow       not %eof
8.06 c                   eval      fodtlv_vare = ldvare
8.06 c                   eval      fodtlv_numm = ldornu
8.06 c                   eval      fodtlv_suff = 0
8.06 c                   eval      fodtlv_enh1 = *blanks
8.06 c     fodtlv_key    setll     fodtlv
8.06 c     fodtlv_key2   reade     fodtlv
8.06 c                   dow       not %eof
8.06 c                   if        fdblin = ldline
8.06  * Første gang gjemmer vi unna suffikset på ordren
8.06 c                   if        w = 0
8.06 c                   eval      w_test = fdsuff
8.06 c                   eval      w = 1
8.06 c                   endif
8.06  *
8.07 c                   if        ldornu <> 0
8.06 c                   if        w_test <> fdsuff
8.06 c                   eval      w_btel = 1
8.06 c                   leave
8.06 c                   endif
8.07 c                   endif
8.06  *
8.06 c                   endif
8.06 c     fodtlv_key2   reade     fodtlv
8.06 c                   enddo
8.06  *
8.06 c     lodtlr_key2   reade     lodtlr
8.06 c                   enddo
8.06 c                   endif
8.06  *
8.06 c                   endsr
8.06  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
6.nu c                   parm                    p_numm
     c                   parm                    p_lpro
      *
      * Key for oppslag på bestilling/suffix
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
     c     lohelr_ke2    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_num2
     c                   kfld                    lohelr_suf2
      *
      * Key for posisjonering på bestilling/suffix
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for posisjonering på leverandørnavn
      *
     c     lohela_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohela_lena
      *
      * Key for posisjonering på bestillings-dato
      *
     c     loheld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    loheld_bdat
      *
      * Key for posisjonering på leverings-dato
      *
     c     lohell_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohell_ldat
      *
      * Key for posisjonering på ordretype
      *
     c     lohelo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelo_otyp
      *
      * Key for posisjonering på vareadresse
      *
     c     lohelv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelv_navn
      *
      * Key for oppdatering av bestillingshode
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for leverandørgister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for lager kode-register
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for brukerregister
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      * Key for ordretyperegister
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre/suffix
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
8.01  *
8.01  * Key for oppslag på ordre/suffix
8.01  *
8.01 c     fohelr_key2   klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fohelr_numm
6.31  *
6.31  * Key for file : Firmastyrt validering
6.31  *
6.31 c     avall1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    avall1_apgm
7.07  *
7.07  * Key for oppslag på elektroniske pakksedler
7.07  *
7.07 c     lbhoi4_key    klist
7.07 c                   kfld                    w_firm
7.07 c                   kfld                    lbhoi4_hnum
7.07 c                   kfld                    lbhoi4_hsuf
7.10  *
7.10  * Key for oppslag på leverandør distribusjon
7.10  *
7.10 c     lldilr_key    klist
7.10 c                   kfld                    w_firm
7.10 c                   kfld                    lldilr_ldor
7.10 c                   kfld                    lldilr_lage
7.fb  *
7.fb  * Key for oppslag på depositum-fil
7.fb  *
7.fb c     sdepl1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    sdepl1_numm
7.fb c                   kfld                    sdepl1_kund
7.fb c                   kfld                    sdepl1_tell
8.06  *
8.06  * Key for oppdatering av bestillingshode
8.06  *
8.06 c     lodtlr_key    klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    lodtlr_numm
8.06 c                   kfld                    lodtlr_suff
8.06 c                   kfld                    lodtlr_line
8.06  *
8.06  * Key for oppdatering av bestillingshode
8.06  *
8.06 c     lodtlr_key2   klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    lodtlr_numm
8.06 c                   kfld                    lodtlr_suff
8.06  *
8.06  * Key for oppdatering av ordrelinjer
8.06  *
8.06 c     fodtlv_key    klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    fodtlv_vare
8.06 c                   kfld                    fodtlv_numm
8.06 c                   kfld                    fodtlv_suff
8.06 c                   kfld                    fodtlv_enh1
8.06  *
8.06  * Key for oppdatering av ordrelinjer
8.06  *
8.06 c     fodtlv_key2   klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    fodtlv_vare
8.06 c                   kfld                    fodtlv_numm
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
7.03  *
7.03  * Endring 7.03
7.03  *
7.03   CallP CO402R(l_filg:w_firm:0:'LO500R_703':
7.03                    co402_verdi1:co402_verdi2);
7.03   if co402_verdi1 = '1';
7.03     u_703 = *on;
7.03   endif;
7.04  *
7.04  * Endring 7.04
7.04  *
7.04   CallP CO402R(l_filg:w_firm:0:'LO500R_704':
7.04                    co402_verdi1:co402_verdi2);
7.04   if co402_verdi1 = '1';
7.04     u_704 = *on;
7.04   endif;
7.05  *
7.05  * Endring 7.05
7.05  *
7.05   CallP CO402R(l_filg:w_firm:0:'LO500R_705':
7.05                    co402_verdi1:co402_verdi2);
7.05   if co402_verdi1 = '1';
7.05     u_705 = *on;
7.05   endif;
7.06  *
7.06  * Endring 7.06
7.06  *
7.06   CallP CO402R(l_filg:w_firm:0:'LO500R_706':
7.06                    co402_verdi1:co402_verdi2);
7.06   if co402_verdi1 = '1';
7.06     u_706 = *on;
7.06     *in41 = *on;
7.06   endif;
      *
      * Initierer skjermbilde
      *
     c                   if        p_numm = 0
     c                   eval      *in22 = *on
     c                   endif
      *
     c                   eval      b2hed2 = 'Best.verdi'
     c                   eval      b2hedd = 'Best.dato'
7.11 c                   eval      b2hed3 = 'Bekr.ldat'
     c                   eval      *in71  = *on
     c                   eval      w_numx = *zero
     c                   eval      w_sufx = *zero
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
6.31  *
6.31  * Firmastyrt validering av utvalgte felter
6.31  *
6.31  *
6.31 c                   eval      avall1_apgm = 'LO102R'
6.31 c     avall1_key    setll     avall1
6.31 c     avall1_key    reade     avall1
6.31 c                   dow       not %eof(avall1)
6.31 c                   if        avaflt = 'LOLMAT'  and
6.31 c                             avaval = 1
6.31 c                   eval      u_lmat = *on
6.31 c                   endif
6.31 c     avall1_key    reade     avall1
6.31 c                   enddo
7.04  *
7.04  * Key for henting av bruker
7.04  *
7.04 c     ausrl1_key    klist
7.04 c                   kfld                    ausrl1_user
7.05  *
7.05  * Key for kunderegister
7.05  *
7.05 c     rkunl1_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    rkunl1_kund
      *
      * Hente inn brukerregister
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1r                            90
7.04 c                   if        u_704 = *on
7.04  *
7.04 c                   eval      ausrl1_user = lusrl1_user
7.04 c     ausrl1_key    chain     ausrl1
7.04 c                   eval      h1avde = absavd
7.04 c                   endif
      *
      * Hente inn ofak kode-register
      *
     c     lstsl1_key    chain     lstsl1r                            90
     c                   eval      w_fell = laabrk
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = 0
     c     lohel1_key    setll     lohel1
      *
      * Dersom ordretype er oppgitt på bruker,
      * posisjoner første gang på denne ordretype
      *
     c***                if        fbotyp     <> *blank
     c***                eval      w_seqe      = 'O'
     c***                eval      lohelo_otyp = fbotyp
     c***  lohelo_key    setll     lohelo
     c***                endif
      *
7.02 c                   if        p_numm = 0
7.02 c                   eval      w_seqe = 'S'
7.02 c                   eval      b_open_sok = *on
7.02 c                   endif
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
**     M e l d i n g e r
Feil, Den valgte ordretype er ikke tilbud         <---
Bestillingens ordre er i bruk,vent med vedlikehold<---
