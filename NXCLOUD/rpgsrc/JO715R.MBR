     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO715R
      * Beskrivelse . . : Maskinell matching mot varetekst og
      *                   leverandørens varenummer, parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121196 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl2    if   e           k disk    rename(jmatpfr:jmatl2r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
     d p_tek1          s                   like(jotek1)
     d p_ldo2          s                   like(joldo2)
      *
      * Definering av variable i nøkler
      *
     d jmatl2_tek1     s                   like(jotek1)
     d jmatlu_var1     s                   like(jovar1)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_l             s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finner lengden på oppgitt varetekst
      *
     c                   eval      w_l = %len(%trim(p_tek1))
      *
      * Leser matche-registeret sortert etter varetekst
      *
     c                   eval      jmatl2_tek1 = p_tek1
     c     jmatl2_key    setll     jmatl2
     c                   read      jmatl2                                 90
     c                   dow       *in90 = *off
      *
     c                   if        jotek1 = *blank or
     c                             jokode <> 0 or
     c                             jovkod <> 0
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker match på varetekst
      *
     c                   if        %subst(jotek1:1:w_l) <> p_tek1
     c                   leave
     c                   endif
      *
      * Sjekker match på leverandørs varenummer
      *
     c                   eval      jvarl8_ldor = p_ldo2
     c                   eval      jvarl8_lvar = jolvar
     c     jvarl8_key    chain     jvarl8                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
      * Oppdaterer matche-register med matchet vare
      *
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur                            92
     c                   if        *in92 = *off
     c                   eval      jovar2 = jvvare
     c                   eval      joldo2 = jvldor
     c                   eval      jokode = 2
     c                   update    jmatlur
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      jmatl2                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_tek1
     c                   parm                    p_ldo2
      *
      * Key for les på matche-register
      *
     c     jmatl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl2_tek1
      *
      * Key for oppdatering av matche-register
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      * Key for oppslag på vare
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
