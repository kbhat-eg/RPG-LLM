     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL517R
      * Beskrivelse . . : Leverandørposter, spørring utbet.forslag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170305 KOB  Nytt program
      * V5.50 190108 KOB  Endret antall siffer i skjermbildet for linjenr. til 5
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frltrld    if   e           k disk    rename(rltrpfr:rltrldr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl2    if   e           k disk    rename(rltrpfr:rltrl2r)
      *
     frl517d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rltrld_raar     s                   like(roraar)
     d rltrld_bfnr     s                   like(robfnr)
     d rltrld_bfli     s                   like(robfli)
     d rlevl1_levr     s                   like(rolevr)
     d rltrl2_levr     s                   like(rolevr)
     d rltrl2_bdat     s                   like(robdat)
     d rltrl2_tist     s                   like(rotist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rofirm)
     d w_bfnr          s                   like(robfnr)                                      .
     d w_levr          s                   like(rolevr)                                      .
     d w_raar          s                   like(roraar)                                      .
      *
     d p_raar          s                   like(roraar)                                      .
     d p_bfnr          s                   like(robfnr)                                      .
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rltrld_bfnr = b1bfnr
     c     rltrld_key    setll     rltrld
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2win                                                  . .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     rltrld_key    reade     rltrld
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        p_raar <> roraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rltrld_bfnr = robfnr
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = robdat
     c                   eval      b1tist = rotist
     c                   eval      b1bfnr = robfnr
     c                   eval      b1bfli = robfli
     c                   eval      b1biln = robiln
     c     *dmy          move      robdat        b1bida
     c                   eval      b1bilk = robilk
     c                   eval      b1text = rotext
     c                   eval      b1belØ = robelØ
     c                   eval      b1levr = rolevr
      *                                                                 .
      * Hent leverandør                                                 .
      *                                                                 .
     c                   eval      rlevl1_levr = rolevr                                      .
     c     rlevl1_key    chain     rlevl1                             61                     .
     c                   if        *in61 = *off                                              .
     c                   eval      b1navn = rlnavn                                           .
     c                   endif                                                               .
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rltrld_key    setgt     rltrld
     c                   readp     rltrld
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rltrld
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        p_raar <> roraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rltrld_bfnr = robfnr
      *
     c                   enddo
      *
     c                   if        %eof
     c     rltrld_key    setll     rltrld
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for vis                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rltrl2_levr = b1levr
     c                   eval      rltrl2_bdat = b1bdat
     c                   eval      rltrl2_tist = b1tist
     c     rltrl2_key    chain     rltrl2                             60
      *
     c                   move      robfnr        c2bfnr                                      .
     c                   move      robfli        c2bfli                                      .
     c                   move      robiln        c2biln                                      .
     c                   move      robilk        c2bilk                                      .
     c                   move      rotext        c2text                                      .
     c     *dmy          move      robdat        c2bdat                                      .
     c     *dmy          move      rofdat        c2fdat                                      .
     c                   move      robetb        c2betb                                      .
     c                   move      rorper        c2rper                                      .
     c                   move      roraar        c2raar                                      .
     c                   move      rojour        c2jour                                      .
     c                   move      rorefn        c2refn                                      .
     c                   move      robelØ        c2belØ                                      .
     c                   move      rorest        c2rest                                      .
     c                   move      rovalk        c2valk                                      .
     c                   move      rovalb        c2valb                                      .
     c                   move      rovalr        c2valr                                      .
     c                   move      rorrf1        c2rrf1                                      .
     c                   move      rorrf2        c2rrf2                                      .
     c                   move      rokidi        c2kidi                                      .
      *
     c                   if        roadat <> *loval                                          .
     c     *dmy          move      roadat        c2adat                                      .
     c                   else
     c                   eval      c2adat = 0
     c                   endif
      *
     c                   if        rofoda <> *loval                                          .
     c     *dmy          move      rofoda        c2foda                                      .
     c                   else
     c                   eval      c2foda = 0
     c                   endif
      *
     c                   if        rordat <> *loval                                          .
     c     *dmy          move      rordat        c2rdat                                      .
     c                   else
     c                   eval      c2rdat = 0
     c                   endif
      *
     c                   eval      c2ftim = roftim                                           .
     c                   eval      c2rtim = rortim                                           .
     c                   eval      c2fsig = rofsig                                           .
     c                   eval      c2rsig = rorsig                                           .
     c                   eval      c2attu = roattu                                           .
     c                   eval      c2tkto = rotkto                                           .
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_raar
     c                   parm                    p_bfnr
      *
      * Key for posisjonering på betalingsforslagsnummer
      *
     c     rltrld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrld_raar
     c                   kfld                    rltrld_bfnr
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for lesing av postering
      *
     c     rltrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl2_levr
     c                   kfld                    rltrl2_bdat
     c                   kfld                    rltrl2_tist
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = p_raar
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rltrld_raar = p_raar
     c                   eval      rltrld_bfnr = p_bfnr
     c     rltrld_key    setll     rltrld
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
