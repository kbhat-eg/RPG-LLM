     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB011R
      * Beskrivelse . . : Leser linjer fra Fovf til kommaseparert til
      *                   (Business, kundeiFfo)
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
6.20  *       040414 nho  Firma ble ikke flyttet til nøkkel
6.21  * V6.20 141014 nho  Test på firma
6.30  *       091117 nho  Skriver ikke ut alle adresser
8.02  *       220422 mst  InvoCust og FactNO skal sendes blank, hvis de er 0 i NextStep
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffovfl1    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
      *
     frv01pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d w_kund          s                   like(rkkund)
     d w_biln          s                   like(ffbiln)
     d w_firm          s                   like(fffirm)
     d rkunl1_kund     s                   like(rkkund)
     d tell            s              1  0
     d w_rkfaku        s              8
     d w_rkfacn        s              8
      *
6.20 c                   eval      w_firm = l_firm
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Leser post
     c     nyles         tag
      *
     c                   read      fovfl1                                 60
      *
610  c                   if        *in60 = '1'
610  c                   goto      slutt
610  c                   endif
      *
6.21  *
6.21 c                   if        l_firm <> fffirm
6.21 c                   goto      nyles
6.21 c                   endif
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
      *
     c                   eval      rvdata = *blank
     c                   write     rv01pfr
      *
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@IMPORT_METHOD(1)')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = %trim('@ActoR(=CustNO,') +
     c                                           ('Nm,Shrt,Ad1,Ad2,') +
     c                                           ('PNo,PArea,Phone,') +
     c                                           ('PrivPh,Fax,') +
     c                                           ('InvoCust,FactNo)')
     c                   write     rv01pfr
      *
      * Ta vare på første bilagsnr
     c                   eval      w_biln = ffbiln
      *
     c                   endif
      *
      *
      * Finne navn og adresse på første post som har 0 i linjenummer
      * første gang brukes en teller og ikke test på brudd på bilagsnr
     c
     c                   if        tell = 1 and
     c                             ffbill = 0
     c                   exsr      finnadr
     c                   add       1             tell
     c                   goto      nyles
     c                   else
     c                   if        tell = 1 and
     c                             ffbill <> 0
     c                   goto      nyles
     c                   endif
     c                   endif
      *
      * Sjekker resterende poster ved brudd på bilagsnr og linjenr = 0
      * for å finne navn
6.30 c                   if        ffbiln <> w_biln
6.30 c                   eval      w_biln = ffbiln
6.30 c                   endif
      *
6.30 c                   if        ffbiln = w_biln and
     c                             ffbill = 0
     c                   exsr      finnadr
     c** 6.30            eval      w_biln = ffbiln
     c                   goto      nyles
     c                   endif
      *
      * Les ny post
     c                   goto      nyles
      *
     c     slutt         tag
      *
      * Avslutnings tekster
      *
     c                   eval      rvdata = *blank
     c                   write     rv01pfr
      *
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@FIRM_END(279159)')
     c                   write     rv01pfr
      *
     c                   eval      *inlr = *on
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Finner navn, adresse osv. / skriv post
      *
     c     finnadr       begsr
      *
     c                   if        ffckto <> 0
     c                   eval      w_kund = ffckto
     c                   else
     c                   eval      w_kund = ffdkto
     c                   endif
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             90
      *
     c                   if        *in90 = *off
8.02 c*
8.02 c                   if        rkfaku = 0
8.02 c                   movel     *blanks       w_rkfaku
8.02 c                   else
8.02 c                   movel     rkfaku        w_rkfaku
8.02 c                   endif
8.02 c                   if        rkfacn = 0
8.02 c                   movel     *blanks       w_rkfacn
8.02 c                   else
8.02 c                   movel     rkfacn        w_rkfacn
8.02 c                   endif
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('"') +
      * Kundenr
     c                                     %char(w_kund)     + '" "' +
      * Navn
     c                                     %trim(rknavn)     + '" "' +
      * Søkenavn
     c                                     %trim(rkalfa)    + '" "' +
      * Adresse 1
     c                                     %trim(rkgate)     + '" "' +
      * Adresse 2
     c                                     %trim(rkadr2)     + '" "' +
      * Postnummer
     c                                     %char(rkponr)     + '" "' +
      * Poststed
     c                                     %trim(rksted)     + '" "' +
      * Telefon
     c                                     %trim(rkmobn)     + '" "' +
      * Priv. telefon
     c                                     %trim(rktlfn)     + '" "' +
      * Fax
     c                                     %trim(rktlfx)     + '" "' +
      * Fakturaknr.
8.02 c*                                    %char(rkfaku)     + '" "' +
8.02 c                                     %trim(w_rkfaku)     + '" "' +
      * Factoringnr.
8.02 c*                                    %char(rkfacn)     + '"'
8.02 c                                     %trim(w_rkfacn)     + '"'
      *
     c                   write     rv01pfr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
