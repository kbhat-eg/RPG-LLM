     h option(*nodebugio) datedit(*dmy) decedit('.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NW820R
      * Beskrivelse . . : WAP, API - hente vareinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       291003 HKO  Nytt program
      *       180304 HKO  Tilpasset nye feltlenger
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.31  * 6.30  151018 bof  Utvidet med trelast-sortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_avde          s              4  0
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                        47
     d  d_kund                        9  0 overlay(d_inpu: 1)
     d  d_kpro                       12    overlay(d_inpu:10)
     d  d_eann                       26    overlay(d_inpu:22)
      *
      * Datakø - ut
      *
     d                 ds
     d d_outp                       281
     d  d_vare                       16    overlay(d_outp:  1)
     d  d_nobb                        8  0 overlay(d_outp: 17)
     d  d_tek1                       35    overlay(d_outp: 25)
     d  d_tek2                       35    overlay(d_outp: 60)
     d  d_kpri                       10    overlay(d_outp: 95)
     d  d_enh1                        3    overlay(d_outp:105)
     d  d_enh2                        3    overlay(d_outp:108)
     d  d_enh3                        3    overlay(d_outp:111)
     d  d_sap1                       10    overlay(d_outp:114)
     d  d_sap2                       10    overlay(d_outp:124)
     d  d_sap3                       10    overlay(d_outp:134)
     d  d_rab1                        6    overlay(d_outp:144)
     d  d_rab2                        6    overlay(d_outp:150)
     d  d_rab3                        6    overlay(d_outp:156)
     d  d_ntp1                       10    overlay(d_outp:162)
     d  d_ntp2                       10    overlay(d_outp:172)
     d  d_ntp3                       10    overlay(d_outp:182)
     d  d_lag1                       12    overlay(d_outp:192)
     d  d_lag2                       12    overlay(d_outp:204)
     d  d_lag3                       12    overlay(d_outp:216)
     d  d_omr1                       13    overlay(d_outp:228)
     d  d_omr2                       13    overlay(d_outp:241)
     d  d_omr3                       13    overlay(d_outp:254)
     d  d_enhe                        3    overlay(d_outp:267)
     d  d_anta                       12    overlay(d_outp:270)
      *
      * Datastruktur for Ean128 - kode
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eean                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_eant                        6  2 overlay(d_e128:21)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
      *
      * Definering av variable
      *
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_eanx          s             14
     d w_vare          s             15
     d w_kund          s              6  0
     d w_kpro          s              6  0
     d w_anta          s             11  3
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.31 d w_lv_vare       s                   like(vvvare)
      *
     d w_firm          s                   like(vvfirm)
      *
      * Definering av konstanter
      *
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av forespørsel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        d_eann = *blank
     c                   eval      p_feil = *on
     c                   leavesr
     c                   endif
      *
      * Indentifiserer varen
      *
     c                   exsr      ident_vare
     c                   if        p_feil = *on
     c                   leavesr
     c                   endif
      *
      * Henter informasjon
      *
     c                   eval      w_kund = d_kund
     c***                eval      w_kpro = d_kpro
     c                   movel     d_kpro        w_kpro
      *
     c                   eval      d_outp = *blank
      *
     c                   call      'VV990R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    d_outp
      *
     c                   if        w_anta <> 0
     c                   eval      d_enhe = d_enh1
     c                   eval      d_anta = %editc(w_anta:'4')
     c                   endif
      *
      * Skriver til output-datakø
      *
     c                   call      'QSNDDTAQ'
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_outl
     c                   parm                    d_outp
     c                   parm                    p_keyl
     c                   parm                    p_uniq
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for identifisering/validering av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ident_vare    begsr
      *
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
      * 4) num + lengde <= 13:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
      * Finner lengde og sjekker om varenummer inneholder alfa-verdi
      *
     c                   eval      w_leng = %len(%trim(d_eann))
      *
     c     digit         check     d_eann        w_posi
      *
      * Behandling dersom alfa
      *
     c                   if        w_posi > 0
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :d_eann
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      vvvare = w_lv_vare
6.31 c                   leavesr
6.31 c                   endif
      *
     c                   eval      vvarl4_lvar = d_eann
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   eval      p_feil = *on
     c                   leavesr
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      d_e128 = d_eann
     c                   eval      w_eann = d_eean
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      w_anta = d_eant
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      p_feil = *on
     c                   leavesr
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vare = %subst(null:1:8-w_leng) + d_eann
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
6.11  * Behandling dersom lengde <= 14
      *
6.11 c                   if        w_leng <= 14
      *
6.11 c                   eval      w_eanx = %subst(null:1:14-w_leng) +
     c                                      d_eann
      *
6.11 c                   movel     w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :d_eann
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      vvvare = w_lv_vare
6.31 c                   leavesr
6.31 c                   endif
      *
     c                   eval      vvarl4_lvar = d_eann
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   leavesr
     c                   endif
      *
      * Setter feil-flagg dersom vare ikke ble funnet
      *
     c                   eval      p_feil = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = *on
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_avde
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenr
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
