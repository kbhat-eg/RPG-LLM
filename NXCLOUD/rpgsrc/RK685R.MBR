     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK685R                                              *
      * Beskrivelse . . : Legger ut poster til factoringsselskap              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.40     250405  Nytt program                                   KOB
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl1    if   e           k disk    rename(rkwbpfr:rkwbl1r)
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     fra12l1    if   e           k disk    rename(ra12pfr:ra12l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frefapf    o    e           k disk    rename(refapfr:refapfr)
     frekupf    o    e           k disk    rename(rekupfr:rekupfr)
      *
      *  LDA omrÃ¥det
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pparam               300    490
      *
     d l_ruti                800    809
     d l_oflw                860    862  0
     d l_kidk                883    883  0
     d l_ocrk                885    885  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_firm          s                   like(rnfirm)
     d w_kund          s                   like(rnkund)
     d w_9kun          s                   like(ra9kun)
     d w_pkdat         s               d   datfmt(*iso)
      *
     d rkunl1_kund     s                   like(rnkund)
     d ra12l1_lkod     s                   like(ralkod)
      *
      *
     c     dummy         tag
      *
     c     rkwbl1_key    setll     rkwbl1                                 60
     c     rkwbl1_key    reade     rkwbl1                                 60
      *
     c                   dow       *in60 = *off
      *
      * Legg ut kunder med poster
      *
     c                   if        w_9kun = 1 and
     c                             rnkund <> w_kund
     c                   eval      rkunl1_kund = rnkund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
      *
     c                   eval      w_kund = rnkund
     c                   write     rekupfr
      *
     c                   endif
     c                   endif
      *
     c                   write     refapfr
      *
     c     rkwbl1_key    reade     rkwbl1                                 60
     c                   enddo
      *
      * Legg ut alle kunder
      *
     c                   if        w_9kun = 2
      *
     c     rkunlr_key    setll     rkunlr                                 61
     c     rkunlr_key    reade     rkunlr                                 61
      *
     c                   dow       *in61 = *off
      *
     c                   write     rekupfr
      *
     c     rkunlr_key    reade     rkunlr                                 61
     c                   enddo
      *
     c                   endif
      *
     c     slutt         tag
      *
     c                   seton                                        lr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Key for transaksjonsregister
      *
     c     rkwbl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 9
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for landkode
      *
     c     ra12l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra12l1_lkod
      *
      * Key for les av kunderegister
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
      *
      * Key for chain av kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   eval      w_firm = l_firm
      *
      * Hent statusinformasjon
      *
     c     raa9l1_key    chain     raa9l1
     c                   if        %found
     c                   eval      w_9kun = ra9kun
     c                   endif
      *
     c                   endsr
