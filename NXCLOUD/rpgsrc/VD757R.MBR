     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD757R
      * Beskrivelse . . : Varesortiment-linje, oppretter sortiment
      *
      *                   Leser JVFFPF - frigitt pr. filgruppe
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080208 bof  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  051110 bof  Utvidet med lager på sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvfflr    if   e           k disk    rename(jvffpfr:jvfflrr)
      *
     fvshelu    uf a e           k disk    rename(vshepfr:vshelur)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_fgrp                931    933
     d l_user                911    920
      *
      * Definering av datastruktur - parameterliste inn
      *
     d                 ds
     d d_list                        48
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_vsor                       10    overlay(d_list:4)
     d  d_besk                       35    overlay(d_list:14)
      *
      * Definering av parametre
      *
     d p_list          s             48
      *
      * Definering av variable i nøkler
      *
     d vshelu_vsor     s                   like(vcvsor)
     d vsdtlu_vsor     s                   like(vdvsor)
     d jvfflr_ffgr     s                   like(jvffgr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vdfirm)
     d w_gvar          s                   like(jvfvar)
     d w_vare          s                   like(jvfvar)
     d w_fgrp          s                   like(l_fgrp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Oppretter/oppdaterer sortiment-hode
      *
     c                   eval      vshelu_vsor = d_vsor
     c     vshelu_key    chain     vshelur                            90
     c                   eval      vcvsor = d_vsor
6.20 c                   eval      vclage = 0
     c                   eval      vcbesk = d_besk
     c                   eval      vceusr = l_user
     c                   time                    vcedat
     c                   time                    vcetim
     c                   if        *in90 = *off
     c                   update    vshelur
     c                   else
     c                   eval      vcfirm = w_firm
6.10 c                   time                    vcodat
6.10 c                   time                    vcotim
6.10 c                   eval      vcousr = l_user
     c                   write     vshelur
     c                   endif
      *
      * Sletter eksisterende sortiment-linje
      *
     c                   eval      vsdtlu_vsor = d_vsor
     c     vsdtlu_key    setll     vsdtlur
     c     vsdtlu_key    reade     vsdtlur                                90
     c                   dow       *in90 = *off
     c                   delete    vsdtlur
     c     vsdtlu_key    reade     vsdtlur                                90
     c                   enddo
      *
      * Leser vareregister, og oppretter sortiment
      * Dersom varen har byttet varernummer via matching, opprettes nytt
      * varenummer i sortimentet
      *
     c     jvfflr_key    setll     jvfflr
     c     jvfflr_key    reade     jvfflr                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      vdfirm = w_firm
     c                   eval      vdvsor = d_vsor
6.20 c                   eval      vdlage = 0
     c                   eval      vdldor = 0
     c                   eval      vdogrp = 0
     c                   eval      vdhgrp = 0
     c                   eval      vdugrp = 0
     c                   eval      vdmodn = 0
     c                   eval      vdvare = jvfvar
      *
     c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
     c                   eval      vdeusr = l_user
      *
     c                   write     vsdtlur
      *
     c     jvfflr_key    reade     jvfflr                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på vare
      *
     c     jvfflr_key    klist
     c                   kfld                    w_fgrp
      *
      * Key for oppdatering av varesortiment-hode
      *
     c     vshelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshelu_vsor
      *
      * Key for oppdatering av varesortiment-linje
      *
     c     vsdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtlu_vsor
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = d_firm
     c                   eval      w_fgrp = d_vsor
      *
     c                   endsr
