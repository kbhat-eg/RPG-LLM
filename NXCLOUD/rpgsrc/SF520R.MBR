      /TITLE  ASSTAT Vedlikehold av salgsstatistikk, søkerutine
     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF520R
      * Beskrivelse . . : Vedlikehold av salgsstatistikk, søkerutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220599 KOB  Nytt program
      * 5.30  141104 KOB  Oppdaterer skjermbilde med endret kostpris
7.xx  * 7.00  300916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal2    if   e           k disk    rename(sstapfr:sstal2r)
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
      *
     fsf520d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sstal2_vare     s                   like(sfvare)
      *
     d sstal1_binr     s                   like(sfbinr)
     d sstal1_line     s                   like(sfline)
     d sstal1_bida     s                   like(sfbida)
      *
      * Definering av variable
      *
     d w_spge          s              3  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_swtc          s              1  0
     d w_tek1          s             30
      *
     d w_firm          s                   like(sffirm)
     d w_paar          s                   like(sfpaar)
     d w_vkun          s                   like(sfvkun)
     d w_bida          s                   like(sfbida)
     d w_vare          s                   like(sfvare)
     d w_sumk          s                   like(sfsumk)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      /EJECT
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2paar <> w_paar
     c                   eval      w_paar = b2paar                              CRT SFL
     c                   eval      b2vare = 1                                   CRT SFL
     c                   endif
      *
     c                   if        b2vare <> 0 or
     c                             b2binr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Vare
      *
     c                   if        wactfe = 'B2VARE'
     c                   movel     b2vare        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2vare
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'F'
     c     sstal1_key    setll     sstal1
     c                   other
     c     sstal2_key    setll     sstal2
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = ' '
     c                   movel     b2vare        sstal2_vare
     c     sstal2_key    setll     sstal2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på fakturanummer
      *
     c                   if        b2binr <> 0
     c                   eval      w_seqe = 'F'
     c                   eval      sstal1_binr = b2binr
     c     sstal1_key    setll     sstal1
     c                   goto      end_pos
      *
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = 0
     c                   eval      b2binr = 0
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn =  wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Endre salgsdetaljer
      *
     c                   if        b1valg = '2'
     c     *dmy          move      b1bida        w_bida
     c                   call      'SF521R'
     c                   parm                    w_firm
     c                   parm                    b1paar
     c                   parm                    b1binr
     c                   parm                    b1line
     c                   parm                    w_bida
     c                   parm                    w_sumk
     c                   eval      b1valg = *blank
     c                   eval      b1sumk = w_sumk
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'F'
     c                   read      sstal1                                 15
     c                   other
     c                   read      sstal2                                 15
     c                   endsl
      *
     c                   if        *in15                                        EOF
     c                   goto      end_crt
     c                   endif
      *
     c                   if        sffirm <> w_firm or
     c                             sfpaar <> w_paar
     c***                eval      *in15 = *on
     c***                goto      end_crt
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   eval      b1line = sfline
     c                   eval      b1wsid = sfwsid
     c                   eval      b1paar = sfpaar
     c                   eval      b1vare = sfvare
     c                   eval      b1tek1 = sftek1
     c                   eval      b1binr = sfbinr
     c                   eval      b1ornr = sfornr
     c                   eval      b1sumk = sfsumk
      *
     c                   eval      b1bida = 0
      *
     c                   if        sfbida <> *loval
     c     *dmy          move      sfbida        b1bida
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på fakturanummer
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    sstal1_binr
     c                   kfld                    sstal1_line
     c                   kfld                    sstal1_bida
      *
      * Key for posisjonering på varenummer
      *
     c     sstal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal2_vare
      *
      * Henter firmakode
      *
     c                   eval      w_firm = l_firm
      *
      * Sett startverdi for år
      *
     c                   eval      b2paar = *year
     c                   eval      w_paar = *year
      *
      * Fyller subfile
      *
     c                   eval      *in22 = *on
      *
     c     sstal2_key    setll     sstal2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
