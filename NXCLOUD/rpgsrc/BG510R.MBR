     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSHOP
      * Program . . . . : BG510R
      * Beskrivelse . . : ButikkGavekort, vis
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       021104 HKO  Nytt program
      *       250208 ASK  Lagt inn mulighet for å endre restbeløp
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbgavir    if   e           k disk    rename(bgavst:bgavirr)
     fbgavi1    if   e           k disk    rename(bgavst:bgavi1r)
     fbgaviu    uf   e           k disk    rename(bgavst:bgaviur)
     fbgavi2    if   e           k disk    rename(bgavst:bgavi2r)
     fbgavi3    if   e           k disk    rename(bgavst:bgavi3r)
     fbgavi4    if   e           k disk    rename(bgavst:bgavi4r)
      *
     fbg510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d bgavir_gave     s                   like(bggave)
     d bgavi1_gave     s                   like(bggave)
     d bgaviu_gave     s                   like(bggave)
     d bgavi2_odat     s                   like(bgodat)
     d bgavi3_kund     s                   like(bgkund)
     d bgavi4_navn     s                   like(bgnavn)
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
      *
     d w_seqe          s              1
     d w_knav          s             30
      *
     d w_firm          s                   like(bgfirm)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2odat <> 0 or
     c                             b2gave <> *blank or
     c                             b2kund <> 0 or
     c                             b2navn <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'G'
     c                   eval      bgavi1_gave = b1gave
     c     bgavi1_key    setll     bgavi1
     c                   when      w_seqe = 'K'
     c                   eval      bgavi3_kund = b1kund
     c     bgavi3_key    setll     bgavi3
     c                   when      w_seqe = 'N'
     c                   eval      bgavi4_navn = b1navn
     c     bgavi4_key    setll     bgavi4
     c                   other
     c     *dmy          move      b1odat        bgavi2_odat
     c     bgavi2_key    setll     bgavi2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Opprettet dato
      *
     c                   if        b2odat <> 0
     c                   eval      w_seqe = *blank
     c     *dmy          test(d)                 b2odat                 40
     c                   if        *in40 = *off
     c     *dmy          move      b2odat        bgavi2_odat
     c     bgavi2_key    setll     bgavi2
     c                   endif
     c                   goto      end_pos
     c                   endif
      *
      * Gavekortnr
      *
     c                   if        b2gave <> *blank
     c                   eval      w_seqe = 'G'
     c                   eval      bgavi1_gave = b2gave
     c     bgavi1_key    setll     bgavi1
     c                   goto      end_pos
     c                   endif
      *
      * Kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      bgavi3_kund = b2kund
     c     bgavi3_key    setll     bgavi3
     c                   goto      end_pos
     c                   endif
      *
      * Navn
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      bgavi4_navn = b2navn
     c     bgavi4_key    setll     bgavi4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2odat = 0
     c                   eval      b2gave = *blank
     c                   eval      b2kund = 0
     c                   eval      b2navn = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1gave = b1gave
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      *in30 = *off
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1gave = b1gave
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kjøp på gavekortet
      *
     c                   if        b1valg = 7
     c                   call      'BG550R'
     c                   parm                    w_firm
     c                   parm                    b1gave
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      bgavir_gave = c1gave
     c     bgavir_key    chain     bgavir
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2kund = bgkund
     c                   eval      c2navn = bgnavn
     c                   eval      c2obel = bgobel
     c                   eval      c2gbel = bggbel
     c                   eval      c2bong = bgbong
     c                   eval      c2eusr = bgeusr
     c     *dmy          move      bgodat        c2odat
     c     *dmy          move      bgedat        c2edat
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
      * F13=Kjøp på gavekort
      *
     c                   if        *inkm
     c                   call      'BG550R'
     c                   parm                    w_firm
     c                   parm                    c1gave
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering av restbeløp er valgt
      *
     c                   if        *in30 = *on
     c                   eval      bgaviu_gave = c1gave
     c     bgaviu_key    chain     bgaviur
     c                   if        %found
     c                   eval      bggbel = c2gbel
     c                   eval      bgeusr = l_user
     c                   eval      bgedat = w_date
     c                   update    bgaviur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'G'
     c                   read      bgavi1
     c                   when      w_seqe = 'K'
     c                   read      bgavi3
     c                   when      w_seqe = 'N'
     c                   read      bgavi4
     c                   other
     c                   read      bgavi2
     c                   endsl
      *
     c                   if        %eof or
     c                             bgfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'G'
     c                   eval      bgavi1_gave = bggave
     c                   when      w_seqe = 'K'
     c                   eval      bgavi3_kund = bgkund
     c                   when      w_seqe = 'N'
     c                   eval      bgavi4_navn = bgnavn
     c                   other
     c                   eval      bgavi2_odat = bgodat
     c                   endsl
      *
     c                   eval      b1valg = 0
     c     *dmy          move      bgodat        b1odat
     c                   eval      b1gave = bggave
     c                   eval      b1kund = bgkund
     c                   eval      b1navn = bgnavn
     c                   eval      b1obel = bgobel
     c                   eval      b1gbel = bggbel
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'G'
     c     bgavi1_key    setgt     bgavi1
     c                   readp     bgavi1
     c                   when      w_seqe = 'K'
     c     bgavi3_key    setgt     bgavi3
     c                   readp     bgavi3
     c                   when      w_seqe = 'N'
     c     bgavi4_key    setgt     bgavi4
     c                   readp     bgavi4
     c                   other
     c     bgavi2_key    setgt     bgavi2
     c                   readp     bgavi2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'G'
     c                   readp     bgavi1
     c                   when      w_seqe = 'K'
     c                   readp     bgavi3
     c                   when      w_seqe = 'N'
     c                   readp     bgavi4
     c                   other
     c                   readp     bgavi2
     c                   endsl
      *
     c                   if        %eof or
     c                             bgfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'G'
     c                   eval      bgavi1_gave = bggave
     c                   when      w_seqe = 'K'
     c                   eval      bgavi3_kund = bgkund
     c                   when      w_seqe = 'N'
     c                   eval      bgavi4_navn = bgnavn
     c                   other
     c                   eval      bgavi2_odat = bgodat
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'G'
     c     bgavi1_key    setll     bgavi1
     c                   when      w_seqe = 'K'
     c     bgavi3_key    setll     bgavi3
     c                   when      w_seqe = 'N'
     c     bgavi4_key    setll     bgavi4
     c                   other
     c     bgavi2_key    setll     bgavi2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        w_afld = 'B2KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på gavekortnr
      *
     c     bgavi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bgavi1_gave
      *
      * Key for oppdatering av gavekortnr
      *
     c     bgaviu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bgaviu_gave
      *
      * Key for posisjonering på opprettet dato
      *
     c     bgavi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bgavi2_odat
      *
      * Key for posisjonering på kunde
      *
     c     bgavi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bgavi3_kund
      *
      * Key for posisjonering på navn
      *
     c     bgavi4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bgavi4_navn
      *
      * Key for oppslag på ButikkGavekort
      *
     c     bgavir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bgavir_gave
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
     c                   time                    w_date
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      bgavi2_odat = *hival
     c     bgavi2_key    setll     bgavi2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
