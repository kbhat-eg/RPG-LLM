     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX720R
      * Beskrivelse . . : Legger lager-parameter fra vare til lager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       310507 BOF  Nytt program
5.60  *       270808 BOF  Utvidet med lagerplass og ltid
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  *       310812 bof  Nytt felt, endringsdato for faste data som vises
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_firm     s                   like(vlfirm)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   read      vvarl1
     c                   dow       not %eof
      *
     c                   if        vvmini <> 0 or
     c                             vvmaks <> 0 or
     c                             vvltid <> 0 or
     c                             vvbpun <> 0 or
     c                             vvbmen <> 0 or
     c                             vvltid <> 0 or
     c                             vvplas <> *blank or
     c                             vvsikl <> 0
     c                   exsr      upd_lager
     c                   endif
      *
     c                   read      vvarl1
     c                   enddo
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å flytte poster på lager til annet lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_lager     begsr
      *
     c                   eval      vlaglu_firm = vvfirm
     c                   eval      vlaglu_vare = vvvare
     c     vlaglu_key    setll     vlaglu
     c     vlaglu_key    reade     vlaglu
     c                   dow       not %eof(vlaglu)
      *
     c                   if        vlmini = 0
     c                   eval      vlmini = vvmini
     c                   endif
     c                   if        vlmaks = 0
     c                   eval      vlmaks = vvmaks
     c                   endif
     c                   if        vlltid = 0
     c                   eval      vlltid = vvltid
     c                   endif
     c                   if        vlbpun = 0
     c                   eval      vlbpun = vvbpun
     c                   endif
     c                   if        vlbmen = 0
     c                   eval      vlbmen = vvbmen
     c                   endif
     c                   if        vlsikl = 0
     c                   eval      vlsikl = vvsikl
     c                   endif
     c                   if        vlplas = *blank
     c                   eval      vlplas = vvplas
     c                   endif
     c                   if        vlltid = 0
     c                   eval      vlltid = vvltid
     c                   endif
      *
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.20 c                   time                    vlfeda
6.20 c                   time                    vlfeti
6.20 c                   eval      vlfeus = l_user
     c                   update    vlaglur
      *
     c     les_neste     tag
     c                   reade     vlaglu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på varereg
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    vlaglu_firm
     c                   kfld                    vlaglu_vare
      *
      * Henter firma
      *
     c                   eval      w_firm = 000
      *
     c                   endsr
