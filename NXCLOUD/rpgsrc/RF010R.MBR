      /TITLE  Bilagsregistrering
     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF010R                                              *
      * Beskrivelse . . : Bilagsregistrering                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.20     051003  Lagt inn kontroll mot lev. fakturanummer        KOB *
      * R5.20     271003  Fjernet buntsumkontroll                         KOB *
      * R5.20     271003  Fjernet valg av posteringstype                  KOB *
      * R5.20     311003  Lagt inn nye koder for feltstyring              KOB *
      * R5.20     311003  Skilt ut spørring i egen subrutine              KOB *
      * R5.20     050104  Korrigert oppdatering av buntsumpost v/endring  KOB *
      * R5.20     150204  Korrigert testing av valuta                     KOB *
      * R5.30     150304  Legger ut forslag til buntnr og periode/år      KOB *
      * R5.30     150304  Tillater posisjonering av cursor på avd/konto   KOB *
      * R5.30     150304  Skjermbildet ble ikke nullstilt ved nytt buntnr.KOB *
      * R5.30     270304  Tillater posisjonering av cursor på bærer/beløp KOB *
      * R5.30     010404  Korrigert sjekk av kostnadsbærer                HFL *
      * R5.30     050404  Utvidet beløpsfelt i bunthodet til 13.2         KOB *
      * R5.30     050404  Utvidet beløpsfelt i reg.bilder til 11.2        KOB *
      * R5.30     050404  Lagt ut dagens dato i bunt ved ny/endring       KOB *
      * R5.30     170404  Blanker kvitteringsbildet dersom sist           KOB *
      *                   registrerte linje blir slettet                  KOB *
      * R5.30     230604  Lagt inn test på negativt beløp                 KOB *
      * R5.30     060704  Kontroll av kid                                 HFL *
      * R5.30     280904  RTRESB ble ødelagt for prosjektposter.          HFL *
      * R5.30     241104  Kontroll av mva-kode kunder/leverandører        KOB *
      * R5.31     100205  Tillater registreing av poster uten valuta      KOB *
      *                   på reskontrokonti med valuta                    KOB *
      * R5.31     100205  Lagt inn call av rutine for utskrift av         KOB *
      *                   etiketter for MultiSupport for scanning         KOB *
      * R5.40     260305  Delt rutinen XNULLE i to pga. pos. av cursor    KOB *
      * R5.40     230505  Leverandørs gironr. vises i kvitteringsbildet.  HFL *
      * R5.40     240505  Lagt inn parameter for styring av etiketter     KOB *
      * R5.40     020605  ENDRE etter VISE gikk feil                      HFL *
      * R5.40     150705  Lev. fakturanr. utvidet til 15 pos.             HFL *
      * R5.40     230805  w_feil ble ikke 0-stilt                         KOB *
T5.40 * R5.40     010306  Prosjektkode 0-stilt                            HFL *
T5.40 * R5.40     170306  Posisjonering av cursor var falt ut             HFL *
      * R5.41     110206  Lagt inn bilde A7, reg. av interimsposter       KOB *
      * R5.41     140406  Lagt inn test for bare en etikett pr. bilag     KOB *
      * R5.41     100606  Lagt inn blanking av tekstfelt i kvittering     KOB *
      * R5.41     270706  Tatt bort høyrestilling av lev. fakturanr.      HFL *
      * R5.41     310806  Rettet ind. ved sjekk av bilagsnr.              HFL *
      * R5.41     210606  Utvidet kontroll av samme bilagsnummer i år     KOB *
      *                   til å omfatte kunde og leverandørposter         KOB *
      * R5.41     210606  Lagt inn kontroll av at begge konti ikke kan    KOB *
      *                   være reskontrokonti                             KOB *
      * R5.41     221106  Lagt inn mulighet for å korrigere gr.lag mva    KOB *
      * R5.41     221106  funksjonen bare tilgjengelig for bruker = ASP_..KOB *
      * R5.50     140607  Lagt inn melding hvis bærer er sperret.         HFL *
      * R5.50     300807  Lagt opp riktig buntsum ved henting av bunt     KOB *
      * R5.50     270907  År i bunthode må være 4-sifret.                 HFL *
      * R5.50     121107  Cursorplassering er endret.                     HFL *
      * R5.60     150208  Lagt inn bruk av funksjonstaster for valg       KOB *
      * R5.60     030308  Endret rutinen for preregistrering              KOB *
      * R5.60     151008  Kontroll av bilagsdato > 31.12.2039             KOB *
      * 6.10      011009  Dersom F4 tastes på debet/kredit og det er
      * 6.10              angitt en kode på vedl. bilagskoder.. skal
      * 6.10              bildet  hentes direkte
      * 6.11      210510  Får lov til å legge inn avd. på debet           NHO *
      * 6.11              kredit konto
      * 6.11              samt fjernet feilmelding 'Begge kan ikke være
      * 6.11              reskontro kont'
      * 6.12      080711  Lagt ny indikator på feilmelding felt A2BUNT
      * 6.20      111110  Prosjekt og aktivitet skal fjernes.             NHO
6.21  * 6.21      290512  Feilind. *in63 hang igjen ved endr bilagsnr.    NHO
6.22  * 6.22      290512  Nullst. felter ved overg. endr./registrering    NHO
6.23  * 6.23      291112  Sjekk om lev.faktnr. finnes fra før, dette år   NHO
6.23  *                   er endret dersom levnr er angitt både på deb.   NHO
6.23  *                   og kred.konto. F.eks bilagskode 90 (Samlefakt)  NHO
6.24  * 6.24      061212  I RF104R kommer feilmelding dersom bilagsdto    NHO
6.24  * 6.24              er lavere enn dagens dato i foreværende år. S   NHO
6.24  * 6.24              Samme dersom bilagsdato større enn neste år.    NHO
6.25  * 6.25 sst  211013  Tillate valutakode på hovedbokskonto (fra compello)
6.26  * 6.26 sst  291013  Kollisjon i bruk av *in85 / *in57
6.27 A* 6.20  220814 NHO  Endret indikator på feilmelding A2BUNT
6.27 A*                   Resulterte i at den ikke tok med seg
6.27 A*                   verdier fra bildet F5
6.28 A*      nho  101214  Fjernet endring 6.24 etter møte med T og Bir
7.xx  * 7.00  020216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  061119 bkv  Hardkodet plassering av popup pga Newlook
7.02  * K00   050520 bsa  Skrevet om til 27x132 bilde
7.03  * K00   020221 sst  Sjekk av posisjoner for m1win
7.04  * K00   150221 sst  Innhenting av posteringer fra excel-ark
7.05  * K00   160221 sst  Rettet størrelse på subfile
7.11 A*      nho  270416  Mulighet for å sortere på bilagskode
7.12 A*      nho  310516  Feil når ny sortering og bla
      *
8.01 A* K000 bsa  241121  Endre logikk rundt bilagsnummer
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    01-09 = Protect ved utblending av felter                     *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *    17-19 = Benyttes i forbindelse med DUP                       *
      *    20-29 = Protect ved utblending av felter                     *
      *       30 = Protect av felter ved vise                           *
      *    31-56 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    58-59 = Benyttes i forbindelse med DUP                       *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Benyttes i forbindelse med DUP                       *
      *    80-90 = Arbeidsindikatorer + ifbm. DUP                       *
      *       98 = Put override                                         *
      *                                                                 *
      *    Disponering av indikatorer i hovedbildet (A5BLD):            *
      *    --------------------------------------------------           *
      *    Feltnavn  Protect  Nondisp  Poscur  Duppe                    *
      *    --------  -------  -------  ------  -----                    *
      *    A5BILK                        31      17                     *
      *    A5BDAT                        32      18                     *
      *    A5BILN                        33      19                     *
      *    A5DKTO                        34      38                     *
      *    A5DSPS      01       01       35      59                     *
      *    A5DAVD      02       02       36      70                     *
      *    A5DMVA      03       03       37      71                     *
      *    A5BELØ                        38      72                     *
      *    A5CKTO                        39      73                     *
      *    A5CSPS      01       01       40      74                     *
      *    A5CAVD      02       02       41      75                     *
      *    A5CMVA      03       03       42      76                     *
6.20  *    A5ARBO      07       07       43      77                     *
6.20  *    A5PROS      07       07       57      77                     *
6.20  *    A5UPRO      07       07       58      77                     *
6.20  ***  A5AKTI      08       08       44      78                     *
      *    A5KRYS      09       09       45      79                     *
      *    A5TEXT      20       20       46      80                     *
      *    A5FDAT      21       21       47      81                     *
      *    A5KRAB      22       22       48      82                     *
      *    A5VALK      23       23       49      83                     *
      *    A5VALB      24       24       50      84                     *
      *    A5REFN      25       25       51      85                     *
      *    A5LFAK      26       26       52      86                     *
      *    A5KIDI      27       27       53      87                     *
      *    A5ATTE      28       28       54      88                     *
      *    A5MNGD      29       29       55      89                     *
      *    A5VIRK      06       06       56      90                     *
      *                                                                 *
      *    Disse subrutiner er benyttet (i denne rekkefølge):           *
      *    --------------------------------------------------           *
      *    XC2WIN  Subrutine for C2-vindu, endring av posteringer       *
      *    XD1WIN  Subrutine for D1-vindu, sletting av posteringer      *
      *    YD1WIN  Subrutine for D1-vindu, vising av posteringer        *
      *    XM1WIN  Subrutine for M1-vindu, meldinger som krever svar    *
      *    XV1WIN  Subrutine for V1-vindu, redigeringsvalg              *
      *    XX1WIN  Subrutine for X1-vindu, valg av kontotype (HKL)      *
      *                                                                 *
      *    XDUPPE  Subrutine for duplisering av inputfelter             *
      *    XKVITT  Subrutine for kvittering av felter ved registrering  *
      *    XNUL_IN Subrutine for nullstilling av indikatorer            *
      *    XNUL_FL Subrutine for nullstilling av felter mm              *
      *                                                                 *
      *    XA5SAV  Subrutine for save av felter for senere duplisering  *
      *    XA5STF  Subrutine som flytter A5-felter fra skjerm til file  *
      *    XA5FTS  Subrutine for flytter A5-felter fra file til skjerm  *
      *                                                                 *
      *    XSRBUN  Subrutine for sletting / reaktivering av bunter      *
      *                                                                 *
      *    XCLR01  Subrutine for clear   av subfile transaksjoner       *
      *    XCRT01  Subrutine for create  av subfile transaksjoner       *
      *    XDSP01  Subrutine for display av subfile transaksjoner       *
      *    XCLR02  Subrutine for clear   av subfile bunter              *
      *    XCRT02  Subrutine for create  av subfile bunter              *
      *    XDSP02  Subrutine for display av subfile bunter              *
      *    XBILAG  Subrutine for å finne bilagsnummer                   *
      *                                                                 *
      *    *INZSR  Subrutine for initiering ved programstart            *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frbunl1    if   e           k disk    rename(rbunpfr:rbunl1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtral1    if   e           k disk    rename(rtrapfr:rtral1r)
7.11 frtralx    if   e           k disk    rename(rtrapfr:rtralxr)
     frtralu    uf a e           k disk    rename(rtrapfr:rtralur)
     fausrl1    if   e           k disk
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.20 f*rprol1    if   e           k disk    rename(rpropfr:rprol1r)
6.20 frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
6.20 frp2ul1    if   e           k disk    rename(rp2upfr:rp2ul1r)
6.20 frp3al1    if   e           k disk    rename(rp3apfr:rp3al1r)
6.20 frp4ol1    if   e           k disk    rename(rp4opfr:rp4ol1r)
     frltrl3    if   e           k disk    rename(rltrpfr:rltrl3r)
     frltrl7    if   e           k disk    rename(rltrpfr:rltrl7r)
     frktrl3    if   e           k disk    rename(rktrpfr:rktrl3r)
     frhtrl4    if   e           k disk    rename(rhtrpfr:rhtrl4r)
     frktrlb    if   e           k disk    rename(rktrpfr:rktrlbr)
     frltrlc    if   e           k disk    rename(rltrpfr:rltrlcr)
6.10 fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     frf010d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
     f                                     sfile(u1wsf:srrn02)
     d mld             s             11    dim(5) ctdata perrcd(1)
     d var             s             50    dim(26) ctdata perrcd(1)
     d wmv             s              1    dim(8)
     d tegn            s              1    dim(25)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
6.10 d ra01l1_akod     s                   like(raakod)
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
6.20 d*rprol1_pros     s                   like(rppros)
6.20 d*rprol1_akti     s                   like(rpakti)
6.20 d*rprol1_kont     s                   like(rpkont)
6.20 d*rprol1_spes     s                   like(rpspes)
6.20 d rp1pl1_firm     s                   like(rp1frm)
6.20 d rp1pl1_pros     s                   like(rp1pro)
6.20 d rp2ul1_firm     s                   like(rp2frm)
6.20 d rp2ul1_pros     s                   like(rp2pro)
6.20 d rp2ul1_upro     s                   like(rp2upr)
6.20 d rp4ol1_firm     s                   like(rp4frm)
6.20 d rp4ol1_arbo     s                   like(rp4ord)
     d rltrl3_biln     s                   like(robiln)
     d rltrl7_levr     s                   like(rolevr)
     d rltrl7_raar     s                   like(roraar)
     d rltrl7_lfak     s                   like(rolfak)
     d rktrl3_biln     s                   like(rnbiln)
     d ra16l1_pkod     s                   like(rapkod)
     d rhtrl4_raar     s                   like(rbraar)
     d rhtrl4_biln     s                   like(rbbiln)
     d rkunl1_kund     s                   like(rkkund)
     d rlevl1_levr     s                   like(rllevr)
610  d rbunl1_nivo     s                   like(rsnivo)
610  d rbunl1_memb     s                   like(rsmemb)
     d rbunl1_bunt     s                   like(rsbunt)
610  d rtral1_nivo     s                   like(rtnivo)
610  d rtral1_memb     s                   like(rtmemb)
     d rtral1_bunt     s                   like(rtbunt)
     d rtral1_linj     s                   like(rtlinj)
     d rltrl1_bunt     s                   like(rtbunt)
     d rktrlb_raar     s                   like(rnraar)
     d rktrlb_biln     s                   like(rnbiln)
     d rltrlc_raar     s                   like(roraar)
     d rltrlc_biln     s                   like(robiln)
7.11 d rtralx_nivo     s                   like(rtnivo)
7.11 d rtralx_memb     s                   like(rtmemb)
7.11 d rtralx_bunt     s                   like(rtbunt)
7.11 d rtralx_bilk     s                   like(rtbilk)
      *
6.20 d*axakti          s                   like(a5akti)
     d axatte          s                   like(a5atte)
     d axdavd          s                   like(a5davd)
     d axcavd          s                   like(a5cavd)
     d axbdat          s                   like(a5bdat)
     d axbelØ          s                   like(a5belØ)
     d axbilk          s                   like(a5bilk)
     d axbiln          s                   like(a5biln)
     d axlfak          s                   like(a5lfak)
     d axfdat          s                   like(a5fdat)
     d axkidi          s                   like(a5kidi)
     d axkrab          s                   like(a5krab)
     d axkrys          s                   like(a5krys)
     d axdkto          s                   like(a5dkto)
     d axckto          s                   like(a5ckto)
     d axmngd          s                   like(a5mngd)
     d axdmva          s                   like(a5dmva)
     d axcmva          s                   like(a5cmva)
6.20 d axpros          s                   like(a5pros)
6.20 d axupro          s                   like(a5pros)
6.20 d axarbo          s                   like(a5pros)
6.20sd axakti          s                   like(a5akti)
     d axrefn          s                   like(a5refn)
     d axdsps          s                   like(a5dsps)
     d axcsps          s                   like(a5csps)
     d axtext          s                   like(a5text)
     d axvalb          s                   like(a5valb)
     d axvalk          s                   like(a5valk)
     d axvirk          s                   like(a5virk)
      *
     d n               s              2  0
     d sfrn01          s                   like(srrn01)
     d sfrn02          s                   like(srrn02)
     d spge01          s              4  0
     d spge02          s              4  0
     d srrn01          s              4  0
     d srrn02          s              4  0
     d ssrn01          s                   like(srrn01)
     d ssrn02          s                   like(srrn02)
      *
     d werror          s              1  0
     d wdbhkl          s              1
     d wkrhkl          s              1
     d wltrok          s              1
     d wmdlst          s             25
     d wfresn          s                   like(rtresn)
     d wsavli          s              2  0
     d wsavpo          s              3  0
     d wsdprk          s              1
     d wscprk          s              1
     d wsdval          s              1
     d wscval          s              1
     d wsdmva          s              1
     d wscmva          s              1
     d wsstat          s                   like(rsstat)
     d x1valg          s              1
     d wobdat          s              6  0
      *
     d p_valg          s              1
     d p_stat          s              1
     d p_refk          s              6  0
     d p_tx20          s             20
     d p_tx30          s             30
     d p_txt2          s             30
     d p_akod          s                   like(rtbilk)
     d p_ekod          s                   like(rhmvak)
     d p_gkod          s                   like(rhavde)
     d p_pkod          s                   like(rhvalk)
     d*p_ukod          s                   like(rpakti)
     d p_wkod          s                   like(rtvirk)
     d p_Økod          s                   like(rhspes)
     d p_kont          s                   like(rhkont)
     d p_spes          s                   like(rhspes)
     d p_avde          s                   like(rhavde)
6.12 d p_akti          s                   like(rp3akt)
6.20 d p_pros          s                   like(rp1pro)
6.20 d p_upro          s                   like(rp2upr)
6.20 d p_arbo          s                   like(rp4ord)
6.20 d p_prt1          s                   like(rp4txt)
6.20 d p_uprs          s              1
6.28 d*w_dato          s                   like(rtbdat)
     d p_kund          s                   like(rkkund)
     d p_levr          s                   like(rllevr)
     d p_biln          s                   like(rtbiln)
     d p_rest          s                   like(rnrest)
      *
610  d w_memb          s                   like(rtmemb)
     d w_firm          s                   like(rtfirm)
     d w_bunt          s                   like(rtbunt)
     d w_linj          s                   like(rtlinj)
     d w_bdat          s               d   datfmt(*iso)
     d w_fdat          s               d   datfmt(*iso)
     d w_maxdat        s               d   datfmt(*iso)
     d w_fors          s              1  0
     d w_kont          s              6  0
     d w_avde          s                   like(rhavde)
     d w_spes          s                   like(rhspes)
     d w_text          s                   like(rhtext)
     d w_valk          s                   like(rhvalk)
     d w_mvak          s                   like(rhmvak)
     d w_btxt          s                   like(a6btxt)
     d w_pdat          s                   like(rapkda)
     d w_ptxt          s                   like(a6ptxt)
     d w_dtxt          s                   like(a61txt)
     d w_ktxt          s                   like(a61txt)
T5.40d w_bgir          s                   like(rlbgir)
T5.40d w_dgir          s                   like(a6dgir)
T5.40d w_cgir          s                   like(a6cgir)
     d w_user          s                   like(abuser)
     d w_ktyp          s              1
     d w_proj          s              1
     d w_feil          s              1  0
     d w_aspuser       s              4
      *
T5.30 * Felt for kontroll av kid
T5.30d kid             s              1    dim(25)
T5.30d nyki            s              1    dim(25)
T5.30d tall            s              1    dim(24)
T5.30d ktr             s              1
T5.30d p_rkod          s              1
T5.30d p_tall          s             25
T5.30d w_kidi          s             25
T5.30d w_ki24          s             24
T5.30d w_sist          s              1
T5.30d v               s              2  0
T5.30d w               s              2  0
T5.30d tell            s              1  0
T5.39d psum            s              3  0
T5.30d vekt            s              1  0
T5.30d faktor          s              1  0
T5.30d prod            s              2  0
T5.30d difktr          s              2  0
T5.30d divtal          s              2  0
T5.30d rest            s              2  0
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * W_AROW - Actual line, angir startlinje for vindu.
      * W_ACOL - Actual posision, angir startposisjon for vindu
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Åpningsbilde for registrering
      * A5BLD  - A5 Hovedbilde for registrering
      * A7BLD  - A7 Hovedbilde for registrering av interimsposter
      * B1SFL  - B1 Vindu for bunter subfile record
      * B2CTL  - B2 Vindu for bunter control record
      * B2CMD  - B2 Vindu for bunter CMD-taster
      * C2WIN  - C2 Vindu for endring
      * D1WIN  - D1 Vindu for slette (Delete)
      * X1WIN  - X1 Vindu for valg av kontotyper (Hov-Kun-Lev)
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene C2WIN og D1WIN
      *             er referert til dette.
      * U1WSF  - U1 Subfile - record
      * U2WCT  - U2 Subfile - control record
      * U2CMD  - U2 Eget skjermbilde for CMD-taster
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for registrering av periode / buntsum      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Hent statusinfo/indikatorer for skjermbildet
      *
     c                   exsr      hnt_stat
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
     c                   exsr      xnulle_a5
      *
     c                   exsr      xnulle_a6
      *
     c                   exsr      xnul_in
     c                   exsr      xnul_fl
      *
     c                   eval      w_fors = 0
     c                   eval      *in31 = *on
      *
     c                   eval      wsavli = 4
     c                   eval      wsavpo = 26
      *
      * Valg av firma
      *
     c                   if        *inka = *on
     c                   call      'AA002R'
     c                   parm                    w_user
     c     rausrl1_key   chain     ausrl1
     c                   move      absfir        w_firm
     c                   move      absfir        l_firm
     c                   move      abfnav        l_fnav
     c                   goto      a2taga
     c                   endif
      *
      * Gå ut
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Spørre på bunter
      *
     c                   if        *inke = *on
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = *zero
     c     rbunl1_key    setll     rbunl1
     c                   exsr      xclr02
     c                   exsr      xcrt02
     c                   goto      u2taga
     c                   endif
      *
      * Ny bunt
      *
     c                   if        *inkf = *on
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = 99999
     c     rbunl1_key    setgt     rbunl1
     c                   readp     rbunl1                                 64
      *
     c                   if        *in64 = *on
     c                   eval      a2bunt = 1
     c                   else
     c                   eval      a2bunt = rsbunt + 1
     c                   move      rsraar        a2raar
6.12 c****               move      rsrper        a2rper
6.12 c                   eval      a2rper = *month
     c                   endif
610   *
610  c                   if        rsmemb <> w_memb
610  c                   eval      a2bunt = 1
610  c                   endif
      *
     c                   if        rsfirm <> l_firm
     c                   eval      a2bunt = 1
     c                   endif
      *
     c                   eval      w_bunt = a2bunt
     c                   eval      a2bsum = *zero
     c                   move      *on           *in33
     c                   goto      a2tagb
     c                   endif
7.04  *
7.04  * Ny bunt  fra Excel
7.04  *
7.04 c                   if        *inkh = *on
7.04 c                   unlock    rbunlu
7.04 c                   call      'HENTOKPC'
7.04 c                   goto      a2tagb
7.04 c                   endif
      * _____________________________________________________________
      *
      * Hente bunt dersom periode og buntsum ikke er oppgitt
      *
     c                   if        a2bunt <> *zero
     c                   if        a2raar = *zero or
     c                             a2rper = *zero
     c                   if        a2bsum = *zero
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = a2bunt
     c     rbunl1_key    chain     rbunlur
      *
     c                   if        %found
     c                   move      rsraar        a2raar
     c                   move      rsrper        a2rper
     c                   move      rsbsum        a2bsum
     c                   goto      a2tagb
     c                   endif
      *
     c                   endif
     c                   endif
     c                   endif
      *
      * Er periode = 0
      *
     c                   if        a2raar = *zero or
     c                             a2rper = *zero
     c                   move      *on           *in31
     c                   move      *on           *in81
     c                   goto      a2tagb
     c                   endif
      *
      * Periode (mnd) må ligge fra 1 til 13
      *
     c                   if        a2rper < 1 or
     c                             a2rper > 13
     c                   move      *on           *in31
     c                   move      *on           *in82
     c                   goto      a2tagb
     c                   endif
      *
T5.50 * År må være 4-sifret
      *
T5.50c                   if        a2raar < 2000
T5.50c                   move      *on           *in31
T5.50c                   move      *on           *in84
T5.50c                   goto      a2tagb
T5.50c                   endif
      *
      * Varsle ved å benytte år større enn *year
      *
     c                   if        a2raar > *year
     c                   move      var(23)       m1mld1
     c                   move      var(24)       m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *
      * Varsle dersom år er mindre enn *year-1
      *
     c                   if        a2raar < *year - 1
     c                   move      var(25)       m1mld1
     c                   move      var(26)       m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *
      * Postere på fjor-år?
      *
     c                   if        a2raar = *year - 1
     c                   move      var(7)        m1mld1
     c                   move      var(8)        m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *
      * Koderegister sier at det bare kan registreres på
      * periode 2-4-6-8-10-12, men her er 1-3-5-7-9-11-13 benyttet
      *
     c                   if        ra1avs = 1
     c                   if        a2rper = 1 or
     c                             a2rper = 3 or
     c                             a2rper = 5 or
     c                             a2rper = 7 or
     c                             a2rper = 9 or
     c                             a2rper = 11 or
     c                             a2rper = 13
     c                   move      *on           *in31
     c                   move      *on           *in85
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *
      * Koderegister sier at denne periode er avsluttet
      * Skal periode benyttes ?
      *
     c                   if        a2raar < ra1aar or
     c                             a2raar = ra1aar and
     c                             a2rper <= ra1per
     c                   move      var(11)       m1mld1
     c                   move      var(12)       m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *
      * Er buntnr. = 0
      *
     c                   if        a2bunt = *zero
6.27 c**                 move      *on           *in32
6.27 c                   move      *on           *in97
     c                   goto      a2tagb
     c                   endif
      *
      * Hente bunt
      *
     c                   eval      w_bunt = a2bunt
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = w_bunt
     c     rbunl1_key    chain     rbunlur
      *
      * Kontroll dersom bunt finnes
      *
     c                   if        %found
      *
      * Endret periode ?
      *
     c                   if        rsraar <> a2raar or
     c                             rsrper <> a2rper
     c                   move      var(1)        m1mld1
     c                   move      var(2)        m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   move      rsrper        a2rper
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *                                                                 .
      * Endret buntsum ?                                                .
      *                                                                 .
     c                   if        rsbsum <> a2bsum
     c                   move      var(3)        m1mld1
     c                   move      var(4)        m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   move      rsbsum        a2bsum
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *
      * Varsle at bunt er benyttet allerede
      *
     c                   move      var(21)       m1mld1
     c                   move      var(22)       m1mld2
     c                   move      'J'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   goto      a2tagb
     c                   endif
      *
     c                   endif
      *
      * Oppdater bunt
      *
     c                   move      a2raar        rsraar
     c                   move      a2rper        rsrper
     c                   move      a2bsum        rsbsum
     c                   time                    rspdat
      *
     c                   if        %found
610  c                   time                    rsedat
610  c                   time                    rsetim
     c                   update    rbunlur
     c                   else
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   eval      rsfirm = l_firm
     c                   eval      rsbunt = w_bunt
     c                   eval      rspdeb = *zero
     c                   eval      rspkre = *zero
     c                   eval      rsstat = *blank
     c                   write     rbunlur
     c                   endif
      *
      * Klargjøre skjerm-felter
      *
     c                   eval      a5pdeb = *zero
     c                   eval      a5pkre = *zero
      *
     c                   if        %found
     c                   move      rspdeb        a5pdeb
     c                   move      rspkre        a5pkre
     c                   endif
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A5BLD Behandle bilde for registrering transaksjoner             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a5taga        tag
      *
     c                   move      *off          *in98
     c                   write     a5bld
      *
      * Finne neste linjenummer i bunten
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_bunt = w_bunt
     c                   eval      rtral1_linj = 99999
     c     rtral1_key    setgt     rtral1
     c                   readp     rtral1                                 61
      *
     c                   if        *in61 = *on
     c                   eval      a5linj = 10
     c                   else
     c                   eval      a5linj = rtlinj + 1
     c                   endif
      *
     c                   if        rtbunt <> a2bunt
     c                   eval      a5linj = 10
     c                   endif
      *
     c                   if        rtfirm <> l_firm
     c                   eval      a5linj = 10
     c                   endif
      *
     c                   eval      w_linj = a5linj
     c                   eval      a6linj = a5linj
      *
     c     a5tagb        tag
      *
     c                   eval      wsavli = 4
     c                   eval      wsavpo = 26
     c                   eval      a5pdif = a5pdeb - a5pkre
     c                   eval      a5bdif = a2bsum - a5pdeb
      *
     c                   if        w_fors <> 0
     c                   exsr      xduppe
T5.50c                   exsr      xnul_cur
     c                   endif
      *
     c                   move      *on           *in98
     c                   exfmt     a5bld
      *
     c                   exsr      xnul_in
T5.40c                   eval      wsdprk = *blank
T5.40c                   eval      wscprk = *blank
      *
      * Spørre på transaksjoner
      *
     c                   if        *inka = *on
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_linj = *zero
     c     rtral1_key    setll     rtral1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   goto      b2taga
     c                   endif
      *
      * Avslutte, men differanse debet / kredit
      *
     c                   if        *inkc = *on
      *
     c                   if        a5pdeb <> a5pkre
     c                   move      var(17)       m1mld1
     c                   move      var(18)       m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   goto      a5tagb
     c                   endif
     c                   endif
      *
      * Avslutte, men differanse i buntsummering
      *
     c                   if        ra1bun = 'J' and
     c                             a2bsum <> *zero and
     c                             a5bdif <> *zero
     c                   move      var(19)       m1mld1
     c                   move      var(20)       m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   goto      a5tagb
     c                   endif
     c                   endif
      *
      * Avslutning av bunt er godkjent
      *
     c                   goto      a2taga
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spr_rut
     c                   move      *on           *in51
     c                   eval      w_fors = 0
     c                   goto      a5tagb
     c                   endif
      *
      * Rediger skjermbilde
      *
     c                   if        *inkm = *on
     c                   exsr      xv1win
     c                   goto      a5tagb
     c                   endif
      *
      * Gå til preregistrering
      *
     c                   if        *inkg = *on
     c                   goto      a7taga
     c                   endif
      *
      * Hent postering fra kundereskontro
      *
     c                   if        a5refn <> 0 and
     c                             a5dkto = 0 and
     c                             a5ckto = 0 and
     c                             a5belØ = 0
     c                   eval      rktrl3_biln = a5refn
     c     rktrl3_key    chain     rktrl3
      *
     c                   if        %found
      *
     c     ra04l1_key    chain     ra04l1
      *
     c                   if        rnrest > 0
     c                   eval      a5davd = rada15
     c                   eval      a5dkto = radk15
     c                   eval      a5dsps = rads15
      *
     c                   eval      a5cavd = 0
     c                   eval      a5ckto = rnkund
     c                   eval      a5csps = 0
      *
     c                   else
      *
     c                   eval      a5cavd = rada15
     c                   eval      a5ckto = radk15
     c                   eval      a5csps = rads15
      *
     c                   eval      a5davd = 0
     c                   eval      a5dkto = rnkund
     c                   eval      a5dsps = 0
     c                   endif
      *
     c                   eval      w_fors = 0
     c                   eval      a5belØ = rnrest
     c                   eval      *in34 = *on
     c                   goto      a5tagb
     c                   endif
     c                   endif
      *
      * Begge kontonummer blanke - er bilag registrert før???
      *       (preregistrert??)
      *
     c                   if        a5biln <> 0 and
     c                             a5dkto = *zero and
     c                             a5ckto = *zero
      *
     c                   exsr      xbilag
      *
     c                   if        wltrok = '1'
     c                   move      *on           *in34
     c                   goto      a5tagb
     c                   endif
     c                   endif
      *
      * Kontroll
      *
     c                   eval      werror = 0
     c                   eval      w_feil = 0
      *
     c                   exsr      chk_rut
      *
     c                   if        werror <> 0 or
     c                             w_feil <> 0
     c                   eval      w_fors = 0
     c                   goto      a5tagb
     c                   endif
      *
      * Oppdater buntsumpost
      *
     c     rbunl1_key    chain     rbunlur
      *
     c                   if        a5dkto <> *zero
     c                   eval      rspdeb = rspdeb + a5belØ
     c                   endif
      *
     c                   if        a5ckto <> *zero
     c                   eval      rspkre = rspkre + a5belØ
     c                   endif
      *
     c                   if        %found
610  c                   time                    rsedat
610  c                   time                    rsetim
     c                   update    rbunlur
     c                   endif
      *
     c                   move      rspdeb        a5pdeb
     c                   move      rspkre        a5pkre
      *
      * Oppdater transaksjoner
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_linj = w_linj
     c     rtral1_key    chain     rtralur
      *
      * Flytte felter fra skjerm til file
      *
     c                   exsr      xa5stf
      *
      * Tar vare på reskontronummer
      *
     c                   if        rtdkto <> *zeros and
     c                             rtckto <> *zeros
     c                   move      wfresn        rtresn
     c                   goto      wfsamm
     c                   endif
      *
     c                   if        rtdkto = *zeros or
     c                             rtckto = *zeros
     c                             and rtbiln = axbiln
     c                   move      wfresn        rtresn
     c                   endif
      *
     c     wfsamm        tag
      *
      * Oppdater
      *
     c                   if        %found
610  c                   time                    rtedat
610  c                   time                    rtetim
     c                   update    rtralur
     c                   else
610  c                   eval      rtnivo  = *blank
610  c                   eval      rtmemb  = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   eval      rtfirm  = l_firm
     c                   eval      rtbunt  = w_bunt
     c                   eval      rtlinj  = w_linj
     c                   write     rtralur
      *
      * Skriv etikett for Multisupport
      *
     c                   if        ra1skd = 'R' and
     c                             rtbiln <> axbiln
     c                   call      'RF011C'
     c                   parm                    w_firm
     c                   parm                    rsraar
     c                   parm                    rtbiln
      *
     c                   endif
      *
     c                   endif
      *
      * Første post registrert - gjør klar for dup-funksjon
      *
     c                   eval      w_fors = 1
      *
      * Ta vare på felter for ev. senere dup
      *
     c                   exsr      xa5sav
      *
      * Legge opp felter til kvitteringsbilde
      *
     c                   exsr      xkvitt
      *
      * Klargjøre ny linje
      *
     c                   if        rtbiln <> axbiln
     c                   eval      wfresn = *zeros
     c                   endif
      *
     c                   eval      a5linj = a5linj + 1
     c                   eval      w_linj = a5linj
      *
     c                   exsr      xnulle_a5
      *
      * Neste postering
      *
     c                   goto      a5tagb
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A7BLD Behandle bilde for registrering interimstransaksjoner     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a7taga        tag
      *
     c                   move      *off          *in98
     c                   write     a7bld
      *
      * Finne neste linjenummer i bunten
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_bunt = w_bunt
     c                   eval      rtral1_linj = 99999
     c     rtral1_key    setgt     rtral1
     c                   readp     rtral1                                 61
      *
     c                   if        *in61 = *on
     c                   eval      a5linj = 10
     c                   else
     c                   eval      a5linj = rtlinj + 1
     c                   endif
      *
     c                   if        rtbunt <> a2bunt
     c                   eval      a5linj = 10
     c                   endif
      *
     c                   if        rtfirm <> l_firm
     c                   eval      a5linj = 10
     c                   endif
      *
     c                   eval      w_linj = a5linj
     c                   eval      a6linj = a5linj
      *
      * Hent interimskonto fra faste konti
      *
     c     ra04l1_key    chain     ra04l1
      *
     c     a7tagb        tag
      *
     c                   eval      wsavli = 4
     c                   eval      wsavpo = 26
     c                   eval      a5pdif = a5pdeb - a5pkre
     c                   eval      a5bdif = a2bsum - a5pdeb
      *
     c                   if        w_fors <> 0
     c                   exsr      xduppe
     c                   endif
      *
     c                   move      *on           *in98
     c                   exfmt     a7bld
      *
     c                   exsr      xnul_in
      *
      * Spørre på transaksjoner
      *
     c                   if        *inka = *on
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_linj = *zero
     c     rtral1_key    setll     rtral1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   goto      b2taga
     c                   endif
      *
      * Avslutning av bunt er godkjent
      *
     c                   if        *inkc = *on
     c                   goto      a2taga
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spr_rut
     c                   move      *on           *in51
     c                   eval      w_fors = 0
     c                   goto      a7tagb
     c                   endif
      *
      * Rediger skjermbilde
      *
     c                   if        *inkm = *on
     c                   exsr      xv1win
     c                   goto      a7tagb
     c                   endif
      *
      * Gå til vanlig registrering
      *
     c                   if        *inkg = *on
     c                   goto      a5tagb
     c                   endif
      *
      * Legg ut interimskonto
      *
     c                   if        a5dkto <> *zero
     c                   eval      a5ckto = radk21
     c                   eval      a5cavd = rada21
     c                   eval      a5csps = rads21
     c                   else
     c                   eval      a5dkto = radk21
     c                   eval      a5davd = rada21
     c                   eval      a5dsps = rads21
     c                   endif
      *
      * Legg ut interimskonto i reskontrofeltene på leverandørposten
      *
     c                   eval      rtresn = radk21
     c                   eval      rtresa = rada21
     c                   eval      rtress = rads21
      *
      * Kontroll
      *
     c                   eval      werror = 0
     c                   eval      w_feil = 0
      *
     c                   exsr      chk_rut
      *
     c                   if        werror <> 0 or
     c                             w_feil <> 0
     c                   eval      w_fors = 0
     c                   goto      a7tagb
     c                   endif
      *
      * Oppdater buntsumpost
      *
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c     rbunl1_key    chain     rbunlur
      *
     c                   if        a5dkto <> *zero
     c                   eval      rspdeb = rspdeb + a5belØ
     c                   endif
      *
     c                   if        a5ckto <> *zero
     c                   eval      rspkre = rspkre + a5belØ
     c                   endif
      *
     c                   if        %found
610  c                   time                    rsedat
610  c                   time                    rsetim
     c                   update    rbunlur
     c                   endif
      *
     c                   move      rspdeb        a5pdeb
     c                   move      rspkre        a5pkre
      *
      * Oppdater transaksjoner
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_linj = w_linj
     c     rtral1_key    chain     rtralur
      *
      * Flytte felter fra skjerm til file
      *
     c                   exsr      xa5stf
      *
      * Oppdater
      *
     c                   if        %found
610  c                   time                    rtedat
610  c                   time                    rtetim
     c                   update    rtralur
     c                   else
610  c                   eval      rtnivo  = *blank
610  c                   eval      rtmemb  = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
     c                   eval      rtfirm  = l_firm
     c                   eval      rtbunt  = w_bunt
     c                   eval      rtlinj  = w_linj
     c                   write     rtralur
      *
      * Skriv etikett for Multisupport
      *
     c                   if        ra1skd = 'R' and
     c                             rtbiln <> axbiln
     c                   call      'RF011C'
     c                   parm                    w_firm
     c                   parm                    rsraar
     c                   parm                    rtbiln
      *
     c                   endif
      *
     c                   endif
      *
      * Første post registrert - gjør klar for dup-funksjon
      *
     c                   eval      w_fors = 1
      *
      * Ta vare på felter for ev. senere dup
      *
     c                   exsr      xa5sav
      *
      * Legge opp felter til kvitteringsbilde
      *
     c                   exsr      xkvitt
      *
      * Klargjøre ny linje
      *
     c                   if        rtbiln <> axbiln
     c                   eval      wfresn = *zeros
     c                   endif
      *
     c                   eval      a5linj = a5linj + 1
     c                   eval      w_linj = a5linj
      *
     c                   exsr      xnulle_a5
      *
      * Neste postering
      *
     c                   goto      a7tagb
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2BLD Behandle forespørsel på transaksjoner                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   eval      a5pdif = a5pdeb - a5pkre
     c                   eval      a5bdif = a2bsum - a5pdeb
      *
     c                   exsr      xdsp01
      *
      * Gå tilbake
      *
6.22 c*    *inkc         cabeq     *on           a5taga
6.22 c                   if        *inkc = *on
6.22 c                   exsr      xnulle_a5
6.22 c                   goto      a5taga
6.22 c                   endif
      *
      * Forny
      *
     c                   if        *inke = *on
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1sfl
     c                   endif
      *
     c                   eval      rtral1_linj = b1linj - 1
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c     rtral1_key    setll     rtral1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      * Roll
      *
7.12 c*                  if        *in10 = *on
7.12 c                   if        *in10 = *on and
7.12 c                             b2linj = 0 and
7.12 c                             b2bilk = 0
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
7.12 c                   if        *in10 = *on and
7.12 c                             b2linj <> 0
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
7.12 c                   if        *in10 = *on and
7.12 c                             b2bilk <> 0
7.12 c                   exsr      xcrt0x
     c                   goto      b2tagb
7.12 c                   endif
      *
      * Posisjoner startverdi
      *
     c                   if        b2linj <> *zero
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_linj = b2linj
     c     rtral1_key    setll     rtral1
     c                   exsr      xclr01
     c                   exsr      xcrt01
7.12 c***                eval      b2linj = *zero
     c                   goto      b2tagb
     c                   endif
      *
7.11  *
7.11 c                   if        b2bilk <> *zero
7.11 c                   eval      rtralx_nivo = *blank
7.11 c                   eval      rtralx_memb = w_memb
7.11 c                   eval      rtralx_bunt = w_bunt
7.11 c                   eval      rtralx_bilk = b2bilk
7.11 c     rtralx_key    setll     rtralx
7.11 c                   exsr      xclr01
7.11 c                   exsr      xcrt0x
7.12 c***                eval      b2bilk = *zero
7.11 c                   goto      b2tagb
7.11 c                   endif
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg er foretatt i subfile
      *
     c                   do        ssrn01
     c                   readc     b1sfl                                  16
     c                   if        *in16 = *on
     c                   leave
     c                   endif
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   move      b1linj        a5linj
     c                   move      b1linj        w_linj
     c                   exsr      xc2win
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
      *
      * Hvis sist registrerte linje slettes, blank kvitteringsbildet
      *
     c                   if        b1linj = a6linj
     c                   exsr      xnulle_a6
     c                   endif
      *
     c                   move      b1linj        a5linj
     c                   move      b1linj        w_linj
     c                   exsr      xd1win
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   move      *on           *in30
     c                   move      b1linj        a5linj
     c                   move      b1linj        w_linj
     c                   exsr      yd1win
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   goto      b2tagb
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * U2WCT Behandle forespørsel på bunter                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     u2taga        tag
      *
     c                   eval      w_arow = 4
     c                   eval      w_acol = 35
     c                   write     u2cmd
      *
      * Bare data
      *
     c     u2tagb        tag
      *
     c                   exsr      xdsp02
      *
      * Gå tilbake
      *
     c     *inkc         cabeq     *on           a2taga
      *
      * Forny
      *
     c                   if        *inke = *on
     c                   if        sfrn02 <> *zero
     c     sfrn02        chain     b1sfl
     c                   endif
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = u1bunt
     c     rbunl1_key    setll     rbunl1
     c                   exsr      xclr02
     c                   exsr      xcrt02
     c                   goto      u2tagb
     c                   endif
      *
      * Roll
      *
7.12 c*                  if        *in10 = *on
7.12 c                   if        *in10 = *on and
7.12 c                             b2linj <> 0
     c                   exsr      xcrt02
     c                   goto      u2tagb
     c                   endif
7.12 c                   if        *in10 = *on and
7.12 c                             b2bilk <> 0
7.12 c                   exsr      xcrt0x
     c                   goto      b2tagb
7.12 c                   endif
      *
      * Posisjoner startverdi
      *
     c                   if        u2bunt <> *zero
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = u2bunt
     c     rbunl1_key    setll     rbunl1
     c                   exsr      xclr02
     c                   exsr      xcrt02
     c                   eval      u2bunt = *zero
     c                   goto      u2tagb
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg er foretatt i subfile
      *
     c                   do        ssrn02
     c                   readc     u1wsf                                  16
     c                   if        *in16 = *on
     c                   leave
     c                   endif
      *
      * Velge post
      *
     c                   if        u1valg = 1
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = u1bunt
     c     rbunl1_key    chain     rbunl1
     c                   move      rsstat        wsstat
      *
     c                   if        rsstat = 'U'
     c                   exsr      xsrbun
     c                   else
     c                   move      u1raar        a2raar
     c                   move      u1rper        a2rper
     c                   move      u1bunt        a2bunt
     c                   move      u1bunt        w_bunt
R5.50c                   move      u1bsum        a2bsum
     c                   eval      u1valg = 0
     c                   goto      a2tagb
     c                   endif
     c                   endif
      *
      * Slette bunt
      *
     c                   if        u1valg = 4
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = u1bunt
     c     rbunl1_key    chain     rbunl1
     c                   move      rsstat        wsstat
      *
     c                   if        rsstat = 'U'
     c                   move      *on           *in31
     c                   eval      u1valg = 0
     c                   iter
     c                   endif
      *
     c                   exsr      xsrbun
     c                   endif
      *
     c                   enddo
      *
     c                   goto      u2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for endring                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_bunt = w_bunt
     c                   eval      rtral1_linj = w_linj
     c     rtral1_key    chain     rtral1
      *
     c                   if        %found
     c                   movel     mld(2)        c2nyen
      *
      * Flytte felter fra file til skjerm
      *
     c                   exsr      xa5fts
      *
     c                   else
     c                   movel     mld(1)        c2nyen
      *
      * Clear format
      *
     c                   clear                   c2win
      *
     c                   endif
      *
     c                   eval      w_arow = 7
     c                   eval      w_acol = 2
     c                   write     s1win
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2win
     c                   exsr      xnul_in
     c                   exsr      xnul_fl
      *
     c     *inkc         cabeq     *on           c2tag1
     c     *in30         cabeq     *on           c2tag1
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spr_rut
     c                   move      *on           *in51
     c                   goto      c2tagb
     c                   endif
      *
      * Skriv etikett for Multisupport
      *
     c                   if        *inke = *on
     c                   call      'RF011C'
     c                   parm                    w_firm
     c                   parm                    rsraar
     c                   parm                    a5biln
      *
     c                   endif
      *
      * Kontroll
      *
     c                   exsr      chk_rut
      *
     c                   if        werror <> 0 or
     c                             w_feil <> 0
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdater buntsumpost
      *
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c     rbunl1_key    chain     rbunlur
      *
     c                   if        rtdkto <> *zero
      *
     c                   if        a5dkto <> *zero
     c                   eval      rspdeb = rspdeb - rtbelØ
     c                   eval      rspdeb = rspdeb + a5belØ
     c                   else
     c                   eval      rspdeb = rspdeb - rtbelØ
     c                   endif
      *
     c                   else
      *
     c                   if        a5dkto <> *zero
     c                   eval      rspdeb = rspdeb + a5belØ
     c                   endif
     c                   endif
      *
     c                   if        rtckto <> *zero
      *
     c                   if        a5ckto <> *zero
     c                   eval      rspkre = rspkre - rtbelØ
     c                   eval      rspkre = rspkre + a5belØ
     c                   else
     c                   eval      rspkre = rspkre - rtbelØ
     c                   endif
      *
     c                   else
      *
     c                   if        a5ckto <> *zero
     c                   eval      rspkre = rspkre + a5belØ
     c                   endif
     c                   endif
      *
     c                   if        %found
610  c                   time                    rsedat
610  c                   time                    rsetim
     c                   update    rbunlur
     c                   endif
      *
     c                   move      rspdeb        a5pdeb
     c                   move      rspkre        a5pkre
      *
      * Oppdater transaksjon
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c     rtral1_key    chain     rtralur
      *
      * Flytte felter fra skjerm til file
      *
     c                   exsr      xa5stf
      *
      * Først registrert som leverandører kredit (bildet)
      *
     c                   if        rtblde = '5'
     c                   if        wkrhkl = 'H'
     c                   move      rtckto        rtresn
     c                   move      rtcsps        rtress
     c                   move      rtcavd        rtresa
     c                   move      rtcmva        rtresm
     c                   endif
      *
     c                   if        wdbhkl = 'H'
     c                   move      rtdkto        rtresn
     c                   move      rtdsps        rtress
     c                   move      rtdavd        rtresa
     c                   move      rtdmva        rtresm
     c                   endif
     c                   endif
      *
      * Oppdater (endre)
      *
     c                   if        %found
610  c                   time                    rtedat
610  c                   time                    rtetim
     c                   update    rtralur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   move      a5bilk        b1bilk
     c                   move      a5bdat        b1bdat
     c                   move      a5biln        b1biln
     c                   move      a5dkto        b1dkto
     c                   move      a5dsps        b1dsps
     c                   move      a5davd        b1davd
     c                   move      a5dmva        b1dmva
     c                   move      a5belØ        b1belØ
     c                   move      a5ckto        b1ckto
     c                   move      a5csps        b1csps
     c                   move      a5cavd        b1cavd
     c                   move      a5cmva        b1cmva
     c                   endif
      *
     c     c2tag1        tag
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
     c                   move      srrn01        svrn01
     c                   eval      b1valg = *zeros
     c                   update    b1sfl
     c                   endif
      *
     c                   eval      b2linj = *zero
      *
      * Dersom dette er sist registrerte linje,
      * skal kvitteringsbildet ajourføres
      *
     c                   if        a5linj = a6linj
      *
     c                   move      a5dkto        a5dkto
     c                   move      a5dsps        a5dsps
     c                   move      a5davd        a5davd
     c                   move      a5dmva        a5dmva
     c                   move      a5ckto        a5ckto
     c                   move      a5csps        a5csps
     c                   move      a5cavd        a5cavd
     c                   move      a5cmva        a5cmva
      *
     c                   exsr      xa5sav
     c                   exsr      xkvitt
      *
     c                   eval      a5dkto = *zero
     c                   eval      a5dsps = *zero
     c                   eval      a5davd = *zero
     c                   eval      a5dmva = *blank
     c                   eval      a5ckto = *zero
     c                   eval      a5csps = *zero
     c                   eval      a5cavd = *zero
     c                   eval      a5cmva = *blank
6.20 c                   eval      a5pros = *blank
6.20 c                   eval      a5upro = *blank
6.20 c                   eval      a5arbo = *blank
6.20sc                   eval      a5akti = *blank
      *
     c                   endif
      *
      * Clear format
      *
     c                   clear                   c2win
      *
      * Sjekke eventuell blending av felter,
      * og sette indikatorer som styrer dette
      *
     c                   eval      *in01 = v1dsps = 'X'
     c                   eval      *in02 = v1davd = 'X'
     c                   eval      *in03 = v1dmva = 'X'
     c                   eval      *in07 = v1pros = 'X'
     c                   eval      *in08 = v1akti = 'X'
     c                   eval      *in09 = v1krys = 'X'
     c                   eval      *in20 = v1text = 'X'
     c                   eval      *in21 = v1fdat = 'X'
     c                   eval      *in22 = v1krab = 'X'
     c                   eval      *in23 = v1valk = 'X'
     c                   eval      *in24 = v1valb = 'X'
     c                   eval      *in25 = v1refn = 'X'
     c                   eval      *in27 = v1kidi = 'X'
     c                   eval      *in28 = v1atte = 'X'
     c                   eval      *in29 = v1mngd = 'X'
     c                   eval      *in06 = v1virk = 'X'
      *
     c                   endsr
      *
      /eject
      *
      * Subrutine for forespørsel
      *
     c     spr_rut       begsr
      *
T5.50 * Ind. 69 brukes til å hindre at cursor hopper tilbake
T5.50 * til et felt med C etter F4.
T5.50c                   move      *on           *in69
      *
T5.50c                   exsr      xnul_f4
      *
      * Call program for bilagskoder
      *
     c                   if        w_afld = 'A5BILK'
     c                   call      'RA501R'
     c                   parm                    w_firm
     c                   parm                    p_akod
     c                   parm                    p_tx20
      *
     c                   move      p_akod        a5bilk
     c                   move      *on           *in31
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for debetkonto
      *
     c                   if        w_afld = 'A5DKTO'
T5.50c                   eval      *in34 = *on
      *
      * Vindu for valg kontotype
      *
     c                   exsr      xx1win
     c     x1valg        cabeq     *blank        end_spr_rut
      *
      * Call kontoplan
      *
     c                   if        x1valg = '1'
      *
     c                   call      'RH500R'
     c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_tx30
      *
     c                   eval      a5dkto = p_kont
      *
     C                   if        p_spes <> *zeros
     c                   eval      a5dsps = p_spes
     C                   endif
      *
     C                   if        p_avde <> *zeros
     c                   eval      a5davd = p_avde
     C                   endif
      *
     c                   move      *on           *in34
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call kunder
      *
     c                   if        x1valg = '2'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_tx30
      *
     c                   move      p_kund        a5dkto
     c                   move      *on           *in34
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call leverandører
      *
     c                   if        x1valg = '3'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    p_levr
     c                   parm                    p_tx30
      *
     c                   move      p_levr        a5dkto
     c                   move      *on           *in34
     c                   goto      end_spr_rut
     c                   endif
      *
     c                   endif
      *
      * Call program for mvakoder
      *
     c                   if        w_afld = 'A5DMVA'
     c                   call      'RA505R'
     c                   parm                    w_firm
     c                   parm                    p_ekod
     c                   parm                    p_tx30
      *
     c                   move      p_ekod        a5dmva
     c                   move      *on           *in37
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kreditkonto
      *
     c                   if        w_afld = 'A5CKTO'
T5.50c                   eval      *in39 = *on
      *
      * Vindu for valg av kontotype
      *
     c                   exsr      xx1win
     c     x1valg        cabeq     *blank        end_spr_rut
      *
      * Call kontoplan
      *
     c                   if        x1valg = '1'
      *
     c                   call      'RH500R'
     c                   parm                    w_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_tx30
      *
     c                   eval      a5ckto = p_kont
      *
     C                   if        p_spes <> *zeros
     c                   eval      a5csps = p_spes
     C                   endif
      *
     C                   if        p_avde <> *zeros
     c                   eval      a5cavd = p_avde
     C                   endif
      *
     c                   move      *on           *in39
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call kunder
      *
     c                   if        x1valg = '2'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_tx30
      *
     c                   move      p_kund        a5ckto
     c                   move      *on           *in39
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call leverandører
      *
     c                   if        x1valg = '3'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    p_levr
     c                   parm                    p_tx30
      *
     c                   move      p_levr        a5ckto
     c                   move      *on           *in39
     c                   goto      end_spr_rut
     c                   endif
      *
     c                   endif
      *
      * Call program for debet avdeling
      *
     c                   if        w_afld = 'A5DAVD'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   move      p_gkod        a5davd
     c                   move      *on           *in36
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kredit avdeling
      *
     c                   if        w_afld = 'A5CAVD'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   move      p_gkod        a5cavd
     c                   move      *on           *in41
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for debet kostnadsbærer
      *
     c                   if        w_afld = 'A5DSPS'
     c                   call      'RA528R'
     c                   parm                    w_firm
     c                   parm                    p_Økod
     c                   parm                    p_tx30
      *
     c                   move      p_Økod        a5dsps
     c                   move      *on           *in35
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kredit kostnadsbærer
      *
     c                   if        w_afld = 'A5CSPS'
     c                   call      'RA528R'
     c                   parm                    w_firm
     c                   parm                    p_Økod
     c                   parm                    p_tx30
      *
     c                   move      p_Økod        a5csps
     c                   move      *on           *in40
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kredit mvakoder
      *
     c                   if        w_afld = 'A5CMVA'
     c                   call      'RA505R'
     c                   parm                    w_firm
     c                   parm                    p_ekod
     c                   parm                    p_tx30
      *
     c                   move      p_ekod        a5cmva
     c                   move      *on           *in42
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for valutakoder
      *
     c                   if        w_afld = 'A5VALK'
     c                   call      'RA516R'
     c                   parm                    w_firm
     c                   parm                    p_pkod
     c                   parm                    p_tx30
      *
     c                   move      p_pkod        a5valk
     c                   move      *on           *in49
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for arb.ordre
      *
6.20 c                   if        w_afld = 'A5ARBO'
6.20 c                   call      'RP602R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_arbo
6.20 c                   parm                    p_tx30
6.20 c                   parm                    p_pros
6.20 c                   parm                    p_upro
6.20 c                   parm                    p_akti
6.20 c                   parm                    p_prt1
6.20  *
6.20 c                   move      p_pros        a5pros
6.20 c                   move      p_upro        a5upro
6.20 c                   move      p_arbo        a5arbo
6.20sc                   move      p_akti        a5akti
6.20 c                   move      *on           *in31
6.20 c                   goto      end_spr_rut
6.20 c                   endif
      *
      * Call program for prosjekt
      *
6.20 c                   if        w_afld = 'A5PROS'
6.20 c                   call      'RP600R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_pros
6.20 c                   parm                    p_tx30
6.20 c                   parm                    p_uprs
6.20 c                   if        p_pros <> *blank
6.20 c                   eval      a5pros = p_pros
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20  * Call program for underprosjekt
6.20  *
6.20 c                   if        w_afld = 'A5UPRO'
6.20 c                   call      'RP601R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_pros
6.20 c                   parm                    p_upro
6.20 c                   parm                    p_tx30
6.20 c                   parm                    p_txt2
6.20 c                   parm                    p_kund
6.20 c                   parm                    p_uprs
6.20 c*  p_uprs sier at dette prosjektet skal ha underprosjekt
6.20 c*                  if        p_uprs <> *blank
6.20 c                   eval      a5upro = p_upro
6.20 c*                  endif
6.20 c*                  if        a5pros = *blank
6.20 c                   eval      a5pros = p_pros
6.20 c*                  endif
6.20 c                   endif
      *
      * Call program for aktivitet
      *
6.20 c**                 if        w_afld = 'A5AKTI'
6.20 c**                 call      'RA521R'
6.20 c**                 parm                    w_firm
6.20 c**                 parm                    p_ukod
6.20 c**                 parm                    p_tx30
6.20  *
6.20 c**                 move      p_ukod        a5akti
6.20 c**                 move      *on           *in44
6.20 c**                 goto      end_spr_rut
6.20 c**                 endif
      *
      * Call program for referansenummer
      *
     c                   if        w_afld = 'A5REFN'
      *
     c                   move      '2'           p_valg
     c                   move      a5dkto        p_refk
      *
      * Sjekke om debetkonto er kunde
      *
     c                   if        a5dkto > ra1hkt and
     c                             a5dkto < ra1hrs
      *
     c                   call      'RK512R'
     c                   parm                    p_valg
     c                   parm                    p_refk
     c                   parm                    p_biln
     c                   parm                    p_rest
      *
     c                   move      p_biln        a5refn
      *
     c                   if        a5belØ = *zeros
     c                   if        p_rest < *zeros
     c                   eval      a5belØ = 0 - p_rest
     c                   else
     c                   move      p_rest        a5belØ
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Sjekke om debetkonto er leverandør
      *
     c                   if        a5dkto > ra1hrs
      *
     c                   call      'RL512R'
     c                   parm                    p_valg
     c                   parm                    p_refk
     c                   parm                    p_biln
     c                   parm                    p_rest
      *
     c                   move      p_biln        a5refn
      *
     c                   if        a5belØ = *zeros
     c                   if        p_rest < *zeros
     c                   eval      a5belØ = 0 - p_rest
     c                   else
     c                   move      p_rest        a5belØ
     c                   endif
     c                   endif
     c                   endif
      *
     c                   move      a5ckto        p_refk
      *
      * Sjekke om kreditkonto er kunde
      *
     c                   if        a5ckto > ra1hkt and
     c                             a5ckto < ra1hrs
      *
     c                   call      'RK512R'
     c                   parm                    p_valg
     c                   parm                    p_refk
     c                   parm                    p_biln
     c                   parm                    p_rest
      *
     c                   move      p_biln        a5refn
      *
     c                   if        a5belØ = *zeros
     c                   if        p_rest < *zeros
     c                   eval      a5belØ = 0 - p_rest
     c                   else
     c                   move      p_rest        a5belØ
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Sjekke om kreditkonto er leverandør
      *
     c                   if        a5ckto > ra1hrs
      *
     c                   call      'RL512R'
     c                   parm                    p_valg
     c                   parm                    p_refk
     c                   parm                    p_biln
     c                   parm                    p_rest
      *
     c                   move      p_biln        a5refn
      *
     c                   if        a5belØ = *zeros
     c                   if        p_rest < *zeros
     c                   eval      a5belØ = 0 - p_rest
     c                   else
     c                   move      p_rest        a5belØ
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c     end_spr_rut   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for kontroll av posteringer                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     chk_rut       begsr
      *
      * Sjekke om bilagskode er gyldig
      *
     c                   move      a5bilk        p_akod
     c                   call      'RS701R'
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in31
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   else
     c                   movel     p_tx20        w_btxt
     c                   endif
      *
      * Sjekke om bilagsdato er gyldig
      *
     c                   if        a5bdat = *zero
     c                   move      udate         a5bdat
     c                   endif
      *
     c     *dmy          test(d)                 a5bdat                 32
     c                   if        *in32 = *on
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
      *
      * Ta vare på bilagsdato(ÅÅÅÅMMDD) for senere test
      *
     c     *dmy          move      a5bdat        w_pdat
6.24  *
6.24  * Sjekk at bilagsdato er maks 1 år tilbake i tid
6.24  *
6.28 c*                  time                    w_dato
6.28 c*    w_dato        subdur    1 : *y        w_dato
6.24  *
6.28 c*                  if        w_pdat < w_dato
6.28 c*                  move      *on           *in95
6.28 c*                  move      *on           *in32
6.28 c*                  eval      werror = 1
6.24 c*                  goto      end_chk_rut
6.24 c*                  endif
6.24  * Sjekk at bilagsdato er maks 1 år fram i tid
6.24  *
6.28 c*                  time                    w_dato
6.28 c*    w_dato        adddur    1 : *y        w_dato
6.28 c*                  if        w_pdat > w_dato
6.28 c*                  move      *on           *in96
6.28 c*                  move      *on           *in32
6.28 c*                  eval      werror = 1
6.28 c*                  goto      end_chk_rut
6.28 c*                  endif
      *
6.24  *
      * Varsle dersom bilagsnummer mangler
      *
     c                   if        a5biln = *zero
     c                   move      *on           *in33
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
      *
      * Varsle dersom bilagsnummer brukt før dette år
      *
     c                   if        ra1bnr = 'J' and
     c                             wltrok <> '1' and
     c                             a5biln <> *zero
      *
6.23  *
      * Sjekk hovedboksposter
      *
     c                   eval      rhtrl4_biln = a5biln
     c                   eval      rhtrl4_raar = a2raar
     c     rhtrl4_key    chain     rhtrl4
     c                   if        %found(rhtrl4)
     c                   move      *on           *in63
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
      *
      * Sjekk kundeposter
      *
     c                   if        not%found(rktrlb)
     c                   eval      rktrlb_raar = a2raar
     c                   eval      rktrlb_biln = a5biln
     c     rktrlb_key    chain     rktrlb
     c                   if        %found(rktrlb)
     c                   move      *on           *in63
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
      *
      * Sjekk leverandørposter
      *
     c                   if        not%found(rhtrl4)
     c                   eval      rltrlc_raar = a2raar
     c                   eval      rltrlc_biln = a5biln
     c     rltrlc_key    chain     rltrlc
     c                   if        %found(rltrlc)
     c                   move      *on           *in63
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Varsle dersom leverandørens fakturanr. brukt før dette år
      *
     c                   if        a5ckto > ra1hrs
     c                   eval      rltrl7_levr = a5ckto
6.23  *
6.23  * Sjekker om levnr/lev.faktnr finnes fra før
6.23 c                   if        a5lfak <> *blank
6.23  *
6.23 c                   eval      rltrl7_raar = a2raar
6.23 c                   eval      rltrl7_lfak = a5lfak
6.23 c     rltrl7_key    chain     rltrl7
6.23  *
6.23 c                   if        %found
6.23 c                   move      *on           *in52
6.23 c                   move      *on           *in81
6.23 c                   eval      werror = 1
6.23 c                   goto      end_chk_rut
6.23 c                   endif
6.23 c                   endif
6.23  *
     c                   endif
      *
     c                   if        a5dkto > ra1hrs
     c                   eval      rltrl7_levr = a5dkto
6.23 c**                 if        a5lfak <> *blank
6.23 c                   if        a5lfak <> *blank and
6.23 c                             *in52 = *off and
6.23 c                             *in81 = *off
      *
     c                   eval      rltrl7_raar = a2raar
     c                   eval      rltrl7_lfak = a5lfak
     c     rltrl7_key    chain     rltrl7
      *
     c                   if        %found
     c                   move      *on           *in52
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
     c                   endif
      *
      *
      * Begge kontonummer kan ikke være blanke
      *
     c                   if        a5dkto = *zero and
     c                             a5ckto = *zero
     c                   eval      w_feil = 1
     c                   move      *on           *in34
     c                   move      *on           *in84
     c                   goto      end_chk_rut
     c                   endif
      *
      * Begge kontonummer kan ikke være reskontrokonti
      *
6.11 c***                if        a5dkto > ra1hkt and
6.11 c***                          a5ckto > ra1hkt
6.11 c***                eval      w_feil = 1
6.11 c***                move      *on           *in34
6.11 c***                move      *on           *in93
6.11 c***                goto      end_chk_rut
6.11 c***                endif
      *
      * Sjekke debetkonto dersom oppgitt
      *
     c                   if        a5dkto <> *zero or
     c                             a5davd <> *zero or
     c                             a5dsps <> *zero
      *
     c                   eval      w_kont = a5dkto
     c                   eval      w_avde = a5davd
     c                   eval      w_spes = a5dsps
     c                   eval      w_mvak = a5dmva
      *
     c                   exsr      chk_kont
      *
     c                   eval      w_dtxt = w_text
      *
T5.40c                   if        w_ktyp = 'L'
T5.40c                   eval      w_dgir = w_bgir
T5.40c                   else
T5.40c                   eval      w_dgir = *zero
T5.40c                   endif
      *
     c                   eval      wdbhkl = w_ktyp
     c                   eval      wsdprk = w_proj
      *
     c                   eval      rtblde = ' '
      *
     c                   if        w_valk <> ' '
     c                   eval      wsdval = 'J'
     c                   endif
      *
     c                   eval      a5dmva = w_mvak
      *
      * Pos cursor til konto/avd/spes ved feil
      *
     c                   select
     c                   when      w_feil = 1
     c                   move      *on           *in34
     c                   when      w_feil = 2
     c                   move      *on           *in36
     c                   when      w_feil = 3
     c                   move      *on           *in35
     c                   endsl
      *
     c                   if        werror <> 0 or
     c                             w_feil <> 0
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
      *
      * Sjekke kreditkonto dersom oppgitt
      *
     c                   if        a5ckto <> *zero or
     c                             a5cavd <> *zero or
     c                             a5csps <> *zero
      *
     c                   eval      w_kont = a5ckto
     c                   eval      w_avde = a5cavd
     c                   eval      w_spes = a5csps
     c                   eval      w_mvak = a5cmva
      *
     c                   exsr      chk_kont
      *
     c                   eval      w_ktxt = w_text
      *
T5.40c                   if        w_ktyp = 'L'
T5.40c                   eval      w_cgir = w_bgir
T5.40c                   else
T5.40c                   eval      w_cgir = *zero
T5.40c                   endif
      *
     c                   eval      wkrhkl = w_ktyp
     c                   eval      wscprk = w_proj
      *
     c                   eval      rtblde = ' '
     c                   if        w_ktyp = 'L'
     c                   eval      rtblde = '5'
     c                   endif
      *
     c                   if        w_valk <> ' '
     c                   eval      wscval = 'J'
     c                   endif
      *
     c                   eval      a5cmva = w_mvak
      *
      * Pos cursor til konto/avd/spes ved feil
      *
     c                   select
     c                   when      w_feil = 1
     c                   move      *on           *in39
     c                   when      w_feil = 2
     c                   move      *on           *in41
     c                   when      w_feil = 3
     c                   move      *on           *in40
     c                   endsl
      *
     c                   if        w_feil <> 0
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
      *
      * Avkryssing forskjellig fra X, S eller ' ',  gir X
      *
     c                   if        a5krys <> 'X' and
     c                             a5krys <> 'S' and
     c                             a5krys <> ' '
     c                   move      'X'           a5krys
     c                   endif
      *
      * Avkryssing = X og referansenummer finnes
      *
     c                   if        a5krys = 'X' and
     c                             a5refn <> *zero
     c                   eval      werror = 1
     c                   move      *on           *in51
     c                   move      *on           *in82
     c                   goto      end_chk_rut
     c                   endif
      *
      * Avkryssing = S og referansenummer finnes ikke
      *
     c                   if        a5krys = 'S' and
     c                             a5refn = *zero
     c                   eval      werror = 1
     c                   move      *on           *in51
     c                   move      *on           *in83
     c                   goto      end_chk_rut
     c                   endif
      *
      * Attestasjonskode forskjellig fra 1 eller 0,  gir 1
      *
     c                   if        a5atte <> '1' and
     c                             a5atte <> ' '
     c                   move      1             a5atte
     c                   endif
      *
      * Det kan ikke være null i både beløp og valutabeløp
      *
     c                   if        a5belØ = 0 and
     c                             a5valb = 0
     c                   move      *on           *in38
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
      *
      * Beløp kan ikke være negativt
      *
     c                   if        a5belØ < 0
     c                   move      *on           *in38
     c                   move      *on           *in92
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
      *
      * Sjekke om forfallsdato er gyldig dersom oppgitt
      *
     c                   if        a5fdat > *zero
      *
     c     *dmy          test(d)                 a5fdat                 47
     c                   if        *in47 = *on
     c                   move      *on           *in81
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
      *
     c     *dmy          move      a5fdat        w_fdat
     c     *dmy          move      a5bdat        w_bdat
     c                   if        w_bdat > w_fdat
     c                   move      *on           *in47
     c                   move      *on           *in82
     c                   eval      werror = 1
     c                   goto      end_chk_rut
     c                   endif
      *
     c                   endif
      *
      * Kontroll av virksomhet mot koderegister
      *
     c                   if        a5virk <> *blank
     c                   move      a5virk        p_wkod
      *
     c                   call      'RS723R'
     c                   parm                    p_stat
     c                   parm                    p_wkod
     c                   parm                    p_tx30
      *
     c                   if        p_stat = 'N'
     c                   eval      werror = 1
     c                   move      *on           *in56
     c                   move      *on           *in81
     c                   goto      end_chk_rut
     c                   endif
      *
     c                   endif
      *
      * Kontroll av KID-felt
      *
     c                   if        a5kidi <> *blank
      * Melding er registrert?!?
     c                   movea     a5kidi        tegn
     c     1             do        25            n
     c                   if        tegn(n) <> ' '
     c                   if        tegn(n) < '0' or
     c                             tegn(n) > '9'
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
     c                   enddo
      *
T5.30 * Dersom KID-feltet er utfylt med KID, så blir det først
T5.30 * høyrejustert, og deretter modulustestet.
      *
T5.30c                   movea     a5kidi        kid
T5.30c                   eval      v = 25
T5.30c                   eval      w = 25
T5.30c                   eval      nyki = *blanks
      *
T5.30c                   dow       v >= 1
      *
T5.30c                   if        kid(v) >= '0' and
T5.30c                             kid(v) <= '9' or
T5.30c                             kid(v) = '-'
T5.30c                   move      kid(v)        nyki(w)
T5.30c                   eval      w = w - 1
T5.30c                   endif
      *
T5.30c                   eval      v = v - 1
T5.30c                   enddo
      *
T5.30c                   movea     nyki          w_kidi
T5.30c                   move      w_kidi        w_sist
T5.30c                   movel     w_kidi        w_ki24
T5.30c                   movea     w_ki24        tall
T5.30c                   eval      p_tall = w_kidi
T5.30c                   eval      p_rkod = *off
      *
T5.30c                   exsr      xmod10
T5.30c     xno1          tag
      *
T5.30c                   if        p_rkod = *on and
T5.30c                             ktr <> w_sist
     c                   eval      werror = 1
     c                   move      *on           *in53
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
      *
      * Kontroll av prosjekt
      *
     c                   if        wsdprk <> ' ' or
     c                             wscprk <> ' '
      *
6.20 c                   exsr      chk_proj
      *
     c                   if        werror <> 0
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
      *
      * Kontroll av valuta
      *
     c                   if        wsdval <> ' ' or
     c                             wscval <> ' ' or
     c                             a5valk <> ' '
      *
     c                   exsr      chk_valu
      *
     c                   if        werror <> 0
     c                   goto      end_chk_rut
     c                   endif
     c                   endif
      *
     c     end_chk_rut   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1WIN Behandle bilde for slette                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xd1win        begsr
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_bunt = w_bunt
     c                   eval      rtral1_linj = w_linj
     c     rtral1_key    chain     rtral1
      *
     c                   move      mld(4)        d1nyen
     c                   eval      w_arow = 7
     c                   eval      w_acol = 2
     c                   write     s1win
      *
     c     *dmy          move      rtbdat        d1bdat
      *
     c                   if        rtfdat = *loval
     c                   eval      d1fdat = 0
     c                   else
     c     *dmy          move      rtfdat        d1fdat
     c                   endif
      *
     c                   exfmt     d1win
      *
     c     *inkc         cabeq     *on           d1tag1
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c     rtral1_key    chain     rtralur
      *
     c                   if        %found
      *
      * Oppdater buntsumpost
      *
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c     rbunl1_key    chain     rbunlur
      *
     c                   if        rtdkto <> *zero
     c                   eval      rspdeb = rspdeb - rtbelØ
     c                   endif
      *
     c                   if        rtckto <> *zero
     c                   eval      rspkre = rspkre - rtbelØ
     c                   endif
      *
     c                   if        %found
610  c                   time                    rsedat
610  c                   time                    rsetim
     c                   update    rbunlur
     c                   endif
      *
     c                   move      rspdeb        a5pdeb
     c                   move      rspkre        a5pkre
      *
     c                   delete    rtralur
     c                   endif
      *
     c     d1tag1        tag
      *
     c                   if        b1valg = 4
     c                   eval      b1valg = *zeros
     c                   eval      b1bilk = 0
     c                   eval      b1bdat = 0
     c                   eval      b1biln = 0
     c                   eval      b1davd = 0
     c                   eval      b1dkto = 0
     c                   eval      b1dsps = 0
     c                   eval      b1dmva = ' '
     c                   eval      b1belØ = 0
     c                   eval      b1cavd = 0
     c                   eval      b1ckto = 0
     c                   eval      b1csps = 0
     c                   eval      b1cmva = ' '
     c                   update    b1sfl
     c                   endif
      *
     c                   eval      b2linj = *zero
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Y1WIN Behandle bilde for vise                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     yd1win        begsr
      *
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memb
     c                   eval      rtral1_bunt = w_bunt
     c                   eval      rtral1_linj = w_linj
     c     rtral1_key    chain     rtral1
      *
     c                   move      mld(3)        d1nyen
     c                   eval      w_arow = 7
     c                   eval      w_acol = 2
     c                   write     s1win
      *
     c     *dmy          move      rtbdat        d1bdat
      *
     c                   if        rtfdat <> *loval
     c     *dmy          move      rtfdat        d1fdat
     c                   else
     c                   eval      d1fdat = 0
     c                   endif
      *
     c                   exfmt     d1win
      *
      * Oppdater subfile
      *
     c                   eval      b1valg = *zeros
     c                   update    b1sfl
      *
     c                   eval      b2linj = *zero
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * M1WIN Behandle bilde for melding med svar                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xm1win        begsr
      *
     c                   eval      wsavli = wsavli + 2
     c                   eval      wsavpo = wsavpo - 4
7.03 c                   if        wsavli < 1
7.03 c                   eval      wsavli = 5
7.03 c                   endif
7.03 c                   if        wsavpo < 1
7.03 c                   eval      wsavpo = 5
7.03 c                   endif
     c                   eval      w_arow = wsavli
     c                   eval      w_acol = wsavpo
      *
     c                   exfmt     m1win
      *
     c                   if        *inkc = *on
     c                   move      'N'           m1svar
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * V1WIN Behandle bilde for redigeringsvalg                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xv1win        begsr
      *
     c                   eval      w_arow = 7
     c                   eval      w_acol = 2
      *
     c                   write     s1win
     c                   exfmt     v1win
      *
     c     *inkc         cabeq     *on           v1tag1
      *
     c                   eval      *in01 = v1dsps = 'X'
     c                   eval      *in02 = v1davd = 'X'
     c                   eval      *in03 = v1dmva = 'X'
     c                   eval      *in07 = v1pros = 'X'
     c                   eval      *in08 = v1akti = 'X'
     c                   eval      *in09 = v1krys = 'X'
     c                   eval      *in20 = v1text = 'X'
     c                   eval      *in21 = v1fdat = 'X'
     c                   eval      *in22 = v1krab = 'X'
     c                   eval      *in23 = v1valk = 'X'
     c                   eval      *in24 = v1valb = 'X'
     c                   eval      *in25 = v1refn = 'X'
     c                   eval      *in26 = v1lfak = 'X'
     c                   eval      *in27 = v1kidi = 'X'
     c                   eval      *in28 = v1atte = 'X'
     c                   eval      *in29 = v1mngd = 'X'
     c                   eval      *in06 = v1virk = 'X'
      *
     c     v1tag1        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * X1WIN Behandle bilde for valg av kontotype (Hov Kun Lev)        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xx1win        begsr
      *
     c                   eval      x1valg = *blank
     c                   eval      w_arow = 6
     c                   eval      w_acol = 34
     c                   write     x1win
      *
6.10  * Finner spørrebilde for hovedbok, leverandør eller kunde dersom
6.10  * bilagskode ikke er 0
6.10 c                   if        a5bilk <> *zeros
6.10 c                   eval      ra01l1_akod = a5bilk
6.10 c     ra01l1_key    chain     ra01l1r
6.10 c                   if        %found
6.10 c* Sjekk debetkonto
6.10 c                   if        w_afld = 'A5DKTO'
6.10 c                   if        radebe = 'H'
6.10 c                   eval      *inke = '1'
6.10 c                   goto      dd001
6.10 c                   end
6.10 c                   if        radebe = 'K'
6.10 c                   eval      *inkf = '1'
6.10 c                   goto      dd001
6.10 c                   end
6.10 c                   if        radebe = 'L'
6.10 c                   eval      *inkg = '1'
6.10 c                   goto      dd001
6.10 c                   end
6.10 c                   end
6.10  *Sjekk kreditkonto
6.10 c                   if        w_afld = 'A5CKTO'
6.10 c                   if        rakred = 'H'
6.10 c                   eval      *inke = '1'
6.10 c                   goto      dd001
6.10 c                   end
6.10 c                   if        rakred = 'K'
6.10 c                   eval      *inkf = '1'
6.10 c                   goto      dd001
6.10 c                   end
6.10 c                   if        rakred = 'L'
6.10 c                   eval      *inkg = '1'
6.10 c                   goto      dd001
6.10 c                   end
6.10 c                   end
6.10  *
6.10 c                   end
6.10 c                   endif
6.10  *
     c                   exfmt     x1win
6.10  *
6.10 c     dd001         tag
      *
      * Valg av kontotype
      *
     c                   select
     c                   when      x1vlg1 = 1 or
     c                             *inke = *on
     c                   move      '1'           x1valg
     c                   when      x1vlg2 = 1  or
     c                             *inkf = *on
     c                   move      '2'           x1valg
     c                   when      x1vlg3 = 1  or
     c                             *inkg = *on
     c                   move      '3'           x1valg
     c                   endsl
      *
     c                   eval      x1vlg1 = 0
     c                   eval      x1vlg2 = 0
     c                   eval      x1vlg3 = 0
      *
     c                   endsr
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Duplisering av inputfelter i A5BLD, når DUP-tast er benyttet    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xduppe        begsr
      *
     c                   eval      *in17 = v1biko =  'D'
     c                   if        *in17 = *on
     c                   move      axbilk        a5bilk
     c                   endif
      *
     c                   eval      *in18 = v1bida =  'D'
     c                   if        *in18 = *on
     c                   move      axbdat        a5bdat
     c                   endif
      *
     c                   eval      *in19 = v1binr =  'D'
     c                   if        *in19 = *on
     c                   move      axbiln        a5biln
     c                   endif
      *
     c                   eval      *in58 = v1dkto =  'D'
     c                   if        *in58 = *on
     c                   move      axdkto        a5dkto
6.26 c                   eval      *in58 = *off
     c                   endif
      *
     c                   eval      *in59 = v1dsps =  'D'
     c                   if        *in59 = *on
     c                   move      axdsps        a5dsps
     c                   endif
      *
     c                   eval      *in70 = v1davd =  'D'
     c                   if        *in70 = *on
     c                   move      axdavd        a5davd
     c                   endif
      *
     c                   eval      *in71 = v1dmva =  'D'
     c                   if        *in71 = *on
     c                   move      axdmva        a5dmva
     c                   endif
      *
     c                   eval      *in72 = v1belp =  'D'
     c                   if        *in72 = *on
     c                   move      axbelØ        a5belØ
     c                   endif
      *
     c                   eval      *in73 = v1dkto =  'D'
     c                   if        *in73 = *on
     c                   move      axckto        a5ckto
     c                   endif
      *
     c                   eval      *in74 = v1dsps =  'D'
     c                   if        *in74 = *on
     c                   move      axcsps        a5csps
     c                   endif
      *
     c                   eval      *in75 = v1davd =  'D'
     c                   if        *in75 = *on
     c                   move      axcavd        a5cavd
     c                   endif
      *
     c                   eval      *in76 = v1dmva =  'D'
     c                   if        *in76 = *on
     c                   move      axcmva        a5cmva
     c                   endif
      *
     c                   eval      *in77 = v1pros =  'D'
6.20 c                   if        *in77 = *on
6.20 c                   move      axpros        a5pros
6.20 c                   move      axupro        a5upro
6.20 c                   move      axarbo        a5arbo
6.20sc                   move      axakti        a5akti
6.20 c                   endif
      *
     c                   eval      *in78 = v1akti =  'D'
6.20 c**                 if        *in78 = *on
6.20 c**                 move      axakti        a5akti
6.20 c**                 endif
      *
     c                   eval      *in79 = v1krys =  'D'
     c                   if        *in79 = *on
     c                   move      axkrys        a5krys
     c                   endif
      *
     c                   eval      *in80 = v1text =  'D'
     c                   if        *in80 = *on
     c                   move      axtext        a5text
     c                   endif
      *
     c                   eval      *in81 = v1fdat =  'D'
     c                   if        *in81 = *on
     c                   move      axfdat        a5fdat
     c                   endif
      *
     c                   eval      *in82 = v1krab =  'D'
     c                   if        *in82 = *on
     c                   move      axkrab        a5krab
     c                   endif
      *
     c                   eval      *in83 = v1valk =  'D'
     c                   if        *in83 = *on
     c                   move      axvalk        a5valk
     c                   endif
      *
     c                   eval      *in84 = v1valb =  'D'
     c                   if        *in84 = *on
     c                   move      axvalb        a5valb
     c                   endif
      *
     c                   eval      *in85 = v1refn =  'D'
     c                   if        *in85 = *on
     c                   move      axrefn        a5refn
6.26 c                   eval      *in85 = *off
     c                   endif
      *
     c                   eval      *in86 = v1lfak =  'D'
     c                   if        *in86 = *on
     c                   move      axlfak        a5lfak
     c                   endif
      *
     c                   eval      *in87 = v1kidi =  'D'
     c                   if        *in87 = *on
     c                   move      axkidi        a5kidi
     c                   endif
      *
     c                   eval      *in88 = v1atte =  'D'
     c                   if        *in88 = *on
     c                   move      axatte        a5atte
     c                   endif
      *
     c                   eval      *in89 = v1mngd =  'D'
     c                   if        *in89 = *on
     c                   move      axmngd        a5mngd
     c                   endif
      *
     c                   eval      *in90 = v1virk =  'D'
     c                   if        *in90 = *on
     c                   move      axvirk        a5virk
     c                   endif
      *
      * Adder 1 til bilagsnummer hvis saldo D/K = 0
      *
8.01  * Kan ikke gjøres hvis ikke bilagsnummer er 99999999
      *
8.01 c                   if        axbiln < 99999999
     c                   if        v1binr = 'A' and
     c                             a5pdif = 0
     c                   eval      a5biln = axbiln + 1
     c                   else
     c                   eval      a5biln = axbiln
     c                   endif
8.01 c                   else
8.01 c                   eval      a5biln = axbiln
8.01 c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kvittere opp felter ved registrering fra A5 til A6-felter       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xkvitt        begsr
      *
      * Legge opp felter til kvitteringsbildet
      *
     c                   movel     w_dtxt        a61txt
     c                   movel     w_ktxt        a62txt
     c                   movel     w_btxt        a6btxt
     c                   movel     w_ptxt        a6ptxt
T5.40c                   move      w_dgir        a6dgir
T5.40c                   move      w_cgir        a6cgir
     c                   move      a5linj        a6linj
     c                   move      a5bilk        a6bilk
     c                   move      a5bdat        a6bdat
     c                   move      a5biln        a6biln
     c                   move      a5dkto        a6dkto
     c                   move      a5dsps        a6dsps
     c                   move      a5davd        a6davd
     c                   move      a5dmva        a6dmva
     c                   move      a5belØ        a6belØ
     c                   move      a5ckto        a6ckto
     c                   move      a5csps        a6csps
     c                   move      a5cavd        a6cavd
     c                   move      a5cmva        a6cmva
6.20 c**                 move      a5pros        a6pros
6.20 c                   move      a5arbo        a6pros
6.20 c**                 move      a5akti        a6akti
      *
      * Kvittere mvakode i skjermbildet
      *
     c                   if        a5dmva = ' '
     c                   move      wsdmva        a6dmva
     c                   endif
      *
     c                   if        a5cmva = ' '
     c                   move      wscmva        a6cmva
     c                   endif
      *
     c                   eval      w_dtxt = ' '
     c                   eval      w_ktxt = ' '
     c                   eval      w_btxt = ' '
     c                   eval      w_ptxt = ' '
T5.40c                   eval      w_dgir = 0
T5.40c                   eval      w_cgir = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * 0-Stilling av arbeidsindikatorer og arbeidsfelt                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnul_in       begsr
      *
     c                   eval      *in04 = *off
     c                   eval      *in05 = *off
     c                   eval      *in11 = *off
     c                   eval      *in17 = *off
     c                   eval      *in18 = *off
     c                   eval      *in19 = *off
T5.40c                   eval      *in30 = *off
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
6.27 c                   eval      *in97 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
     c                   eval      *in38 = *off
     c                   eval      *in39 = *off
     c                   eval      *in40 = *off
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
     c                   eval      *in46 = *off
     c                   eval      *in47 = *off
     c                   eval      *in48 = *off
     c                   eval      *in49 = *off
     c                   eval      *in50 = *off
     c                   eval      *in51 = *off
     c                   eval      *in52 = *off
     c                   eval      *in53 = *off
     c                   eval      *in54 = *off
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *off
6.21 c                   eval      *in63 = *off
     c                   eval      *in64 = *off
T5.50c                   eval      *in69 = *off
      *
     c                   eval      *in70 = *off
     c                   eval      *in71 = *off
     c                   eval      *in72 = *off
     c                   eval      *in73 = *off
     c                   eval      *in74 = *off
     c                   eval      *in75 = *off
     c                   eval      *in76 = *off
     c                   eval      *in77 = *off
     c                   eval      *in78 = *off
     c                   eval      *in79 = *off
     c                   eval      *in80 = *off
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in83 = *off
     c                   eval      *in84 = *off
     c                   eval      *in85 = *off
     c                   eval      *in86 = *off
     c                   eval      *in87 = *off
     c                   eval      *in88 = *off
     c                   eval      *in90 = *off
     c                   eval      *in92 = *off
     c                   eval      *in93 = *off
T5.50c                   eval      *in94 = *off
6.28 c*                  eval      *in95 = *off
6.28 c*                  eval      *in96 = *off
      *
T5.40 * Posisjoner cursor
      *
     c                   eval      *in31 = v1biko = 'C'
     c                   eval      *in32 = v1bida = 'C'
     c                   eval      *in33 = v1binr = 'C'
     c                   eval      *in34 = v1dkto = 'C'
     c                   eval      *in35 = v1dsps = 'C'
     c                   eval      *in36 = v1davd = 'C'
     c                   eval      *in38 = v1belp = 'C'
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * 0-Stilling av arbeidsindikatorer for cursor-plassering          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.50c     xnul_cur      begsr
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
6.27 c                   eval      *in97 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in38 = *off
      *
      * Posisjoner cursor
      *
     c                   eval      *in31 = v1biko = 'C'
     c                   eval      *in32 = v1bida = 'C'
     c                   eval      *in33 = v1binr = 'C'
     c                   eval      *in34 = v1dkto = 'C'
     c                   eval      *in35 = v1dsps = 'C'
     c                   eval      *in36 = v1davd = 'C'
     c                   eval      *in38 = v1belp = 'C'
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * 0-Stilling av arbeidsfelt                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnul_fl       begsr
      *
      * Posisjoner cursor
      *
     c                   eval      *in31 = v1biko = 'C'
     c                   eval      *in32 = v1bida = 'C'
     c                   eval      *in33 = v1binr = 'C'
     c                   eval      *in34 = v1dkto = 'C'
     c                   eval      *in35 = v1dsps = 'C'
     c                   eval      *in36 = v1davd = 'C'
     c                   eval      *in38 = v1belp = 'C'
      *
     c                   eval      w_dtxt = *blank
     c                   eval      w_ktxt = *blank
     c                   eval      wsdprk = *blank
     c                   eval      wscprk = *blank
     c                   eval      wsdval = *blank
     c                   eval      wscval = *blank
     c                   eval      w_btxt = *blank
     c                   eval      w_ptxt = *blank
     c                   eval      wsdmva = *blank
     c                   eval      wscmva = *blank
      *
     c                   eval      w_valk = *blank
     c                   eval      werror = 0
     c                   eval      w_feil = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.50 * 0-Stilling av F4-felt                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnul_f4       begsr
      *
     c                   eval      p_kont = *zero
     c                   eval      p_spes = *zero
     c                   eval      p_avde = *zero
     c                   eval      p_kund = *zero
     c                   eval      p_levr = *zero
     c                   eval      p_gkod = *zero
     c                   eval      p_Økod = *zero
     c                   eval      p_refk = *zero
     c                   eval      p_biln = *zero
     c                   eval      p_rest = *zero
      *
     c                   eval      p_ekod = *blank
     c                   eval      p_pkod = *blank
6.20 c                   eval      p_arbo = *blank
6.20 c                   eval      p_pros = *blank
6.20 c                   eval      p_upro = *blank
6.20 c                   eval      p_uprs = *blank
6.20 c                   eval      p_akti = *blank
     c*                  eval      p_ukod = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Nullstill felter i registreringsbildet                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnulle_a5     begsr
      *
     c                   eval      a5bilk = *zero
     c                   eval      a5bdat = *zero
     c                   eval      a5biln = *zero
     c                   eval      a5dkto = *zero
     c                   eval      a5dsps = *zero
     c                   eval      a5davd = *zero
     c                   eval      a5dmva = *blank
     c                   eval      a5belØ = *zero
     c                   eval      a5gmva = *zero
     c                   eval      a5ckto = *zero
     c                   eval      a5csps = *zero
     c                   eval      a5cavd = *zero
     c                   eval      a5cmva = *blank
6.20 c                   eval      a5pros = *blank
6.20 c                   eval      a5upro = *blank
6.20 c                   eval      a5arbo = *blank
6.20sc                   eval      a5akti = *blank
     c                   eval      a5krys = *blank
     c                   eval      a5text = *blank
     c                   eval      a5fdat = *zero
     c                   eval      a5krab = *zero
     c                   eval      a5valk = *blank
     c                   eval      a5valb = *zero
     c                   eval      a5refn = *zero
     c                   eval      a5lfak = *blank
     c                   eval      a5kidi = *blank
     c                   eval      a5atte = *blank
     c                   eval      a5mngd = *zero
     c                   eval      a5virk = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Nullstill felter i kviteringsbildet                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnulle_a6     begsr
      *
     c                   eval      a61txt = ' '
     c                   eval      a62txt = ' '
     c                   eval      a6btxt = ' '
     c                   eval      a6ptxt = ' '
     c                   eval      a6linj = 0
     c                   eval      a6bilk = 0
     c                   eval      a6bdat = 0
     c                   eval      a6biln = 0
     c                   eval      a6dkto = 0
     c                   eval      a6dsps = 0
     c                   eval      a6davd = 0
     c                   eval      a6dmva = ' '
     c                   eval      a6belØ = 0
     c                   eval      a6ckto = 0
     c                   eval      a6csps = 0
     c                   eval      a6cavd = 0
     c                   eval      a6cmva = ' '
6.20 c                   eval      a6pros = ' '
6.20 c**                 eval      a6akti = ' '
      *
     c                   eval      w_dtxt = ' '
     c                   eval      w_ktxt = ' '
     c                   eval      w_btxt = ' '
     c                   eval      w_ptxt = ' '
T5.40c                   eval      w_dgir = 0
T5.40c                   eval      w_cgir = 0
      *
     c                   endsr
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Save av inputfelter i A5BLD, for senere duplisering             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xa5sav        begsr
      *
     c                   move      a5bilk        axbilk
     c                   move      a5bdat        axbdat
     c                   move      a5biln        axbiln
     c                   move      a5dkto        axdkto
     c                   move      a5dsps        axdsps
     c                   move      a5davd        axdavd
     c                   move      a5dmva        axdmva
     c                   move      a5belØ        axbelØ
     c                   move      a5ckto        axckto
     c                   move      a5csps        axcsps
     c                   move      a5cavd        axcavd
     c                   move      a5cmva        axcmva
6.20 c                   move      a5pros        axpros
6.20 c                   move      a5upro        axupro
6.20 c                   move      a5arbo        axarbo
6.20sc                   move      a5akti        axakti
     c                   move      a5krys        axkrys
     c                   move      a5text        axtext
     c                   move      a5fdat        axfdat
     c                   move      a5krab        axkrab
     c                   move      a5valk        axvalk
     c                   move      a5valb        axvalb
     c                   move      a5refn        axrefn
     c                   move      a5lfak        axlfak
     c                   move      a5kidi        axkidi
     c                   move      a5atte        axatte
     c                   move      a5mngd        axmngd
     c                   move      a5virk        axvirk
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Flytte felter A5 fra skjerm til file (STF)                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xa5stf        begsr
      *
     c                   move      a5bilk        rtbilk
     c     *dmy          move      a5bdat        rtbdat
     c                   move      a5biln        rtbiln
     c                   move      a5dkto        rtdkto
     c                   move      a5dsps        rtdsps
     c                   move      a5davd        rtdavd
      *
     c                   if        a5dmva = ' '
     c                   move      wsdmva        rtdmva
     c                   else
     c                   move      a5dmva        rtdmva
     c                   endif
      *
     c                   move      a5belØ        rtbelØ
     c                   move      a5ckto        rtckto
     c                   move      a5csps        rtcsps
     c                   move      a5cavd        rtcavd
      *
     c                   if        a5cmva = ' '
     c                   move      wscmva        rtcmva
     c                   else
     c                   move      a5cmva        rtcmva
     c                   endif
      *
6.20 c                   move      a5pros        rtpros
6.20 c                   move      a5upro        rtupro
6.20 c                   move      a5arbo        rtarbo
6.20sc                   move      a5akti        rtakti
     c                   move      a5krys        rtkrys
     c                   move      a5text        rttext
     c                   move      a5gmva        rtgmva
      *
     c                   if        a5fdat <> *zeros
     c     *dmy          move      a5fdat        rtfdat
     c                   else
     c                   eval      rtfdat = *loval
     c                   endif
      *
     c                   move      a5krab        rtkrab
     c                   move      a5valk        rtvalk
     c                   move      a5valb        rtvalb
     c                   move      a5refn        rtrefn
     c                   move      a5lfak        rtlfak
     c                   move      a5kidi        rtkidi
     c                   move      a5atte        rtatte
     c                   move      a5mngd        rtmngd
     c                   move      a5virk        rtvirk
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Flytte felter A5 fra file til skjerm (FTS)                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xa5fts        begsr
      *
     c                   move      rtbilk        a5bilk
     c                   if        rtbdat > w_maxdat
     c                   eval      rtbdat = w_maxdat
     c                   endif
     c     *dmy          move      rtbdat        a5bdat
     c                   move      rtbiln        a5biln
     c                   move      rtdkto        a5dkto
     c                   move      rtdsps        a5dsps
     c                   move      rtdavd        a5davd
     c                   move      rtdmva        a5dmva
     c                   move      rtbelØ        a5belØ
     c                   move      rtckto        a5ckto
     c                   move      rtcsps        a5csps
     c                   move      rtcavd        a5cavd
     c                   move      rtcmva        a5cmva
6.20 c                   move      rtpros        a5pros
6.20 c                   move      rtupro        a5upro
6.20 c                   move      rtarbo        a5arbo
6.20sc                   move      rtakti        a5akti
     c                   move      rtkrys        a5krys
     c                   move      rttext        a5text
     c                   move      rtgmva        a5gmva
      *
     c                   if        rtfdat = *loval
     c                   eval      a5fdat = *zeros
     c                   else
     c                   if        rtfdat > w_maxdat
     c                   eval      rtfdat = w_maxdat
     c                   endif
     c     *dmy          move      rtfdat        a5fdat
     c                   endif
      *
     c                   move      rtkrab        a5krab
     c                   move      rtvalk        a5valk
     c                   move      rtvalb        a5valb
     c                   move      rtrefn        a5refn
     c                   move      rtlfak        a5lfak
     c                   move      rtkidi        a5kidi
     c                   move      rtatte        a5atte
     c                   move      rtmngd        a5mngd
     c                   move      rtvirk        a5virk
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for kontroll av konto                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     chk_kont      begsr
      *
      * Sjekke om kontotype er H/K/L
      *
     c                   if        w_kont < ra1hkt
     c                   move      'H'           w_ktyp
     c                   endif
      *
     c                   if        w_kont > ra1hkt and
     c                             w_kont < ra1hrs
     c                   move      'K'           w_ktyp
     c                   endif
      *
     c                   if        w_kont > ra1hrs
     c                   move      'L'           w_ktyp
     c                   endif
      *
      * Sjekk for ugyldige kombinasjoner
      *
     c                   if        w_ktyp = 'H' and
6.11 c                             w_kont = *zero
6.11 c***                          w_ktyp = 'K' or
6.11 c***                          w_ktyp = 'L'
      *
      * Avdeling ugyldig utfylt
      *
     c                   if        w_avde <> 0
     c                   eval      w_feil = 2
     c                   move      *on           *in82
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kostnadsbærer ugyldig utfylt
      *
     c                   if        w_spes <> 0
     c                   eval      w_feil = 3
     c                   move      *on           *in68
      *
      * Retur hvis konto ikke utfylt
      *
     c                   if        w_kont = 0
     c                   eval      w_ktyp = ' '
     c                   endif
      *
     c                   goto      end_chk_kont
     c                   endif
     c                   endif
      *
      * Sjekke konto dersom hovedbok
      *
     c                   if        w_ktyp = 'H'
      *
     c                   move      w_kont        rhovl1_kont
     c                   move      w_spes        rhovl1_spes
     c                   move      w_avde        rhovl1_avde
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   move      *zeros        rhovl1_avde
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   move      *zeros        rhovl1_spes
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   eval      w_feil = 1
     c                   move      *on           *in81
     c                   goto      end_chk_kont
     c                   endif
     c                   endif
     c                   endif
      *
     c                   movel     rhtext        w_text
      *
      * Er konto sperret?
      *
     c                   if        rhsprk = 'J'
     c                   eval      w_feil = 1
     c                   move      *on           *in88
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll av konto :
      * Skal ha avdeling
      *
     c                   if        rhavdk = 'J' and
     c                             w_avde = *zero
     c                   eval      w_feil = 2
     c                   move      *on           *in81
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll av konto :
      * Skal ikke ha avdeling
      *
     c                   if        rhavdk = 'N' and
     c                             w_avde <> *zero
     c                   eval      w_feil = 2
     c                   move      *on           *in82
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll om avdeling finnes i koderegister
      *
     c                   if        w_avde <> *zero
     c                   move      w_avde        p_gkod
      *
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx30
      *
     c                   if        p_stat = 'N' or
     c                             p_stat = 'S'
     c                   eval      w_feil = 2
     c                   if        p_stat = 'S'
     c                   move      *on           *in57
     c                   else
     c                   move      *on           *in83
     c                   endif
     c                   goto      end_chk_kont
     c                   endif
      *
     c                   endif
      *
      * Kontroll av konto :
      * Skal ha kostnadsbærer
      *
     c                   if        rhbÆrk = 'J' and
     c                             w_spes = *zero
     c                   eval      w_feil = 3
     c                   move      *on           *in04
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll av konto :
      * Skal ikke ha kostnadsbærer
      *
     c                   if        rhbÆrk = 'N' and
     c                             w_spes <> *zero
     c                   eval      w_feil = 3
     c                   move      *on           *in68
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll om kostnadsbærer finnes i koderegister
      *
T5.30c                   if        ra3bÆr = 'J' and
E5.30c                             w_spes <> *zero
     c                   move      w_spes        p_Økod
      *
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    p_Økod
     c                   parm                    p_tx30
      *
E5.50c                   if        p_stat = 'N' or
T5.50c                             p_stat = 'S'
     c                   eval      w_feil = 3
T5.50c                   if        p_stat = 'S'
T5.50c                   move      *on           *in94
T5.50c                   else
     c                   move      *on           *in11
T5.50c                   endif
     c                   goto      end_chk_kont
     c                   endif
     c                   endif
      *
      * Kontroll av mvakode :
      * Ikke tillatt med overstyring
      *
     c                   if        rhmvao <> 'J' and
     c                             w_mvak <> *blank and
     c                             w_mvak <> rhmvak
     c                   eval      w_feil = 1
     c                   move      *on           *in86
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll av mvakode :
      * Overstyring, men feil kode
      *
     c                   movea     rhmvag        wmv
      *
     c                   if        rhmvao = 'J' and
     c                             w_mvak <> *blank and
     c                             w_mvak <> rhmvak
     c     rhmvag        cabeq     *blank        xa5031
      *
     c     1             do        8             n
     c     w_mvak        cabeq     wmv(n)        xa5031
     c                   enddo
      *
     c                   eval      w_feil = 1
     c                   move      *on           *in87
     c                   goto      end_chk_kont
      *
     c     xa5031        tag
      *
     c                   endif
      *
      * Kontroll av mvakode :  Kode mangler i koderegister
      *
     c                   if        w_mvak <> *blank
     c                   move      w_mvak        p_ekod
      *
     c                   call      'RS705R'
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_tx30
      *
     c                   if        p_stat = 'N'
     c                   eval      w_feil = 1
     c                   move      *on           *in87
     c                   goto      end_chk_kont
     c                   endif
     c                   endif
      *
      * Ta vare på mvakode for
      * kvittering i skjermbilde
      *
     c                   if        w_mvak = *blank
     c                   move      rhmvak        w_mvak
     c                   endif
      *
      * Ta vare på kode for kontroll av konto
      * vedrørende prosjekt
      *
     c                   if        rhproj = ' '
     c                   eval      w_proj = 'K'
     c                   else
     c                   eval      w_proj = rhproj
     c                   endif
      *
     c                   endif
      *
      * Sjekke konto, dersom kunder
      *
     c                   if        w_ktyp = 'K'
     c                   move      w_kont        rkunl1_kund
     c     rkunl1_key    chain     rkunl1r
      *
     c                   if        not%found
     c                   eval      w_feil = 1
     c                   move      *on           *in82
     c                   goto      end_chk_kont
     c                   else
     c                   movel     rknavn        w_text
     c                   move      rkkund        wfresn
     c                   endif
      *
      * Er konto sperret?
      *
     c                   if        rksprk = 'J'
     c                   eval      w_feil = 1
     c                   move      *on           *in88
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll av mvakode :
      * Ikke tillatt med overstyring
      *
     c                   if        w_mvak <> *blank and
     c                             w_mvak <> '0'
     c                   eval      w_feil = 1
     c                   move      *on           *in86
     c                   goto      end_chk_kont
     c                   endif
      *
     c                   endif
      *
      * Sjekke konto, dersom leverandør
      *
     c                   if        w_ktyp = 'L'
     c                   move      w_kont        rlevl1_levr
     c     rlevl1_key    chain     rlevl1r
      *
     c                   if        not%found
     c                   eval      w_feil = 1
     c                   move      *on           *in83
     c                   goto      end_chk_kont
     c                   else
     c                   movel     rlnavn        w_text
     c                   move      rllevr        wfresn
T5.40c                   move      rlbgir        w_bgir
     c                   endif
      *
      * Er konto sperret?
      *
     c                   if        rlsprk = 'J'
     c                   eval      w_feil = 1
     c                   move      *on           *in88
     c                   goto      end_chk_kont
     c                   endif
      *
      * Kontroll av mvakode :
      * Ikke tillatt med overstyring
      *
     c                   if        w_mvak <> *blank and
     c                             w_mvak <> '0'
     c                   eval      w_feil = 1
     c                   move      *on           *in86
     c                   goto      end_chk_kont
     c                   endif
      *
     c                   endif
      *
      * Ta vare på valutakode
      * for senere test i sub-
      * rutine chk_valu
      *
     c                   select
     c                   when      w_ktyp = 'K'
     c                   move      rkvalk        w_valk
     c                   when      w_ktyp = 'L'
     c                   move      rlvalk        w_valk
     c                   endsl
      *
     c     end_chk_kont  endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.20  * Rutine for kontroll av prosjekt                                 *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.20  *
6.20 c     chk_proj      begsr
6.20  *
6.20  * Debet konto skal ha prosjekt
6.20  * men prosjekt mangler
6.20  *
6.20 c                   if        wdbhkl = 'H'
6.20  *
6.20 c                   if        wsdprk = 'J' and
6.20 c                             a5pros = *blank
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in82
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20  *
6.20  * Debet konto skal ikke ha prosjekt
6.20  * men prosjekt tastet
6.20  *
6.20 c                   if        wsdprk = 'N' and
6.20 c                             a5pros <> *blank
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in84
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20  * Kredit konto skal ha prosjekt
6.20  * men prosjekt mangler
6.20  *
6.20 c                   if        wkrhkl = 'H'
6.20  *
6.20 c                   if        wscprk = 'J' and
6.20 c                             a5pros = *blank
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in83
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20  *
6.20  * Kredit konto skal ikke ha prosjekt
6.20  * men prosjekt tastet
6.20  *
6.20 c                   if        wscprk = 'N' and
6.20 c                             a5pros <> *blank
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in84
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20  * Prosjekt ikke funnet for debet konto
6.20  *
6.20 c                   if        wdbhkl = 'H' and
6.20 c                             wsdprk <> 'N' and
6.20 c                             a5pros <> *blank
6.20 c                   eval      rp1pl1_pros = a5pros
6.20 c     rp1pl1_key    chain     rp1pl1r
6.20  *
6.20 c                   if        not%found
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in85
6.20 c                   goto      end_chk_proj
6.20 c                   else
6.20 c                   movel     rp1txt        w_ptxt
6.20 c                   endif
6.20  *
6.20 c                   endif
6.20  *
6.20  * Prosjekt ikke funnet for kredit konto
6.20  *
6.20 c                   if        wkrhkl = 'H' and
6.20 c                             wscprk <> 'N' and
6.20 c                             a5pros <> *blank
6.20 c                   eval      rp1pl1_pros = a5pros
6.20 c     rp1pl1_key    chain     rp1pl1r
6.20  *
6.20 c                   if        not%found
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in86
6.20 c                   goto      end_chk_proj
6.20 c                   else
6.20 c                   movel     rp1txt        w_ptxt
6.20 c                   endif
6.20  *
6.20 c                   endif
6.20  *
6.20  * Prosjekt er avsluttet og ferdigmeldt
6.20  *
6.20 c                   if        rp1sts = 'A' and
6.20 c                             a5pros <> *blank
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in87
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20  *
6.20  * Sjekk om kombinasjon Arb.ordre, Prosjekt og underprosjekt finnes
6.20  *
6.20 c                   if        a5arbo <> *blank
6.20 c                   eval      rp4ol1_arbo = a5arbo
6.20 c     rp4ol1_key    chain     rp4ol1
6.20 c                   if        not %found(rp4ol1)
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in89
6.20 c                   goto      end_chk_proj
     c                   else
     c                   if        a5pros = *blank
     c                   eval      a5pros = rp4pro
     c                   endif
     c                   if        a5upro = *blank
     c                   eval      a5upro = rp4upr
     c                   endif
     c                   if        a5akti = *blank
     c                   eval      a5akti = rp4akt
     c                   endif
6.20 c                   endif
6.20 c                   if        %found(rp4ol1) and
6.20 c                             (rp4pro <> a5pros or
6.20 c                              rp4upr <> a5upro)
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in43
6.20 c                   move      *on           *in90
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20  * Sjekk om Prosjekt finnes
6.20  *
6.20 c                   if        a5pros <> *blank
6.20 c                   eval      rp1pl1_pros = a5pros
6.20 c     rp1pl1_key    chain     rp1pl1r
6.20  *
6.20 c                   if        not %found(rp1pl1)
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in57
6.20 c                   move      *on           *in85
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20  * Sjekk om Underprosjekt finnes
6.20  *
6.20 c                   if        a5upro <> *blank
6.20 c                   eval      rp2ul1_pros = a5pros
6.20 c                   eval      rp2ul1_upro = a5upro
6.20 c     rp2ul1_key    chain     rp2ul1
6.20  *
6.20 c                   if        not %found(rp2ul1)
6.20 c                   eval      werror = 1
6.20 c                   move      *on           *in58
6.20 c                   move      *on           *in85
6.20 c                   goto      end_chk_proj
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20 c     end_chk_proj  endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kontroll av valutta                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     chk_valu      begsr
      *
      * Debetkonto har valuta
      *
     c                   if        wsdval = 'J'
      *
      * Sjekke om oppgitt valutakode stemmer med den på konto
      *
     c                   if        a5valk <> ' ' and
     c                             a5valk <> w_valk
     c                   eval      werror = 1
     c                   move      *on           *in49
     c                   move      *on           *in81
     c                   goto      end_chk_valu
     c                   endif
      *
     c                   endif
      *
      * Kreditkonto har valuta
      *
     c                   if        wscval = 'J'
      *
      * Sjekke om oppgitt valutakode stemmer med den på konto
      *
     c                   if        a5valk <> ' ' and
     c                             a5valk <> w_valk
     c                   eval      werror = 1
     c                   move      *on           *in49
     c                   move      *on           *in82
     c                   goto      end_chk_valu
     c                   endif
      *
     c                   endif
      *
      * Hverken debet eller kreditkonto har valuta
      *
     c                   if        wsdval = ' ' and
     c                             wscval = ' ' and
6.25 c                             w_ktyp <> 'H' and
     c                             a5valk <> ' '
     c                   eval      werror = 1
     c                   move      *on           *in49
     c                   move      *on           *in83
     c                   goto      end_chk_valu
     c                   endif
      *
      * Dersom valutabeløp er oppgitt,
      * må det finnes valutakode
      *
     c                   if        a5valb <> *zero and
     c                             a5valk = *blank
     c                   eval      werror = 1
     c                   move      *on           *in49
     c                   move      *on           *in83
     c                   goto      end_chk_valu
     c                   endif
      *
      * Hente inn valutapost fra koderegister
      *
     c                   if        a5valk <> *blank
     c                   move      a5valk        ra16l1_pkod
      *
     c     ra16l1_key    chain     ra16l1
      *
     c                   if        not%found
     c                   eval      werror = 1
     c                   move      *on           *in49
     c                   move      *on           *in84
     c                   goto      end_chk_valu
     c                   endif
      *
     c                   endif
      *
      * Kursdifferanser(Agio/Disagio) må kunne føres med null i valutabeløp
      *
     c                   if        a5valk <> *blank
      *
     c                   if        a5bilk = 88
     c                   goto      end_chk_valu
     c                   endif
      *
      * Omregning fra NOK til valuta
      *
     c                   if        a5belØ <> *zero and
     c                             a5valb = *zero
     c                   if        w_pdat > rapkda
     c                   eval      a5valb = a5belØ * rapmti
     c                   else
     c                   eval      a5valb = a5belØ * rapmtg
     c                   endif
     c                   endif
      *
      * Omregning fra Valuta til NOK
      *
     c                   if        a5belØ = *zero and
     c                             a5valb <> *zero
     c                   if        w_pdat > rapkda
     c                   eval      a5belØ = a5valb * rapmku
     c                   else
     c                   eval      a5belØ = a5valb * rapmkg
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c     end_chk_valu  endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Sletting / reaktivering av bunter                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsrbun        begsr
      *
      * Slette/reaktivere
      * bunt m/angrefrist
      *
     c                   if        wsstat <> 'U'
     c                   move      var(13)       m1mld1
     c                   move      var(14)       m1mld2
     c                   else
     c                   move      var(15)       m1mld1
     c                   move      var(16)       m1mld2
     c                   endif
      *
     c                   exsr      xm1win
      *
     c                   if        m1svar = 'N'
     c                   move      srrn02        svrn02
     c                   eval      u1valg = 0
     c                   update    u1wsf
     c                   goto      xsrend
     c                   endif
      *
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c     rbunl1_key    chain     rbunlur
     c                   if        wsstat <> 'U'
     c                   move      'U'           rsstat
     c                   move      'JA'          u1slet
     c                   else
     c                   eval      rsstat = *blank
     c                   eval      u1slet = *blank
     c                   endif
      *
     c                   if        %found
610  c                   time                    rsedat
610  c                   time                    rsetim
     c                   update    rbunlur
     c                   endif
      *
     c                   move      srrn02        svrn02
     c                   eval      u1valg = 0
     c                   update    u1wsf
      *
      *
     c     xsrend        endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile for transaksjoner                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
      *
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile for transaksjoner                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
      *
7.05 c                   eval      spge01 = spge01 + 13
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01
     c                   read      rtral1                                 15
     c     *in15         cabeq     *on           xend01
      *
     c                   if        rtfirm <> l_firm
     c                   move      *on           *in15
     c                   endif
610   *
610  c                   if        rtmemb <> w_memb
610  c                   move      *on           *in15
610  c                   endif
7.12 c                   if        *in15 = *on
7.12 c                   eval      b2linj = *zero
7.12 c                   endif
     c     *in15         cabeq     *on           xend01
      *
     c                   if        rtbunt <> w_bunt
     c                   move      *on           *in15
     c                   endif
     c     *in15         cabeq     *on           xend01

     c                   eval      srrn01 = srrn01 + 1
     c                   move      rtlinj        b1linj
     c                   move      rtbilk        b1bilk
     c     *dmy          move      rtbdat        b1bdat
     c                   move      rtbiln        b1biln
     c                   move      rtdavd        b1davd
     c                   move      rtdkto        b1dkto
     c                   move      rtdsps        b1dsps
     c                   move      rtdmva        b1dmva
     c                   move      rtbelØ        b1belØ
     c                   move      rtcavd        b1cavd
     c                   move      rtckto        b1ckto
     c                   move      rtcsps        b1csps
     c                   move      rtcmva        b1cmva
      *
     c                   write     b1sfl
     c                   enddo
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01
     c                   eval      sfrn01 = ssrn01 + 1
     c                   endif
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      svrn01 = sfrn01
      *
     c                   endsr
      *
      /eject
7.01  * 7.01   Begin      Begin   7.01  Begin  Begin    - - - - - - - - *
      * Fyll opp subfile for transaksjoner                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt0x        begsr
      *
     c                   eval      spge01 = spge01 + 10
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01
     c                   read      rtralx                                 15
     c     *in15         cabeq     *on           xend0x
      *
     c                   if        rtfirm <> l_firm
     c                   move      *on           *in15
     c                   endif
610   *
610  c                   if        rtmemb <> w_memb
610  c                   move      *on           *in15
610  c                   endif
     c     *in15         cabeq     *on           xend0x
      *
     c                   if        rtbunt <> w_bunt
     c                   move      *on           *in15
     c                   endif
     c     *in15         cabeq     *on           xend0x
      *
     c                   eval      srrn01 = srrn01 + 1
     c                   move      rtlinj        b1linj
     c                   move      rtbilk        b1bilk
     c     *dmy          move      rtbdat        b1bdat
     c                   move      rtbiln        b1biln
     c                   move      rtdavd        b1davd
     c                   move      rtdkto        b1dkto
     c                   move      rtdsps        b1dsps
     c                   move      rtdmva        b1dmva
     c                   move      rtbelØ        b1belØ
     c                   move      rtcavd        b1cavd
     c                   move      rtckto        b1ckto
     c                   move      rtcsps        b1csps
     c                   move      rtcmva        b1cmva
      *
     c                   write     b1sfl
     c                   enddo
      *
     c     xend0x        tag
      *
     c                   if        srrn01 > ssrn01
     c                   eval      sfrn01 = ssrn01 + 1
     c                   endif
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      svrn01 = sfrn01
      *
     c                   endsr
7.01  *  end    7.01   end   7.01  end   7.01 end
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile for transaksjoner                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0
     c                   move      *on           *in12
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile for bunter                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr02        begsr
      *
     c                   move      *on           *in14
     c                   write     u2wct
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn02 = 0
     c                   eval      sfrn02 = 0
     c                   eval      ssrn02 = 0
     c                   eval      spge02 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile for bunter                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt02        begsr
      *
     c                   eval      spge02 = spge02 + 10
     c                   eval      srrn02 = ssrn02
      *
     c                   dou       srrn02 = spge02
     c                   read      rbunl1                                 15
     c     *in15         cabeq     *on           xend02
      *
     c                   if        rsfirm <> l_firm
     c                   move      *on           *in15
     c                   endif
610   *
610  c                   if        rsmemb <> w_memb
610  c                   move      *on           *in15
610  c                   endif
610   *
     c     *in15         cabeq     *on           xend02
      *
     c                   eval      srrn02 = srrn02 + 1
     c                   move      rsbunt        u1bunt
     c                   move      rsraar        u1raar
     c                   move      rsrper        u1rper
     c                   move      rsbsum        u1bsum
     c                   eval      u1diff = rspdeb - rspkre
      *
     c                   if        rsstat = 'U'
     c                   move      'JA'          u1slet
     c                   else
     c                   move      '  '          u1slet
     c                   endif
      *
     c                   write     u1wsf
     c                   enddo
      *
     c     xend02        tag
      *
     c                   if        srrn02 > ssrn02
     c                   eval      sfrn02 = ssrn02 + 1
     c                   endif
      *
     c                   eval      ssrn02 = srrn02
     c                   eval      svrn02 = sfrn02
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile for bunter                                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp02        begsr
      *
     c                   if        srrn02 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     u2wct
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for gjenfinning av preregistrerte bilag               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xbilag        begsr
      *
     c                   eval      wltrok = *blank
     c                   eval      rltrl3_biln = a5biln
     c     rltrl3_key    setll     rltrl3                                 15
     c     rltrl3_key    reade     rltrl3                                 15
      *
     c                   dow       *in15 = *off
      *
     c     *dmy          move      robdat        wobdat
     c     *dmy          move      robdat        a5bdat
     c                   eval      a5bilk = robilk
     c                   eval      a5text = rotext
     c                   move      '1'           wltrok
     c                   move      '1'           a5atte
      *
     c                   if        robelØ < *zero
      *
     c                   eval      a5dkto = *zero
     c                   eval      a5dsps = *zero
     c                   eval      a5davd = *zero
     c                   eval      a5dmva = *blank
     c                   eval      a5ckto = roresn
     c                   eval      a5csps = roress
     c                   eval      a5cavd = roresa
     c                   eval      a5cmva = roresm
     c                   eval      a5belØ = 0 - robelØ
      *
     c                   else
      *
     c                   eval      a5ckto = *zero
     c                   eval      a5csps = *zero
     c                   eval      a5cavd = *zero
     c                   eval      a5cmva = *blank
     c                   eval      a5dkto = roresn
     c                   eval      a5dsps = roress
     c                   eval      a5davd = roresa
     c                   eval      a5dmva = roresm
     c                   eval      a5belØ = robelØ
      *
     c                   endif
      *
     c     rltrl3_key    reade     rltrl3                                 15
      *
     c                   enddo
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine som leser statuskode og setter indikatorer            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hnt_stat      begsr
      *
     c     raa3l1_key    chain     raa3l1r
      *
      * Finnes status-record ?
      *
     c     raa1l1_key    chain     raa1l1r
      *
     c                   if        not%found
     c                   move      var(9)        m1mld1
     c                   move      var(10)       m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   endif
      *
      * Sjekk om buntsum skal benyttes
      *
     c                   if        ra1bun = 'N'
     c                   eval      *in62 = '1'
     c                   endif
      *
      * Sjekke eventuell blending av felter,
      * og sette indikatorer som styrer dette
      *
     c                   if        %found
     c                   eval      *in01 = ra1sps = 'X'
     c                   eval      *in02 = ra1avd = 'X'
     c                   eval      *in03 = ra1mva = 'X'
     c                   eval      *in07 = ra1pro = 'X'
     c                   eval      *in08 = ra1akt = 'X'
     c                   eval      *in09 = ra1akr = 'X'
     c                   eval      *in20 = ra1ftx = 'X'
     c                   eval      *in21 = ra1for = 'X'
     c                   eval      *in22 = ra1rab = 'X'
     c                   eval      *in23 = ra1vko = 'X'
     c                   eval      *in24 = ra1vbe = 'X'
     c                   eval      *in25 = ra1lfa = 'X'
     c                   eval      *in27 = ra1kid = 'X'
     c                   eval      *in28 = ra1att = 'X'
     c                   eval      *in29 = ra1mgn = 'X'
     c                   eval      *in06 = ra1vrk = 'X'
      *
      * Felt som kan dupliseres
      *
     c                   eval      *in17 = ra1bik = 'D'
     c                   eval      *in18 = ra1bid = 'D'
     c                   eval      *in19 = ra1bin = 'D'
     c                   eval      *in58 = ra1kto = 'D'
     c                   eval      *in59 = ra1sps = 'D'
     c                   eval      *in70 = ra1avd = 'D'
     c                   eval      *in71 = ra1mva = 'D'
     c                   eval      *in72 = ra1blp = 'D'
     c                   eval      *in73 = ra1kto = 'D'
     c                   eval      *in74 = ra1sps = 'D'
     c                   eval      *in75 = ra1avd = 'D'
     c                   eval      *in76 = ra1mva = 'D'
     c                   eval      *in77 = ra1pro = 'D'
     c                   eval      *in78 = ra1akt = 'D'
     c                   eval      *in79 = ra1akr = 'D'
     c                   eval      *in80 = ra1ftx = 'D'
     c                   eval      *in81 = ra1for = 'D'
     c                   eval      *in82 = ra1rab = 'D'
     c                   eval      *in83 = ra1vko = 'D'
     c                   eval      *in84 = ra1vbe = 'D'
     c                   eval      *in85 = ra1ref = 'D'
     c                   eval      *in86 = ra1lfa = 'D'
     c                   eval      *in87 = ra1kid = 'D'
     c                   eval      *in88 = ra1att = 'D'
     c                   eval      *in89 = ra1mgn = 'D'
     c                   eval      *in90 = ra1vrk = 'D'
      *
      * Posisjoner cursor
      *
     c                   if        w_fors = 0
     c                   eval      *in31 = *on
     c                   else
     c                   eval      *in31 = ra1bik = 'C'
     c                   eval      *in32 = ra1bid = 'C'
     c                   eval      *in33 = ra1bin = 'C'
     c                   eval      *in36 = ra1avd = 'C'
     c                   eval      *in34 = ra1kto = 'C'
     c                   endif
      *
      * Flytte informasjon fra file til skjerm
      *
     c                   move      ra1sps        v1dsps
     c                   move      ra1avd        v1davd
     c                   move      ra1mva        v1dmva
     c                   move      ra1pro        v1pros
     c                   move      ra1akt        v1akti
     c                   move      ra1akr        v1krys
     c                   move      ra1ftx        v1text
     c                   move      ra1for        v1fdat
     c                   move      ra1rab        v1krab
     c                   move      ra1vko        v1valk
     c                   move      ra1vbe        v1valb
     c                   move      ra1lfa        v1lfak
     c                   move      ra1kid        v1kidi
     c                   move      ra1att        v1atte
     c                   move      ra1mgn        v1mngd
     c                   move      ra1vrk        v1virk
      *
     c                   move      ra1bik        v1biko
     c                   move      ra1bid        v1bida
     c                   move      ra1bin        v1binr
     c                   move      ra1blp        v1belp
     c                   move      ra1kto        v1dkto
     c                   move      ra1ref        v1refn
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * Subrutine for modulus 10 kontrol                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     xmod10        begsr
      *
     c                   call      'RÅ910R'
     c                   parm                    p_tall
     c                   parm                    p_rkod
      *
      * Mod 11 kalles opp dersom feil ved mod 10.
      *
     c                   if        p_rkod = *on
     c                   exsr      xmod11
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * Subrutine for modulus 11 kontrol                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     xmod11        begsr
      *
      * Legger KID-felt over i hjelpefelt.
      *
     c                   eval      tell = 0
     c                   eval      psum = 0
     c                   eval      vekt = 1
     c                   eval      v = 25
      *
     c                   dow       v > 1
     c                   eval      v = v - 1
     c                   if        tell = 6
     c                   eval      tell = 0
     c                   eval      vekt = 1
     c                   endif
     c                   eval      vekt = vekt + 1
     c                   move      tall(v)       faktor
     c                   eval      prod = faktor * vekt
     c                   eval      psum = psum + prod
     c                   eval      tell = tell + 1
     c                   enddo
      *
     c     psum          div       11            divtal
     c                   mvr                     rest
      *
     c                   if        rest = 1
     c                   move      '-'           ktr
     c                   else
     c                   eval      difktr = 11 - rest
     c                   move      difktr        ktr
     c                   if        rest = 0
     c                   move      0             ktr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
610   *
610   * Parameterliste
610   *
610  c     *entry        plist
610  c                   parm                    w_memb
      *
     c                   move      *on           *in31
     c                   eval      n = 0
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      spge02 = 0
     c                   eval      srrn02 = 0
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   eval      wsavli = 0
     c                   eval      wsavpo = 0
     c                   eval      werror = 0
     c                   eval      w_feil = 0
     c                   eval      wltrok = *blank
     c                   eval      wdbhkl = *blank
     c                   eval      wkrhkl = *blank
     c                   eval      wsdprk = *blank
     c                   eval      wscprk = *blank
     c                   eval      wsdval = *blank
     c                   eval      wscval = *blank
     c                   eval      wsdmva = *blank
     c                   eval      wscmva = *blank
     c                   eval      wmdlst = *blank
      *
      * Legge inn firmanummer og navn fra LDA
      *
     c                   move      l_firm        w_firm
     c                   move      l_user        w_user
     c                   movel     l_user        w_aspuser
      *
      *  Sjekk at bruker er ASP
      *  *in60 sperrer for endring av mva-grunnlag
      *
     c                   if        w_aspuser <> 'ASP_'
     c                   eval      *in60 = *on
     c                   endif
      *
      * Kontotype
      *
     c                   eval      x1valg = *blank
      *
      * Finn buntnr
      *
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memb
     c                   eval      rbunl1_bunt = 99999
     c     rbunl1_key    setgt     rbunlu
     c                   readp     rbunlu                                 91
      *
610  c                   if        rsmemb <> w_memb
610  c                   eval      a2bunt = 1
610  c                   eval      *in91 = *on
610  c                   endif
     c                   if        *in91 = *on
     c                   eval      a2bunt = 1
     c                   eval      a2rper = *month
     c                   eval      a2raar = *year
     c                   eval      a2bsum = 0
     c                   else
      *
     c                   if        rsstat = 'U'
     c                   eval      a2bunt = rsbunt + 1
     c                   eval      a2rper = *month
     c                   eval      a2raar = *year
     c                   eval      a2bsum = 0
     c
      *
     c                   else
      *
     c                   eval      a2bunt = rsbunt
     c                   eval      a2rper = rsrper
     c                   eval      a2raar = rsraar
     c                   eval      a2bsum = rsbsum
     c                   endif
     c                   endif
      *
      *  Sett maks mulig dato i 20?? foreløpig
      *
     c     *dmy          move      311239        w_maxdat
      *
      * Key for file RTRAL1 nummerisk
      *
     c     rtral1_key    klist
610  c                   kfld                    rtral1_nivo
610  c                   kfld                    rtral1_memb
     c                   kfld                    w_firm
     c                   kfld                    rtral1_bunt
     c                   kfld                    rtral1_linj
      *
7.11 c     rtralx_key    klist
7.11 c                   kfld                    rtralx_nivo
7.11 c                   kfld                    rtralx_memb
7.11 c                   kfld                    w_firm
7.11 c                   kfld                    rtralx_bunt
7.11 c                   kfld                    rtralx_bilk
      *
      * Key for file RBUNL1 nummerisk
      *
     c     rbunl1_key    klist
610  c                   kfld                    rbunl1_nivo
610  c                   kfld                    rbunl1_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunl1_bunt
      *
      * Key for bruker-file
      *
     c     rausrl1_key   klist
     c                   kfld                    w_user
      *
      *
6.10 c     ra01l1_key    klist
6.10 c                   kfld                    w_firm
6.10 c                   kfld                    ra01l1_akod
      *
      * Key for kontoplan
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for leverandører
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for prosjekt
      *
     c*    rprol1_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    rprol1_pros
     c*                  kfld                    rprol1_akti
     c*                  kfld                    rprol1_kont
     c*                  kfld                    rprol1_spes
6.20  *
6.20  * Key for prosjekt
6.20  *
6.20 c*    rprol1_key    klist
6.20 c*                  kfld                    w_firm
6.20 c*                  kfld                    rprol1_pros
6.20  *
6.20  * Key for prosjekt
6.20  *
6.20 c     rp1pl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rp1pl1_pros
6.20  *
6.20  * Key for underprosjekt
6.20  *
6.20 c     rp2ul1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rp2ul1_pros
6.20 c                   kfld                    rp2ul1_upro
6.20  *
6.20  * Key for arb.ordre
6.20  *
6.20 c     rp4ol1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rp4ol1_arbo
      *
      * Key for statusrecord
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statusrecord
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for leverandørposteringer
      *
     c     rltrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl3_biln
      *
      * Key for leverandørposteringer
      *
     c     rltrl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl7_levr
     c                   kfld                    rltrl7_raar
     c                   kfld                    rltrl7_lfak
      *
      * Key for kundeposteringer
      *
     c     rktrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl3_biln
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for valutakoder
      *
     c     ra16l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16l1_pkod
      *
      * Key for hovedbokstransposteringer
      *
     c     rhtrl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrl4_raar
     c                   kfld                    rhtrl4_biln
      *
      * Key for kundeposteringer
      *
     c     rktrlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlb_raar
     c                   kfld                    rktrlb_biln
      *
      * Key for leverandørposteringer
      *
     c     rltrlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlc_raar
     c                   kfld                    rltrlc_biln
      *
     c                   endsr
**
  Ny post                  001
  Endring                  002
  Vise                     003
  Slette                   004
* Slettet *                005
**
Bunt finnes fra før med en annen regskapsperiode      001
Skal periode endres ?                                 002
Bunt finnes fra før med en annen buntsum.             003
Skal buntsum endres ?                                 004
Buntsum er ikke oppgitt.                              005
Bekreft om det skal registreres uten buntsum          006
Oppgitt periode sier at det skal registreres på       007
fjor-året.  Bekreft dersom dette er riktig.           008
Status-record i koderegister ble ikke funnet.         009
Denne må opprettes før programmet kan benyttes.       010
I følge koderegister er denne periode avsluttet.      011
Skal det registreres på perioden ?.                   012
Valget tilsier sletting av hele bunten.               013
Bekreft dersom dette er riktig.                       014
Denne bunt er tidligere slettet.                      015
Skal den hentes tilbake ?.                            016
Det er Debet / Kredit differanse på bunten...         017
Skal dette aksepteres ?.                              018
Buntsummen stemmer ikke med postert beløp...          019
Skal dette aksepteres ?.                              020
Dette buntnummer finnes allerede...                   021
Er dette en endring ?.                                022
Oppgitt periode sier at det skal registreres på       023
neste år.  Bekreft dersom dette er riktig.            024
Oppgitt periode sier at det skal registreres på       025
før fjor-året.  Bekreft dersom dette er riktig.       026
