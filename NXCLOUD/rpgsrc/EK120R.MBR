     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : ek120R
      * Beskrivelse . . : Bonus, vedlikehold av bonusgrupper
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030310 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     feppsl1    if   e           k disk    rename(eppspfr:eppsl1r)
     feppsl2    if   e           k disk    rename(eppspfr:eppsl2r)
     feppslr    if   e           k disk    rename(eppspfr:eppslrr)
     feppslu    uf a e           k disk    rename(eppspfr:eppslur)
     fekboiu    uf a e           k disk    rename(ekbost:ekboiur)
     fekbeiu    uf a e           k disk    rename(ekbest:ekbeiur)
      *
     fek120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Tabeller
      *
     d a_merk          s              4    dim(99)
     d a_pros          s              3  1 dim(99)
      *
      * Definering av variabler i nøkler
      *
     d eppsl1_aarr     s                   like(epaarr)
     d eppsl1_merk     s                   like(epmerk)
     d eppsl2_aarr     s                   like(epaarr)
     d eppsl2_besk     s                   like(epbesk)
     d eppslr_aarr     s                   like(epaarr)
     d eppslr_merk     s                   like(epmerk)
     d eppslu_aarr     s                   like(epaarr)
     d eppslu_merk     s                   like(epmerk)
     d ekboiu_bkun     s                   like(ekbkun)
     d ekbeiu_ebku     s                   like(ekebku)
     d ekbeiu_erka     s                   like(ekerka)
     d ekbeiu_ekun     s                   like(ekekun)
     d ekbeiu_ekpr     s                   like(ekekpr)
     d ekbeiu_eogr     s                   like(ekeogr)
     d ekbeiu_ehgr     s                   like(ekehgr)
     d ekbeiu_eugr     s                   like(ekeugr)
     d ekbeiu_emod     s                   like(ekemod)
     d ekbeiu_eldo     s                   like(ekeldo)
     d ekbeiu_evar     s                   like(ekevar)
     d ekbeiu_eenh     s                   like(ekeenh)
     d ekbeiu_elet     s                   like(ekelet)
     d ekbeiu_ebot     s                   like(ekebot)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d x               s              2  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(epfirm)
      *
     d s_aarr          s                   like(epaarr)
     d s_merk          s                   like(epmerk)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkh
     c                   exsr      xm1win
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2merk <> *blank or
     c                             b2besk <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      eppsl2_besk = b1besk
     c     eppsl2_key    setll     eppsl2
     c                   other
     c                   eval      eppsl1_merk = b1merk
     c     eppsl1_key    setll     eppsl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Bonusgruppe
      *
     c                   if        b2merk <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      eppsl1_merk = b2merk
     c     eppsl1_key    setll     eppsl1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2besk <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      eppsl2_besk = b2besk
     c     eppsl2_key    setll     eppsl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2merk = *blank
     c                   eval      b2besk = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1aarr = b2aarr
     c                   eval      c1merk = b1merk
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1aarr = b2aarr
     c                   eval      k1merk = b1merk
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1aarr = b2aarr
     c                   eval      d1merk = b1merk
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1aarr = b2aarr
     c                   eval      c1merk = b1merk
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1aarr = b2aarr
     c                   eval      c1merk = b2merk
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1merk = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   if        c1merk = *blank
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      eppslr_aarr = c1aarr
     c                   eval      eppslr_merk = c1merk
     c     eppslr_key    chain     eppslr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1merk = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2merk = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      eppslr_aarr = c1aarr
     c                   eval      eppslr_merk = c1merk
     c     eppslr_key    chain     eppslr
      *
     c                   if        %found
     c                   eval      c2besk = epbesk
     c                   eval      c2pros = eppros
     c                   eval      c2eusr = epeusr
     c                   if        epedat <> *loval
     c     *dmy          move      epedat        c2edat
     c                   else
     c                   eval      c2edat = 0
     c                   endif
     c                   eval      c2eusr = epeusr
     c                   else
     c                   eval      c2besk = *blank
     c                   eval      c2pros = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1merk = k1merk
     c                   eval      c1aarr = k1aarr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2besk = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   eppslur
      *
     c                   eval      eppslu_aarr = c1aarr
     c                   eval      eppslu_merk = c1merk
     c     eppslu_key    chain     eppslur
      *
     c                   eval      epbesk = c2besk
     c                   eval      eppros = c2pros
      *
     c                   time                    epedat
     c                   time                    epetim
     c                   eval      epeusr = l_user
      *
     c                   if        %found
     c                   update    eppslur
     c                   else
     c                   eval      epfirm = w_firm
     c                   eval      epaarr = c1aarr
     c                   eval      epmerk = c1merk
     c                   write     eppslur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1besk = epbesk
     c                   eval      b1pros = eppros
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      eppslr_merk = d1merk
     c     eppslr_key    chain     eppslr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      eppslu_aarr = d1aarr
     c                   eval      eppslu_merk = d1merk
     c     eppslu_key    chain     eppslur
      *
     c                   delete    eppslur
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   endsl
      *
     c                   if        h1aarr = 0
     c                   eval      *in31 = *on
     c                   goto      h1taga
     c                   endif
      *
     c                   if        h1aarr < 2000
     c                   eval      *in32 = *on
     c                   goto      h1taga
     c                   endif
      *
     c                   eval      b2aarr = h1aarr
     c                   eval      eppsl1_aarr = b2aarr
     c                   eval      eppsl1_merk = *blank
     c     eppsl1_key    setll     eppsl1
      *
     c     end_h1win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for å vise at kunder blir oppdatert
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xm1win        begsr
      *
     c     m1taga        tag
      *
     c                   write     m1win
     c                   exfmt     m1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_m1win
     c                   endsl
      *
      * Fyller tabellen
      *
     c                   eval      a_merk(*) = *blank
     c                   eval      a_pros(*) = 0
     c                   eval      x = 0
      *
     c                   eval      eppslr_aarr = b2aarr
     c     eppslr_key2   setll     eppslr
     c     eppslr_key2   reade     eppslr
     c                   dow       not  %eof(eppslr)
     c                   eval      x = x + 1
     c                   eval      a_merk(x) = epmerk
     c                   eval      a_pros(x) = eppros
     c     eppslr_key2   reade     eppslr
     c                   enddo
      *
     c     m1tagb        tag
     c                   eval      ekboiu_bkun = 0
     c     ekboiu_key    setll     ekboiu
     c                   read      ekboiu
      *
     c                   dow       not %eof(ekboiu)
      *
      * Leser tilhørende bonuselementer
      *
     c                   eval      ekbeiu_ebku = ekbkun
     c                   eval      ekbeiu_erka = ekrkat
     c                   eval      ekbeiu_ekun = ekkund
     c                   eval      ekbeiu_ekpr = ekkpro
     c                   eval      ekbeiu_eogr = ekogrp
     c                   eval      ekbeiu_ehgr = ekhgrp
     c                   eval      ekbeiu_eugr = ekugrp
     c                   eval      ekbeiu_eldo = ekldor
     c                   eval      ekbeiu_emod = ekmodn
     c                   eval      ekbeiu_evar = ekvare
     c                   eval      ekbeiu_eenh = ekenhe
     c                   eval      ekbeiu_elet = eklety
     c                   eval      ekbeiu_ebot = ekboty
     c     ekbeiu_key    setll     ekbeiu
     c     ekbeiu_key    reade     ekbeiur
      *
     c                   dow       not %eof(ekbeiu)
      *
      * Finner riktig bonusprosent i hht. kontohenvisning og oppdaterer
      *
     c                   eval      x = %lookup(%subst(ekmerk:1:4):a_merk)
     c                   if        x > 0
     c                   eval      ekebpr = a_pros(x)
     c                   else
     c                   eval      ekebpr = 0
     c                   endif
      *
     c                   eval      ekemrk = ekmerk
     c                   eval      ekeeda = w_date
     c                   eval      ekeeti = w_time
     c                   eval      ekeeus = l_user
     c                   update    ekbeiur
      *
     c     ekbeiu_key    reade     ekbeiur
     c                   enddo
      *
     c                   eval      ekedat = w_date
     c                   eval      eketim = w_time
     c                   eval      ekeusr = l_user
     c                   update    ekboiur
      *
     c                   read      ekboiu
     c                   enddo
      *
     c     end_m1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_aarr = k1aarr
     c                   eval      s_merk = k1merk
      *
      * Fyller kopierings-bildet
      *
     c                   eval      eppslr_aarr = k1aarr
     c                   eval      eppslr_merk = k1merk
     c     eppslr_key    chain     eppslr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1merk = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      eppslr_aarr = k1aarr
     c                   eval      eppslr_merk = k1merk
     c     eppslr_key    chain     eppslr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1aarr = s_aarr
     c                   eval      c1merk = s_merk
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      eppsl2
     c                   other
     c                   read      eppsl1
     c                   endsl
      *
     c                   if        %eof or
     c                             epfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        epaarr <> h1aarr
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      eppsl2_besk = epbesk
     c                   other
     c                   eval      eppsl1_aarr = epaarr
     c                   eval      eppsl1_merk = epmerk
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1merk = epmerk
     c                   eval      b1besk = epbesk
     c                   eval      b1pros = eppros
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     eppsl2_key    setgt     eppsl2
     c                   readp     eppsl2
     c                   other
     c     eppsl1_key    setgt     eppsl1
     c                   readp     eppsl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     eppsl2
     c                   other
     c                   readp     eppsl1
     c                   endsl
      *
     c                   if        %eof or
     c                             epfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      eppsl2_besk = epbesk
     c                   other
     c                   eval      eppsl1_merk = epmerk
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     eppsl2_key    setll     eppsl2
     c                   other
     c     eppsl1_key    setll     eppsl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på Bonusgruppe
      *
     c     eppsl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    eppsl1_aarr
     c                   kfld                    eppsl1_merk
      *
      * Key for posisjonering på beskrivelse
      *
     c     eppsl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    eppsl2_aarr
     c                   kfld                    eppsl2_besk
      *
      * Key for les på Bonusgruppe
      *
     c     eppslr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    eppslr_aarr
     c                   kfld                    eppslr_merk
      *
      * Key for les på Bonusgruppe år
      *
     c     eppslr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    eppslr_aarr
      *
      * Key for oppdatering av Bonusgruppe
      *
     c     eppslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    eppslu_aarr
     c                   kfld                    eppslu_merk
      *
      * Key for oppdatering av bonus
      *
     c     ekboiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboiu_bkun
      *
      * Key for oppdatering av kunde bonuselement
      *
     c     ekbeiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbeiu_ebku
     c                   kfld                    ekbeiu_erka
     c                   kfld                    ekbeiu_ekun
     c                   kfld                    ekbeiu_ekpr
     c                   kfld                    ekbeiu_eogr
     c                   kfld                    ekbeiu_ehgr
     c                   kfld                    ekbeiu_eugr
     c                   kfld                    ekbeiu_eldo
     c                   kfld                    ekbeiu_emod
     c                   kfld                    ekbeiu_evar
     c                   kfld                    ekbeiu_eenh
     c                   kfld                    ekbeiu_elet
     c                   kfld                    ekbeiu_ebot
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
     c                   eval      b2aarr = *year - 1
     c                   eval      h1aarr = b2aarr
     c                   time                    w_date
     c                   time                    w_time
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      eppsl1_aarr = b2aarr
     c                   eval      eppsl1_merk = *blank
     c     eppsl1_key    setll     eppsl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
