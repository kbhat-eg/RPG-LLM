     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LA680R
      * Beskrivelse . . : Kontroll av lagerbeholdning mot milepeler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110402 ASK  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fla680d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter-ut
      *
     d                 ds
     d d_parm                        20
     d  d_firm                        3  0 overlay(d_parm)
     d  d_lage                        2  0 overlay(d_parm:4)
     d  d_val1                        1  0 overlay(d_parm:6)
     d  d_val2                        1  0 overlay(d_parm:7)
     d  d_val3                        1  0 overlay(d_parm:8)
     d  d_dato                         d   datfmt(*iso)
     d                                     overlay(d_parm:9)
     d  d_dumm                        1    overlay(d_parm:20) inz('*')
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_parm          s             18
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
      *
     d b_feil          s              1    inz(*off)
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1BLD  - B1 Skjermbilde for parametervalg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram: Velge lager og angi sjekkparametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Nullstiller/Blanker felter som skal benyttes
      *
     c     start_pgm     tag
      *
     c                   eval      b1lage = 0
     c                   eval      b1latx = *blank
     c                   eval      b1val1 = 0
     c                   eval      b1val2 = 0
     c                   eval      b1val3 = 0
     c                   eval      b1dato = 0
      *
     c     bilde_ut      tag
     c                   exfmt     b1bld
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   goto      end_pgm
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd = *on
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1latx
     c                   goto      bilde_ut
     c                   endif
      *
      *  Er gyldig lager angitt?
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      bilde_ut
     c                   endif
      *
      *  Er gyldig dato angitt?
      *
     c                   if        b1val3 = 1
     c     *dmy          test(d)                 b1dato                 32
     c                   if        *in32 = *on
     c                   goto      bilde_ut
     c                   endif
     c                   endif
      *
      *  Oppkall av kontrollrutine
      *
     c                   if        b1val1 = 0 and
     c                             b1val2 = 0 and
     c                             b1val3 = 0
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      d_firm = l_firm
     c                   eval      d_lage = b1lage
     c                   eval      d_val1 = b1val1
     c                   eval      d_val2 = b1val2
     c                   eval      d_val3 = b1val3
     c                   if        b1val3 = 1
     c     *dmy          move      b1dato        d_dato
     c                   else
     c                   eval      d_dato = *loval
     c                   endif
     c                   eval      w_parm = d_parm
      *
     c                   call      'LA680C'
     c                   parm                    w_parm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
