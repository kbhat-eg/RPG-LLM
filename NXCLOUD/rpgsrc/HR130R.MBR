     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HR130R
      * Beskrivelse . . : Radioterminal trans, varemottak - param
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
5.60  *       191207 bof  Legge ut ordrenr hvis det finnes på bestilling
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  180110 bof  Ekstra sjekk på ean-nr
6.20  * 6.20  070312 bsa  Legger ut lev. ordrenr + siste loggkode
6.21  * 6.10  300113 bsa  Skal ikke være mulig å varemota en bestilling som
6.21  *                   allerede er varemottatt (IP)
6.nu  * 6.30  260514 bof  Sjekket ihht. nummerutvidelse
6.30  * 6.30  040615 bkv  Sjekker om best. låst
6.31  * 6.30  121018 bof  Utvidet med trelast-sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
6.21 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
     fhr130d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d rlevl1_levr     s                   like(rllevr)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
6.21 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_stek          s             35
     d w_lnav          s             30
     d w_kode          s              1
     d w_tims          s               z
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.12 d w_leng          s              2  0
6.12 d w_posi          s              2  0
6.12 d w_eans          s             14
6.31 d w_lv_vare       s                   like(vvvare)
6.12 d c_digi          c                   '0123456789 '
6.12 d c_null          c                   '0000000000000'
      *
6.20 d p_numm          s                   like(lonumm)
6.20 d p_suff          s                   like(losuff)
6.20 d p_logk          s              1
6.20 d p_lbes          s             20
      *
     d b_feil          s              1    inz(*off)
     d b_fonr          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Initierer skjermbildet
      *
     c     b1tag         tag
      *
     c                   eval      *in30  = *on
     c                   eval      *in50  = *off
     c                   eval      b_fonr = *off
     c                   eval      b_feil = *off
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tagb
     c                   endsl
      *
      * Gyldig bestillingsnummer angitt?
      *
     c                   if        b1numm <> 0
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c     lohelr_key    chain     lohelr
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      b1tagb
     c                   endif
6.30 c                   if        lokode <> 0
6.30 c                   eval      *in34 = *on
6.30 c                   goto      b1tagb
6.30 c                   endif
6.21  *
6.21  * Sjekk behandlingskoden på bestillingen
6.21  *
6.21 c                   eval      votyl1_otyp = lootyp
6.21 c     votyl1_key    chain     votyl1
6.21 c                   if        not %found
6.21 c                   eval      *in31 = *on
6.21 c                   goto      b1tagb
6.21 c                   endif
6.21  *
6.21 c                   if        vaoakk > 1
6.21 c                   eval      *in31 = *on
6.21 c                   goto      b1tagb
6.21 c                   endif
6.21  *
     c                   goto      b1tagc
     c                   endif
      *
      * Gjenfinning via leverandør?
      *
     c                   if        b1ldor <> 0
     c                   exsr      sjekk_levr
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
     c                   call      'LO562R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1numm
     c                   parm                    b1suff
      *
     c                   eval      b1ldor = 0
     c                   if        b1numm = 0
     c                   eval      *in31 = *on
     c                   goto      b1tagb
     c                   endif
     c                   goto      b1tagc
     c                   endif
      *
      * Gjenfinning via varenummer?
      *
     c                   if        b1vvar <> *blank
     c                   exsr      sjekk_vare
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
     c                   call      'LO552R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    b1numm
     c                   parm                    b1suff
      *
     c                   eval      b1vvar = *blank
     c                   if        b1numm = 0
     c                   eval      *in31 = *on
     c                   goto      b1tagb
     c                   endif
     c                   goto      b1tagc
     c                   endif
      *
      * Ingen søkebegrep valgt
      *
     c                   if        b1numm = 0
     c                   eval      *in31 = *on
     c                   goto      b1tagb
     c                   endif
      *
      * Finner unik nøkkel
      *
     c     b1tagc        tag
     c                   if        b_fonr = *off
     c                   exsr      finn_onr
     c                   eval      b_fonr = *on
     c                   goto      b1tagb
     c                   endif
     c                   eval      w_kode = 'V'
     c                   time                    w_tims
      *
      * Kaller registeringsprogram
      *
     c                   call      'HR131R'
     c                   parm                    w_firm
     c                   parm                    w_kode
     c                   parm                    w_tims
     c                   parm                    b1numm
     c                   parm                    b1suff
      *
     c                   eval      *in30 = *on
     c                   eval      b1numm = 0
     c                   eval      b1suff = 0
      *
     c                   goto      b1tag
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        wactfe = 'B1LDOR'
     c                   call      'RL580R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Varenummer
      *
     c                   if        wactfe = 'B1VVAR'
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    b1vvar
     c                   parm                    w_stek
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_levr    begsr
      *
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1r
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   endif
      *
     c     end_levr      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne og kontrollere varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vare    begsr
      *
      * EANnr
      *
6.12 c                   eval      w_leng = %len(%trim(b1vvar))
6.12 c     c_digi        check     b1vvar        w_posi
6.12 c                   if        w_leng > 8 and
6.12 c                             w_posi = 0
6.12 c                   eval      w_eans = %subst(c_null:1:14-w_leng) +
6.12 c                                      b1vvar
      *
6.12 c                   movel     w_eans        w_eann
     c                   eval      b_feil = *off
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      vvarl1_vare = w_eava
     c                   goto      les_vare
     c                   endif
6.12 c                   endif
      *
      * Lev vnr
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :b1vvar
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   movel     w_lv_vare     vvarl1_vare
6.31 c                   goto      les_vare
6.31 c                   endif
      *
     c                   movel     b1vvar        vvarl4_lvar
     c     vvarl4_key    chain     vvarl4r
     c                   if        %found
     c                   eval      vvarl1_vare = vvvare
     c                   goto      les_vare
     c                   endif
      *
      * Nobb nr
      *
     c                   movel     b1vvar        vvarl3_nobb
     c     vvarl3_key    chain     vvarl3r
     c                   if        %found
     c                   eval      vvarl1_vare = vvvare
     c                   goto      les_vare
     c                   endif
      *
      * Eget vnr
      *
     c                   movel     b1vvar        vvarl1_vare
      *
     c     les_vare      tag
      *
     c     vvarl1_key    chain     vvarl1r
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne og ordrenr på bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_onr      begsr
     c                   eval      lohelr_numm = b1numm
     c                   eval      lohelr_suff = b1suff
     c     lohelr_key    chain     lohelr
6.20 c                   if        %found
6.20 c                   if        loornr <> 0
     c                   eval      b1ornr = loornr
     c                   eval      *in50  = *on
     c                   endif
6.20 c                   if        losorn <> *blank
6.20 c                   movel     losorn        b1sorn
6.20 c                   eval      *in51  = *on
6.20 c                   endif
6.20  * Sjekk om det finnes loggkoder på ordren
6.20 c                   eval      p_numm = b1numm
6.20 c                   eval      p_suff = b1suff
6.20 c                   eval      p_logk = *blank
6.20 c                   eval      p_lbes = *blank
6.20 c                   call      'LM501R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_numm
6.20 c                   parm                    p_suff
6.20 c                   parm                    p_logk
6.20 c                   parm                    p_lbes
6.20 c                   if        p_logk <> *blank
6.20 c                   eval      *in52  = *on
6.20 c                   eval      b1logk = p_logk
6.20 c                   eval      b1lbes = p_lbes
6.20 c                   endif
6.20 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les på nobb nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      *  Key for les på leverandørens varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *  Key for les av BESTILLING
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
6.21  *
6.21  *  Key for les av ordretype
6.21  *
6.21 c     votyl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    votyl1_otyp
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
