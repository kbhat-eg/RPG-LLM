     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VK900R
      * Beskrivelse . . : Oppretter alternativ varegruppe i varereg.
      *
      *                   Leser vareregister, og oppretter alternativ
      *                   varegruppe på varer uten alternativ varegruppe.
      *                   Alternativ varegruppe hentes fra varegruppe-
      *                   knytnings-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040203 HKO  Nytt program
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvarlu_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_ahgr          s                   like(vvahgr)
     d w_augr          s                   like(vvaugr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser vareregister
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1
     c                   dow       not %eof
      *
      * Leser neste dersom alternativ varegruppe allerede er utfylt
      *
     c                   if        vvahgr <> 0 and
     c                             vvaugr <> 0
     c                   goto      neste_vare
     c                   endif
      *
      * Henter alternativ varegruppe fra varegruppeknytning
      *
     c                   call      'VK700R'
     c                   parm                    w_firm
     c                   parm                    vvogrp
     c                   parm                    vvhgrp
     c                   parm                    vvugrp
     c                   parm                    vvldor
     c                   parm                    w_ahgr
     c                   parm                    w_augr
      *
      * Oppdaterer EVR vareregister
      *
     c                   eval      vvarlu_vare = vvvare
     c     vvarlu_key    chain     vvarlur
     c                   if        %found
     c                   eval      vvahgr = w_ahgr
     c                   eval      vvaugr = w_augr
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarlur
     c                   endif
      *
     c     neste_vare    tag
      *
     c     vvarl1_key    reade     vvarl1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
