     h option(*nodebugio)
      /TITLE  ASOFAK Lev.vare, henter vareregister - Luna
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW705R
      * Beskrivelse . . : Danning av leverandør vareregister, Luna
      *
      *                   Prisdato settes lik dagens dato
      *
      *                   Ved artikkelfil benyttes enhet fra Luna
      *
      *                   Ved prisoppdatering hentes enhet og varetekst
      *                   fra vareregisteret. Enhet settes lik basisenhet
      *                   og varetekst lik varetekst-1
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       041203 GRO  Modif. til håndtering av artikkel- & prisfil i
      *                   samme inputfil (NB! Artikler først; deretter pris)
      *       171203 GRO  Håndtering av 12-sifret EAN-nr (uannonsert endr.)
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fflval2    if   e           k disk    rename(flvapfr:flval2r)
     fflvalu    uf a e           k disk    rename(flvapfr:flvalur)
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Luna
      *
     d                 ds
     d d_inpu                         3
     d  d_type                        3    overlay(d_inpu:1)                    Posttype
      *
      *  Recordbeskrivelse - Luna (artikkelregister)
      *
     d                 ds
     d d_ainp                       200
     d  d_avar                       24    overlay(d_ainp:4)                    Varenr
     d* d_aean                       13  0 overlay(d_ainp:28)                   EAN-nr
     d  d_aean                       13    overlay(d_ainp:28)                   EAN-nr
     d  d_atek                       30    overlay(d_ainp:41)                   Varetekst
     d  d_aenh                        3    overlay(d_ainp:75)                   Enhet
     d  d_aspr                        9  2 overlay(d_ainp:89)                   Salgspris
     d  d_aipr                        9  2 overlay(d_ainp:98)                   Innkjøpspris
     d  d_aeva                       10    overlay(d_ainp:127)                  Erstatnings varenr.
     d  d_aeko                        1    overlay(d_ainp:137)                  U=Utgått
      *
      *  Recordbeskrivelse - Luna (prisoppdatering)
      *
     d                 ds
     d d_pinp                        80
     d  d_pvar                       11    overlay(d_pinp:4)                    Varenr
     d* d_pean                       13  0 overlay(d_pinp:15)                   EAN-nr
     d  d_pean                       13    overlay(d_pinp:15)                   EAN-nr
     d  d_pspr                        9  2 overlay(d_pinp:49)                   Salgspris
     d  d_prab                        3  1 overlay(d_pinp:58)                   Totalrabatt
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_ldor          s                   like(vvldor)
      *
      * Definering av variabler i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
     d vvarl5_lvar     s                   like(vvlvar)
     d flval2_lvar     s                   like(fwlvar)
     d flvalu_line     s                   like(fwline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_ldor          s                   like(vvldor)
     d w_eanx          s             13
     d w_eann          s             13  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = fwirad
      *
     c                   select
     c                   when      d_type = '100'
     c                   eval      d_ainp = fwirad
     c                   exsr      artikkel
     c                   when      d_type = '001'
     c                   eval      d_pinp = fwirad
     c                   exsr      prisoppdat
     c                   endsl
      *
     c                   read      flvwpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling ved les av artikkelregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     artikkel      begsr
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = d_aeko
     c                   eval      fwtek1 = d_atek
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_avar
      *
     c                   eval      w_eann = *zero
     c                   eval      w_eanx = *blank
      *
     c                   if        %len(%trim(d_aean)) < 13
     c                   evalr     w_eanx = %trim(d_aean)
     c                   eval      w_eanx = %xlate(' ':'0':w_eanx)
     c                   move      w_eanx        w_eann
     c                   else
     c                   move      d_aean        w_eann
     c                   endif
      *
     c*                  eval      fweann = d_aean
     c                   eval      fweann = w_eann
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = d_aenh
     c                   eval      fwinpr = d_aipr
     c                   eval      fwsapr = d_aspr
     c                   eval      fwdato = *loval
     c                   eval      fwudat = *loval
     c***                eval      fwlvae = d_aeva
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   eval      fenkl1_enhf = d_aenh
     c     fenkl1_key    chain     fenkl1
     c                   if        %found
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c                   time                    fwdato
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvalur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling ved les av prisoppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prisoppdat    begsr
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      flval2_lvar = d_pvar
     c     flval2_key    setll     flval2
     c     flval2_key    reade     flval2
     c                   if        not %eof
     c                   eval      flvalu_line = fwline
     c     flvalu_key    chain     flvalur
     c                   if        %found
     c                   eval(h)   fwinpr = d_pspr - (d_pspr * d_prab / 100)
     c                   eval      fwsapr = d_pspr
     c                   update    flvalur
     c                   endif
     c                   leavesr
     c                   endif
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwtek1 = *blank
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_pvar
      *
     c                   eval      w_eann = *zero
     c                   eval      w_eanx = *blank
      *
     c                   if        %len(%trim(d_pean)) < 13
     c                   evalr     w_eanx = %trim(d_pean)
     c                   eval      w_eanx = %xlate(' ':'0':w_eanx)
     c                   move      w_eanx        w_eann
     c                   else
     c                   move      d_pean        w_eann
     c                   endif
      *
     c*                  eval      fweann = d_pean
     c                   eval      fweann = w_eann
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = *blank
     c                   eval(h)   fwinpr = d_pspr - (d_pspr * d_prab / 100)
     c                   eval      fwsapr = d_pspr
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   time                    fwdato
      *
     c                   if        fwlvar <> *blank
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5
     c                   if        %found
     c                   eval      fwtek1 = vvtek1
     c                   eval      fwenhe = vvenhe
     c                   endif
     c                   endif
      *
     c                   write     flvalur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppslag på vare-register med leverandørs varenummer
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppslag med leverandørs varenummer
      *
     c     flval2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval2_lvar
      *
      * Key for oppdatering av leverandør vareregister
      *
     c     flvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flvalu_line
      *
      *
      * Henter firma og leverandør fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
     c                   endsr
