     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FR160R
      * Beskrivelse . . : Fjerner rabatter på varenivå eller på hovedgruppe/
      *                   undergruppe-nivå dersom lik rabatt er knyttet
      *                   mot samme kjøper og varegruppe høyere i nivået.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       171103 HKO  Nytt program
      *       130504 HKO  Utvidet til også å teste rabatter på varegrupper
      *                   Ved rabatt på varenivå og priskode sjekkes det kun
      *                   mot samme rabatt på samme priskode på det høyere
      *                   nivået.
5.70  * PRGR  211008 bof  Utvidet med prisgruppe                       per
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffrablu    uf   e           k disk    rename(frabpfr:frablur)
     ffrablr    if   e           k disk    rename(frabpfr:frablrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     ffr160d    cf   e             workstn
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
5.70 d frablr_prgr     s                   like(frprgr)
     d frablr_rkat     s                   like(frrkat)
     d frablr_kkat     s                   like(frkkat)
     d frablr_kund     s                   like(frkund)
     d frablr_kpro     s                   like(frkpro)
     d frablr_ogrp     s                   like(frogrp)
     d frablr_hgrp     s                   like(frhgrp)
     d frablr_ugrp     s                   like(frugrp)
     d frablr_ldor     s                   like(frldor)
     d frablr_modn     s                   like(frmodn)
     d frablr_vare     s                   like(frvare)
     d frablr_enhe     s                   like(frenhe)
     d frablr_lety     s                   like(frlety)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(frfirm)
      *
     d s_rab1          s                   like(frrab1)
     d s_ra1d          s                   like(frra1d)
     d s_ra1f          s                   like(frra1f)
     d s_ra1t          s                   like(frra1t)
     d s_rab2          s                   like(frrab2)
     d s_ra2d          s                   like(frra2d)
     d s_ra2f          s                   like(frra2f)
     d s_ra2t          s                   like(frra2t)
     d s_rabn          s                   like(frrabn)
     d s_rand          s                   like(frrand)
     d s_ranf          s                   like(frranf)
     d s_rant          s                   like(frrant)
     d s_rabb          s                   like(frrabb)
     d s_rabd          s                   like(frrabd)
     d s_rbuf          s                   like(frrbuf)
     d s_rbut          s                   like(frrbut)
     d s_rabt          s                   like(frrabt)
     d s_ratd          s                   like(frratd)
     d s_ratf          s                   like(frratf)
     d s_ratt          s                   like(frratt)
     d s_dekn          s                   like(frdekn)
     d s_dekd          s                   like(frdekd)
     d s_dekf          s                   like(frdekf)
     d s_dekt          s                   like(frdekt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Leser rabattmatrise
      *
     c     frablu_key    setll     frablu
     c     frablu_key    reade     frablur
     c                   dow       not %eof
      *
      * Tar vare på betingelsen
      *
     c                   eval      s_rab1 = frrab1
     c                   eval      s_ra1d = frra1d
     c                   eval      s_ra1f = frra1f
     c                   eval      s_ra1t = frra1t
     c                   eval      s_rab2 = frrab2
     c                   eval      s_ra2d = frra2d
     c                   eval      s_ra2f = frra2f
     c                   eval      s_ra2t = frra2t
     c                   eval      s_rabn = frrabn
     c                   eval      s_rand = frrand
     c                   eval      s_ranf = frranf
     c                   eval      s_rant = frrant
     c                   eval      s_rabb = frrabb
     c                   eval      s_rabd = frrabd
     c                   eval      s_rbuf = frrbuf
     c                   eval      s_rbut = frrbut
     c                   eval      s_rabt = frrabt
     c                   eval      s_ratd = frratd
     c                   eval      s_ratf = frratf
     c                   eval      s_ratt = frratt
     c                   eval      s_dekn = frdekn
     c                   eval      s_dekd = frdekd
     c                   eval      s_dekf = frdekf
     c                   eval      s_dekt = frdekt
      *
      * Behandling avhengig av nivå
      *
     c                   select
     c                   when      frvare <> *blank
     c                   exsr      behandl_vare
     c                   when      frhgrp <> 0
     c                   exsr      behandl_vgrp
     c                   endsl
      *
     c     frablu_key    reade     frablur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av knytning på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vare  begsr
      *
      * Henter varegruppe fra vare
      *
     c                   eval      vvarl1_vare = frvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found or
     c                             vvogrp = 0 or
     c                             vvhgrp = 0 or
     c                             vvugrp = 0
     c                   leavesr
     c                   endif
      *
      * Sjekker om samme betingelse finnes på høyere nivå
      *
5.70 c                   eval      frablr_prgr = frprgr
     c                   eval      frablr_rkat = frrkat
     c                   eval      frablr_kkat = frkkat
     c                   eval      frablr_kund = frkund
     c                   eval      frablr_kpro = frkpro
     c                   eval      frablr_ogrp = vvogrp
     c                   eval      frablr_hgrp = vvhgrp
     c                   eval      frablr_ugrp = vvugrp
     c                   eval      frablr_ldor = frldor
     c                   eval      frablr_modn = frmodn
     c                   eval      frablr_vare = *blank
     c                   eval      frablr_enhe = frenhe
     c                   eval      frablr_lety = frlety
     c     frablr_key    chain     frablr
     c                   if        not %found
     c                   eval      frablr_ugrp = 0
     c     frablr_key    chain     frablr
     c                   if        not %found
     c                   eval      frablr_hgrp = 0
     c     frablr_key    chain     frablr
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
      * Dersom rabatt på varenummernivå og varen har priskode utfylt, testes
      * det kun mot rabatten mot denne priskoden
      *
     c                   if        vvkpri = 'N' and
     c                             frrabn <> 0
     c                   eval      s_rab1 = frrab1
     c                   eval      s_ra1d = frra1d
     c                   eval      s_ra1f = frra1f
     c                   eval      s_ra1t = frra1t
     c                   eval      s_rab2 = frrab2
     c                   eval      s_ra2d = frra2d
     c                   eval      s_ra2f = frra2f
     c                   eval      s_ra2t = frra2t
     c                   eval      s_rabb = frrabb
     c                   eval      s_rabd = frrabd
     c                   eval      s_rbuf = frrbuf
     c                   eval      s_rbut = frrbut
     c                   endif
      *
     c                   if        vvkpri = 'B' and
     c                             frrabb <> 0
     c                   eval      s_rab1 = frrab1
     c                   eval      s_ra1d = frra1d
     c                   eval      s_ra1f = frra1f
     c                   eval      s_ra1t = frra1t
     c                   eval      s_rab2 = frrab2
     c                   eval      s_ra2d = frra2d
     c                   eval      s_ra2f = frra2f
     c                   eval      s_ra2t = frra2t
     c                   eval      s_rabn = frrabb
     c                   eval      s_rand = frrabd
     c                   eval      s_ranf = frranf
     c                   eval      s_rant = frrant
     c                   endif
      *
      * Sletter varebetingelse fra rabattmatrisen dersom betingelsen på
      * det høyere nivået er likt det som ligger på varen
      *
     c                   if        frrab1 = s_rab1 and
     c                             frra1d = s_ra1d and
     c                             frra1f = s_ra1f and
     c                             frra1t = s_ra1t and
     c                             frrab2 = s_rab2 and
     c                             frra2d = s_ra2d and
     c                             frra2f = s_ra2f and
     c                             frra2t = s_ra2t and
     c                             frrabn = s_rabn and
     c                             frrand = s_rand and
     c                             frranf = s_ranf and
     c                             frrant = s_rant and
     c                             frrabb = s_rabb and
     c                             frrabd = s_rabd and
     c                             frrbuf = s_rbuf and
     c                             frrbut = s_rbut and
     c                             frrabt = s_rabt and
     c                             frratd = s_ratd and
     c                             frratf = s_ratf and
     c                             frratt = s_ratt and
     c                             frdekn = s_dekn and
     c                             frdekd = s_dekd and
     c                             frdekf = s_dekf and
     c                             frdekt = s_dekt
      *
     c                   delete    frablur
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av knytning på hovedgruppe/undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vgrp  begsr
      *
      * Sjekker om samme betingelse finnes på høyere nivå
      *
5.70 c                   eval      frablr_prgr = frprgr
     c                   eval      frablr_rkat = frrkat
     c                   eval      frablr_kkat = frkkat
     c                   eval      frablr_kund = frkund
     c                   eval      frablr_kpro = frkpro
     c                   eval      frablr_ogrp = frogrp
     c                   eval      frablr_hgrp = 0
     c                   eval      frablr_ugrp = 0
     c                   eval      frablr_ldor = frldor
     c                   eval      frablr_modn = 0
     c                   eval      frablr_vare = *blank
     c                   eval      frablr_enhe = frenhe
     c                   eval      frablr_lety = frlety
      *
     c                   if        frugrp <> 0
     c                   eval      frablr_hgrp = frhgrp
     c                   endif
      *
     c     frablr_key    chain     frablr
     c                   if        not %found
     c                   eval      frablr_hgrp = 0
     c     frablr_key    chain     frablr
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sletter betingelse fra rabattmatrisen dersom betingelsen på
      * det høyere nivået er likt det som ligger på nivået
      *
     c                   if        frrab1 = s_rab1 and
     c                             frra1d = s_ra1d and
     c                             frra1f = s_ra1f and
     c                             frra1t = s_ra1t and
     c                             frrab2 = s_rab2 and
     c                             frra2d = s_ra2d and
     c                             frra2f = s_ra2f and
     c                             frra2t = s_ra2t and
     c                             frrabn = s_rabn and
     c                             frrand = s_rand and
     c                             frranf = s_ranf and
     c                             frrant = s_rant and
     c                             frrabb = s_rabb and
     c                             frrabd = s_rabd and
     c                             frrbuf = s_rbuf and
     c                             frrbut = s_rbut and
     c                             frrabt = s_rabt and
     c                             frratd = s_ratd and
     c                             frratf = s_ratf and
     c                             frratt = s_ratt and
     c                             frdekn = s_dekn and
     c                             frdekd = s_dekd and
     c                             frdekf = s_dekf and
     c                             frdekt = s_dekt
      *
     c                   delete    frablur
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les/oppdatering av rabattmatrise
      *
     c     frablu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på rabattmatrise
      *
     c     frablr_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    frablr_prgr
     c                   kfld                    frablr_rkat
     c                   kfld                    frablr_kkat
     c                   kfld                    frablr_kund
     c                   kfld                    frablr_kpro
     c                   kfld                    frablr_ogrp
     c                   kfld                    frablr_hgrp
     c                   kfld                    frablr_ugrp
     c                   kfld                    frablr_ldor
     c                   kfld                    frablr_modn
     c                   kfld                    frablr_vare
     c                   kfld                    frablr_enhe
     c                   kfld                    frablr_lety
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
