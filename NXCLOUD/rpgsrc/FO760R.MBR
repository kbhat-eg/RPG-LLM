     h/title  ASOFAK: Oppdatering av lager-register utfra ordre-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO760R
      * Beskrivelse . . : Oppdatering av lager-register utfra ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050313 AMD  Dette programmet er en helt ny versjon av FO760R
      *                   og er skrevet pga. problemer med replikering i
      *                   2DPoint.  Her skal kun avvik oppdateres, og ikke
      *                   alle poster hver gang som i gammel versjon. Det
      *                   er derfor også sterkt forenklet.
6.26  *       180413 AMD  Måtte ha med mer logikk
6.27  *       208013 bof  Teste på leveringstype <> 1
6.nu  *       230614 bof  gjennomgått mtp. nummerutvidelse
6.30  *       200217 bkv  Kun oppdatere hvis Kode, oppd. lager = 1
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.26 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.26 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlx    if   e           k disk    rename(fodtpfr:fodtlxr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
      *
     d                uds
     d l_user                911    920
     d  dssfir               944    946  0
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_anta          s                   like(fdanta)
     d w_oord          s                   like(vloord)
     d w_updt          s              1  0
6.26 d w_omrl          s                   like(veomre)
6.26 d w_omrs          s                   like(veomre)
      *
      * Definering av variable i nøkler
      *
6.26 d vvarl1_vare     s                   like(vvvare)
6.26 d vvenl1_vare     s                   like(vevare)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtlx_lage     s                   like(fdlage)
     d fodtlx_vare     s                   like(fdvare)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser igjennom LAGER-REGISTER for å sjekke avvik
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vlaglu_vare = *blank
     c                   eval      vlaglu_lage = *zero
     c     vlaglu_key    setll     vlaglur
     c                   read      vlaglur
      *
     c                   dow       not %eof
      *
     c                   if        vlfirm <> w_firm
     c                   unlock    vlaglu
     c                   leave
     c                   endif
      *
     c                   exsr      sjek_ordr
      *
      *> Ikke i bruk               vlpakk = 0
      *> Ikke i bruk               vlores = 0
      *
     c                   if        vloord = w_oord
     c                   eval      w_updt = 0
     c                   endif
      *
     c                   if        w_updt = 1
     c                   eval      vloord = w_oord
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   else
     c                   unlock    vlaglu
     c                   endif
      *
     c                   read      vlaglur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekke ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   sjek_ordr     begsr
      *
     c                   eval      w_updt = 1
     c                   eval      w_oord = 0
      *
     c                   eval      fodtlx_lage = vllage
     c                   eval      fodtlx_vare = vlvare
     c     fodtlx_key    setll     fodtlx
     c     fodtlx_key    reade     fodtlx
      *
     c                   dow       not %eof
      *
6.27 c                   if        fdvare <> *blank and
6.27 c                             fdlety <> 1
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c                   eval      fohel1_numm = fdnumm
     c                   eval      fohel1_suff = fdsuff
     c     fohel1_key    chain     fohel1r
     c                   if        %found(fohel1)
      *
      *  Hent opplysninger fra Ordre-type
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
      *
      *  Hent opplysninger fra Linje-type
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1r
      *
      * Oppdatering skal bare foregå for
      * behandlingskode som = 1  og
6.30  * Kode, oppd. lager = 1
      *
     c                   if        vaoakk = 1
6.30 c                             and vaolag = 1
     c                   eval      w_anta = fdanta
      *
      * Sjekk om negativ ordretype
      *
     c                   if        vaokre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
      * Sjekk om negativ linjetype
      *
     c                   if        valkre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
6.26  *
6.26  * Regn om antallet dersom nødvendig
6.26  *
6.26 c                   exsr      omr_antall
      *
     c                   eval      w_oord = w_oord + w_anta
      *
     c                   endif
     c                   endif
     c                   endif
      *
     c     fodtlx_key    reade     fodtlx
     c                   enddo
      *
     csr                 endsr
6.26  *
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  * Subrutine for omregning av antall i forhold til lagerenhet
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  *
6.26 c     omr_antall    begsr
6.26  *
6.26  * Henter vareinformasjon
6.26  *
6.26 c                   eval      vvarl1_vare = fdvare
6.26 c     vvarl1_key    chain     vvarl1r
6.26 c                   if        not %found(vvarl1)
6.26 c                   goto      end_omr
6.26 c                   endif
6.26  *
6.26  * Dersom solgt enhet er lik lagerenhet beholdes antall
6.26  *
6.26 c                   if        fdenh1 = vvenhl
6.26 c                   goto      end_omr
6.26 c                   endif
6.26  *
6.26  * Finner omregningsfaktor volum for lagerenhet og for solgt enhet
6.26  * Antall regnes om etter forholdet mellom disse
6.26  *
6.26 c                   eval      vvenl1_vare = fdvare
6.26 c     vvenl1_key    setll     vvenl1
6.26 c     vvenl1_key    reade     vvenl1                                 90
6.26 c                   dow       *in90 = *off
6.26 c                   if        fdenh1 = veenhe
6.26 c                   eval      w_omrs = veomre
6.26 c                   endif
6.26 c                   if        vvenhl = veenhe
6.26 c                   eval      w_omrl = veomre
6.26 c                   endif
6.26 c     vvenl1_key    reade     vvenl1                                 90
6.26 c                   enddo
6.26  *
6.26 c                   if        w_omrs = 0
6.26 c                   eval      w_omrs = 1
6.26 c                   endif
6.26  *
6.26 c                   if        w_omrl = 0
6.26 c                   eval      w_omrl = 1
6.26 c                   endif
6.26  *
6.26 c                   eval      w_anta = w_anta * w_omrs / w_omrl
6.26  *
6.26 c     end_omr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   *inzsr        begsr
6.26  *
6.26  * Key for oppslag på vare-register
6.26  *
6.26 c     vvarl1_key    klist
6.26 c                   kfld                    w_firm
6.26 c                   kfld                    vvarl1_vare
6.26  *
6.26  * Key for les på vare-enhet-register
6.26  *
6.26 c     vvenl1_key    klist
6.26 c                   kfld                    w_firm
6.26 c                   kfld                    vvenl1_vare
      *
      *  Key for file : LAGER-REGISTER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlx_lage
     c                   kfld                    fodtlx_vare
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : LINJE-TYPE-REGISTER
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *  Legger inn firmanummer i nøklene
      *
     c                   eval      w_firm = dssfir
      *
     csr                 endsr
