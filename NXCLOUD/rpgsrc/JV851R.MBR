     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV851R
      * Beskrivelse . . : Oppdater fra NIB, pris
      *
      *                   Oppdaterer/oppretter priser
      *                   Norgros/NIB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111105 GRo  Nytt program
      *       111207 bof  Utvidet vare-pris med gammel pris
6.10  *       160210 bsa  Endringer i databasefiler
6.11  *       120510 bof  Gjeninnført jxeann, skal utgå etterhvert..
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnxvppf    uf   e           k disk
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
      *
      *  Dataelementer vare
      *
     d                 ds
     d d_vrec                        20
     d  d_vare                        8    overlay(d_vrec:1)
     d  d_nobb                        8    overlay(d_vrec:9)
     d   d_nobb_num                   8  0 overlay(d_nobb:1)
      *
      *  Dataelementer pris
      *
     d                 ds
     d d_prec                       200
     d  d_stat                        1    overlay(d_prec:2)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_ldor                        6    overlay(d_prec:3)
     d   d_ldor_num                   6  0 overlay(d_ldor:1)
     d  d_lvar                       15    overlay(d_prec:9)
     d  d_fdat                       10    overlay(d_prec:24)
     d   d_fdat_iso                    d   overlay(d_fdat:1) datfmt(*iso)
     d  d_tdat                       10    overlay(d_prec:34)
     d   d_tdat_iso                    d   overlay(d_tdat:1) datfmt(*iso)
     d  d_nobp                       10    overlay(d_prec:44)
     d   d_nobp_kx                    7    overlay(d_nobp:1)
     d   d_nobp_kr                    7  0 overlay(d_nobp_kx:1)
     d   d_nobp_Ørx                   2    overlay(d_nobp:9)
     d   d_nobp_Øre                   2  2 overlay(d_nobp_Ørx:1)
     d  d_valu                        3    overlay(d_prec:54)
     d  d_eann                       13    overlay(d_prec:57)
     d   d_eann_num                  13  0 overlay(d_eann:1)
     d  d_inkp                       10    overlay(d_prec:70)
     d   d_inkp_kx                    7    overlay(d_inkp:1)
     d   d_inkp_kr                    7  0 overlay(d_inkp_kx:1)
     d   d_inkp_Ørx                   2    overlay(d_inkp:9)
     d   d_inkp_Øre                   2  2 overlay(d_inkp_Ørx:1)
     d  d_kosp                       10    overlay(d_prec:80)
     d   d_kosp_kx                    7    overlay(d_kosp:1)
     d   d_kosp_kr                    7  0 overlay(d_kosp_kx:1)
     d   d_kosp_Ørx                   2    overlay(d_kosp:9)
     d   d_kosp_Øre                   2  2 overlay(d_kosp_Ørx:1)
     d  d_salp                       10    overlay(d_prec:90)
     d   d_salp_kx                    7    overlay(d_salp:1)
     d   d_salp_kr                    7  0 overlay(d_salp_kx:1)
     d   d_salp_Ørx                   2    overlay(d_salp:9)
     d   d_salp_Øre                   2  2 overlay(d_salp_Ørx:1)
     d  d_frak                       10    overlay(d_prec:100)
     d   d_frak_kx                    7    overlay(d_frak:1)
     d   d_frak_kr                    7  0 overlay(d_frak_kx:1)
     d   d_frak_Ørx                   2    overlay(d_frak:9)
     d   d_frak_Øre                   2  2 overlay(d_frak_Ørx:1)
     d  d_noda                       10    overlay(d_prec:110)
     d   d_noda_iso                    d   overlay(d_noda:1) datfmt(*iso)
     d  d_neda                       10    overlay(d_prec:120)
     d  d_odat                       10    overlay(d_prec:130)
     d  d_edat                       10    overlay(d_prec:140)
     d  d_etim                       10    overlay(d_prec:150)
      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_kild          s              1    inz(*off)
     d b_pris          s              1    inz(*off)
     d b_vfel          s              1    inz(*off)
     d b_pfel          s              1    inz(*off)
     d b_blnk          s              1    inz(*off)
      *
     d w_stda          s              3  0
     d w_slda          s              3  0
     d w_welm          s            400
     d w_wlen          s              3  0
     d w_decp          s              3  0
      *
     d w_nobp          s                   like(jxpris)
     d w_inkp          s                   like(jxpris)
     d w_salp          s                   like(jxpris)
     d w_kosp          s                   like(jxpris)
     d w_frak          s                   like(jxfbel)
      *
     d w_str_i         s            400
     d w_str_u         s            400
     d w_posi          s              4  0
     d w_posu          s              4  0
     d w_str_x         s              1
      *
     d w_firm          s              3  0
      *
      *  Konstanter
      *
     d c_apos          c                   x'7D'
     d c_digits        c                   ' 0123456789'
     d c_nsep          c                   ','
     d c_pris_max      c                   9999999,99
      *
     d c_NIBk          c                   '0'
     d c_NVk           c                   '3'
      *
      * XML string konstanter
      *
     d c_xsle          c                   '</'
     d c_xend          c                   '>'
      *
      * XML feltnavn - meldingsspesifikke
      *
      * Hovedstruktur - Avsender
      *
     d c_xstr          c                   '<Avsender>'
     d c_kild          c                   '<Kilde>'
      *
      * Substruktur #1 - Vare
      *
     d c_vare_s        c                   '<Vare>'
     d c_vare_e        c                   '</Vare>'
     d c_nibn          c                   '<NIBnr>'
     d c_nobb          c                   '<NOBBnr>'
      *
      * Substruktur #2 - Pris
      *
     d c_pris_s        c                   '<Pris>'
     d c_pris_e        c                   '</Pris>'
     d c_levr          c                   '<Lev>'
     d c_lvar          c                   '<Lev_varenr>'
     d c_fdat          c                   '<Fra_dato>'
     d c_stat          c                   '<Status>'
     d c_nobp          c                   '<Nobbpris>'
     d c_tdat          c                   '<Til_dato>'
     d c_valu          c                   '<Valuta>'
     d c_eann          c                   '<EANnr>'
     d c_inkp          c                   '<Innkjopspris>'
     d c_kosp          c                   '<Kostpris>'
     d c_salp          c                   '<Salgspris>'
     d c_frak          c                   '<Frakt>'
     d c_nbdo          c                   '<Nobb_oppr_dato>'
     d c_nbde          c                   '<Nobb_endr_dato>'
     d c_opda          c                   '<Oppr_dato>'
     d c_enda          c                   '<Endr_dato>'
     d c_entd          c                   '<Endr_tidspunkt>'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_inlr = *on
      *
      * Leser XML-fil vare/pris
      *
     c                   read      nxvppfr
     c                   dow       not %eof(nxvppf)
      *
     c                   if        b_kild = *on
     c                   if        %subst(nxvpri:1:6) = c_vare_s
     c                   exsr      beh_vare
     c                   endif
     c                   endif
      *
      *  Finn gyldig avsender/kilde
      *
     c                   eval      w_stda = %scan(c_xend:nxvpri:1)
     c                   eval      w_slda = %scan(c_xsle:nxvpri:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxvpri:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxvpri:1:7) = c_kild
     c                   exsr      uttr_kild
     c                   endsl
      *
     c                   delete    nxvppfr
      *
     c                   read      nxvppfr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av avsenderelement : Kilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_kild     begsr
      *
     c                   eval      b_kild = *off
      *
     c                   if        %trim(w_welm) = c_NIBk
     c                   eval      b_kild = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_vare      begsr
      *
     c                   eval      b_vfel = *off
     c                   exsr      vare_init
      *
     c                   dow       not %eof(nxvppf)
      *
      *  Start på priselement
      *
     c                   if        %subst(nxvpri:1:6) = c_pris_s
      *
     c                   if        b_vfel = *off
     c                   exsr      beh_pris
     c                   endif
      *
     c                   endif
      *
      *  Slutt på vareelement
      *
     c                   if        %subst(nxvpri:1:7) = c_vare_e
     c                   leave
     c                   endif
      *
      *  Enkeltelement varenivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxvpri:1)
     c                   eval      w_slda = %scan(c_xsle:nxvpri:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxvpri:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxvpri:1:7) = c_nibn
     c                   exsr      uttr_nibn
     c                   when      %subst(nxvpri:1:8) = c_nobb
     c                   exsr      uttr_nobb
     c                   endsl
      *
     c                   delete    nxvppfr
      *
     c                   read      nxvppfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vare_init     begsr
      *
     c                   eval      d_vrec = *blank
     c                   eval      d_nobb_num = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : NIB-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nibn     begsr
      *
     c                   if        w_wlen = 8
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_vare = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : NOBB-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nobb     begsr
      *
     c                   eval      d_nobb_num = *zero
      *
     c                   if        w_wlen = 8
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_nobb = %trim(w_welm)
     c                   else
     c                   eval      b_vfel = *on
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av prisinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_pris      begsr
      *
     c                   eval      b_pfel = *off
     c                   eval      d_prec = *blank
     c                   exsr      pris_init
      *
     c                   exsr      les_vare
      *
     c                   dow       not %eof(nxvppf)
      *
      *  Slutt på priselement
      *
     c                   if        %subst(nxvpri:1:7) = c_pris_e
     c                   exsr      skr_pris
     c                   leave
     c                   endif
      *
      *  Enkeltelement prisnivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxvpri:1)
     c                   eval      w_slda = %scan(c_xsle:nxvpri:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxvpri:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxvpri:1:5) = c_levr
     c                   exsr      uttr_levr
     c                   when      %subst(nxvpri:1:12) = c_lvar
     c                   exsr      uttr_lvar
     c                   when      %subst(nxvpri:1:10) = c_fdat
     c                   exsr      uttr_fdat
     c                   when      %subst(nxvpri:1:8) = c_stat
     c                   exsr      uttr_stat
     c                   when      %subst(nxvpri:1:10) = c_nobp
     c                   exsr      uttr_nobp
     c                   when      %subst(nxvpri:1:10) = c_tdat
     c                   exsr      uttr_tdat
     c                   when      %subst(nxvpri:1:8) = c_valu
     c                   exsr      uttr_valu
     c                   when      %subst(nxvpri:1:7) = c_eann
     c                   exsr      uttr_eann
     c                   when      %subst(nxvpri:1:14) = c_inkp
     c                   exsr      uttr_inkp
     c                   when      %subst(nxvpri:1:10) = c_kosp
     c                   exsr      uttr_kosp
     c                   when      %subst(nxvpri:1:11) = c_salp
     c                   exsr      uttr_salp
     c                   when      %subst(nxvpri:1:7) = c_frak
     c                   exsr      uttr_frak
     c                   when      %subst(nxvpri:1:16) = c_nbdo
     c                   exsr      uttr_nbdo
     c                   when      %subst(nxvpri:1:16) = c_nbde
     c                   exsr      uttr_nbde
     c                   when      %subst(nxvpri:1:11) = c_opda
     c                   exsr      uttr_opda
     c                   when      %subst(nxvpri:1:11) = c_enda
     c                   exsr      uttr_enda
     c                   when      %subst(nxvpri:1:16) = c_entd
     c                   exsr      uttr_entd
     c                   endsl
      *
     c                   delete    nxvppfr
      *
     c                   read      nxvppfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av prisinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pris_init     begsr
      *
     c                   eval      d_prec = *blank
     c                   eval      d_ldor_num = *zero
     c                   eval      d_fdat_iso = *loval
     c                   eval      d_tdat_iso = *loval
     c                   eval      d_nobp_kr  = *zero
     c                   eval      d_nobp_Øre = *zero
     c                   eval      d_eann_num = *zero
      *
     c                   eval      d_inkp_kr  = *zero
     c                   eval      d_inkp_Øre = *zero
     c                   eval      d_kosp_kr  = *zero
     c                   eval      d_kosp_Øre = *zero
     c                   eval      d_salp_kr  = *zero
     c                   eval      d_salp_Øre = *zero
     c                   eval      d_frak_kr  = *zero
     c                   eval      d_frak_Øre = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for les av tilhørende varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      b_vfel = *off
      *
     c                   if        jaanob = 1
     c                   eval      jvarl1_vare = d_nobb
     c                   else
     c                   eval      jvarl1_vare = d_vare
     c                   endif
      *
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      b_vfel = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Lev.nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_levr     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_ldor = %trim(w_welm)
     c                   eval      d_ldor = %xlate(' ':'0':d_ldor)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Lev. varenr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_lvar     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_lvar = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Fra-dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_fdat     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_fdat = %trim(w_welm)
     c                   else
     c                   eval      d_fdat_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_stat     begsr
      *
     c                   eval      d_stat_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_stat = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : NOBB-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nobp     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_nobp_kr  = *zero
     c                   eval      d_nobp_Øre = *zero
     c                   eval      w_nobp = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_nobp_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_nobp_kx  = %xlate(' ':'0':d_nobp_kx)
     c                   eval      d_nobp_Øre = *zero
     c                   eval      w_nobp = d_nobp_kr
     c                   else
     c                   evalr     d_nobp_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_nobp_kx  = %xlate(' ':'0':d_nobp_kx)
     c                   evalr     d_nobp_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_nobp_Ørx = %xlate(' ':'0':d_nobp_Ørx)
     c                   if        (d_nobp_kr + d_nobp_Øre) > c_pris_max
     c                   eval      w_nobp = *zero
     c                   else
     c                   eval(h)   w_nobp = (d_nobp_kr + d_nobp_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Til-dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_tdat     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_tdat = %trim(w_welm)
     c                   else
     c                   eval      d_tdat_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Valuta
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_valu     begsr
      *
     c                   if        w_wlen = 3
     c                   eval      d_valu = %trim(w_welm)
     c                   else
     c                   eval      d_valu = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : EAN-nr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_eann     begsr
      *
     c                   eval      d_eann_num = *zero
      *
     c                   if        w_wlen = 13
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_eann = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Innkjøpspris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_inkp     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_inkp_kr  = *zero
     c                   eval      d_inkp_Øre = *zero
     c                   eval      w_inkp = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_inkp_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_inkp_kx  = %xlate(' ':'0':d_inkp_kx)
     c                   eval      d_inkp_Øre = *zero
     c                   eval      w_inkp = d_inkp_kr
     c                   else
     c                   evalr     d_inkp_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_inkp_kx  = %xlate(' ':'0':d_inkp_kx)
     c                   evalr     d_inkp_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_inkp_Ørx = %xlate(' ':'0':d_inkp_Ørx)
     c                   if        (d_inkp_kr + d_inkp_Øre) > c_pris_max
     c                   eval      w_inkp = *zero
     c                   else
     c                   eval(h)   w_inkp = (d_inkp_kr + d_inkp_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Kostpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_kosp     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_kosp_kr  = *zero
     c                   eval      d_kosp_Øre = *zero
     c                   eval      w_kosp = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_kosp_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_kosp_kx  = %xlate(' ':'0':d_kosp_kx)
     c                   eval      d_kosp_Øre = *zero
     c                   eval      w_kosp = d_kosp_kr
     c                   else
     c                   evalr     d_kosp_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_kosp_kx  = %xlate(' ':'0':d_kosp_kx)
     c                   evalr     d_kosp_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_kosp_Ørx = %xlate(' ':'0':d_kosp_Ørx)
     c                   if        (d_kosp_kr + d_kosp_Øre) > c_pris_max
     c                   eval      w_kosp = *zero
     c                   else
     c                   eval(h)   w_kosp = (d_kosp_kr + d_kosp_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Salgspris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_salp     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_salp_kr  = *zero
     c                   eval      d_salp_Øre = *zero
     c                   eval      w_salp = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_salp_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_salp_kx  = %xlate(' ':'0':d_salp_kx)
     c                   eval      d_salp_Øre = *zero
     c                   eval      w_salp = d_salp_kr
     c                   else
     c                   evalr     d_salp_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_salp_kx  = %xlate(' ':'0':d_salp_kx)
     c                   evalr     d_salp_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_salp_Ørx = %xlate(' ':'0':d_salp_Ørx)
     c                   if        (d_salp_kr + d_salp_Øre) > c_pris_max
     c                   eval      w_salp = *zero
     c                   else
     c                   eval(h)   w_salp = (d_salp_kr + d_salp_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Frakt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_frak     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_frak_kr  = *zero
     c                   eval      d_frak_Øre = *zero
     c                   eval      w_frak = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_frak_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_frak_kx  = %xlate(' ':'0':d_frak_kx)
     c                   eval      d_frak_Øre = *zero
     c                   eval      w_frak = d_frak_kr
     c                   else
     c                   evalr     d_frak_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_frak_kx  = %xlate(' ':'0':d_frak_kx)
     c                   evalr     d_frak_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_frak_Ørx = %xlate(' ':'0':d_frak_Ørx)
     c                   if        (d_frak_kr + d_frak_Øre) > c_pris_max
     c                   eval      w_frak = *zero
     c                   else
     c                   eval(h)   w_frak = (d_frak_kr + d_frak_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : NOBB oppr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nbdo     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_noda = %trim(w_welm)
     c                   else
     c                   eval      d_noda_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : NOBB endr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nbde     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_neda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Opprettet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_opda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_odat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Endret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_enda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_edat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priselement : Endringstidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_entd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_etim = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_pris      begsr
      *
     c                   if        b_vfel = *on
     c                   leavesr
     c                   endif
      *
     c                   clear                   jvprlur
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = d_ldor_num
     c     jvprlu_key    chain     jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = d_ldor_num
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   if        %found
     c                   if        d_fdat_iso >= jxgdat
     c                   if        d_lvar <> jxlvar or
     c                             w_nobp <> jxpris or
     c                             d_valu <> jxvalu or
6.11 c                             d_eann_num <> jxeann or
     c                             w_frak <> jxfbel or
     c                             d_fdat_iso <> jxgdat or
     c                             d_tdat_iso <> jxtdat
     c                   eval      jxlvar = d_lvar
     c                   eval      jxgpri = jxpris
     c                   eval      jxpris = w_nobp
     c                   eval      jxvalu = d_valu
6.11 c                   eval      jxeann = d_eann_num
     c                   eval      jxfbel = w_frak
     c                   eval      jxgdat = d_fdat_iso
     c                   eval      jxtdat = d_tdat_iso
     c                   update    jvprlur
     c                   endif
     c                   endif
     c                   else
     c                   eval      jxlvar = d_lvar
     c                   eval      jxgpri = 0
     c                   eval      jxpris = w_nobp
     c                   eval      jxvalu = d_valu
6.11 c                   eval      jxeann = d_eann_num
     c                   eval      jxfbel = w_frak
     c                   eval      jxgdat = d_fdat_iso
     c                   eval      jxtdat = d_tdat_iso
     c                   eval      jxcrdo = 0
     c                   time                    jxodat
     c                   write     jvprlur
     c                   endif
      *
      * Oppdaterer/oppretter søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakestilling av XML-escape tegn i tekstfelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_XML_esc  begsr
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      b_blnk = *off
      *
     c                   dow       w_posi < 395 and
     c                             b_blnk = *off
     c                   eval      w_posi = w_posi + 1
     c                   if        %subst(w_str_i:w_posi) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 395
     c                   select
     c                   when      %subst(w_str_i:w_posi:5) = '&amp;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '&'
     c                   eval      w_posi = w_posi + 4
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&lt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '<'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&gt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '>'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&apos;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = c_apos
     c                   eval      w_posi = w_posi + 5
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&quot;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '"'
     c                   eval      w_posi = w_posi + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare (NOBB)
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
