     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : BO125R
      * Beskrivelse . . : Utskrift av kontroll/avstem., Detaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.30  * R5.30 131004 HFL  I bonghode og linje er bongnr. 12A.
      * R5.30 221204 ASK  Hele programmet er skrevet om til ILE-std
      *                   Korreksjon for mva lagt inn.
      * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
pdf   * R6.10 071010 BSA  Innført PDF utskrift. Innebærer primært å finne
pdf   *                   generert spool, og kall av AP620R med dette som
pdf   *                   parametre.
6.20  * R6.20 031212 BSA  Utvider parameterliste for å berike arkivtagger
6.21  * R6.20 041013 BSA  Endret logikk rundt refn
6.30  * R6.30 161115 bsa  Lagt ut mer info på kvitteringsliste og arkiv.
6.31  * R6.20 251115 BSA  Fjernet '-' i type dokument, da dette skaper trøbbel
6.31  *                   for arkivklienten.
6.32  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.33  * R6.30 250816 bof  Endret på mva.beregning (fjernet en del kode)
6.34  * R6.30 120916 bof  Legger Øreavrunding ut fra bonghode
6.35  * R6.30 220916 bof  Må behandle øreavrunding for faktura spesielt
6.nu  * 6.30  171019 bkv  Nummerserie rettelse ldlavs
6.36  * R6.30 281119 sst  BAVSTE liste teller brukes på alle lister
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbpsolb    ip   e           k disk    rename(bpsopfr:bpsolbr)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
     fbohelu    uf   e           k disk    rename(bohepfr:bohelur)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
      *
     fbo125p    o    e             printer
pdf  f                                     usropn
      *
      * DAtastruktur for parametre
      *
     d                 ds
     d d_prec                       256
     d  d_otal                        4    overlay(d_prec)
     d  d_ot01                        2    overlay(d_prec:5)
     d  d_ot02                        2    overlay(d_prec:7)
     d  d_ot03                        2    overlay(d_prec:9)
     d  d_ot04                        2    overlay(d_prec:11)
     d  d_ot05                        2    overlay(d_prec:13)
     d  d_ot06                        2    overlay(d_prec:15)
     d  d_ot07                        2    overlay(d_prec:17)
     d  d_ot08                        2    overlay(d_prec:19)
     d  d_ot09                        2    overlay(d_prec:21)
     d  d_ot10                        2    overlay(d_prec:23)
     d  d_wsgr                        6    overlay(d_prec:25)
     d  d_wsal                       10    overlay(d_prec:31)
     d  d_ws01                       10    overlay(d_prec:41)
     d  d_ws02                       10    overlay(d_prec:51)
     d  d_ws03                       10    overlay(d_prec:61)
     d  d_ws04                       10    overlay(d_prec:71)
     d  d_ws05                       10    overlay(d_prec:81)
     d  d_ws06                       10    overlay(d_prec:91)
     d  d_ws07                       10    overlay(d_prec:101)
     d  d_ws08                       10    overlay(d_prec:111)
     d  d_ws09                       10    overlay(d_prec:121)
     d  d_ws10                       10    overlay(d_prec:131)
     d  d_avst                        1  0 overlay(d_prec:142)
     d  d_dato                       10d   datfmt(*iso)
     d                                     overlay(d_prec:143)
     d  d_peri                        6  0 overlay(d_prec:153)
     d  lddekn                        1  0 overlay(d_prec:159)
     d  d_vdag                        2  0 overlay(d_prec:160)
     d  d_deta                        1  0 overlay(d_prec:162)
     d  d_tota                        1  0 overlay(d_prec:163)
     d  d_frad                       10d   datfmt(*iso)
     d                                     overlay(d_prec:164)
     d  d_tild                       10d   datfmt(*iso)
     d                                     overlay(d_prec:174)
     d  d_aarr                        4  0 overlay(d_prec:184)
6.nu d  d_lavs                        8  0 overlay(d_prec:188)
6.nu d  d_ldet                        6  0 overlay(d_prec:196)
6.nu d  d_ltim                        6  0 overlay(d_prec:202)
6.nu d  d_lkre                        1  0 overlay(d_prec:208)
      *
      * Definering av LDA
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_utqm                585    594
pdf  d l_bach                595    635
pdf  d l_arch                636    636  0
pdf  d l_skje                637    637  0
pdf  d l_land                638    638  0
pdf  d l_exlp                639    639  0
pdf  d l_mail                640    640  0
     d l_ruti                800    809
pdf  D l_outq                810    819
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *                                                                     -
      * Definering av variable i nøkler
      *                                                                     -
     d aforl1_numm     s                   like(afruti)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_line     s                   like(bdline)
     d bodtl1_onum     s                   like(bdbong)
     d bodtl1_wsid     s                   like(bdwsid)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_bong     s                   like(bobong)
     d bohel1_wsid     s                   like(bowsid)
     d bohelu_aarr     s                   like(boaarr)
     d bohelu_bong     s                   like(bobong)
     d bohelu_wsid     s                   like(bowsid)
     d rkunl1_kund     s                   like(rkkund)
     d vltyl1_ltyp     s                   like(valtyp)
     d votyl1_otyp     s                   like(vaotyp)
     d vpkol1_pkod     s                   like(vapkod)
      *                                                                     -
      * Definering av øvrige variable
      *                                                                     -
     d d1toti          s             11  2
     d l1rund          s                   like(botots)
     d l2rund          s                   like(botots)
     d l4rund          s                   like(botots)
     d l6rund          s                   like(botots)
     d w_mvag          s                   like(bdsums)
     d w_rab1          s                   like(bdsapr)
     d w_rab2          s                   like(bdsapr)
     d w_rabo          s                   like(bdsapr)
     d w_firm          s                   like(baafir)
     d w_mvpr          s              6  2
     d w_mvfr          s             10  9
     d w_mvti          s              5  4
     d w_m2pr          s              6  2
     d w_m2fr          s             10  9
     d w_m2ti          s              5  4
     d w_mvtx          s             30
     d w_pqmv          s              1
     d w_parm          s            256
5.31 d w_stat          s              1
5.31 d w_mvak          s              1
5.31 d w_bpro          s              5  2
5.31 d w_dato          s               d   datfmt(*iso)
5.31 d w_mvap          s              6  2
5.31 d w_mvat          s              5  4
5.31 d w_mvaf          s             10  9
     d b_ukre          s              1    inz(*off)
pdf  d p_btyp          s            100
6.21 d w_year          s              4  0
      *                                                                     -
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.20 d p_tagg0         s             20
6.20 d p_tagg0val      s             50
6.20 d p_tagg1         s             20
6.20 d p_tagg1val      s             50
6.20 d p_tagg2         s             20
6.20 d p_tagg2val      s             50
6.20 d p_tagg3         s             20
6.20 d p_tagg3val      s             50
6.20 d p_tagg4         s             20
6.20 d p_tagg4val      s             50
6.20 d p_tagg5         s             20
6.20 d p_tagg5val      s             50
6.20 d p_tagg6         s             20
6.20 d p_tagg6val      s             50
6.20 d p_tagg7         s             20
6.20 d p_tagg7val      s             50
6.20 d p_tagg8         s             20
6.20 d p_tagg8val      s             50
6.20 d p_tagg9         s             20
6.20 d p_tagg9val      s             50
6.20 d p_refn          s             50
6.20 d p_dspl          s            100
      *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
      *                                                                     -
      * Definere brudd på primary file                                      -
      *
     ibpsolbr       01
     i                                          bsofir        l6
     i                                          bsooty        l4
     i                                          bsowsd        l2
     i                                          bsonum        l1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        *inl6 = *on
     c                   exsr      l6inn
     c                   endif
     c                   if        *inl4 = *on
     c                   exsr      l4inn
     c                   endif
     c                   if        *inl2 = *on
     c                   exsr      l2inn
     c                   endif
     c                   if        *inl1 = *on
     c                   exsr      l1inn
     c                   endif
      *
      *  Hopp til riktig behandlingsrutine utfra recordtype
      *
     c     bsorct        cabne     'H'           taglin
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av BONG-HODE-Opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     taghod        tag
      *
      *  Hent opplysninger fra BONG-HODE-REGISTER
      *
     c                   eval      bohel1_aarr = bsoaar
     c                   eval      bohel1_wsid = bsowsd
     c                   eval      bohel1_bong = bsonum
     c     bohel1_key    chain     bohel1r
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r
      *
      * Tester om kredit omsetning er valgt utelatt.
      *
     c                   eval      b_ukre = *off
     c                   if        d_lkre = 1 and
     c                             vaoakk < 3
     c                   eval      b_ukre = *on
     c                   endif
      *
      *  Oppdaterer BONG-HODE-REGISTER,
      *             denne bong er blitt med på avstemmingsliste
      *
     c                   if        d_avst <> 0
     c                   move      bsoaar        bohelu_aarr
     c                   move      bsowsd        bohelu_wsid
     c                   move      bsonum        bohelu_bong
     c     bohelu_key    chain     bohelur
     c                   if        %found
     c                   if        b_ukre = *on
     c                   eval      bokdet = 4
     c                   else
     c                   eval      bokdet = 1
     c                   endif
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelur
     c                   endif
     c                   endif
      *
      *  Avslutter behandling dersom utelat har blitt valgt
      *
     c                   if        b_ukre = *on
     c                   goto      xslutt
     c                   endif
      *
      *  Snur fortegn på beløp dersom negativ ordre-type
      *
     c                   if        vaokre = '-'
     c                   mult      -1            botots
     c                   mult      -1            bototb
     c                   endif
      *
      *  Legger inn data i utskrifts-felter
      *
     c                   move      botime        c1time
     c                   eval      c1wsid = bowsid
5.30 c                   eval      c1numm = bobong
     c                   eval      c1lage = bolage
     c                   eval      c1betb = bobetb
     c                   eval      c1selg = boselg
     c                   eval      c1kund = bokund
     c                   eval      c1avde = boavde
     c                   eval      c1dref = bodref
      *
     c                   eval      c1binr = bobinr
      *
      *  Hent opplysninger fra KUNDE-REGISTER
      *
     c                   eval      rkunl1_kund = bokund
     c     rkunl1_key    chain     rkunl1r
     c                   eval      c1navn = rknavn
      *
      *  Test for hvilke utskriftsfelter som er utfylt og skal skrives
      *
     c                   eval      *in32 = (c1kund = baakun)
     c                   eval      *in33 = (c1dref = *blank)
     c                   eval      *in35 = (c1binr = 0)
     c                   eval      *in36 = (c1avde = 0)
     c                   eval      *in37 = (c1lage = 0)
     c                   eval      *in38 = (c1kund = baakun)
      *
      *  Skriv bong-hode-opplysninger
      *
     c                   write     c1det                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av BONG-LINJE-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     taglin        tag
      *
      *  Hopper over linje behandling dersom utelat har blitt valgt
      *
     c                   if        b_ukre = *on
     c                   goto      xslutt
     c                   endif
      *
      *  Hent opplysninger fra BONG-LINJE-REGISTER
      *
     c                   eval      bodtl1_aarr = bsoaar
     c                   eval      bodtl1_wsid = bsowsd
     c                   eval      bodtl1_onum = bsonum
     c                   eval      bodtl1_line = bsolin
     c     bodtl1_key    chain     bodtl1r
      *
      *  Hent opplysninger fra LINJETYPE-REGISTER
      *
     c                   eval      vltyl1_ltyp = bdltyp
     c     vltyl1_key    chain     vltyl1r
      *
      *  Nullstilling av variabler
      *
     c                   eval      d1tine = *blank
     c                   eval      *in46 = *off
      *
      *  Sjekk om tilbud/netto-pris
      *
     c                   if        bdrab1 = 0
     c                   eval      *in46 = *on
     c                   eval      d1tine = *blank
     c                   eval      vpkol1_pkod = bdkpri
     c     vpkol1_key    chain     vpkol1r
     c                   if        %found
     c                   eval      d1tine = vaptxt
     c                   endif
     c                   endif
      *
      *  Legger inn data i utskrifts-felter
      *
     c                   eval      d1line = bdline
     c                   movel     bdvare        d1vare
     c                   eval      d1enh1 = bdenh1
5.30 c                   movel     bdtek1        d1tek1
     c                   eval      d1anta = bdanta
     c                   z-add     bdsapr        d1sapr
     c                   eval      d1rab1 = bdrab1
     c                   eval      d1rab2 = bdrab2
      *
     c                   z-add     bdsums        d1sums
      *
      * Finner moms-sats - kode 3
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    bodate
     c                   parm                    w_mvpr
     c                   parm                    w_mvti
     c                   parm                    w_mvfr
      *
      * Finner moms-sats - kode H
      *
3.34 c                   call      'FÅ706R'
3.34 c                   parm                    w_firm
3.34 c                   parm                    bodate
3.34 c                   parm                    w_m2pr
3.34 c                   parm                    w_m2ti
3.34 c                   parm                    w_m2fr
      *
      * Beregner mva-grunlag uten almenningsrabatt
      *
     c                   eval      w_rab1 = 0
     c                   eval      w_rab2 = 0
     c                   eval      w_mvag = bdsums
      *
     c                   if        bdbrr1 <> 0 or
     c                             bdbrr2 <> 0
     c                   eval      w_mvag = bdsapr * bdanta
     c                   if        bdrab1 <> 0
     c                   eval      w_rab1 = (w_mvag * bdrab1)/100
     c                   eval      w_mvag = w_mvag - w_rab1
     c                   endif
     c                   if        bdrab2 <> 0
     c                   eval      w_rab2 = (w_mvag * bdrab2)/100
     c                   eval      w_mvag = w_mvag - w_rab2
     c                   endif
     c                   if        bdorab <> 0
     c                   eval      w_rabo = (w_mvag * bdorab)/100
     c                   eval      w_mvag = w_mvag - w_rabo
     c                   endif
     c                   endif
5.31  *
5.31  * Beregner mva grunnlag i forhold til prosentsats på mva kode
5.31  *
     c                   if        bdkmva = *blank
     c                   eval      bdkmva = '3'
     c                   endif
      *
5.31 c                   eval      w_stat = *blank
5.31 c                   eval      w_mvak = bdkmva
5.31 c                   time                    w_dato
5.31  *
5.31 c                   call      'RS205R'
5.31 c                   parm                    w_stat
5.31 c                   parm                    w_mvak
5.31 c                   parm                    w_mvtx
5.31 c                   parm                    w_dato
5.31 c                   parm                    w_mvap
5.31 c                   parm                    w_mvat
5.31 c                   parm                    w_mvaf
5.31 c                   parm                    w_bpro
5.31  *
5.31 c                   if        w_mvap > 0
5.31 c                   eval      w_mvag = (w_mvag * w_mvap) / 100
5.31 c                   endif
      *
      *  Kontroller om ordren avgiftsfri/pliktig  - beregner inkl. mva
      *
     c                   if        bomkod = 1
5.32 c                   eval      d1sumi = bdsums                              Ekskl.
     c                   else
6.33 c                   eval      d1sumi = bdsumi                              Inkl.
     c                   endif
      *
      *  Snur fortegn på beløp dersom negativ ordre-type
      *
     c                   if        vaokre = '-'
     c                   mult      -1            d1sums
     c                   mult      -1            d1sumi
     c                   endif
      *
      *  Akkumulerer til totalfelter
      *
     c                   add       d1sumi        d1toti
      *
      *  Test for hvilke utskriftsfelter som er utfylt
      *
     c                   eval      *in39 = (bdvare = *blank)
      *
5.30 c                   eval      *in34 = (d1ornr = 0)
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     cl1                 exsr      l1ut
     cl2                 exsr      l2ut
     cl4                 exsr      l4ut
     cl6                 exsr      l6ut
      *
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.32 clr                 close     bo125p
6.32 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  clr                 parm                    l_bach
pdf  clr                 endif
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L6IN      Behandling av brudd på L6 inn  (Firma)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l6inn         begsr
      *
      *  Nullstiller totaler
      *
     c                   eval      l6tots = 0
     c                   eval      l6toti = 0
     c                   eval      l6rund = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L4IN      Behandling av brudd på L4 inn  (Ordretype)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l4inn         begsr
      *
      *  Nullstiller totaler
      *
     c                   eval      l4tots = 0
     c                   eval      l4toti = 0
     c                   eval      l4rund = 0
      *
      *  Skriver ny heading for ny ordretype
      *
     c                   write     a1hdr                                30
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bsooty
     c     votyl1_key    chain     votyl1r
      *
      *  Skriv ut Opplysinger om Ordretype
      *
     c                   eval      b1otyp = bsooty
     c                   eval      c1otyp = bsooty
     c                   eval      b1otxt = vaotxt
      *
     c                   write     b1det                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L2IN      Behandling av brudd på L2 inn  (Arbeidsstasjon)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l2inn         begsr
      *
      *  Nullstiller totaler
      *
     c                   eval      l2tots = 0
     c                   eval      l2toti = 0
     c                   eval      l2rund = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L1IN      Behandling av brudd på L1 inn  (Bongnummer)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l1inn         begsr
      *
      *  Nullstiller totaler
      *
     c                   eval      d1toti = 0
6.35 c                   eval      l1toti = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L1UT      Behandling av brudd på L1 ut   (Bongnummer)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l1ut          begsr
      *
      *  Akkumulerer til totalfelter
      *
     c                   z-add     botots        l1tots
     c                   add       botots        l2tots
     c                   add       botots        l4tots
     c                   add       botots        l6tots
      *
     c                   eval      l1toti = bototb - bobgkv
     c                   add       bototb        l2toti
     c                   add       bototb        l4toti
     c                   add       bototb        l6toti
      *
6.35 c                   if        vaoakk <> 3
     c     l1toti        sub       d1toti        l1rund
6.35 c                   endif
     c                   add       l1rund        l2rund
     c                   add       l1rund        l4rund
     c                   add       l1rund        l6rund
      *
      *  Skriver Øreavrunding hvis det finnes
      *
6.34 c                   if        boavrb <> 0
6.34 c                   z-add     boavrb        t1rund
     c                   write     t1list                               30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
6.35 c                   else
6.35 c                   if        l1rund <> 0
6.35 c                   z-add     l1rund        t1rund
6.35 c                   write     t1list                               30
6.35 c                   if        *in30 = *on
6.35 c                   exsr      ovflow
6.35 c                   endif
6.35 c                   endif
     c                   endif
      *
      *  Skriver total for L1
      *
     c                   write     l1tot                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L2UT      Behandling av brudd på L2 ut   (Arbeidsstasjon)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l2ut          begsr
      *
     c                   eval      l2wsid = bowsid
      *
      *  Skriver Øreavrunding hvis det finnes
      *
     c                   if        l2rund <> 0
     c                   z-add     l2rund        t1rund
     c                   write     t1list                               30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
     c                   endif
      *
      *  Skriver total for L2
      *
     c                   write     l2tot                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L4UT      Behandling av brudd på L4 ut   (Ordretype)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l4ut          begsr
      *
     c                   eval      l4otyp = bootyp
      *
      *  Skriver Øreavrunding hvis det finnes
      *
     c                   if        l4rund <> 0
     c                   z-add     l4rund        t1rund
     c                   write     t1list                               30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
     c                   endif
      *
      *  Skriver total for L4
      *
     c                   write     l4tot                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  L6UT      Behandling av brudd på L6 ut   (Firma)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l6ut          begsr
      *
      *  Legger inn opplysninger i utfelter
      *
     c                   eval      l6firm = bofirm
      *
      *  Skriver Øreavrunding hvis det finnes
      *
     c                   if        l6rund <> 0
     c                   z-add     l6rund        t1rund
     c                   write     t1list                               30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
     c                   endif
      *
      *  Skrive totalsum
      *
     c                   write     l6tot                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ovflow        begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.31 c**                 eval      p_btyp = 'DAGSOPPGJØR-DETALJER'
6.31 c                   eval      p_btyp = 'DAGSOPPGJØR DETALJER'
6.20 c                   eval      p_dspl = 'Avstemmingsliste - detaljer '+
6.30 c                                      %char(a1list) +
6.30 c                                      ' Periode ' + %char(d_peri)  +
6.30 c                                      '  Gruppe ' + %trim(a1wsgr)
6.21 c**                 eval      p_refn = 'BO125-'+%char(a1list)
6.21 c                   eval      p_refn = %char(w_year)+%char(a1list)
6.20 c                   eval      p_tagg0 = 'NUMMER'
6.20 c                   eval      p_tagg0val = %char(a1list)
6.20 c                   eval      p_tagg1 = 'TOTALSUM'
6.20 c                   eval      p_tagg1val = %char(l6tots)
6.20 c                   eval      p_tagg2 = *blanks
6.20 c                   eval      p_tagg2val = *blanks
6.20 c                   eval      p_tagg3 = *blanks
6.20 c                   eval      p_tagg3val = *blanks
6.20 c                   eval      p_tagg4 = *blanks
6.20 c                   eval      p_tagg4val = *blanks
6.20 c                   eval      p_tagg5 = *blanks
6.20 c                   eval      p_tagg5val = *blanks
6.20 c                   eval      p_tagg6 = *blanks
6.20 c                   eval      p_tagg6val = *blanks
6.20 c                   eval      p_tagg7 = *blanks
6.20 c                   eval      p_tagg7val = *blanks
6.20 c                   eval      p_tagg8 = *blanks
6.20 c                   eval      p_tagg8val = *blanks
6.20 c                   eval      p_tagg9 = *blanks
6.20 c                   eval      p_tagg9val = *blanks
pdf   *
6.20 c**                 call      'AP620R'
6.20 c                   call      'AP627R'
pdf  c                   PARM                    splfname
pdf  c                   PARM                    jobname
pdf  c                   PARM                    username
pdf  c                   PARM                    jobnbr
pdf  c                   PARM                    splfnbr
pdf  c                   PARM                    splfname2
pdf  c                   PARM                    jobname2
pdf  c                   PARM                    username2
pdf  c                   PARM                    jobnbr2
pdf  c                   PARM                    splfnbr2
pdf  c                   PARM                    splfname3
pdf  c                   PARM                    jobname3
pdf  c                   PARM                    username3
pdf  c                   PARM                    jobnbr3
pdf  c                   PARM                    splfnbr3
6.20 c                   parm                    p_tagg0
6.20 c                   parm                    p_tagg0val
6.20 c                   parm                    p_tagg1
6.20 c                   parm                    p_tagg1val
6.20 c                   parm                    p_tagg2
6.20 c                   parm                    p_tagg2val
6.20 c                   parm                    p_tagg3
6.20 c                   parm                    p_tagg3val
6.20 c                   parm                    p_tagg4
6.20 c                   parm                    p_tagg4val
6.20 c                   parm                    p_tagg5
6.20 c                   parm                    p_tagg5val
6.20 c                   parm                    p_tagg6
6.20 c                   parm                    p_tagg6val
6.20 c                   parm                    p_tagg7
6.20 c                   parm                    p_tagg7val
6.20 c                   parm                    p_tagg8
6.20 c                   parm                    p_tagg8val
6.20 c                   parm                    p_tagg9
6.20 c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   parm                    p_btyp
6.20 c                   parm                    p_refn
6.20 c                   parm                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
pdf  c                   open      bo125p
      *
      *  Key for les av FORMULAR
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_numm
      *
      *  Key for les av KUNDE
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *  Key for les av ORDRETYPE
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for les av LINJETYPE
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *  Key for les av BUTIKK-KODE
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BONG-HODE
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohel1_aarr
     c                   kfld                    bohel1_wsid
     c                   kfld                    bohel1_bong
      *
      *  Key for oppdatering av BONG-HODE
      *
     c     bohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelu_aarr
     c                   kfld                    bohelu_wsid
     c                   kfld                    bohelu_bong
      *
      *  Key for file : BONG-LINJE-REGISTER
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_onum
     c                   kfld                    bodtl1_line
      *
      *  Key for priskode-register
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      *  Legger inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      a1firm = l_firm
      *
      *  Behandling av parameter-felter
      *
     c                   eval      d_prec = w_parm
      *
      *  Avstemmingsliste/Kontroll-liste   31=ON=Avstemmingsliste
      *
     c                   eval      *in31 = *off
     c                   if        d_avst = 1
     c                   eval      *in31 = *on
     c                   endif
      *
      *  Legger inn opplysninger i utfelter
      *
     c                   eval      a1wsid = l_wsid
     c                   eval      a1user = l_user
     c                   eval      a1fnav = l_fnav
      *
6.36 c                   eval      a1list = d_lavs
6.30 c                   eval      a1wsgr = d_wsgr
      *
     c                   eval      a1date = udate
6.21 c                   eval      w_year = *year
     c                   time                    a1time
      *
      *  Hent opplysninger fra BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    chain     bstsl1r                            90
      *
      *  Hent opplysninger fra RUTINE-REGISTER
      *
     c                   eval      aforl1_numm = l_ruti
     c     aforl1_key    chain     aforl1r
     c                   if        %found
     c                   eval      l6mld1 = afmld1
     c                   endif
      *
     c                   endsr
