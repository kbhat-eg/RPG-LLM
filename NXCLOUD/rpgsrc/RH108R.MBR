     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH108R
      * Beskrivelse . . : Kontoplan, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.20     170104  Lagt inn spørring på konto, avd., bærer        KOB  *
      * R5.20     170104  Lagt inn kontroll på gyldig bærer              KOB  *
      * R5.20     170104  Lagt inn mulighet for overstyring av fra       KOB  *
      * R5.20     170104  avdeling og bærer                              KOB  *
      * R5.20     100205  Tillater å flytte hel konto til bærer (*in34). HFL  *
      * R5.40     200206  Konto må oppgis (*in38)                      . HFL  *
      *
8.01  * 8.xx      150222  Justert i skjermfila pga cloud               . bsa  *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhovl2    if   e           k disk    rename(rhovpfr:rhovl2r)
     frhovl3    if   e           k disk    rename(rhovpfr:rhovl3r)
     frhovl4    if   e           k disk    rename(rhovpfr:rhovl4r)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
      *
     frh108d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
     d rhovl2_alfa     s                   like(rhalfa)
     d rhovl3_kont     s                   like(rhkont)
     d rhovl3_spes     s                   like(rhspes)
     d rhovl3_avde     s                   like(rhavde)
     d rhovl4_kont     s                   like(rhkont)
     d rhovl4_spes     s                   like(rhspes)
     d rhovl4_avde     s                   like(rhavde)
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_spes     s                   like(rhspes)
     d rhovlr_avde     s                   like(rhavde)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(rhfirm)
      *
     d p_stat          s              1
     d p_ekod          s                   like(rhmvak)
     d p_gkod          s                   like(rhavde)
     d p_mkod          s                   like(rhbnØk)
     d p_okod          s                   like(rhmngd)
     d p_pkod          s                   like(rhvalk)
     d p_tx20          s             20
     d p_tx30          s             30
     d p_bkod          s                   like(rhspes)
     d p_kont          s                   like(rhkont)                               Konto
     d p_spes          s                   like(rhspes)                               Spesifikasjon
     d p_avde          s                   like(rhavde)                               Avdeling
      *
      *  Fra konto
      *
     d w_gkto          s                   like(rhkont)                               Konto
     d w_gsps          s                   like(rhspes)                               Spesifikasjon
     d w_gavd          s                   like(rhavde)                               Avdeling
      *
      *  Til konto
      *
     d w_nkto          s                   like(rhkont)                               Konto
     d w_nsps          s                   like(rhspes)                               Spesifikasjon
     d w_navd          s                   like(rhavde)                               Avdeling
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kont <> 0 or
     c                             b2spes <> 0 or
     c                             b2avde <> 0 or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhovl1_kont = b1kont
     c                   eval      rhovl1_spes = b1spes
     c                   eval      rhovl1_avde = b1avde
     c     rhovl1_key    setll     rhovl1
     c                   when      w_seqe = 'A'
     c                   eval      rhovl3_avde = b1avde
     c                   eval      rhovl3_kont = b1kont
     c                   eval      rhovl3_spes = b1spes
     c     rhovl3_key    setll     rhovl3
     c                   when      w_seqe = 'B'
     c                   eval      rhovl4_spes = b1spes
     c                   eval      rhovl4_kont = b1kont
     c                   eval      rhovl4_avde = b1avde
     c     rhovl4_key    setll     rhovl4
     c                   when      w_seqe = 'N'
     c                   eval      rhovl2_alfa = b1alfa
     c     rhovl2_key    setll     rhovl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på konto
      *
     c                   if        b2kont <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      rhovl1_kont = b2kont
     c                   eval      rhovl1_spes = b2spes
     c                   eval      rhovl1_avde = b2avde
     c     rhovl1_key    setll     rhovl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på avdeling
      *
     c                   if        b2avde <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      rhovl3_kont = *zeros
     c                   eval      rhovl3_spes = *zeros
     c                   eval      rhovl3_avde = b2avde
     c     rhovl3_key    setll     rhovl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kostnadsbærer
      *
     c                   if        b2spes <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      rhovl4_kont = *zeros
     c                   eval      rhovl4_spes = b2spes
     c                   eval      rhovl4_avde = *zeros
     c     rhovl4_key    setll     rhovl4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på alfabetisk nøkkel
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      rhovl2_alfa = b2alfa
     c     rhovl2_key    setll     rhovl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kont = 0
     c                   eval      b2spes = 0
     c                   eval      b2avde = 0
     c                   eval      b2alfa = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *                                                                 .
      * Flytting av konto
      *                                                                 .
     c                   if        b1valg = 7                                                .
     c                   move      b1kont        m1fkto                                      .
     c                   move      b1spes        m1fsps                                      .
     c                   move      b1avde        m1favd                                      .
     c                   move      b1kont        rhovlr_kont                                 .
     c                   move      b1spes        rhovlr_spes                                 .
     c                   move      b1avde        rhovlr_avde                                 .
     c                   exsr      xm1win                                                    .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif                                                               .
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rhovlr_kont = c1kont
     c                   eval      rhovlr_spes = c1spes
     c                   eval      rhovlr_avde = c1avde
     c     rhovlr_key    chain     rhovlr
      *
     c                   if        %found
     c                   eval      c2text = rhtext
     c                   eval      c2txt2 = rhtxt2
     c                   eval      c2alfa = rhalfa
     c                   eval      c2mvak = rhmvak
     c                   eval      c2mvao = rhmvao
     c                   eval      c2mvag = rhmvag
     c                   eval      c2bnØk = rhbnØk
     c                   eval      c2valk = rhvalk
     c                   eval      c2mngd = rhmngd
     c                   eval      c2proj = rhproj
     c                   eval      c2bÆrk = rhbÆrk
     c                   eval      c2avdk = rhavdk
     c                   eval      c2nØkk = rhnØkk
     c                   eval      c2sprk = rhsprk
     c                   eval      c2eusr = rheusr
     c     *dmy          move      rhedat        c2edat
     c                   exsr      kontroll
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * M1WIN Behandle bilde for flytting                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xm1win        begsr
      *
     c     rhovlr_key    chain     rhovlr                             60
      *
     c                   eval      m1ftx1 = rhtext
     c                   eval      m1fatx = *blank
     c                   eval      m1tkto = *zeros
     c                   eval      m1tsps = *zeros
     c                   eval      m1tavd = *zeros
     c                   eval      m1ttx1 = *blank
     c                   eval      m1ttx2 = *blank
     c                   eval      m1tatx = *blank
      *
      * Hent fra-avdeling
      *
     c                   if        m1favd <> *zeros
     c                   move      m1favd        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx20
      *
     c                   move      p_tx20        m1fatx
     c                   endif
      *
      * Hent fra-kostnadsbærer
      *
     c                   if        m1fsps <> *zeros
     c                   move      m1fsps        p_bkod
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx30
      *
     c                   move      p_tx30        m1ftx2
     c                   endif
      *
     c                   eval      wactli = 7
     c                   eval      wactpo = 20
      *
     c     m1tagb        tag
      *
     c                   exfmt     m1win
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
T5.40c                   eval      *in38 = *off
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
      *
     c     *inkc         cabeq     *on           m1tag1
     c     *inkl         cabeq     *on           m1tag1
      *
      * Spørring
      *
     c                   if        *inkd = *on                                           .....
      *
      * Call program for avdeling fra
      *
     c                   if        w_afld = 'M1FAVD'                                         .
     c                   eval      p_avde = 0
      *
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_avde                                      .
     c                   parm                    p_tx20                                      .
      *
     c                   if        p_avde <> *zeros
     c                   move      p_avde        m1favd                                      .
     c                   endif                                                               .
      *
     c                   move      *on           *in42
     c                   goto      m1tagb                                                    .
     c                   endif                                                               .
      *
      * Call program for kostnadsbærer fra
      *
     c                   if        w_afld = 'M1FSPS'                                         .
     c                   eval      p_bkod = 0
      *
     c                   call      'RA528R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_bkod                                      .
     c                   parm                    p_tx30                                      .
      *
     c                   if        p_bkod <> *zeros
     c                   move      p_bkod        m1fsps                                      .
     c                   endif                                                               .
      *
     c                   move      *on           *in41
     c                   goto      m1tagb                                                    .
     c                   endif                                                               .
      *
      * Call program for konto
      *
     c                   if        w_afld = 'M1TKTO'                                         .
     c                   eval      p_kont = 0
     c                   eval      p_avde = 0
     c                   eval      p_spes = 0
      *
     c                   call      'RH500R'                                      konto       .
T5.00c                   parm                    w_firm                           info       .
     c                   parm                    p_kont                           info       .
     c                   parm                    p_spes                           info       .
     c                   parm                    p_avde                           info       .
     c                   parm                    p_tx30                           info       .
      *
     c                   if        p_kont <> *zero                                           .
     c                   move      p_kont        m1tkto                                      .
     c                   endif                                                               .
      *
     c                   move      *on           *in31
     c                   goto      m1tagb                                                    .
     c                   endif                                                               .
      *
      * Call program for avdeling til
      *
     c                   if        w_afld = 'M1TAVD'                                         .
     c                   eval      p_avde = 0
      *
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_avde                                      .
     c                   parm                    p_tx20                                      .
      *
     c                   if        p_avde <> *zeros
     c                   move      p_avde        m1tavd                                      .
     c                   endif                                                               .
      *
     c                   move      *on           *in44
     c                   goto      m1tagb                                                    .
     c                   endif                                                               .
      *
      * Call program for kostnadsbærer til
      *
     c                   if        w_afld = 'M1TSPS'                                         .
     c                   eval      p_bkod = 0
      *
     c                   call      'RA528R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_bkod                                      .
     c                   parm                    p_tx30                                      .
      *
     c                   if        p_bkod <> *zeros
     c                   move      p_bkod        m1tsps                                      .
     c                   endif                                                               .
      *
     c                   move      *on           *in43
     c                   goto      m1tagb                                                    .
     c                   endif                                                               .
      *
     c                   endif                                                               .
      *
     c                   if        *in55 = *on
     c     *inke         cabeq     *on           m1tago
     c                   endif
      *
      * Slå opp i filen
      *
     c                   move      m1tkto        rhovlr_kont
     c                   move      m1tsps        rhovlr_spes
     c                   move      m1tavd        rhovlr_avde
     c     rhovlr_key    chain     rhovlr
      *
      * Post finnes fra før
      *
     c***                if        %found
     c***                move      *on           *in31
     c***                move      *on           *in35
     c***                move      *on           *in55
     c***                goto      m1tagb
     c***                endif
      *
      * Sjekk konto - finnes den i kontoplan
      *
     c                   if        m1tkto <> *zeros
     c                   move      m1tkto        rhovlr_kont
     c                   move      *zeros        rhovlr_spes
     c                   move      *zeros        rhovlr_avde
     c     rhovlr_key    chain     rhovlr
      *
     c                   if        not%found
     c                   move      *on           *in31
     c                   move      *on           *in37
     c                   goto      m1tagb
     c                   endif
T5.40c                   else
T5.40 * Konto må oppgis
T5.40c                   move      *on           *in31
T5.40c                   move      *on           *in38
T5.40c                   goto      m1tagb
     c                   endif
      *
      * Sjekk avdeling - finnes den i koderegister
      *
     c                   if        m1tavd <> *zeros
     c                   move      m1tavd        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in31
     c                   move      *on           *in36
     c                   goto      m1tagb
     c                   endif
     c                   endif
      *
      * Sjekk kostnadsbærer - finnes den i koderegister
      *
     c                   if        m1tsps <> *zeros
     c                   move      m1tsps        p_bkod
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx30
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in31
     c                   move      *on           *in36
     c                   goto      m1tagb
     c                   endif
     c                   endif
      *
      * Samme konto men ny spes/avdeling
      *
     c                   if        m1fkto = m1tkto
      *
     c                   select
     c                   when      m1fsps =  m1tsps and m1favd <> m1tavd
     c                   goto      m1tago
     c                   when      m1fsps <> m1tsps and m1favd =  m1tavd
     c                   goto      m1tago
     c                   endsl
      *
     c                   endif
      *
      * Kan ikke flytte en hel konto til spes.
      * (Fordi andre spes på samme konto ikke lenger blir godtatt som konto)
      *
S5.20c*                  if        m1fsps = *zeros and
S5.20c*                            m1tsps <> *zeros
S5.20c*                  move      *on           *in31
S5.20c*                  move      *on           *in34
S5.20c*                  goto      m1tagb
S5.20c*                  endif
      *
      * Flytting pågår
      *
     c     m1tago        tag
      *
     c                   move      *off          *in55
     c                   move      m1fkto        w_gkto
     c                   move      m1fsps        w_gsps
     c                   move      m1favd        w_gavd
     c                   move      m1tkto        w_nkto
     c                   move      m1tsps        w_nsps
     c                   move      m1tavd        w_navd
      *
     c                   call      'RH109R'
     c                   parm                    w_gkto
     c                   parm                    w_gsps
     c                   parm                    w_gavd
     c                   parm                    w_nkto
     c                   parm                    w_nsps
     c                   parm                    w_navd
      *
     c     m1tag1        tag
      *
      * Oppdater subfile
      *
     c                   eval      b1valg = *zero
      *
     c                   eval      b2kont = *zero
     c                   eval      b2spes = *zero
     c                   eval      b2avde = *zero
     c                   eval      b2alfa = *blank
     c                   eval      m1fkto = *zero
     c                   eval      m1fsps = *zero
     c                   eval      m1favd = *zero
      *
     c                   eval      b_forn = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   read      rhovl1                                 15
     c                   when      w_seqe = 'A'
     c                   read      rhovl3                                 15
     c                   when      w_seqe = 'B'
     c                   read      rhovl4                                 15
     c                   when      w_seqe = 'N'
     c                   read      rhovl2                                 15
     c                   endsl
      *
     c                   if        %eof or
     c                             rhfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhovl1_kont = rhkont
     c                   when      w_seqe = 'A'
     c                   eval      rhovl3_avde = rhavde
     c                   when      w_seqe = 'B'
     c                   eval      rhovl4_spes = rhspes
     c                   when      w_seqe = 'N'
     c                   eval      rhovl2_alfa = rhalfa
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = rhkont
     c                   eval      b1spes = rhspes
     c                   eval      b1avde = rhavde
     c                   eval      b1text = rhtext
     c                   eval      b1sprk = rhsprk
     c                   eval      b1mvak = rhmvak
     c                   eval      b1mvao = rhmvao
     c                   eval      b1mvag = rhmvag
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'K'
     c     rhovl1_key    setgt     rhovl1
     c                   readp     rhovl1
     c                   when      w_seqe = 'A'
     c     rhovl3_key    setgt     rhovl3
     c                   readp     rhovl3
     c                   when      w_seqe = 'B'
     c     rhovl4_key    setgt     rhovl4
     c                   readp     rhovl4
     c                   when      w_seqe = 'N'
     c     rhovl2_key    setgt     rhovl2
     c                   readp     rhovl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   readp     rhovl1
     c                   when      w_seqe = 'A'
     c                   readp     rhovl3
     c                   when      w_seqe = 'B'
     c                   readp     rhovl4
     c                   when      w_seqe = 'N'
     c                   readp     rhovl2
     c                   endsl
      *
     c                   if        %eof or
     c                             rhfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhovl1_kont = rhkont
     c                   when      w_seqe = 'A'
     c                   eval      rhovl3_avde = rhavde
     c                   when      w_seqe = 'B'
     c                   eval      rhovl4_spes = rhspes
     c                   when      w_seqe = 'B'
     c                   eval      rhovl2_alfa = rhalfa
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'K'
     c     rhovl1_key    setll     rhovl1
     c                   when      w_seqe = 'A'
     c     rhovl3_key    setll     rhovl3
     c                   when      w_seqe = 'B'
     c     rhovl4_key    setll     rhovl4
     c                   when      w_seqe = 'N'
     c     rhovl2_key    setll     rhovl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for hente tekst
      *
     c     kontroll      begsr
      *
      * Hent mva-kode
      *
     c                   move      c2mvak        p_ekod                                      .
     c                   call      'RS705R'                                      MVA-
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_tx30
      *
     c                   eval      c2etxt = p_tx30                                           .
      *
      * Hent budsjettnøkkel
      *
     c                   if        c2bnØk = *zero                                        .....
     c                   move      99            c2bnØk                                      .
     c                   endif                                                           .....
      *
     c                   if        c2bnØk = 99                                           .....
     c                   eval      c2mtxt = 'Jevn fordeling'                                 .
     c                   else                                                            .....
     c                   move      c2bnØk        p_mkod                                      .
     c                   call      'RS713R'                                      budsjettnøkkel-
     c                   parm                    p_stat
     c                   parm                    p_mkod
     c                   parm                    p_tx30
      *
     c                   eval      c2mtxt = p_tx30                                           .
     c                   endif                                                           .....
      *
      * Hent mengde
      *
     c                   eval      c2otxt = *blank                                           .
     c                   if        c2mngd <> *zero                                       .....
     c                   move      c2mngd        p_okod                                      .
     c                   call      'RS715R'                                      mengde-
     c                   parm                    p_stat
     c                   parm                    p_okod
     c                   parm                    p_tx30
      *
     c                   eval      c2otxt = p_tx30                                           .
     c                   endif                                                           .....
      *
      * Hent valuta
      *
     c                   eval      c2pmna = *blank                                           .
     c                   if        c2valk <> *blank                                      .....
     c                   move      c2valk        p_pkod                                      .
     c                   call      'RS716R'                                      valuta-     .
     c                   parm                    p_stat
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *
     c                   eval      c2pmna =p_tx20                                            .
     c                   endif                                                           .....
      *
     c     kont_end      endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på konto
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for posisjonering på avdeling
      *
     c     rhovl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl3_avde
     c                   kfld                    rhovl3_kont
     c                   kfld                    rhovl3_spes
      *
      * Key for posisjonering på kosnadsbærer
      *
     c     rhovl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl4_spes
     c                   kfld                    rhovl4_kont
     c                   kfld                    rhovl4_avde
      *
      * Key for posisjonering på beskrivelse
      *
     c     rhovl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl2_alfa
      *
      * Key for les på konto
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'K'
     c                   eval      rhovl1_kont = 0
     c                   eval      rhovl1_spes = 0
     c                   eval      rhovl1_avde = 0
     c     rhovl1_key    setll     rhovl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
