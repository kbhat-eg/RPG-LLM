     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : MD201R
      * Beskrivelse . . : Plukkområder fra fil
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051222 bsa  Nytt program
8.01  *K0000  110123 bsa  Bruker ny arbeidsfil
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.01 flwexpf    uf   e           k disk    rename(lexcpfr:lwexpfr)
      *
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     flpomlu    uf a e           k disk    rename(lpompfr:lpomlur)
     flpoml1    if   e           k disk    rename(lpompfr:lpoml1r)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
      *
      * Ny post til plukkområde
      *
     d                 ds
     d d_list                        80
     d  d_lage                        2    overlay(d_list: 1)
     d  d_ogrp                        2    overlay(d_list: 3)
     d  d_hgrp                        2    overlay(d_list: 5)
     d  d_ugrp                        3    overlay(d_list: 7)
     d  d_modn                        8    overlay(d_list:10)
     d  d_vare                       15    overlay(d_list:18)
     d  d_plom                       10    overlay(d_list:34)
     d  d_besk                       30    overlay(d_list:44)
     d  d_rlag                        2    overlay(d_list:75)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lpofir)
      *
      * Definering av variable i nøkler
      *
     d ra10l1_jkod     s                   like(rajkod)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
     d jmodl1_modn     s                   like(jumodn)
     d lpomlu_olag     s                   like(lpolag)
     d lpomlu_oogr     s                   like(lpoogr)
     d lpomlu_ohgr     s                   like(lpohgr)
     d lpomlu_ougr     s                   like(lpougr)
     d lpomlu_omod     s                   like(lpomod)
     d lpomlu_ovar     s                   like(lpovar)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lpofir)
     d w_dato          s               d   datfmt(*iso)
     d w_posi          s              4  0
      *
     d b_feil          s              1    inz(*off)
      *
      * Konstanter
      *
     d c_form          s              1    inz('1')
     d c_digi          s             15    inz('0123456789 ')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser og oppretter/oppdaterer plukkområder
      *
8.01 c                   read      lwexpf
     c                   dow       not %eof
     c                   exsr      utled_sdv
     c                   if        b_feil = *off
     c                   exsr      oppdater
     c                   endif
8.01 c                   read      lwexpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_sdv     begsr
      *
     c                   eval      d_list = *blank
     c                   eval      b_feil = *off
      *
     c     '"':' '       xlate     exclin        exclin
      *
      * Lager
      *
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_lage = %trim(%subst(exclin:1:w_posi-1))
     c     c_digi        check     d_lage                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
     c                   eval      ra10l1_jkod = %int(d_lage)
     c     ra10l1_key    chain     ra10l1
     c                   if        not %found
     c                   goto      end_utled
     c                   endif
      *
      * Overgruppe
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_ogrp = %trim(%subst(exclin:1:w_posi-1))
     c     c_digi        check     d_ogrp                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
     c                   if        %int(d_ogrp) > 0
     c                   eval      vogrl1_oogr = %int(d_ogrp)
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   goto      end_utled
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_hgrp = %trim(%subst(exclin:1:w_posi-1))
     c     c_digi        check     d_hgrp                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
     c                   if        %int(d_hgrp) > 0
     c                   eval      vhgrl1_hogr = %int(d_ogrp)
     c                   eval      vhgrl1_hhgr = %int(d_hgrp)
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   goto      end_utled
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_ugrp = %trim(%subst(exclin:1:w_posi-1))
     c     c_digi        check     d_ugrp                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
     c                   if        %int(d_ugrp) > 0
     c                   eval      vugrl1_uogr = %int(d_ogrp)
     c                   eval      vugrl1_uhgr = %int(d_hgrp)
     c                   eval      vugrl1_uugr = %int(d_ugrp)
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   goto      end_utled
     c                   endif
     c                   endif
      *
      * Modulnummer
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_modn = %trim(%subst(exclin:1:w_posi-1))
     c     c_digi        check     d_modn                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
     c                   if        %int(d_modn) > 0
     c                   eval      jmodl1_modn = %int(d_modn)
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   goto      end_utled
     c                   endif
     c                   endif
      *
      * Varenummer
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_vare = %trim(%subst(exclin:1:w_posi-1))
     c     c_digi        check     d_vare                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
     c                   if        %trim(d_vare) <> *blanks
     c                   eval      vvarl1_vare = d_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      end_utled
     c                   endif
     c                   endif
      *
      * Plukkområde
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_plom = %trim(%subst(exclin:1:w_posi-1))
      *
     c                   if        %trim(d_plom) = *blanks
     c                   goto      end_utled
     c                   endif
      *
      * Plukkområde beskrivelse
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_besk = %trim(%subst(exclin:1:w_posi-1))
      *
      * Plukkområde RDS lager
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c**                 eval      w_posi = %scan(';':exclin)
     c                   eval      d_rlag = %trim(exclin)
     c                   if        %trim(d_rlag) = *blanks
     c                   eval      d_rlag = '00'
     c                   endif
      *
     c                   if        %int(d_rlag) > 0
     c                   eval      ra10l1_jkod = %int(d_rlag)
     c     ra10l1_key    chain     ra10l1
     c                   if        not %found
     c                   goto      end_utled
     c                   endif
     c                   endif
      *
     c     end_utled     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Avbryter dersom nobb-nummer eller pris ikke er utfylt
      *
     c                   if        %int(d_lage) = 0 or
     c                             %trim(d_plom) = *blanks
     c                   leavesr
     c                   endif
      *
      * Legg ut ny post
      *
     c                   eval      lpomlu_olag = %int(d_lage)
     c                   eval      lpomlu_oogr = %int(d_ogrp)
     c                   eval      lpomlu_ohgr = %int(d_hgrp)
     c                   eval      lpomlu_ougr = %int(d_ugrp)
     c                   eval      lpomlu_omod = %int(d_modn)
     c                   eval      lpomlu_ovar = d_vare
     c     lpomlu_key    chain     lpomlu
     c                   if        not %found
     c                   clear                   lpomlur
     c                   eval      lpofir = w_firm
     c                   eval      lpolag = lpomlu_olag
     c                   eval      lpoogr = lpomlu_oogr
     c                   eval      lpohgr = lpomlu_ohgr
     c                   eval      lpougr = lpomlu_ougr
     c                   eval      lpomod = lpomlu_omod
     c                   eval      lpovar = lpomlu_ovar
     c                   endif
      *
     c                   eval      lpopom = d_plom
     c                   eval      lpotxt = %trim(d_besk)
     c                   eval      lporla = %int(d_rlag)
      *
     c                   eval      lpoeus = l_user
     c                   time                    lpoeda
     c                   time                    lpoeti
      *
     c                   if        %found(lpomlu)
     c                   update    lpomlur
     c                   else
     c                   eval      lpoous = l_user
     c                   time                    lpooda
     c                   time                    lpooti
     c                   write     lpomlur
     c                   endif
      *
      * Sletter rad i arbeidsfil
      *
8.01 c                   delete    lwexpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c*    *entry        plist
     c*                  parm                    p_firm
      *
     c     lpomlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpomlu_olag
     c                   kfld                    lpomlu_oogr
     c                   kfld                    lpomlu_ohgr
     c                   kfld                    lpomlu_ougr
     c                   kfld                    lpomlu_omod
     c                   kfld                    lpomlu_ovar
      *
      * Key for les på Overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for les på Hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for les på Undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
