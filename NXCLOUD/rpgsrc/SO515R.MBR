      /TITLE  ASOFAK Faktura, vise ordrehode (fra fakturahode-register)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : SO515R
      * Beskrivelse . . : Faktura, vise ordrehode (fra fakturahode-register)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
3.31  * R3.31 150601 AMD  Differensiert moms, 24 og 12 %
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     fSo515d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sofirm)
     d p_aarr          s                   like(soaarr)
     d p_binr          s                   like(sobinr)
     d p_numm          s                   like(sonumm)
     d p_suff          s                   like(sosuff)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d sohel1_suff     s                   like(sosuff)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sofirm)
     d*** w_raba          s                   like(forab1)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      sohel1_aarr = p_aarr
     c                   eval      sohel1_binr = p_binr
     c                   eval      sohel1_numm = p_numm
     c                   eval      sohel1_suff = p_suff
     c     sohel1_key    chain     sohel1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b1numm = sonumm
     c                   eval      b1suff = sosuff
     c                   eval      b1otyp = sootyp
      *
     c                   eval      b1selg = soselg
     c                   eval      b1avde = soavde
     c                   eval      b1proj = soproj
     c                   eval      b1lage = solage
      *
     c                   eval      b1vref = sovref
     c                   eval      b1dref = sodref
     c                   eval      b1rekv = sorekv
     c                   eval      b1betb = sobetb
      *
     c                   eval      b1kund = sokund
     c                   eval      b1kpro = sokpro
     c                   eval      b1navn = sonavn
     c                   eval      b1adr1 = soadr1
     c                   eval      b1adr2 = soadr2
     c                   eval      b1sted = sosted
      *
3.31 c                   eval      b1totk = sokstp + soksth + sokstf
3.31 c                   eval      b1totr = sork1p + sork1h + sork1f +
3.31 c                                      sork2p + sork2h + sork2f +
3.31 c                                      sorkop + sorkoh + sorkof
3.31 c                   eval      b1tots = sosalp + sosalh + sosalf -
3.31 c                                      b1totr
      *
     c                   eval      b1sped = sosped
     c                   eval      b1lmat = solmat
     c                   eval      b1lmtx = solmtx
     c                   eval      b1lbet = solbet
     c                   eval      b1lbt1 = %subst(solbtx :  1 : 25)
     c                   eval      b1lbt2 = %subst(solbtx : 26 : 25)
     c                   eval      b1lbt3 = %subst(solbtx : 51 : 25)
      *
     c                   eval      b1bdat = 0
     c                   eval      b1ldat = 0
     c                   eval      b1fkda = 0
      *
     c                   if        sobdat <> *loval
     c     *dmy          move      sobdat        b1bdat
     c                   endif
      *
     c                   if        soldat <> *loval
     c     *dmy          move      soldat        b1ldat
     c                   endif
      *
     c                   if        sofkda <> *loval
     c     *dmy          move      sofkda        b1fkda
     c                   endif
      *
     c**                 if        fosalg <> 0
     c**                 eval(h)   w_raba = forab1 +
     c**                                    ((100 - forab1) * forab2 / 100)
     c**                 eval(h)   b1salg = fosalg - (fosalg * w_raba / 100)
     c**                 endif
      *
     c                   if        sokund <> 0
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      b1knav = rknavn
     c                   eval      b1kgat = rkgate
     c                   eval      b1kad2 = rkadr2
     c                   eval      b1kste = rksted
     c                   endif
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
6.nu c                   parm                    p_binr
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
     c                   kfld                    sohel1_suff
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
