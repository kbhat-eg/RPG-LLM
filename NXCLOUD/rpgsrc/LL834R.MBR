     H/TITLE  ASLAGR: Nullstiller lagersaldo        , Oppdateringen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LL824R                                       *
      *  Beskrivelse . . : Nullstiller lagersaldo, Oppdateringen        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign *
      * --------- ------  ----------------------------------------------*
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *
     FVLAGL1    IF   E           K DISK
     F                                     RENAME(VLAGPFR:VLAGL1R)
     FVLAGLU    UF   E           K DISK
     F                                     RENAME(VLAGPFR:VLAGLUR)
      *
     D                 DS
      *                                                      Parameter-inn
     D  DSPREC                 1     64
     D  DSBATC                 1     10
     D  DSVAL1                11     11  0
     D  DSLAGE                12     13  0
      *
     D/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  Local Data Area                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                UDS
6.10 d l_user                911    920
     D  DSFIRM               944    946  0
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utplukk, Setter inn riktige parameterverdier          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *BLANK        LAGVAR
     C                   MOVE      DSLAGE        LAGNUM
     C     LAGKEY        SETLL     VLAGL1R
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Leser poster fra register for utplukks-test                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSTART        TAG
      *
     C                   MOVE      *OFF          *IN90
     C                   READ      VLAGL1R                                90
     C     *IN90         CABEQ     *ON           XSLUTT
      *
      *  Sjekk   Er det korrekt firma som leses,    Ellers hopp til Slutt
      *
     C     VLFIRM        CABNE     LAGFIR        XSLUTT
      *
      *  Sjekk   Er det korrekt lager som leses,    Ellers hopp til Start
      *
     C     VLLAGE        CABNE     LAGNUM        XSTART
      *
      *  Nullstiller lagersaldo på valgt record
      *
     C                   MOVE      VLVARE        LAGVAR
     C                   MOVE      VLLAGE        LAGNUM
     C     LAGKEY        CHAIN     VLAGLU                             94
     C     *IN94         IFEQ      *OFF
     C                   EVAL      VLBEHL = 0
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     C                   UPDATE    VLAGLUR
     C                   END
      *
      *  Velg neste
      *
     C                   GOTO      XSTART
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
      *  Redefinering av felter
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VLFIRM        LAGFIR
     C     *LIKE         DEFINE    VLVARE        LAGVAR
     C     *LIKE         DEFINE    VLLAGE        LAGNUM
      *
      *  Key for file : LAGER-REGISTER
      *
     C     LAGKEY        KLIST
     C                   KFLD                    LAGFIR
     C                   KFLD                    LAGVAR
     C                   KFLD                    LAGNUM
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WWPARM           64
     C                   MOVE      WWPARM        DSPREC
      *
      *  Legge inn firmanummer i nøklene
      *
     C                   MOVE      DSFIRM        LAGFIR
      *
     CSR                 ENDSR
