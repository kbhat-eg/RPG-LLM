     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RK521R
      * Beskrivelse . . : Kunder, vedlikehold uttrekk
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120802 KOB  Nytt program
      * R5.60      021108 Endret til bruk av ny timestamp på RKWBPF      KOB  *
      * R5.60      191208 Korrigert posisjonering etter sletting         KOB  *
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.xx  060516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
     frkwbl1    if   e           k disk    rename(rkwbpfr:rkwbl1r)
     frkwblu    uf   e           k disk    rename(rkwbpfr:rkwblur)
     frksopf    if   e           k disk
      *
     frk521d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rkunlr_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
     d rkunl2_alfa     s                   like(rkalfa)
     d rkwbl1_kund     s                   like(rnkund)
     d rkwblu_kund     s                   like(rnkund)
     d rkwblu_bdat     s                   like(rnbdat)
     d rkwblu_tist     s                   like(rwtist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d b_open_sok      s              1    inz(*off)
      *
     d w_udat_iso      s               d   datfmt(*iso)
     d w_udat_alf      s              8
     d w_udat          s              8  0
     d w_valg          s              1  0
     d w_retk          s              1
     d w_4knr          s              6  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d s_stek          s             35
      *
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
     d w_sald          s                   like(rksald)
      *
     d p_stat          s              1
     d p_syst          s              4
      *
      * Definering av konstanter
      *
     d s_kund          s                   like(rkkund)
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kund <> 0 or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'A'
     c                   eval      rkunl2_alfa = b1alfa
     c     rkunl2_key    setll     rkunl2
     c                   other
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søk i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    setll     rkunl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på alfafelt
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      rkunl2_alfa = b2alfa
     c     rkunl2_key    setll     rkunl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kund = 0
     c                   eval      b2alfa = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Slette poster
      *
     c                   if        b1valg = 4
     c                   eval      *in30  = *on
     c                   eval      w_kund = b1kund
     c                   eval      w_valg = b1valg
     c                   exsr      xslett
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise posteringer
      *
     c                   if        b1valg = 8
     c                   eval      *in30  = *on
     c                   exsr      posteringer
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_kund = b1kund
     c                   call      'RK502R'
     c                   parm                    w_kund
     c                   parm                    w_valg
      *
     c                   eval      b2kund = *zeros
     c                   eval      b2alfa = *blank
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *on
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select rksfir,
     c+                         rkstek,
     c+                         rkskun
     c+                  from   rksopf
     c+                  where  rksfir = :w_firm and
     c+                         rkstek like :w_ste1
     c+                  order by rksfir, rkstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :rksfir,
     c+                        :rkstek,
     c+                        :rkskun
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'A'
     c                   read      rkunl2                                 15
     c                   other
     c                   read      rkunl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(rkstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      rkunl1_kund = rkskun
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        rkfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   move      rkkund        rkwbl1_kund
     c     rkwbl1_key    chain     rkwbl1                             60
     c                   if        *in60 = *on
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1kund = rkkund
     c                   eval      b1navn = rknavn
     c                   eval      b1sted = rksted
     c                   eval      b1alfa = rkalfa
7.01  *
7.01  * Sjekk om inkassokode = 'J' - hvis ja sett linja rød
7.01  *
7.01 c                   if        rkinkk = 'J'
7.01 c                   eval      *in80 = *on
7.01 c                   else
7.01 c                   eval      *in80 = *off
7.01 c                   endif
      *
     c                   exsr      sum_poster
     c                   eval      b1sald = w_sald
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setgt     rkunl2
     c                   readp     rkunl2
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setgt     rkunl1
     c                   readp     rkunl1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     rkunl2
     c                   other
     c                   readp     rkunl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rkfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rkunl2_alfa = rkalfa
     c                   other
     c                   eval      rkunl1_kund = rkkund
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rkunl2)
     c     rkunl2_key    setll     rkunl2
     c                   when      w_seqe = *blank and
     c                             %eof(rkunl1)
     c     rkunl1_key    setll     rkunl1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Vise posteringer for kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posteringer   begsr
      *
     c                   call      'RK120R'
     c                   parm                    w_valg
     c                   parm                    b1kund
     c                   parm                    p_syst
      *
     c                   eval      b2kund = *zeros
     c                   eval      b2alfa = *blank
     c                   eval      *in31  = *on
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å summere posteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_poster    begsr
      *
     c                   eval      w_sald = 0                                              . .
      *
     c                   eval      rkwbl1_kund = b1kund
     c     rkwbl1_key    setll     rkwbl1
      *
     c     rkwbl1_key    reade     rkwbl1                                 16
      *
     c                   dow       *in16 = *off
      *
     c                   eval      w_sald = w_sald + rnrest                                . .
      *
     c     rkwbl1_key    reade     rkwbl1                                 16
     c                   enddo
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslett        begsr
      *
     c                   eval      d1kund = b1kund
     c                   eval      d1navn = b1navn
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      rkwbl1_kund = w_kund
     c     rkwbl1_key    setll     rkwbl1
     c     rkwbl1_key    reade     rkwbl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      rkwblu_kund = w_kund
     c                   eval      rkwblu_bdat = rnbdat
     c                   eval      rkwblu_tist = rwtist
     c     rkwblu_key    chain     rkwblu                             91
     c                   if        *in91 = *off
     c                   delete    rkwblur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     rkwbl1_key    reade     rkwbl1                                 90
     c                   enddo
      *
     c     end_d1win     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_syst
      *
      * Key for oppslag på kunde
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      * Key for posisjonering på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på alfa
      *
     c     rkunl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl2_alfa
      *
      * Key for kundeposter via kundenummer
      *
     c     rkwbl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkwbl1_kund
      *
      * Key for kundeposter via kundenummer
      *
     c     rkwblu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkwblu_kund
     c                   kfld                    rkwblu_bdat
     c                   kfld                    rkwblu_tist
      *
      * Henter dagens dato
      *
     c                   time                    w_udat_iso
     c     *iso0         move      w_udat_iso    w_udat_alf
     c                   move      w_udat_alf    w_udat
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rkunl1_kund = 0
     c     rkunl1_key    setll     rkunl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
