      /title  ASLAGR Håndterminal ordre-hode, sletting av referanse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HO400R
      * Beskrivelse . . : Sletting av ordretranser fra håndterminal
      *                   Sletter plukkede ordretransaksjoner fra
      *                   håndterminalfil for gitt referanse
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190600 HKO  Nytt program
      *       280900 HKO  Tilpasset nye filer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhohelu    uf   e           k disk    rename(hohepfr:hohelur)
     fhodtlu    uf   e           k disk    rename(hodtpfr:hodtlur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(hofirm)
     d p_refn          s                   like(horefn)
      *
      * Definering av variable
      *
     d w_firm          s                   like(hofirm)
     d w_refn          s                   like(horefn)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprorgram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter alle plukkede ordretransaksjoner
      *
     c     hohelu_key    chain     hohelur                            90
     c                   if        *in90 = *off and
     c                             hostat = 1
      *
     c                   delete    hohelur
      *
     c     hodtlu_key    setll     hodtlu
     c     hodtlu_key    reade     hodtlur                                91
     c                   dow       *in91 = *off
      *
     c                   delete    hodtlur
      *
     c     hodtlu_key    reade     hodtlur                                91
     c                   enddo
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_refn
      *
      * Key for oppdatering av håndterminal ordre-hode
      *
     c     hohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_refn
      *
      * Key for oppdatering av håndterminal ordre-linje
      *
     c     hodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_refn
      *
      *
      * Henter firma og referanse fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_refn = p_refn
      *
     c                   endsr
