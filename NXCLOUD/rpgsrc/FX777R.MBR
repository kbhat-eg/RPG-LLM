     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FX777R
      * Beskrivelse . . : Oppdatere kontohenvisning og varegruppe
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200208 KOB  Nytt program
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsodtlu    uf   e           k disk    rename(sodtpfr:sodtlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sdfirm)
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vkhvl1_kkod     s                   like(vakkod)
     d vkhvl1_klty     s                   like(vaklty)
     d vkhvl1_koty     s                   like(vakoty)
     dkai              s              1
      *
      * Definering av variable
      *
     d w_firm          s                   like(sdfirm)
      *
     c     sodtlu_key    setll     sodtlu                                 90
     c     sodtlu_key    reade     sodtlu                                 90
      *
     c                   dow       *in90 = *off
      *
      * Hent kontohenvisning fra vare
      *
     c                   if        sdvare = '11302684'
     c                   eval      kai = '1'
     c                   endif
      *
     c                   eval      vvarl1_vare = sdvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   if        %found
      *
      * Hvis varegruppe ikke er lagt ut på transaksjon, hent fra vare
      *
     c                   if        sdogrp = 0
     c                   eval      sdogrp = vvogrp
     c                   eval      sdhgrp = vvhgrp
     c                   eval      sdugrp = vvugrp
     c                   endif
      *
     c                   if        vvkhev <> ' '
     c                   eval      sdkhev = vvkhev
     c                   goto      oppdat
     c                   endif
      *
      * Hent kontohenvisning fra undergruppe
      *
     c                   eval      vugrl1_uogr = sdogrp
     c                   eval      vugrl1_uhgr = sdhgrp
     c                   eval      vugrl1_uugr = sdugrp
     c     vugrl1_key    chain     vugrl1
      *
     c                   if        %found
     c                   if        vgukhv <> ' '
     c                   eval      sdkhev = vgukhv
     c                   goto      oppdat
     c                   endif
     c                   endif
      *
      * Hent kontohenvisning fra hovedgruppe
      *
     c                   eval      vhgrl1_hogr = sdogrp
     c                   eval      vhgrl1_hhgr = sdhgrp
     c     vhgrl1_key    chain     vhgrl1
      *
     c                   if        %found
     c                   if        vghkhv <> ' '
     c                   eval      sdkhev = vghkhv
     c                   goto      oppdat
     c                   endif
     c                   endif
      *
      * Hent kontohenvisning fra overgruppe
      *
     c                   eval      vogrl1_oogr = sdogrp
     c     vogrl1_key    chain     vogrl1
      *
     c                   if        %found
     c                   if        vgokhv <> ' '
     c                   eval      sdkhev = vgokhv
     c                   else
     c                   eval      sdkhev = '9999'
     c                   endif
     c                   endif
      *
     c     oppdat        tag
      *
      * Dersom salgskonto ikke er utfult, hent denne fra kontohenvisningsregisteret
      *
     c                   exsr      hent_kto_henv
      *
     c                   if        sdomva = '3'
     c                   eval      sdavde = vakav1
     c                   eval      sdkont = vakko1
     c                   eval      sdspes = vaksp1
     c                   else
     c                   eval      sdavde = vakav2
     c                   eval      sdkont = vakko2
     c                   eval      sdspes = vaksp2
     c                   endif
      *
6.10 c                   time                    sdedat
6.10 c                   time                    sdetim
6.10 c                   eval      sdeusr = l_user
     c                   update    sodtlur
      *
     c                   endif
      *
     c     sodtlu_key    reade     sodtlu                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent kontohenvisningsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kto_henv begsr
      *
     c                   eval      vkhvl1_kkod = sdkhev
     c                   eval      vkhvl1_klty = sdlety
     c                   eval      vkhvl1_koty = sdotyp
     c     vkhvl1_key    chain     vkhvl1
      *
     c                   if        not%found
     c                   eval      vkhvl1_koty = ' '
     c     vkhvl1_key    chain     vkhvl1
      *
     c                   if        not%found
     c                   eval      vkhvl1_klty = 0
     c     vkhvl1_key    chain     vkhvl1
     c                   endif
      *
     c                   if        not%found
     c                   eval      vkhvl1_kkod = '9999'
     c                   eval      sdkhev = '9999'
     c     vkhvl1_key    chain     vkhvl1
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Key for les av varlinjer
      *
     c     sodtlu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på kontohenvisningsregisteret
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
     c                   kfld                    vkhvl1_klty
     c                   kfld                    vkhvl1_koty
      *
      *
     c                   endsr
