     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP141R
      * Beskrivelse . . : Prosjekt, Oppdatere prosjetk i FODT fra FOHE på en ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110320 sst  Nytt program
7.01  *       040820 sst  Justert håndtering av ordrenummer
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
      *
      * Definering av Local data area
      *
     d                udS
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      * Definering av variabler i nøkler
      *
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
     d p_numm          s              8
     d p_suff          s              2
     d w_numm          s              8  0
     d w_suff          s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Oppdatere fodtPF   fra FOHEPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      fohel1_numm = w_numm
     c                   eval      fohel1_suff = w_suff
     c     fohel1_key    chain     fohel1
      *
     c                   if        %found(fohel1)
     c                   eval      fodtlu_numm = w_numm
     c                   eval      fodtlu_suff = w_suff
     c     fodtlu_key    setll     fodtlu
     c     fodtlu_key    reade     fodtlu
      *
     c                   dow       not %eof(fodtlu)
      *
     c                   eval      fdproj = foproj
     c*                  eval      fdupro = foupro
     c                   eval      fdakti = foakti
      *
     c                   update    fodtlur
      *
     c     fodtlu_key    reade     fodtlu
     c                   enddo
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
     c                   eval      w_firm  = l_firm
     c                   eval      w_numm = %dec(p_numm:8:0)
     c                   eval      w_suff = %dec(p_suff:2:0)
      *
     c                   endsr
