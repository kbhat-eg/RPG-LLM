      /TITLE  ASLAGR: Utskrift av diversevarer, plukker varenummer
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LE622R                                       *
      *  Beskrivelse . . : Utskrift av diversevarer, plukker varenummer *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign *
      * --------- ------  ----------------------------------------------*
5.70  *  PRGR     021208  Utvidet med prisgruppe                   bof
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *
     fltell1    if   e           k disk     rename(ltelpfr:ltell1r)
     fvvarl1    if   e           k disk     rename(vvarpfr:vvarl1r)
     flsorlu    uf a e           k disk     rename(lsorpfr:lsorlur)
      *
      *   Local Data Area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      *
      *   Definering av datastrukturer
      *
     d                 ds
      *                                                      Parameter-inn
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fogr                        2  0 overlay(d_prec:11)
     d  d_togr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_thgr                        2  0 overlay(d_prec:17)
     d  d_fugr                        3  0 overlay(d_prec:19)
     d  d_tugr                        3  0 overlay(d_prec:22)
     d  d_fvar                       15    overlay(d_prec:25)
     d  d_tvar                       15    overlay(d_prec:40)
     d  d_flag                        2  0 overlay(d_prec:55)
     d  d_tlag                        2  0 overlay(d_prec:57)
     d  d_sort                        1  0 overlay(d_prec:59)
      *
      *
      *   Definering av variable
      *
     d ltell1_batc     s                   like(lebatc)
     d ltell1_firm     s                   like(lefirm)
     d ltell1_line     s                   like(leline)
     d ltell1_numm     s                   like(lenumm)
     d ltell1_suff     s                   like(lesuff)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
     d w_parm          s            128
     d w_firm          s                   like(vvfirm)
     d w_slbe          s                   like(lenumm)
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
      *
      *
      * Definering av parametre
      *
     d p_dato          s               d   datfmt(*iso)
     d p_time          s               t   timfmt(*iso)
     d p_inlr          s              1
     d p_lety          s              1  0
      *
      *
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utplukk, Setter inn riktige parameterverdier          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   eval      d_prec = w_parm
      *
     c                   eval      ltell1_batc = d_batc
     c                   eval      ltell1_numm = *zero
     c                   eval      w_slbe = 999999
      *
     c     ltell1_key    setll     ltell1r
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Leser poster fra register for utplukks-test                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     start         tag
      *
     c                   eval      *in90 = *off
     c                   read      ltell1r                                90
      *
     c                   if        *in90 = *on
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er firmanummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        lefirm <> ltell1_firm
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er batchnummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        lebatc <> ltell1_batc
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er listenummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        lenumm > w_slbe
     c                   goto      slutt
     c                   endif
      *
      *  Sjekk   Er listenummer en subliste        Hvis nei les neste post
      *
     c                   if        lesuff <> *zero
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er overgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        leogrp < d_fogr
     c                             or leogrp > d_togr
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er hovedgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        lehgrp < d_fhgr
     c                             or lehgrp > d_thgr
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er Undergruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        leugrp < d_fugr
     c                             or leugrp > d_tugr
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er Varenummer  innenfor grensene, Hvis nei les neste post
      *
     c                   if        levare < d_fvar
     c                             or levare > d_tvar
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er Lagernummer innenfor grensene  Hvis nei les neste post
      *
     c                   if        lelage < d_flag
     c                             or lelage > d_tlag
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Kun diversevarer tas med
      *
     c                   if        ledlop = *zero
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Kun varer meg antall tellet tas med
      *
     c                   if        leantt = *zero
     c                   goto      start
     c                   endif
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk opplysninger i Plukk-register                            *
      *  Utfør de riktige valgene utfra hva som er tastet               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   eval      vvarl1_vare = levare
     c     vvarl1_key    chain     vvarl1r                            90
      *
     c                   if        leogrp = *zero
     c                   eval      leogrp = vvogrp
     c                   endif
     c                   if        lehgrp = *zero
     c                   eval      lehgrp = vvhgrp
     c                   endif
     c                   if        leugrp = *zero
     c                   eval      leugrp = vvugrp
     c                   endif
      *
     c                   eval      p_lety = 0
     c                   eval      p_inlr = ' '
     c                   move      udate         p_dato
5.70 c                   exsr      hent_prisgr
      *
      * Henter og beregner prisgrunnlag fra vareregisteret
      *
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    levare
5.70 c                   parm                    w_prgr
     c                   parm                    leenh1
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
      *
     c                   if        lespny = *zero
     c                   eval      lespny = w_sapr
     c                   endif
     c                   if        lekpny = *zero
     c                   eval      lekpny = w_kopr
     c                   endif
      *
      *  Sorteringsrekkefølge
      *
     c                   eval      lwfirm = lefirm
     c                   eval      lwlage = lelage
     c                   eval      lwlagr = lelage
     c                   eval      lwvare = levare
     c                   eval      lwnumm = ledlop
      *
     c                   eval      lwogrp = leogrp
     c                   eval      lwhgrp = lehgrp
     c                   eval      lwugrp = leugrp
      *
     c                   eval      lwspny = lespny
     c                   eval      lwkpny = lekpny
     c                   eval      lwantt = leantt
     c                   eval      lwenh1 = leenh1
     c                   eval      lwtek1 = letek1
      *
     c                   if        lwtek1 = *blank
     c                   eval      lwtek1 = vvtek1
     c                   endif
      *
     c                   if        lwenh1 = *blank
     c                   eval      lwenh1 = vvenhl
     c                   endif
      *
     c                   write     lsorlur
      *
      *  Velg neste varelinje
      *
     c                   goto      start
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lelage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      *  Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for file : LAGER-TELLE-REGISTER
      *
     c     ltell1_key    klist
     c                   kfld                    ltell1_firm
     c                   kfld                    ltell1_batc
     c                   kfld                    ltell1_numm
     c                   kfld                    ltell1_suff
     c                   kfld                    ltell1_line
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
      *  Legge inn firmanummer i nøkklene
      *
     c                   eval      vvarl1_firm = l_firm
     c                   eval      ltell1_firm = l_firm
     c                   eval      w_firm = l_firm
      *
     c                   endsr
