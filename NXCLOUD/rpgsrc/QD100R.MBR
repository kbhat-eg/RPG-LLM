     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : QD100R
      * Beskrivelse . . : Synke ordrelinje med bestilling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260319 bsa  Nytt program
8.01  *       010419 bsa  Tilleggskontroller
8.02  * 8.xx  070322 bsa  Skal akseptere å legge til linjer selv om bestillingen
8.02  * 8.xx              er sendt. NXS-12225
8.03  * 8.xx  100723 bsa  Switch for om det skal flagges at nye linjer til, og
8.03  * 8.xx              også legges til. NXS-17400
8.04  * 8.xx  021023 bsa  Utvidet funksjonalitet med valg 3 i verdi 2.
8.05  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flloglr    if   e           k disk    rename(llogpfr:lloglrr)
     flodtlc    if   e           k disk    rename(lodtpfr:lodtlcr)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
8.03 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av datastrukturer
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
     d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
     d  hlonum                        8  0 overlay(hlrec:90)
     d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d lloglr_numm     s                   like(lonumm)
     d lloglr_suff     s                   like(losuff)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d lodtlc_ornu     s                   like(ldornu)
     d lodtlc_orsu     s                   like(ldorsu)
     d lodtlc_orli     s                   like(ldorli)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d vmval1_mkod     s                   like(vamkod)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
     d b_mult          s              1    inz(*off)
      *
     d w_kamp          s              5
     d w_kpny          s                   like(ldkpny)
     d w_lety          s              1  0
     d w_lrec          s            128
     d w_lv_lldo       s                   like(fdldor)
     d w_lv_llva       s                   like(fdlvar)
     d w_date          s               d   datfmt(*iso)
     d w_enor          s              1
     d w_ldor          s                   like(fdldor)
     d w_lina          s              4
     d w_numa          s              8
     d w_firm          s                   like(fdfirm)
     d w_line          s                   like(fdline)
     d w_lv_nobb       s                   like(vvnobb)
     d w_lv_modn       s                   like(vvnmod)
     d w_stat          s              1
     d w_vtyp          s                   like(vvvtyp)
     d w_udat          s               d   datfmt(*iso)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_prgr          s                   like(fdprgr)
     d w_mld1          s             50
     d w_mld2          s             50
     d w_olin          s                   like(fdline)
     d w_benr          s                   like(fdnumm)
     d w_besu          s                   like(fdsuff)
     d w_in12          s              1  0
     d w_svar          s              1
8.03 d w_type          s              1
      * Eksternt program
8.03   dcl-s co402_verdi1  char(1);
8.03   dcl-s co402_verdi2  char(2048);
8.03   DCL-PR CO402R EXTPGM('CO402R');
8.03     p_filgrp            char(3)     const;
8.03     p_firm              packed(3:0) const;
8.03     p_lager             packed(2:0) const;
8.03     p_nokkel            char(50)    const;
8.03     p_verdi1            char(1);
8.03     p_verdi2            char(2048);
8.03   END-PR;
8.03 d u_order         s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk om det ordrelinje finnes
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtl1_line = p_line
     c     fodtl1_key    chain     fodtl1r
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk om det ordrelinjen allerede er knyttet
      *
     c                   eval      lodtlc_ornu = p_numm
     c                   eval      lodtlc_orsu = p_suff
     c                   eval      lodtlc_orli = p_line
     c     lodtlc_key    chain     lodtlcr
     c                   if        %found
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk om det finnes bestilling knyttet mot denne ordren.
      *
     c     lodtlc_key2   chain     lodtlcr
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker om bestillingshode finnes
      *
     c                   eval      lohel1_numm = ldnumm
     c                   eval      lohel1_suff = ldsuff
     c     lohel1_key    chain     lohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Bestilling kan ikke være sendt
8.02  * Skal ikke sjekke på dette. Ref Tom Erik NXS-12225
      *
8.02 c**                 eval      lloglr_numm = lohel1_numm
8.02 c**                 eval      lloglr_suff = lohel1_suff
8.02 c**   lloglr_key    setll     lloglr
8.02 c**   lloglr_key    reade     lloglr
8.02 c**                 dow       not %eof
8.02 c**                 if        %trim(lmkode) > '1'
8.02  *
8.02 c**                 eval      w_mld1 = 'Bestilling er allerede +
8.02 c**                                     sendt til leverandør'
8.02 c**                 call      'AA005R'
8.02 c**                 parm                    w_mld1
8.02  *
8.02 c**                 goto      avslutt
8.02 c**                 endif
8.02 c**   lloglr_key    reade     lloglr
8.02 c**                 enddo
      *
      * Varelinje ?
      *
     c*                  if        fdvare = *blanks
     c*                  goto      avslutt
     c*                  endif
      *
      * Linje er allerede bestilt
      *
     c                   if        fdantb <> 0
     c                   goto      avslutt
     c                   endif
8.01  *
8.01  * Skal ikke kunne legge til vare med akkumulatortype 1
8.01  *
8.01 c                   eval      votyl1_otyp = lootyp
8.01 c     votyl1_key    chain     votyl1r
8.01  *
8.01 c                   if        vaoakk > 1
8.01  *
8.01 c                   eval      w_mld1 = 'Bestilling allerede +
8.01 c                                       motatt/fakt. Ingen oppdatering'
8.01 c                   call      'AA005R'
8.01 c                   parm                    w_mld1
8.01  *
8.01 c                   goto      avslutt
8.01 c                   endif
8.03  *
8.03  * Sjekk om linja skal legges til
8.03  *
8.03 c                   if        u_order = *on and
8.03 c                             w_type = '2'
8.03 c                   goto      avslutt
8.03 c                   endif
8.03  *
8.03  * Sjekk om linja skal legges til, eller
8.03  *
8.03 c                   if        u_order = *off or
8.03 c                             u_order = *on and
8.04 c                             w_type = '0' or
8.04 c                             u_order = *on  and
8.04 c                             w_type = '3'
8.01  *
8.01  * Varselbilde for bekrefting av opprettelse av bestillingslinje
8.01  *
8.01 c                   eval      w_mld1 = 'Skal vare opprettes på bestilling'
8.01 c                   eval      w_mld2 = '(bekreft at dette ønskes)        '
8.01 c                   eval      w_svar = 'J'
8.04 c                   if        w_type = '3'
8.04 c                   eval      w_svar = 'N'
8.04 c                   endif
8.01  *
8.01 c                   call      'AA007R'
8.01 c                   parm                    w_mld1
8.01 c                   parm                    w_mld2
8.01 c                   parm                    w_svar
8.01 c                   parm                    w_in12
8.01  *
8.01  * F12 er tatt
8.01  *
8.01 c                   if        w_in12 = 1 or
8.01 c                             w_svar = 'N'
8.01 c                   goto      avslutt
8.01 c                   endif
8.01  *
8.03 c                   endif
8.01  *
8.01  * Opprettelse er akseptert - sjekk om det er mer enn
8.01  * et bestillingsnummer.
8.01  *
8.01 c                   eval      w_benr = 0
8.01 c                   eval      w_besu = 0
8.01 c                   eval      w_olin = p_line
8.01  *
8.01 c                   call      'QD101R'
8.01 c                   parm                    w_firm
8.01 c                   parm                    w_numm
8.01 c                   parm                    w_suff
8.01 c                   parm                    w_olin
8.01 c                   parm                    w_benr
8.01 c                   parm                    w_besu
      *
8.01 c                   if        w_benr <> 0
      *
      * Finn siste linjenr på bestilling
      *
     c                   exsr      ex_hnt_blin
     c                   eval      w_line = w_line + 10
      *
     c                   clear                   lodtlur
      *
     c                   eval      lodtlu_numm = w_benr
     c                   eval      lodtlu_suff = w_besu
     c                   eval      lodtlu_line = w_line
     c     lodtlu_key    chain     lodtlur
      *
     c                   if        not %found
     c                   eval      ldfirm = w_firm
     c                   eval      ldnumm = w_benr
     c                   eval      ldsuff = w_besu
     c                   eval      ldline = w_line
      *
     c                   eval      ldltyp = fdltyp
     c                   eval      ldlety = fdlety
     c                   eval      ldvare = fdvare
     c                   eval      ldenh1 = fdenh1
     c                   eval      ldlage = fdlage
     c                   eval      ldldor = fdldor
     c                   eval      ldldat = *loval
     c                   eval      ldavde = *zero
     c                   eval      ldkont = *zero
     c                   eval      ldspes = *zero
     c                   eval      ldproj = *blank
     c                   eval      ldakti = *blank
     c                   eval      ldlvar = fdlvar
     c                   eval      ldnobb = fdnobb
     c                   eval      ldeann = fdeann
     c                   eval      ldtek1 = fdtek1
     c                   eval      ldtek2 = fdtek2
     c                   eval      ldogrp = fdogrp
     c                   eval      ldhgrp = fdhgrp
     c                   eval      ldugrp = fdugrp
     c                   eval      ldlvre = fdlvre
     c                   eval      ldipny = *zero
     c                   eval      ldkpny = *zero
     c                   eval      ldrab1 = *zero
     c                   eval      ldrab2 = *zero
      *
     c                   eval      ldornu = fdnumm
     c                   eval      ldorsu = fdsuff
     c                   eval      ldorli = fdline
     c                   eval      ldotyp = footyp
     c                   eval      ldanta = fdanta
     c                   eval      ldkpny = fdkopr
     c                   eval      ldipny = fdinpr
      *
     c                   if        laaenl = 1
     c                   eval      ldipny = ldkpny
     c                   else
     c                   if        ldipny = *zero
      *
      * Finner innkjøpspris fra register dersom den skal overstyre.
      * Gjelder bare lagervarer.
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r                            90
      *
     c                   if        %found
      * finner varetype
     c                   call      'VL710R  '
     c                   parm                    w_firm
     c                   parm                    fdvare
     c                   parm                    fdlage
     c                   parm                    w_vtyp
     c                   parm                    w_udat
     c                   parm                    w_ldor
     c                   parm                    b_inlr
     c                   parm                    w_lv_nobb
     c                   parm                    w_lv_modn
     c                   parm                    w_lv_lldo
     c                   parm                    w_lv_llva
      *
     c                   if        w_vtyp = 'L'
     c                   eval      w_lety = 0
     c                   eval      w_prgr = fdprgr
     c                   eval      w_kamp = lokamp
      *
     c                   call      'VP753R'
     c                   parm                    w_firm
     c                   parm                    fdvare
     c                   parm                    w_prgr
     c                   parm                    fdenh1
     c                   parm                    w_lety
     c                   parm                    w_date
     c                   parm                    w_kpny
     c                   parm                    ldipny
     c                   parm                    w_kamp
     c                   endif
     c                   endif
      *
     c                   if        ldipny = *zero
     c                   eval      ldipny = ldkpny
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      ldsumk = ldkpny * ldanta
     c                   eval      ldsumi = ldipny * ldanta
      *
      * Vi må sette mvakode på linja
      *
     c                   eval      ldomva = '1'
     c                   if        lomkod = 1
     c                   eval      ldomva = '0'
     c                   goto      endtes
     c                   endif
     c                   if        lomkod = 2
     c                   eval      ldomva = 'C'
     c                   endif
      *
      * Sjekk evnt overstyring fra varen
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   eval      vmval1_mkod = vvkmva
     c     vmval1_key    chain     vmval1
     c                   if        %found
     c                   eval      ldomva = vamkla
     c                   else
     c                   select
     c                   when      vvkmva = 1
     c                   eval      ldomva = '0'
     c                   when      vvkmva = 2
     c                   eval      ldomva = 'I'
     c                   when      vvkmva = 3
     c                   eval      ldomva = 'K'
     c                   endsl
     c                   endif
     c                   endif
      *
     c     endtes        tag
      *
      * Kaller opp subrutine for oppdatering av lager
      * Trekker ut antall for vedlikeholdt linje
      *
     c                   if        laalag <> *zero
     c                   exsr      opplag
     c                   endif
      *
     c                   time                    ldodat
     c                   time                    ldotim
     c                   eval      ldousr = l_user
     c                   time                    ldedat
     c                   time                    ldetim
     c                   eval      ldeusr = l_user
     c                   write     lodtlur
     c                   endif
      *
      * Oppdaterer ordre-linje-register med nytt antall bestilt
      *
     c     fodtl1_key    chain     fodtlur
     c                   if        %found
     c                   eval      fdantb = fdanta
     c                   eval      fdbenr = w_benr
     c                   eval      fdbsuf = w_besu
     c                   eval      fdblin = ldline
      *
     c                   if        fdvare = *blank
     c                   eval      fdantb = 0
     c                   eval      fdbenr = 0
     c                   eval      fdbsuf = 0
     c                   eval      fdblin = 0
     c                   endif
      *
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   endif
      *
      * Opdater ordrehodet med info om bestillingsnummer
      *
     c     fohel1_key    chain     fohelur
     c                   if        %found
     c                   eval      fobenr = w_benr
     c                   eval      fobsuf = w_besu
      *
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * Oppdaterer bestillingshode med ordre, hvis den ikke finnes fra før
      *
     c                   eval      lohel1_numm = w_benr
     c                   eval      lohel1_suff = w_besu
     c     lohel1_key    chain     lohelur
     c                   if        %found
     c                   eval      loornr = fdnumm
     c                   eval      loorsu = fdsuff
      *
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
      * Må oppdatere nye summer og totaler for bestilling
      *
      * Statuskode = 0  gir beregning av nye summer og totaler
      * Statuskode = 1  gir henting av nye priser/rabatter samt beregning
      *
     c                   eval      w_enor = ' '
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    lohel1_numm
     c                   parm                    lohel1_suff
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente høyeste linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ex_hnt_blin   begsr
      *
     c                   eval      lodtlr_numm = w_benr
     c                   eval      lodtlr_suff = w_besu
     c                   eval      lodtlr_line = 9999
     c     lodtlr_key    setll     lodtlr
     c                   readp     lodtlr
     c                   if        not %eof and
     c                             ldfirm = w_firm and
     c                             ldnumm = w_benr and
     c                             ldsuff = w_besu
     c                   eval      w_line = ldline
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = ldvare
     c                   eval      hllage = ldlage
     c                   eval      hlenhe = ldenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = footyp
     c                   eval      hlltyp = ldltyp
      *
     c                   eval      hlanta = ldanta
     c                   eval      hlkopr = ldkpny
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'L'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   move      fdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
      *
      * Key for oppslag på status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bestilling loggfil
      *
     c     lloglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lloglr_numm
     c                   kfld                    lloglr_suff
      *
      * Key for oppslag på bestillingsfil
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      * Key for oppslag på bestillingsfil
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      * Key for oppslag på bestillingsfil
      *
     c     lodtlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlc_ornu
     c                   kfld                    lodtlc_orsu
     c                   kfld                    lodtlc_orli
      *
      * Key for oppslag på bestillingsfil
      *
     c     lodtlc_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlc_ornu
     c                   kfld                    lodtlc_orsu
      *
      * Key for oppdat av mva kode
      *
     c     vmval1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmval1_mkod
      *
      * Key for lese : Ordre-hode-register, for bestilling
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for lese : Ordre-hode-register, for bestilling
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for lese : Ordre-linje-register, for bestilling
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for lese : Ordretype register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : Status-koder Lager
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     lstsl1_key    chain     lstsl1r
      *
      * Hent opplysninger fra ordre-hode
      *
     c                   eval      fohel1_numm = w_numm
     c                   eval      fohel1_suff = w_suff
     c     fohel1_key    chain     fohel1r
      *
8.03   u_order = *off;
8.03   CallP CO402R(l_filg:w_firm:0:'BESTILLING_TIL_SALGSORDRE':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_order = *on;
8.03     w_type = %trim(co402_verdi2);
8.03   endif;
      *
     c                   endsr
