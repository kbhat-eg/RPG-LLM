     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR151R
      * Beskrivelse . . : Radioterminal trans, plukking
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140806 BOF  Nytt program
      *       060307 bø   Hvis enhet på ean - skal denne foreslås.
      *       041007 bø   Endring er vedr. å endre enhet.
      *       011107 bø   Hvis vare ikke finnes ved scanning - legg i JVSCAPF
5.60  *       010208 bø   Justert i subf.hvis plukket er mer enn ordre
5.61  *       220508 bø   Tester nobb-/eannr mot ordre for gjennfinning
5.62  *       110509 ask  Rettet problemer med innscanning
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  270909 bof  Fjerne mulighet for å registrere ean nr her
6.13  * 6.10  300312 bof  Når scan av 128-ean ta hensyn til antall
6.14  * 6.10  050612 bof  Gir feilmelding når vare ikke finnes på ordre
6.21  * 6.20  030712 bof  Hvis man avslutter med 2 og status = BU, rydd og avslutt
6.22  * 6.20  211013 bof  Tar clear før man legger danner trans, sikre kvalitet.
6.23  * 6.20  310315 bkv  Ikke P for kontantkunder
6.24  * 6.20  300915 bof  Feilretting vedr. enhp,
6.nu  *  6.30 060614 bof  Endringer ihht. nummerutvidelse
6.31  * 6.30  151018 bof  Utvidet med nytt trelast-sortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwpll2    if   e           k disk    rename(lwplpfr:lwpll2r)
     flwpll3    if   e           k disk    rename(lwplpfr:lwpll3r)
     flwpllr    if   e           k disk    rename(lwplpfr:lwpllrr)
     flwpllu    uf a e           k disk    rename(lwplpfr:lwpllur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
6.11 fvvenlu    uf   e           k disk    rename(vvenpfr:vvenlur)
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvenl4    if   e           k disk    rename(vvenpfr:vvenl4r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.23 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     fHR151d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(mkfirm)
     d p_numm          s                   like(mknumm)
     d p_suff          s                   like(mksuff)
     d p_endr          s              1
     d p_stat          s              1
     d p_urec          s             80
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Datastruktur for navn på member
      *
     d                 ds
     d d_memb                        10
     d  d_alf1                        1    overlay(d_memb)
     d  d_firm                        3  0 overlay(d_memb:2)
     d  d_numm                        6  0 overlay(d_memb:5)
      *
      *
     d                 ds
     d d_urec                        80
     d  d_ufir                        3  0 overlay(d_urec)
     d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_unum                        8  0 overlay(d_urec:8)
6.nu d  d_usuf                        2  0 overlay(d_urec:16)
6.nu d  d_ukod                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
      *
      * Definering av variabler i nøkler
      *
     d lwpllr_numm     s                   like(mknumm)
     d lwpllr_suff     s                   like(mksuff)
     d lwpllr_line     s                   like(mkline)
     d lwpllu_numm     s                   like(mknumm)
     d lwpllu_suff     s                   like(mksuff)
     d lwpllu_line     s                   like(mkline)
     d lwpll2_numm     s                   like(mknumm)
     d lwpll2_suff     s                   like(mksuff)
     d lwpll2_stat     s                   like(mkstat)
     d lwpll2_loka     s                   like(mkloka)
     d lwpll2_vare     s                   like(mkvare)
     d lwpll3_numm     s                   like(mknumm)
     d lwpll3_suff     s                   like(mksuff)
     d lwpll3_vare     s                   like(mkvare)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
6.11 d vvenlu_vare     s                   like(vevare)
6.11 d vvenlu_enhe     s                   like(veenhe)
     d veanl1_eann     s                   like(vxeann)
     d veanlu_eann     s                   like(vxeann)
     d vvenl1_vare     s                   like(vevare)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenl4_vare     s                   like(vevare)
     d vvenl4_inen     s                   like(veinen)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d fodtlr_numm     s                   like(fdnumm)
     d fodtlr_suff     s                   like(fdsuff)
     d fodtlv_vare     s                   like(fdvare)
     d fodtlv_numm     s                   like(fdnumm)
     d fodtlv_suff     s                   like(fdsuff)
     d votyl1_otyp     s                   like(vaotyp)
6.23 d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_scan          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_plukket       s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_till          s              1    inz(*off)
     d b_nean          s              1    inz(*off)
     d b_eaok          s              1    inz(*off)
     d b_pluk          s              1    inz(*off)
     d b_rest          s              1    inz(*off)
     d b_ordok         s              1    inz(*off)
     d b_avsl          s              1    inz(*off)
     d b_endr          s              1    inz(*off)
6.13 d b_128           s              1    inz(*off)
6.14 d b_vafu          s              1    inz(*off)
     d w_knav          s             30
      *
     d w_udat          s               d   datfmt(*iso)
     d w_scan          s             26
5.61 d w_sctx          s             30
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_vars          s             15
6.11 d w_eans          s             14
     d w_kod2          s              1
     d w_rkod          s              1
     d w_lety          s              1  0
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_stek          s             35
     d w_dirk          s              1  0
     d w_stat          s              1
     d w_outq          s             10
     d x               s              1  0
     d w_nnum          s              6  0
     d w_alf1          s              1
      *
     d w_firm          s                   like(mkfirm)
     d w_anta          s                   like(mkanta)
6.13 d w_ants          s                   like(mkanta)
     d w_antp          s                   like(mkanta)
     d w_omra          s                   like(mkanta)
     d w_omro          s                   like(veomre)
     d w_omrp          s                   like(veomre)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_eann          s                   like(mkeann)
     d w_dato          s                   like(vvedat)
     d w_time          s                   like(vvetim)
     d w_anta_kalk     s                   like(fdanta)
     d w_vare          s                   like(vvvare)
     d w_line          s                   like(fdline)
6.11 d w_xenhe         s                   like(vvenhe)
     d w_venhe         s                   like(mkenhp)
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.31 d w_lv_vare       s                   like(vvlvar)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(2)
     d c_digi          c                   '0123456789 '
     d c_null          c                   '0000000000000'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for registrering
      * D1BLD  - D1 Skjermbilde for slette (Delete)
      * E1BLD  - E1 Skjermbilde for avslutting av varemottak
      * F1BLD  - F1 Skjermbilde for melding om vare ikke funnet
      * G1BLD  - F1 Skjermbilde for melding om vare ikke er på ordre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   endsl
      *
      * F4=Spørring på vare
      *
     c                   if        *inkd
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    b2sca1
     c                   parm                    w_stek
     c                   eval      *in22 = *on
     c                   if        b2sca1 = *blank
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av scanning
      *
     c                   if        b2sca1 <> *blank or
     c                             b2sca2 <> *blank
     c                   exsr      scanning
     c                   eval      *in22 = *on
     c                   if        b_feil = *on or
     c                             b_plukket = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      * Behandler valg i avslutningsbilde
      *
     c                   eval      b_avsl = *off
     c                   exsr      xe1bld
     c                   if        b_avsl = *off
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
     c
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
6.11  * 4) num + lengde <= 14:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
      * 6) num + lengde <= 8:  mot ordre
      *      - Varenr
      * 7) num + lengde <= 13: mot ordre
      *      - EAN-nummer
      *
     c                   eval      b_feil = *off
     c                   eval      b_plukket= *off
     c                   eval      b_eaok = *off
     c                   eval      b_nean = *off
     c                   eval      b_scan = *on
     c                   eval      w_eann = 0
      *
     c                   eval      c1enhe = *blank
     c                   eval      c1enhp = *blank
6.24 c                   eval      w_xenhe = *blank
     c                   eval      w_anta = 0
6.13 c                   eval      w_ants = 0
      *
     c                   eval      w_scan = %trim(b2sca1) + %trim(b2sca2)
      *
     c                   eval      w_leng = %len(%trim(w_scan))
      *
      * Sjekker om scannefelt inneholder alfa-verdi
      *
     c     c_digi        check     w_scan        w_posi
      *
      * Behandling dersom alfa
      *
6.14 c                   eval      b_vafu = *off
6.14  *
     c                   if        w_posi > 0
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :w_scan
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   movel     w_lv_vare     c1vare
6.31 c                   eval      b_vafu = *on
6.31 c                   goto      sjekk_ordre
6.31 c                   endif
      *
     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
6.14 c                   eval      b_vafu = *on
5.62 c                   goto      sjekk_ordre
     c                   endif
      *
5.61 c*                  call      'LL871C '
     c*                  parm                    w_scan
5.61 c*                  parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      w_xenhe = *blank
     c                   eval      d_e128 = w_scan
     c                   eval      w_eann = d_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
6.13 c                   eval      w_ants = d_anta
6.14 c                   eval      b_vafu = *on
5.62 c                   goto      sjekk_ordre
     c                   endif
     c                   endif
      *
5.61 c*                  call      'LL871C '
     c*                  parm                    w_scan
5.61 c*                  parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vars = %subst(c_null:1:8-w_leng) +
     c                                      w_scan
      *
     c                   eval      vvarl1_vare = w_vars
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
6.14 c                   eval      b_vafu = *on
5.62 c                   goto      sjekk_ordre
     c                   endif
      *
     c                   endif
      *
6.11  * Behandling dersom lengde <= 14
      *
6.11 c                   if        w_leng <= 14
      *
6.11 c                   eval      w_eans = %subst(c_null:1:14-w_leng) +
     c                                      w_scan
      *
     c                   eval      w_xenhe = *blank
     c                   movel     w_eans        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.24 c                   eval      w_xenhe     = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
6.14 c                   eval      b_vafu = *on
5.62 c                   goto      sjekk_ordre
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :w_scan
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   movel     w_lv_vare     c1vare
6.31 c                   eval      b_vafu = *on
6.31 c                   goto      sjekk_ordre
6.31 c                   endif

     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   endif
      *
      * sjekke om ean-nr eller varenr finnes på ordren
      *
     c     sjekk_ordre   tag
      *
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key    reade     fodtlr
     c                   dow       not %eof
5.62 c*                  if        w_leng <= 8
5.62 c*                  eval      w_vars = %subst(c_null:1:8-w_leng) +
5.62 c*                                     w_scan
5.62 c                   if        fdvare = c1vare
5.62 c*                  movel     fdvare        c1vare
     c                   eval      c1line = fdline
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c*                  endif
      *
5.62 c*                  if        w_leng <= 13
5.62 c*                  eval      w_eans = %subst(c_null:1:13-w_leng) +
5.62 c*                                     w_scan
5.62 c*                  movel     w_eans        w_eann
5.62 c*                  if        fdeann = w_eann
5.62 c*                  movel     fdvare        c1vare
5.62 c*                  eval      c1line = fdline
5.62 c*                  exsr      xc2bld
5.62 c*                  goto      end_scan
5.62 c*                  endif
5.62 c*                  endif
      *
     c     fodtlr_key    reade     fodtlrr
     c                   enddo
      *
      * Vare ikke identifisert - nytt ean nr?
      *
6.14 c                   if        b_vafu = *on
6.14 c                   exsr      xg1bld
6.14 c                   goto      end_scan
6.14 c                   endif
6.14  *
     c                   exsr      sjekk_ean
      *
     c                   if        b_eaok = *on
     c                   exsr      xf1bld
     c                   else
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
     c                   endif
      *
     c                   if        b_nean = *on
     c                   exsr      xc2bld
     c                   endif
      *
      * Setter feil-indikator dersom vare ikke ble funnet
      *
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c     end_scan      tag
      *
     c                   eval      b_scan = *off
      *
     c                   if        b_feil = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
      *
     c     lwpll2_key    setll     lwpll2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
     c                   eval      *in22 = *off
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   eval      c1enhp = b1enhp
     c                   eval      c1line = b1line
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
     c                   eval      d1enhe = b1enhe
     c                   exsr      xd1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
      * Henter vareinformasjon
      *
      *
     c                   eval      fodtl1_line = c1line
     c     fodtl1_key    chain     fodtl1
     c                   if        %found(fodtl1)
     c                   eval      b1tek1  = %subst(fdtek1:1:17)
     c                   eval      b1tek2  = %subst(fdtek1:18:17)
     c                   else
     c                   goto      end_c2bld
     c                   endif
      *
      * Sjekker om vare er på ordre
      *
     c                   eval      b_feil = *off
     c                   if        b1valg = 2
      * når valg 2 vet vi at den finnes
     c                   eval      lwpllr_line = c1line
     c     lwpllr_key    chain     lwpllr
     c                   if        not %found(lwpllr)
     c                   eval      b_feil = *on
     c                   endif
     c                   else
      * når man bare har tastet et varenr i b2vare så må det letes
     c                   eval      b_feil = *off
     c                   eval      w_vare = fdvare
     c                   exsr      finn_linje
     c                   eval      c1line = w_line
     c                   endif
      *
     c                   if        b_plukket = *on
     c                   exsr      xg2bld
     c                   if        b_till = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   eval      b_plukket = *off
     c                   goto      end_c2bld
     c                   endif
     c                   endif
      *
     c                   if        b_feil = *on
     c                   exsr      xg1bld
     c                   if        b_till = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   goto      end_c2bld
     c                   endif
     c                   eval      b_feil = *off
     c                   endif
      *
     c                   eval      fdtek1 = %trim(fdtek1)
     c                   eval      c2tek1 = %subst(fdtek1:1:19)
     c                   eval      c2tek2 = %subst(fdtek1:20:16)
      *
      * Henter tre salgsenheter - hvis enhet ikke er oppgitt
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      c2enh1 = *blank
     c                   eval      c2enh2 = *blank
     c                   eval      c2enh3 = *blank
      *
     c                   eval      vvenl2_vare = fdvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = veenhe
     c                   when      x = 2
     c                   eval      c2enh2 = veenhe
     c                   when      x = 3
     c                   eval      c2enh3 = veenhe
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   if        w_xenhe <> *blank
     c                   eval      c1enhp  = w_xenhe
     c                   select
     c                   when      w_xenhe = c2enh1
     c                   eval      *in80  = *on
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh2
     c                   eval      *in80  = *off
     c                   eval      *in81  = *on
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh3
     c                   eval      *in80  = *off
     c                   eval      *in81  = *off
     c                   eval      *in82  = *on
     c                   endsl
     c                   else
     c                   if        c1enhp <> *blank
     c                   select
     c                   when      c1enhp  = c2enh1
     c                   eval      *in80  = *on
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   when      c1enhp  = c2enh2
     c                   eval      *in80  = *off
     c                   eval      *in81  = *on
     c                   eval      *in82  = *off
     c                   when      c1enhp  = c2enh3
     c                   eval      *in80  = *off
     c                   eval      *in81  = *off
     c                   eval      *in82  = *on
     c                   endsl
     c                   else
     c                   select
     c                   when      c1enhe  = c2enh1
     c                   eval      *in80  = *on
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   when      c1enhe  = c2enh2
     c                   eval      *in80  = *off
     c                   eval      *in81  = *on
     c                   eval      *in82  = *off
     c                   when      c1enhe  = c2enh3
     c                   eval      *in80  = *off
     c                   eval      *in81  = *off
     c                   eval      *in82  = *on
     c                   endsl
     c                   endif
     c                   endif
      *
      * Antall
      *
6.13 c                   eval      c2anta = mkanta - mkantp
      *
      * Plukking
      *
     c                   if        b_scan = *off
      *
     c                   eval      lwpllr_line = c1line
     c     lwpllr_key    chain     lwpllr
     c                   if        %found
     c                   eval(h)   c2anto = mkanta
     c                   eval(h)   c2antp = mkantp
      *
     c                   eval      *in80 = *off
      *
     c                   if        mkenhp <> *blank
     c                   select
     c                   when      mkenhp = c2enh1
     c                   eval      *in80 = *on
     c                   when      mkenhp = c2enh2
     c                   eval      *in81 = *on
     c                   when      mkenhp = c2enh3
     c                   eval      *in82 = *on
     c                   endsl
     c                   else
     c                   select
     c                   when      mkenhe = c2enh1
     c                   eval      *in80 = *on
     c                   when      mkenhe = c2enh2
     c                   eval      *in81 = *on
     c                   when      mkenhe = c2enh3
     c                   eval      *in82 = *on
     c                   endsl
     c                   endif
      *
     c                   eval      c2anta = mkanta - mkantp
      *
     c                   endif
     c                   endif
      *
      * Viser bildet
      *
     c     c2tagb        tag
     c                   if        c1enhp <> *blank and
     c                             c1enhp <> c1enhe
     c                   exsr      omr_til_bild
     c                   else
     c                   eval      c2anto = mkanta
     c                   eval      c2antp = mkantp
6.13 c                   if        w_ants > 0
6.13 c                   eval      c2anta = w_ants
6.13 c                   else
     c                   eval      c2anta = c2anto - c2antp
6.13 c                   endif
     c                   endif
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc
     c                   goto      end_c2bld
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   select
     c                   when      *in80 = *on and
     c                             c2enh2 <> *blank
     c                   eval      c1enhp = c2enh2
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        c2enh3 <> *blank
     c                   eval      *in82 = *on
     c                   eval      c1enhp = c2enh3
     c                   else
     c                   eval      c1enhp = c2enh1
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   eval      c1enhp = c2enh1
     c                   endsl
     c                   goto      c2tagb
     c                   endif
      *
      * F2=Kalkulator
      *
     c*                  if        *inkb
     c*                  call      'HR200R'
     c*                  parm                    w_firm
     c*                  parm                    vvvare
     c*                  parm                    w_anta_kalk
     c*                  goto      c2tagb
     c*                  endif
      *
      * F5=Forny
      *
     c                   if        *inke
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      c2anta = 0
     c                   goto      c2tagb
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2anta <= 0
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   lwpllur
      *
     c                   if        c1enhp <> *blank and
     c                             c1enhp <> c1enhe
     c                   exsr      omr_til_ordre
     c                   eval      c2anta = w_antp
     c                   endif
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      c1enhe = c2enh1
     c                   when      *in81 = *on
     c                   eval      c1enhe = c2enh2
     c                   when      *in82 = *on
     c                   eval      c1enhe = c2enh3
     c                   endsl
      *
     c                   eval      lwpllu_line = c1line
     c     lwpllu_key    chain     lwpllur
      *
     c                   eval      mkvare = c1vare
     c                   eval      mkeann = w_eann
     c                   eval      mkenhp = c1enhp
      *
     c                   if        %found and
     c                             b_scan = *on
     c                   eval      mkantp = mkantp + c2anta
     c                   else
     c                   eval      mkantp = c2anta
     c                   endif
     c                   if        mkantp >= mkanta
     c                   eval      mkstat  = 'P'
     c                   endif
      *
     c                   time                    mkdato
     c                   time                    mktime
      *
     c                   if        %found
     c                   update    lwpllur
     c                   else
     c                   eval      mkfirm = w_firm
     c                   write     lwpllur
     c                   endif
      *
     c                   eval      mkstat = ' '
     c                   eval      b_endr = *on
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      w_anta = mkanta - mkantp
5.60 c                   if        w_anta < 0
5.60 c                   eval      b1anta = 0
5.60 c                   else
     c     w_anta        mult      1             b1anta
5.60 c                   endif
     c                   eval      b1enhp = mkenhp
     c                   endif
      *
     c     end_c2bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (d1bld)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1bld        begsr
      *
     c                   eval      lwpllr_line = d1line
     c     lwpllr_key    chain     lwpllr
      *
     c                   eval      d1anta = mkanta
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
      *
     c                   eval      vvarl1_vare = *blank
     c                   movel     mkvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      d1tek1 = %subst(vvtek1:1:19)
     c                   eval      d1tek2 = %subst(vvtek1:20:16)
     c                   endif
      *
     c                   exfmt     d1bld
      *
     c                   if        *inkc
     c                   goto      end_d1bld
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      lwpllu_line = d1line
     c     lwpllu_key    chain     lwpllur
      *
     c                   delete    lwpllur
      *
     c     end_d1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare ikke funnet i vareregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1bld        begsr
      *
     c                   exfmt     f1bld
      *
     c                   if        *inkc
     c                   goto      end_f1bld
     c                   endif
      *
     c                   if        *inkd
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_stek
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
6.12 c**                 exsr      reg_ean
     c                   eval      b_nean = *on
     c                   endif
     c                   endif
      *
      *
     c     end_f1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare ikke funnet på bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xg1bld        begsr
      *
     c                   eval      b_till = *off
     c                   exfmt     g1bld
      *
     c                   if        *inkc
     c                   goto      end_g1bld
     c                   endif
      *
     c*                  exsr      ny_linje
     c*                  eval      b_till = *on
      *
      *
     c     end_g1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare ikke funnet på bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xg2bld        begsr
      *
     c                   eval      b_till = *off
     c                   exfmt     g2bld
      *
     c                   if        *inkc
     c                   goto      end_g2bld
     c                   endif
      *
     c     end_g2bld     endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å vise melding
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     xm2msg        begsr
6.21  *
6.21 c                   exfmt     m2msg
6.21  *
6.21 c                   if        *inkc
6.21 c                   goto      end_m2msg
6.21 c                   endif
6.21  *
6.21 c     end_m2msg     endsr
6.23  *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for å vise melding
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     xm3msg        begsr
6.23  *
6.23 c                   exfmt     m3msg
6.23  *
6.23 c                   if        *inkc
6.23 c                   goto      end_m3msg
6.23 c                   endif
6.23  *
6.23 c     end_m3msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     lwpll2_key3   reade     lwpll2
      *
     c                   if        %eof
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1tek1 = *blank
     c                   eval      b1enhe = mkenhe
     c                   eval      b1anta = 0
     c                   eval      b1vare = mkvare
     c                   eval      b1line = mkline
     c                   eval      b1enhp = mkenhp
      *
     c                   eval      w_anta = mkanta - mkantp
5.60 c                   if        w_anta < 0
5.60 c                   eval      b1anta = 0
5.60 c                   else
     c     w_anta        mult      1             b1anta
5.60 c                   endif
      *
     c                   eval      fodtl1_line = mkline
     c     fodtl1_key    chain     fodtl1
     c                   if        %found(fodtl1)
     c                   eval      b1tek1  = %subst(fdtek1:1:17)
     c                   eval      b1tek2  = %subst(fdtek1:18:17)
     c                   else
     c                   eval      b1tek1  = *blank
     c                   eval      b1tek2  = *blank
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å avslutte varemottak
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xe1bld        begsr
      *
      *
     c                   call      'RS750R '
     c                   parm                    w_stat
     c                   parm                    fokund
     c                   parm                    w_knav
     c                   eval      e1knav = w_knav
     c                   eval      e1numm = p_numm
      *
      *
     c     e1taga        tag
      *
     c                   eval      e1valg = 0
     c                   exfmt     e1bld
      *
     c                   if        *inkc
     c                   goto      end_mottak
     c                   endif
      *
     c                   select
     c                   when      e1valg = 0
     c                   if        b_endr = *on
     c                   exsr      paabegynt
     c                   endif
     c                   eval      b_avsl = *on
     c                   when      e1valg = 1
     c                   exsr      rest_beh
     c                   when      e1valg = 2
6.21 c     fohel1_key    chain     fohel1
6.21 c                   if        fokode = 7
6.21 c                   exsr      rydd
6.21 c                   exsr      xm2msg
6.21 c                   leavesr
6.21 c                   endif
6.23 c                   eval      rkunl1_kund = fokund
6.23 c     rkunl1_key    chain     rkunl1r
6.23 c                   if        rkkres = 2
6.23 c                   exsr      rydd
6.23 c                   exsr      xm3msg
6.23 c                   leavesr
6.23 c                   else
     c                   exsr      oppdater
6.23 c                   endif
     c                   other
     c                   goto      end_mottak
     c                   endsl
      *
     c     end_mottak    endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne varelinje på varenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_linje    begsr
      *
     c                   eval      w_line = 0
     c                   eval      b_feil = *on
      *
     c                   eval      lwpll3_vare = w_vare
      *
     c     lwpll3_key    setll     lwpll3
     c     lwpll3_key    reade     lwpll3
     c                   dow       not %eof(lwpll3)
      *
5.60 c                   if        mkantp <  mkanta
     c                   eval      w_line = mkline
     c                   eval      c2anto = mkanta
     c                   eval      c2antp = mkantp
     c                   eval      c1enhp = mkenhp
     c                   eval      c1enhe = mkenhe
     c                   eval      b_feil = *off
     c                   eval      b_plukket = *off
     c                   goto      end_linje
     c                   else
     c                   eval      b_plukket = *on
     c                   endif
      *
     c     lwpll3_key    reade     lwpll3
     c                   enddo
      *
     c     end_linje     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om gyldig ean-nr er angitt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ean     begsr
      *
     c                   eval      w_rkod = '1'
     c                   call      'FÅ900R'
     c                   parm                    w_eann
     c                   parm                    w_rkod
      *
     c                   if        w_rkod = ' '
     c                   eval      b_eaok = *on
     c                   else
     c                   eval      w_eann = 0
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av ny ordrelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    ny_linje      begsr
      *
      * Finn siste linje på ordre
      *
     c*                  eval      lwpllr_line = 9999
     c*    lwpllr_key    setll     lwpllr
     c*                  readp     lwpllr
     c*                  if        %eof
     c*                  goto      end_ny_linje
     c*                  endif
      *
      * Tilordner verdier til ny bestillingslinje
      *
     c*                  eval      lwpllu_line = mkline + 10
     c*    lwpllu_key    chain     lwpllu
     c*                  if        %found
     c*                  goto      end_ny_linje
     c*                  endif
      *
     c*                  clear                   lwpllur
     c*                  eval      mkline = mkline + 10
     c*                  eval      mkvare = vvvare
     c*                  eval      mkeann = w_eann
     c*                  eval      mkenhe = *blank
      *
     c*                  eval      mkanta = 0
      *
     c*                  write     lwpllur
      *
     c*    end_ny_linje  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning til bildets enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_til_bild  begsr
      *
      * Finner omregningsfaktor volum for ordr.enhet og for registrert enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   movel     c1vare        vvenl1_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   if        c1enhe = veenhe
     c                   eval      w_omro = veomre
     c                   endif
     c                   if        c1enhp = veenhe
     c                   eval      w_omrp = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omrp = 0
     c                   eval      w_omrp = 1
     c                   endif
      *
     c                   if        w_omrp <> 0
     c                   eval(h)   c2anto = mkanta * w_omro / w_omrp
     c                   eval(h)   c2antp = mkantp * w_omro / w_omrp
     c                   endif
     c                   eval      c2anta = c2anto - c2antp
      *
     c     end_omr_b     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning til ordrens enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_til_ordre begsr
      *
      * Finner omregningsfaktor volum for fra enhet i bildet til ordrens
      * opprinnelige enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   movel     c1vare        vvenl1_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   if        c1enhe = veenhe
     c                   eval      w_omro = veomre
     c                   endif
     c                   if        c1enhp = veenhe
     c                   eval      w_omrp = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omro = 0
     c                   eval      w_omro = 1
     c                   endif
      *
     c                   eval      w_antp = c2anta
     c                   if        w_omro <> 0
     c                   eval      w_antp = c2anta * w_omrp / w_omro
     c                   endif
      *
     c     end_omr_o     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese varer fra ordre og legge i lwplpf
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
      * Leser varelinjer fra ordre
      *
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key    reade     fodtlr
     c                   dow       not %eof
      *
      * finner neste ordretype, som har behandlingstype = 2
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r                            90
      *
     c                   if        vaoakk <> 2
     c     nxtotyp       tag
     c                   eval      votyl1_otyp = vaonxt
     c     votyl1_key    chain     votyl1                             90
     c                   if        %found(votyl1) and
     c                             vaoakk <> 2
     c                   goto      nxtotyp
     c                   endif
     c                   endif
6.24 c                   clear     *all          lwpllur
      *
     c                   eval      mkotyp = vaotyp
      *
     c                   eval      mkfirm = w_firm
     c                   eval      mknumm = p_numm
     c                   eval      mksuff = p_suff
     c                   eval      mkvare = fdvare
     c                   eval      mkanta = fdanta
     c                   eval      mkrkod = 0
     c                   eval      mkstat = *blank
     c                   eval      mkline = fdline
     c                   eval      mkvare = *blank
     c                   movel     fdvare        mkvare
     c                   eval      mkenhe = fdenh1
     c                   write     lwpllur
      *
     c     fodtlr_key    reade     fodtlrr
     c                   enddo
      *
      * Viser avslutingsbildet
      *
     c     end_opprett   tag
      *
     c                   eval      b_oppr = *on
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere varene på ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Nå er varene plukket og det skal skrives pakkseddel - finner neste
      * ordretype og oppdaterer plukkfilen med dette
     c*
      *
     c*    lwpll2_key3   setll     lwpll2
     c*    lwpll2_key3   reade     lwpll2
5.45 c*                  dow       not %eof(lwpll2)
      *
     c*                  eval      lwpllu_line = mkline
     c*    lwpllu_key    chain     lwpllur
     c*                  if        %found(lwpllu)
      *
      * finner neste ordretype  utgår - riktig er funnet tidligere
      *
     c*                  eval      votyl1_otyp = mkotyp
     c*    votyl1_key    chain     votyl1r                            90
     c*                  eval      mkotyp = vaonxt
     c*                  update    lwpllur
     c*                  endif
     c*
5.45 c*    lwpll2_key3   reade     lwpll2
     c*                  enddo
      *
      * Henter siste nummer for File-member
      *
     c                   call      'FA920R'
     c                   parm                    w_alf1
     c                   parm                    w_nnum
      *
     c                   move      'A'           d_alf1
     c                   move      w_nnum        d_numm
3.36 c                   eval      d_firm      = w_firm
      *
      *
     c                   call      'FO619C  '
     c                   parm                    w_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    d_memb
     c                   eval      b_avsl = *on
      *
     c     end_oppdat    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette status på ordrehode til Oppfølging plukking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rest_beh      begsr
      *
      * restbehandling oppdateres på ordre
      *
     c     fohelu_key    chain     fohelu
     c                   if        %found(fohelu)
     c                   eval      forest = 1
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
     c                   eval      d_ufir = fofirm
     c                   eval      d_unum = fonumm
     c                   eval      d_usuf = fosuff
     c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      d_ukod = '7'
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
     c                   eval      b_avsl = *on
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette status på ordrehode til Oppfølging plukking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     paabegynt     begsr
      *
     c                   eval      d_ufir = fofirm
     c                   eval      d_unum = fonumm
     c                   eval      d_usuf = fosuff
     c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      d_ukod = '4'
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
     c                   eval      b_avsl = *on
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     sjekk_oppr    begsr
      *
     c     lwpll2_key3   setll     lwpll2
     c     lwpll2_key3   reade     lwpll2
     c                   if        %eof(lwpll2)
     c                   eval      b_oppr = *off
     c                   else
     c                   eval      b_oppr = *on
     c                   endif
      *
     c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å rydde unna transer
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21 c     rydd          begsr
6.21  *
6.21 c     lwpllu_key2   setll     lwpllu
6.21 c     lwpllu_key2   reade     lwpllu
6.21 c                   dow       not %eof(lwpllu)
6.21 c                   delete    lwpllur
6.21 c     lwpllu_key2   reade     lwpllu
6.21 c                   enddo
6.21  *
6.21 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_endr
      *
      * Key for les på radioterminal-trans
      *
     c     lwpll2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll2_numm
     c                   kfld                    lwpll2_suff
     c                   kfld                    lwpll2_stat
     c                   kfld                    lwpll2_loka
      *
      * Key for les på radioterminal-trans
      *
     c     lwpll2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll2_numm
     c                   kfld                    lwpll2_suff
     c                   kfld                    lwpll2_stat
     c                   kfld                    lwpll2_loka
     c                   kfld                    lwpll2_vare
      *
      * Key for les på radioterminal-trans - les subfile
      *
     c     lwpll2_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll2_numm
     c                   kfld                    lwpll2_suff
      *
      * Key for leser alle med stat = 'P'
      *
     c     lwpll2_key4   klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll2_numm
     c                   kfld                    lwpll2_suff
     c                   kfld                    lwpll2_stat
      *
      * Key for les på radioterminal-trans - finn en vare
      *
     c     lwpll3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll3_numm
     c                   kfld                    lwpll3_suff
     c                   kfld                    lwpll3_vare
      *
      * Key for oppslag på radioterminal-trans
      *
     c     lwpllr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpllr_numm
     c                   kfld                    lwpllr_suff
     c                   kfld                    lwpllr_line
      *
      * Key for oppdatering av radioterminal-trans
      *
     c     lwpllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpllu_numm
     c                   kfld                    lwpllu_suff
     c                   kfld                    lwpllu_line
6.21  *
6.21  * Key for rydding av radioterminal-transer
6.21  *
6.21 c     lwpllu_key2   klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    lwpllu_numm
6.21 c                   kfld                    lwpllu_suff
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
6.11  *
6.11  * Key for oppdatering av EAN-register på vare-enhet
6.11  *
6.11 c     vvenlu_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    vvenlu_vare
6.11 c                   kfld                    vvenlu_enhe
      *
      * Key for les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
     c     vvenl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
     c                   kfld                    vvenl4_inen
      *
     c     vvenl4_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
      *
      * Key for oppdatering av ordre
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for les av ordre
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppdatering av bestillingslinje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      * Key for les av bestillingslinje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for les av bestillingslinje
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
      *
      * Key for posisjonering av bestillingslinje på varenummer
      *
     c     fodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlv_vare
     c                   kfld                    fodtlv_numm
     c                   kfld                    fodtlv_suff
6.23  *
6.23  * Key for file : KUNDE-REGISTER
6.23  *
6.23 c     rkunl1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Henter firma og timestamp fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn bestillingsnummer i nøkler
      *
     c                   eval      fohelu_numm = p_numm
     c                   eval      fohelu_suff = p_suff
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c                   eval      fodtlu_numm = p_numm
     c                   eval      fodtlu_suff = p_suff
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtlr_numm = p_numm
     c                   eval      fodtlr_suff = p_suff
     c                   eval      lwpllu_numm = p_numm
     c                   eval      lwpllu_suff = p_suff
     c                   eval      fodtlv_numm = p_numm
     c                   eval      fodtlv_suff = p_suff
     c                   eval      lwpll2_numm = p_numm
     c                   eval      lwpll2_suff = p_suff
     c                   eval      lwpll3_numm = p_numm
     c                   eval      lwpll3_suff = p_suff
     c                   eval      lwpllr_numm = p_numm
     c                   eval      lwpllr_suff = p_suff
     c                   eval      lwpllu_numm = p_numm
     c                   eval      lwpllu_suff = p_suff
     c                   eval      b_endr = *off
      *
      * Leser ordrehode
      *
     c     fohel1_key    chain     fohel1
      *
      * hvis det er første gang den plukkes hentes detaljer fra ordre
      *
     c                   exsr      sjekk_oppr
      *
     c                   if        b_oppr = *off
     c                   exsr      opprett
     c                   endif
      *
5.61 c                   eval      w_sctx = 'HR151R Plukking'
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22  = *on
      *
     c     lwpll2_key3   setll     lwpll2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
