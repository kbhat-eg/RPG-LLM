      /TITLE  Utskrift av varebevegelse - parameter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LL620R
      * Beskrivelse . . : Utskrift av varebevegelse - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           010800  Nytt program                             KOB
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KC = F3  = Exit
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fra26l1    if   e           k disk    rename(ra26pfr:ra26l1r)
      *
     fll620d    cf   e             workstn
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *  Parameter-rec
      *
     d                 ds
     d d_prec                       128
     d  d_fvar                        8  0 overlay(d_prec)
     d  d_tvar                        8  0 overlay(d_prec:9)
     d  d_flag                        2  0 overlay(d_prec:17)
     d  d_tlag                        2  0 overlay(d_prec:19)
     d  d_fogr                        2  0 overlay(d_prec:21)
     d  d_togr                        2  0 overlay(d_prec:23)
     d  d_fhgr                        2  0 overlay(d_prec:25)
     d  d_thgr                        2  0 overlay(d_prec:27)
     d  d_fugr                        3  0 overlay(d_prec:29)
     d  d_tugr                        3  0 overlay(d_prec:32)
     d  d_levn                        6  0 overlay(d_prec:35)
     d  d_sakb                        4    overlay(d_prec:41)
     d  d_vari                        1  0 overlay(d_prec:45)
     d  d_anta                        5  0 overlay(d_prec:46)
     d  d_sort                        1  0 overlay(d_prec:51)
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d ra26l1_sakb     s                   like(razsak)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_in03          s              1
     d w_latx          s             20
     d w_letx          s             30
     d w_vare          s             15
     d w_vtxt          s             35
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_satx          s             30
     d w_parm          s            128
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle bilde for valg av utskriftskriteria
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1taga        tag
      *
     c                   exfmt     a1bld
      *
      * CMD3 = Avslutt
      *
     c                   if        *inkc = *on
     c                   eval      w_in03 = '1'
     c                   goto      end_pgm
     c                   endif
      *
      * CMD4 = Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   goto      a1taga
     c                   endif
      *
      * Kontroll av valg fra/til
      *
     c                   if        a1fvar > a1tvar
     c                   eval      *in30 = *on
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1flag > a1tlag
     c                   eval      *in31 = *on
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1fogr > a1togr
     c                   eval      *in32 = *on
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1fhgr > a1thgr
     c                   eval      *in33 = *on
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1fugr > a1tugr
     c                   eval      *in34 = *on
     c                   goto      a1taga
     c                   endif
      *
      *  Sjekk, Finnes leverandør?
      *
     c                   if        a1lvnr > 0
     c                   eval      rlevl1_levr = a1lvnr
     c     rlevl1_key    chain     rlevl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in35 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Finnes saksbehandler/innkjøper ?
      *
     c                   if        a1sakb <> *blank
     c                   eval      ra26l1_sakb = a1sakb
     c     ra26l1_key    chain     ra26l1r                            90
     c                   if        *in90 = *on
     c                   eval      *in38 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Er variant ok?
      *
     c                   if        a1vari  <> 1 and
     c                             a1vari  <> 2
     c                   eval      *in36 = *on
     c                   goto      a1taga
     c                   endif
      *
      *  Sjekk, Er sortering ok?
      *
     c                   if        a1sort  <> 0 and
     c                             a1vari  <> 1
     c                   eval      *in37 = *on
     c                   goto      a1taga
     c                   endif
      *
      * Skriv parameter til Local
      *
     c                   eval      d_fvar = a1fvar
     c                   eval      d_tvar = a1tvar
     c                   eval      d_flag = a1flag
     c                   eval      d_tlag = a1tlag
     c                   eval      d_fogr = a1fogr
     c                   eval      d_togr = a1togr
     c                   eval      d_fhgr = a1fhgr
     c                   eval      d_thgr = a1thgr
     c                   eval      d_fugr = a1fugr
     c                   eval      d_tugr = a1tugr
     c                   eval      d_levn = a1lvnr
     c                   eval      d_sakb = a1sakb
     c                   eval      d_vari = a1vari
     c                   eval      d_anta = a1anta
     c                   eval      d_sort = a1sort
     c                   eval      w_parm = d_prec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på input A1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring i vareregister
      *
     c                   if        wactfe = 'A1FVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        a1fvar
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1TVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        a1tvar
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i overgrupperegister
      *
     c                   if        wactfe = 'A1FOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    a1fogr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1TOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    a1togr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i hovedgrupperegister
      *
     c                   if        wactfe = 'A1FHGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    a1fogr
 001 C                   parm                    a1fhgr
 001 C                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1THGR'
     c                   if        a1togr = 99
     c                   eval      a1togr = 0
     c                   eval      a1thgr = 0
     c                   endif
 001 c                   call      'VG511R'
 001 c                   parm                    w_firm
 001 c                   parm                    a1togr
 001 c                   parm                    a1thgr
 001 c                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring i undergrupperegister
      *
     c                   if        wactfe = 'A1FUGR'
 001 c                   call      'VG512R'
 001 c                   parm                    w_firm
 001 c                   parm                    a1fogr
 001 c                   parm                    a1fhgr
 001 c                   parm                    a1fugr
 001 c                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1TUGR'
     c                   if        a1togr = 99
     c                   eval      a1togr = 0
     c                   eval      a1thgr = 0
     c                   eval      a1tugr = 0
     c                   endif
     c                   if        a1thgr = 99
     c                   eval      a1thgr = 0
     c                   eval      a1tugr = 0
     c                   endif
 001 c                   call      'VG512R'
 001 c                   parm                    w_firm
 001 c                   parm                    a1togr
 001 c                   parm                    a1thgr
 001 c                   parm                    a1tugr
 001 c                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring lager
      *
     c                   if        wactfe = 'A1FLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    a1flag
     c                   parm                    w_latx
     c                   goto      sp_end
     c                   endif
      *
     c                   if        wactfe = 'A1TLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    a1tlag
     c                   parm                    w_latx
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på leverandør
      *
     c                   if        wactfe = 'A1LVNR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    a1lvnr
     c                   parm                    w_letx
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring saksbehandler/innkjøper
      *
     c                   if        wactfe = 'A1SAKB'
     c                   call      'RA526R'
     c                   parm                    w_firm
     c                   parm                    a1sakb
     c                   parm                    w_satx
     c                   goto      sp_end
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *  Key for les av SAKSBEHANDLER
      *
     c     ra26l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra26l1_sakb
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_in03
     c                   parm                    w_parm
      *
      * Setter standardverdier for skjermbilde
      *
     c                   eval      a1fvar = 0
     c                   eval      a1tvar = 99999999
     c                   eval      a1flag = 0
     c                   eval      a1tlag = 99
     c                   eval      a1fogr = 0
     c                   eval      a1togr = 99
     c                   eval      a1fhgr = 0
     c                   eval      a1thgr = 99
     c                   eval      a1fugr = 0
     c                   eval      a1tugr = 999
     c                   eval      a1vari = 1
     c                   eval      w_firm = l_firm
      *
     c                   endsr
