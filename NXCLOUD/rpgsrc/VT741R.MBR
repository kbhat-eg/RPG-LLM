     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VT741R
      * Beskrivelse . . : Tilbudspriser, XML-oppdatering fra Solve
      *
      *                   NB! Oppdater lager dersom tvangs-opprettet vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121219 hko  Nytt program
7.01  *       150120 bof  Logging av inputkontroll
      *       230120 hko  Sletter kampanje med samme navn og varer
      *                   innefor samme kampanjeperiode
7.03  *       110320 bof  Endre på utlegg til logg ved skriv.  (midlertidig)
      *       120320 hko  Logistikksortiment: Dersom vare ikke finnes på
      *                   Nobbnr, så letes det etter vare på varenr
9.01  *       160320 hko  Midlertidig i påvente av fullversjon Solve fase 2:
9.01  *                   Lagt inn oppretting/oppdatering av kampanje i
9.01  *                   JKAHPF og JKAVPF.
      *
7.04  *       160320 bof  Legge ut ut logg i tabell, slip stream fxl2st
7.05  *       280320 bkv  Endret para asptoxlsx
7.06  *K00    280420 bof  Utvidet med filgruppe som kolonne i xls
7.07  *K00    170620 bof  Endret litt på logikk vedr. mailsending
7.08  *K00    260820 bof  Nullstilte mail-tabell + test om mail skal sendes.
7.09  *       041020 bof  Styrer om det skal gå blind-kopi på mail
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
      *
     fvtill5    uf   e           k disk    rename(vtilpfr:vtill5r)
     fvtillu    uf a e           k disk    rename(vtilpfr:vtillur)
9.01 fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
9.01 fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
9.01 fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
9.01 fjkahlu    uf a e           k disk    rename(jkahpfr:jkahlur)
9.01 fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
7.04 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
7.04 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
7.04 frax9l1    if   e           k disk    rename(rax9pfr:rax9l1r)
7.04 ffxl2st    uf a e           k disk    rename(fxl2st:fxl2str)
      *
      * Definering av variable i nøkler
      *
     d vtill5_kamp     s                   like(vtkamp)
     d vtillu_ogrp     s                   like(vtogrp)
     d vtillu_hgrp     s                   like(vthgrp)
     d vtillu_ugrp     s                   like(vtugrp)
     d vtillu_ldor     s                   like(vtldor)
     d vtillu_vare     s                   like(vtvare)
     d vtillu_prgr     s                   like(vtprgr)
     d vtillu_lety     s                   like(vtlety)
     d vtillu_fdat     s                   like(vtfdat)
     d vtillu_ftim     s                   like(vtftim)
     d vtillu_tdat     s                   like(vttdat)
     d vtillu_ttim     s                   like(vtttim)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
9.01 d jvarl3_nobb     s                   like(jvnobb)
9.01 d jvlel1_lvnr     s                   like(jvlvnr)
9.01 d jvlel1_lldo     s                   like(jvlldo)
9.01 d jvpkl2_vare     s                   like(jwvare)
9.01 d jkahlu_kamp     s                   like(jmkamp)
9.01 d jkavlu_vkam     s                   like(jmvkam)
7.04 d rlevl1_levr     s                   like(rllevr)
7.04 d ra09l1_ikod     s                   like(raikod)
      *
7.01  * Brukes som parametre til jobblogging
7.01 d p_jnav          s             15
7.01 d p_jbid          s             41
7.01 d p_jtxt          s             35
7.01 d p_jsts          s              2  0
7.01 d p_jmld          s            200
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
7.04 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_filNavn       s            255a
     d p_lengde        s              3p 0
     d p_sistDato      s             10a
     d p_stat          s              1
      *
      * Dataelementer kampanje hode
      *
7.04 d b_kamp          s              1    inz(*off)
7.08 d b_feil          s              1    inz(*off)

     d w_kamp          s              5
     d w_besk          s             35
     d w_ansv          s             35
8.01 d w_prgr          s              2
     d w_fdat          s               d   datfmt(*iso)
     d w_ftim          s               t   timfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)                         x
     d w_ttim          s               t   timfmt(*iso)
     d*w_ifda          s               d   datfmt(*iso)                         Innkjøp fra dato
     d*w_itda          s               d   datfmt(*iso)                         Innkjøp til dato
      *
      * Dataelementer kampanje linje
      *
     d w_nobb          s              8  0                                      Nobbnr
     d w_avar          s             15                                         Avs. varenr
     d w_avtx          s             35                                         Avs. varetekst
     d w_asts          s              5                                         Avs. status
     d w_enhe          s              3                                         Enhet
     d w_inpr          s             13  2                                      Innkj.pris
     d w_kopr          s             13  2                                      Kostpris
     d w_akpr          s             13  2                                      Anb. kamp. pris
     d w_pkpr          s             13  2                                      Pål. kamp. pris
     d w_kinf          s             70                                         Kjøpsinformasjon
     d w_vtxt          s             70                                         Varetekst
     d w_linf          s             70                                         Lev. informasjon
     d w_delt          s              6  0                                      Nobb deltakernr
     d w_vans          s             70                                         Vareansvarlig
     d w_loka          s             35                                         Lokasjon
     d w_tvre          s              5                                         TV-reklame
     d w_ifda          s               d   datfmt(*iso)                         Innkjøp fra dato
     d w_itda          s               d   datfmt(*iso)                         Innkjøp til dato
     d w_bipr          s              5                                         Forbedret innkj.pris
     d w_fkod          s              5                                         Feilkode
     d w_ftxt          s              5                                         Feiltekst
7.05 d w_exsql         s          10000
7.04 d w_exmap         s             30    inz('excel')
7.05 d w_exfil         s             50    inz('Kampanje')
7.04 d w_extyp         s              4    inz('XLS')
7.04 d w_feil          s             50
7.04 d w_lnav          s             35
7.04 d w_rec1          s             50
7.04 d w_rec2          s             50
7.04 d w_rec3          s             50
7.04 d w_subj          s            100
7.04 d w_txt1          s            100
7.04 d w_txt2          s            100
7.04 d w_txt3          s            100
7.04 d w_txt4          s            100
7.04 d w_avsl          s            100
7.04 d w_filn          s            256
7.04 d w_fnav          s             50
7.04 d w_mime          s            150
7.09 d w_bcc           s              1
      *
      * Definering av variable
      *
     d x_kamp          s             50
     d x_besk          s             50
     d x_ansv          s             50
     d x_fdat          s             50
     d x_ftim          s             50
     d x_tdat          s             50
     d x_ttim          s             50
     d x_nobb          s             50
     d x_avar          s             50
     d x_avtx          s             50
     d x_asts          s             50
     d x_enhe          s             50
     d x_inpr          s             50
     d x_akpr          s             50
     d x_kopr          s             50
     d x_pkpr          s             50
     d x_kinf          s             70
     d x_vtxt          s             70
     d x_delt          s             50
     d x_vans          s             70
     d x_loka          s             50
     d x_tvre          s             50
     d x_ifda          s             50
     d x_itda          s             50
     d x_bipr          s             50
     d x_fkod          s             50
     d x_ftxt          s             50
      *
     d w_firm          s                   like(vvfirm)
     d w_omre          s                   like(veomre)
     d w_enh1          s                   like(vtenh1)
     d w_enh2          s                   like(vtenh2)
     d w_enh3          s                   like(vtenh3)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
      *
     d x               s              1  0
      *
7.01 d dirSolveKonf    s             10a   Inz('SLVEVRKAM')
      *
      * Definering av konstanter
      *
     d c_digi          s             10    inz('0123456789')
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
     d navnet          s            100a   varying
6.33 d len             s              3i 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

       len = p_lengde;
       navnet = %trim(%subst(p_filNavn:1:len));

       // setter navn på XML fil, brukes av view
       exec sql SET KAMPANJE_XML_SOLVE_VIEW_V = :navnet;

7.08   b_feil = *off;
7.08   exsr rydd_xls;

       exsr beh_kamp_hode;
       exsr beh_kamp_linje;

       p_sistDato = %char(%date(): *iso);  // OK

7.08   if  b_feil = *on;
7.08  * sende feil på innlesingslinje på mail
7.04   exsr lag_excel;
7.04   exsr send_mail;
7.08   endif;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle kampanje-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c     beh_kamp_hode begsr
      *
       // bruk view til å lese XML fil
       exec sql
         declare c1 cursor for
            select  distinct
               CampaignCode
              ,CampaignDescription
              ,CampaignResponsible
              ,CampaignFromDate
              ,CampaignFromTime
              ,CampaignToDate
              ,CampaignToTime
            from KAMPANJE_XML_SOLVE_VIEW
                for read only;
       exec sql
         Open c1;

       doW (1 = 1);

         exec sql
         Fetch c1 INTO
            :x_kamp         -- kamp
           ,:x_besk         --,besk
           ,:x_ansv         --,ansv
           ,:x_fdat         --,fdat
           ,:x_ftim         --,ftim
           ,:x_tdat         --,tdat
           ,:x_ttim         --,ttim
           ;

         if ((SQLCode < 0) or (SQLCode = 100));
           leave;
         endif;

     *** if %check(c_digi : %trim(w_vare)) = 0;
         if x_kamp <> *blank and x_fdat <> *blank and x_tdat <> *blank;
           exsr prs_hode;
           exsr dlt_kamp_hode;
         endif;
       enddo;

       exec sql close c1;

     c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle kampanje-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c     beh_kamp_linjebegsr

       // bruk view til å lese XML fil
       exec sql
         declare c2 cursor for
            select
            NobbNumber
       //    ,SenderItemNumber
       //    ,SenderItemText
       //    ,SenderItemStatus
           ,SalesPriceUnit
           ,CampaignPurchasePrice
           ,CampaignCostPrice
           ,RecommendedCampaignPrice
           ,ImposedCampaignPrice
       //    ,PurchaseInformation
       //    ,ItemInformation
       //    ,SupplierInformation
           ,NobbParticipantNumber
       //    ,ItemResponsible
       //    ,StorageLocation
       //    ,Tv
           ,PurchasePeriodFromDate
           ,PurchasePeriodToDate
       //    ,ImprovedPurchasePrice
       //    ,Code
       //    ,Description
            from KAMPANJE_XML_SOLVE_VIEW
                for read only;

       exec sql Open c2;

       doW (1 = 1);

         exec sql
         Fetch c2 INTO
            :x_nobb         -- VARENR
       //     ,:x_avar         --,NOBBLDOR
       //    ,:x_avtx         --,lev.vnr
       //    ,:x_asts         --,lev.stat
           ,:x_enhe         --,enhet
           ,:x_inpr         --,innkj.pris
           ,:x_kopr         --,kost.pris
           ,:x_akpr         --,foreslått pris
           ,:x_pkpr         --,kamp.pris
       //    ,:x_linf         --,kjøpsinfo.
       //    ,:x_vtxt         --,vare info
       //    ,:x_kinf         --,..info
           ,:x_delt         --,deltaker
       //    ,:x_vans         --,vare.ansvar.
       //    ,:x_loka         --,lokasjon
       //    ,:x_tvre         --,tv
           ,:x_ifda         --,innk.fra dato
           ,:x_itda         --,innk. til ato
       //    ,:x_bipr         --,best.innk.pris
       //    ,:x_fkod         --,feilkode
       //    ,:x_ftxt         --,feiltekst
           ;

         if ((SQLCode < 0) or (SQLCode = 100));
           leave;
         endif;

      *  if %check(c_digi : %trim(w_vare)) = 0;
         if x_nobb <> *blank;
7.04       b_kamp = *off;
           exsr prs_linj;
           exsr skr_kamp_linje;
         endif;
       enddo;

       exec sql close c2;
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Parse       hode -info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prs_hode      begsr
      *
     c                   eval      w_kamp = %trim(x_kamp)
     c     Lo:Up         xlate     w_kamp        w_kamp
     c                   eval      w_besk = %trim(x_besk)
     c                   eval      w_ansv = %trim(x_ansv)
     c                   eval      w_fdat = %date(%SUBST(x_fdat:1:10):*iso)
     c*                   eval      w_ftim = %date(%SUBST(x_ftim:1:10):*iso)
     c                   eval      w_ftim = *loval
     c                   eval      w_tdat = %date(%SUBST(x_tdat:1:10):*iso)
     c*                   eval      w_ttim = %date(%SUBST(x_ttim:1:10):*iso)
     c                   eval      w_ttim = *hival
      *** MIDL!
7.03 c                   eval      p_stat = '1'                                 MIDL!
7.03 c                   eval      p_jsts = 30
7.03 c                   eval      p_jmld = *blanks
7.03 c                   eval      p_jmld = 'Start  ' +
7.03 c                             '** prs_hode ** ' +
7.03 c                             %trim(w_kamp) +
7.03 c                             %char(w_fdat) + %char(w_tdat)
7.03 c                   call      'AB705R'
7.03 c                   parm                    p_jbid
7.03 c                   parm                    p_jsts
7.03 c                   parm                    p_jmld
      ***
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Parse       hode -info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prs_linj      begsr
      *
     c                   eval      w_enhe = %trim(x_enhe)
     c                   eval      w_nobb = %dec(x_nobb:8:0)
      *
     c                   if        x_pkpr <> *Blank
     c                   eval      w_pkpr = %Dech(x_pkpr:9:2)
     c                   else
     c                   eval      w_pkpr = 0
     c                   endif
     c                   if        x_kopr <> *blank
     c                   eval      w_kopr = %Dech(x_kopr:9:2)
     c                   else
     c                   eval      w_kopr = 0
     c                   endif
     c                   if        x_inpr <> *blank
     c                   eval      w_inpr = %Dech(x_inpr:9:2)
     c                   else
     c                   eval      w_inpr = 0
     c                   endif
     c                   if        x_akpr <> *blank
     c                   eval      w_akpr = %Dech(x_akpr:9:2)
     c                   else
     c                   eval      w_akpr = 0
     c                   endif
     c                   if        x_delt <> *blank
     c                   eval      w_delt = %dec(x_delt:6:0)
     c                   else
     c                   eval      w_delt = 0
     c                   endif
     c                   if        x_ifda <> *blank
     c                   eval      w_ifda = %date(%SUBST(x_ifda:1:10):*iso)
     c                   else
     c                   eval      w_ifda = *loval
     c                   endif
     c                   if        x_itda <> *blank
     c                   eval      w_itda = %date(%SUBST(x_itda:1:10):*iso)
     c                   else
     c                   eval      w_itda = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletter eksisterende kampanje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dlt_kamp_hode begsr
      *
     c                   if        w_ttim = *loval
     c                   eval      w_ttim = *hival
     c                   endif
      *
      * Sletter eventuell eksisterende kampanje -
      * med samme kampanjenavn
      *
     c                   if        w_kamp <> *blank
     c                   eval      vtill5_kamp = w_kamp
     c     vtill5_key    setll     vtill5
     c     vtill5_key    reade     vtill5r
     c                   dow       not %eof
     c                   delete    vtill5r
     c     vtill5_key    reade     vtill5r
     c                   enddo
     c                   endif
      *
9.01 c                   if        w_kamp <> *blank
9.01  *
9.01 c                   eval      jkahlu_kamp = w_kamp
9.01 c     jkahlu_key    chain     jkahlu
9.01 c                   if        %found
9.01 c                   delete    jkahlur
9.01 c                   endif
9.01  *
9.01 c                   eval      jkavlu_vkam = w_kamp
9.01 c     jkavlu_key    setll     jkavlu
9.01 c     jkavlu_key    reade     jkavlur
9.01 c                   dow       not %eof
9.01 c                   delete    jkavlur
9.01 c     jkavlu_key    reade     jkavlur
9.01 c                   enddo
9.01  *
9.01 c                   clear                   jkahlur
9.01 c                   eval      jmkamp = w_kamp
9.01 c                   eval      jmbesk = w_besk
9.01 c                   eval      jmfdat = w_fdat
9.01 c                   eval      jmtdat = w_tdat
9.01 c                   eval      jmansv = w_ansv
9.01 c                   eval      jmooda = %date
9.01 c                   eval      jmedat = %date
9.01 c                   eval      jmetim = %time
9.01 c                   eval      jmeusr = l_user
9.01 c                   write     jkahlur
9.01  *
9.01 c                   endif
      *
      ***
7.03 c                   eval      p_stat = '1'                                 MIDL!
7.03 c                   eval      p_jsts = 30
7.03 c                   eval      p_jmld = *blanks
7.03 c                   eval      p_jmld = 'Start  ' +
7.03 c                             '** dlt_hode ** ' +
7.03 c                             %trim(w_kamp) +
7.03 c                             %char(w_fdat) + %char(w_tdat)
7.03 c                   call      'AB705R'
7.03 c                   parm                    p_jbid
7.03 c                   parm                    p_jsts
7.03 c                   parm                    p_jmld
      ***
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av kampanje (linje)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_kamp_linjebegsr
      *
7.03 c                   eval      p_stat = '1'                                 MIDL!
7.03 c                   eval      p_jsts = 30
7.03 c                   eval      p_jmld = *blanks
7.03 c                   eval      p_jmld = 'Start  ' +
7.03 c                             '** skr_kamp_linj ** ' +
7.03 c                             %char(w_nobb) + %trim(w_kamp) +
7.03 c                             %char(w_fdat) + %char(w_tdat)
7.03 c                   call      'AB705R'
7.03 c                   parm                    p_jbid
7.03 c                   parm                    p_jsts
7.03 c                   parm                    p_jmld
      *
9.01 c                   if        w_kamp <> *blank
9.01 c                   exsr      oppr_kamp_va
9.01 c                   endif
      *
      * Avbryter dersom vare ikke finnes
      *
     c                   if        w_nobb = 0
     c                   leavesr
     c                   endif
      *
     c                   eval      vvarl3_nobb = w_nobb
     c     vvarl3_key    chain     vvarl3
     c                   if        not %found
      *
     c                   eval      vvarl1_vare = %trim(%char(w_nobb))
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
      *
7.01 c                   eval      p_stat = '1'
7.01 c                   eval      p_jsts = 30
7.01 c                   eval      p_jmld = *blanks
7.04 c                   eval      w_feil = 'Vare finnes ikke       ' +
7.04 c                             '** skr_kamp_linj ** '
7.04 c                   exsr      skriv_xls
     c                   leavesr
      *
     c                   endif
     c                   endif
      *
      * Henter omr.faktor for enhet
      * Avbryter dersom vare-enhet ikke finnes
      *
     c                   eval      vvenl1_vare = vvvare
     c                   eval      vvenl1_enhe = w_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
7.01 c                   eval      p_stat = '1'
7.01 c                   eval      p_jsts = 30
7.01 c                   eval      p_jmld = *blanks
7.04 c                   eval      w_feil = 'Enhet finnes ikke       ' +
7.04 c                             '** skr_kamp_linj ** '
7.04 c                   exsr      skriv_xls
     c                   leavesr
     c                   endif
      *
     c                   eval      w_omre = veomre
      *
      * Henter tre salgsenheter
      *
     c                   eval      x = 0
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2r
     c     vvenl2_key2   reade     vvenl2r
     c                   dow       not %eof and
     c                             x < 3
     c                   eval      x = x + 1
     c                   select
     c                   when      x = 1
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   endsl
     c     vvenl2_key2   reade     vvenl2r
     c                   enddo
      *
     c                   if        w_enh1 = *blank
7.01 c                   eval      p_stat = '1'
7.01 c                   eval      p_jsts = 30
7.01 c                   eval      p_jmld = *blanks
7.01 c                   eval      p_jmld = 'Enhet er blank ' +
7.01 c                             '** skr_kamp_linj ** '
7.01  *
7.01 c                   call      'AB705R'
7.01 c                   parm                    p_jbid
7.01 c                   parm                    p_jsts
7.01 c                   parm                    p_jmld
7.04 c                   eval      w_feil = 'Enhet er blank       ' +
7.04 c                             '** skr_kamp_linj ** '
7.04 c                   exsr      skriv_xls
     c                   leavesr
     c                   endif
      *
      * Skriver til kampanje (tilbud)
      * Oppdaterer eventuelt eksisterende kampanje-linje med ny kampanje-linje
      *
     c                   eval      vtillu_ogrp = 0
     c                   eval      vtillu_hgrp = 0
     c                   eval      vtillu_ugrp = 0
     c                   eval      vtillu_ldor = 0
     c                   eval      vtillu_vare = vvvare
     c                   eval      vtillu_prgr = w_prgr
     c                   eval      vtillu_lety = 0
     c                   eval      vtillu_fdat = w_fdat
     c                   eval      vtillu_ftim = w_ftim
     c                   eval      vtillu_tdat = w_tdat
     c                   eval      vtillu_ttim = w_ttim
     c     vtillu_key    chain     vtillur
      *
     c                   clear                   vtillur
      *
     c                   if        w_akpr <> 0 and
     c                             w_pkpr = 0
     c                   eval      w_pkpr = w_akpr
     c                   endif
      *
     c                   eval      vtfirm = w_firm
     c                   eval      vtogrp = 0
     c                   eval      vthgrp = 0
     c                   eval      vtugrp = 0
     c                   eval      vtldor = 0
     c                   eval      vtvare = vvvare
     c                   eval      vtprgr = w_prgr
     c                   eval      vtkamp = w_kamp
     c                   eval      vtlety = 0
     c                   eval      vtfdat = w_fdat
     c                   eval      vtftim = w_ftim
     c                   eval      vttdat = w_tdat
     c                   eval      vtttim = w_ttim
      *
     c                   eval      vtenh1 = w_enh1
     c                   eval(h)   vttip1 = w_pkpr * w_omr1 / w_omre
     c                   eval(h)   vtkop1 = w_kopr * w_omr1 / w_omre
     c                   eval(h)   vtinp1 = w_inpr * w_omr1 / w_omre
     c                   if        vtkop1 = 0
     c                   eval      vtkop1 = vtinp1
     c                   endif
      *
     c                   if        w_enh2 <> *blank
     c                   eval      vtenh2 = w_enh2
     c                   eval(h)   vttip2 = w_pkpr * w_omr2 / w_omre
     c                   eval(h)   vtkop2 = w_kopr * w_omr2 / w_omre
     c                   eval(h)   vtinp2 = w_inpr * w_omr2 / w_omre
     c                   if        vtkop2 = 0
     c                   eval      vtkop2 = vtinp2
     c                   endif
     c                   endif
      *
     c                   if        w_enh3 <> *blank
     c                   eval      vtenh3 = w_enh3
     c                   eval(h)   vttip3 = w_pkpr * w_omr3 / w_omre
     c                   eval(h)   vtkop3 = w_kopr * w_omr3 / w_omre
     c                   eval(h)   vtinp3 = w_inpr * w_omr3 / w_omre
     c                   if        vtkop3 = 0
     c                   eval      vtkop3 = vtinp3
     c                   endif
     c                   endif
      *
     c                   eval      vtodat = %date
     c                   eval      vtotim = %time
     c                   eval      vtousr = l_user
     c                   eval      vtedat = %date
     c                   eval      vtetim = %time
     c                   eval      vteusr = l_user
      *
     c                   if        %found(vtillu)
     c                   update    vtillur
7.03 c                   eval      p_stat = '1'
7.03 c                   eval      p_jsts = 30
7.03 c                   eval      p_jmld = *blanks
7.03 c                   eval      p_jmld = 'Update ' +
7.03 c                             '** skr_kamp_linj ** ' +
7.03 c                             %char(w_nobb) + %trim(w_kamp) +
7.03 c                             %char(w_fdat) + %char(w_tdat)
7.03 c                   call      'AB705R'
7.03 c                   parm                    p_jbid
7.03 c                   parm                    p_jsts
7.03 c                   parm                    p_jmld
     c                   else
     c                   write     vtillur
7.03 c                   eval      p_stat = '1'
7.03 c                   eval      p_jsts = 30
7.03 c                   eval      p_jmld = *blanks
7.03 c                   eval      p_jmld = 'Write  ' +
7.03 c                             '** skr_kamp_linj ** ' +
7.03 c                             %char(w_nobb) + %trim(w_kamp) +
7.03 c                             %char(w_fdat) + %char(w_tdat)
7.03 c                   call      'AB705R'
7.03 c                   parm                    p_jbid
7.03 c                   parm                    p_jsts
7.03 c                   parm                    p_jmld
     c                   endif
      *
      * MIDL!!!!!
      *
7.03 c                   eval      p_stat = '1'
7.03 c                   eval      p_jsts = 30
7.03 c                   eval      p_jmld = *blanks
7.03 c                   eval      p_jmld = 'Vare i XML       ' +
7.03 c                             '** skr_kamp_linj ** ' +
7.03 c                             %char(w_nobb)
7.03  *
7.03 c                   call      'AB705R'
7.03 c                   parm                    p_jbid
7.03 c                   parm                    p_jsts
7.03 c                   parm                    p_jmld
     c                   endsr
      *
9.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  * Subrutine for oppretting av kampanje i VA
9.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  *
9.01 c     oppr_kamp_va  begsr
9.01  *
9.01 c                   eval      jvarl3_nobb = w_nobb
9.01 c     jvarl3_key    chain     jvarl3
9.01 c                   if        not %found
9.01 c                   leavesr
9.01 c                   endif
9.01  *
9.01 c                   clear                   jkavlur
9.01  *
9.01 c                   eval      jmvfir = w_firm
9.01 c                   eval      jmvkam = w_kamp
9.01 c                   eval      jmvvar = jvvare
9.01 c                   eval      jmvldo = w_delt
9.01 c                   eval      jmvkai = w_inpr
9.01 c                   eval      jmvkpr = w_kopr
9.01 c                   eval      jmvkaa = w_pkpr
9.01 c                   eval      jmvkau = 0
9.01 c                   eval      jmvans = *blank
9.01 c                   eval      jmvidf = w_ifda
9.01 c                   eval      jmvidt = w_itda
9.01 c                   eval      jmvlas = 0
9.01  *
9.01 c                   if        jmvldo = 0
9.01 c                   eval      jvlel1_lvnr = jvvare
9.01 c                   eval      jvlel1_lldo = 900001
9.01 c     jvlel1_key    chain     jvlel1
9.01 c                   if        %found
9.01 c                   eval      jmvldo = 900001
9.01 c                   else
9.01 c                   eval      jmvldo = jvldor
9.01 c                   endif
9.01 c                   endif
9.01  *
9.01 c                   if        w_enhe <> jvpenh
9.01 c                   eval      jvpkl2_vare = jvvare
9.01 c     jvpkl2_key    setll     jvpkl2
9.01 c     jvpkl2_key    reade     jvpkl2
9.01 c                   dow       not %eof
9.01 c                   if        w_enhe = jwenhe
9.01 c                   eval(h)   jmvkai = jmvkai / jwofak
9.01 c                   eval(h)   jmvkpr = jmvkpr / jwofak
9.01 c                   eval(h)   jmvkaa = jmvkaa / jwofak
9.01 c                   eval(h)   jmvkau = jmvkau / jwofak
9.01 c                   leave
9.01 c                   endif
9.01 c     jvpkl2_key    reade     jvpkl2
9.01 c                   enddo
9.01 c                   endif
9.01  *
9.01 c                   eval      jmvoda = %date
9.01 c                   eval      jmveda = %date
9.01 c                   eval      jmveti = %time
9.01 c                   eval      jmveus = l_user
9.01  *
9.01 c                   write     jkavlur
      *
7.04  *
7.04  * skriver til xls-innlesing
7.04  *
7.04 c                   eval      b_kamp = *on
7.04 c                   exsr      skriv_xls
9.01  *
9.01 c                   endsr
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.04  * Subrutiner for XML integrasjon - detaljlinjer                   *
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.04 c     skriv_xls     begsr
7.04 c                   eval      w_lnav = *blank
7.08 c                   eval      b_feil = *on
7.04  *tar en les på leverandør her for å få riktig tekst
7.04 c                   eval      rlevl1_levr = vvldor
7.04 c     rlevl1_key    chain     rlevl1
7.04  *
7.04 c
7.04 c                   clear                   fxl2str
7.06  * Filegruppe
7.06 c                   eval      fxlfgr=%trim(l_filg)
7.04  *
7.04  * Vare
7.04 c                   eval      fxlvar=%char(vvnobb)
7.04  * Tekst
7.04 c                   eval      fxltxt=%trim(vvtek1)
7.04  * Leverandør
7.04 c                   eval      fxlldor=vvldor
7.04  * Lev.navn
7.04 c                   eval      fxllnav=%trim(rlnavn)
7.04  * Enhe basis
7.04 c                   eval      fxlenh0=%trim(vvenhe)
7.04  * enhet
7.04 c                   eval      fxlenhe=%trim(w_enhe)
7.04  * Prisgiver
7.04 c*                   eval      fxlpgiv=d_ldor
7.04  * Prisgruppe
7.04 c*                   eval      fxlprgr=%trim(w_prgr)
7.04  * Kampanje
7.04  *
7.04 c                   eval      fxlkamp=%trim(w_kamp)
7.04  * Kampanje fra dato
7.04 c                   if        w_kamp <> *blank and
7.04 c                             b_kamp = *on
7.04  * fradato
7.04 c                   eval      ffxfdat=vtfdat
7.04  * fra time
7.04 c                   eval      ffxftim=vtftim
7.04  * tildato
7.04 c                   eval      ffxtdat=vttdat
7.04  * fra time
7.04 c                   eval      ffxftim=vtttim
7.04  * enh1
7.04 c                   eval      fxlken1=%trim(vtenh1)
7.04  * kostpr 1
7.04 c                   eval      fxlkko1=vtkop1
7.04  * tilbudspris 1
7.04 c                   eval      fxlkti1=vttip1
7.04  * enh2
7.04 c                   eval      fxlken2=%trim(vtenh2)
7.04  * kostpr 2
7.04 c                   eval      fxlkko2=vtkop2
7.04  * tilbudspris 2
7.04 c                   eval      fxlkti2=vttip2
7.04  * enh3
7.04 c                   eval      fxlken3=%trim(vtenh3)
7.04  * kostpr 3
7.04 c                   eval      fxlkko3=vtkop3
7.04  * tilbudspris 3
7.04 c                   eval      fxlkti3=vttip3
7.04  *
7.04 c                   endif
7.04  * utgår dato
7.04 c                   eval      fxludat=vvudat
7.04
7.04  * Feiltekst
7.04 c                   eval      fxlfeil=%trim(w_feil)
7.04 c                   write     fxl2str
7.04 c                   endsr
7.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.08  * Subrutiner for XML å slette innhold i coolspool tabell          *
7.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.08 c     rydd_xls      begsr
7.08 c/exec sql
7.08 c+ delete from fxl2st
7.08 c/end-exec
7.08   exec sql commit;
7.08 c                   endsr
7.08

7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  * Lager Excel via Coolspool
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  *
7.04 c     lag_excel     begsr
7.04  *
7.04 c                   eval      w_exsql = 'select * from fxl2st'
7.04 c
7.04 c                   call      'ASPTOXLSX'
7.04 c                   parm                    w_exsql
7.04 c                   parm                    w_exmap
7.04 c                   parm                    w_exfil
7.04 c                   parm                    w_extyp
7.04  *
7.04 c                   endsr
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  * Sender Excel på mail med proa
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  *
7.04 c     send_mail     begsr
7.04  *
7.07  * blanker felter
7.07 c                   eval      w_rec1 = *blank
7.07 c                   eval      w_rec2 = *blank
7.07 c                   eval      w_rec3 = *blank
7.04  * finner mailadresser
7.04 c     rax9l1_key    setll     rax9l1
7.04 c     rax9l1_key    reade     rax9l1
7.04 c                   dow       not %eof
7.04  *
7.04 c                   if        raik01 = 1
7.04 c                   eval      ra09l1_ikod = raikod
7.04 c     ra09l1_key    chain     ra09l1
7.04 c                   if        %found(ra09l1) and
7.04 c                             raiema <> *Blank
7.04  *
7.04 c                   if        w_rec1 = *blank
7.04 c                   eval      w_rec1 = raiema
7.04 c                   else
7.04 c                   if        w_rec2 = *blank
7.04 c                   eval      w_rec2 = raiema
7.04 c                   else
7.04 c                   if        w_rec3 = *blank
7.04 c                   eval      w_rec3 = raiema
7.04 c                   endif
7.04 c                   endif
7.04 c                   endif
7.04 c                   endif
7.04 c                   endif
7.04  *
7.04 c     rax9l1_key    reade     rax9l1
7.04 c                   enddo
      *hvis ingen mail er valgt, så hopp ut.
7.07 c                   if        w_rec1 = *blank and
7.07 c                             w_Rec2 = *Blank and
7.07 c                             w_Rec3 = *blank
7.07 c                   leavesr
7.07 c                   endif
7.04  *
7.04 c*                   eval      p_rec1 = 'bjkva@eg.no'
7.04 c*                   eval      p_rec2 = *blank
7.04 c*                   eval      p_rec3 = *blank
7.04 c                   eval      w_subj = 'Kampanje innlesing fra Solve'
7.04 c                   eval      w_txt1 = 'Kampanje  blitt lest inn, +
7.04 c                             se vedlegg for informasjon.'
7.04 c                   eval      w_txt2 = *blank
7.04 c                   eval      w_txt3 = *blank
7.04 c                   eval      w_txt4 = 'Med vennlig hilsen'
7.04 c                   eval      w_filn = '/asp/'+l_filg+'/excel/'+
7.04 c                             %trim(w_exfil)
7.04 c                   eval      w_fnav = 'Vareinnlesing' + %char(%date) +
7.04 c                                      '.xlsx'
7.04 c                   eval      w_mime = 'application/vnd.openxmlformats-'
7.04 c                   eval      w_mime = %trim(w_mime)+'officedocument.'
7.04 c                   eval      w_mime = %trim(w_mime)+'spreadsheetml.sheet'
7.09 c                   eval      w_bcc  = *blank
7.04 c                   call      'AF728R'
7.04 c                   parm                    w_rec1
7.04 c                   parm                    w_rec2
7.04 c                   parm                    w_rec3
7.04 c                   parm                    w_subj
7.04 c                   parm                    w_txt1
7.04 c                   parm                    w_txt2
7.04 c                   parm                    w_txt3
7.04 c                   parm                    w_txt4
7.04 c                   parm                    w_avsl
7.04 c                   parm                    w_filn
7.04 c                   parm                    w_fnav
7.04 c                   parm                    w_mime
7.09 c                   parm                    w_bcc
7.04  *
7.04 c                   endsr
7.04  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_filNavn
     c                   parm                    p_lengde
     c                   parm                    p_sistDato
     c                   parm                    p_stat
      *
      * Key for oppdatering av kampanje (tilbud)
      *
     c     vtill5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill5_kamp
      *
     c     vtillu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtillu_ogrp
     c                   kfld                    vtillu_hgrp
     c                   kfld                    vtillu_ugrp
     c                   kfld                    vtillu_ldor
     c                   kfld                    vtillu_vare
     c                   kfld                    vtillu_prgr
     c                   kfld                    vtillu_lety
     c                   kfld                    vtillu_fdat
     c                   kfld                    vtillu_ftim
     c                   kfld                    vtillu_tdat
     c                   kfld                    vtillu_ttim
      *
9.01  * Key for oppdatering av kampanje-hode (VA)
9.01  *
9.01 c     jkahlu_key    klist
9.01 c                   kfld                    w_firm
9.01 c                   kfld                    jkahlu_kamp
9.01  *
9.01  * Key for oppdatering av kampanje-vare (VA)
9.01  *
9.01 c     jkavlu_key    klist
9.01 c                   kfld                    w_firm
9.01 c                   kfld                    jkavlu_vkam
      *
      * Key for oppslag på vare
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
9.01  * Key for oppslag på vare (VA)
9.01  *
9.01 c     jvarl3_key    klist
9.01 c                   kfld                    jvarl3_nobb
9.01  *
9.01  * Key for oppslag på vare-pakning (VA)
9.01  *
9.01 c     jvlel1_key    klist
9.01 c                   kfld                    jvlel1_lvnr
9.01 c                   kfld                    jvlel1_lldo
9.01  *
9.01  * Key for les på vare-pakning (VA)
9.01  *
9.01 c     jvpkl2_key    klist
9.01 c                   kfld                    jvpkl2_vare
7.04  *
7.04  * Key for les av leverandør
7.04  *
7.04 c     rlevl1_key    klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    rlevl1_levr
7.04  *
7.04  * Key for oppslag på selger
7.04  *
7.04 c     ra09l1_key    klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    ra09l1_ikod
7.04  *
7.04  * Key for oppslag på selger sidereg
7.04  *
7.04 c     rax9l1_key    klist
7.04 c                   kfld                    w_firm
      *
      * Key for oppslag og les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
7.01  *
7.01  * Vi starter jobblogging
7.01  *
7.01 c                   eval      p_jnav = dirSolveKonf
7.01 c                   eval      p_jtxt = *blank
7.01 c                   call      'AB700R'
7.01 c                   parm                    p_jnav
7.01 c                   parm                    p_jbid
7.01 c                   parm                    p_jtxt
      *
     c                   endsr
