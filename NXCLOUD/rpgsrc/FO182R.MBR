     h option(*nodebugio) datedit(*ymd)
     h decedit('0,')
      /title  ASOFAK Gen. faktura i Logiq/Norgros formatparam. (EDI)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO182R
      * Beskrivelse . . : Fakturabeh., gen. faktura i Logiq/Norgros format
      *                   - Håndtering av utg. EDI-faktura (egen struktur)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       181004 GRO  Nytt program
      *       040507 BOF  Kundeprosjekt tas fra SOKPRO
      *       180507 BOF  varetekst 2 skal også legges ut
      *       180607 BOF  Sjekker skafferegister i tilfelle skaffevare
      *       290108 BOF  sjekker om lang el. kort kide - rut.reg FO181
      *       260508 BOF  hindre at ledende 0 blir fjernet i kort-kid
      *       101208 BOF  Omskriving av les og kontroll
5.61  *       190109 BOF  Endret / i fakturanr. til bindestrek
5.62  *       270109 BOF  hindre at ledende 0 blir fjernet i kort-kid
5.63  *       270109 BOF  Dato korrekt ut på liste
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  231209 BOF  Ekstern prosjektnr legges på 13
6.13  *       100210 BOF  Hvis ' i varetekst ++ erstattes med blank
6.13  *       030510 BOF  hindre at ledende 0 blir fjernet i postnr.
6.14  *       160610 BOF  Brudd pr. ean faktura kunde kun en pr.
      *                   konvolutt, dvs. 01-record
6.15  *       270110 BOF   prosjektnavn vises i prosj.EDI til MGR
6.16  *       241110 BOF  Justert 6.15, fjernet en sep. som ble for mye
6.17  *       070911 BOF  Hvis org.nr (9 langt) er lagt i ean-kunde så settes
      *                   ORG som identifikasjon.
6.18  *       061211 BOF  Rettelse når kun en faktura mht. fnuean
6.19  *       140912 Bsa  Rettelse rundt mva spesifisering
6.1a  *       050213 bof  Legger ut nettopris
6.1b  *       060513 bof  EHF - rabatt på kr.nota, gjør om til nettobeløp
6.1b  *                   + mulighet for å skrive ut nettobeløp (faktura)
6.1c  *       030613 bof  Sende netto enhetspris med 4 desimaler
6.1d  *       040613 bof  Justeringer vedr. avrunding
6.1e  *       010813 bof  Omskriving i forb. f11 linjer /tekstlinjer
6.1f  *       170913 bof  Sender 3 desimaler i antall (peppol-krav)
6.20  *       200512 BOF  Utvidet med fakt.type 28 EDI Samle
6.21  *       060612 BOF  Utvide med div. felter på rec. 35
6.22  *       050213 bof  Legger ut nettopris
6.23  *       221013 bof  Samme mulighet for flere mva.k som std.faktura
6.23  *       221013 bof  mye kode var overflødig, dette er fjernet.
6.24  *       061113 bof  Henter IV vha. rkfaku, gir ekstakt kundeinfo
6.25  *       201113 bof  Endret editc pga. må ha 0 ut i felt.
6.26  *       181213 bof  Tillate tekstlinje rett etter samlef. rec. 35
6.27  *       201213 bof  Hvis netto pris, så brutto-enh.pris lik netto.
6.28  *       131213 bof  Legger ut brutto enhetspris med 4 desimaler også
6.29  *       030214 bof  Utvider til 6 des. i netto og brutto- enhetspris
6.2a  *       180214 bof  Utvidet logikk i Kjøpers best.nr
6.2b  *       040214 bof  Takler rec 41 etter 35 og 11 etter 10
6.2c  *       070514 bof  7 desimaler + avrundet mva
6.2e  *       221214 bof  hvis 0 %mva, så må det legges 0,00 i rec. 81
6.2f  *       270815 bof  Hvis Deres ref = blank, så legg inn NA
6.2g  *       171215 nho  Må også teste på kidvariant 3 faktnr + kontr
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * R6.30 151015 bsa  Legger ut feil felter for mailadresse på BY og IV
6.31  * R6.30 151216 bsa  Div sjekk hvis litt mangelfull data fra ordrelinje
6.32  * R6.30 080416 bsa  Hvis faktura uten ordrelinjer, skal "hodet" merkes
6.32  *                   som overført.
6.33  * R6.30 220616 bof  Hvis F11 linje, men blank,skal ikke sendes.
6.34  *  6.30 310517 bof  hvis 0 %mva, må det legges 0,00 i rec. 81  mva.bel
6.35  *  6.30 020617 bof  Hvis ekst.prosjektnr, så kommer det før rekvn.nr
6.36  *  6.30 201017 bsa  Hvis nettopriset kunde, må netto og bruttobeløp
6.36  *                   behandles likt.
6.37  *       160518 bkv  Byggmester-Faktura vedlegg til faktura (5.62 FO181)
6.38  *       070818 bsa  Legger ut iBan og Swift på recordtype 20
6.39  *       191218 bof  Utvidet med trelast-logikk + feilrett.gtin
6.3a  *  6.30 260719 bof  Rettelse 6.35, rec.type35, rekv kommer før ekst.proj
7.01  *  7.00 231019 bsa  Blir feil ved EHF faktura og byggmester faktura
7.02  * K000  040620 bsa  Legger internt eller eksternt prosjektnummer i pos 13
7.03  *K000   160421 bsa  Legger ut avdeling og utsalgssted på 35. Reaktivert
7.03  *K000               mye av de koden som er stjernet ut.
7.04  *K000   080221 sst  Hente factoring bankgironr fra rkbgir på fact.kunde.
7.04  *                   hvis den er definert
      *
8.01  *       181019 bsa  Hvis SOLKUN utfylt, hent eank fra denne kunden
8.02  * 800   230222 ask  Lagt inn switchstyrt valg av bankkonto ved factoring
8.03  *K000   140622 bsa  Switchstyrer om firma eller avdelingsinfo skal legges
8.03  *K000               ut som SU på fila.
8.04  *K000   070223 sst  Lagge ut Priskode/rabatt på linjetype 41
8.05  *K000   221223 bsa  Scanner strengene for ugyldige tegn før den skrives.
8.06  *800    201123 ask  Lagt på sjekk på ordretype og kunde ved factoring
8.07  *800    060224 bsa  Lagt inn samme logikk ved F11 linjer som i FO181.
8.08  *800    070224 bsa  Endrer videre, da utvalget ikke er helt likt med FO181.
8.09  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner(kompilert)
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.04 frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
7.04 f                                     prefix(rr:2)
     frkunl5    if   e           k disk    rename(rkunpfr:rkunl5r)
     fsohelr    if   e           k disk    rename(sohepfr:sohelrr)
     f                                     prefix(xx:2)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsodtl1    if   e           k disk    rename(sodtpfr:sodtl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.12 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fra18l1    if   e           k disk    rename(ra18pfr:ra18l1r)
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
7.04 fraa9l1    if   e           k disk    rename(raa9pfr:raa9pfr)
     ffmftl1    if   e           k disk    rename(fmftpfr:fmftl1r)
     ffnufl1    if   e           k disk    rename(fnufpfr:fnufl1r)
     ffnufl2    if   e           k disk    rename(fnufpfr:fnufl2r)
6.14 ffnufl3    if   e           k disk    rename(fnufpfr:fnufl3r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.32 fsodtlr    if   e           k disk    rename(sodtpfr:sodtlrr)
     ffnuflu    uf a e           k disk    rename(fnufpfr:fnuflur)
7.03 fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
8.04 fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     ffnedpf    o  a e           k disk    rename(fnedpfr:fnedpfr)
     ffo182p    o    e             printer
      *
      * Definering av Local data area
      *
     d                uds
     d d_kidk                883    883  0
     d l_wsid                901    910
     d l_user                911    920
7.02 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
6.23  *
6.23  * Definering av array's
6.23  *
6.23 d mko             s              1    dim(9)
6.23 d msa             s              5  2 dim(9)
6.23 d mgr             s             11  2 dim(9)
6.23 d mbe             s             11  2 dim(9)
6.23 d mog             s             11  2 dim(9)
6.23 d mor             s             11  2 dim(9)
      *
      * Definering av variabler i nøkler
      *
     d sohelr_aarr     s                   like(soaarr)
     d sohelr_binr     s                   like(sobinr)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sodtl1_aarr     s                   like(sdaarr)
     d sodtl1_binr     s                   like(sdbinr)
     d sodtl1_numm     s                   like(sdnumm)
     d sodtl1_suff     s                   like(sdsuff)
     d rkunl1_kund     s                   like(rkkund)
7.04 d rkunlr_kund     s                   like(rkkund)
     d rkunl5_eank     s                   like(rkeank)
     d vvarl1_vare     s                   like(vvvare)
     d vltyl1_ltyp     s                   like(sdltyp)
     d votyl1_otyp     s                   like(vaotyp)
     d ra10l1_jkod     s                   like(rajkod)
     d ra18l1_rkod     s                   like(rarkod)
6.12 d fkprl1_kund     s                   like(flkund)
6.12 d fkprl1_kpro     s                   like(flkpro)
     d fmftl1_faty     s                   like(fmtfat)
     d fnufl1_aarr     s                   like(fnuaar)
     d fnufl1_binr     s                   like(fnubin)
     d fnufl2_ufat     s                   like(fnufat)
     d fnufl2_ukda     s                   like(fnukda)
6.14 d fnufl3_ufat     s                   like(fnufat)
6.14 d fnufl3_ukda     s                   like(fnukda)
     d fnuflu_aarr     s                   like(fnuaar)
     d fnuflu_binr     s                   like(fnubin)
     d jvarl1_vare     s                   like(jvvare)
6.32 d sodtlr_aarr     s                   like(sdaarr)
6.32 d sodtlr_binr     s                   like(sdbinr)
7.03 d ra07l1_gkod     s                   like(ragkod)
8.04 d vpkol1_pkod     s                   like(vapkod)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_flin          s           1024
     d w_flinx1        s           1024
     d w_flinx2        s           1024
     d w_flinx3        s           1024
     d w_raba          s              5  2
     d w_tnto          s             13  2
     d w_tmva          s             13  2
     d w_tmva_m        s             15  5
     d w_tmvg          s             13  2
     d w_mdat_iso      s               d   datfmt(*iso)
     d w_mdat_alf      s              8
     d w_mtim          s              6  0
     d w_mtim_alf      s              6
     d w_udat_iso      s               d   datfmt(*iso)
     d w_utim_iso      s               t   timfmt(*iso)
     d w_levd_alf      s              8
     d w_ford_alf      s              8
     d w_fakd_alf      s              8
     d w_odat_alf      s              8
     d w_mref          s              6  0
     d w_ftnr          s              9
     d w_ftex          s             14
     d w_npri          s             13  2
     d w_tbto          s             13  2
     d w_cbda          s               d   datfmt(*iso)
     d w_cfda          s               d   datfmt(*iso)
     d w_retk          s              1
     d w_ctx1          s             20
     d w_ctx2          s             20
     d w_ctxt          s             40
     d w_posm          s              4  0
     d w_numf          s             20
6.1f d w_antx          s             12  3
6.1f d w_anta          s             15
     d w_ntpe          s             15
6.1d d*w_ntpe16        s             16
6.2c d w_ntpe19        s             19
     d w_ntpf          s             15
     d w_mvap          s              8
     d w_dnto          s             20
     d w_dbto          s             20
     d w_drab          s             20
     d w_antf          s              6  0
     d w_antl          s              6  0
     d w_anto          s              6  0
     d w_tfak          s              6  0
6.1d d*w_enhp          s             15  4
6.2c d w_enhp          s             18  7
     d w_enhx          s             13
     d w_avru          s             11  2
     d w_avrx          s             13
     d w_rkrx          s             13
     d w_rabx          s              7
     d w_rabp          s              5  2
     d w_rabg          s             11  2
     d w_dmva          s             11  2
     d w_teid          s              1
     d w_fsum          s                   like(sofsum)
     d w_kidd          s                   like(sokidd)
6.2g d w_kid2          s                   like(sokidd)
6.2g d w_kidh          s             21
6.2g d w_kid3          s              8  0
     d w_kidr          s                   like(sokidr)
6.nu d*w_kidx          s              7
6.nu d w_kidx          s              9
     d w_knav          s                   like(rknavn)
     d w_iean          s                   like(rkeanf)
     d w_vare          s                   like(vvvare)
     d w_nobb          s                   like(vvnobb)
     d w_aarr          s              4
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
6.13 d w_ponx          s              4
6.15 d w_pnav          s             25
6.1b d w1rab1          s                   like(sdsapr)
7.04 d w_fbgi          s                   like(aafbgi)
7.04 d w_sbgi          s                   like(aafbgi)
6.23 d m               s              2  0
6.23 d w_mvas          s              6  2
6.23 d w_mvat          s              5  4
6.23 d w_mvaf          s             10  9
6.23 d w_bpro          s              5  2
6.23 d w_mvtx          s             30
6.12 d w_mvak          s              1
6.12 d w_stat          s              1
6.24 d w_faku          s                   like(rkfaku)
6.24 d w_kund          s                   like(rkkund)
6.2a d w_epro          s                   like(flepro)
6.37  * byggm.fakt
7.01 d w_bf_nl         s             11  2
6.37 d w_bf_n          s             11  2
6.37 d w_bf_g          s             11  2
6.37 d w_bf_m          s             11  2
6.37 d w_bf_b          s             11  2
6.37 d w_fbim          s             11  2
6.39 d w_lv_nobb       s                   like(vvnobb)
6.39 d w_lv_modn       s                   like(vvnmod)
6.39 d w_lv_lldo       s                   like(vvldor)
6.39 d w_lv_llva       s                   like(vvlvar)
6.39 d w_lv_vtyp       s                   like(vvvtyp)
6.39 d w_lv_udat       s                   like(vvedat)
6.39 d w_lv_ldor       s                   like(vvldor)
6.39 d b_inlr          s              1    inz(*off)
8.01 d w_eank          s                   like(rkeank)
8.01 d w_kunv          s                   like(rkkund)
8.01 d w_navn          s                   like(rknavn)
8.01 d w_gate          s                   like(rkgate)
8.01 d w_adr2          s                   like(rkadr2)
8.01 d w_ponr          s                   like(rkponr)
8.01 d w_sted          s                   like(rksted)
8.02 d w_fbkk          s             11  0
8.06 d w_fot1          s              2
8.06 d w_fot2          s              2
8.06 d w_fot3          s              2
8.06 d w_fot4          s              2
8.06 d w_fot5          s              2
8.06 d w_fack          s              1  0
8.03 d w_fmai          s                   like(aamail)
8.03 d w_fad1          s                   like(aafad1)
8.03 d w_fad2          s                   like(aafad2)
8.03 d w_fste          s                   like(aafste)
8.03 d w_ftlf          s                   like(aaftlf)
8.03 d w_ffax          s                   like(aaffax)
8.04 d w_tek1          s             70
      *
     d s_fsum          s             11  2
     d s_salp          s             11  2
     d s_salh          s             11  2
     d s_salf          s             11  2
     d s_rk1p          s             11  2
     d s_rk2p          s             11  2
     d s_rkop          s             11  2
     d s_rk1h          s             11  2
     d s_rk2h          s             11  2
     d s_rkoh          s             11  2
     d s_rk1f          s             11  2
     d s_rk2f          s             11  2
     d s_rkof          s             11  2
     d s_fnet          s             11  2
     d s_fbru          s             11  2
     d s_fmva          s             15  5
     d s_fmvg          s             11  2
     d s_frab          s             11  2
     d s_ltyp          s              2
     d s_mvp1          s              6  2
     d s_mvg1          s             11  2
     d s_mvb1          s             15  5
     d s_mvp2          s              6  2
     d s_mvg2          s             11  2
     d s_mvb2          s             15  5
     d s_mvp3          s              6  2
     d s_mvg3          s             11  2
6.14 d s_ueaf          s                   like(fnueaf)
      *
     d                 ds
     d w_kvan                        14  3
     d   w_kvanh                     11    overlay(w_kvan:1)
     d   w_kvand                      3    overlay(w_kvan:12)
     d                 ds
     d w_belo                        17  2
     d   w_beloh                     15    overlay(w_belo:1)
     d   w_belod                      2    overlay(w_belo:16)
     d                 ds
     d w_pros                         5  2
     d   w_prosh                      3    overlay(w_pros:1)
     d   w_prosd                      2    overlay(w_pros:4)
     d                 ds
     d w_pris                        14  3
     d   w_prish                     11    overlay(w_pris:1)
     d   w_prisd                      3    overlay(w_pris:12)
      *
     d b_init          s              1    inz(*off)
     d b_binr          s              1    inz(*off)
     d b_numm          s              1    inz(*off)
     d b_logf          s              1    inz(*off)
     d b_saml          s              1    inz(*off)
     d b_kred          s              1    inz(*off)
     d b_skfl          s              1    inz(*off)
     d b_uskr          s              1    inz(*off)
6.32 d b_alin          s              1    inz(*off)
8.06 d b_fact          s              1    inz(*off)
      *
     d s_binr          s                   like(sobinr)
     d s_numm          s                   like(sonumm)
     d s_lbin          s                   like(sobinr)
     d s_lnum          s                   like(sonumm)
     d s_kund          s                   like(sokund)
     d s_lsuf          s                   like(sosuff)
      *
      * Definering av parametre
      *
     d p_aarr          s                   like(soaarr)
     d p_faty          s                   like(sofaty)
6.nu d p_fnum          s                   like(sobinr)
6.nu d p_tnum          s                   like(sobinr)
     d p_omkj          s              1
     d p_rapp          s              1
     d p_anta          s              4  0
     d p_feil          s              1
      *
     d p_stat          s              1
     d p_ekod          s              1
     d p_etxt          s             30
     d p_dato          s               d   datfmt(*iso)
     d p_mvap          s              6  2
     d p_mvat          s              5  4
     d p_mvaf          s             10  9
      *
      * Konstanter
      *
     d c_fsep          c                   '|'
     d c_veid          c                   '1.0'
     d c_teid          c                   '1'
      *
7.02   dcl-s co402_verdi1  char(1);
7.02   dcl-s co402_verdi2  char(2048);
7.02   DCL-PR CO402R EXTPGM('CO402R');
7.02     p_filgrp            char(3)     const;
7.02     p_firm              packed(3:0) const;
7.02     p_lager             packed(2:0) const;
7.02     p_nokkel            char(50)    const;
7.02     p_verdi1            char(1);
7.02     p_verdi2            char(2048);
7.02   END-PR;
7.02 d u_702           s              1    inz(*off)
8.01 d u_801           s              1    inz(*off)
8.02 d u_802           s              1    inz(*off)
8.03 d u_avde          s              1    inz(*off)
8.04 d u_prko          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.14 c                   eval      s_ueaf = 0
      *
      * Leser firmadata ifm avsenderinformasjon
      *
     c     afir_key      chain     afirl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter OFAK informasjon
      *
     c     fstsl1_key    chain     fstsl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c*                  eval      ra10l1_jkod = faahla
     c*    ra10l1_key    chain     ra10l1
     c*                  if        not %found
     c*                  goto      avslutt
     c*                  endif
      *
      * Hent statuskode 8
      *
     c     raa8l1_key    chain     raa8l1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter fakturatype info
      *
     c                   eval      fmftl1_faty = p_faty
     c     fmftl1_key    chain     fmftl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   time                    w_udat_iso
     c                   time                    w_utim_iso
5.60  *
5.60 c                   select
5.60 c                   when      p_omkj = *blank
6.14 c                   eval      fnufl3_ufat = p_faty
6.14 c                   eval      fnufl3_ukda = *loval
6.14 c     fnufl3_key    setll     fnufl3
6.14 c                   read      fnufl3                                 90
5.60 c                   when      p_omkj = '1'
5.60 c                   eval      fnufl1_aarr = p_aarr
5.60 c                   eval      fnufl1_binr = p_fnum
5.60 c     fnufl1_key    setll     fnufl1
5.60 c                   read      fnufl1                                 90
5.60 c                   endsl
6.14 c                   eval      s_ueaf = fnueaf
5.60  *
5.60 c                   dow       *in90 = *off
6.14  * brudd pr. ean faktura kundenr - kun 1 pr. konvolutt
6.18 c                   if        s_ueaf <> fnueaf and
6.18 c                             w_antl > 0
6.14 c                   goto      siste
6.14 c                   endif
6.14 c                   eval      s_ueaf = fnueaf
5.60  *
5.60  * hvis vanlig kjøring - og kjørt dato <> *loval da kan man avslutte
5.60 c                   if        p_omkj = *blank and
5.60 c                             fnukda <> *loval
5.60 c                   goto      siste
5.60 c                   endif
5.60  * hvis omkjøring  - og til faktnr. er passert kan man avslutte
5.60 c                   if        p_omkj = '1' and
5.60 c                             fnubin > p_tnum
5.60 c                   goto      siste
5.60 c                   endif
      *
     c                   if        fnuaar = p_aarr and
     c                             fnufat = p_faty
      *
     c                   exsr      sjk_uf_log
      *
     c                   if        b_logf = *on
      *
     c                   eval      sohel1_aarr = p_aarr
     c                   eval      sohel1_binr = fnubin
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1
6.32  * Sjekk om faktura har linjer
6.32 c                   exsr      sjk_fakt
6.32  *
6.32 c                   if        b_alin = *off
6.32 c                   eval      s_binr = sobinr
6.32 c                   exsr      upd_uf_log
6.32 c                   goto      noline
6.32 c                   endif
6.32  *
     c                   exsr      sjk_saml
     c                   exsr      gen_fakthod
      *
     c                   dow       not %eof(sohel1)
      *
     c                   exsr      skriv_olin
      *
      *
      * Iterasjon på fakturahode
      *
     c                   eval      sodtl1_aarr = soaarr
     c                   eval      sodtl1_binr = sobinr
     c                   eval      sodtl1_numm = sonumm
     c                   eval      sodtl1_suff = sosuff
     c                   eval      w_tbto = w_tbto + sofsum
      *
     c     sodtl1_key    setll     sodtl1
     c     sodtl1_key    reade     sodtl1
      *
      * Iterasjon på fakturalinje
      *
     c                   dow       not %eof(sodtl1)
      *
8.08 c                   if        %trim(sdvare) <> *blanks
     c                   eval      vltyl1_ltyp = sdltyp
     c     vltyl1_key    chain     vltyl1r
     c                   if        valktx <> 1
      *
     c                   eval      vvarl1_vare = sdvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   exsr      sjk_50rec
     c                   exsr      skriv_vlin
     c                   else
     c                   exsr      skriv_ftxl
     c                   endif
8.08 c                   else
8.08 c                   exsr      skriv_ftxl
8.08 c                   endif
      *
     c     sodtl1_key    reade     sodtl1
      *
     c                   eval      s_numm = sdnumm
     c                   eval      s_binr = sdbinr
     c                   enddo
      *
     c                   exsr      sjk_50rec
     c                   exsr      save_fsum
      *
     c     sohel1_key    reade     sohel1
     c                   if        %eof(sohel1) or
     c                             sobinr <> fnubin
     c                   exsr      skriv_fslut
     c                   endif
      *
     c                   enddo
      *
     c                   endif
      *
     c                   endif
6.32  * Hvis ingen linjer på faktura, kommer du hit.
6.32 c     noline        tag
      *
     c                   select
     c                   when      p_omkj = *blank
6.14 c                   read      fnufl3                                 90
     c                   when      p_omkj = '1'
     c                   read      fnufl1                                 90
     c                   endsl
      *
     c                   enddo
      *
     c     siste         tag
      *
      * Sluttrecord - recordtype 90
      *
     c                   if        w_antl > *zero
     c                   eval      w_flin = *blank
     c                   eval      w_antl = w_antl + 1
      *
      * - Recordtype / ant.linjer / ant.fakturaer
      *
     c                   eval      w_flin = '90' + c_fsep +
     c                                      %char(w_antl) + c_fsep +
     c                                      %char(w_antf) + c_fsep
      *
     c                   exsr      write_outp
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      p_anta = w_tfak
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av faktura-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     gen_fakthod   begsr
      *
     c                   if        s_lbin <> 0 and
     c                             s_lbin = sobinr
     c                   leavesr
     c                   endif
6.37  *
6.37 c                   eval      w_bf_n = 0
6.37 c                   eval      w_bf_g = 0
6.37 c                   eval      w_bf_m = 0
6.37 c                   eval      w_bf_b = 0
      *
      *
      * Faktura identifikasjonspost - recordtype 01
      *
     c                   eval      w_flin = *blank
      *
      * - Recordtype/avsender-id./kvalifikator
      *
     c*                  if        s_kund <> sokund
     c                   if        b_init = *off
     c                   eval      w_flin = '01' + c_fsep +
     c                                      %char(ra8ean) + c_fsep +
     c                                      'EAN' + c_fsep
      *
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1
      *
      * - Mottakers-id eller behandlingstype / kvalifikator
      *
     c                   if        rkeanf <> *zero
6.17 c                   if        %len(%char(rkeanf)) = 9
6.17 c                   eval      w_flin = %trim(w_flin) +
6.17 c                                      %char(rkeanf) + c_fsep +
6.17 c                                      'ORG' + c_fsep
6.17 c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %char(rkeanf) + c_fsep +
     c                                      'EAN' + c_fsep
6.17 c                   endif
     c                   else
6.17 c                   if        %len(%char(rkeank)) = 9
6.17 c                   eval      w_flin = %trim(w_flin) +
6.17 c                                      %char(rkeank) + c_fsep +
6.17 c                                      'ORG' + c_fsep
6.17 c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %char(rkeank) + c_fsep +
     c                                      'EAN' + c_fsep
6.17 c                   endif
     c                   endif
      *
      * - Meldingstype
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      'LOGINV' + c_fsep
      *
     c                   time                    w_mdat_iso
     c     *iso0         move      w_mdat_iso    w_mdat_alf
     c                   time                    w_mtim
     c                   move      w_mtim        w_mtim_alf
      *
      * - Meldingstidspunkt / versjon / testindikator
      *
     c                   if        fmttes = 1
     c                   eval      w_teid = '1'
     c                   else
     c                   eval      w_teid = *blank
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      w_mdat_alf +
     c                                      %subst(w_mtim_alf:1:4) +
     c                                      c_fsep + c_veid + c_fsep +
     c                                      %trim(w_teid) + c_fsep
      *
     c                   eval      w_antf = *zero
     c                   eval      w_antl = *zero
     c                   exsr      write_outp
     c                   eval      b_init = *on
     c                   endif
      *
     c                   eval      w_mref = *zero
      *
      * Faktura hodepost - recordtype 10
      *
     c                   eval      w_flin = *blank
      *
     c                   eval      votyl1_otyp = sootyp
     c     votyl1_key    chain     votyl1r
6.37  *
6.37  * leser kunde for å finne evt. byggm.faktura
6.37 c                   eval      rkunl1_kund = sokund
6.37 c     rkunl1_key    chain     rkunl1
      *
      * - Recordtype / faktura eller kreditnota
      *
     c                   if        vaokre = '-'
     c                   eval      w_flin = '10' + c_fsep + 'K' + c_fsep
     c                   eval      b_kred = *on
     c                   else
     c                   eval      w_flin = '10' + c_fsep + 'F' + c_fsep
     c                   eval      b_kred = *off
     c                   endif
      *
      * - Fakturanummer
      *
     c                   move      soaarr        w_aarr
     c                   eval      w_flin = %trim(w_flin) +
5.61 c                                      %char(sobinr) + '-' +
     c                                      %subst(w_aarr:3:2)  + c_fsep
      *
      * - Fakturadato / forfallsdato / leveringsdato
      *
     c                   if        sofkda <> *loval
     c     *iso0         move      sofkda        w_fakd_alf
     c                   else
     c                   eval      w_fakd_alf = *blank
     c                   endif
      *
     c                   if        soffda <> *loval
     c     *iso0         move      soffda        w_ford_alf
     c                   else
     c                   eval      w_ford_alf = *blank
     c                   endif
      *
     c                   if        soldat <> *loval
     c     *iso0         move      soldat        w_levd_alf
     c                   else
     c                   eval      w_levd_alf = *blank
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_fakd_alf) + c_fsep +
     c                                      %trim(w_ford_alf) + c_fsep
      *
     c                   if        b_saml = *on
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_levd_alf) + c_fsep
     c                   endif
      *
      * - Valuta
      *
     c                   if        sovalk <> *blank
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(sovalk) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + 'NOK' + c_fsep
     c                   endif
      *
6.2a c                   eval      w_epro = *blank
6.2a c                   if        sokpro <> 0
6.2a c                   eval      fkprl1_kund = sokund
6.2a c                   eval      fkprl1_kpro = sokpro
6.2a c     fkprl1_key    chain     fkprl1
6.2a c                   if        %found(fkprl1) and
6.2a c                             flepro <> *blank
6.2a c                   eval      w_epro = flepro
6.2a c                   endif
6.2a c                   endif
      *
      * - Kjøpers best.nr. (eller rekv.nr.)
      *
     c                   if        sokbnr <> *blank
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(sokbnr) + c_fsep
     c                   else
6.35 c                   if        w_epro <> *blank
     c                   eval      w_flin = %trim(w_flin) +
6.35 c                                      %trim(w_epro) + c_fsep
     c                   else
6.35 c                   if        sorekv <> *blank
6.2a c                   eval      w_flin = %trim(w_flin) +
6.35 c                                      %trim(sorekv) + c_fsep
6.2a c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
6.2a c                   endif
     c                   endif
     c                   endif
      *
      * - Selgers ordrenr.
      *
     c                   if        b_saml = *on
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %char(sonumm) + c_fsep
     c                   endif
      *
      * - Pakkseddelnr.
      *
     c*                  eval      w_flin = %trim(w_flin) + c_fsep
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %char(sonumm) + c_fsep
      *
      * - Ref. fakturanr. (kred.)
      *
     c                   if        b_kred = *on and
     c                             sokref <> *zero
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %char(sokref) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
      *
      * - Kundereferansenr.
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(sorekv) + c_fsep
6.15  *
6.15  * henter prosjektnavn
6.15  *
6.15 c                   if        solkun <> 0 and
6.15 c                             sokpro <> 0
6.15 c                   eval      w_pnav = *blank
6.15 c                   eval      fkprl1_kund = solkun
6.15 c                   eval      fkprl1_kpro = sokpro
6.15 c     fkprl1_key    chain     fkprl1
6.15 c                   if        %found(fkprl1)
6.15 c                   movel     flnavn        w_pnav
6.15 c                   endif
6.15 c                   endif
      *
      * - Prosjektnr.
      *
7.02 c                   if        u_702 = *off
      *
     c                   if        sokpro <> *zero
     c                   eval      w_flin = %trim(w_flin) +
6.15 c                                      %char(sokpro) + '  '    +
6.15 c                                      %trim(w_pnav) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
7.02  *
7.02 c                   else
7.02 c                   eval      w_flin = %trim(w_flin) +
7.02 c                                      %trim(w_epro) + c_fsep
7.02 c                   endif
      *
      * - KID-nr. (ref. eller fullt KID)
      *
5.62 c                   move      w_kidr        w_kidx
6.2g c*                  if        w_kidd <> *blank
6.2g c                   if        d_kidk = 1
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_kidd) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) +
5.62 c                                      %trim(w_kidx) + c_fsep
     c                   endif
      *
      * - Kontraktsnr.
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep
      *
      * - Samlefakturaind.
      *
     c                   if        b_saml = *on
     c                   eval      w_flin = %trim(w_flin) + 'S' + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
      *
      * - Leveringsbet. (tekst/kode/sted)
      *
     c                   if        solbet <> *zero
     c                   eval      ra18l1_rkod = solbet
     c     ra18l1_key    chain     ra18l1r
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(rartx1) + c_fsep +
     c                                      %trim(%subst(rartx1:1:3)) +
     c                                      c_fsep + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                                      'EXW' + c_fsep +
     c                                      'EXW' + c_fsep + c_fsep
     c                   endif
      *
      * - Transportmåte/transportør
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
      *
      * - Betalingsbet.tekst / rabatt / straffegebyr
      *
     c                   eval      w_ctxt = *blank
     c                   if        sobetb <> *zero
     c                   eval      w_cbda = *loval
     c                   eval      w_cfda = *loval
     c                   eval      w_ctx1 = *blank
     c                   eval      w_ctx2 = *blank
      *
     c                   call      'FÅ703R'
     c                   parm                    w_firm
     c                   parm                    sobetb
     c                   parm                    w_cbda
     c                   parm                    w_retk
     c                   parm                    w_cfda
     c                   parm                    w_ctx1
     c                   parm                    w_ctx2
      *
     c                   eval      w_ctxt = w_ctx1 + w_ctx2
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(%subst(w_ctxt:1:35)) +
     c                                      c_fsep + c_fsep + c_fsep
      *
      * - Fakturakopi
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep
      *
      * - Spesifikasjon
      *
6.37 c                   if        rkfcop = 2 or
6.37 c                             rkfcop = 3
6.37 c                   eval      w_flin = %trim(w_flin) + 'J' + c_fsep
6.37 c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
6.37 c                   endif
      *
      * - Samlekundenr.
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep
      *
      * - Generalnota
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep
      *
     c                   exsr      write_outp
     c                   eval      w_antf = w_antf + 1
     c                   eval      w_tfak = w_tfak + 1
6.12  *
6.12  *
6.12  * Exteral references  recordtype 13
6.12  *
6.12 c                   if        sokpro <> 0
6.12  *
6.12 c                   eval      fkprl1_kund = sokund
6.12 c                   eval      fkprl1_kpro = sokpro
6.12 c     fkprl1_key    chain     fkprl1
6.12 c                   if        %found(fkprl1) and
6.12 c                             flepro <> *blank
6.12 c                   eval      w_flin = *blank
6.12  *
6.12  * - Recordtype / ekstern kundes prosjekt nr.
6.12  *
6.12 c                   eval      w_flin = '13' + c_fsep
6.12  *
6.12 c                   eval      w_flin = %trim(w_flin) +
6.12 c                             %trim(flepro) + c_fsep
6.12  *
6.12 c                   exsr      write_outp
6.12  *
6.12 c                   endif
6.12 c                   endif
      *
      * Partsinformasjon - recordtype 20
      *
     c                   eval      w_flin = *blank
      *
      * > Kjøperinfo. (BY)
      *
      * - Recordtype / partskvalifikator
      *
     c                   eval      w_flin = '20' + c_fsep + 'BY' + c_fsep
8.01  *
8.01 c                   if        u_801 = *on
8.01  *
8.01  * hvis leveringskunde utfylt hent eank fra dennee
8.01  *
8.01 c                   eval      w_eank = 0
8.01 c                   eval      w_kunv = 0
8.01 c                   eval      w_navn = *blank
8.01 c                   eval      w_gate = *blank
8.01 c                   eval      w_adr2 = *blank
8.01 c                   eval      w_ponr = 0
8.01 c                   eval      w_sted = *blank
8.01 c                   if        solkun <> 0
8.01 c                   eval      rkunlr_kund = solkun
8.01 c     rkunlr_key    chain     rkunlr
8.01 c                   if        %found(rkunlr) and
8.01 c                             rreank <> 0
8.01 c                   eval      w_eank = rreank
8.01 c                   eval      w_kunv = rrkund
8.01 c                   eval      w_navn = rrnavn
8.01 c                   eval      w_gate = rrgate
8.01 c                   eval      w_adr2 = rradr2
8.01 c                   eval      w_ponr = rrponr
8.01 c                   eval      w_sted = rrsted
8.01 c                   endif
8.01 c                   endif
8.01  *
8.01 c                   endif
      *
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1
      *
8.01 c                   if        u_801 = *on
8.01  *
8.01 c                   if        w_eank = 0
8.01 c                   eval      w_eank = rkeank
8.01 c                   eval      w_kunv = rkkund
8.01 c                   eval      w_navn = rknavn
8.01 c                   eval      w_gate = rkgate
8.01 c                   eval      w_adr2 = rkadr2
8.01 c                   eval      w_ponr = rkponr
8.01 c                   eval      w_sted = rksted
8.01 c                   endif
8.01  *
8.01 c                   endif
      *
      * Partsinformasjon består for alle parter av :
      * - EAN-nr./foretaksnr/kundenr.
      * - Navn/adresse 1+2/postnr./poststed/landkode
      * - Kont.pers./bankkto.nr./e-mail
      * - Tlf.nr./faxnr./bet.kortnr./utløpsdato
      *
     c                   eval      w_ftex = *blank
     c                   if        rkftnr <> *zero
     c                   move      rkftnr        w_ftnr
     c                   eval      w_ftex = 'NO' + w_ftnr + 'MVA'
     c                   endif
      *
8.01 c     x'7D':' '     xlate     w_navn        w_navn
8.01 c     x'7D':' '     xlate     w_gate        w_gate
8.01 c     x'7D':' '     xlate     w_adr2        w_adr2
      *
6.13 c     x'7D':' '     xlate     rknavn        rknavn
6.13 c     x'7D':' '     xlate     rkgate        rkgate
6.13 c     x'7D':' '     xlate     rkadr2        rkadr2
      *
8.01 c                   if        u_801 = *on
8.01 c                   eval      w_flin = %trim(w_flin) +
8.01 c                             %char(w_eank) + c_fsep +
8.01 c                             %trim(w_ftex) + c_fsep +
8.01 c                                             c_fsep +
8.01 c                             %char(w_kunv) + c_fsep +
8.01 c                             %trim(w_navn) + c_fsep +
8.01 c                             %trim(w_gate) + c_fsep +
8.01 c                             %trim(w_adr2) + c_fsep
8.01 c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                             %char(rkeank) + c_fsep +
     c                             %trim(w_ftex) + c_fsep +
     c                                             c_fsep +
     c                             %char(sokund) + c_fsep +
     c                             %trim(rknavn) + c_fsep +
     c                             %trim(rkgate) + c_fsep +
     c                             %trim(rkadr2) + c_fsep
8.01 c                   endif
      *
     c                   eval      w_knav = rknavn
     c                   eval      w_iean = rkeanf
6.24 c                   if        rkfaku <> 0
6.24 c                   eval      w_faku = rkfaku
6.24 c                   else
6.24 c                   eval      w_faku = rkkund
6.24 c                   endif
6.24 c                   eval      w_kund = rkkund
      *
8.01 c                   if        u_801 = *on
8.01 c                   if        w_ponr <> *zero
8.01 c                   move      w_ponr        w_ponx
8.01 c                   eval      w_flin = %trim(w_flin) +
8.01 c                             %trim(w_ponx) + c_fsep
8.01 c                   else
8.01 c                   eval      w_flin = %trim(w_flin) +
8.01 c                             %trim(%subst(w_sted:1:4)) + c_fsep
8.01 c                   endif
8.01 c                   else
     c                   if        rkponr <> *zero
6.13 c                   move      rkponr        w_ponx
     c                   eval      w_flin = %trim(w_flin) +
6.13 c                             %trim(w_ponx) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(%subst(rksted:1:4)) + c_fsep
     c                   endif
8.01 c                   endif
      *
8.01 c                   if        u_801 = *on
8.01 c                   eval      w_flin = %trim(w_flin) +
8.01 c                             %trim(%subst(w_sted:5)) + c_fsep
8.01 c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(%subst(rksted:5)) + c_fsep
8.01 c                   endif
      *
     c                   if        rkland <> *blank
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(rkland) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
      *
6.2f c                   if        sodref = *blank
6.2f c                   eval      sodref = 'NA'
6.2f c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(sodref) + c_fsep +
6.30 c**                           %char(rkbgir) + c_fsep + c_fsep +
6.30 c                             %char(rkbgir) + c_fsep +
     c                             %trim(rkemal) + c_fsep +
     c                             %trim(rktlfn) + c_fsep +
     c                             %trim(rktlfx) + c_fsep +
6.38 c                                             c_fsep +
6.38 c                                             c_fsep +
6.38 c                             %trim(aaweba) + c_fsep +
6.38 c                                             c_fsep +
6.38 c                             %trim(aaiban) + c_fsep +
6.38 c                             %trim(aaswif) + c_fsep +
6.38 c                                             c_fsep
      *
     c                   exsr      write_outp
     c                   eval      w_flin = *blank
      *
      * > Selger (SU)
      *
      * - Recordtype / partskvalifikator
      *
     c                   eval      w_flin = '20' + c_fsep + 'SU' + c_fsep
      *
     c                   eval      w_ftex = *blank
     c                   if        aafftn <> *zero
     c                   move      aafftn        w_ftnr
     c                   eval      w_ftex = 'NO' + w_ftnr + 'MVA'
     c                   endif
      *
8.02  *  Switchstyrt - sjekker om bankkonto skal overstyres
8.02  *
8.02       if u_802 = *on;
8.06         exsr sjk_fact;
8.06         if b_fact = *off;
8.01          exec sql
8.01            select rkfbkk into :w_fbkk
8.01              from ra34st
8.01               where rkffir = :w_firm;
8.01          aafbgi = w_fbkk;
8.02         aafbgi = w_fbkk;
8.06        endif;
8.02       endif;
8.02  *
8.03 c                   eval      w_ftlf = aaftlf
8.03 c                   eval      w_ffax  =  aaffax
8.03 c                   eval      w_fmai  =  aamail
8.03 c                   eval      w_fad1  =  aafad1
8.03 c                   eval      w_fad2  =  aafad2
8.03 c                   eval      w_fste  =  aafste
      *
8.03 c                   if        u_avde = *on
8.03 c                   eval      ra07l1_gkod = soavde
8.03 c     ra07l1_key    chain     ra07l1
8.03 c                   if        %found(ra07l1)
8.03 c                   eval      w_ftlf  =  ragtlf
8.03 c                   eval      w_ffax  =  ragfax
8.03 c                   eval      w_fad1  =  %trim(ragtxt)
8.03 c                   eval      w_fad2  =  %trim(ragadr)
8.03 c                   eval      w_fste  =  %char(ragpnr) + %trim(ragste)
8.03 c                   eval      w_fmai  =  %trim(ragmai)
8.03 c                   endif
8.03 c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                             %char(ra8ean) + c_fsep +
     c                             %trim(w_ftex) + c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                             %trim(aafnav) + c_fsep +
8.03 c**                           %trim(aafad1) + c_fsep +
8.03 c                             %trim(w_fad1) + c_fsep +
8.03 c**                           %trim(aafad2) + c_fsep
8.03 c                             %trim(w_fad2) + c_fsep
      *
     c                   eval      w_flin = %trim(w_flin) +
8.03 c                             %trim(%subst(w_fste:1:4)) + c_fsep +
8.03 c                             %trim(%subst(w_fste:5)) + c_fsep
8.03 c**                           %trim(%subst(aafste:1:4)) + c_fsep +
8.03 c**                           %trim(%subst(aafste:5)) + c_fsep
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep
7.04  *
7.04 c                   if        w_sbgi <> *zero
7.04 c                   eval      w_fbgi = w_sbgi
7.04 c                   else
7.04 c                   eval      w_fbgi = aafbgi
7.04 c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(sovref) + c_fsep +
7.04 c*                            %char(aafbgi) + c_fsep +
7.04 c                             %char(w_fbgi) + c_fsep +
8.03 c                             %trim(w_fmai) + c_fsep +
8.03 c                             %trim(w_ftlf) + c_fsep +
8.03 c                             %trim(w_ffax) + c_fsep +
8.03 c**                           %trim(aamail) + c_fsep +
8.03 c**                           %trim(aaftlf) + c_fsep +
8.03 c**                           %trim(aaffax) + c_fsep +
     c                                             c_fsep + c_fsep + c_fsep
      *
     c                   exsr      write_outp
      *
      * > Fakturamottager (IV)
      *
      * - Recordtype / partskvalifikator
      *
6.24 c                   if        w_faku <> *zero
      *
     c                   eval      w_flin = *blank
      *
6.24 c                   eval      rkunl1_kund = w_faku
6.24 c     rkunl1_key    chain     rkunl1
6.24 c                   if        %found(rkunl1)
      *
     c                   eval      w_flin = '20' + c_fsep + 'IV' + c_fsep
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                             %char(w_iean) + c_fsep
      *
     c                   eval      w_ftex = *blank
     c                   if        rkftnr <> *zero
     c                   move      rkftnr        w_ftnr
     c                   eval      w_ftex = 'NO' + w_ftnr + 'MVA'
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(w_ftex) + c_fsep +
     c                                             c_fsep +
     c                             %char(rkkund) + c_fsep +
     c                             %trim(rknavn) + c_fsep +
     c                             %trim(rkgate) + c_fsep +
     c                             %trim(rkadr2) + c_fsep
      *
     c                   if        rkponr <> *zero
6.13 c                   move      rkponr        w_ponx
     c                   eval      w_flin = %trim(w_flin) +
6.13 c                             %trim(w_ponx) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(%subst(rksted:1:4)) + c_fsep
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(%subst(rksted:5)) + c_fsep
      *
     c                   if        rkland <> *blank
     c                   eval      w_flin = %trim(w_flin) +
     c                             %trim(rkland) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep +
6.30 c**                           %char(rkbgir) + c_fsep + c_fsep +
6.30 c                             %char(rkbgir) + c_fsep +
     c                             %trim(rkemal) + c_fsep +
     c                             %trim(rktlfn) + c_fsep +
     c                             %trim(rktlfx) + c_fsep +
     c                                             c_fsep + c_fsep
      *
     c                   exsr      write_outp
      *
6.24  * henter riktig kundeinfo igjen
6.24 c                   eval      rkunl1_kund = w_kund
6.24 c     rkunl1_key    chain     rkunl1
      *
     c                   endif
      *
     c                   endif
      *
      * > Leveringssted (DP)
      *
     c                   if        sonavn <> *blank
     c                   eval      w_flin = *blank
      *
      * - Recordtype / partskvalifikator
      *
     c                   eval      w_flin = '20' + c_fsep + 'DP' + c_fsep
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                             c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                             %trim(sonavn) + c_fsep +
     c                             %trim(soadr1) + c_fsep +
     c                             %trim(soadr2) + c_fsep +
     c                                             c_fsep +
     c                             %trim(sosted) + c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                                             c_fsep +
     c                                             c_fsep + c_fsep + c_fsep
      *
     c                   exsr      write_outp
     c                   endif
      *
      * Oppdater rapportfelter
      *
     c                   eval      a1binr = sobinr
     c                   eval      a1numm = sonumm
     c                   move      sosuff        a1suff
     c                   eval      a1otyp = sootyp
     c     *dmy          move      sofkda        a1fkda
     c     *dmy          move      soffda        a1ffda
     c                   eval      a1kund = sokund
     c                   eval      a1navn = w_knav
     c                   eval      a2navn = sonavn
      *
     c                   eval      s_lbin = sobinr
      *
     c                   if        p_rapp = '1'
     c                   if        b_uskr = *off
     c                   exsr      skr_rapph
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av ordrehodelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_olin    begsr
      *
     c                   if        s_lnum = sonumm
     c                   if        s_lsuf = sosuff
     c                   leavesr
     c                   endif
     c                   endif
      *
6.20 c*                  if        sdvare = *blank and
6.20 c*                            sdltyp = *blank
6.20 c*                  leavesr
6.20 c*                  endif
      *
      *
      * Ordre hodepost (samlefaktura/flere ordre) - recordtype 35
      *
     c                   eval      w_flin = *blank
      *
     c                   eval      w_flin = '35' + c_fsep +
     c                             %char(sonumm) + c_fsep
      *
      * - Ordredato
      *
     c                   if        sobdat <> *loval
     c     *iso0         move      sobdat        w_odat_alf
     c                   else
     c                   eval      w_odat_alf = *blank
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_odat_alf) + c_fsep
      *
      * - Levering
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep
      *
      * - Deres ref
      *
6.2f c                   if        sodref = *blank
6.2f c                   eval      sodref = 'NA'
6.2f c                   endif
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(sodref) + c_fsep
      *
      * - Vår ref
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(sovref) + c_fsep
      *
      * - Leveringsdato
      *
     c                   if        soldat <> *loval
     c     *iso0         move      soldat        w_levd_alf
     c                   else
     c                   eval      w_levd_alf = *blank
     c                   endif
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_levd_alf) + c_fsep
      *
      * - Utsalgssted
      *
7.03 c                   if        soavde = 0
     c                   eval      w_flin = %trim(w_flin) + c_fsep
7.03 c                   else
7.03 c                   eval      ra07l1_gkod = soavde
7.03 c     ra07l1_key    chain     ra07l1
7.03 c                   eval      w_flin = %trim(w_flin) +
7.03 c                                      %trim(ragtxt) + c_fsep
7.03 c                   endif
      *
      * - Leveringsadr.1/2/3
      *
7.03 c                   eval      w_flin = %trim(w_flin) +
7.03 c                                      %trim(sonavn) + c_fsep
7.03 c                   eval      w_flin = %trim(w_flin) +
7.03 c                                      %trim(soadr1) + c_fsep
7.03 c                   eval      w_flin = %trim(w_flin) +
7.03 c                                      %trim(sosted) + c_fsep
7.03 c*                  eval      w_flin = %trim(w_flin) + c_fsep
7.03 c*                  eval      w_flin = %trim(w_flin) + c_fsep
7.03 c*                  eval      w_flin = %trim(w_flin) + c_fsep
6.21  *
6.21  * - Kjøpers best.nr. (eller rekvisisjonsnr)
6.21  *
6.35 c                   eval      w_epro = *blank
6.35 c                   if        sokpro <> 0
6.35 c                   eval      fkprl1_kund = sokund
6.35 c                   eval      fkprl1_kpro = sokpro
6.35 c     fkprl1_key    chain     fkprl1
6.35 c                   if        %found(fkprl1) and
6.35 c                             flepro <> *blank
6.35 c                   eval      w_epro = flepro
6.35 c                   endif
6.35 c                   endif
      *
6.21 c                   if        sokbnr <> *blank
6.21 c                   eval      w_flin = %trim(w_flin) +
6.21 c                                      %trim(sokbnr) + c_fsep
6.21 c                   else
6.3a c                   if        sorekv <> *blank
6.35 c                   eval      w_flin = %trim(w_flin) +
6.3a c                                      %trim(sorekv) + c_fsep
6.35 c                   else
6.3a c                   if        w_epro <> *blank
6.21 c                   eval      w_flin = %trim(w_flin) +
6.3a c                                      %trim(w_epro) + c_fsep
6.21 c                   else
6.21 c                   eval      w_flin = %trim(w_flin) + c_fsep
6.35 c                   endif
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21  * - Selger og Lager (kodene)
6.21  *
6.21 c                   if        soselg <> 0
6.21 c                   eval      w_flin = %trim(w_flin) +
6.21 c                                      %char(soselg) + c_fsep
6.21 c                   else
6.21 c                   eval      w_flin = %trim(w_flin) + c_fsep
6.21 c                   endif
6.21  *
6.21 c                   if        solage <> 0
6.21 c                   eval      w_flin = %trim(w_flin) +
6.21 c                                      %char(solage) + c_fsep
6.21 c                   else
6.21 c                   eval      w_flin = %trim(w_flin) + c_fsep
6.21 c                   endif
6.21  * Hent over eksternt prosjektnr
6.21  * dersom dette finnes
6.21  *
6.21 c                   eval      fkprl1_kund = sokund
6.21 c                   eval      fkprl1_kpro = sokpro
6.21 c     fkprl1_key    chain     fkprl1r
6.21 c                   if        %found
6.21 c                   if        flepro <> *blank
6.21 c                   eval      w_flin = %trim(w_flin) +
6.21 c                                      %trim(flepro) + c_fsep
6.21 c                   else
6.21 c                   eval      w_flin = %trim(w_flin) + c_fsep
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21  * -kundeprosjekt
6.21  *
6.21 c                   if        sokpro <> 0
6.21 c                   eval      w_flin = %trim(w_flin) +
6.21 c                                      %char(sokpro) + c_fsep
6.21 c                   else
6.21 c                   eval      w_flin = %trim(w_flin) + c_fsep
6.21 c                   endif
      *
     c                   exsr      write_outp
     c                   eval      s_lnum = sonumm
     c                   eval      s_lsuf = sosuff
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av fakturalinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_vlin    begsr
      *
      * Varelinjeinfo. - recordtype 40
      *
     c                   eval      w_flin = *blank
6.33  *
6.33  * - Hvis både varenr  og tekst er blank, så ikke behandle
6.33  *
6.33 c                   if        sdvare = *blank and
6.33 c                             sdtek1 = *blank and
6.33 c                             sdtek2 = *blank
6.33 c                   leavesr
6.33 c                   endif
      *
      * - Recordtype / linjenummer
      *
     c                   eval      w_flin = '40' + c_fsep +
     c                                      %char(sdline) + c_fsep
      *
      * - Hvis ikke vare funnet eller skaffevare (sdlvre = 1)-sjekk jvarpf
      *
     c                   eval      w_vare = sdvare
     c                   eval      w_nobb = 0
     c                   if        not %found(vvarl1) or
     c                             sdlvre = 1
     c                   eval      jvarl1_vare = sdvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
     c                   eval      w_vare  = jvvare
     c                   eval      w_nobb = jvnobb
     c                   endif
     c                   else
     c                   eval      w_vare = vvvare
     c                   eval      w_nobb = vvnobb
     c                   endif
6.39 c                   exsr      sjekk_logi
6.39 c                   if        w_lv_nobb <> 0
6.39 c                   eval      w_nobb = 0
6.39 c                   endif
      *
      * - EAN-nummer
      *
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.39 c                   call      'VE713R'
6.11 c                   parm                    w_firm
6.39 c                   parm                    w_lv_ldor
6.11 c                   parm                    sdvare
6.11 c                   parm                    sdenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1' and
6.11 c                             w_eann <> *zero
     c                   eval      w_flin = %trim(w_flin) +
6.11 c                                      %char(w_eann) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
      *
      * - NOBB-nr.
      *
     c                   if        w_nobb <> *zero
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %char(w_nobb) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
      *
      * - Leverandørs varenr (eget)
      *
     c                   if        w_nobb = *zero
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_vare) + c_fsep +
     c                                      'SA' + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
     c                   endif
      *
      * - Varebetegnelse
      *
6.31 c                   if        %trim(sdtek1) = *blanks
6.31 c                   eval      sdtek1 = '.'
6.31 c                   endif
6.31  *
6.13 c     x'7D':' '     xlate     sdtek1        sdtek1
6.13 c     x'7D':' '     xlate     sdtek2        sdtek2
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(sdtek1) + ' ' +
     c                                      %trim(sdtek2) + c_fsep
      *
      * - Fakturert kvantum / enhet
      *
     c                   eval(h)   w_antx = sdanta
     c                   eval      w_anta = %editc(w_antx : 'M')
     c                   eval      w_numf = w_anta
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep +
     c                                      %trim(sdenh1) + c_fsep
      *
      * - Levert kvantum / enhet
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep +
     c                                                      c_fsep
      *
      * - Netto varelinjebeløp
      *
     c                   eval      w_npri = sdsalg -
     c                                      sdrkr1 - sdrkr2 - sdrkro
     c                   eval      s_fnet = s_fnet + w_npri
      *
     c                   eval      w_ntpe = %editc(w_npri : 'M')
     c                   eval      w_numf = w_ntpe
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Brutto varelinjebeløp
      *
6.36 c                   if        soneto = 1
6.36 c                   eval      w_ntpf = %editc(w_npri : 'M')
6.37 c                   eval      w_bf_n = w_bf_n + w_npri
7.01 c                   eval      w_bf_nl = w_npri
6.36 c                   else
     c                   eval      w_ntpf = %editc(sdsalg : 'M')
6.37 c                   eval      w_bf_n = w_bf_n + sdsalg
7.01 c                   eval      w_bf_nl = sdsalg
6.36 c                   endif
     c                   eval      w_numf = w_ntpf
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Netto enhetspris
      *
6.22 c**                 eval      w_flin = %trim(w_flin) + c_fsep
6.22 c                   eval      w_npri = sdsalg -
6.22 c                                      sdrkr1 - sdrkr2 - sdrkro
6.22  *
6.22 c                   if        sdanta <> *zero
6.2c c                   eval(h)   w_enhp = w_npri / sdanta
6.22 c                   else
6.22 c                   eval      w_enhp = *zero
6.22 c                   endif
6.22 c*                  eval      w_ntpe = %editc(w_enhp : 'M')
6.22 c*                  eval      w_numf = w_ntpe
6.22 c*                  exsr      sjekk_minus
6.22 c*                  eval      w_flin = %trim(w_flin) +
6.22 c*                                     %trim(w_numf) + c_fsep
6.2c c                   eval      w_ntpe19 = %editc(w_enhp : 'L')
6.1c c                   eval      w_numf = w_ntpe19
6.1a c                   exsr      sjekk_minus
6.1a c                   eval      w_flin = %trim(w_flin) +
6.1a c                                      %trim(w_numf) + c_fsep
6.1b  *
6.1b  * - EHF - hvis kreditnota
6.1b  *
6.1b c                   if        vaokre = '-'  or
6.1b c                             soneto = 1
6.1b c                   eval      sdsalg = sdsalg -
6.1b c                                      sdrkr1 - sdrkr2 - sdrkro
6.1b c                   endif
      *
      * - Brutto enhetspris
      *
     c*                  eval      w_enhp = sdsapr
     c*                  if        sdanta <> *zero
     c*                  eval      w_enhp = sdsalg / sdanta
     c*                  else
     c*                  eval      w_enhp = *zero
     c*                  endif
6.1b  *
6.1b  * hvis Valgt Nettopris skal rabatt trekkes ut
6.1b  *
6.1b c                   if        vaokre = '-'  or
6.1b c                             soneto = 1
6.1b  * Beregne snittrabatt
6.1b  *
6.1b  * - Her skrives Brutto enhetspris  - nettopris
6.1b  *
6.1b c                   eval      w1rab1 = 100 - sdrab1
6.1b c                   eval      w1rab1 = w1rab1 * sdrab2
6.1b c                   eval      w1rab1 = w1rab1 / 100
6.1b c                   eval      w1rab1 = w1rab1 + sdrab1
6.27 c*                  eval(h)   w_enhp = sdsapr -
6.27 c*                                    (sdsapr * w1rab1 / 100)
6.27  * her blir bruttoenh.p lik netto.enh.pris
6.2c c                   eval      w_ntpe19 = %editc(w_enhp : 'L')
6.1c c                   eval      w_numf = w_ntpe19
6.1b c                   exsr      sjekk_minus
6.1b c                   eval      w_flin = %trim(w_flin) +
6.1b c                                      %trim(w_numf) + c_fsep
6.1b c                   else
      *
      * - Her skrives Brutto enhetspris  - vanlig
      *
6.28  * beregner bruttoenhetpris m/4 desimaler
6.28  *
6.28 c                   eval      w_enhp = sdsapr
6.28 c                   if        sdanta <> *zero
6.2c c                   eval(h)   w_enhp = sdsalg / sdanta
6.28 c                   endif
      *
6.28 c*                  eval      w_enhx = %editc(sdsapr : 'L')
6.2c c                   eval      w_ntpe19 = %editc(w_enhp : 'L')
6.28 c*                  eval      w_numf = w_enhx
6.2c c                   eval      w_numf = w_ntpe19
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
6.1b c                   endif
      *
      * - Prisenhet / enhetsmengde
      *
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(sdenh1) + c_fsep + c_fsep
6.23  *
6.23 c                   if        sdomva <> *blank
6.23 c                   eval      w_stat = *blank
6.23 c                   eval      w_mvak = sdomva
6.23  *
6.23 c                   call      'RS205R'
6.23 c                   parm                    w_stat
6.22 c                   parm                    w_mvak
6.23 c                   parm                    w_mvtx
6.23 c                   parm                    sofkda
6.23 c                   parm                    w_mvas
6.23 c                   parm                    w_mvat
6.23 c                   parm                    w_mvaf
6.23 c                   parm                    w_bpro
6.23  *
6.23 c                   if        w_mvak <> *blanks
6.23 c                   exsr      xarray
6.23 c                   endif
6.23 c                   endif
      *
      * - MVA-prosent
      *
6.23 c                   if        w_mvas <> *zero
6.23 c                   eval      w_mvap = %editc(w_mvas : 'M')
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_mvap) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) +
     c                                      '0' + c_fsep
     c                   endif
      *
6.23 c                   if        w_mvas <> *zero
     c                   eval      s_fmvg = s_fmvg + w_npri
6.37 c                   if        soneto = 1
6.37 c                   eval      w_bf_g = w_bf_g + w_npri
6.37 c                   else
7.01 c**                 eval      w_bf_g = w_bf_g + sdsalg
7.01 c                   eval      w_bf_g = w_bf_g + w_bf_nl
6.37 c                   endif
     c                   endif
6.37 c                   if        w_mvak = '3'
6.37 c                   if        soneto = 1
6.37 c                   eval      w_bf_m = w_bf_m +
6.37 c                                      ((w_npri * w_mvas) / 100)
6.37 c                   else
6.37 c                   eval      w_bf_m = w_bf_m +
7.01 c                                      ((w_bf_nl * w_mvas) / 100)
7.01 c**                                    ((sdsalg * w_mvas) / 100)
6.37 c                   endif
6.37 c                   endif
6.37 c                   if        w_mvak = 'H'
6.37 c                   if        soneto = 1
6.37 c                   eval      w_bf_m = w_bf_m +
6.37 c                                      ((w_npri * w_mvas) / 100)
6.37 c                   else
6.37 c                   eval      w_bf_m = w_bf_m +
7.01 c                                      ((w_bf_nl * w_mvas) / 100)
7.01 c**                                    ((sdsalg * w_mvas) / 100)
6.37 c                   endif
6.37 c                   endif
6.37 c                   if        w_mvak = '4'
6.37 c                   if        soneto = 1
6.37 c                   eval      w_bf_m = w_bf_m + w_npri
6.37 c                   else
7.01 c**                 eval      w_bf_m = w_bf_m + sdsalg
7.01 c                   eval      w_bf_m = w_bf_m + w_bf_nl
6.37 c                   endif
6.37 c                   endif
      *
      *
      *
      * - Div. felter (ikke obl. p.t.)
      *   > Kjøpers best.nr/linjenr i best.
      *   > Selgers ordrenr/linjenr i ordre
      *   > Pakkseddelnr./linjenr i ordre
      *   > Fakturanr./linjenr på fakt.
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
      *
      * - Varebeskrivelse 2+3
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
      *
      * - Enhets listepris + Listepris linje
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep + c_fsep
      *
     c                   exsr      write_outp
     c                   eval      b_skfl = *on
8.04  *
8.04  * Hvis tilbud/nettopris - legges det ut en linje 41 med teksten på denne
8.04 c                   if        u_prko = *on
8.04 c                             and sdrab1 = *zero
8.04 c                   eval      w_tek1 = *blank
8.04 c                   eval      vpkol1_pkod = sdkpri
8.04 c     vpkol1_key    chain     vpkol1r
8.04 c                   if        %found(vpkol1)
8.04 c                   eval      w_tek1 = '.                   ' +
8.04 c                                      '                    ' +
8.04 c                                      '          ' +
8.04 c                                      vaptxt +
8.04 c                                      '               '
8.04 c                   eval      w_flin = '41' + c_fsep +
8.04 c                                      %trim(w_tek1) + c_fsep
8.04 c                   exsr      write_outp
8.04 c                   endif
8.04 c                   endif
8.04  *
8.04  *
      *
      * Rabatt/linjeinfo. - recordtype 50
      *
     c                   if        sdrkr1 = *zero and
     c                             sdrkr2 = *zero and
     c                             sdrkro = *zero
     c                   leavesr
     c                   endif
6.1b  *
6.1b  * hvis nettopris eller kredtinota skal ikke rabatt skrives ut.
6.1b  *
6.1b c                   if        vaokre = '-'  or
6.1b c                             soneto = 1
6.1b c                   leavesr
6.1b c                   endif
      *
      * Tar vare på rabatt-linjer inntil evt. tekstlinjer på 41 er skrevet
     c                   eval      w_flinx1 = *blank
     c                   eval      w_flinx2 = *blank
     c                   eval      w_flinx3 = *blank
      *
      * Rabatt #1
      *
     c                   if        sdrkr1 <> *zero
     c                   eval      w_flin = *blank
      *
      * - Recordtype, fradrag, rabattkode
      *
6.31 c                   if        sdrkr1 > 0
     c                   eval      w_flin = '50' + c_fsep + '-' + c_fsep +
     c                                      'PAR' + c_fsep
6.31 c                   else
6.31 c                   eval      w_flin = '50' + c_fsep + '+' + c_fsep +
6.31 c                                      'PC' + c_fsep
6.31 c                   eval      sdrkr1 = sdrkr1 * -1
6.31 c                   eval      sdrab1 = sdrab1 * -1
6.31 c                   endif
      *
      * - Rabattbeløp
      *
     c                   eval      w_rkrx = %editc(sdrkr1 : 'M')
     c                   eval      w_numf = w_rkrx
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Rabattprosent
      *
     c                   eval      w_rabx = %editc(sdrab1 : 'M')
     c                   eval      w_numf = w_rabx
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
     c                   eval      w_flinx1 = w_flin
     c*                  exsr      write_outp
      *
     c                   eval      s_frab = s_frab + sdrkr1
     c                   endif
      *
      * Rabatt #2
      *
     c                   if        sdrkr2 <> *zero
     c                   eval      w_flin = *blank
      *
      * - Recordtype, fradrag, rabattkode
      *
6.31 c                   if        sdrkr2 > 0
     c                   eval      w_flin = '50' + c_fsep + '-' + c_fsep +
     c                                      'PAR' + c_fsep
6.31 c                   else
6.31 c                   eval      w_flin = '50' + c_fsep + '+' + c_fsep +
6.31 c                                      'PC' + c_fsep
6.31 c                   eval      sdrkr2 = sdrkr2 * -1
6.31 c                   eval      sdrab2 = sdrab2 * -1
6.31 c                   endif
      *
      * - Rabattbeløp
      *
     c                   eval      w_rkrx = %editc(sdrkr2 : 'M')
     c                   eval      w_numf = w_rkrx
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Rabattprosent
      *
     c                   eval      w_rabx = %editc(sdrab2 : 'M')
     c                   eval      w_numf = w_rabx
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
     c                   eval      w_flinx2 = w_flin
     c*                  exsr      write_outp
      *
     c                   eval      s_frab = s_frab + sdrkr2
     c                   endif
      *
      * Rabatt #3 (ordre)
      *
     c                   if        sdrkro <> *zero
     c                   eval      w_flin = *blank
      *
      * - Recordtype, fradrag, rabattkode
      *
6.31 c                   if        sdrkro > 0
     c                   eval      w_flin = '50' + c_fsep + '-' + c_fsep +
     c                                      'PAR' + c_fsep
6.31 c                   else
6.31 c                   eval      w_flin = '50' + c_fsep + '+' + c_fsep +
6.31 c                                      'PC' + c_fsep
6.31 c                   eval      sdrkro = sdrkro * -1
6.31 c                   endif
      *
      * - Rabattbeløp
      *
     c                   eval      w_rkrx = %editc(sdrkro : 'M')
     c                   eval      w_numf = w_rkrx
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Rabattprosent
      *
     c                   eval      w_rabg = sdsalg - sdrkr1 - sdrkr2
     c                   eval(h)   w_rabp = (sdrkro / w_rabg) * 100
6.31 c                   if        w_rabp < 0
6.31 c                   eval      w_rabp = w_rabp * -1
6.31 c                   endif
     c                   eval      w_rabx = %editc(w_rabp : 'M')
     c                   eval      w_numf = w_rabx
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
     c                   eval      w_flinx3 = w_flin
     c*                  exsr      write_outp
      *
     c                   eval      s_frab = s_frab + sdrkro
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for generering av fakturalinje/fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_ftxl    begsr
      *
      * Varelinje/fritekst - recordtype 41
      *
     c                   eval      w_flin = *blank
8.07  *
8.07  * Spes. Per Strand
8.07  *
8.07 c                   if        sdktxt = 1 or
8.07 c                             sdktxt = 5
8.07 c                   leavesr
8.07 c                   endif
6.1e  *
6.1e c                   eval      vltyl1_ltyp = sdltyp
6.1e c     vltyl1_key    chain     vltyl1r
6.1e c                   if        sdotyp = valo01 or
6.1e c                             sdotyp = valo02 or
6.1e c                             sdotyp = valo03 or
6.1e c                             sdotyp = valo04
6.1e c                   leavesr
6.1e c                   endif
6.1e  *
6.1e  * - Recordtype / fritekst
6.1e  *
6.1e c                   if        sdtek1 = *blank
6.1e c                   if        sdtek2 = *blank
6.1e c                   leavesr
6.1e c                   endif
6.1e c                   endif
      *
      * - Recordtype / fritekst
      *
6.2b c                   if        s_ltyp = '10' or
6.2b c                             s_ltyp = '11'
6.2b c                   eval      w_flin = '11' + c_fsep +
6.2b c                                      %trim(sdtek1) +
6.2b c                                      %trim(sdtek2) + c_fsep
6.2b c                   exsr      write_outp
6.2b c                   leavesr
6.2b c                   endif
6.2b  *
6.2b c                   if        s_ltyp = '35' or
6.2b c                             s_ltyp = '36'
6.2b c                   eval      w_flin = '36' + c_fsep +
6.2b c                                      %trim(sdtek1) +
6.2b c                                      %trim(sdtek2) + c_fsep
6.2b c                   exsr      write_outp
6.2b c                   endif
      *
     c                   if        s_ltyp = '40' or
6.26 c                             s_ltyp = '41'
     c                   if        sdtek1 <> *blank
     c                   eval      w_flin = '41' + c_fsep +
     c                                      %trim(sdtek1) +
     c                                      %trim(sdtek2) + c_fsep
     c                   exsr      write_outp
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for gen. av avsluttende fakturadata
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_fslut   begsr
      *
      * Sumrecord - recordtype 80
      *
     c                   eval      w_flin = *blank
      *
      * - Recordtype
      *
     c                   eval      w_flin = '80' + c_fsep
      *
      * Kalk. MVA-grunnlag
      *
     c                   eval      w_tmvg = *zero
     c                   eval      w_tmvg = ((s_salp + s_salh) -
     c                                       (s_rk1p + s_rk2p + s_rkop +
     c                                        s_rk1h + s_rk2h + s_rkoh))
     c                   eval      w_tmva = *zero
6.23  *
6.23  * Beregner mva for de forskjellige satser
6.23  *
6.2c c                   eval(h)   mbe = (mgr - mor) * msa / 100
6.23 c                   xfoot     mbe           w_tmva
      *
      * - Nettobeløp (eks. MVA)
      *
     c                   eval      w_belo = s_fnet
      *
     c                   if        w_belo <> *zero
     c                   eval      w_dnto = %editc(w_belo : 'M')
     c                   else
     c                   eval      w_dnto = '0'
     c                   endif
      *
     c                   eval      w_numf = w_dnto
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
     c                   eval      s_fbru = s_fnet + w_tmva
6.37 c                   eval      w_fbim = s_fmvg + w_tmva
      *
      * - MVA-grunnlag
      *
     c                   if        s_fmvg <> *zero
     c                   eval      w_numf = %editc(s_fmvg : 'M')
     c                   else
     c                   eval      w_numf = '0'
     c                   endif
      *
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Totalt MVA beløp
      *
     c                   if        w_tmva <> *zero
     c                   eval      w_numf = %editc(w_tmva : 'M')
     c                   else
     c                   eval      w_numf = '0'
     c                   endif
      *
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Bruttobeløp / fakt.total
      *
     c                   eval      w_belo = s_fsum
      *
     c                   if        w_belo <> *zero
     c                   eval      w_dbto = %editc(w_belo : 'M')
     c                   else
     c                   eval      w_dbto = '0'
     c                   endif
      *
     c                   eval      w_numf = w_dbto
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Div. tot.felter (EDI)
      * - Sum netto varelinjer
      *
     c                   eval      w_belo = s_fnet
      *
     c                   if        w_belo <> *zero
     c                   eval      w_dnto = %editc(w_belo : 'M')
     c                   else
     c                   eval      w_dnto = '0'
     c                   endif
      *
     c                   eval      w_numf = w_dnto
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Sum tillegg (=0)
      *
     c                   eval      w_flin = %trim(w_flin) + '0' + c_fsep
      *
      * - Sum fradrag
      *
     c                   eval      w_belo = s_frab
      *
     c                   if        w_belo <> *zero
     c                   eval      w_drab = %editc(w_belo : 'M')
     c                   else
     c                   eval      w_drab = '0'
     c                   endif
      *
     c                   eval      w_numf = w_drab
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
      *
      * - Øreavrunding
      *
     c                   if        s_fsum <> s_fbru
     c                   eval      w_avru = s_fsum - s_fbru
     c                   eval      w_avrx = %editc(w_avru : 'M')
     c                   eval      w_numf = w_avrx
     c                   exsr      sjekk_minus
     c                   eval      w_flin = %trim(w_flin) +
     c                                      %trim(w_numf) + c_fsep
     c                   else
     c                   eval      w_flin = %trim(w_flin) + c_fsep
     c                   endif
      *
      * - Sum listeprislinjer
      *
     c                   eval      w_flin = %trim(w_flin) + c_fsep
6.37  * Byggmester faktura
6.37 c                   if        rkfcop = 2 or
6.37 c                             rkfcop = 3
6.37  *
6.37  * - Bruttosum uten rabatt
6.37  *
6.37 c                   if        w_bf_n <> *zero
6.37 c                   eval      w_numf = %editc(w_bf_n : 'M')
6.37 c                   else
6.37 c                   eval      w_numf = '0'
6.37 c                   endif
6.37  *
6.37 c                   exsr      sjekk_minus
6.37 c                   eval      w_flin = %trim(w_flin) +
6.37 c                                      %trim(w_numf) + c_fsep
6.37  *
6.37  * - Bruttosum uten rabatt -  mva-grunnlag
6.37  *
6.37 c                   if        w_bf_g <> *zero
6.37 c                   eval      w_numf = %editc(w_bf_g : 'M')
6.37 c                   else
6.37 c                   eval      w_numf = '0'
6.37 c                   endif
6.37  *
6.37 c                   exsr      sjekk_minus
6.37 c                   eval      w_flin = %trim(w_flin) +
6.37 c                                      %trim(w_numf) + c_fsep
6.37  *
6.37  * - Bruttosum uten rabatt - mva
6.37  *
6.37 c                   if        w_bf_m <> *zero
6.37 c                   eval      w_numf = %editc(w_bf_m : 'M')
6.37 c                   else
6.37 c                   eval      w_numf = '0'
6.37 c                   endif
6.37  *
6.37 c                   exsr      sjekk_minus
6.37 c                   eval      w_flin = %trim(w_flin) +
6.37 c                                      %trim(w_numf) + c_fsep
6.37  *
6.37  * - Bruttosum uten rabatt - total-sum
6.37  *
6.37 c                   eval      w_bf_b = w_bf_n + w_bf_m
6.37 c                   if        w_bf_b <> *zero
6.37 c                   eval      w_numf = %editc(w_bf_b : 'M')
6.37 c                   else
6.37 c                   eval      w_numf = '0'
6.37 c                   endif
6.37  *
6.37 c                   exsr      sjekk_minus
6.37 c                   eval      w_flin = %trim(w_flin) +
6.37 c                                      %trim(w_numf) + c_fsep
6.37  *
6.37 c                   else
6.37  * ikke byggmesterfaktura
6.37  *                  else
6.37  * - Faktura beløp inkl. MVA inkl. mva eks rabatt.
6.37  *
6.37 c                   if        w_fbim <> *zero
6.37 c                   eval      w_numf = %editc(w_fbim : 'M')
6.37 c                   else
6.37 c                   eval      w_numf = '0'
6.37 c                   endif
6.37  *
6.37 c                   exsr      sjekk_minus
6.37 c                   eval      w_flin = %trim(w_flin) +
6.37 c                                             c_fsep +
6.37 c                                             c_fsep +
6.37 c                                      %trim(w_numf) + c_fsep
6.37  *
6.37 c                   endif
      *
     c                   exsr      write_outp
6.23  *
6.23 c     1             do        9             m
6.23 c                   if        mko(m) <> ' '
6.23 c                   eval      w_flin = *blank
6.23 c                   eval      w_flin = '81' + c_fsep
6.23  *
6.23  * - MVA-grunnlag
6.23  *
6.23 c                   eval      w_numf = %editc(mgr(m) : 'M')
6.23 c                   exsr      sjekk_minus
6.23 c                   eval      w_flin = %trim(w_flin) +
6.23 c                                      %trim(w_numf) + c_fsep
6.23  *
6.23  * - MVA-prosent
6.23  *
6.2e c                   eval      w_mvap = %editc(msa(m) : 'L')
6.23 c                   eval      w_flin = %trim(w_flin) +
6.23 c                                      %trim(w_mvap) + c_fsep
6.23  *
6.23  * - MVA-beløp
6.23  *
6.34 c                   eval      w_numf = %editc(mbe(m) : 'L')
6.23 c                   exsr      sjekk_minus
6.23 c                   eval      w_flin = %trim(w_flin) +
6.23 c                                      %trim(w_numf) + c_fsep
6.23  *
6.23 c                   exsr      write_outp
6.23  *
6.23 c                   endif
6.23 c                   enddo
      *
     c                   if        p_rapp = '1'
     c                   exsr      skr_rappl
     c                   endif
      *
     c                   exsr      upd_uf_log
      *
     c                   eval      s_binr = *zero
     c                   eval      s_fbru = *zero
     c                   eval      s_fnet = *zero
     c                   eval      s_fmva = *zero
     c                   eval      s_fmvg = *zero
     c                   eval      s_frab = *zero
      *
6.19 c                   eval      s_mvp1 = *zero
6.19 c                   eval      s_mvg1 = *zero
6.19 c                   eval      s_mvb1 = *zero
6.19 c                   eval      s_mvp2 = *zero
6.19 c                   eval      s_mvg2 = *zero
6.19 c                   eval      s_mvb2 = *zero
6.19 c                   eval      s_mvp3 = *zero
6.19 c                   eval      s_mvg3 = *zero
6.23 c                   eval      mko = *blank
6.23 c                   eval      msa = *zero
6.23 c                   eval      mgr = *zero
6.23 c                   eval      mbe = *zero
6.23 c                   eval      mog = *zero
6.23 c                   eval      mor = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av fysisk record - m/konv. av bokstaver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     write_outp    begsr
      *
     c*    'Æ':X'46'     xlate     w_flin        w_flin
     c*    'æ':X'A0'     xlate     w_flin        w_flin
     c*    'Ø':X'77'     xlate     w_flin        w_flin
     c*    'ø':X'90'     xlate     w_flin        w_flin
     c*    'Å':X'2A'     xlate     w_flin        w_flin
     c*    'å':X'EF'     xlate     w_flin        w_flin
      *
8.05 c     x'1C':' '     xlate     w_flin        w_flin
8.05 c     x'25':' '     xlate     w_flin        w_flin
8.05 c     x'7D':' '     xlate     w_flin        w_flin
8.05 c     X'2B':' '     xlate     w_flin        w_flin
8.05 c     X'17':' '     xlate     w_flin        w_flin
8.05 c     x'05':' '     xlate     w_flin        w_flin
8.05 c     x'0D':' '     xlate     w_flin        w_flin
      *
     c                   if        p_faty = 24
     c                   eval      fnedln = w_flin
     c                   write     fnedpfr
     c                   endif
6.20  *
6.20 c                   if        p_faty = 28
6.20 c                   eval      fnedln = w_flin
6.20 c                   write     fnedpfr
6.20 c                   endif
      *
     c                   eval      w_antl = w_antl + 1
     c                   eval      s_ltyp = %subst(w_flin:1:2)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for mellomlagring av hodesummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     save_fsum     begsr
      *
     c                   eval      s_fsum = sofsum
     c                   eval      s_salp = sosalp
     c                   eval      s_salh = sosalh
     c                   eval      s_salf = sosalf
     c                   eval      s_rk1p = sork1p
     c                   eval      s_rk2p = sork2p
     c                   eval      s_rkop = sorkop
     c                   eval      s_rk1h = sork1h
     c                   eval      s_rk2h = sork2h
     c                   eval      s_rkoh = sorkoh
     c                   eval      s_rk1f = sork1f
     c                   eval      s_rk2f = sork2f
     c                   eval      s_rkof = sorkof
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av utgående fakturalogg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_uf_log    begsr
      *
     c                   eval      b_logf = *off
      *
      * Sjekk omkjøring og kjøredato
      *
     c                   if        p_omkj = *blank
     c                   if        fnukda <> *loval
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        p_omkj = '1'
     c                   if        fnukda = *loval
     c                   leavesr
     c                   endif
     c                   if        p_fnum <= 1 or
6.nu c                             p_tnum = 99999999
     c                   leavesr
     c                   endif
     c                   if        fnubin < p_fnum or
     c                             fnubin > p_tnum
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      b_logf = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdat. av utgående fakturalogg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_uf_log    begsr
      *
     c                   eval      fnuflu_aarr = p_aarr
     c                   eval      fnuflu_binr = s_binr
     c     fnuflu_key    chain     fnuflur
     c                   if        %found
     c*                  time                    fnukda
     c                   eval      fnukda = w_udat_iso
     c*                  time                    fnukti
     c                   eval      fnukti = w_utim_iso
     c                   eval      fnukws = l_wsid
     c                   eval      fnutel = fnutel + 1
     c                   time                    fnueda
     c                   time                    fnueti
     c                   eval      fnueus = l_user
     c                   update    fnuflur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for manipulering til ledende minustegn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_minus   begsr
      *
     c                   eval      w_posm = 0
     c                   eval      w_posm = %scan('-':w_numf)
      *
     c                   if        w_posm > 0
     c                   eval      w_numf = %subst(w_numf:1:w_posm - 1)
     c                   eval      w_numf = '-' + %trim(w_numf)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk om samlefaktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_saml      begsr
      *
     c                   eval      b_saml = *off
     c                   eval      w_anto = *zero
     c                   eval      w_fsum = *zero
     c                   eval      w_kidr = *zero
     c                   eval      w_kidd = *blank
      *
     c                   eval      sohelr_aarr = soaarr
     c                   eval      sohelr_binr = sobinr
      *
     c     sohelr_key    setll     sohelr
     c     sohelr_key    reade     sohelr
      *
     c                   dow       not %eof
      *
     c                   eval      w_anto = w_anto + 1
      *
     c                   eval      w_fsum = xxfsum
     c                   if        d_kidk <> 0
      *
     c                   if        d_kidk = 2
     c                   eval      w_kidr = xxkidr
     c                   else
6.2g c                   if        d_kidk = 1
     c                   eval      w_kidd = xxkidd
6.2g c                   else
6.2g c                   if        d_kidk = 3
6.2g c                   movel     xxkidr        w_kid3
6.2g c                   call      'AK710R'
6.2g c                   parm                    w_firm
6.2g c                   parm                    w_kid3
6.2g c                   parm                    w_kidd
6.2g c                   parm                    w_kid2
6.2g c                   movel     w_kid2        w_kidh
6.2g c                   move      w_kidh        w_kidr
     c                   else
     c                   eval      w_kidr = xxkidr
     c                   eval      w_kidd = xxkidd
     c                   endif
6.2g c                   endif
6.2g c                   endif
6.2g c                   endif
      *
     c     sohelr_key    reade     sohelr
     c                   enddo
      *
     c                   if        w_anto > 1
     c                   eval      b_saml = *on
     c                   endif
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for sjekk om faktura har linjer
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjk_fakt      begsr
6.32  *
6.32 c                   eval      b_alin = *off
6.32  *
6.32  * Sjekk om det finnes linjer
6.32  *
6.32 c                   eval      sodtlr_aarr = soaarr
6.32 c                   eval      sodtlr_binr = sobinr
6.32  *
6.32 c     sodtlr_key    setll     sodtlr
6.32 c     sodtlr_key    reade     sodtlr
6.32  *
6.32 c                   if        not %eof(sodtlr)
6.32 c                   eval      b_alin = *on
6.32 c                   endif
6.32  *
6.32 c                   endsr
6.32  *
8.06  *
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  * Subrutine for sjekk om ordretype eller kunde er utelatt fra factoring
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  *
8.06 c     sjk_fact      begsr
8.06  *
8.06  * Ordretype utelatt?
8.06  *
8.06           b_fact = *off;
8.06
8.06          exec sql
8.06            select rkfot1, rkfot2, rkfot3, rkfot4, rkfot5
8.06              into :w_fot1, :w_fot2, :w_fot3, :w_fot4, :w_fot5
8.06            from ra34st
8.06               where rkffir = :w_firm;
8.06
8.06          if w_fot1 = sootyp or
8.06             w_fot2 = sootyp or
8.06             w_fot3 = sootyp or
8.06             w_fot4 = sootyp or
8.06             w_fot5 = sootyp;
8.06           b_fact = *on;
8.06          endif;
8.06  *
8.06  * Kunde utelatt ?
8.06  *
8.06          exec sql
8.06            select rkfack
8.06              into :w_fack
8.06            from rkunpf
8.06               where rkfirm = :w_firm and
8.06                     rkkund = :sokund;
8.06
8.06          if w_fack = 1;
8.06           b_fact = *on;
8.06          endif;
8.06  *
8.06 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportheader v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rapph     begsr
      *
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1wsid = l_wsid
     c                   time                    a1time
5.63 c     *dmy          move      w_udat_iso    a1date
     c                   eval      a1user = l_user
      *
     c                   eval      a1ftyp = fmtbes
      *
     c                   if        p_omkj = '1'
     c                   eval      a1omkj = 'Omkjøring'
     c                   else
     c                   eval      a1omkj = *blank
     c                   endif
      *
     c                   write     a1hdr                                30
      *
     c                   eval      b_uskr = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportlinje v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rappl     begsr
      *
     c                   eval      a1fsum = s_fsum
      *
     c                   write     a1det                                30
      *
      * Er det overflow ?
      *
     c                   select
     c                   when      *in30 = *on
     c                   exsr      xovrfl
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for sjekker om det noen 50 - records som ikke er skrevet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sjk_50rec     begsr
      *
     c                   if        w_flinx1 <> *blank
     c                   eval      w_flin = w_flinx1
     c                   exsr      write_outp
     c                   endif
      *
     c                   if        w_flinx2 <> *blank
     c                   eval      w_flin = w_flinx2
     c                   exsr      write_outp
     c                   endif
      *
     c                   if        w_flinx3 <> *blank
     c                   eval      w_flin = w_flinx3
     c                   exsr      write_outp
     c                   endif
      *
     c                   eval      w_flinx1 = *blank
     c                   eval      w_flinx2 = *blank
     c                   eval      w_flinx3 = *Blank
      *
     c                   endsr
6.23  *
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for array-håndtering av mva
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     xarray        begsr
6.23  *
6.23 c     1             do        9             m
6.23 c                   if        mko(m) = w_mvak or
6.23 c                             mko(m) = ' '
6.23 c                   eval      mko(m) = w_mvak
6.23 c                   eval      msa(m) = w_mvas
6.23 c                   eval      mgr(m) = mgr(m) + w_npri
6.23  *
6.23 c*                  if        sorabp > 0
6.23 c*                  if        vvkord = 0
6.23 c*                  eval      mog(m) = mog(m) + w_npri
6.23 c*                  eval      w_orab = w_npri * sorabp / 100
6.23 c*                  eval      mor(m) = mor(m) + w_orab
6.23 c*                  endif
6.23 c*                  endif
6.23  *
6.23 c                   leave
6.23 c                   endif
6.23 c                   enddo
6.23  *
6.23 c                   endsr
6.39  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.39  * Subrutine for å finne om det er logistikkvare
6.39  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.39  *
6.39 c     sjekk_logi    begsr
6.39 c                   eval      w_lv_nobb = 0
5.39  * Kaller program for henting av leverandør og logistikk-info
5.39  *
5.39 c                   call      'VL710R'
5.39 c                   parm                    w_firm
5.39 c                   parm                    sdvare
5.39 c                   parm                    sdlage
6.39 c                   parm                    w_lv_vtyp
6.39 c                   parm                    w_lv_udat
6.39 c                   parm                    w_lv_ldor
6.39 c                   parm                    b_inlr
6.39 c                   parm                    w_lv_nobb
6.39 c                   parm                    w_lv_modn
6.39 c                   parm                    w_lv_lldo
6.39 c                   parm                    w_lv_llva
6.39 c                   endsr
      *
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_aarr
     c                   parm                    p_faty
6.nu c                   parm                    p_fnum
6.nu c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_feil
      *
     c                   eval      p_anta = *zero
     c                   eval      p_feil = *blank
      *
     c                   eval      w_antf = *zero
     c                   eval      w_antl = *zero
     c                   eval      w_tfak = *zero
      *
     c                   eval      w_firm = l_firm
      *
      * Key for les av kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
7.04  *
7.04 c     rkunlr_key    klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    rkunlr_kund
      *
      * Key for oppslag på kunde-register (EAN)
      *
     c     rkunl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl5_eank
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohelr_aarr
     c                   kfld                    sohelr_binr
      *
      * Key for oppslag på faktura-hode-register (les ant. ordre)
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
      *
      * Key for les av fakturalinjer
      *
     c     sodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtl1_aarr
     c                   kfld                    sodtl1_binr
     c                   kfld                    sodtl1_numm
     c                   kfld                    sodtl1_suff
      *
      * Key for firma
      *
     c     afir_key      klist
     c                   kfld                    w_firm
      *
      * Key for linjetyper
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *
      * Key for ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ofak-koderegister (statuskoder)
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for kode
      *
     c     ra18l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra18l1_rkod
      *
      * Key for statuskode 8
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av fakturatype
      *
     c     fmftl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmftl1_faty
6.12  *
6.12  * Key for les av kundeprosjekt
6.12  *
6.12 c     fkprl1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    fkprl1_kund
6.12 c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag av fakturalogg
      *
     c     fnufl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnufl1_aarr
     c                   kfld                    fnufl1_binr
      *
      * Key for oppslag av fakturalogg (type/dato)
      *
     c     fnufl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnufl2_ufat
     c                   kfld                    fnufl2_ukda
6.14  *
6.14  * Key for oppslag av fakturalogg (type/dato)
6.14  *
6.14 c     fnufl3_key    klist
6.14 c                   kfld                    w_firm
6.14 c                   kfld                    fnufl3_ufat
6.14 c                   kfld                    fnufl3_ukda
      *
      * Key for oppdat. av fakturalogg
      *
     c     fnuflu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnuflu_aarr
     c                   kfld                    fnuflu_binr
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
6.32  *
6.32  * Key for les av fakturalinjer
6.32  *
6.32 c     sodtlr_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    sodtlr_aarr
6.32 c                   kfld                    sodtlr_binr
7.03  *
7.03  * Key for oppslag på avdeling
7.03  *
7.03 c     ra07l1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    ra07l1_gkod
8.04  *
8.04  * Key for priskode-register
8.04  *
8.04 c     vpkol1_key    klist
8.04 c                   kfld                    w_firm
8.04 c                   kfld                    vpkol1_pkod
7.04  *
7.04  *  Sjekk om eget bankgironummer på factoring kunde
7.04  *
7.04 c                   eval      w_sbgi = *zero
7.04 c     afir_key      chain     raa9l1
7.04 c                   if        %found(raa9l1)
7.04 c                             and ra9knr <> 0
7.04 c                   eval      rkunlr_kund = ra9knr
7.04 c     rkunlr_key    chain     rkunlr
7.04 c                   if        %found(rkunlr)
7.04 c                   eval      w_sbgi = rrbgir
7.04 c                   endif
7.04 c                   endif
7.04  *
7.02  * Legge ut internt eller eksternt prosjektnummer
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'FO182_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
8.01  *
8.01  * Endring 8.01
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'LEVERINGSKUNDE_BETALER':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_801 = *on;
8.01   endif;
8.02  *
8.02  * Factoring via izzy
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'FACTORING_VIA_IIZY':
8.02                    co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_802 = *on;
8.02   endif;
8.03  *
8.03  * Sjekk om firma eller avdeling skal legges som SU
8.03  *
8.03   CallP CO402R(l_filg:w_firm:0:'AVDELING_ELLER_FIRMA_LOGINV':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_avde = *on;
8.03   endif;
8.04  *
8.04  * Legge ut priskode på EHF Faktura til Logiq
8.04  *
8.04   CallP CO402R(l_filg:w_firm:0:'PRISKODE_EHF_LOGIQ':
8.04                    co402_verdi1:co402_verdi2);
8.04   if co402_verdi1 = '1';
8.04     u_prko = *on;
8.04   endif;
      *
     c                   endsr
      *
