      /TITLE  ASSTAT Vedlikehold av innkjøpsstatistikk, endringsrutine
     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SI521R
      * Beskrivelse . . : Vedlikehold av innkjøpsstatistikk, endringsrutine
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260305 KOB  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsistl1    uf   e           k disk    rename(sistpfr:sistl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fsi521d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sifirm)
     d p_paar          s                   like(sipaar)
     d p_binr          s                   like(sibinr)
     d p_line          s                   like(siline)
     d p_bida          s                   like(sibida)
     d p_sumk          s                   like(sisumk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sistl1_paar     s                   like(sipaar)
     d sistl1_binr     s                   like(sibinr)
     d sistl1_line     s                   like(siline)
     d sistl1_bida     s                   like(sibida)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sifirm)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      sistl1_paar = p_paar
     c                   eval      sistl1_binr = p_binr
     c                   eval      sistl1_line = p_line
     c                   eval      sistl1_bida = p_bida
     c     sistl1_key    chain     sistl1                             60
     c                   if        *in60 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b1valn = sivaln
     c                   eval      b1binr = sibinr
     c                   eval      b1ornr = siornr
     c                   eval      b1otyp = siotyp
     c                   eval      b1wsid = siwsid
     c                   eval      b1vare = sivare
     c                   eval      b1tek1 = sitek1
     c                   eval      b1nobb = sinobb
     c                   eval      b1oenh = sioenh
     c                   eval      b1sumo = sisumo
     c                   eval      b1sumb = sisumb
     c                   eval      b1rab1 = sirab1
     c                   eval      b1rab2 = sirab2
     c                   eval      b1rabo = sirabo
      *
     c                   eval      b1sumk = sisumk
     c                   eval      b1kamp = sikamp
     c                   eval      b1kavd = sikavd
     c                   eval      b1selg = siselg
      *
     c                   if        sibida <> *loval
     c     *dmy          move      sibida        b1bida
     c                   endif
      *
     c                   if        siorda <> *loval
     c     *dmy          move      siorda        b1orda
     c                   endif
      *
     c     *hms          move      sitime        b1time
      *
     c                   eval      b1sumn = sisumb - sirab1 - sirab2 -
     c                                      sirabo
      *
     c                   if        sivaln <> 0
     c                   eval      rlevl1_levr = sivaln
     c     rlevl1_key    chain     rlevl1                             60
     c                   if        *in60 = *off
     c                   eval      b1lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Oppdater statistikkregister
      *
     c                   select
     c                   when      b1sumk <> sisumk or
     c                             b1kamp <> sikamp or
     c                             b1kavd <> sikavd or
     c                             b1selg <> siselg
     c                   eval      sisumk = b1sumk
     c                   eval      sikamp = b1kamp
     c                   eval      sikavd = b1kavd
     c                   eval      siselg = b1selg
     c                   update    sistl1r
     c                   endsl
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      p_sumk = b1sumk
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_paar
     c                   parm                    p_binr
     c                   parm                    p_line
     c                   parm                    p_bida
     c                   parm                    p_sumk
      *
      * Key for oppslag på statistikk-register
      *
     c     sistl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistl1_paar
     c                   kfld                    sistl1_binr
     c                   kfld                    sistl1_line
     c                   kfld                    sistl1_bida
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
