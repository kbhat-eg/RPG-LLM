     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD801R
      * Beskrivelse . . : Ordre-linje, summering av vekt ordre -linjer
      *
      * Spesialversjon. : Kalles fra FD100R, ordreinfo (F15)
      * Tilpasset for . : TMB (TM Bergersen - Dahlgruppen)
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251007 TSV  Nytt program
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_totkg         s              9  3
      *
      * Definering av variabler i nøkler
      *
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d vvenl1_vevare   s                   like(vevare)
     d vvenl1_veenhe   s                   like(veenhe)
      *
      * Definering av variabler
      *
     d w_firm          s                   like(fdfirm)
     d w_numm          s                   like(fdnumm)
     d w_suff          s                   like(fdsuff)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese ordrelinjer og summer opp
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   eval      p_totkg = 0
      *
     c                   dow       not %eof
      *
     c                   eval      vvenl1_vevare = fdvare
     c                   eval      vvenl1_veenhe = fdenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      p_totkg = p_totkg + ( fdanta * vevekt)
     c                   endif
      *
      * Hvis enhet er M2 må det kanskje omregnes fra STK/PAK/PAL/ARK ?
      *
     c                   if        vevekt = 0 and fdenh1 = 'M2'
      *
     c                   eval      vvenl1_veenhe = 'STK'
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   if        veomre <> 0
     c                   eval      p_totkg= p_totkg+(fdanta*(vevekt/veomre))
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
     c                   eval      vvenl1_veenhe = 'PAK'
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   if        veomre <> 0
     c                   eval      p_totkg= p_totkg+(fdanta*(vevekt/veomre))
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
     c                   eval      vvenl1_veenhe = 'PAL'
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   if        veomre <> 0
     c                   eval      p_totkg= p_totkg+(fdanta*(vevekt/veomre))
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
     c                   eval      vvenl1_veenhe = 'ARK'
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   if        veomre <> 0
     c                   eval      p_totkg= p_totkg+(fdanta*(vevekt/veomre))
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
     c                   endif
     c     ny_les        tag
      *
     c     fodtl1_key    reade     fodtl1
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Definisjoner og initiering
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vevare
     c                   kfld                    vvenl1_veenhe
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_totkg
      *
      * Henter firmakode fra parameterliste
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
