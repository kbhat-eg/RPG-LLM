     h datedit(*dmy)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BO122R
      * Beskrivelse . . : Utskrift av kontroll/avstem.-liste, utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.01  * R5.01 151102 AMD  Dersom vare er hentet fra LVR, skal varegruppe
      *                   hentes fra varelinje og ikke fra vareregistere
5.30  * R5.30 131004 HFL  I bonghode og linje er bongnr. 12A.
      *              AMD  + lagt inn utvalg av grupper også mot nytt
      *              AMD  butikk-system (eget kasse-register)
      * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  011010 BSA  Leser poster kun et år tilbake. Sjekker mot
6.11  *                   inntastet dato som til-dato. Gir mulighet for
6.11  *                   å lese inn poster i forskjellige perioder.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvvarl1    if   e           k disk
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fbwsdl1    if   e           k disk    rename(bwsdpfr:bwsdl1r)
     fboheld    if   e           k disk    rename(bohepfr:boheldr)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
     fbohelu    uf   e           k disk    rename(bohepfr:bohelur)
     fbpsolu    uf a e           k disk    rename(bpsopfr:bpsolur)
5.30 fbkasi1    if   e           k disk    rename(bkasst:bkasi1r)
      *
      * Definering av array's
      *
     d oty             s              2    dim(10)                               Ordretyper
     d wsd             s             10    dim(10)                               Skjerm/Kasse
      *
      * Definering av datastruktur
      *
     d                 ds
     d ldprec                       256
     d  ldotal                        4    overlay(ldprec)
     d  ldot01                        2    overlay(ldprec:5)
     d  ldot02                        2    overlay(ldprec:7)
     d  ldot03                        2    overlay(ldprec:9)
     d  ldot04                        2    overlay(ldprec:11)
     d  ldot05                        2    overlay(ldprec:13)
     d  ldot06                        2    overlay(ldprec:15)
     d  ldot07                        2    overlay(ldprec:17)
     d  ldot08                        2    overlay(ldprec:19)
     d  ldot09                        2    overlay(ldprec:21)
     d  ldot10                        2    overlay(ldprec:23)
     d  ldwsgr                        6    overlay(ldprec:25)
     d  ldwsal                       10    overlay(ldprec:31)
     d  ldws01                       10    overlay(ldprec:41)
     d  ldws02                       10    overlay(ldprec:51)
     d  ldws03                       10    overlay(ldprec:61)
     d  ldws04                       10    overlay(ldprec:71)
     d  ldws05                       10    overlay(ldprec:81)
     d  ldws06                       10    overlay(ldprec:91)
     d  ldws07                       10    overlay(ldprec:101)
     d  ldws08                       10    overlay(ldprec:111)
     d  ldws09                       10    overlay(ldprec:121)
     d  ldws10                       10    overlay(ldprec:131)
     d  ldavst                        1  0 overlay(ldprec:142)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:143)
     d  ldperi                        6  0 overlay(ldprec:153)
     d  lddekn                        1  0 overlay(ldprec:159)
     d  ldvdag                        2  0 overlay(ldprec:160)
     d  lddeta                        1  0 overlay(ldprec:162)
     d  ldtota                        1  0 overlay(ldprec:163)
     d  ldfrad                       10d   datfmt(*iso)
     d                                     overlay(ldprec:164)
     d  ldtild                       10d   datfmt(*iso)
     d                                     overlay(ldprec:174)
     d  ldaarr                        4  0 overlay(ldprec:184)
      *
      * Definering av local data area
      *
     d                uds
     d l_alt1                101    101
     d l_alt2                102    102
     d l_alt3                103    103
     d l_alt4                104    104
     d l_wsid                901    910
     d l_user                911    920
     d l_sfir                944    946  0
      *
      * Definering av parametere
      *
     d p_parm          s            256
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(bofirm)
     d boheld_aarr     s                   like(boaarr)
     d boheld_date     s                   like(bodate)
     d boheld_wsid     s                   like(bowsid)
5.30 d boheld_numm     s                   like(bobong)
     d bohelu_aarr     s                   like(boaarr)
     d bohelu_wsid     s                   like(bowsid)
5.30 d bohelu_numm     s                   like(bobong)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_wsid     s                   like(bdwsid)
5.30 d bodtl1_numm     s                   like(bdbong)
     d bodtl1_line     s                   like(bdline)
     d votyl1_otyp     s                   like(vaotyp)
     d bpsolu_oaar     s                   like(bsoaar)
     d bpsolu_owsd     s                   like(bsowsd)
     d bpsolu_onum     s                   like(bsonum)
     d bpsolu_olin     s                   like(bsolin)
     d vvarl1_vare     s                   like(vvvare)
     d bwsdl1_wsid     s                   like(bwwsid)
5.30 d bkasi1_kass     s                   like(bkkass)
      *
      * Definering av variable
      *
     d w_wsar          s            100
     d w_otar          s             20
5.30 d nummer          c                   '0123456789'
5.30 d n               s              2  0
5.30 d w_bong6         s              6
5.30 d w_syst          s              4
6.11 d w_aarr          s              4  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeider med klargjøring av varaiable
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn parametre i variablene
      *
     c                   eval      ldprec = p_parm
      *
      * Legger inn alternativene i forbindelse med listene
      *
     c                   move      ldtota        l_alt1
     c                   move      lddeta        l_alt2
     c                   move      lddekn        l_alt3
     c                   move      ldavst        l_alt4
      *
      * Dekningsbidragsliste skal alltid oppdateres ved avstemmingsliste
      *
     c                   if        ldavst = 1
     c                   if        ldtota = 1
     c                   move      ldavst        l_alt3
     c                   endif
     c                   endif
      *
      * Legg inn skjerm/kasse i tabell, dersom alle/gruppe ikke er valgt
      *
     c                   if        ldwsal = *blank
     c                   if        ldwsgr = *blank
     c                   move      ldws01        wsd(01)
     c                   move      ldws02        wsd(02)
     c                   move      ldws03        wsd(03)
     c                   move      ldws04        wsd(04)
     c                   move      ldws05        wsd(05)
     c                   move      ldws06        wsd(06)
     c                   move      ldws07        wsd(07)
     c                   move      ldws08        wsd(08)
     c                   move      ldws09        wsd(09)
     c                   move      ldws10        wsd(10)
     c                   endif
     c                   endif
      *
      * Legger inn skjerm/kasse i tabell, dersom alle ikke er valgt
      *
     c                   if        ldwsal <> *blank
     c                   if        ldwsal <> '*ALL'
     c                   movel     ldwsal        wsd(01)
     c                   endif
     c                   endif
      *
     c                   movea     wsd           w_wsar
      *
      * Legger inn ordretyper i tabell, dersom alle ikke er valgt
      *
     c                   if        ldotal = *blank
     c                   move      ldot01        oty(01)
     c                   move      ldot02        oty(02)
     c                   move      ldot03        oty(03)
     c                   move      ldot04        oty(04)
     c                   move      ldot05        oty(05)
     c                   move      ldot06        oty(06)
     c                   move      ldot07        oty(07)
     c                   move      ldot08        oty(08)
     c                   move      ldot09        oty(09)
     c                   move      ldot10        oty(10)
     c                   endif
      *
      * Legger inn ordretyper i tabell, dersom alle ikke er valgt
      *
     c                   if        ldotal <> *blank
     c                   if        ldotal <> '*ALL'
     c                   movel     ldotal        oty(01)
     c                   endif
     c                   endif
      *
     c                   movea     oty           w_otar
      *
      * Starter 5 dager før med utplukk
      *
     c                   if        ldfrad <> *loval
     c                   subdur    5:*d          ldfrad
     c                   endif
      *
      * Legger inn start f.o.m. i nøkklene
      *
     c                   eval      w_firm = l_sfir
6.11 c**                 eval      boheld_aarr = ldaarr
6.11 c                   eval      boheld_aarr = w_aarr
     c                   eval      boheld_date = ldfrad
     c                   eval      boheld_wsid = *blank
5.30 c                   eval      boheld_numm = *blank
      *
     c     boheld_key    setll     boheldr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn linjer fra  BUTIKK-BONG-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xstart        tag
      *
     c                   read      boheldr                                90
      *
      * Hopper ut dersom ikke flere records
      *
     c                   if        *in90 = *on
     c                   goto      xslutt
     c                   endif
      *
      * Hopper ut dersom dato er større enn dato til
      *
6.11 c**                 if        bodate > ldtild
6.11 c                   if        bodate > ldfdat
     c                   goto      xslutt
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tester om bongene skal taes med til utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hopper ut dersom feil firma
      *
     c     bofirm        cabne     l_sfir        xstart
      *
      * Sjekk   Skal denne skjerm/kasse være med til utskrift
      *
     c                   if        w_wsar <> *blank
     c     bowsid        lookup    wsd                                    96
     c                   if        *in96 = *off
     c                   goto      xstart
     c                   endif
     c                   endif
5.30  *
5.30  * Finn ut om dette er en bong fra
5.30  * gammelt eller nytt butikksystem
5.30  *
5.30 c                   exsr      srbong
5.30 c                   if        n = *zeros
5.30 c                   eval      w_syst = 'gmlt'
5.30 c                   else
5.30 c                   eval      w_syst = 'nytt'
5.30 c                   endif
      *
      * Sjekk   Skal denne skjerm/kasse være med til utskrift
      *         (gammelt system)
      *
5.30 c                   if        w_syst = 'gmlt'
     c                   if        ldwsal = *blank
     c                   if        ldwsgr <> *blank
     c                   eval      bwsdl1_wsid = bowsid
      *
     c     bwsdl1_key    chain     bwsdl1r
     c                   if        not %found
     c                   goto      xstart
     c                   endif
      *
     c                   if        bwwsgr <> ldwsgr
     c                   goto      xstart
     c                   endif
      *
     c                   endif
     c                   endif
5.30 c                   endif
5.30  *
5.30  * Sjekk   Skal denne skjerm/kasse være med til utskrift
5.30  *         (nytt system)
5.30  *
5.30 c                   if        w_syst = 'nytt'
5.30 c                   if        ldwsal = *blank
5.30 c                   if        ldwsgr <> *blank
5.30 c                   eval      bkasi1_kass = bowsid
5.30  *
5.30 c     bkasi1_key    chain     bkasi1r
5.30 c                   if        not %found
5.30 c                   goto      xstart
5.30 c                   endif
5.30  *
5.30 c                   if        bkgrup <> ldwsgr
5.30 c                   goto      xstart
5.30 c                   endif
5.30  *
5.30 c                   endif
5.30 c                   endif
5.30 c                   endif
      *
      * Sjekk   Skal denne ordretype være med til utskrift
      *
     c                   if        w_otar <> *blank
     c     bootyp        lookup    oty                                    96
     c                   if        *in96 = *off
     c                   goto      xstart
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tester i forbindelse med kodene for tatt med til utskrift !!
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Dersom bongen allerede er tatt med på avstem/detaljs-liste
      * ta neste bong (gå opp til xstart)
      *
     c                   if        bokavs = 1 or
     c                             bokavs = 2
     c                   if        bokdet = 1 or
     c                             bokdet = 2
     c                   goto      xstart
     c                   endif
     c                   endif
      *
      * Dersom avstemmingsliste kjøres, men bare detaljer
      * MÅ totaler være kjørt eller kjøres nå !!
      *
     c                   if        ldavst = 1
     c                   if        lddeta = 1
     c                   if        ldtota = 0
     c                   if        bokavs = 0
     c                   goto      xstart
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * Oppdaterer recorden med at den er plukket avst/detalj.
      *
     c                   if        ldavst = 1
     c                   eval      w_firm = bofirm
     c                   eval      bohelu_aarr = boaarr
     c                   eval      bohelu_wsid = bowsid
5.30 c                   eval      bohelu_numm = bobong
     c     bohelu_key    chain     bohelur                            91
     c                   if        *in91 = *off
      *
      * Oppdater med kode for av avstem.liste totaler er kjørt
      *
     c                   if        ldtota = 1
     c                   if        bokavs = *zero
     c                   eval      bokavs = 2
     c                   endif
     c                   endif
      *
      * Oppdater med kode for av avstem.liste detaljer er kjørt
      *
     c                   if        lddeta = 1
     c                   if        bokdet = *zero
     c                   eval      bokdet = 2
     c                   endif
     c                   endif
      *
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelur
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Rutine for innhenting av riktig utplukk
      * Henter inn hode og linje-opplysninger via subrutiner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn firma i nøkklene
      *
     c                   eval      w_firm = bofirm
      *
      * Legger inn årstall, Skjerm/kasse og bongnummer i nøkklene
      *
     c                   eval      bodtl1_aarr = boaarr
     c                   eval      bpsolu_oaar = boaarr
      *
     c                   eval      bodtl1_wsid = bowsid
     c                   eval      bpsolu_owsd = bowsid
      *
5.30 c                   eval      bodtl1_numm = bobong
5.30 c                   eval      bpsolu_onum = bobong
      *
      * Sjekker om denne bongen er kjørt ut på total-liste før
      *
     c                   eval      bsodiv = *blank
      *
     c                   if        bokavs = 1
     c                   move      'X'           bsodiv
     c                   endif
      *
      * Henter inn opplysninger fra registrene
      *
     c                   exsr      ohead
      *
      * Henter inn opplysninger fra BONG-LINJE-REGISTER
      * Dersom     a. detaljeliste skal skrives
      *            b. avstemmingsliste skal skrives
      *            c. Dekingsbidragsliste skal skrives
      *
     c                   if        lddeta = 1 or
     c                             ldavst = 1 or
     c                             lddekn = 1
     c                   exsr      linje
     c                   endif
      *
     c                   goto      xstart
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter bong-heading og legger ut til sorterings-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ohead         begsr
      *
     c     bpsolu_key    chain     bpsolur                            98
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r                            94
     c                   eval      bsoakk = *zero
     c                   if        *in94  = *off
     c                   eval      bsoakk = vaoakk
     c                   endif
      *
     c                   if        *in98 = *off
     c                   update    bpsolur
     c                   else
     c                   eval      bsorct = 'H'
     c                   eval      bsofir = w_firm
     c                   eval      bsoaar = bpsolu_oaar
     c                   eval      bsowsd = bpsolu_owsd
     c                   eval      bsonum = bpsolu_onum
     c                   eval      bsolin = *zero
     c                   eval      bsooty = bootyp
     c                   eval      bsoogr = *zero
     c                   eval      bsohgr = *zero
     c                   eval      bsougr = *zero
     c                   eval      bsovar = *blank
     c                   write     bpsolur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter bong-linjer og legger ut til sorterings-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje         begsr
      *
     c                   eval      bodtl1_line = *zero
     c     bodtl1_key    setll     bodtl1r
      *
     c     linje1        tag
     c                   eval      *in90 = *off
     c                   read      bodtl1r                                90
     c                   if        *in90 = *off
     c                   if        bdfirm = w_firm
     c                   if        bdaarr = bodtl1_aarr
     c                   if        bdwsid = bodtl1_wsid
5.30 c                   if        bdbong = bodtl1_numm
      *
     c                   z-add     bdline        bpsolu_olin
     c     bpsolu_key    chain     bpsolur                            98
      *
      * Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = bdvare
     c     vvarl1_key    chain     vvarl1                             94
     c                   eval      bsoogr = *zero
     c                   eval      bsohgr = *zero
     c                   eval      bsougr = *zero
     c                   if        *in94 = *off
     c                   eval      bsoogr = vvogrp
     c                   eval      bsohgr = vvhgrp
     c                   eval      bsougr = vvugrp
     c                   endif
      *
5.01  *
5.01  * Dersom vare er hentet fra LVR
5.01  * skal varegruppe fra bonglinje
5.01  * benyttes i stedet for varereg
5.01  *
5.01 c                   if        bdlvre = 1
5.01 c                   eval      bsoogr = bdogrp
5.01 c                   eval      bsohgr = bdhgrp
5.01 c                   eval      bsougr = bdugrp
5.01 c                   endif
5.01  *
     c                   if        *in98 = *off
     c                   update    bpsolur
     c                   else
     c                   eval      bsorct = 'V'
     c                   eval      bsofir = w_firm
     c                   eval      bsoaar = bpsolu_oaar
     c                   eval      bsowsd = bpsolu_owsd
     c                   eval      bsonum = bpsolu_onum
     c                   eval      bsolin = bpsolu_olin
     c                   eval      bsovar = bdvare
      *
      * Innbetaling/utbetaling skal ikke skrives på dekn.liste
      *
     c                   if        vaoakk = 5
     c                   eval      bsovar = *blank
     c                   endif
      *
     c                   write     bpsolur
     c                   endif
      *
     c                   goto      linje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.30  * Subrutine for å sjekke om 6-sifret bongnr.(gammelt butikksystem
5.30  * Sjekk om pos. 1-6 i bongnr. er nummerisk.
5.30  * Dersom N > 0 finnes ikke-nummeriske, dvs. nytt system.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
5.30 c     srbong        begsr
      *
5.30 c                   movel     bobong        w_bong6
5.30 c     nummer        check     w_bong6       n
      *
5.30 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_otar = *blank
     c                   eval      w_wsar = *blank
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for file : BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : SKJERM/KASSE-REGISTER
      *
     c     bwsdl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bwsdl1_wsid
      *
      * Key for file : BUTIKK-HODE-REGISTER
      *
     c     bohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelu_aarr
     c                   kfld                    bohelu_wsid
     c                   kfld                    bohelu_numm
      *
      * Key for file : BUTIKK-DATO-HODE-REGISTER
      *
     c     boheld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    boheld_aarr
     c                   kfld                    boheld_date
     c                   kfld                    boheld_wsid
     c                   kfld                    boheld_numm
      *
      * Key for file : BUTIKK-LINJE-REGISTER
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_numm
     c                   kfld                    bodtl1_line
      *
      * Key for file : BUTIKK-PLUKK-S-REGISTER
      *
     c     bpsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bpsolu_oaar
     c                   kfld                    bpsolu_owsd
     c                   kfld                    bpsolu_onum
     c                   kfld                    bpsolu_olin
5.30  *
5.30  * Key for lesing fra kasseregister (nytt system)
5.30  *
5.30 c     bkasi1_key    klist
5.30 c                   kfld                    w_firm
5.30 c                   kfld                    bkasi1_kass
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = l_sfir
      *
      * Hent opplysninger fra BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    chain     bstsl1r                            90
     c                   if        *in90 = *on
     c                   eval      baaavs = *blank
     c                   eval      baadet = *blank
     c                   endif
      *
6.11 c                   eval      w_aarr = *year - 1
      *
     c                   endsr
