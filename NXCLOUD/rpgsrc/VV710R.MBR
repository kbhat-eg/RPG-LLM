     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV710R
      * Beskrivelse . . : Vare, returnerer NOBBnr og lev.varenummer
      *
      *                   Programmet henter ut NOBBnr og lev.varenr. Henter fra
      *                   VA hvis den ikke finnes i EVR.
      *
      *                   Parametre inn: Varenummer
      *                             ut:  NOBBnr og lev.varenummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180612 bsa  Nytt program
6.31  *       161018 bof  utvidet med trelast-sortiment
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
      * Definering av parametre
      *
     d p_lvar          s                   like(vvlvar)
     d p_nobb          s                   like(vvnobb)
     d p_vare          s                   like(vvvare)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
     d w_pvar          s              8  0
     d w_vare          s              8  0
6.31 d w_lv_nobb       s                   like(vvnobb)
6.31 d w_lv_lvar       s                   like(vvlvar)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_avde                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Initierer parametre
      *
     c                   eval      p_nobb = *zeros
     c                   eval      p_lvar = *blank
      *
      * Finner neste ledige varenummer
      *
     c                   if        p_vare = *blank
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_nobb = 0
6.31 c/exec sql
6.31 c+ select vvlnob,
6.31 c+        vvllva
6.31 c+ into   :w_lv_nobb,
6.31 c+        :w_lv_lvar
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :vvvare and
6.31 c+        vvlldo = :vvldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_nobb <> 0
6.31 c                   eval      p_nobb = w_lv_nobb
6.31 c                   eval      p_lvar = w_lv_lvar
6.31 c                   else
     c                   eval      p_nobb = vvnobb
     c                   eval      p_lvar = vvlvar
6.31 c                   endif
     c                   goto      avslutt
     c                   endif
      *
      * Finnes ikke, forsøk i VA
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      p_nobb = jvnobb
     c                   eval      p_lvar = jvlvar
     c                   goto      avslutt
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_nobb
     c                   parm                    p_lvar
      *
      * Key for les på vareregister etter varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vareregister etter varenummer
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
