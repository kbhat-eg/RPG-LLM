     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM710R
      * Beskrivelse . . : Nettbutikk, eksport av vareinformasjon
      * Beskrivelse . . : Nettbutikk, eksport av vareinformasjon
      *
      * Spesialversjon. : Uttrekk av varer basert på master sortiment
      * Tilpasset for . : Mestergruppen, basert på NN710R
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110416 ASK  Nytt program
6.31  * 6.30  121018 bof  Utvidet med trelast-sortiment
6.32  * 6.30  300119 bof  Skal legge ut en linje pr. nobb ulikt nobbnummer
9.01  * 6.30  190619 ask  Kaller nytt program for GTIN - alfanumerisk
      *
8.01  * 8.xx  141021 bsa  Henter pris fra neste dag.
8.02  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fcsodi2    if   e           k disk    rename(csodst:csodi2r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrpfr)
     fvogrlr    if   e           k disk    rename(vogrpfr:vogrlrr)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrpfr)
     fvhgrlr    if   e           k disk    rename(vhgrpfr:vhgrlrr)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrpfr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
6.25 fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.25 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
6.13 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     frlevpf    if   e           k disk    rename(rlevpfr:rlevpfr)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
      *
     fnetvare   uf a e           k disk
     fnetvrapp  o  a e             disk    rename(netvrapp:netvrappr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgrp                931    933
5.70 d l_lage               1001   1002  0
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         16
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                 9    overlay(d_bgm: 5) inz('IMLITMIMP')
     d  d_bgmfill2                    2    overlay(d_bgm:14) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:16) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - varegruppe
      *
     d                 ds
     d d_igr                         60
     d  d_igrrcd                      3    overlay(d_igr: 1) inz('IGR')
     d  d_igrfill1                    1    overlay(d_igr: 4) inz(';')
     d  d_igrogr                      2  0 overlay(d_igr: 5)
     d  d_igrfill2                    1    overlay(d_igr: 7) inz(';')
     d  d_igrhgr                      2  0 overlay(d_igr: 8)
     d  d_igrfill3                    1    overlay(d_igr:10) inz(';')
     d  d_igrtex                     50    overlay(d_igr:11)
      *
      * Recordbeskrivelse - vare
      *
     d                 ds
6.25 d d_itm                        300
     d  d_itmrcd                      3    overlay(d_itm:  1) inz('ITM')
     d  d_itmfill1                    1    overlay(d_itm:  4) inz(';')
     d  d_itmnum                      8    overlay(d_itm:  5)
     d  d_itmfill2                    1    overlay(d_itm: 13) inz(';')
     d  d_itmtx1                     35    overlay(d_itm: 14)
     d  d_itmfill3                    1    overlay(d_itm: 49) inz(';')
     d  d_itmtx2                     35    overlay(d_itm: 50)
     d  d_itmfill4                    1    overlay(d_itm: 85) inz(';')
     d  d_itmogr                      2  0 overlay(d_itm: 86)
     d  d_itmfill5                    1    overlay(d_itm: 88) inz(';')
     d  d_itmhgr                      2  0 overlay(d_itm: 89)
     d  d_itmfill6                    1    overlay(d_itm: 91) inz(';')
     d  d_itmugr                      3  0 overlay(d_itm: 92)
     d  d_itmfill7                    1    overlay(d_itm: 95) inz(';')
     d  d_itmgtx                     50    overlay(d_itm: 96)
     d  d_itmfill8                    1    overlay(d_itm:146) inz(';')
     d  d_itmnob                      8  0 overlay(d_itm:147)
     d  d_itmfill9                    1    overlay(d_itm:155) inz(';')
     d  d_itmmod                      8  0 overlay(d_itm:156)
     d  d_itmfill10                   1    overlay(d_itm:164) inz(';')
     d  d_itmdel                      6  0 overlay(d_itm:165)
     d  d_itmfill11                   1    overlay(d_itm:171) inz(';')
     d  d_itmpro                     35    overlay(d_itm:172)
     d  d_itmfill12                   1    overlay(d_itm:207) inz(';')
     d  d_itmpnu                     15    overlay(d_itm:208)
     d  d_itmfill13                   1    overlay(d_itm:223) inz(';')
9.01 d  d_itmean                     14    overlay(d_itm:224)
6.12 d  d_itmfill14                   1    overlay(d_itm:238) inz(';')
6.12 d  d_itmunt                      3    overlay(d_itm:239)
6.12 d  d_itmfill15                   1    overlay(d_itm:242) inz(';')
6.12 d  d_itmprc                     10    overlay(d_itm:243)
6.12 d  d_itmfill16                   1    overlay(d_itm:253) inz(';')
6.12 d  d_itmrgr                      7  0 overlay(d_itm:254)
6.12 d  d_itmfill17                   1    overlay(d_itm:261) inz(';')
6.12 d  d_itmrgt                     35    overlay(d_itm:262)
6.12 d  d_itmfill18                   1    overlay(d_itm:297) inz(';')
6.12 d  d_itmact                      1    overlay(d_itm:298)
6.25 d  d_itmfill19                   1    overlay(d_itm:299) inz(';')
6.25 d  d_itmtyp                      1    overlay(d_itm:300)
      *
      * Recordbeskrivelse - enheter
      *
     d                 ds
     d d_uom                        115
     d  d_uomrcd                      3    overlay(d_uom:  1) inz('UOM')
     d  d_uomfill1                    1    overlay(d_uom:  4) inz(';')
     d  d_uomnum                      8    overlay(d_uom:  5)
     d  d_uomfill2                    1    overlay(d_uom: 13) inz(';')
     d  d_uomunt                      3    overlay(d_uom: 14)
     d  d_uomfill3                    1    overlay(d_uom: 17) inz(';')
     d  d_uomupc                     14  0 overlay(d_uom: 18)
     d  d_uomfill4                    1    overlay(d_uom: 32) inz(';')
     d  d_uomcal                     13    overlay(d_uom: 33)
     d  d_uomfill5                    1    overlay(d_uom: 46) inz(';')
     d  d_uomhig                     13    overlay(d_uom: 47)
     d  d_uomfill6                    1    overlay(d_uom: 60) inz(';')
     d  d_uomlen                     13    overlay(d_uom: 61)
     d  d_uomfill7                    1    overlay(d_uom: 74) inz(';')
     d  d_uomwid                     13    overlay(d_uom: 75)
     d  d_uomfill8                    1    overlay(d_uom: 88) inz(';')
     d  d_uomvol                     13    overlay(d_uom: 89)
     d  d_uomfill9                    1    overlay(d_uom:102) inz(';')
     d  d_uomwei                     13    overlay(d_uom:103)
      *
      * Recordbeskrivelse - priser
      *
     d                 ds
     d d_prc                         74
     d  d_prcrcd                      3    overlay(d_prc: 1) inz('PRC')
     d  d_prcfill1                    1    overlay(d_prc: 4) inz(';')
     d  d_prcnum                      8    overlay(d_prc: 5)
     d  d_prcfill2                    1    overlay(d_prc:13) inz(';')
     d  d_prcunt                      3    overlay(d_prc:14)
     d  d_prcfill3                    1    overlay(d_prc:17) inz(';')
     d  d_prctyp                      1  0 overlay(d_prc:18)
     d  d_prcfill4                    1    overlay(d_prc:19) inz(';')
     d  d_prcltb                     35    overlay(d_prc:20)
     d  d_prcfill5                    1    overlay(d_prc:55) inz(';')
     d  d_prcpri                     10    overlay(d_prc:56)
     d  d_prcfill6                    1    overlay(d_prc:66) inz(';')
     d  d_prcdat                      8    overlay(d_prc:67)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d csodi2_dkod     s                   like(csdkod)
     d vvarl1_vare     s                   like(vvvare)
     d vhgrlr_hogr     s                   like(vghogr)
     d vhgrlr_hhgr     s                   like(vghhgr)
      *
     d vogrlr_oogr     s                   like(vgoogr)
      *
     d vhgrl1_hogr     s                   like(vghogr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vpkol1_pkod     s                   like(vapkod)
6.25 d vvenl2_vare     s                   like(vevare)
6.25 d vvenl1_vare     s                   like(vevare)
6.25 d vvenl1_enhe     s                   like(veenhe)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
6.13 d vlagl1_vare     s                   like(vlvare)
     d rlevpf_levr     s                   like(rllevr)
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
      *
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d jlevl3_enum     s                   like(jlenum)
     d jvpkl2_vare     s                   like(jwvare)
     d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
      *
      * Definering av variable
      *
6.25 d b_vtyp          s              1    inz(*off)
6.25 d b_udat          s              1    inz(*off)
6.25 d b_behl          s              1    inz(*off)
     d b_pris          s              1    inz(*off)
6.25 d b_enhe          s              1    inz(*off)
6.32 d b_logi          s              1    inz(*off)
6.32 d b_eof           s              1    inz(*off)
      *
     d w_tmst          s               z
     d w_dato          s               d   datfmt(*iso)
     d w_tell          s             12  0
     d w_ponr          s              4
     d w_omre          s             12
6.25 d w_enhe          s              3
     d w_hoyd          s             12
     d w_leng          s             12
     d w_bred          s             12
     d w_volu          s             12
     d w_vekt          s             12
     d w_sapr          s              9
      *
     d w_firm          s                   like(vvfirm)
5.70 d w_lage          s                   like(vllage)
6.20 d w_vtyp          s                   like(vlvtyp)
6.20 d w_ldor          s                   like(vpldor)
      *
8.02 d w_prgr          s              2
6.11 d w_east          s              1
9.01 d w_eann          s             14
6.11 d w_vnum          s             15
6.32 d w_lv_vare       s                   like(vvvare)
6.31 d w_lv_modn       s                   like(vvnmod)
6.31 d w_lv_nobb       s                   like(vvnobb)
6.31 d w_lv_lvar       s                   like(vvlvar)
6.31 d w_lv_nlev       s                   like(vvnlev)
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgrp
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
6.25 c                   if        aollag <> *blank
6.25 c                   move      aollag        w_lage
6.25 c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netvarer
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netvarer
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netvarer
      *
      * Varegruppe
      *
     c                   exsr      vgrp
      *
      * Vare m/enheter og priser
      *
     c                   exsr      varer_lag
     c                   exsr      varer_va
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netvarer
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av varegruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vgrp          begsr
      *
     c     vogrl1_key    setll     vogrl1
     c     vogrl1_key    reade     vogrl1
     c                   dow       %eof = *off
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_igrogr = vgoogr
     c                   eval      d_igrhgr = 0
     c                   eval      d_igrtex = vgotxt
      *
     c                   eval      data = d_igr
     c                   write     netvarer
      *
     c                   eval      vhgrl1_hogr = vgoogr
     c     vhgrl1_key    setll     vhgrl1
     c     vhgrl1_key    reade     vhgrl1
     c                   dow       %eof = *off
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_igrhgr = vghhgr
     c                   eval      d_igrtex = vghtxt
      *
     c                   eval      data = d_igr
     c                   write     netvarer
      *
     c     vhgrl1_key    reade     vhgrl1
     c                   enddo
      *
     c     vogrl1_key    reade     vogrl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare m/enheter og priser på lagervarer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     varer_lag     begsr
      *
      * Leser alle varer i lager sortiment
      *
     c                   eval      w_vnum = *blank
     c                   eval      csodi2_dkod = 'Grunnsortiment'
     c     csodi2_key    setll     csodi2
     c     csodi2_key    reade     csodi2
     c                   dow       %eof(csodi2) = *off
      *
     c                   if        csdvnu = w_vnum
     c                   goto      neste_lvare
     c                   else
     c                   eval      w_vnum = csdvnu
     c                   endif
      *
     c                   if        csdvnu = '11302650'
     c                   eval      csdvnu = csdvnu
     c                   endif
      *
     c                   eval      vvarl1_vare = csdvnu
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found(vvarl1)
     c                   goto      neste_lvare
     c                   endif
     c
      *
5.60 c     ';':' '       xlate     vvtek1        vvtek1
5.60 c     ';':' '       xlate     vvtek2        vvtek2
     c                   eval      d_itmnum = vvvare
     c                   eval      d_itmtx1 = vvtek1
     c                   eval      d_itmtx2 = vvtek2
     c                   eval      d_itmogr = vvogrp
     c                   eval      d_itmhgr = vvhgrp
     c                   eval      d_itmugr = vvugrp
     c                   eval      d_itmgtx = *blank
     c                   eval      w_ldor = vvldor
     c                   exsr      sjekk_logi
6.31 c                   eval      d_itmnob = vvnobb
6.31 c                   eval      d_itmmod = vvnmod
6.31 c                   eval      d_itmdel = vvnlev
6.31 c                   eval      d_itmpnu = vvlvar
     c                   eval      d_itmpro = *blank
9.01 c                   eval      d_itmean = *blank
     c                   eval      d_itmunt = vvenhe
     c                   eval      d_itmprc = *blank
     c                   eval      d_itmrgr = 0
     c                   eval      d_itmrgt = *blank
     c                   eval      d_itmact = 'Y'
6.25 c                   eval      d_itmtyp = w_vtyp
      *
      * Varegruppetekst
      *
     c                   if        vvogrp = 0 and vvhgrp = 0 and
     c                             vvugrp = 0
     c                   goto      neste_lvare
     c                   endif
     c                   eval      vogrlr_oogr = vvogrp
     c     vogrlr_key    chain     vogrlrr
     c                   if        not %found
     c                   eval      SrcDta = 'Overgruppe ' +
     c                             %trim(%char(vvogrp)) +
     c                             '  *  *  finnes ikke. ' +
     c                             'Vare ' + %trim(vvvare) + ' ' +
     c                             %trim(vvtek1) + ' ble utelatt.'
     c                   write     netvrappr
     c                   eval      p_stat = 'R'
     c                   goto      neste_lvare
     c                   endif
      *
     c                   eval      vhgrlr_hogr = vvogrp
     c                   eval      vhgrlr_hhgr = vvhgrp
     c     vhgrlr_key    chain     vhgrlrr
     c                   if        not %found
     c                   eval      SrcDta = 'Hovedgruppe ' +
     c                             %trim(%char(vvogrp)) +
     c                             '  ' + %trim(%char(vvhgrp)) +
     c                             '  *  finnes ikke. Vare ' +
     c                             %trim(vvvare) + ' ' +
     c                             %trim(vvtek1) + ' ble utelatt.'
     c                   write     netvrappr
     c                   eval      p_stat = 'R'
     c                   goto      neste_lvare
     c                   endif
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      d_itmgtx = vgutxt
     c                   else
     c                   eval      SrcDta = 'Undergruppe ' +
     c                             %trim(%char(vvogrp)) +
     c                             '  ' + %trim(%char(vvhgrp)) +
     c                             '  ' + %trim(%char(vvugrp)) +
     c                             ' finnes ikke. Vare ' +
     c                             %trim(vvvare) + ' ' +
     c                             %trim(vvtek1) + ' ble utelatt.'
     c                   write     netvrappr
     c                   eval      p_stat = 'R'
     c                   goto      neste_lvare
     c                   endif
      *
      * Leverandørnavn
      *
     c                   if        vvldor <> 0
     c                   eval      rlevpf_levr = vvldor
     c     rlevpf_key    chain     rlevpf
     c                   if        %found
     c                   eval      d_itmpro = rlnavn
     c                   endif
     c                   endif
      *
      * EAN-nummer
      *
9.01 c                   call      'VE720R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    vvvare
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      d_itmean = w_eann
     c                   endif
      *
      * Priskode-beskrivelse
      *
     c                   eval      vpkol1_pkod = vvkpri
     c     vpkol1_key    chain     vpkol1
     c                   if        %found
     c                   eval      d_itmprc = vaptxt
     c                   endif
      *
6.32  * hvis logistikk-vare ta en ekstra loop og legg ut pr.
6.32  * unike nobbnr.
6.32 c                   if        b_logi = *on
6.32 c                   exsr      logi_skriv
6.32 c                   else
      *
      * Skriver til fil
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      data = d_itm
     c                   write     netvarer
      *
      * Tilhørende enheter og priser
      *
     c                   exsr      enheter
6.32 c                   endif
      *
     c     neste_lvare   tag
      *
     c     csodi2_key    reade     csodi2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av enheter og priser for vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     enheter       begsr
      *
6.25 c                   eval      b_enhe = *off
6.25 c                   eval      w_enhe = *blank
      *
6.25 c                   eval      vvenl2_vare = vvvare
6.25 c     vvenl2_key    setll     vvenl2
6.25 c     vvenl2_key    reade     vvenl2
6.25 c                   dow       %eof = *off
      *
6.25 c                   if        vesaen = 0
6.25 c                   goto      neste_enh
6.25 c                   endif
      *
6.25 c                   if        b_enhe = *off
6.25 c                   if        veenhe = 'M2 ' or
6.25 c                             veenhe = 'M3 ' or
6.25 c                             veenhe = 'KG ' or
6.25 c                             veenhe = 'LTR'
6.25 c                   eval      b_enhe = *on
6.25 c                   eval      w_enhe = veenhe
6.25 c                   goto      neste_enh
6.25 c                   endif
6.25 c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_uomnum = vevare
     c                   eval      d_uomunt = veenhe
     c                   eval      d_uomupc = 0
     c                   eval      d_uomcal = *blank
     c                   eval      d_uomhig = '000000,000000'
     c                   eval      d_uomlen = '000000,000000'
     c                   eval      d_uomwid = '000000,000000'
     c                   eval      d_uomvol = '000000,000000'
     c                   eval      d_uomwei = '000000,000000'
      *
     c                   move      veomre        w_omre
     c                   eval      d_uomcal = %subst(w_omre:1:6) + ',' +
     c                                        %subst(w_omre:7:6)
      *
     c                   eval      data = d_uom
     c                   write     netvarer
      *
      * Tilhørende pris
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      priser
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      priser
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      priser
6.20 c                   endif
6.20 c                   endif
      *
6.25 c     neste_enh     tag
      *
6.25 c     vvenl2_key    reade     vvenl2
6.25 c                   enddo
      *
      * Flytter kalk.enhet som siste enhet i overføringsfilen
      *
6.25 c                   if        b_enhe = *on
      *
6.25 c                   eval      vvenl1_vare = vvvare
6.25 c                   eval      vvenl1_enhe = w_enhe
6.25 c     vvenl1_key    chain     vvenl1
6.25 c                   if        %found
      *
6.25 c                   eval      w_tell = w_tell + 1
      *
6.25 c                   eval      d_uomnum = vevare
6.25 c                   eval      d_uomunt = veenhe
6.25 c                   eval      d_uomupc = 0
6.25 c                   eval      d_uomcal = *blank
6.25 c                   eval      d_uomhig = '000000,000000'
6.25 c                   eval      d_uomlen = '000000,000000'
6.25 c                   eval      d_uomwid = '000000,000000'
6.25 c                   eval      d_uomvol = '000000,000000'
6.25 c                   eval      d_uomwei = '000000,000000'
      *
6.25 c                   move      veomre        w_omre
6.25 c                   eval      d_uomcal = %subst(w_omre:1:6) + ',' +
6.25 c                                        %subst(w_omre:7:6)
      *
6.25 c                   eval      data = d_uom
6.25 c                   write     netvarer
      *
      * Tilhørende pris
      *
6.25  * 1. leverandør for lageret
6.25 c                   eval      vvprl1_ldor = w_ldor
6.25 c                   exsr      priser
6.25 c                   if        b_pris = *off  and
6.25 c                             w_ldor <> vvldor
6.25  * 2. hovedleverandør
6.25 c                   eval      vvprl1_ldor = vvldor
6.25 c                   exsr      priser
6.25 c                   if        b_pris = *off
6.25  * siste mulighet...
6.25 c                   eval      vvprl1_ldor = 0
6.25 c                   exsr      priser
6.25 c                   endif
6.25 c                   endif
      *
6.25 c                   endif
6.25 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for priser for vare og enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     priser        begsr
      *
      * Finner pris
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvprl1_vare = vvvare
5.70 c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1
     c                   dow       %eof = *off
      *
     c                   eval      b_pris = *on
      *
     c                   move      vpsapr        w_sapr
     c                   eval      d_prcpri = %subst(w_sapr:1:7) + ',' +
     c                                        %subst(w_sapr:8:2)
      *
     c     *iso0         move      vppdat        d_prcdat
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1
     c                   enddo
      *
5.70 c                   if        b_pris = *off
5.70 c                   eval      vvprl1_vare = vvvare
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = veenhe
5.70 c                   eval      vvprl1_lety = 0
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1
5.70 c                   dow       %eof = *off
5.70  *
5.70 c                   eval      b_pris = *on
5.70  *
5.70 c                   move      vpsapr        w_sapr
5.70 c                   eval      d_prcpri = %subst(w_sapr:1:7) + ',' +
5.70 c                                        %subst(w_sapr:8:2)
5.70  *
5.70 c     *iso0         move      vppdat        d_prcdat
5.70  *
5.70 c                   if        w_dato >= vppdat
5.70 c                   leave
5.70 c                   endif
5.70  *
5.70 c     vvprl1_key    reade     vvprl1
5.70 c                   enddo
5.70  *
5.70 c                   endif
      *
      * Skriver til fil
      *
     c                   if        b_pris = *on
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_prcnum = vpvare
     c                   eval      d_prcunt = vpenhe
     c                   eval      d_prctyp = vplety
     c                   eval      d_prcltb = *blank
      *
     c                   eval      data = d_prc
     c                   write     netvarer
      *
     c                   endif
      *
     c                   endsr
      *
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13  * Subrutine for initiering av program
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13  *
6.13 c     sjekk_lager   begsr
      *
6.25 c                   eval      b_vtyp = *off
6.25 c                   eval      b_udat = *off
6.25 c                   eval      b_behl = *off
      *
6.24 c                   eval      w_vtyp = *blank
6.25 c                   eval      w_ldor = vvldor
      *
6.13 c                   eval      vlagl1_vare = vvvare
6.13 c     vlagl1_key    setll     vlagl1
6.13 c     vlagl1_key    reade     vlagl1
      *
6.25 c                   if        not %eof
6.25 c                   eval      b_vtyp = *on
6.25 c                   endif
      *
6.13 c                   dow       not %eof
      *
6.25 c                   if        vllage = w_lage
6.25 c                   if        vlvtyp = 'S'
6.25 c                   eval      w_vtyp = vlvtyp
6.25 c                   endif
6.25 c                   if        vlbldo <> 0
6.25 c                   eval      w_ldor = vlbldo
6.25 c                   endif
6.25 c                   endif
      *
6.25 c                   if        vlvtyp <> 'L' and
6.25 c                             vlvtyp <> 'S'
6.25 c                   eval      b_vtyp = *off
     c                   endif
      *
6.25 c                   if        vlbehl > 0
6.25 c                   eval      b_behl = *on
6.25 c                   endif
      *
6.25 c                   if        vludat <> *loval and
6.25 c                             vludat <= w_dato
6.25 c                   eval      b_udat = *on
6.25 c                   endif
      *
6.13 c     vlagl1_key    reade     vlagl1
6.13 c                   enddo
6.13  *
6.13  *
6.13 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekke om dette er et logistikknr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_logi    begsr
      *
      *
6.31  * sjekker om logistikk-vare
6.32 c                   eval      w_lv_vare = *blank
6.32 c                   eval      b_logi = *off
6.32 c/exec sql
6.32 c+ select jvkvnr
6.32 c+ into   :w_lv_vare
6.32 c+ from   jvkhpf
6.32 c+ where  jvkvnr = :vvvare
6.32 c+ fetch first 1 rows only
6.31 c/end-exec
6.32 c                   if        w_lv_vare <> *blank
6.32 c                   eval      b_logi = *on
6.32 c                   endif
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for sjekke om dette er et logistikknr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     logi_skriv    begsr
6.32  *
6.32 c                   eval      w_lv_nobb = 0
6.32 c                   eval      w_lv_modn = 0
6.32 c                   eval      w_lv_nlev = 0
6.32 c                   eval      w_lv_lvar =*blank
6.32  * Finne alle leverandører fra VVEPPF
6.32 c/exec sql
6.32 c+    declare vvle  cursor for
6.32 c+    select          a.vvlvnr,
6.32 c+                    a.vvlnob,
6.32 c+                    jvmodn,
6.32 c+                    jvlldo,
6.32 c+                    jvllva
6.32 c+      from
6.32 c+      (select distinct vvlfir,vvlvnr, vvlnob
6.32 c+                  from   vvlepf)  a
6.32 c+      inner join jvarpf on a.vvlnob = jvnobb
6.32 c+      inner join jvlepf on jvvare = jvlvnr and jvldor = jvlldo
6.32 c+      where  a.vvlfir = :w_firm and
6.32 c+             a.vvlvnr = :vvvare
6.32 c+      for read only
6.32 c/end-exec
6.32 c/exec sql
6.32 c+                  close vvle
6.32 c/end-exec
6.32 c/exec sql
6.32 c+                  open vvle
6.32 c/end-exec
6.32  *
6.32 c                   eval      b_eof = *off
6.32  *
6.32 c                   dow       b_eof = *off
6.32 c/exec sql
6.32 c+                  fetch next
6.32 c+                  from  vvle
6.32 c+                  into   :w_lv_vare,
6.32 c+                         :w_lv_nobb,
6.32 c+                         :w_lv_modn,
6.32 c+                         :w_lv_nlev,
6.32 c+                         :w_lv_lvar
6.32 c/end-exec
6.32 c                   if        sqlcod = 100 or sqlstt = '02000'
6.32 c                   eval      b_eof = *on
6.32 c                   leave
6.32 c                   endif
6.32 c                   if        sqlcod <> 0
6.32 c                   eval      b_eof = *on
6.32 c                   leave
6.32 c                   endif
6.32  * Skriver til fil
6.32  *
6.32 c                   if        w_lv_nobb <> 0
6.32 c                   eval      d_itmnob = w_lv_nobb
6.32 c                   eval      d_itmmod = w_lv_modn
6.32 c                   eval      d_itmdel = w_lv_nlev
6.32 c                   eval      d_itmpnu = w_lv_lvar
6.32 c                   eval      w_tell = w_tell + 1
6.32  *
6.32 c                   eval      data = d_itm
6.32 c                   write     netvarer
6.32  * Tilhørende enheter og priser
6.32  *
6.32 c                   exsr      enheter
6.32 c                   endif
6.32  *
6.32 c                   enddo
6.32  *
6.32 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare m/enheter og priser på va-varer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     varer_va      begsr
      *
      * Leser alle varer i PK1 sortiment
      *
     c                   eval      w_vnum = *blank
     c                   eval      csodi2_dkod = 'PGRUNN'
     c     csodi2_key    setll     csodi2
     c     csodi2_key    reade     csodi2
     c                   dow       %eof(csodi2) = *off
      *
     c                   if        csdvnu = w_vnum
     c                   exsr      enhet_va
     c                   goto      neste_vvare
     c                   else
     c                   eval      w_vnum = csdvnu
     c                   endif
      *
     c                   eval      jvarl1_vare = csdvnu
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found(jvarl1)
     c                   goto      neste_vvare
     c                   endif
      *
5.60 c     ';':' '       xlate     jvtek1        jvtek1
5.60 c     ';':' '       xlate     jvtek2        jvtek2
     c                   eval      d_itmnum = jvvare
     c                   eval      d_itmtx1 = jvtek1
     c                   eval      d_itmtx2 = jvtek2
     c                   eval      d_itmogr = jvogrp
     c                   eval      d_itmhgr = jvhgrp
     c                   eval      d_itmugr = jvugrp
     c                   eval      d_itmgtx = *blank
     c                   eval      d_itmnob = jvnobb
     c                   eval      d_itmmod = jvmodn
      *
      * Finner leverandørnr i reskontro
      *
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not %found(jlevl1)
     c                   goto      neste_vvare
     c                   endif
     c                   eval      d_itmdel = jlenum
     c                   eval      d_itmpro = *blank
     c                   eval      d_itmpnu = jvlvar
9.01 c                   eval      d_itmean = *blank
     c                   eval      d_itmunt = csdenh
     c                   eval      d_itmprc = *blank
     c                   eval      d_itmrgr = 0
     c                   eval      d_itmrgt = *blank
     c                   eval      d_itmact = 'Y'
6.25 c                   eval      d_itmtyp = 'S'
      *
      * Varegruppetekst
      *
     c                   if        jvogrp = 0 and jvhgrp = 0 and
     c                             jvugrp = 0
     c                   goto      neste_vvare
     c                   endif
     c                   eval      vogrlr_oogr = jvogrp
     c     vogrlr_key    chain     vogrlrr
     c                   if        not %found
     c                   goto      neste_vvare
     c                   endif
      *
     c                   eval      vhgrlr_hogr = jvogrp
     c                   eval      vhgrlr_hhgr = jvhgrp
     c     vhgrlr_key    chain     vhgrlrr
     c                   if        not %found
     c                   goto      neste_vvare
     c                   endif
      *
     c                   eval      vugrl1_uogr = jvogrp
     c                   eval      vugrl1_uhgr = jvhgrp
     c                   eval      vugrl1_uugr = jvugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      d_itmgtx = vgutxt
     c                   else
     c                   goto      neste_vvare
     c                   endif
      *
      * Leverandørnavn
      *
     c                   if        jlenum <> 0
     c                   eval      rlevpf_levr = jlenum
     c     rlevpf_key    chain     rlevpf
     c                   if        %found
     c                   eval      d_itmpro = rlnavn
     c                   endif
     c                   endif
      *
9.01 c                   eval      d_itmean = *blank
     c                   eval      d_itmprc = *blank
      *
      * Skriver til fil
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      data = d_itm
     c                   write     netvarer
      *
      * Tilhørende enheter og priser
      *
     c                   exsr      enhet_va
      *
     c     neste_vvare   tag
      *
     c     csodi2_key    reade     csodi2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av enheter og priser for vare VA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     enhet_va      begsr
      *
     c                   eval      jlevl3_enum = csdlnr
     c     jlevl3_key    chain     jlevl3
     c                   if        not %found(jlevl3)
     c                   goto      end_va
     c                   endif
      *
     c                   eval      jvpkl2_vare = csdvnu
     c                   eval      jvpkl2_ldor = jlldor
     c                   eval      jvpkl2_enhe = csdenh
     c     jvpkl2_key    setll     jvpkl2
     c     jvpkl2_key    reade     jvpkl2
      *
     c                   if        jwenhe = csdenh
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_uomnum = jwvare
     c                   eval      d_uomunt = jwenhe
     c                   eval      d_uomupc = 0
     c                   eval      d_uomcal = *blank
     c                   eval      d_uomhig = '000000,000000'
     c                   eval      d_uomlen = '000000,000000'
     c                   eval      d_uomwid = '000000,000000'
     c                   eval      d_uomvol = '000000,000000'
     c                   eval      d_uomwei = '000000,000000'
      *
     c                   move      jwofak        w_omre
     c                   eval      d_uomcal = %subst(w_omre:1:6) + ',' +
     c                                        %subst(w_omre:7:6)
      *
     c                   eval      data = d_uom
     c                   write     netvarer
     c                   endif
      *
      * Legger ut pris record
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_prcnum = csdvnu
     c                   eval      d_prcunt = csdenh
     c                   eval      d_prctyp = 0
     c                   eval      d_prcltb = *blank
     c                   eval      d_prcpri = '0,00'
     c                   eval      d_prcdat = '20160101'
      *
     c                   eval      data = d_prc
     c                   write     netvarer
      *
      *
     c     end_va        endsr
6.13  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for les på sortiment linjer
      *
     c     csodi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    csodi2_dkod
      *
      * Key for les på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på overgruppe
      *
     c     vogrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrlr_oogr
      *
      * Key for les på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrlr_hogr
     c                   kfld                    vhgrlr_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på priskode
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
6.13  *
6.13  * Key for les av lager
6.13  *
6.13 c     vlagl1_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    vlagl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevpf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevpf_levr
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør - nobb deltager
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på leverandør - reskontro
      *
     c     jlevl3_key    klist
     c                   kfld                    jlevl3_enum
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
     c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      *
      * Henter firma og lager fra LDA
      *
     c                   eval      w_firm = l_firm
5.70 c                   eval      w_lage = 00
      *
8.01 c                   eval      w_dato = %date
8.01 c                   adddur    1:*days       w_dato
      *
5.70  * Henter prisgruppe
5.70  *
5.70 c*                  call      'VL711R'
5.70 c*                  parm                    w_prgr
     c                   eval      w_prgr = *blank
      *
     c                   endsr
