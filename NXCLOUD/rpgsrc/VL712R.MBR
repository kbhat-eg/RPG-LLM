     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VL712R
      * Beskrivelse . . : Prisgruppe - henter prisgr. ut fra bruker lager
      *                   og sist brukte avdeling
      *
      *                   Brukes hvis kallende program har lager / avdeling
      *
      *
      *                   Input : lager / avdeling
      *                   Output: prisgruppe
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       241008 bof  Nytt program
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvpgrl1    if   e           k disk    rename(vpgrpfr:vpgrl1r)
      *
      * Definering av variable i nøkler
      *
     d vpgrl1_gfir     s                   like(vpgfir)
     d vpgrl1_gavd     s                   like(vpgavd)
     d vpgrl1_glag     s                   like(vpglag)
      *
      * Definering av pamametre
      *
     d p_gprg          s                   like(vpgprg)
     d p_gavd          s                   like(vpgavd)
     d p_glag          s                   like(vpglag)
     d p_firm          s                   like(vpgfir)
      *
      *
     d w_firm          s                   like(vpgfir)
     d w_savd          s                   like(vpgavd)
     d w_lagr          s                   like(vpglag)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Initierer output-parametre
      *
     c                   eval      p_gprg = *blank
      *
      * Henter prisgruppe
      *
     c                   eval      vpgrl1_gfir = w_firm
     c                   eval      vpgrl1_glag = p_glag
     c                   eval      vpgrl1_gavd = p_gavd
     c     vpgrl1_key    chain     vpgrl1
     c                   if        not %found(vpgrl1)
     c                   eval      vpgrl1_gavd = 0
     c     vpgrl1_key    chain     vpgrl1
     c                   endif
      *
     c                   if        %found(vpgrl1)
     c                   eval      p_gprg = vpgprg
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_glag
     c                   parm                    p_gavd
     c                   parm                    p_gprg
      *
      * Key for les på vare-pris-register
      *
     c     vpgrl1_key    klist
     c                   kfld                    vpgrl1_gfir
     c                   kfld                    vpgrl1_glag
     c                   kfld                    vpgrl1_gavd
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
