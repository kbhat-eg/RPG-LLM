     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VK990R
      * Beskrivelse . . : Oppretter alternativ varegruppe i salgsstatistikk
      *
      *                   Leser salgsstatistikk, og oppretter alternativ
      *                   varegruppe på alle varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070203 HKO  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    uf   e           k disk    rename(sstapfr:sstal1r)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser og oppdaterer salgsstatistikk
      *
     c     sstal1_key    setll     sstal1
     c     sstal1_key    reade     sstal1r
     c                   dow       not %eof
      *
     c                   if        sfvare <> *blank
      *
     c                   call      'VK701R'
     c                   parm                    w_firm
     c                   parm                    sfogrp
     c                   parm                    sfhgrp
     c                   parm                    sfugrp
     c                   parm                    sfldor
     c                   parm                    sfvare
     c                   parm                    sfahgr
     c                   parm                    sfaugr
      *
     c                   update    sstal1r
      *
     c                   endif
      *
     c     sstal1_key    reade     sstal1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vare
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
