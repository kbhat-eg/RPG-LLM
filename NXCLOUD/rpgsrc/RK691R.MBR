     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK691R                                              *
      * Beskrivelse . . : Utplukk av poster til generalnota                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.50 260907 HFL  Mulig med generalnota for en kunde            *
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 070510 BSA  Endring av sporing
      *                                                                       *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
T5.50frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     frktrl2    if   e           k disk    rename(rktrpfr:rktrl2r)
     frkw2pf    o    e           k disk
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                 ds
      *
     d wksort                129    192
     d  wksfir                        3  0 overlay(wksort)                      Firma
     d  wksknr                        6  0 overlay(wksort:4)                    Kunden
     d  wksbda                         d   overlay(wksort:10)                   År
     d                                     datfmt(*iso)
     d  wkstis                         t   overlay(wksort:20)                   Teller
      *
     d wwfdat                          d   datfmt(*iso)
     d wnfdat                          d   datfmt(*iso)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
     d pdato                 300    305  0
     d pbdat                          8  0
     d praar                          4  0
     d prper                          2  0
     d pbtyp                          1
     d pforf                          1
     d pbetb                          1
T5.50d ppknf                          6  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_nbel          s             13  2
     d w_firm          s                   like(rkfirm)
      *
     d p_stat          s              1
     d p_ckod          s              2  0
     d p_ctxt          s             20
     d p_ctx2          s             20
     d*p_fkda          s              8  0
     d*p_ffda          s              8  0
     d p_fkda          s               d   datfmt(*iso)
     d p_ffda          s               d   datfmt(*iso)
     d p_rda1          s               d   datfmt(*iso)
     d p_rda2          s               d   datfmt(*iso)
     d p_rda3          s               d   datfmt(*iso)
      *
      /eject
      *
T5.50 * Sjekk om enkeltkunde eller alle
      *
T5.50c                   if        ppknf <> *zeros
      *
T5.50 *  Enkeltkunde
      *
T5.50c     rkunlr_key    chain     rkunlr                             91
      *
T5.50c                   if        *in91 = *on                                           .....
T5.50c                   goto      slutt
T5.50c                   endif
      *
T5.50c                   eval      w_nbel = *zeros
      *
T5.50 * Test betalingsmåte
      *
T5.50c                   if        rkbetm <> 3
T5.50c                             and rkbetm <> 4
T5.50c                   goto      slutt
T5.50c                   endif
      *
T5.50c                   exsr      beh_kunde
T5.50c                   goto      slutt
T5.50c                   endif
      *
T5.50 *  Alle kunder
      ** Leser kunde-register     ######################## START ######
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1                                 98
      *
     c                   dow       *in98 = *off
      *
     c                   eval      w_nbel = *zeros
      *
      * Test betalingsmåte
      *
     c                   if        rkbetm <> 3
     c                             and rkbetm <> 4
     c                   goto      nykund
     c                   endif
      *
T5.50c                   exsr      beh_kunde
      *
     c     nykund        tag
      *
     c     rkunl1_key    reade     rkunl1                                 98
      *
     c                   enddo
      /space 3
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - -
T5.50 * Subrutine som behandler kunde
      * - - - - - - - - - - - - - - - - - - - -
      *
T5.50c     beh_kunde     begsr
      *
      * Beregn forfallsdato basert
      * kundens betalingsbetingelse
      *
     c                   if        pbetb = 'J'
     c                   move      rkbetb        p_ckod
     c                   exsr      xforf
     c                   endif
      *
     c                   exsr      les_trans
      *
      * Oppdater kunde - generalnotamerke
      *
     c     rkunlu_key    chain     rkunlu                             91
      *
     c                   if        *in91 = *off                                          .....
      *
     c                   if        w_nbel <= *zero                                       ... .
     c                   move      'S'           rksamm                                    . .
     c                   else                                                              . .
     c                   eval      rksamm = *blank                                         . .
     c                   endif                                                           ... .
      *                                                                 .
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK691R    '
     c                   update    rkunlur                                                   .
     c                   endif                                                           .....
T5.50c                   endsr
      *
      /eject
      *
      * Leser kundetransaksjoner ######################## START ######
      *
     c     les_trans     begsr
      *
     c                   move      rkkund        rnkund
     c     rktrl2_key    setll     rktrl2
     c     rktrl2_key    reade     rktrl2                                 97
      *
     c                   dow       *in97 = *off
      *
      * Med på generalnota tidligere
      *
     c                   if        rnsamf <> *zero
     c                   goto      nyles
     c                   endif
      *
      * Riktig dato <===> regnskapsperiode
      *
     c                   if        rnraar > praar
     c                   goto      nyles
     c                   endif
      *
     c                   if        rnraar = praar
     c                             and rnrper > prper
     c                   goto      nyles
     c                   endif
      *
      * Sjekk reklamasjon
      *
     c                   if        rnrekl <> *blank
     c                   goto      nyles
     c                   endif
      *
      * Sjekk saldorecord
      *
     c                   if        rnbilk = 98
     c                   goto      nyles
     c                   endif
      *
      * Sjekk faktura/kreditnota
      * hvis kun disse skal med
      *
     c                   if        pbtyp = 'J'
     c                   if        rnbilk <> 01
     c                             and rnbilk <> 03
     c                   goto      nyles
     c                   endif
     c                   endif
      *
      * Sjekk om posten er
      * saldert tidligere
      *
     c                   if        rnbelØ <> rnrest
     c                   goto      nyles
     c                   endif
      *
      * Sjekk om posten er
      * forfalt hvis kun
      * forfalt poster skal med
      *
     c                   if        pforf = 'J'
      *
     c                   move      rnfdat        wnfdat
      *
     c                   if        wnfdat > wwfdat
     c                   goto      nyles
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_nbel = w_nbel + rnbelØ
      *
      * Legg ut transaksjon
      *
     c                   move      rnkund        wksknr
     c                   move      rnbdat        wksbda
     c                   move      rntist        wkstis
     c                   movel     wksort        rwkkey
      *
     c                   move      wwfdat        rnfdat
      *
     c                   if        rnbelØ <> *zero
     c                   write     rkw2pfr
     c                   endif
      *
     c     nyles         tag
      *
     c     rktrl2_key    reade     rktrl2                                 97
      *
     c                   enddo
      *
      ** Leser kundetransaksjoner ######################## SLUTT ######
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for beregning av forfallsdato                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xforf         begsr
      *
      * Flytt til packede felt for call
      *
     c                   move      pbdat         p_fkda
      *
     c                   eval      p_ffda = *loval
     c                   eval      p_rda1 = *loval
     c                   eval      p_rda2 = *loval
     c                   eval      p_rda3 = *loval
      *
      * Henter betalingsbetingelse
      *
     c                   call      'RS203R'                                      betalingsbetingelse
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
     c                   parm                    p_ctx2
     c                   parm                    p_fkda
     c                   parm                    p_ffda
     c                   parm                    p_rda1
     c                   parm                    p_rda2
     c                   parm                    p_rda3
      *
     c                   move      p_ffda        wwfdat
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Behandle bilagsdato
      *
     c                   eval      wwfdat = *loval
     c                   eval      wnfdat = *loval
      *
      * Beregn forfallsdato basert
      * betalingsbetingelse = 99
      *
     c                   if        pbetb = ' '
     c                   move      99            p_ckod
     c                   exsr      xforf
     c                   if        p_stat <> *blank
     c                   move      'J'           pbetb
     c                   endif
     c                   endif
      *
     c     rktrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rnkund
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkkund
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
T5.50c     rkunlr_key    klist
T5.50c                   kfld                    w_firm
T5.50c                   kfld                    ppknf
      *
      * Setter startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      wksfir = l_firm
     c                   eval      wksort = *zero
      *
     c                   endsr
