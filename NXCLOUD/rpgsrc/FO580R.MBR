     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO580R
      * Beskrivelse . . : Ordre-hode, tilbud som skal følges opp
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141005 AMD  Nytt program
      *       241105 HKO  Lagt inn filter for ikke å vise utgåtte tilbud.
      *                   Viser utgår-dato istedenfor ordredato i subfile
      *                   Lagt inn vedlikehold av utgår-dato
5.51  *       200407 AMD  Det er lagt inn en endring her som krever
      *                   litt forklaring: Se starten på calulation
      *                   hvor det er lagt inn nærmere beskrivelse
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.30  * 6.30  020214 bof  Utvidet med parameter med selger
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  290116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohela    if   e           k disk    rename(fohepfr:fohelar)
     ffohelm    if   e           k disk    rename(fohepfr:fohelmr)
     ffohelo    if   e           k disk    rename(fohepfr:fohelor)
     ffohelq    if   e           k disk    rename(fohepfr:fohelqr)
     ffohelv    if   e           k disk    rename(fohepfr:fohelvr)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffusrlu    uf   e           k disk    rename(fusrpfr:fusrlur)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffo580d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
6.30  *
6.30  * Definering av parametre
6.30  *
6.30 d p_selg          s              4
      *
      * Definering av variabler i nøkler
      *
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d fohelr_num2     s                   like(fonumm)
     d fohelr_suf2     s                   like(fosuff)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohela_kuna     s                   like(fokuna)
     d fohelq_tdat     s                   like(fotdat)
5.51 d fohelq_numm     s                   like(fonumm)
5.51 d fohelq_suff     s                   like(fosuff)
     d fohelm_ddat     s                   like(foddat)
5.51 d fohelm_numm     s                   like(fonumm)
5.51 d fohelm_suff     s                   like(fosuff)
     d fohelo_otyp     s                   like(footyp)
     d fohelv_navn     s                   like(fonavn)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d rkunl1_kund     s                   like(rkkund)
     d fusrl1_user     s                   like(fbuser)
     d fusrlu_user     s                   like(fbuser)
     d votyl1_otyp     s                   like(vaotyp)
     d vinfl1_ityp     s                   like(vaityp)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_sjek          s              1    inz(*off)
     d b_exit          s              1    inz(*off)
     d b_slet          s              1    inz(*off)
      *
     d w_salg          s             13
      *
     d w_firm          s                   like(fofirm)
     d w_knav          s                   like(rknavn)
     d w_gtxt          s             20
     d w_itx2          s             20
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d b_moms          s              1    inz(*off)
      *
     d w_retu          s              1
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_benr          s                   like(fdbenr)
     d w_lpro          s              1
     d w_fell          s              6
     d w_funn          s              1  0
6.30 d w_dato          s                   like(foddat)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
5.51  * Forklaring (Se ref 5.51 i toppen av programmet)
5.51  * -----------------------------------------------
5.51  * F23 - skifter informasjon i subfilen. Av det som skiftes er
5.51  *       også Oppfølgingsdato og Utgår dato. Når utgår dato bare
5.51  *       f.eks. er lagt inn noen få poster, mister subfilen start
5.51  *       punktet når det switches tilbake til oppfølgingsdato. Til
5.51  *       slutt blir hele subfilen tom. Her derfor foreløpig landet
5.51  *       på denne løsningen: Når F23 trykkes posisjoneres subfilen
5.51  *       på første post igjen slik at i hvert fall alt som ligger
5.51  *       inne blir vist. Ved å oppgi dato på toppen vil jo ny
5.51  *       posisjonering bli foretatt og vanlig blaing fungere som
5.51  *       normalt.
5.51  *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F1=Bla-alternativer
      *
     c                   if        *inka
     c                   exsr      xh1win
     c                   goto      b2taga
     c                   endif
      *
      * F22=Vise inkl/eks mva.
      *
     c                   if        *inkw
     c                   if        *in71   = *on
     c                   if        b_moms  = *off
     c                   eval      b_moms  = *on
     c                   eval      b2hed2  = '   Inkl.mva.'
     c                   else
     c                   eval      b_moms  = *off
     c                   eval      b2hed2  = '  Ordreverdi'
     c                   endif
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * F23=Vise alternativ informasjon
      *
     c                   if        *inkx
     c                   if        *in71  = *off
     c                   eval      *in71  = *on
5.51 c                   eval      w_seqe = 'U'
     c                   eval      b2hedd = ' Utg.dato'
     c                   eval      b2hed2 = '  Ordreverdi'
     c                   if        b_moms = *on
     c                   eval      b2hed2  = '   Inkl.mva.'
     c                   endif
     c                   else
     c                   eval      *in71  = *off
5.51 c                   eval      w_seqe = 'M'
     c                   eval      b2hedd = 'Oppf.dato'
     c                   eval      b2hed2 = 'Informasjon '
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      xsjekk_inp
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0      or
     c                             b2dato <> 0      or
     c                             b2kuna <> *blank or
     c                             b2navn <> *blank or
     c                             b2otyp <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_exit = *off
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fohela_kuna = b1kuna
     c     fohela_key    setll     fohela
     c                   when      w_seqe = 'U'
     c                   if        b1dato <> 0
     c     *dmy          move      b1dato        fohelq_tdat
     c                   endif
5.51 c                   eval      fohelq_numm = b1numm
5.51 c                   eval      fohelq_suff = b1suff
     c     fohelq_key    setll     fohelq
     c                   when      w_seqe = 'M'
     c                   if        b1dato <> 0
     c     *dmy          move      b1dato        fohelm_ddat
     c                   endif
5.51 c                   eval      fohelm_numm = b1numm
5.51 c                   eval      fohelm_suff = b1suff
     c     fohelm_key    setll     fohelm
     c                   when      w_seqe = 'O'
     c                   eval      fohelo_otyp = b1otyp
     c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'V'
     c                   eval      fohelv_navn = b1navn
     c     fohelv_key    setll     fohelv
     c                   other
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    setll     fohel1
     c                   endsl
      *
5.51  * Dersom det er valgt F23 for bytting av info i subfilen
5.51  * posisjoneres det på nytt i og med at dato også endres
5.51  *
5.51 c                   if        *inkx
5.51 c                   select
5.51 c                   when      w_seqe = 'U'
5.51 c                   eval      fohelq_tdat = *loval
5.51 c                   eval      fohelq_numm = 0
5.51 c                   eval      fohelq_suff = 0
5.51 c     fohelq_key    setll     fohelq
5.51 c                   when      w_seqe = 'M'
5.51 c                   eval      fohelm_ddat = *loval
5.51 c                   eval      fohelm_numm = 0
5.51 c                   eval      fohelm_suff = 0
5.51 c     fohelm_key    setll     fohelm
5.51 c                   endsl
5.51 c                   endif
5.51  *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = ' '
     c                   eval      fohel1_numm = b2numm
     c                   eval      fohel1_suff = b2suff
     c     fohel1_key    setll     fohel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordretype
      *
     c                   if        b2otyp <> *blank
     c                   eval      w_seqe = 'O'
     c                   eval      fohelo_otyp = b2otyp
     c     fohelo_key    setll     fohelo
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundenavn
      *
     c                   if        b2kuna <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      fohela_kuna = b2kuna
     c     fohela_key    setll     fohela
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vareadresse
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      fohelv_navn = b2navn
     c     fohelv_key    setll     fohelv
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på utgår-dato
      *
     c                   if        b2dato <> 0
     c                   if        *in71  =  *on
     c                   eval      w_seqe = 'U'
      *
     c     *dmy          move      b2dato        fohelq_tdat
5.51 c                   eval      fohelq_numm = b1numm
5.51 c                   eval      fohelq_suff = b1suff
     c     fohelq_key    setll     fohelq
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner startverdi på oppfølgingsdato
      *
     c                   if        b2dato <> 0
     c                   if        *in71  =  *off
     c                   eval      w_seqe = 'M'
      *
     c     *dmy          move      b2dato        fohelm_ddat
5.51 c                   eval      fohelm_numm = b1numm
5.51 c                   eval      fohelm_suff = b1suff
     c     fohelm_key    setll     fohelm
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
     c                   eval      b2suff = 0
     c                   eval      b2dato = 0
     c                   eval      b2otyp = *blank
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_exit = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Hvem behandler ordre ?
      *
     c                   if        b1valg = 8
     c                   call      'FO710R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = 0
     c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Er ordre ledig for bruk ?
      *
     c                   exsr      xsjekk_ord1
     c                   if        b_sjek = *on
     c                   eval      b1valg = 0
     c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endre på tilbud
      *
     c                   if        b1valg = 2
     c                   exsr      xc2win
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   exsr      ajo_subfile
     c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      w_numm = b1numm
     c                   eval      w_suff = b1suff
      *
     c                   call      'FO505R'
     c                   parm                    w_firm
6.nu c                   parm                    w_numm
     c                   parm                    w_suff
     c                   eval      b1valg = 0
     c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Velge ordre til utskrift
      *
     c                   if        b1valg = 6
     c                   eval      fohelr_numm = b1numm
     c                   eval      fohelr_suff = b1suff
     c                   exsr      xb6win
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   exsr      ajo_subfile
     c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Behandle tilbud
      * (Gå over til FO500r)
      *
     c                   if        b1valg = 7
     c                   eval      w_numm = b1numm
     c                   eval      w_lpro = ' '
      *
     c                   call      'FO500R'
6.nu c                   parm                    w_numm
     c                   parm                    w_lpro
     c                   eval      b1valg = 0
     c                   exsr      xfarge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - -
     c                   enddo
      * - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsjekk_inp    begsr
      *
     c                   eval      b_feil = *off
      *
      * Dato
      *
     c                   if        b2dato <> 0
     c     *dmy          test(d)                 b2dato                 34
     c                   if        *in34  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk om ordren allerede er til behandling (subfile)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsjekk_ord1   begsr
      *
     c                   eval      b_sjek = *off
      *
      *  Sjekk, er denne ordren allerede til behandling
      *
     c                   if        b1kode <> *blank
     c                   eval      b_sjek =  *on
     c                   endif
      *
      *  Sjekk, er denne ordren allerede til behandling,
      *         Denne sjekken må utføres fordi subfilen
      *         ikke alltid er oppdatert
      *
     c                   if        b_sjek = *off
     c                   eval      fohelr_numm = b1numm
     c                   eval      fohelr_suff = b1suff
     c     fohelr_key    chain     fohelrr                            90
     c                   if        *in90  =  *off
     c                   if        fokode <> 0
     c                   eval      b_sjek =  *on
     c                   endif
     c                   endif
     c                   endif
      *
      *  Dersom det er valgt 5 for å vise ordre, skal det ikke
      *  være noen sperre på å få fram ordren.  I så fall
      *  nullstilles koden igjen for å kunne vise alle ordre.
      *
     c                   if        b1valg = 5
     c                   eval      b_sjek = *off
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker om oppgitt lager finnes på linje-nivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsjekk_lag    begsr
      *
      * Sjekker om oppgitt lagernummer er representert på
      * en av varelinjene på ordren
      *
     c                   eval      w_funn = 0
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
      *
     c                   dow       not %eof
     c                   if        fdlage = h1lage
     c                   eval      w_funn = 1
     c                   leave
     c                   endif
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  C2 Behandle bilde for endring av oppfølgings-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2win        begsr
      *
     c                   eval      c2ddat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2inf1 = *blank
     c                   eval      c2inf2 = *blank
      *
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    chain     fohel1r
      *
     c                   if        %found
     c                   eval      c2inf1 = foinf1
     c                   eval      c2inf2 = foinf2
     c                   if        foddat <> *loval
     c     *dmy          move      foddat        c2ddat
     c                   endif
     c                   if        fotdat <> *loval
     c     *dmy          move      fotdat        c2tdat
     c                   endif
     c                   endif
      *
     c     c2taga        tag
      *
     c                   write     c2win
     c                   exfmt     c2win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   leavesr
     c                   endsl
      *
      * Oppdater ordrehode
      *
     c                   eval      fohelu_numm = b1numm
     c                   eval      fohelu_suff = b1suff
     c     fohelu_key    chain     fohelur
      *
     c                   if        %found
     c                   eval      foinf1 = c2inf1
     c                   eval      foinf2 = c2inf2
     c                   move      *loval        foddat
     c                   move      *loval        fotdat
     c                   if        c2ddat <> 0
     c     *dmy          move      c2ddat        foddat
     c                   endif
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        fotdat
     c                   endif
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B6 Behandle bilde for utskrift av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb6win        begsr
      *
      *  Kaller opp subprogrammet for utskriftsvalg
      *
     c                   call      'FO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
6.nu c                   parm                    fohelr_numm
     c                   parm                    fohelr_suff
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Setter opp farge på infokode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xfarge        begsr
      *
      * Hent inn ordre-info-kode i inforegister
      *
     c                   eval      vinfl1_ityp = b1ityp
     c     vinfl1_key    chain     vinfl1r                            92
     c                   if        *in92  = *on
     c                   eval      vaifrg = *blank
     c                   endif
      *
     c                   if        vaifrg = ' '
     c                   eval      *in41  = *off
     c                   eval      *in42  = *off
     c                   eval      *in43  = *off
     c                   eval      *in44  = *off
     c                   endif
      *
     c                   if        vaifrg = 'H'
     c                   eval      *in41  = *on
     c                   eval      *in42  = *off
     c                   eval      *in43  = *off
     c                   eval      *in44  = *off
     c                   endif
      *
     c                   if        vaifrg = 'G'
     c                   eval      *in41  = *off
     c                   eval      *in42  = *on
     c                   eval      *in43  = *off
     c                   eval      *in44  = *off
     c                   endif
      *
     c                   if        vaifrg = 'B'
     c                   eval      *in41  = *off
     c                   eval      *in42  = *off
     c                   eval      *in43  = *on
     c                   eval      *in44  = *off
     c                   endif
      *
     c                   if        vaifrg = 'R'
     c                   eval      *in41  = *off
     c                   eval      *in42  = *off
     c                   eval      *in43  = *off
     c                   eval      *in44  = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   leavesr
     c                   when      *inkd
     c                   exsr      spØrring
     c                   when      *inki
     c                   exsr      lagring
     c                   goto      h1taga
     c                   endsl
      *
      * Etter at man har vært inne i dette vindu (H1WIN)
      * velges logisk fil tilbake til ordrerekkefølge
      * og sub-file fylles på nytt.
      *
     c                   eval      w_seqe = *blank
     c                   eval      fohel1_numm = 0
     c                   eval      fohel1_suff = 0
      *
      * Fyller subfile på nytt
      *
     c     fohel1_key    setll     fohel1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for ajourføring av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Det er laget en egen subrutine for ajourføring av subfile.
      * Denne blir aktivisert når det er et valg som betinger call til et
      * nytt program som oppdaterer ordre.  For å få ajourført subfile
      * uten ny posisjonering må det gjøres et nytt oppslag mot ordren.
      *
     c     ajo_subfile   begsr
      *
     c                   eval      fohelr_numm = b1numm
     c                   eval      fohelr_suff = b1suff
     c     fohelr_key    chain     fohelrr                            90
      *
      * Ajourfør subfile
      *
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1otyp = footyp
     c                   eval      b1navn = fonavn
     c                   eval      b1info = *blank
     c                   eval      b1ityp = foityp
     c                   eval      b1dato = 0
      *
     c                   if        *in71  =  *off
     c                   if        foddat <> *loval
     c     *dmy          move      foddat        b1dato
     c                   endif
     c                   eval      b1info = foinf1
     c                   else
     c                   if        fotdat <> *loval
     c     *dmy          move      fotdat        b1dato
     c                   endif
     c                   endif
      *
     c                   eval      b1kode = *blank
     c                   if        fokode <> 0
     c                   eval      b1kode = '**'
     c                   endif
     c                   if        fokode = 2
     c                   eval      b1kode = 'UT'
     c                   endif
     c                   if        fokode = 3
     c                   eval      b1kode = 'OK'
     c                   endif
     c                   if        fokode = 5
     c                   eval      b1kode = 'VE'
     c                   endif
     c                   if        fokode = 7
     c                   eval      b1kode = 'BU'
     c                   endif
      *
     c                   eval      b1knav      = *blank
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r                            90
     c                   if        *in90
     c                   eval      b1knav = fokuna
     c                   else
     c                   eval      b1knav = rknavn
     c                   endif
     c                   eval      b1kund = fokund
     c                   eval      b1kuna = fokuna
      *
     c                   eval      b1salg = (fotots - fototr)
     c                   if        b_moms = *on
     c                   if        (b1salg * w_mvat) > 999999999,99
     c                   eval      b1salg = 999999999,99
     c                   else
     c                   eval      b1salg = (b1salg * w_mvat)
     c                   endif
     c                   endif
     c                   if        *in71
     c                   eval      w_salg = %editw(b1salg : '         ,  -')
     c                   move      w_salg        b1info
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14  = *on
     c                   write     b2ctl
     c                   eval      *in14  = *off
      *
     c                   eval      *in15  = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      fohela
     c                   when      w_seqe = 'U'
     c                   read      fohelq
     c                   when      w_seqe = 'M'
     c                   read      fohelm
     c                   when      w_seqe = 'O'
     c                   read      fohelo
     c                   when      w_seqe = 'V'
     c                   read      fohelv
     c                   other
     c                   read      fohel1
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *on
     c                   iter
     c                   endif
      *
      * Fyll opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fohela_kuna = fokuna
     c                   when      w_seqe = 'U'
     c                   eval      fohelq_tdat = fotdat
     c                   when      w_seqe = 'M'
     c                   eval      fohelm_ddat = foddat
     c                   when      w_seqe = 'O'
     c                   eval      fohelo_otyp = footyp
     c                   when      w_seqe = 'V'
     c                   eval      fohelv_navn = fonavn
     c                   other
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   endsl
      *
     c                   eval      *in39  = *off
     c                   eval      b1valg = 0
     c                   eval      b1numm = fonumm
     c                   eval      b1suff = fosuff
     c                   eval      b1otyp = footyp
     c                   eval      b1navn = fonavn
     c                   eval      b1info = *blank
     c                   eval      b1ityp = foityp
     c                   eval      b1dato = 0
      *
     c                   exsr      xfarge
      *
     c                   if        *in71  =  *off
     c                   if        foddat <> *loval
     c     *dmy          move      foddat        b1dato
     c                   endif
     c                   eval      b1info = foinf1
     c                   else
     c                   if        fotdat <> *loval
     c     *dmy          move      fotdat        b1dato
     c                   endif
     c                   endif
      *
     c                   eval      b1kode = *blank
     c                   if        fokode <> 0
     c                   eval      b1kode = '**'
     c                   endif
     c                   if        fokode = 2
     c                   eval      b1kode = 'UT'
     c                   endif
     c                   if        fokode = 3
     c                   eval      b1kode = 'OK'
     c                   endif
     c                   if        fokode = 5
     c                   eval      b1kode = 'VE'
     c                   endif
     c                   if        fokode = 7
     c                   eval      b1kode = 'BU'
     c                   endif
      *
     c                   eval      b1knav      = *blank
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r                            90
      *
     c                   if        *in90
     c                   eval      b1knav = fokuna
     c                   else
     c                   eval      b1knav = rknavn
     c                   endif
     c                   eval      b1kund = fokund
     c                   eval      b1kuna = fokuna
      *
     c                   eval      b1salg = (fotots - fototr)
     c                   if        b_moms = *on
     c                   if        (b1salg * w_mvat) > 999999999,99
     c                   eval      b1salg = 999999999,99
     c                   else
     c                   eval      b1salg = (b1salg * w_mvat)
     c                   endif
     c                   endif
      *
     c                   if        *in71
     c                   eval      w_salg = %editw(b1salg : '         ,  -')
     c                   move      w_salg        b1info
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'A'
     c     fohela_key    setgt     fohela
     c                   readp     fohela
     c                   when      w_seqe = 'U'
     c     fohelq_key    setgt     fohelq
     c                   readp     fohelq
     c                   when      w_seqe = 'M'
     c     fohelm_key    setgt     fohelm
     c                   readp     fohelm
     c                   when      w_seqe = 'O'
     c     fohelo_key    setgt     fohelo
     c                   readp     fohelo
     c                   when      w_seqe = 'V'
     c     fohelv_key    setgt     fohelv
     c                   readp     fohelv
     c                   other
     c     fohel1_key    setgt     fohel1
     c                   readp     fohel1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     fohela
     c                   when      w_seqe = 'U'
     c                   readp     fohelq
     c                   when      w_seqe = 'M'
     c                   readp     fohelm
     c                   when      w_seqe = 'O'
     c                   readp     fohelo
     c                   when      w_seqe = 'V'
     c                   readp     fohelv
     c                   other
     c                   readp     fohel1
     c                   endsl
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   exsr      chk_subfile
     c                   if        b_subf = *on
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fohela_kuna = fokuna
     c                   when      w_seqe = 'U'
     c                   eval      fohelq_tdat = fotdat
     c                   when      w_seqe = 'M'
     c                   eval      fohelm_ddat = foddat
     c                   when      w_seqe = 'O'
     c                   eval      fohelo_otyp = footyp
     c                   when      w_seqe = 'V'
     c                   eval      fohelv_navn = fonavn
     c                   other
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'A'
     c     fohela_key    setll     fohela
     c                   when      w_seqe = 'U'
     c     fohelq_key    setll     fohelq
     c                   when      w_seqe = 'M'
     c     fohelm_key    setll     fohelm
     c                   when      w_seqe = 'O'
     c     fohelo_key    setll     fohelo
     c                   when      w_seqe = 'V'
     c     fohelv_key    setll     fohelv
     c                   other
     c     fohel1_key    setll     fohel1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om rad skal være med i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     chk_subfile   begsr
      *
     c                   eval      b_subf = *off
      *
      * Uaktuell ordretype ?
      * Kun tilbud skal vises
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
     c                   if        %found
     c                   if        vaoakk <> 0
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Uaktuell bruker ?
      *
     c                   if        h1user <> *blank and
     c                             h1user <> fouser
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
      *
      * Uaktuell kunde ?
      *
     c                   if        h1kund <> 0 and
     c                             h1kund <> fokund
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
      *
      * Uaktuell selger ?
      *
     c                   if        h1selg <> 0 and
     c                             h1selg <> foselg
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
      *
      * Uaktuell avdeling ?
      *
     c                   if        h1avde <> 0 and
     c                             h1avde <> foavde
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
      *
      * Uaktuelt lager ?
      *
     c                   if        h1lage <> 0
     c                   exsr      xsjekk_lag
     c                   if        w_funn =  0
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Uaktuell rekvisisjon ?
      *
     c                   if        h1rekv <> *blank and
     c                             h1rekv <> forekv
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
      *
      * Utelate utgåtte tilbud
      *
     c                   if        h1utgt = 1 and
     c                             fotdat <> *loval and
     c                             fotdat < %date
     c                   eval      b_subf = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12  = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13  = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12  = *off
     c                   eval      *in13  = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        w_afld = 'H1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    h1kund
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Selger
      *
     c                   if        w_afld = 'H1SELG'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    h1selg
     c                   parm                    w_itx2
     c                   leavesr
     c                   endif
      *
      * Avdeling
      *
     c                   if        w_afld = 'H1AVDE'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    h1avde
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lagre valg i h1win på bruker
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lagring       begsr
      *
      * Hente inn bruker-register for oppdatering
      *
     c                   eval      fusrlu_user = l_user
     c     fusrlu_key    chain     fusrlur
      *
     c                   if        %found
     c                   eval      fbuse2 = h1user
     c                   eval      fbkund = h1kund
     c                   eval      fbsel2 = h1selg
     c                   eval      fbavde = h1avde
     c                   eval      fblage = h1lage
     c                   eval      fbrekv = h1rekv
6.10 c                   time                    fbedat
6.10 c                   time                    fbetim
6.10 c                   eval      fbeusr = l_user
     c                   update    fusrlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.30  *
6.30 c     *entry        plist
6.30 c                   parm                    p_selg
      *
      * Key for oppslag på ordre/suffix
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
      *
     c     fohelr_ke2    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_num2
     c                   kfld                    fohelr_suf2
      *
      * Key for posisjonering på ordre/suffix
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for posisjonering på kundenavn
      *
     c     fohela_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohela_kuna
      *
      * Key for posisjonering på oppfølgings-dato
      *
     c     fohelm_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelm_ddat
5.51 c                   kfld                    fohelm_numm
5.51 c                   kfld                    fohelm_suff
      *
      * Key for posisjonering på ordretype
      *
     c     fohelo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelo_otyp
      *
      * Key for posisjonering på utgår-dato
      *
     c     fohelq_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelq_tdat
5.51 c                   kfld                    fohelq_numm
5.51 c                   kfld                    fohelq_suff
      *
      * Key for posisjonering på vareadresse
      *
     c     fohelv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelv_navn
      *
      * Key for oppdatering av ordrehode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for ordrelinje-register
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for ofak kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for brukerregister
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for brukerregister (update)
      *
     c     fusrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrlu_user
      *
      * Key for ordretyperegister
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre-info-type
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22  = *on
      *
     c                   eval      b2hed2 = 'Informasjon '
     c                   eval      b2hedd = 'Oppf.dato'
     c                   eval      *in71  = *off
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Hente inn brukerregister
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1r                            90
      *
      * Hente inn ofak kode-register
      *
     c     fstsl1_key    chain     fstsl1r                            90
     c                   eval      w_fell = faaork
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'M'
6.30 c                   if        p_selg <> *blank
6.30 c                   time                    w_dato
     c     w_dato        subdur    fbko16:*days  w_dato
     c                   eval      fohelm_ddat = w_dato
6.30 c                   else
     c                   eval      fohelm_ddat = *loval
6.30 c                   endif
5.51 c                   eval      fohelm_numm = 0
5.51 c                   eval      fohelm_suff = 0
     c     fohelm_key    setll     fohelm
      *
      * Hente inn verdier fra brukerregister,
      * for å oppdatere H1-bildet med valg
      *
6.30 c                   if        p_selg = *blank
     c                   eval      h1user = fbuse2
     c                   eval      h1kund = fbkund
     c                   eval      h1selg = fbsel2
     c                   eval      h1avde = fbavde
     c                   eval      h1lage = fblage
     c                   eval      h1rekv = fbrekv
6.30 c                   else
     c                   move      p_selg        h1selg
6.30 c                   endif
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
