     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV681R
      * Beskrivelse . . : Vare, forkastede varer v/innlesing spesialpriser
      *
      *                   Rapport over varer som ikke ble lest inn i
      *                   spesialprisregisteret - ASOFAK
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010201 HKO  Nytt program
      *       270105 HKO  Format som input-parameter
6.11  * 6.10  110709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  250809 bof  Fjernet 000 fra pakning, ligger på vvarpf
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
      *
     fjv681p    o    e             printer
      *
      * Definering av parametre
      *
     d p_form          s              1
      *
      * Definering av local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av format
      *
      * Nobb format
      *
     d                 ds
     d d_nobb                        74
     d  d_nobb_nobb                   8  0 overlay(d_nobb:28)
     d  d_nobb_sapr                   9  2 overlay(d_nobb:39)
      *
     d                 ds
     d d_nib                        341
     d  d_nib_nobb                    8  0 overlay(d_nib:  1)
     d  d_nib_ntla                    9  2 overlay(d_nib:207)
     d  d_nib_ntdi                    9  2 overlay(d_nib:216)
      *
      * Definering av variable i nøkler
      *
     d jvarl3_nobb     s                   like(jvnobb)
     d jlevl1_ldor     s                   like(jlldor)
     d vvarl3_nobb     s                   like(vvnobb)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_nobb          s              8  0
     d w_sapr          s              9  2
     d w_utim          s              6  0
      *
     d w_eann          s                   like(jvean1)
6.11 d w_eava          s                   like(jvvare)
6.12 d w_eaen          s                   like(jvpenh)
6.11 d w_east          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Header
      *
     c                   exsr      hode
      *
      * Detalj
      *
     c                   exsr      detalj
      *
      * Avslutter prorgram
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = l_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1date = udate
     c                   time                    w_utim
     c                   eval      a1time = w_utim
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Skriver heading
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utplukk til - og utkskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   select
     c                   when      p_form = '1'
     c                   eval      d_nobb = fwirad
     c                   eval      w_nobb = d_nobb_nobb
     c                   eval      w_sapr = d_nobb_sapr
     c                   when      p_form = '3'
     c                   eval      d_nib  = fwirad
     c                   eval      w_nobb = d_nib_nobb
     c                   if        d_nib_ntla <> 0
     c                   eval      w_sapr = d_nib_ntla
     c                   else
     c                   eval      w_sapr = d_nib_ntdi
     c                   endif
     c                   other
     c                   leavesr
     c                   endsl
      *
      * Avbryter dersom vare ikke ble funnet
      *
     c                   if        w_nobb = 0
     c                   goto      neste_vare
     c                   endif
      *
      * Flytter verdier til detaljlinje
      *
     c                   eval      d1ogrp = 0
     c                   eval      d1hgrp = 0
     c                   eval      d1ugrp = 0
     c                   eval      d1ldor = 0
     c                   eval      d1lnav = *blank
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
     c                   eval      d1vare = *blank
     c                   eval      d1nobb = 0
     c                   eval      d1enhe = *blank
     c                   eval      d1sapr = w_sapr
     c                   eval      d1var2 = *blank
     c                   eval      d1stat = *blank
      *
     c                   eval      w_eann = 0
      *
      * Henter vareinfo
      *
     c                   eval      jvarl3_nobb = w_nobb
     c     jvarl3_key    chain     jvarl3                             91
     c                   if        *in91 = *off
     c                   eval      d1ogrp = jvogrp
     c                   eval      d1hgrp = jvhgrp
     c                   eval      d1ugrp = jvugrp
     c                   eval      d1ldor = jvldor
     c                   eval      d1tek1 = jvtek1
     c                   eval      d1tek2 = jvtek2
     c                   eval      d1vare = jvvare
     c                   eval      d1nobb = jvnobb
     c                   eval      w_eann = jvean1
6.12 c                   eval      d1enhe = jvpenh
     c                   else
     c                   eval      d1stat = 'Vare ble ikke funnet i LVR'
     c                   endif
      *
      * Henter leverandørinfo
      *
     c                   if        d1ldor <> 0
     c                   eval      jlevl1_ldor = d1ldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      d1lnav = jlnavn
     c                   endif
     c                   endif
      *
      * Henter vare EVR v/Nobb-nr
      *
     c                   eval      vvarl3_nobb = w_nobb
     c     vvarl3_key    chain     vvarl3                             91
     c                   if        *in91 = *off
     c                   eval      d1var2 = vvvare
     c                   eval      d1stat = 'Enhet ble ikke funnet på ' +
     c                                      'varen i EVR'
     c                   endif
      *
      * Henter vare EVR v/EAN-nr
      *
     c                   if        w_eann <> 0 and
     c                             d1var2 = *blank
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
     c                   eval      d1var2 = w_eava
     c                   eval      d1stat = 'Vare identifisert i EVR ' +
     c                                      'v.h.a. EAN-nr'
     c                   endif
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      skriv_dtl
      *
     c     neste_vare    tag
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_dtl     begsr
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
      *
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_form
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare EVR
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      *
      * Henter firma
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
