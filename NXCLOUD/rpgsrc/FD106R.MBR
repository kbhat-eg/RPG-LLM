     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD106R
      * Beskrivelse . . : Ordre-linje, hurtigoppretting av varelinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050502 HKO  Nytt program
5.01  * R5.01 241002 AMD  Lagt om rutiner for oppdatering av lager
      *       161203 HKO  Tar hensyn til bruksrett-rabatt
      *                   Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
5.51  *       281005 BOF  Nullstilling av bestillingsreferanser
5.70  * PRGR  290908 sst  Prisgruppe
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.1a  * 6.10  070513 bof  EHF krever avrunding av FDSUMS
6.20  * 6.20  121110 NHO  Proj er nå alfa
6.21  * 6.20  190511 bof  Utvidet VL710R med utg.dato og l.dor
6.nu  * R6.30 030614 bsa  Endringer grunnet nummerutvidelser
6.30  * 6.20  170614 bkv  sjekk om tilbudspris finnes på annen ordre.
6.31  * 6.30  060515 bof  RKU2PF fases ut 6.30,          .
6.32  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.33  * 6.30  130317 bof  Feilrett. hvis pris fra tilbud, så kost.
6.34  * 6.30  191017 bof  Kode som hindrer at tilbudspriser blir vist (faak05)
6.35  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
7.00  *K00  260321 sst NXS-8235 Ovf alle varetyper fra ekst.ordre
7.01  * 6.30  020318 bsa  Hent inn leverandør på samme måten som på EDDT
7.02  * 7.xx  141112 bof  Hvis ordretype = intern, så benytt innkjøpspris
      *
8.01  * 8.00  090921 bsa  Legger innkjøpspris på ordrelinja
8.02  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.03  *K0000  310524 bsa  Legger på GTIN nummer ved opprettelse av varelinjer.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
6.30 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.35 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.01 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
7.02 fvot1l1    if   e           k disk    rename(vot1pfr:vot1l1r)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(fdfirm)
6.nu d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
     d p_ltyp          s                   like(fdltyp)
     d p_lety          s                   like(fdlety)
     d p_vare          s                   like(fdvare)
     d p_anta          s                   like(fdanta)
     d p_enhe          s                   like(fdenh1)
     d p_ldat          s                   like(fdldat)
5.70 d p_vtyp          s                   like(vvvtyp)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
      *
      * Definering av datastrukturer
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.02 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.02 d  hoprgr                        2    overlay(horec:71)
      *
      * Lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
6.31 d rkunl1_kund     s                   like(rkkund)
7.01 d vlagl1_vare     s                   like(vlvare)
7.01 d vlagl1_lage     s                   like(vllage)
7.02 d vot1l1_otyp     s                   like(va1typ)
      *
      * Definering av variable
      *
     d w_stat          s              1
      *
     d w_firm          s                   like(vvfirm)
6.nu d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_line          s                   like(fdline)
      *
     d w_raba          s                   like(fdsums)
     d w_sumr          s                   like(fdsums)
     d w_sums          s                   like(fdsums)
5.70 d w_prgr          s                   like(fdprgr)
6.21 d w_udat          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
6.35 d w_lv_nobb       s                   like(vvlnob)
6.35 d w_lv_modn       s                   like(vvlmod)
6.35 d w_lv_lldo       s                   like(vvlnle)
6.35 d w_lv_llva       s                   like(vvllva)
6.32 d w_numa          s              8
5.02 d w_lina          s              4
7.02 d w_sura          s                   like(fdsums)
7.02 d w_rabw          s             11  4
7.02 d w_enh1          s                   like(fdenh1)
7.02 d w_kopr          s                   like(fdkopr)
7.02 d w_inpr          s                   like(fdinpr)
7.02 d w_dato          s                   like(fdedat)
7.02 d w_lety          s                   like(fdlety)
7.02 d w_vare          s                   like(fdvare)
7.02 d w_kamp          s              5
8.03 d w_gtin          s                   like(fdeann)
8.03 d w_levr          s              6  0
      *
6.21 d b_inlr          s              1    inz(*off)
6.30 d b_kont          s              1    inz(*off)
6.30 d w_tilb          s                   like(fdsapr)
6.30 d w_tra1          s                   like(fdrab1)
6.30 d w_tra2          s                   like(fdrab2)
6.30 d w_enhe          s                   like(fdenh1)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
7.00  * Eksternt program
7.00  *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.00 d ukjent_vare_ok  s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom vare eller antall ikke er utfylt
      *
     c                   if        p_vare = *blank or
     c                             p_anta = 0
     c                   goto      avslutt
     c                   endif
      *  DEBUGSST
     c                   if        p_vare = '15297000'
     c                   eval      p_vare = p_vare
     c                   endif
      *
      * Avbryter dersom enhet ikke finnes på vare
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found
7.02 c                             and ukjent_vare_ok = *off
     c                   goto      avslutt
     c                   endif
      *
      * Avbryter dersom ordre-linje finnes fra før
      *
     c     fodtl1_key    chain     fodtl1
     c                   if        %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
7.02  *
7.02  *     Logikk fra HENTFAPR vedr ukjente varer
7.02  *
7.02 c                   if        ukjent_vare_ok = *on
7.00 c                   call      'FD120R'
7.00 c                   parm                    p_kode
7.00 c                   parm                    w_firm
7.00 c                   parm                    w_numm
7.00 c                   parm                    w_suff
7.00 c                   parm                    w_line
7.00 c                   parm                    p_ltyp
7.00 c                   parm                    p_lety
7.00 c                   parm                    p_vare
7.00 c                   parm                    p_anta
7.00 c                   endif
7.00  *
     c                   goto      avslutt
     c                   endif
5.70  *
5.70  * Kaller program for henting av Varetype fra lager/vare
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    folage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.35 c                   parm                    w_lv_nobb
6.35 c                   parm                    w_lv_modn
6.35 c                   parm                    w_lv_lldo
6.35 c                   parm                    w_lv_llva
      *
6.30 c                   if        b_kont = *off
6.30 c                   eval      w_tilb = 0
6.30 c                   eval      w_tra1 = 0
6.30 c                   eval      w_tra2 = 0
6.30 c                   eval      w_enhe = *blank
6.34 c                   if        faak05 = 0
6.30 c                   call      'FO751R  '
6.30 c                   parm                    w_firm
6.30 c                   parm                    fonumm
6.30 c                   parm                    fokund
6.30 c                   parm                    p_vare
6.30 c                   parm                    w_tilb
6.30 c                   parm                    w_tra1
6.30 c                   parm                    w_tra2
6.30 c                   parm                    w_enhe
6.30 c                   endif
6.34 c                   endif
5.70  *
5.70  * Kaller program for henting av prisgruppe fra lager/avdeling
5.70  *
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    folage
5.70 c                   parm                    foavde
5.70 c                   parm                    w_prgr
      *
      * Initierer ordre-linje
      *
     c                   clear                   fodtlur
      *
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = w_numm
     c                   eval      fdsuff = w_suff
     c                   eval      fdline = w_line
E    c                   eval      fdltyp = p_ltyp
     c                   eval      fdvare = p_vare
     c                   eval      fdanta = p_anta
     c                   eval      fdenh1 = p_enhe
     c                   eval      fdtek1 = vvtek1
     c                   eval      fdtek2 = vvtek2
     c                   eval      fdlety = p_lety
     c                   eval      fdpasl = fopasl
     c                   eval      fdldat = p_ldat
8.03  *
8.03 c                   exsr      hent_GTIN
      *
      * Henter inn priser og rabatter
      *
     c                   eval      hienhe = fdenh1
     c                   eval      hilety = fdlety
     c                   eval      hidekn = fdpasl
      *
     c                   exsr      hent_pris
      *
6.33 c                   eval      fdkopr = hokopr
7.02  *
7.02 c                   eval      fdprgr = hoprgr
7.02 c*8.01              if        va1int = 1
7.02 c                   exsr      hent_inpr
7.02 c*8.01              endif
      *
8.01 c                   if        fdinpr = 0
8.01 c                   eval      fdinpr = w_inpr
8.01 c                   endif
6.30 c                   if        w_tilb <> 0
6.30 c                   eval      fdsapr = w_tilb
6.30 c                   eval      fdrab1 = w_tra1
6.30 c                   eval      fdrab2 = w_tra2
6.30 c                   eval      fdkpri = 'A'
6.30 c                   else
     c                   eval      fdsapr = hosapr
7.02 c                   if        va1int = 1
7.02 c                   eval      fdkopr = w_inpr
7.02 c                   else
7.02 c                   eval      fdkopr = hokopr
7.02 c                   endif
     c                   eval      fdkpri = hokpri
7.02 c                   if        va1int = 1
7.02 c                   eval      w_sura = fdsapr - fdkopr
7.02 c                   if        fdsapr <> 0
7.02 c                   eval      w_rabw = w_sura /fdsapr
7.02 c                   else
7.02 c                   eval      w_rabw = 0
7.02 c                   endif
7.02 c                   eval(h)   fdrab1 = w_rabw * 100
7.02 c                   eval      fdrab2 = 0
7.02 c                   else
     c                   eval      fdrab1 = horab1
     c                   eval      fdrab2 = horab2
7.02 c                   endif
6.30 c                   endif
6.33 c                   eval      fdkopr = hokopr
     c                   eval      fdpasl = hodekn
     c                   eval      fdkamp = hokamp
     c                   eval      fdalme = hoalme
     c                   eval      fdomva = hoomva
     c                   eval      fdbrty = hobrty
     c                   eval      fdbrr1 = hobrr1
     c                   eval      fdbrr2 = hobrr2
     c                   eval      fdpriv = 0
5.70 c                   eval      fdprgr = hoprgr
      *
      * Beregning av totaler
      *
6.1a c*                  eval      w_sums = fdsapr * fdanta
6.1a c                   eval(h)   w_sums = fdsapr * fdanta
     c                   eval      w_sumr = 0
      *
      * Rabatt
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_raba = (w_sums * forabp) / 100
     c                   eval      w_sumr = w_raba
     c                   endif
      *
     c                   if        fdrab1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdrab2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdrab2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr1 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        fdbrr2 <> 0
     c                   eval(h)   w_raba = (w_sums - w_sumr) * fdbrr2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
      * Kostpris
      *
     c                   if        fdkopr = 0
     c                   eval      fdkopr = fdsapr * hopros
     c                   endif
      *
      * Legger inn totaler
      *
     c                   eval      fdsums = w_sums
     c                   eval      fdsumr = w_sumr
     c                   eval      fdsumk = fdkopr * fdanta
      *
      * Legger inn faste opplysninger fra vare og ordre
      *
7.01 c                   eval      vlagl1_vare = fdvare
7.01 c                   eval      vlagl1_lage = folage
7.01 c     vlagl1_key    chain     vlagl1
     c                   eval      fdotyp = footyp
     c                   eval      fdlage = folage
     c                   eval      fdkakk = 0
     c                   eval      fdkgeb = 0
     c                   eval      fdlass = 0
      *
     c                   eval      fdkund = fokund
     c                   if        folkun <> 0
     c                   eval      fdkund = folkun
     c                   endif
      *
     c                   eval      fdbenr = 0
5.51 c                   eval      fdbsuf = 0
5.51 c                   eval      fdblin = 0
     c                   eval      fdavde = 0
     c                   eval      fdkont = 0
     c                   eval      fdspes = 0
6.20 c**                 eval      fdproj = 0
6.20 c                   eval      fdproj = *blank
     c                   eval      fdakti = *blank
6.35 c                   if        w_lv_nobb > 0
6.35 c                   eval      fdnobb = w_lv_nobb
6.35 c                   eval      fdlvar = w_lv_llva
6.35 c                   else
     c                   eval      fdnobb = vvnobb
     c                   eval      fdlvar = vvlvar
6.35 c                   endif
     c                   eval      fdldor = vvldor
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
7.01 c                   if        vlbldo <> 0
7.01 c                   eval      fdldor = vlbldo
7.01 c                   endif
      *
      * Oversetter varetekst til store bokstaver dersom dette er valgt
      *
     c                   if        faahoy = 1
     c     lo:up         xlate     fdtek1        fdtek1
     c                   endif
      *
      * Skriver til ordre-linje
      *
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
      * Oppdatering av lager
      *
     c                   exsr      oppdat_lager
      *
      * Legger inn kode som sier at minst en linje er registrert
      *
     c                   eval      p_kode = *on
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter opplysninger om varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
      *
     c                   eval      hikund = fokund
     c                   if        folkun <> 0
     c                   eval      hikund = folkun
     c                   endif
      *
     c                   eval      hikpro = fokpro
     c                   eval      hivare = vvvare
     c                   eval      hidato = fobdat
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = fopask
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiprgr = w_prgr
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_lager  begsr
      *
     c                   if        faalag = 1 and
5.70 c                             p_vtyp = 'L'
     c                   else
     c                   goto      end_lager
     c                   endif
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
     c                   eval      hllage = folage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = footyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
3.12 c                   move      fdlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c     end_lager     endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for oppdat. av utg.fakt. logg
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     hent_inpr     begsr
7.02  *
7.02 c                   eval      w_vare = fdvare
7.02 c                   eval      w_enh1 = fdenh1
7.02 c                   eval      w_lety = fdlety
7.02 c                   call      'VP753R   '
7.02 c                   parm                    w_firm
7.02 c                   parm                    w_vare
7.02 c                   parm                    w_prgr
7.02 c                   parm                    w_enh1
7.02 c                   parm                    w_lety
7.02 c                   parm                    w_dato
7.02 c                   parm                    w_kopr
7.02 c                   parm                    w_inpr
7.02 c                   parm                    w_kamp
7.02 c                   if        w_inpr = 0
7.02 c                   eval      w_inpr = fdkopr
7.02 c                   endif
7.02  *
7.02 csr                 endsr
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  * Henter oGTIN informasjon om vare/enhet
8.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.03  *
8.03 c     hent_GTIN     begsr
8.03  *
8.03 c                   eval      w_gtin = 0
8.03 c                   eval      w_levr = 0
8.03 c                   eval      w_stat = *blanks
8.03  *
8.03 c                   call      'VE713R'
8.03 c                   parm                    w_firm
8.03 c                   parm                    w_levr
8.03 c                   parm                    p_vare
8.03 c                   parm                    p_enhe
8.03 c                   parm                    w_gtin
8.03 c                   parm                    w_stat
8.03  *
8.03 c                   eval      fdeann = w_gtin
8.03  *
8.03 c                   endsr
8.03  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_vare
     c                   parm                    p_anta
     c                   parm                    p_enhe
     c                   parm                    p_ldat
      *
      * Key for oppslag på status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
6.31  *
6.31  * Key for oppslag på kunde - sideregister
6.31  *
6.31 c     rkunl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    rkunl1_kund
      *
7.01  *
7.01  * Key for oppslag på lager
7.01  *
7.01 c     vlagl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    vlagl1_vare
7.01 c                   kfld                    vlagl1_lage
7.02  *
7.02  * Key for oppslag på ordretype sideregister
7.02  *
7.02 c     vot1l1_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    vot1l1_otyp
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
6.nu c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
7.00  *
7.00  * Endring 7.00 Ukjent vare fra ekst.ordre ok
7.00  *
7.00   CallP CO402R(l_filg:w_firm:0:'EKSTORDRE_TILORDRE_UKJENT_VARE_OK':
7.00                    co402_verdi1:co402_verdi2);
7.00   if co402_verdi1 = '1';
7.00     ukjent_vare_ok = *on;
7.00   endif;
7.00  *
      *
      * Legger inn type for oppdatering av lager (R=Registrering)
      *
5.01 c                   eval      hltype = ' '
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * Hent opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1r                            90
6.30  *
6.30  * sjekker om kunde har kontant-kunde satt på
6.30  *
6.30 c                   eval      b_kont = *off
6.31 c                   eval      rkunl1_kund = fokund
6.31 c     rkunl1_key    chain     rkunl1
6.31 c                   if        %found(rkunl1) and
6.31 c                             rkkoti = 1
6.31 c                   eval      b_kont = *on
6.31 c                   endif
7.02  *
7.02  * gjør oppslag i side-register av ordretype - sys inn i 6.30
7.02  *
7.02 c                   eval      vot1l1_otyp = footyp
7.02 c     vot1l1_key    chain     vot1l1
7.02 c                   if        not %found
7.02 c                   eval      va1int = 0
7.02 c                   endif
      *
      *
     c                   endsr
