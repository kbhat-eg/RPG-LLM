      /TITLE  Vedlikehold - Flytting av konto
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH109R                                              *
      * Beskrivelse . . : Vedlikehold av kontoplan - Flytting av konto        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.20     170104  Rettet feil vedr oppdatering av RHSAPF         KOB  *
      * R5.20     170104  Ved flytting er det bare hovedkonto som        KOB
      * R5.20     170104  etableres i kontoplan                          KOB
      * R5.40     170405  Fjernet feilkonto                              KOB
T5.40 * R5.40     150605  Leser RKLAL1 i stedet for RKLAPF.              HFL
6.10  * V6.10     210909  Lagt inn sporingsfelter                        BSA
6.30  * V6.30     040719  Utvidet saldofelter                            BSA
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fra04lu    uf   e           k disk    rename(ra04pfr:ra04lur)
     fra11lu    uf   e           k disk    rename(ra11pfr:ra11lur)
     fra14lu    uf   e           k disk    rename(ra14pfr:ra14lur)
     fra16lu    uf   e           k disk    rename(ra16pfr:ra16lur)
     fra23lu    uf   e           k disk    rename(ra23pfr:ra23lur)
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhovlu    uf a e           k disk    rename(rhovpfr:rhovlur)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
     frhtrl7    uf   e           k disk    rename(rhtrpfr:rhtrl7r)
      *
E5.40frklal1    uf   e           k disk    rename(rklapfr:rklal1r)
     frklalu    uf a e           k disk    rename(rklapfr:rklalur)
     f                                     prefix(rx:2)
      *
      *
     d txt             s             30    dim(3) ctdata perrcd(1)              Standard-TXT
6.30 d isa             s             15  2 dim(14)                              SALDOMATRISE INN
     d isu             s             11  2 dim(14)                              SALDOMATRISE UT
     d qsa             s             11  2 dim(14)                              SALDOMATRISE INN
     d qsu             s             11  2 dim(14)                              SALDOMATRISE UT
      *
     d s_spes          s                   like(rhspes)                               Spesifikasjon
     d s_avde          s                   like(rhavde)                               Avdeling
     d s_raar          s                   like(riraar)                               Avdeling
     d s_type          s                   like(ritype)                               Konto
      *
     d w_nkto          s                   like(rhkont)                               Konto
     d w_nsps          s                   like(rhspes)                               Spesifikasjon
     d w_navd          s                   like(rhavde)                               Avdeling
      *
     d w_gkto          s                   like(rhkont)                               Konto
     d w_gsps          s                   like(rhspes)                               Spesifikasjon
     d w_gavd          s                   like(rhavde)                               Avdeling
      *
     d w_kont          s                   like(rhkont)                               Konto
     d w_spes          s                   like(rhspes)                               Spesifikasjon
     d w_avde          s                   like(rhavde)                               Avdeling
      *
     d w_raar          s                   like(riraar)                               Avdeling
     d w_type          s                   like(ritype)                               Konto
      *
      * Definisjon av nøkkelfelt
      *
     d rhovl1_kont     s                   like(rhkont)                               Konto
     d rhovl1_spes     s                   like(rhspes)                               Spesifikasjon
     d rhovl1_avde     s                   like(rhavde)                               Avdeling
     d rhovlu_kont     s                   like(rhkont)                               Konto
     d rhovlu_spes     s                   like(rhspes)                               Spesifikasjon
     d rhovlu_avde     s                   like(rhavde)                               Avdeling
     d rhsal1_kont     s                   like(rikont)
     d rhsal1_spes     s                   like(rispes)
     d rhsal1_avde     s                   like(riavde)
     d rhsal1_raar     s                   like(riraar)
     d rhsal1_type     s                   like(ritype)
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
     d rhtrl7_kont     s                   like(rbkont)
      *
      * Informasjon til tekst2 - hovedbok
      *
     d                 ds
     d whrhds                        15
     d  whrhkt                        5    overlay(whrhds)
     d  whrhsp                        4    overlay(whrhds:7)
     d  whrhav                        4    overlay(whrhds:12)
      *
      * Informasjon til tekst2 - prosjekt
      *
     d                 ds
     d wprpds                        15
     d  wprpkt                        5    overlay(wprpds)
     d  wprpsp                        4    overlay(wprpds:7)
     d  wprpav                        4    overlay(wprpds:12)
      *
     d                 ds
     d wprqds                        15
     d  wprqkt                        5    overlay(wprqds)
     d  wprqsp                        4    overlay(wprqds:7)
     d  wprqav                        4    overlay(wprqds:12)
      *
     d                uds
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d whrht2          s                   like(rhtxt2)
     d whrit2          s                   like(rhtxt2)
     d w_firm          s                   like(l_firm)
     d wparam          s             22
      *
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *                     I   N   P   U   T                            +
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
      * Redefinering til array's
      *
     irhsal1r
     i              risa01                      isa(1)
     i              risa02                      isa(2)
     i              risa03                      isa(3)
     i              risa04                      isa(4)
     i              risa05                      isa(5)
     i              risa06                      isa(6)
     i              risa07                      isa(7)
     i              risa08                      isa(8)
     i              risa09                      isa(9)
     i              risa10                      isa(10)
     i              risa11                      isa(11)
     i              risa12                      isa(12)
     i              risa13                      isa(13)
     i              risa00                      isa(14)
      *
     irhsalur
     i              risa01                      isa(1)
     i              risa02                      isa(2)
     i              risa03                      isa(3)
     i              risa04                      isa(4)
     i              risa05                      isa(5)
     i              risa06                      isa(6)
     i              risa07                      isa(7)
     i              risa08                      isa(8)
     i              risa09                      isa(9)
     i              risa10                      isa(10)
     i              risa11                      isa(11)
     i              risa12                      isa(12)
     i              risa13                      isa(13)
     i              risa00                      isa(14)
      *
      /eject
      *
      * Hovedrutine
      *
     c                   exsr      opd_faste
     c                   exsr      opd_kunkat
     c                   exsr      opd_levkat
     c                   exsr      opd_valuta
     c                   exsr      opd_virkso
     c                   exsr      opd_hopost
     c                   exsr      opd_hosald
     c                   exsr      opd_kladde
     c                   exsr      opd_kontop
      *
     c                   move      *on           *inlr
     c                   return
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av faste konti                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_faste     begsr
      *
     c                   move      l_firm        radfir
     c     ra04lu_key    chain     ra04lu
      *
     c                   if        %found
      *
      * Sjekk konto for utgående mva
      *
     c                   if        w_gkto = radk01
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads01
     c                   move      w_nsps        rads01
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada01
     c                   move      w_navd        rada01
     c                   endif
      *
     c                   move      w_nkto        radk01
     c                   endif
      *
      * Sjekk konto for inngående mva
      *
     c                   if        w_gkto = radk02
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads01
     c                   move      w_nsps        rads02
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada02
     c                   move      w_navd        rada02
     c                   endif
      *
     c                   move      w_nkto        radk02
     c                   endif
      *
      * Sjekk konto for investeringsavgift
      *
     c                   if        w_gkto = radk03
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads03
     c                   move      w_nsps        rads03
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada03
     c                   move      w_navd        rada03
     c                   endif
     4*
     c                   move      w_nkto        radk03
     c                   eval      *in51 = *on
     c                   endif
      *
      * Sjekk konto for fylkesskattesjefen
      *
     c                   if        w_gkto = radk04
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads04
     c                   move      w_nsps        rads04
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada04
     c                   move      w_navd        rada04
     c                   endif
      *
     c                   move      w_nkto        radk04
     c                   endif
      *
      * Sjekk konto for kunder
      *
     c                   if        w_gkto = radk05
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads05
     c                   move      w_nsps        rads05
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada05
     c                   move      w_navd        rada05
     c                   endif
      *
     c                   move      w_nkto        radk05
     c                   eval      *in51 = *on
     c                   endif
      *
      * Sjekk konto for leverandører
      *
     c                   if        w_gkto = radk06
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads06
     c                   move      w_nsps        rads06
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada06
     c                   move      w_navd        rada06
     c                   endif
      *
     c                   move      w_nkto        radk06
     c                   endif
      *
      * Sjekk konto for kontantrabatt kunder
      *
     c                   if        w_gkto = radk07
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads07
     c                   move      w_nsps        rads07
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada07
     c                   move      w_navd        rada07
     c                   endif
      *
     c                   move      w_nkto        radk07
     c                   endif
      *
      * Sjekk konto for renteinntekter
      *
     c                   if        w_gkto = radk11
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads11
     c                   move      w_nsps        rads11
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada11
     c                   move      w_navd        rada11
     c                   endif
      *
     c                   move      w_nkto        radk11
     c                   endif
      *
      * Sjekk konto for agio
      *
     c                   if        w_gkto = radk12
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads12
     c                   move      w_nsps        rads12
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada12
     c                   move      w_navd        rada12
     c                   endif
      *
     c                   move      w_nkto        radk12
     c                   endif
      *
      * Sjekk konto for disagio
      *
     c                   if        w_gkto = radk13
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads13
     c                   move      w_nsps        rads13
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada13
     c                   move      w_navd        rada13
     c                   endif
      *
     c                   move      w_nkto        radk13
     c                   endif
      *
      * Sjekk konto for øreavrunding
      *
     c                   if        w_gkto = radk14
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads14
     c                   move      w_nsps        rads14
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada14
     c                   move      w_navd        rada14
     c                   endif
      *
     c                   move      w_nkto        radk14
     c                   endif
      *
      * Sjekk konto for bank
      *
     c                   if        w_gkto = radk15
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads15
     c                   move      w_nsps        rads15
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada15
     c                   move      w_navd        rada15
     c                   endif
      *
     c                   move      w_nkto        radk15
     c                   endif
      *
      * Sjekk konto for kontantrabatt leverandører
      *
     c                   if        w_gkto = radk16
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads16
     c                   move      w_nsps        rads16
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada16
     c                   move      w_navd        rada16
     c                   endif
      *
     c                   move      w_nkto        radk16
     c                   endif
      *
      * Sjekk konto for kasse
      *
     c                   if        w_gkto = radk17
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads17
     c                   move      w_nsps        rads17
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada17
     c                   move      w_navd        rada17
     c                   endif
      *
     c                   move      w_nkto        radk17
     c                   endif
      *
      * Sjekk konto for inkassoprovisjon
      *
     c                   if        w_gkto = radk19
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads19
     c                   move      w_nsps        rads19
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada19
     c                   move      w_navd        rada19
     c                   endif
      *
     c                   move      w_nkto        radk19
     c                   endif
      *
      * Sjekk konto for betalingsgebyr
      *
     c                   if        w_gkto = radk20
      *
     c                   if        w_nsps <> *zero  and
     c                             w_gsps = rads20
     c                   move      w_nsps        rads20
     c                   endif
      *
     c                   if        w_navd <> *zero  and
     c                             w_gavd = rada20
     c                   move      w_navd        rada20
     c                   endif
      *
     c                   move      w_nkto        radk20
     c                   endif
      *
     c     kouk41        tag
      *
     c                   update    ra04lur
      *
     c                   endif
      *
     c                   unlock    ra04lu
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av kundekategori                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_kunkat    begsr
      *
     c     ra11lu_key    setll     ra11lu                                 61
     c     ra11lu_key    reade     ra11lu                                 61
      *
     c                   dow       *in61=*off
      *
     c                   if        w_gkto = rakkto
      *
     c                   if        w_nsps <> *zero and
     c                             w_gsps = raksps
     c                   move      w_nsps        raksps
     c                   endif
      *
     c                   if        w_navd <> *zero and
     c                             w_gavd = rakavd
     c                   move      w_navd        rakavd
     c                   endif
      *
     c                   move      w_nkto        rakkto
      *
     c                   update    ra11lur
     c                   endif
      *
     c     ra11lu_key    reade     ra11lu                                 61
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av leverndørkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_levkat    begsr
      *
     c     ra14lu_key    setll     ra14lu
     c     ra14lu_key    reade     ra14lu                                 61
      *
     c                   dow       *in61=*off
      *
     c                   if        w_gkto = rankto
      *
     c                   if        w_nsps <> *zero and
     c                             w_gsps = ransps
     c                   move      w_nsps        ransps
     c                   endif
      *
     c                   if        w_navd <> *zero and
     c                             w_gavd = ranavd
     c                   move      w_navd        ranavd
     c                   endif
      *
     c                   move      w_nkto        rankto
     c                   update    ra14lur
     c                   endif
      *
     c     ra14lu_key    reade     ra14lu                                 61
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av valutakoder                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_valuta    begsr
      *
     c     ra16lu_key    setll     ra16lu
     c     ra16lu_key    reade     ra16lu                                 61
      *
     c                   dow       *in61 = *off
      *
     c                   if        w_gkto = rapkto
      *
     c                   if        w_nsps <> *zero and
     c                             w_gsps = rapsps
     c                   move      w_nsps        rapsps
     c                   endif
      *
     c                   if        w_navd <> *zero and
     c                             w_gavd = rapavd
     c                   move      w_navd        rapavd
     c                   endif
      *
     c                   move      w_nkto        rapkto
     c                   update    ra16lur
     c                   endif
      *
     c     ra16lu_key    reade     ra16lu                                 61
      *
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av virksomhetskoder                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_virkso    begsr
      *
     c     ra23lu_key    setll     ra23lu
     c     ra23lu_key    reade     ra23lu                                 61
      *
     c                   dow       *in61 = *off
      *
     c                   if        w_gkto = rawkto
      *
     c                   if        w_nsps <> *zero and
     c                             w_gsps = rawsps
     c                   move      w_nsps        rawsps
     c                   endif
      *
     c                   if        w_navd <> *zero and
     c                             w_gavd = rawavd
     c                   move      w_navd        rawavd
     c                   endif
      *
     c                   move      w_nkto        rawkto
     c                   update    ra23lur
     c                   endif
      *
     c     ra23lu_key    reade     ra23lu                                 61
      *
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av hovedboksposter                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_hopost    begsr
      *
     c                   eval      rhtrl7_kont = w_gkto
     c     rhtrl7_key    setll     rhtrl7r
     c     rhtrl7_key    reade     rhtrl7r                                61
      *
     c                   dow       *in61 = *off
      *
     c                   if        rbspes <> w_gsps and
     c                             w_gsps <> 0
     c                   goto      hopost_ut
     c                   endif
      *
     c                   if        rbavde <> w_gavd and
     c                             w_gavd <> 0
     c                   goto      hopost_ut
     c                   endif
      *
      * Hvis gml. bærer og ny bærer begge = 0
      * behold bærer
      *
     c                   if        w_gsps = 0 and
     c                             w_nsps = 0
     c                   eval      w_spes = rbspes
     c                   endif
      *
      * Hvis gml. bærer oppgitt,
      * flytt til ny bærer
      *
     c                   if        w_gsps <> 0
     c                   eval      w_spes = w_nsps
     c                   endif
      *
      * Hvis gml. bærer = 0 og ny bærer utfylt
      * flytt til ny bærer
      *
     c                   if        w_gsps =  0 and
     c                             w_nsps <> 0
     c                   eval      w_spes = w_nsps
     c                   endif
      *
      * Hvis gml. avd. og ny avd. begge = 0
      * behold avdeling
      *
     c                   if        w_gavd = 0 and
     c                             w_navd = 0
     c                   eval      w_avde = rbavde
     c                   endif
      *
      * Hvis gml. avd. oppgitt,
      * flytt til ny avd.
      *
     c                   if        w_gavd <> 0
     c                   eval      w_avde = w_navd
     c                   endif
      *
      * Hvis gml. avd. = 0 og ny avd. utfylt
      * flytt til ny avd.
      *
     c                   if        w_gavd =  0 and
     c                             w_navd <> 0
     c                   eval      w_avde = w_navd
     c                   endif
      *
     c                   eval      rbkont = w_nkto
     c                   eval      rbspes = w_spes
     c                   eval      rbavde = w_avde
6.10 c                   time                    rbedat
6.10 c                   time                    rbetim
6.10 c                   eval      rbeusr = l_user
      *
     c                   update    rhtrl7r
      *
     c     hopost_ut     tag
      *
     c     rhtrl7_key    reade     rhtrl7r                                61
      *
     c                   enddo
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av hovedbokssaldo                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_hosald    begsr
      *
     c                   eval      rhsal1_kont = w_gkto
     c     rhsal1_key    setll     rhsal1r
     c     rhsal1_key    reade     rhsal1r                                61
      *
     c                   dow       *in61 = *off
      *
     c                   if        rispes <> w_gsps and
     c                             w_gsps <> 0
     c                   goto      hosald_ut
     c                   endif
      *
     c                   if        riavde <> w_gavd and
     c                             w_gavd <> 0
     c                   goto      hosald_ut
     c                   endif
      *
      * Ta vare på informasjon
      *
     c                   eval      isu = isa
     c                   eval      s_spes = rispes
     c                   eval      s_avde = riavde
     c                   eval      s_raar = riraar
     c                   eval      s_type = ritype
      *
      * Hvis gml. bærer og ny bærer begge = 0
      * behold bærer
      *
     c                   if        w_gsps = 0 and
     c                             w_nsps = 0
     c                   eval      w_spes = rispes
     c                   endif
      *
      * Hvis gml. bærer oppgitt,
      * flytt til ny bærer
      *
     c                   if        w_gsps <> 0
     c                   eval      w_spes = w_nsps
     c                   endif
      *
      * Hvis gml. bærer = 0 og ny bærer utfylt
      * flytt til ny bærer
      *
     c                   if        w_gsps =  0 and
     c                             w_nsps <> 0
     c                   eval      w_spes = w_nsps
     c                   endif
      *
      * Hvis gml. avd. og ny avd. begge = 0
      * behold avdeling
      *
     c                   if        w_gavd = 0 and
     c                             w_navd = 0
     c                   eval      w_avde = riavde
     c                   endif
      *
      * Hvis gml. avd. oppgitt,
      * flytt til ny avd.
      *
     c                   if        w_gavd <> 0
     c                   eval      w_avde = w_navd
     c                   endif
      *
      * Hvis gml. avd. = 0 og ny avd. utfylt
      * flytt til ny avd.
      *
     c                   if        w_gavd =  0 and
     c                             w_navd <> 0
     c                   eval      w_avde = w_navd
     c                   endif
      *
      * Oppdater record
      *
     c                   eval      rhsalu_kont = w_nkto
     c                   eval      rhsalu_spes = w_spes
     c                   eval      rhsalu_avde = w_avde
     c                   eval      rhsalu_raar = s_raar
     c                   eval      rhsalu_type = s_type
      *
     c     rhsalu_key    chain     rhsalur
      *
6.10 c                   time                    riedat
6.10 c                   time                    rietim
6.10 c                   eval      rieusr = l_user
      *
     c                   if        %found
     c                   eval      isa = isa + isu
     c                   update    rhsalur
      *
     c                   else
      *
     c                   eval      rifirm = w_firm
     c                   eval      rikont = w_nkto
     c                   eval      rispes = w_spes
     c                   eval      riavde = w_avde
     c                   eval      riraar = s_raar
     c                   eval      ritype = s_type
     c                   eval      isa = isu
6.10 c                   time                    riodat
6.10 c                   time                    riotim
6.10 c                   eval      riousr = l_user
     c                   write     rhsalur
     c                   endif
      *
      * Slett gammel record
      *
     c                   eval      rhsalu_kont = w_gkto
     c                   eval      rhsalu_spes = s_spes
     c                   eval      rhsalu_avde = s_avde
     c                   eval      rhsalu_raar = s_raar
     c                   eval      rhsalu_type = s_type
      *
     c     rhsalu_key    chain     rhsalur
      *
     c                   if        %found
     c                   delete    rhsalur
     c                   endif
      *
     c     hosald_ut     tag
      *
     c     rhsal1_key    reade     rhsal1r                                61
      *
     c                   enddo
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av kladdeblokk                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_kladde    begsr
      *
     c                   move      l_firm        rxfirm
     c                   move      'H'           rxsyst
     c                   eval      rxpros = *zeros
     c                   eval      rxakti = *blank
     c                   move      w_gkto        rxkont
     c                   move      w_gsps        rxspes
     c                   move      w_gavd        rxavde
T5.40c                   eval      rxtist = *loval
     c                   eval      rxlinj = *zeros
E5.40c     rxkey         setll     rklal1r
      *
     c     rxles         tag
      *
E5.40c                   read      rklal1r                                10
      *
     c                   if        *in10 = *on or
     c                             rxfirm > l_firm
     c                   goto      kladde_ut
     c                   endif
      *
     c                   if        rxkont <> w_gkto or
     c                             rxsyst <> 'H'
     c                   goto      rxles
     c                   endif
      *
     c                   move      w_nkto        rxkont
      *
     c                   if        w_nsps <> *zero
     c                   if        w_gsps = rxspes
     c                   move      w_nsps        rxspes
     c                   else
     c                   goto      rxles
     c                   endif
     c                   endif
      *
     c                   if        w_navd <> *zero
     c                   if        w_gavd = rxavde
     c                   move      w_navd        rxavde
     c                   else
     c                   goto      rxles
     c                   endif
     c                   endif
      *
     c     rxkey         chain     rklalur
      *
     c                   if        %found
6.10 c                   time                    rxodat
6.10 c                   time                    rxotim
6.10 c                   eval      rxousr = l_user
6.10 c                   time                    rxedat
6.10 c                   time                    rxetim
6.10 c                   eval      rxeusr = l_user
     c                   write     rklalur
E5.40c                   delete    rklal1r
     c                   endif
      *
     c                   goto      rxles
      *
     c     kladde_ut     tag
      *
     c                   unlock    rklalu
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av kontoplan                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opd_kontop    begsr
      *
     c                   eval      rhovl1_kont = w_gkto
      *
     c     rhovl1_key    setll     rhovl1r                                61
     c     rhovl1_key    reade     rhovl1r                                61
      *
     c                   dow       *in61 = *off
      *
     c                   move      w_gkto        whrhkt
     c                   move      w_gsps        whrhsp
     c                   move      w_gavd        whrhav
      *
     c                   if        w_gsps <> *zero or
     c                             w_gavd <> *zero or
     c                             w_gkto <> w_nkto
      *
     c                   eval      rhovlu_kont = w_gkto
     c                   eval      rhovlu_spes = w_gsps
     c                   eval      rhovlu_avde = w_gavd
     c     rhovlu_key    chain     rhovlur
      *
     c                   if        %found
     c                   delete    rhovlur
     c                   endif
      *
     c                   endif
      *
      * Oppdater ny konto
      *
     c                   eval      rhovlu_kont = w_nkto
     c                   eval      rhovlu_spes = 0
     c                   eval      rhovlu_avde = 0
      *
     c     rhovlu_key    chain     rhovlur
      *
6.10 c                   time                    rhedat
6.10 c                   time                    rhetim
6.10 c                   eval      rheusr = l_user
      *
     c                   if        %found
     c                   move      txt(2)        rhtxt2
     c                   move      whrhds        rhtxt2
     c                   update    rhovlur
      *
     c                   else
      *
     c                   move      txt(1)        rhtxt2
     c                   move      whrhds        rhtxt2
     c                   eval      rhkont = w_nkto
     c                   eval      rhspes = 0
     c                   eval      rhavde = 0
6.10 c                   time                    rhodat
6.10 c                   time                    rhotim
6.10 c                   eval      rhousr = l_user
     c                   write     rhovlur
     c                   endif
      *
     c                   eval      whrhds = *blank
      *
     c     rhovl1_key    reade     rhovl1r                                61
      *
     c                   enddo
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_gkto
     c                   parm                    w_gsps
     c                   parm                    w_gavd
     c                   parm                    w_nkto
     c                   parm                    w_nsps
     c                   parm                    w_navd
      *
     c                   move      l_firm        w_firm
      *
      * Key for faste konti
      *
     c     ra04lu_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundekategori
      *
     c     ra11lu_key    klist
     c                   kfld                    w_firm
      *
      * Key for leverandørkategori
      *
     c     ra14lu_key    klist
     c                   kfld                    w_firm
      *
      * Key for valutakoder
      *
     c     ra16lu_key    klist
     c                   kfld                    w_firm
      *
      * Key for virksomhetskoder
      *
     c     ra23lu_key    klist
     c                   kfld                    w_firm
      *
      * Key for kontoplan
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
      *
      * Key for kontoplan
      *
     c     rhovlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlu_kont
     c                   kfld                    rhovlu_spes
     c                   kfld                    rhovlu_avde
      *
      * Key for saldomatrise
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
      *
      * Key for saldomatrise
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for hovedboks-poster (RHTRL7)
      *
     c     rhtrl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrl7_kont
      *
      * Key for kladdeblokk
      *
     c     rxkey         klist
     c                   kfld                    w_firm
     c                   kfld                    rxsyst
     c                   kfld                    rxpros
     c                   kfld                    rxakti
     c                   kfld                    rxkont
     c                   kfld                    rxspes
     c                   kfld                    rxavde
T5.40c                   kfld                    rxtist
     c                   kfld                    rxlinj
      *
     c                   endsr
      ******
** TXT - STANDARD-TXT
Flyttet fra :                 01
Tilført fra :                 02
12345678901234567890123456789003
