     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH100R
      * Beskrivelse . . : Kontoplan, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
T5.20 * R5.00 211003 HFL  Rettet slik at notatblokk virker.
T5.20 * R5.20 170104 KOB  Tillater sletting av konto m/ avd. eller bærer
T5.30 * R5.30 090804 HFL  To nye felt: Refnr og tekst.
T5.30 *                   Brukes i totalkontoplan.
T5.40 * V5.40 040805 HFL  Utvidet parameterliste for RF020R
T5.40 * V5.40 140507 HFL  Endret test ved sletting av konto.
T5.60 * V5.60 200508 KOB  Lagt inn muligheter for kontoplangrupper for workflow
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
6.21  * V6.21 210212 AMD  Endringer pga. prosjekt
7.xx  * 7.00  020216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.02  * 7.00  080120 sst  Oppsalg mot standard kontoplan  RHSTPF       de DDS
7.03  * 7.00  140120 sst  Sjekk om alle kontoer har std.kto ved Avsluttde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhovl2    if   e           k disk    rename(rhovpfr:rhovl2r)
     frhovl3    if   e           k disk    rename(rhovpfr:rhovl3r)
     frhovl4    if   e           k disk    rename(rhovpfr:rhovl4r)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
     frhovlu    uf a e           k disk    rename(rhovpfr:rhovlur)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
7.02 frhstl1    if   e           k disk    rename(rhstpfr:rhstl1r)
T5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
      *
     frh100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
     d rhovl2_alfa     s                   like(rhalfa)
     d rhovl2_spes     s                   like(rhspes)
     d rhovl2_avde     s                   like(rhavde)
     d rhovl3_kont     s                   like(rhkont)
     d rhovl3_spes     s                   like(rhspes)
     d rhovl3_avde     s                   like(rhavde)
     d rhovl4_kont     s                   like(rhkont)
     d rhovl4_spes     s                   like(rhspes)
     d rhovl4_avde     s                   like(rhavde)
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_spes     s                   like(rhspes)
     d rhovlr_avde     s                   like(rhavde)
     d rhovlu_kont     s                   like(rhkont)
     d rhovlu_spes     s                   like(rhspes)
     d rhovlu_avde     s                   like(rhavde)
     d rhsal1_kont     s                   like(rikont)
     d rhsal1_spes     s                   like(rispes)
     d rhsal1_avde     s                   like(riavde)
7.02 d rhstl1_kont     s                   like(shkont)
      *
T5.40d rklal2_syst     s                   like(rxsyst)
T5.40d rklal2_pros     s                   like(rxpros)
T5.40d rklal2_akti     s                   like(rxakti)
T5.40d rklal2_kont     s                   like(rxkont)
T5.40d rklal2_spes     s                   like(rxspes)
T5.40d rklal2_avde     s                   like(rxavde)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.21 d w_vala          s              1
      *
     d w_firm          s                   like(rhfirm)
      *
     d p_stat          s              1
     d p_ekod          s                   like(rhmvak)
     d p_mkod          s                   like(rhbnØk)
     d p_okod          s                   like(rhmngd)
     d p_pkod          s                   like(rhvalk)
     d p_tx20          s             20
     d p_tx30          s             30
7.02 d p_kont          s                   like(shkont)
7.02 d p_tx60          s                   like(shtext)
      *
     d wpkont          s                   like(rhkont)
     d wpspes          s                   like(rhspes)
     d wpavde          s                   like(rhavde)
     d wpraar          s              4  0
     d wprper          s              2  0
     d wpsude          s              1
7.03 d w_m1            s             50
7.03 d w_m2            s             50
7.03 d w_sv            s              1
7.03 d w_f3            s              1  0
      *
      * Parameter - program RF020R
6.21 i/COPY QCPYLESRC,RF020PAR
6.21 d*                ds
6.21 d*rxulda                       175
6.21 d* rxstat                        1    overlay(rxulda)
6.21 d* rxfirm                        3  0 overlay(rxulda:2)
6.21 d* rxsyst                        1    overlay(rxulda:5)
6.21 d* rxpros                        6  0 overlay(rxulda:6)
6.21 d* rxakti                        3    overlay(rxulda:12)
6.21 d* rxf001                        3    overlay(rxulda:15)
6.21 d* rxkont                        6  0 overlay(rxulda:18)
6.21 d* rxspes                        4  0 overlay(rxulda:24)
6.21 d* rxavde                        4  0 overlay(rxulda:28)
6.21 d* rxlinj                        4  0 overlay(rxulda:32)
6.21 d* rxtgrp                        3    overlay(rxulda:36)
6.21 d* rxtext                       78    overlay(rxulda:39)
6.21 d* rxf002                       13    overlay(rxulda:117)
6.21 d* rxvalg                        1  0 overlay(rxulda:130)
6.21 d* rxtist                         z   overlay(rxulda:131)
6.21 d* rxbiln                        6  0 overlay(rxulda:158)
6.21 d* rxrper                        2  0 overlay(rxulda:164)
6.21 d* rxraar                        4  0 overlay(rxulda:166)
6.21 d* rxonr                         6  0 overlay(rxulda:170)
     d rxupar          s                   like(rxulda)
      *
     d s_kont          s                   like(rhkont)
     d s_spes          s                   like(rhspes)
     d s_avde          s                   like(rhavde)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kont <> 0 or
     c                             b2spes <> 0 or
     c                             b2avde <> 0 or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
7.03  *
7.03  *  Sjekk om alle kontoer har referanse til std. kontoplan
7.03  *
7.03 c                   eval      rhovl1_kont = *zero
7.03 c                   eval      rhovl1_spes = *zero
7.03 c                   eval      rhovl1_avde = *zero
7.03 c     rhovl1_key    setll     rhovl1
7.03 c                   read      rhovl1
7.03 c                   dow       not %eof(rhovl1) and
     c                             rhfirm = w_firm
7.03 c                   if        rhrefn = *zero
7.03 c                   eval      w_m1 = 'En eller flere kontoer mangler +
7.03 c                                     standard konto'
7.03 c                   eval      w_m2 = 'Den første er konto ' +
7.03 c                                    %trim(%char(rhkont))
7.03 c                   call      'AA007R'
7.03 c                   parm                    w_m1
7.03 c                   parm                    w_m2
7.03 c                   parm                    w_sv
7.03 c                   parm                    w_f3
7.03 c                   if        w_sv <> 'J'
7.03 c                   goto      b2taga
7.03 c                   else
7.03 c                   leave
7.03 c                   endif
7.03 c                   endif
7.03 c                   read      rhovl1
7.03 c                   enddo
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhovl1_kont = b1kont
     c                   eval      rhovl1_spes = b1spes
     c                   eval      rhovl1_avde = b1avde
     c     rhovl1_key    setll     rhovl1
     c                   when      w_seqe = 'A'
     c                   eval      rhovl3_avde = b1avde
     c                   eval      rhovl3_kont = b1kont
     c                   eval      rhovl3_spes = b1spes
     c     rhovl3_key    setll     rhovl3
     c                   when      w_seqe = 'B'
     c                   eval      rhovl4_spes = b1spes
     c                   eval      rhovl4_kont = b1kont
     c                   eval      rhovl4_avde = b1avde
     c     rhovl4_key    setll     rhovl4
     c                   when      w_seqe = 'N'
     c                   eval      rhovl2_alfa = b1alfa
     c     rhovl2_key    setll     rhovl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på konto
      *
     c                   if        b2kont <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      rhovl1_kont = b2kont
     c                   eval      rhovl1_spes = b2spes
     c                   eval      rhovl1_avde = b2avde
     c     rhovl1_key    setll     rhovl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på avdeling
      *
     c                   if        b2avde <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      rhovl3_kont = *zeros
     c                   eval      rhovl3_spes = *zeros
     c                   eval      rhovl3_avde = b2avde
     c     rhovl3_key    setll     rhovl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kostnadsbærer
      *
     c                   if        b2spes <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      rhovl4_kont = *zeros
     c                   eval      rhovl4_spes = b2spes
     c                   eval      rhovl4_avde = *zeros
     c     rhovl4_key    setll     rhovl4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på alfabetisk nøkkel
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      rhovl2_alfa = b2alfa
     c     rhovl2_key    setll     rhovl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kont = 0
     c                   eval      b2spes = 0
     c                   eval      b2avde = 0
     c                   eval      b2alfa = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kont = b1kont
     c                   eval      k1spes = b1spes
     c                   eval      k1avde = b1avde
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kont = b1kont
     c                   eval      d1spes = b1spes
     c                   eval      d1avde = b1avde
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold posteringer
      *
     c                   if        b1valg = 8
     c                   eval      wpraar = *year
     c                   eval      wprper = 0
     c                   eval      wpkont = b1kont
     c                   eval      wpspes = b1spes
     c                   eval      wpavde = b1avde
     c                   eval      wpsude = 'S'
      *
     c                   call      'RH110R'
     c                   parm                    wpsude
     c                   parm                    wpraar
     c                   parm                    wprper
     c                   parm                    wpkont
     c                   parm                    wpspes
     c                   parm                    wpavde
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
      *
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1kont = b2kont
     c                   eval      c1avde = b2avde
     c                   eval      c1spes = b2spes
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
kob  c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1kont = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
kob  c***                if        c1kont = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rhovlr_kont = c1kont
     c                   eval      rhovlr_spes = c1spes
     c                   eval      rhovlr_avde = c1avde
     c     rhovlr_key    chain     rhovlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1kont = 0
     c                   eval      c1avde = 0
     c                   eval      c1spes = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kont = 0
     c                   eval      b2avde = 0
     c                   eval      b2spes = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rhovlr_kont = c1kont
     c                   eval      rhovlr_spes = c1spes
     c                   eval      rhovlr_avde = c1avde
     c     rhovlr_key    chain     rhovlr
      *
     c                   if        %found
     c                   eval      c2text = rhtext
     c                   eval      c2txt2 = rhtxt2
     c                   eval      c2alfa = rhalfa
     c                   eval      c2mvak = rhmvak
     c                   eval      c2mvao = rhmvao
     c                   eval      c2mvag = rhmvag
     c                   eval      c2bnØk = rhbnØk
     c                   eval      c2valk = rhvalk
     c                   eval      c2mngd = rhmngd
     c                   eval      c2proj = rhproj
     c                   eval      c2bÆrk = rhbÆrk
     c                   eval      c2avdk = rhavdk
     c                   eval      c2nØkk = rhnØkk
     c                   eval      c2sprk = rhsprk
T5.30c                   eval      c2refn = rhrefn
T5.30c                   eval      c2reft = rhreft
7.02 c                   eval      rhstl1_kont = rhrefn
7.02 c     rhstl1_key    chain     rhstl1
7.02 c                   if        %found(rhstl1)
7.02 c                   eval      c2reft = shtext
7.02 c                   endif
T5.60c                   eval      c2kpg1 = rhkpg1
T5.60c                   eval      c2kpg2 = rhkpg2
T5.60c                   eval      c2kpg3 = rhkpg3
T5.60c                   eval      c2kpg4 = rhkpg4
T5.60c                   eval      c2kpg5 = rhkpg5
     c                   eval      c2eusr = rheusr
T5.30c                   if        rhedat = *loval
T5.30c                   move      *zeros        c2edat
T5.30c                   else
     c     *dmy          move      rhedat        c2edat
T5.30c                   endif
     c                   exsr      kontroll
     c                   else
     c                   eval      c2text = *blank
     c                   eval      c2txt2 = *blank
     c                   eval      c2alfa = *blank
     c                   eval      c2mvak = *blank
     c                   eval      c2etxt = *blank
     c                   eval      c2mvao = *blank
     c                   eval      c2mvag = *blank
     c                   eval      c2bnØk = *zeros
     c                   eval      c2mtxt = *blank
     c                   eval      c2valk = *blank
     c                   eval      c2pmna = *blank
     c                   eval      c2mngd = *zeros
     c                   eval      c2otxt = *blank
     c                   eval      c2proj = *blank
     c                   eval      c2bÆrk = *blank
     c                   eval      c2avdk = *blank
     c                   eval      c2nØkk = *zeros
     c                   eval      c2sprk = *blank
T5.30c                   eval      c2refn = *zeros
T5.30c                   eval      c2reft = *blank
T5.60c                   eval      c2kpg1 = *blank
T5.60c                   eval      c2kpg2 = *blank
T5.60c                   eval      c2kpg3 = *blank
T5.60c                   eval      c2kpg4 = *blank
T5.60c                   eval      c2kpg5 = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1kont = k1kont
     c                   eval      c1avde = k1avde
     c                   eval      c1spes = k1spes
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
T5.40 * Finnes notatark på kontoen?
      *
T5.40c                   eval      rklal2_syst = 'H'
6.21 c                   eval      rklal2_pros = *blanks
T5.40c                   eval      rklal2_akti = *blanks
T5.40c                   eval      rklal2_kont = c1kont
T5.40c                   eval      rklal2_spes = c1spes
T5.40c                   eval      rklal2_avde = c1avde
T5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.40c                   if        *in94 = *on
T5.40c                   eval      C2notb = 'NOTATARK'
T5.40c                   endif
      *
     c                   exfmt     c2bld
      *
7.02 c                   eval      *in37 = *off
7.02sc                   eval      *in38 = *off
7.02sc                   eval      *in81 = *off
7.02  *
S5.20c*                  if        *inkc or *inkl or *in30
T5.20c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppkall av notatark.
      *
     c                   if        *inkb = *on
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'H'
6.21 c                   eval      rxpros = *blank
     c                   eval      rxakti = *blank
E5.20c                   eval      rxkont = c1kont
E5.20c                   eval      rxspes = c1spes
E5.20c                   eval      rxavde = c1avde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
6.21 c                   move      b1valg        w_vala
6.21 c                   eval      rxvalg = w_vala
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
     c                   move      rxulda        rxupar
     c                   call      'RF020R'
     c                   parm                    rxupar
     c                   move      rxupar        rxulda
T5.20c                   eval      *inkb = *off
T5.20c                   if        *inkc or *inkl
T5.20c                   goto      end_c2bld
T5.20c                   endif
     c                   goto      c2taga
     c                   endif
      *
      * Spørring
      *
      * Call program for
      * MVA-kode
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2MVAK'
     c                   call      'RA505R'
     c                   parm                    w_firm
     c                   parm                    p_ekod
     c                   parm                    p_tx30
      *                                                                 .
     c                   if        p_ekod <> *blank
     c                   eval      c2mvak = p_ekod
     c                   eval      c2etxt = p_tx30
     c                   endif
      *                                                                 .
     c                   move      *on           *in35
     c                   goto      c2taga
     c                   endif
      *
      * Call program for
      * fordelingsnøkkel
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2BNØK'
     c                   call      'RA513R'
     c                   parm                    w_firm
     c                   parm                    p_mkod
     c                   parm                    p_tx30
      *                                                                 .
     c                   if        p_mkod <> *zero
     c                   eval      c2bnØk = p_mkod
     c                   eval      c2mtxt = p_tx30
     c                   endif
      *                                                                 .
     c                   move      *on           *in32
     c                   goto      c2taga
     c                   endif
      *
      * Call program for
      * mengde
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2MNGD'
     c                   call      'RA515R'
     c                   parm                    w_firm
     c                   parm                    p_okod
     c                   parm                    p_tx30
      *                                                                 .
     c                   if        p_okod <> *zero
     c                   eval      c2mngd = p_okod
     c                   eval      c2otxt = p_tx30
     c                   endif
      *                                                                 .
     c                   move      *on           *in33
     c                   goto      c2taga
     c                   endif
      *
      * Call program for
      * valuta
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2VALK'
     c                   call      'RA516R'
     c                   parm                    w_firm
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *                                                                 .
     c                   if        p_pkod <> *zeros
     c                   eval      c2valk = p_pkod
     c                   eval      c2pmna = p_tx20
     c                   endif
      *                                                                 .
     c                   move      *on           *in34
     c                   goto      c2taga
     c                   endif
7.02  *
7.02  * Call program for
7.02  * standard kontoplan
7.02  *
7.02 c                   if        *inkd = *on and
7.02 c                             w_afld = 'C2REFN'
7.02 c                   call      'RH102R'
7.02 c                   parm                    p_kont
7.02 c                   parm                    p_tx60
7.02  *                                                                 .
7.02 c                   if        p_kont <> *zeros
7.02 c                   eval      c2refn = p_kont
7.02 c                   eval      c2reft = p_tx60
7.02 c                   endif
7.02  *                                                                 .
7.02 c*                  move      *on           *in37
7.02 c                   goto      c2taga
7.02 c                   endif
      *                                                                 .
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2text = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
7.xx c                   if        c2mvao <> 'J'
7.xx c                   eval      c2mvao = 'N'
7.xx c                   endif
      *
     c                   exsr      kontroll
7.02  *
7.02  * Sjekke om standard konto er lagt inn
7.02  *
7.02 c                   if        c2refn = 0
7.02 c                   eval      *in37 = *on
7.02 c                   eval      *in81 = *on
7.02 c                   endif
7.02  *
7.02  * Sjekke om standard konto finnes i RHSTPF
7.02  *
7.02 c                   eval      rhstl1_kont = c2refn
7.02 c     rhstl1_key    chain     rhstl1
7.02 c                   if        not %found(rhstl1)
7.02 c                   eval      *in38 = *on
7.02 c                   eval      *in81 = *on
7.02 c                   endif
7.02  *   Hvis feil gå tilbake
7.02 c                   if        *in81 = *on
7.02 c                   goto      c2taga
7.02 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rhovlur
      *
     c                   eval      rhovlu_kont = c1kont
     c                   eval      rhovlu_spes = c1spes
     c                   eval      rhovlu_avde = c1avde
     c     rhovlu_key    chain     rhovlur
      *
     c                   eval      rhtext = c2text
     c                   eval      rhtxt2 = c2txt2
     c                   eval      rhalfa = c2alfa
      *
     c                   if        rhalfa = *blank
     c                   eval      rhalfa = rhtext
     c                   endif
      *
     c     lo:up         xlate     rhalfa        rhalfa
      *
     c                   eval      rhmvak = c2mvak
     c                   eval      rhmvao = c2mvao
     c                   eval      rhmvag = c2mvag
     c                   eval      rhbnØk = c2bnØk
     c                   eval      rhvalk = c2valk
     c                   eval      rhmngd = c2mngd
     c                   eval      rhproj = c2proj
     c                   eval      rhbÆrk = c2bÆrk
     c                   eval      rhavdk = c2avdk
     c                   eval      rhnØkk = c2nØkk
     c                   eval      rhsprk = c2sprk
T5.30c                   eval      rhrefn = c2refn
T5.30c                   eval      rhreft = c2reft
T5.60c                   eval      rhkpg1 = c2kpg1
T5.60c                   eval      rhkpg2 = c2kpg2
T5.60c                   eval      rhkpg3 = c2kpg3
T5.60c                   eval      rhkpg4 = c2kpg4
T5.60c                   eval      rhkpg5 = c2kpg5
      *
     c                   time                    rhedat
     c                   time                    rhetim
     c                   eval      rheusr = l_user
      *
     c                   if        %found
     c                   update    rhovlur
     c                   else
     c                   eval      rhfirm = w_firm
     c                   eval      rhkont = c1kont
     c                   eval      rhspes = c1spes
     c                   eval      rhavde = c1avde
6.10 c                   time                    rhodat
6.10 c                   time                    rhotim
6.10 c                   eval      rhousr = l_user
     c                   write     rhovlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1text = rhtext
     c                   eval      b1sprk = rhsprk
     c                   eval      b1mvak = rhmvak
     c                   eval      b1mvao = rhmvao
     c                   eval      b1mvag = rhmvag
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rhovlr_kont = d1kont
     c                   eval      rhovlr_spes = d1spes
     c                   eval      rhovlr_avde = d1avde
     c     rhovlr_key    chain     rhovlr
      *
     c     d1taga        tag
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sjekk om konto uten avdeling/bærer finnes
      *
     c                   if        d1spes <> *zero
     c                   eval      rhovl1_avde = *zero
     c                   eval      rhovl1_spes = *zero
     c     rhovl1_key    chain     rhovl1                             60
     c                   if        *in60 = *on
     c                   move      *on           *in32
     c                   goto      d1taga
     c                   endif
     c                   endif
      *
      * Sjekk om det finnes saldorecords hvis kun konto utfylt
      *
     c                   if        d1spes = 0 and
     c                             d1avde = 0
     c                   eval      rhsal1_kont = d1kont
     c                   eval      rhsal1_spes = d1spes
     c                   eval      rhsal1_avde = d1avde
     c     rhsal1_key    setll     rhsal1                             61
      *
     c                   read      rhsal1r                                61
E5.40c                   if        rikont = d1kont and
T5.40c                             rifirm = w_firm
     c                   move      *on           *in33
     c                   goto      d1taga
     c                   endif
      *
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rhovlu_kont = d1kont
     c                   eval      rhovlu_spes = d1spes
     c                   eval      rhovlu_avde = d1avde
     c     rhovlu_key    chain     rhovlur
      *
     c                   delete    rhovlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kont = k1kont
     c                   eval      s_spes = k1spes
     c                   eval      s_avde = k1avde
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rhovlr_kont = k1kont
     c                   eval      rhovlr_spes = k1spes
     c                   eval      rhovlr_avde = k1avde
     c     rhovlr_key    chain     rhovlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1kont = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rhovlr_kont = k1kont
     c                   eval      rhovlr_spes = k1spes
     c                   eval      rhovlr_avde = k1avde
     c     rhovlr_key    chain     rhovlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kont = s_kont
     c                   eval      c1spes = s_spes
     c                   eval      c1avde = s_avde
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   read      rhovl1                                 15
     c                   when      w_seqe = 'A'
     c                   read      rhovl3                                 15
     c                   when      w_seqe = 'B'
     c                   read      rhovl4                                 15
     c                   when      w_seqe = 'N'
     c                   read      rhovl2                                 15
     c                   endsl
      *
     c                   if        %eof or
     c                             rhfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhovl1_kont = rhkont
     c                   when      w_seqe = 'A'
     c                   eval      rhovl3_avde = rhavde
     c                   when      w_seqe = 'B'
     c                   eval      rhovl4_spes = rhspes
     c                   when      w_seqe = 'N'
     c                   eval      rhovl2_alfa = rhalfa
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = rhkont
     c                   eval      b1spes = rhspes
     c                   eval      b1avde = rhavde
     c                   eval      b1text = rhtext
     c                   eval      b1sprk = rhsprk
     c                   eval      b1mvak = rhmvak
     c                   eval      b1mvao = rhmvao
     c                   eval      b1mvag = rhmvag
     c                   eval      b1alfa = rhalfa
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'K'
     c     rhovl1_key    setgt     rhovl1
     c                   readp     rhovl1
     c                   when      w_seqe = 'A'
     c     rhovl3_key    setgt     rhovl3
     c                   readp     rhovl3
     c                   when      w_seqe = 'B'
     c     rhovl4_key    setgt     rhovl4
     c                   readp     rhovl4
     c                   when      w_seqe = 'N'
     c     rhovl2_key    setgt     rhovl2
     c                   readp     rhovl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   readp     rhovl1
     c                   when      w_seqe = 'A'
     c                   readp     rhovl3
     c                   when      w_seqe = 'B'
     c                   readp     rhovl4
     c                   when      w_seqe = 'N'
     c                   readp     rhovl2
     c                   endsl
      *
     c                   if        %eof or
     c                             rhfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhovl1_kont = rhkont
     c                   when      w_seqe = 'A'
     c                   eval      rhovl3_avde = rhavde
     c                   when      w_seqe = 'B'
     c                   eval      rhovl4_spes = rhspes
     c                   when      w_seqe = 'B'
     c                   eval      rhovl2_alfa = rhalfa
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'K'
     c     rhovl1_key    setll     rhovl1
     c                   when      w_seqe = 'A'
     c     rhovl3_key    setll     rhovl3
     c                   when      w_seqe = 'B'
     c     rhovl4_key    setll     rhovl4
     c                   when      w_seqe = 'N'
     c     rhovl2_key    setll     rhovl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for kontroll av inputfelt
      *
     c     kontroll      begsr
      *
      * Sjekke om mva-kode er gyldig
      *
     c                   move      c2mvak        p_ekod
     c                   call      'RS705R'
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_tx30
      *
     c                   eval      c2etxt = p_tx30
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in35
     c                   move      *on           *in81
     c                   goto      kont_end
     c                   endif
      *
      * Sjekke om budsjettnøkkel
      * er gyldig - default = 99
      *
     c                   if        c2bnØk = *zero
     c                   move      99            c2bnØk
     c                   endif
      *
     c                   if        c2bnØk = 99
     c                   eval      c2mtxt = 'Jevn fordeling'
     c                   else
     c                   move      c2bnØk        p_mkod
     c                   call      'RS713R'
     c                   parm                    p_stat
     c                   parm                    p_mkod
     c                   parm                    p_tx30
      *
     c                   eval      c2mtxt = p_tx30
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in32
     c                   move      *on           *in81
     c                   goto      kont_end
     c                   endif
     c                   endif
      *
      * Sjekke om mengde er gyldig
      *
     c                   eval      c2otxt = *blank
     c                   if        c2mngd <> *zero
     c                   move      c2mngd        p_okod
     c                   call      'RS715R'
     c                   parm                    p_stat
     c                   parm                    p_okod
     c                   parm                    p_tx30
      *
     c                   eval      c2otxt = p_tx30
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in33
     c                   move      *on           *in81
     c                   goto      kont_end
     c                   endif
     c                   endif
      *
      * Sjekke om valuta er gyldig
      *
     c                   eval      c2pmna = *blank
     c                   if        c2valk <> *blank
     c                   move      c2valk        p_pkod
     c                   call      'RS716R'
     c                   parm                    p_stat
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *
     c                   eval      c2pmna =p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in34
     c                   move      *on           *in81
     c                   goto      kont_end
     c                   endif
     c                   endif
7.02  *
     c     kont_end      endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på konto
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for posisjonering på avdeling
      *
     c     rhovl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl3_avde
     c                   kfld                    rhovl3_kont
     c                   kfld                    rhovl3_spes
      *
      * Key for posisjonering på kosnadsbærer
      *
     c     rhovl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl4_spes
     c                   kfld                    rhovl4_kont
     c                   kfld                    rhovl4_avde
      *
      * Key for posisjonering på beskrivelse
      *
     c     rhovl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl2_alfa
      *
      * Key for les på konto
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Key for oppdatering av konto
      *
     c     rhovlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlu_kont
     c                   kfld                    rhovlu_spes
     c                   kfld                    rhovlu_avde
      *
      * Key for saldomatrise via kontonummer
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
     c                   kfld                    rhsal1_spes
     c                   kfld                    rhsal1_avde
      *
T5.40 * Key for notatark
      *
T5.40c     rklal2_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rklal2_syst
T5.40c                   kfld                    rklal2_pros
T5.40c                   kfld                    rklal2_akti
T5.40c                   kfld                    rklal2_kont
T5.40c                   kfld                    rklal2_spes
T5.40c                   kfld                    rklal2_avde
      *
T5.40 * Key for oppslag mot standard kontoplan
      *
T5.40c     rhstl1_key    klist
T5.40c                   kfld                    rhstl1_kont
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'K'
     c                   eval      rhovl1_kont = 0
     c                   eval      rhovl1_spes = 0
     c                   eval      rhovl1_avde = 0
     c     rhovl1_key    setll     rhovl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
