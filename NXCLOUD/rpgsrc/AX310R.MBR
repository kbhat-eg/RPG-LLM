     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . :
      *  Program . . . . : AX310R
      *  Beskrivelse . . : HOVEDBOK: Legger ut XML til SAF-T
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
Versj *Uts   Dato Usr Referanse Beskrivelse
      *____________________________________________________________________________________________
      *K00-200420               START FOR VERSJON K00
7.00  *K00 200420 SST NXS-4426  Utrulling av SAFT-Rapporter til alle kundene
7.01  *K00 141220 SST           Justering av loop vedr tom RXMLL1
7.02  *K00 081121 SST           Justering av behandling av firma
8.00  *K00 100222 sst           Ny utgave av RHTRL5
8.01  *K00 140222 sst NXS_11659 Kjøre alle bilag med samme nummer som et bilag.
8.02  *K00 160222 sst NXS_11659 Endret mva-andl til mva-grunnlag i Hovedbok
8.03  *K00 210222 sst NXS_11659 Endret Record_ID til løpenr i steden for bilagsnummer
8.04  *K00 240222 sst NXS_11659 Momskode blank behandles som '0'
      *____________________________________________________________________________________________
      *
      *  Dette programmet henter data fra ADBxxx/RHTRPF m/RBFIRM = Firma
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     frat5pf    if   e           k disk    rename(rat5pfr:rat5pfr)
     frhtrl5    if   e           k disk    rename(rhtrpfr:rhtrl5r)
     frktrlb    if   e           k disk    rename(rktrpfr:rktrlbr)
     frltrlc    if   e           k disk    rename(rltrpfr:rltrlcr)
     frxmll1    if   e           k disk    rename(rxmlpfr:rxmll1r)
      *
     faxmlut    o    e             disk
      *
     d ana             S             80    DIM(9) CTDATA PERRCD(1)
      *
     d ra04l1_firm     s                       like(radfir)
      *
     d rat5pf_ekod     s                       like(rtekod)
      *
     d rhtrl5_firm     s                       like(rbfirm)
     d rhtrl5_raar     s                       like(rbraar)
     d rhtrl5_rper     s                       like(rbrper)
     d rhtrl5_biln     s                       like(rbbiln)
8.01 d*rhtrl5_jour     s                       like(rbjour)
      *
     d rktrlb_firm     s                       like(rnfirm)
     d rktrlb_raar     s                       like(rnraar)
     d rktrlb_biln     s                       like(rnbiln)
      *
     d rltrlc_firm     s                       like(rofirm)
     d rltrlc_raar     s                       like(roraar)
     d rltrlc_biln     s                       like(robiln)
      *
     d rxmll1_head     s                       like(axhead)
     d rxmll1_linj     s                       like(axlinj)
      *
      *
      *
     d XML_Input       s            256a   Varying
     d XML_Output      s            256a   Varying
     d XML_Outhead     s            256a   Varying
     d XML_path        s            256a
     d w_xmlp          s             50
     d options         s            200a   Varying
      *
     d w_firma         s              3A   inz('000')
     d w_firm          s              3S 0
      *
     d w_neto          s                   like(rbneto)
     d text            s             50
     d txt2            s             50
     d stmt            s            500A
     d xmlut           s            500A
      *
     d i               s              9S 0
     d j               s              9S 0
     d s_i             s              9S 0
     d f               s              3
     d t               s              3
     d s               s              3S 0
     d fn              s              3S 0
     d tn              s              3S 0
     d s_head          s                   like(axhead)
     d s_linj          s                   like(axlinj)
     d s_kont          s                   like(rbkont)
     d s_kontx         s              5
     d s_biln          s                   like(rbbiln)
8.00 d s_jour          s                   like(rbjour)
     d w_rbvalb        s                   like(rbvalb)
     d w_rnkund        s              6
     d w_rolevr        s              6
8.03 d w_lope          s              5  0
8.03 d w_lopx          s              5
      *
     d s_linja         s                   like(axlinj)
     d w_iso_dat       s               d   datfmt(*iso)
     d w_x_dat         s             10A
     d w_salix         s             18
     d w_salux         s             18
     d w_test          s            100
     d w_02            s              2S 2
     d w_s             s             15S 2
     d w_110           s             15S 0
     d w_110x          s             15
     d w_02x           s              2
     d w_saldoi        s             15S 2
     d w_saldou        s             15S 2
     d w_kurs          s              6S 3
     d w_kx            s              6
      *
     d w_type          s              1
      *
     d w_str_i         s            100
     d w_str_u         s            100
     d w_posi          s              2  0
     d w_posu          s              2  0
     d w_str_x         s              1
     d c_apos          c                   x'7D'
      *
     d p_perfm         s              2
     d p_perfa         s              4
     d p_pertm         s              2
     d p_perta         s              4
     d p_pfm           s              2S 0
     d p_pfa           s              4S 0
     d p_ptm           s              2S 0
     d p_pta           s              4S 0
     d p_hcom          s             50
      *
     d p_bilfra        s                       like(rbbiln)
     d                                         inz(00000000)
     d p_biltil        s                       like(rbbiln)
     d                                         inz(00000000)
      *
     d w_sum_ant       s             15S 0
     d w_sum_deb       s             15S 3
     d w_sum_kre       s             15S 3
     d w_sum_alfa      s             15
      *
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller opp SUB-program for henting av totaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   CALL      'AX390R'
     c                   PARM                    ax000par
     c                   PARM                    p_bilfra
     c                   PARM                    p_biltil
     c                   PARM                    w_sum_ant
     c                   PARM                    w_sum_deb
     c                   PARM                    w_sum_kre
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Start
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *    Posisjoner Hovedbok på fra periode
      *
     c     STARTP        tag
      *
     c                   eval      rhtrl5_firm = w_firm
     c                   eval      rhtrl5_raar = p_pfa
     c                   eval      rhtrl5_rper = p_pfm
     c                   eval      rhtrl5_biln = *zero
8.01 c*                  eval      rhtrl5_jour = *zero
     c     rhtrl5_key    setll     rhtrl5
      *
     c                   read      rhtrl5
      *   Momskode blank skal behandles som '0'
8.04 c                   if        rbmvak = ' '
8.04 c                   eval      rbmvak = '0'
8.04 c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *    Skriv ut ev heading xml - linjer før loop pr bilag, until 'S'
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
7.01 c                   dow       not %eof(rxmll1)
7.01 c                             and axmaop <> 'S'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   enddo
      *
     c                   eval      s_linja = axlinj
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Behandle alle bilagstransaksjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     HOVEDP        tag
      *
      *  Hopper ut dersom utenfor parameter-grenser
      *
     c                   if        %eof(rhtrl5)
     c                   goto      xsiste
     c                   endif
      *
     c                   if        rbfirm = w_firm   and
     c                             rbraar < p_pta
     c                   goto      xneste
     c                   endif
      *
     c                   if        rbfirm = w_firm   and
     c                             rbraar = p_pta   and
     c                             rbrper <= p_ptm
     c                   goto      xneste
     c                   endif
      *   Momskode blank skal behandles som '0'
8.04 c                   if        rbmvak = ' '
8.04 c                   eval      rbmvak = '0'
8.04 c                   endif
      *
     c                   goto      xsiste
      *
      *  Test benyttes p_bilfra <> 0
      *
     c     xneste        tag
      *
     c                   if        p_bilfra <> 0
     c                   if        rbbiln   <  p_bilfra
     c                   read      rhtrl5
      *   Momskode blank skal behandles som '0'
8.04 c                   if        rbmvak = ' '
8.04 c                   eval      rbmvak = '0'
8.04 c                   endif
     c                   goto      xlesny
     c                   endif
     c                   if        rbbiln   >  p_biltil
     c                   read      rhtrl5
      *   Momskode blank skal behandles som '0'
8.04 c                   if        rbmvak = ' '
8.04 c                   eval      rbmvak = '0'
8.04 c                   endif
     c                   goto      xlesny
     c                   endif
     c                   endif
      *
      *    Her skal man komme tilbake for hvert nytt bilag
      *
     c                   eval      rbbiln = rbbiln
8.03 c                   eval      w_lope = 0
     c                   exsr      Bilag
      *
      *   Les neste konto(postering)
      *
     c     xlesny        tag
      *
     c                   goto      hovedp
      *
      *    Siste bilag er nå behandlet
      *    Legg ut ev faste linjer som avlsutning på Hovedbok
      *
     c     xsiste        tag
      *
     c                   dow       not %eof(rxmll1) and
     c                             axhead = 'HOVEDBOK'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   if        axlinj = 350
     c                   eval      axlinj = axlinj
     c                   endif
     c                   enddo
      *
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine = Bilag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     Bilag         begsr
      *
      *    Her skal man start på nytt på nytt bilag   linje 20
      *
     c                   eval      rxmll1_linj = s_linja
     c     rxmll1_key    setll     rxmll1
     c                   read      rxmll1
     c                   if        axmaop = 'S'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   endif
      *
      *  Skriv xml for start bilag
      *
     c                   dow       axmaop <> 'S' and
     c                             axmaop <> 'E'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   enddo
      *
      *   Lagre start xml for ny konto(postering)
      *
     c                   eval      s_linj = axlinj
      *
      *   Lagre dette bilagsnummer
      *
     c                   eval      s_biln = rbbiln
8.00 c                   eval      s_jour = rbjour
      *
      *  Skrive  alle linjene på en postering
      *
     c                   dow       not %eof(rhtrl5)
     c                             and rbbiln = s_biln
8.01 c*                            and rbjour = s_jour
     c                   exsr      BehKonto
      *
     c                   enddo
      *
      *  Skrive slutt xml for bilaget
      *
     c                   dow       not %eof(rxmll1) and
     c                             axmaop <> 'E'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   enddo
      *
     c                   if        axmaop = 'E'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine = Behkonto
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     BehKonto      begsr
      *
     c                   eval      rbkont = rbkont
      *
      *  Skriv xml Start konto
      *
     c                   eval      rxmll1_linj = s_linj
     c     rxmll1_key    setll     rxmll1
     c                   read      rxmll1
      *
      *  Beh alle xml linjer for denne konto(postering)
      *
     c                   dow       axmaop <> 'E'
     c                   exsr      sxmlut
     c                   read      rxmll1
     c                   enddo
      *
      *   Skriv xml Slutt konto
      *
     c                   exsr      sxmlut
     c                   read      rxmll1
      *
      *   Les neste konto(postering)
      *
     c                   read      rhtrl5
      *   Momskode blank skal behandles som '0'
8.04 c                   if        rbmvak = ' '
8.04 c                   eval      rbmvak = '0'
8.04 c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  SXMLUT  legger ut linja etter koder fra alle nivåer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     SXMLUT        begsr
      *
      *  Hvis type er merket med 'X' er ikke denne linja klargjort enda
      *
     c                   if        axtype = 'X'
     c                   goto      EXMLUT
     c                   endif
      *
      *  Total, Antall trans>0
      *
     c                   if        w_sum_ant >= 0 and
     c                             axfelt = 'TOTALTRANS'
     c                   eval      i = %scan('?B?':axxmlt)
      *
     c                   move      w_sum_ant     w_sum_alfa
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_sum_alfa) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
      *  Total, Debet  beløp>0
      *
     c                   if        w_sum_deb >= 0 and
     c                             axfelt = 'TOTALDEB  '
     c                   eval      i = %scan('?B?':axxmlt)
     c                   eval      w_saldoi = w_sum_deb
     c                   exsr      konvtall
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_salix) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
      *  Total, Kredit beløp>0
      *
     c                   if        w_sum_kre >= 0 and
     c                             axfelt = 'TOTALKRE  '
     c                   eval      i = %scan('?B?':axxmlt)
     c                   eval      w_saldoi = w_sum_kre
     c                   exsr      konvtall
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_salix) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   goto      EXMLUT
     c                   endif
      *
      *  Sjekk om linja er avhengig av innhold i felt
      *
     c                   if        axmaop = 'V'
     c                   if        axfast = '*IF +'   and
     c                             axfelt = 'RBNETO'  and
     c                             rbneto < 0
     c                   goto      EXMLUT
     c                   endif
      *
     c                   if        axfast = '*IF -'   and
     c                             axfelt = 'RBNETO'  and
     c                             rbneto >= 0
     c                   goto      EXMLUT
     c                   endif
     c                   endif
      *
      *  Skip en sekvens dersom felt inneholder ??
      *
     c                   if        axmaop = 'O' and
     c                             %subst(axfast:1:9) = 'SKIP IF =' and
     c                             axfelt = 'RBMVAK' and
     c                             rbmvak = '0'
     c                   eval      s = %scan('<':axxmlt)
      *
     c                   if        s > 0
     c                   eval      w_test = %subst(axxmlt:1:s) + '/' +
     c                                      %subst(axxmlt:s+1:100-s)
      *
     c                   dow       w_test <> axxmlt and
     c                             not %eof(rxmll1)
      *
     c                   read      rxmll1
     c                   eval      axxmlt = axxmlt
     c                   enddo
      *                  read      rxmll1
     c                   goto      EXMLUT
     c                   endif
      *
     c                   endif
      *
      *  Sjekk om bare xml linja skal ut
      *
     c                   if        axtype = 'F'
     c                   eval      xmlrec = axxmlt
     c                   goto      skrivxml
     c                   endif
      *
      *  Skal det hentes inn et variabelt felt?
      *
     c                   eval      i = %scan('?V?':axxmlt)
     c                   if        i > 0
      *
      *  Dagens dato hentes dersom axtype = 'C' (current)
      *
     c                   if        axtype = 'C'
     c     *dmy          move      udate         w_iso_dat
     c                   move      w_iso_dat     w_x_dat
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_x_dat) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
     c                   eval      text = *blank
      *
      *  Hente inn tekst felt
      *
     c                   if        axtype = 'T'
      *
     c                   select
     c                   when      axfelt = 'RBOUSR'
     c                   movel     rbousr        text
     c                   when      axfelt = 'RBTEXT'
     c                   movel     rbtext        text
     c                   exsr      spstgn
     c                   when      axfelt = 'RBMVAK'
     c                   movel     rbmvak        text
     c                   exsr      redmvak
     c                   endsl
      *
     c                   if        %subst(axfast:1:1) = '('
     c                   eval      j = %scan(',':axfast)
     c                   if        j > 0
     c                   eval      f = %subst(axfast:2:3)
     c                   eval      t = %subst(axfast:6:3)
     c                   move      f             fn
     c                   move      t             tn
     c                   eval      txt2 = %subst(text:fn:tn-fn+1)
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(txt2) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
     c                   endif
      *
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn numerisk felt
      *
     c                   if        axtype = 'N'
     c                   select
     c                   when      axfelt = 'RBBILN'
     c                   eval      text = %char(rbbiln)
8.03 c                   when      axfelt = 'W_LOPE'
8.03 c                   eval      w_lope = w_lope + 1
8.03 c                   eval      w_lopx = %char(w_lope)
8.03 c                   eval      text = %char(rbbiln) + '_' + %trim(w_lopx)
     c                   when      axfelt = 'RBRPER'
     c                   movel     rbrper        text
     c                   when      axfelt = 'RBRAAR'
     c                   movel     rbraar        text
     c                   when      axfelt = 'RBBDAT'
     c                   movel     rbbdat        text
     c                   when      axfelt = 'RBJOUR'
     c                   movel     rbjour        text
      *
     c                   when      axfelt = 'RBODAT'
     c                   movel     rbodat        text
      *
     c                   when      axfelt = 'RBKONT'
     c                   move      rbkont        s_kontx
     c                   dow       %subst(s_kontx:1:1) = '0' or
     c                             rbkont = 0
     c                   eval      %subst(s_kontx:1:4) = %subst(s_kontx:2:4)
     c                   eval      %subst(s_kontx:5:1) = ' '
     c                   enddo
     c                   movel     s_kontx       text
      *
      *
     c                   when      axfelt = 'RBPMVA'
     c                   movel     rbpmva        text
     c                   if        rbmvak = '0'  and
     c                             rbmvag = 0
     c                   eval      text   = *blank
     c                   endif
     c                   exsr      redefnum
      *
8.02 c                   when      axfelt = 'RBMVAG'
8.02 c                   if        rbmvag < 0
8.02 c                   eval      rbmvag = rbmvag * -1
8.02 c                   endif
8.02 c                   movel     rbmvag        text
     c                   exsr      redefnum
      *
     c                   when      axfelt = 'RBMVAB'
     c                   if        rbmvab < 0
     c                   eval      rbmvab = rbmvab * -1
     c                   endif
     c                   movel     rbmvab        text
     c                   exsr      redefnum
      *
     c                   when      axfelt = 'RBNETO'
     c                   if        rbneto < 0
     c                   eval      w_neto = rbneto * -1
     c                   else
     c                   eval      w_neto = rbneto
     c                   endif
     c                   movel     w_neto        text
     c                   exsr      redefnum
      *
     c                   eval      text = text
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   write     axmlutr
      *
     c                   if        rbvalk <> '   ' and
     c                             rbvalk <> 'NOK' and
     c                             rbvalb <> 0     and
     c                             rbvalb <> rbneto
     c                   eval      xmlrec = '               ' +
     c                                      '<nl:CurrencyCode>' +
     c                                      %trim(rbvalk) +
     c                                      '</nl:CurrencyCode>'
     c                   write     axmlutr
      *
     c                   eval      w_rbvalb =    rbvalb
     c                   if        w_rbvalb < 0
     c                   mult      -1            w_rbvalb
     c                   endif
      *
     c                   movel     w_rbvalb      text
     c                   exsr      redefnum
     c                   eval      xmlrec = '               ' +
     c                                      '<nl:CurrencyAmount>' +
     c                                      %trim(text) +
     c                                      '</nl:CurrencyAmount>'
     c                   write     axmlutr
      *
     c                   eval      w_kurs = rbneto / rbvalb
     c                   move      w_kurs        w_kx
     c                   eval      xmlrec = '               ' +
     c                                      '<nl:ExchangeRate>' +
     c                                      %trim(%subst(w_kx:1:3)) + '.' +
     c                                      %trim(%subst(w_kx:4:3)) +
     c                                      '</nl:ExchangeRate>'
     c                   write     axmlutr
     c                   endif
      *
     c                   goto      Exmlut
      *
     c                   endsl
      *
     c                   eval      text = text
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn dato felt
      *
     c                   if        axtype = 'D'
     c                   select
     c                   when      axfelt = 'RBBDAT'
     c                   movel     rbbdat        text
     c                   when      axfelt = 'RBODAT'
     c                   movel     rbodat        text
     c                   endsl
      *
     c                   eval      w_x_dat = text
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_x_dat) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn parameter via p_xxxxx felt fra axfelt
      *
     c                   if        axtype = 'P'
     c                   eval      text = *blank
     c                   select
     c                   when      axfelt = 'P_PERFM'
     c                   movel     p_perfm       text
     c                   when      axfelt = 'P_PERFA'
     c                   movel     p_perfa       text
     c                   when      axfelt = 'P_PERTM'
     c                   movel     p_pertm       text
     c                   when      axfelt = 'P_PERTA'
     c                   movel     p_perta       text
     c                   when      axfelt = 'P_HCOM'
     c                   movel     p_hcom        text
     c                   when      axfelt = 'L_USER'
     c                   movel     l_user        text
     c                   endsl
      *
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
     c                   goto      skrivxml
      *  END ?V?
     c                   endif
      *
      *  Sjekk om faste felter skal legges ut
      *
     c                   eval      i = %scan('?F?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
      *  Sjekk xml linja skal ut med parameter
      *
     c                   if        axtype = 'F'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      skrivxml
     c                   endif
      *
     c     skrivxml      tag
     c                   write     axmlutr
      *
      *  OBS!OBS! Legger ut Analysis koder  og Reskontro-konti
      *
     c                   if        axfelt = 'RBKONT'
     c                   exsr      TILANA
      *
     c                   if        rbkont = RADK05
     c                   exsr      TILKUN
     c                   endif
      *
     c                   if        rbkont = RADK06
     c                   exsr      TILLEV
     c                   endif
      *
     c                   endif
      *
     c     EXMLUT        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     Spesial-tegn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     spstgn        begsr
      *
     c                   eval      s_i = i
      *
     c                   eval      i = s_i
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      w_str_i = text
      *
     c                   dow       w_posi < 70
     c                   eval      w_posi = w_posi + 1
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 90
     c                   select
     c                   when      w_str_x = '&'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:5) = '&amp;'
     c                   eval      w_posu = w_posu + 4
      *
     c                   when      w_str_x = '<'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&lt;'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = '>'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&gt;'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = c_apos
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&apos;'
     c                   eval      w_posu = w_posu + 5
      *
     c                   when      w_str_x = '"'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&quot;'
     c                   eval      w_posu = w_posu + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   enddo
      *
     c                   eval      text   =  w_str_u
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     SUBrutine Redefinering av nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     redefnum      begsr
      *
     c                   dow       %subst(text:1:1) = '0'
     c                             and %subst(text:3:1) <> ' '
     c                   eval      %subst(text:1:20) = %subst(text:2:20)
     c                   enddo
     c     ' '           scan      text          j
     c                   if        j > 3
     c                   eval      text = %subst(text:1:j-3) + '.' +
     c                                    %subst(text:j-2:2)
     c                   else
     c                   eval      text = '0.' + %subst(text:1:2)
     c                   endif
     c                   if        text = '0.'
     c                   eval      text = '0 '
     c                   endif
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     SUBrutine Redefinering av mvakode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     redmvak       begsr
      *
     c*                  movel     text          rat5pf_ekod
     c*    rat5pf_key    chain     rat5pf
     c*>                 move      *blank        w_taxCode
     c*                  if        %found(rat5pf)
     c*                  movel     rtesmk        text
     c                   if        %subst(text:1:1) = '0'
     c                   eval      %subst(text:1:1) = %subst(text:2:1)
     c                   eval      %subst(text:2:1) = ' '
     c                   endif
     c*                  endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     SUBrutine Legger ut Analysis-koder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     tilana        begsr
      *
      *  Legger ut for Bilagskode
      *
     c                   if        rbbilk <> 0
     c                   movel     rbbilk        axfast
     c                   eval      w_type = 'B'
     c                   exsr      subana
     c                   endif
      *
      *  Legger ut for Avdelingskode
      *
     c                   if        rbavde <> 0
     c                   movel     rbavde        axfast
     c                   eval      w_type = 'A'
     c                   exsr      subana
     c                   endif
      *
      *  Legger ut for Kostnadsbærer
      *
     c                   if        rbspes <> 0
     c                   movel     rbspes        axfast
     c                   eval      w_type = 'K'
     c                   exsr      subana
     c                   endif
      *
      *  Legger ut for Kostnadsbærer
      *
     c                   if        rbhpro <> *blank
     c                   movel     rbhpro        axfast
     c                   eval      w_type = 'P'
     c                   exsr      subana
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     SUBrutine Legger ut Analysis-koder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     subana        begsr
      *
      *  Legger ut Start-kode
      *
     c                   movel     *blank        xmlrec
     c                   movel     ana(1)        xmlrec
     c                   write     axmlutr
      *
      *  Legger ut Type-kode
      *
     c                   eval      axxmlt = ana(2)
     c                   eval      i = %scan('?P?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_type) +
     c                             %subst(axxmlt:i+3:50)
     c                   write     axmlutr
     c                   endif
      *
      *  Legger ut Kode
      *
     c                   eval      axxmlt = ana(3)
     c                   eval      i = %scan('?P?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   write     axmlutr
     c                   endif
      *
      *  Legger ut Slutt-kode
      *
     c                   movel     *blank        xmlrec
     c                   movel     ana(7)        xmlrec
     c                   write     axmlutr
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     SUBrutine Legger ut Kunde-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     tilkun        begsr
      *
      *  henter kunde-nummer fra transaksjon
      *
     c                   eval      rktrlb_firm = rbfirm
     c                   eval      rktrlb_raar = rbraar
     c                   eval      rktrlb_biln = rbbiln
     c     rktrlb_key    chain     rktrlb                             90
     c   90              goto      endkun
      *
      *  Legger ut Type-kode
      *
     c                   movel     rnkund        w_rnkund
     c                   movel     *blank        xmlrec
     c                   eval      axxmlt = ana(8)
     c                   eval      i = %scan('?P?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_rnkund) +
     c                             %subst(axxmlt:i+3:50)
     c                   write     axmlutr
     c                   endif
      *
     c     endkun        endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *     SUBrutine Legger ut Leverandør-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     tillev        begsr
      *
      *  henter kunde-nummer fra transaksjon
      *
     c                   eval      rltrlc_firm = rbfirm
     c                   eval      rltrlc_raar = rbraar
     c                   eval      rltrlc_biln = rbbiln
     c     rltrlc_key    chain     rltrlc                             90
     c   90              goto      endlev
      *
      *  Legger ut Type-kode
      *
     c                   movel     rolevr        w_rolevr
     c                   movel     *blank        xmlrec
     c                   eval      axxmlt = ana(9)
     c                   eval      i = %scan('?P?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_rolevr) +
     c                             %subst(axxmlt:i+3:50)
     c                   write     axmlutr
     c                   endif
      *
     c     endlev        endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for konvertering av tall som kan legges ut i fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     konvtall      begsr
      *
     c                   eval      w_s = w_saldoi
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_salix = w_110x + '.' + w_02x
     c                   dow       %subst(w_salix:1:1) = '0'
     c                   eval      %subst(w_salix:1:17) = %subst(w_salix:2:17)
     c                   eval      %subst(w_salix:18:1) = ' '
     c                   enddo
      *
     c                   eval      w_s = w_saldou
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_salux = w_110x + '.' + w_02x
     c                   dow       %subst(w_salux:1:1) = '0'
     c                   eval      %subst(w_salux:1:17) = %subst(w_salux:2:17)
     c                   eval      %subst(w_salux:18:1) = ' '
     c                   enddo
      *
     c                   if        w_saldoi <> 0 or
     c                             w_saldou <> 0
     c                   eval      w_saldoi = w_saldoi
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      *
     c     ra04l1_key    klist
     c                   kfld                    ra04l1_firm
      *
     c     rat5pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rat5pf_ekod
      *
     c     rhtrl5_key    klist
     c                   kfld                    rhtrl5_firm
     c                   kfld                    rhtrl5_raar
     c                   kfld                    rhtrl5_rper
     c                   kfld                    rhtrl5_biln
8.01 c*                  kfld                    rhtrl5_jour
      *
     c     rktrlb_key    klist
     c                   kfld                    rktrlb_firm
     c                   kfld                    rktrlb_raar
     c                   kfld                    rktrlb_biln
      *
     c     rltrlc_key    klist
     c                   kfld                    rltrlc_firm
     c                   kfld                    rltrlc_raar
     c                   kfld                    rltrlc_biln
      *
     c     rxmll1_key    klist
     c                   kfld                    rxmll1_head
     c                   kfld                    rxmll1_linj
      *
      *  Hent Balansekonti for kunder og leverandører
      *
     c                   move      w_firma       ra04l1_firm
     c     ra04l1_key    chain     ra04l1                             90
      *
      *
     c     *dtaara       define                  ax000par        100
     c     *lock         in        *dtaara
     c                   eval      p_perfm = %subst(ax000par:1:2)
     c                   eval      p_perfa = %subst(ax000par:3:4)
     c                   eval      p_pertm = %subst(ax000par:7:2)
     c                   eval      p_perta = %subst(ax000par:9:4)
     c                   eval      p_hcom  = %subst(ax000par:13:50)
      *
     c                   move      p_perfm       p_pfm
     c                   move      p_perfa       p_pfa
     c                   move      p_pertm       p_ptm
     c                   move      p_perta       p_pta
      *
7.02 c                   move      l_firm        w_firm
     c                   movel     'HOVEDBOK'    rxmll1_head
     c                   movel     'HOVEDBOK'    s_head
     c                   move      *zero         rxmll1_linj
     c     rxmll1_key    setll     rxmll1
     c                   read      rxmll1
      *
     c                   endsr
      *
      *
      *
**     A N A L Y S I S
                  <nl:Analysis>
                     <nl:AnalysisType>?P?</nl:AnalysisType>
                     <nl:AnalysisID>?P?</nl:AnalysisID>
                        <nl:AnalysisAmount>
                        <nl:Amount>?B?<nl:Amount>
                        </nl:AnalysisAmount>
                   </nl:Analysis>
                  <nl:CustomerID>?P?</nl:CustomerID>
                  <nl:SupplierID>?P?</nl:SupplierID>
