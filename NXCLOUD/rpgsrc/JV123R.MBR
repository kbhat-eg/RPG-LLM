     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV123R
      * Beskrivelse . . : Overføring av varer til EVR, start overføring
      *
7.01  * Spesialversjon. : Utvidet pga. JV128R (overfør en vare)
7.01  * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120896 HKO  Nytt program
      *       131196 ASK  Oppkall av subprogram med parametre
      *       240497 HKO  Kontroll-liste som default valg
      *       101202 HKO  Lagt inn grossistløsning
      *       130905 HKO  Overstyring til skaffevare som parameter
5.70  * PRGR  191108 bof  Utvidet med prisgruppe
6.10  *       030909 bof  Utvidet med sortimentskode
6.12  *       070812 bof  Løper igjennom og setter holdekode,for sikk.skyld
6.30  *       140714 bof  Utvidet med prisendringsdato + parameter for utg.priser
6.31  *       010715 bof  Fjerner at den setter holde kode her.
7.01  *       170616 bof  Utvidet pga. JV128R (overfør en vare)
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.12 fjvarl5    if   e           k disk    rename(jvarpfr:jvarl5r)
6.12 fjvprl2    if   e           k disk    rename(jvprpfr:jvprl2r)
6.12 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.12 fjvdtlu    uf   e           k disk    rename(jvdtpfr:jvdtlur)
     fjv123d    cf   e             workstn
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
6.30 d d_list                        50
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldor                        6  0 overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_ldop                        6  0 overlay(d_list:17)
     d  d_skaf                        1  0 overlay(d_list:23)
     d  d_kntr                        1  0 overlay(d_list:24)
8.01 d  d_prgr                        2    overlay(d_list:25)
8.01 d  d_osor                       10    overlay(d_list:27)
8.01 d  d_dato                         d   overlay(d_list:37)
8.01 d  d_utgp                        1  0 overlay(d_list:48)
8.01 d  d_dumm                        1    overlay(d_list:49)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
     d p_ogrp          s              2  0
     d p_hgrp          s              2  0
     d p_ugrp          s              3  0
     d p_ldop          s              6  0
     d p_skaf          s              1  0
8.01 d p_prgr          s              2
6.10 d p_osor          s             10
6.30 d p_dato          s               d   datfmt(*iso)
6.30 d p_utgp          s              1  0
      *
      *
6.12 d w_firm          s                   like(jvdfir)
7.01 d w_vare          s                   like(jvdvar)
      *
6.12 d jvarl5_ldor     s                   like(jvldor)
6.12 d jvprl2_ldor     s                   like(jxldor)
6.12 d jvarl1_vare     s                   like(jvvare)
6.12 d jvdtlu_dvar     s                   like(jvdvar)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standad funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Kopierier skjermvariable til datastruktur
      *
     c                   eval      d_kntr = b1valg
7.01 c                   eval      w_vare = *blank
      *
      * Kaller behandlingsprogram
      *
     c                   call      'JV620C'
     c                   parm                    d_list
7.01 c                   parm                    w_vare
      *
6.31 c*                  if         b1valg = 0
6.31 c*                  exsr      nullst_frigj
6.31 c*                  endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  * Subrutine for å sette frigj.i hold. Egentlig gjøres dette i JV621R
6.12  * men noen ganger henger det igjen.
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.12 c     nullst_frigj  begsr
6.12  *
6.12 c                   if        p_ldor <> 0
6.12  *
6.12 c                   eval      jvarl5_ldor = p_ldor
6.12 c     jvarl5_key    setll     jvarl5
6.12 c     jvarl5_key    reade     jvarl5
6.12 c                   dow       not %eof
6.12 c                   eval      jvdtlu_dvar = jvvare
6.12 c     jvdtlu_key    chain     jvdtlur
6.12 c                   eval      jvdhko = 1
6.12 c                   if        %found
6.12 c                   update    jvdtlur
6.12 c                   endif
6.12 c     jvarl5_key    reade     jvarl5
6.12 c                   enddo
6.12  *
6.12 c                   else
6.12  *
6.12 c                   eval      jvprl2_ldor = p_ldop
6.12 c     jvprl2_key    setll     jvprl2
6.12 c     jvprl2_key    reade     jvprl2
6.12 c                   dow       not %eof
6.12 c                   eval      jvarl1_vare = jxvare
6.12 c     jvarl1_key    chain     jvarl1
6.12 c                   if        %found
6.12 c                   eval      jvdtlu_dvar = jvvare
6.12 c     jvdtlu_key    chain     jvdtlur
6.12 c                   eval      jvdhko = 1
6.12 c                   if        %found
6.12 c                   update    jvdtlur
6.12 c                   endif
6.12 c                   endif
6.12 c     jvprl2_key    reade     jvprl2
6.12 c                   enddo
6.12  *
6.12 c                   endif
6.12  *
6.12 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
6.12  *
6.12  * Key for les av varer pr. leverandør
6.12  *
6.12 c     jvarl5_key    klist
6.12 c                   kfld                    jvarl5_ldor
6.12  *
6.12  * Key for oppdat. på vare-detalj
6.12  *
6.12 c     jvdtlu_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    jvdtlu_dvar
6.12  *
6.12  * Key for les på vare-pris (prisgivere)
6.12  *
6.12 c     jvprl2_key    klist
6.12 c                   kfld                    jvprl2_ldor
6.12  *
6.12  * Key for oppslag på vare
6.12  *
6.12 c     jvarl1_key    klist
6.12 c                   kfld                    jvarl1_vare
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldop
     c                   parm                    p_skaf
5.70 c                   parm                    p_prgr
6.10 c                   parm                    p_osor
6.30 c                   parm                    p_dato
6.30 c                   parm                    p_utgp
      *
      * Flytter inngående parametre til utgående.
      *
     c                   eval      d_firm = p_firm
     c                   eval      d_ldor = p_ldor
     c                   eval      d_ogrp = p_ogrp
     c                   eval      d_hgrp = p_hgrp
     c                   eval      d_ugrp = p_ugrp
     c                   eval      d_ldop = p_ldop
     c                   eval      d_skaf = p_skaf
5.70 c                   eval      d_prgr = p_prgr
6.10 c                   eval      d_osor = p_osor
6.30 c                   eval      d_dato = p_dato
6.30 c                   eval      d_utgp = p_utgp
6.30 c                   eval      d_dumm = '*'
      *
      * Initiering av skjermfelter
      *
     c                   eval      b1valg = 0
      *
     c                   endsr
