     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR890R
      * Beskrivelse . . : Rapp. vareregister, innlesing av anbrekksantall/
      *                   bestillingsmengde fra S4
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110302 HKO  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvprl2    if   e           k disk    rename(jvprpfr:jvprl2r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av recordbeskrivelse - S4
      *
     d                 ds
     d m1_list                       75
      *
     d                 ds
     d m3_list                      102
     d  m3_ldor                      15    overlay(m3_list:4)
     d  m3_lvar                      20    overlay(m3_list:19)
      *
     d m5_list                       56
     d  m5_anta                       8    overlay(m5_list:49)
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d jvprl2_ldor     s                   like(jxldor)
     d jvprl2_lvar     s                   like(jxlvar)
     d vvarlu_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_fØrste        s              1
      *
     d w_rect          s              3
     d w_ldor_4        s              4
     d w_anta          s              8  0
     d w_leng          s              1  0
      *
     d w_firm          s                   like(vvfirm)
     d w_ldor          s                   like(jxldor)
     d w_lvar          s                   like(jxlvar)
      *
      * Definering av konstanter
      *
     d c_null          s              8    inz('00000000')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_fØrste = *on
      *
      * Leser fil
      *
     c                   read      jrvapf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      w_rect = %subst(jrvrad:1:3)
      *
      * Kaller oppdateringsprogram ved brudd på vare
      *
     c                   if        w_rect = '161' and
     c                             m1_list <> *blank and
     c                             b_fØrste = *off
      *
     c                   exsr      oppdater
      *
     c                   eval      m1_list = *blank
     c                   eval      m3_list = *blank
     c                   eval      m5_list = *blank
      *
     c                   endif
      *
      * Tar vare på data fra record-typer for vare
      *
     c                   select
     c                   when      w_rect  = '161'
     c                   eval      m1_list = jrvrad
     c                   when      w_rect  = '163'
     c                   eval      m3_list = jrvrad
     c                   when      w_rect  = '165'
     c                   eval      m5_list = jrvrad
     c                   endsl
      *
     c                   if        w_rect = '161'
     c                   eval      b_fØrste = *off
     c                   endif
      *
     c                   read      jrvapf                                 90
     c                   enddo
      *
      * Oppdaterer siste vare
      *
     c                   if        m1_list <> *blank and
     c                             b_fØrste = *off
     c                   exsr      oppdater
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Test på at kun leverandør 9998 og leverandør 9999 leses inn
      *
     c                   if        m3_ldor <> '9998' and
     c                             m3_ldor <> '9999'
     c                   leavesr
     c                   endif
      *
     c                   if        m5_anta = *blank
     c                   leavesr
     c                   endif
      *
     c                   eval      w_leng = %len(%trim(m5_anta))
     c                   eval      m5_anta = %subst(c_null:1:8-w_leng) +
     c                                       %trim(m5_anta)
     c                   move      m5_anta       w_anta
      *
     c                   if        w_anta = 0 or
     c                             w_anta = 1
     c                   leavesr
     c                   endif
      *
     c                   eval      w_ldor = 0
     c                   movel     m3_ldor       w_ldor_4
     c                   move      w_ldor_4      w_ldor
      *
     c                   eval      jlevl1_ldor = w_ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not %found or
     c                             jlenum = 0
     c                   leavesr
     c                   endif
      *
      * Finner og behandler vare
      *
     c                   eval      w_lvar = m3_lvar
      *
     c                   eval      jvprl2_ldor = w_ldor
     c                   eval      jvprl2_lvar = w_lvar
     c     jvprl2_key    chain     jvprl2
     c                   if        %found
     c                   eval      vvarlu_vare = jxvare
     c     vvarlu_key    chain     vvarlur
     c                   if        %found and
     c                             vvldor = jlenum
     c                   eval      vvbmen = w_anta
     c                   update    vvarlur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leveradør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl2_key    klist
     c                   kfld                    jvprl2_ldor
     c                   kfld                    jvprl2_lvar
      *
      * Key for oppdatering av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
