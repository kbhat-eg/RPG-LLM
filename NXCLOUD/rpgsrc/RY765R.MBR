     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY765R
      * Beskrivelse . . : FIX: Setter RSTS=0 på varer fra Christ og Løvensk.
      *
      *                   Endrer til rsts=0 i vare-pakning-register og vare-
      *                   pris-register på varer fra Christ og Løvenskiold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100200 HKO  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpklu    uf   e           k disk    rename(jvpkpfr:jvpklur)
     fjvprlu    uf   e           k disk    rename(jvprpfr:jvprlur)
      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jvpklu_vare     s                   like(jwvare)
     d jvprlu_vare     s                   like(jxvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser vareregisteret
      *
     c                   eval      jvarl1_vare = '90000000'
     c     jvarl1_key    setll     jvarl1
     c                   read      jvarl1r                                90
     c                   dow       *in90 = *off
      *
     c                   if        jvvare < '90000000'
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        jvldor <> 9998 and
     c                             jvldor <> 9999
     c                   goto      neste_vare
     c                   endif
      *
      * Oppdaterer vare-pakning-register
      *
     c                   eval      jvpklu_vare = jvvare
     c     jvpklu_key    setll     jvpklu
     c     jvpklu_key    reade     jvpklur                                91
     c                   dow       *in91 = *off
      *
     c                   if        jwpakn = '000' or
     c                             jwpakn = '001' or
     c                             jwpakn = '002' or
     c                             jwpakn = '003' or
     c                             jwpakn = '004'
     c                   eval      jwrsts = 0
     c                   update    jvpklur
     c                   endif
      *
     c     jvpklu_key    reade     jvpklur                                91
     c                   enddo
      *
      * Oppdaterer vare-pris-register
      *
     c                   eval      jvprlu_vare = jvvare
     c     jvprlu_key    chain     jvprlur                            91
     c                   if        *in91 = *off
     c                   eval      jxrsts = 0
     c                   update    jvprlur
     c                   endif
      *
     c     neste_vare    tag
      *
     c                   read      jvarl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
      *
     c                   endsr
