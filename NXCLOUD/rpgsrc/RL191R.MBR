     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : RL191R
      * Beskrivelse . . : Leverandør, kopiering til annen filgruppe
      *
      *                   Kalles gjennom CL som har overridet RLEVLU, RLBALU
      *                   og RLSOLU til filgruppen det skal kopieres til.
      *
      *                   Dersom leverandør ikke er utfylt behandles alle
      *                   leverandører
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220503 HKO  Nytt program
6.20  *       130415 bof  Setter endringsdato lik dd, slik at det repl.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kopieres fra
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlbapf    if   e           k disk    rename(rlbapfr:rlbapfr)
     frlsol1    if   e           k disk    rename(rlsopfr:rlsol1r)
      *
      * Kopieres til
      *
     frlevlu    uf a e           k disk    rename(rlevpfr:rlevlur)
     frlbalu    uf a e           k disk    rename(rlbapfr:rlbalur)
     frlsolu    uf a e           k disk    rename(rlsopfr:rlsolur)
      *
      * Definering parametre
      *
     d p_firm          s                   like(rlfirm)
     d p_levr          s                   like(rllevr)
     d p_firt          s                   like(rlfirm)
     d p_oppr          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d rlbapf_levr     s                   like(rvlevr)
     d rlsol1_slev     s                   like(rlslev)
     d rlevlu_levr     s                   like(rllevr)
     d rlbalu_levr     s                   like(rvlevr)
     d rlsolu_slev     s                   like(rlslev)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rlfirm)
     d w_firt          s                   like(rlfirm)
      *
     d s_levr          s                   like(rllevr)
     d s_navn          s                   like(rlnavn)
     d s_alfa          s                   like(rlalfa)
     d s_gate          s                   like(rlgate)
     d s_adr2          s                   like(rladr2)
     d s_ponr          s                   like(rlponr)
     d s_sted          s                   like(rlsted)
     d s_komn          s                   like(rlkomn)
     d s_land          s                   like(rlland)
     d s_ftnr          s                   like(rlftnr)
     d s_pers          s                   like(rlpers)
     d s_emal          s                   like(rlemal)
     d s_tlfn          s                   like(rltlfn)
     d s_tlfx          s                   like(rltlfx)
     d s_mobn          s                   like(rlmobn)
     d s_bgir          s                   like(rlbgir)
     d** s_flev          s                   like(rlflev)
     d s_facn          s                   like(rlfacn)
     d s_fact          s                   like(rlfact)
     d s_nobb          s                   like(rlnobb)
     d s_edik          s                   like(rledik)
     d s_eanl          s                   like(rleanl)
     d s_dist          s                   like(rldist)
     d s_avde          s                   like(rlavde)
     d s_katg          s                   like(rlkatg)
     d s_rabk          s                   like(rlrabk)
     d s_betb          s                   like(rlbetb)
     d s_betm          s                   like(rlbetm)
     d s_saks          s                   like(rlsaks)
     d s_isok          s                   like(rlisok)
     d s_mkod          s                   like(rlmkod)
     d s_form          s                   like(rlform)
     d s_frie          s                   like(rlfrie)
     d s_ndat          s                   like(rlndat)
     d s_ntim          s                   like(rlntim)
     d s_nsig          s                   like(rlnsig)
     d s_edat          s                   like(rledat)
     d s_etim          s                   like(rletim)
     d s_esig          s                   like(rlesig)
6.20 d s_2dpt          s                   like(rl2dpt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler valgt leverandør
      *
     c                   if        p_levr <> 0
     c                   eval      rlevl1_levr = p_levr
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   exsr      kopier
     c                   endif
     c                   goto      avslutt
     c                   endif
      *
      * Behandler alle leverandører
      *
     c     rlevl1_key2   setll     rlevl1
     c     rlevl1_key2   reade     rlevl1
     c                   dow       not %eof(rlevl1)
     c                   exsr      kopier
     c     rlevl1_key2   reade     rlevl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kopierer leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopier        begsr
      *
      * Tar vare på informasjone fra leverandør
      *
     c                   eval      s_levr = rllevr
     c                   eval      s_navn = rlnavn
     c                   eval      s_alfa = rlalfa
     c                   eval      s_gate = rlgate
     c                   eval      s_adr2 = rladr2
     c                   eval      s_ponr = rlponr
     c                   eval      s_sted = rlsted
     c                   eval      s_komn = rlkomn
     c                   eval      s_land = rlland
     c                   eval      s_ftnr = rlftnr
     c                   eval      s_pers = rlpers
     c                   eval      s_emal = rlemal
     c                   eval      s_tlfn = rltlfn
     c                   eval      s_tlfx = rltlfx
     c                   eval      s_mobn = rlmobn
     c                   eval      s_bgir = rlbgir
     c***                eval      s_flev = rlflev
     c                   eval      s_facn = rlfacn
     c                   eval      s_fact = rlfact
     c                   eval      s_nobb = rlnobb
     c                   eval      s_edik = rledik
     c                   eval      s_eanl = rleanl
     c                   eval      s_dist = rldist
     c***                eval      s_avde = rlavde
     c                   eval      s_katg = rlkatg
     c                   eval      s_rabk = rlrabk
     c                   eval      s_betm = rlbetm
     c                   eval      s_saks = rlsaks
     c                   eval      s_isok = rlisok
     c                   eval      s_mkod = rlmkod
     c                   eval      s_form = rlform
     c                   eval      s_frie = rlfrie
     c                   eval      s_ndat = rlndat
     c                   eval      s_ntim = rlntim
     c                   eval      s_nsig = rlnsig
     c                   eval      s_edat = rledat
     c                   eval      s_etim = rletim
     c                   eval      s_esig = rlesig
6.20 c                   eval      s_2dpt = rl2dpt
      *
     c                   eval      s_betb = rlbetb
      *
      * Oppdaterer eller oppretter leverandør
      * Dersom ny leverandør, og nye leverandører ikke skal opprettes
      * avbrytes rutinen.
      *
     c                   eval      rlevlu_levr = s_levr
     c     rlevlu_key    chain     rlevlur
     c                   if        not %found and
     c                             p_oppr = *off
     c                   leavesr
     c                   endif
      *
     c                   if        not %found(rlevlu)
     c                   clear                   rlevlur
     c                   endif
      *
     c                   eval      rlnavn = s_navn
     c                   eval      rlalfa = s_alfa
     c                   eval      rlgate = s_gate
     c                   eval      rladr2 = s_adr2
     c                   eval      rlponr = s_ponr
     c                   eval      rlsted = s_sted
     c                   eval      rlkomn = s_komn
     c                   eval      rlland = s_land
     c                   eval      rlftnr = s_ftnr
     c                   eval      rlpers = s_pers
     c                   eval      rlemal = s_emal
     c                   eval      rltlfn = s_tlfn
     c                   eval      rltlfx = s_tlfx
     c                   eval      rlmobn = s_mobn
     c                   eval      rlbgir = s_bgir
     c***                eval      rlflev = s_flev
     c                   eval      rlfacn = s_facn
     c                   eval      rlfact = s_fact
     c                   eval      rlnobb = s_nobb
     c                   eval      rledik = s_edik
     c                   eval      rleanl = s_eanl
     c                   eval      rldist = s_dist
     c***                eval      rlavde = s_avde
     c                   eval      rlkatg = s_katg
     c                   eval      rlrabk = s_rabk
     c                   eval      rlbetm = s_betm
     c                   eval      rlsaks = s_saks
     c                   eval      rlisok = s_isok
     c                   eval      rlmkod = s_mkod
     c                   eval      rlform = s_form
     c                   eval      rlfrie = s_frie
     c                   eval      rlndat = s_ndat
     c                   eval      rlntim = s_ntim
     c                   eval      rlnsig = s_nsig
6.20 c                   if        s_2dpt > rl2dpt
6.20 c                   time                    rledat
6.20 c                   time                    rletim
6.20 c                   endif
6.20 c                   eval      rl2dpt = s_2dpt
     c                   eval      rlesig = s_esig
      *
     c                   if        %found(rlevlu)
     c                   update    rlevlur
     c                   else
     c                   eval      rlfirm = w_firt
     c                   eval      rllevr = s_levr
     c                   eval      rlbetb = s_betb
     c                   write     rlevlur
     c                   endif
      *
      * Oppdaterer bankinformasjon-utland
      *
     c                   eval      rlbalu_levr = s_levr
     c     rlbalu_key    chain     rlbalur
     c                   if        %found
     c                   delete    rlbalur
     c                   endif
      *
     c                   eval      rlbapf_levr = s_levr
     c     rlbapf_key    chain     rlbapf
     c                   if        %found
     c                   eval      rvfirm = w_firt
     c                   write     rlbalur
     c                   endif
      *
      * Oppdaterer leverandør-søketekst
      *
     c                   eval      rlsolu_slev = s_levr
     c     rlsolu_key    chain     rlsolur
     c                   if        %found
     c                   delete    rlsolur
     c                   endif
      *
     c                   eval      rlsol1_slev = s_levr
     c     rlsol1_key    chain     rlsol1
     c                   if        %found
     c                   eval      rlsfir = w_firt
     c                   write     rlsolur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametere
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_levr
     c                   parm                    p_firt
     c                   parm                    p_oppr
      *
      * Key for oppslag/les på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
     c     rlevl1_key2   klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bankinformasjon-utland
      *
     c     rlbapf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlbapf_levr
      *
      * Key for oppslag på leverandør-søketekst
      *
     c     rlsol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlsol1_slev
      *
      * Key for oppdatering av leverandør (til)
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firt
     c                   kfld                    rlevlu_levr
      *
      * Key for oppdatering av bankinformasjon-utland (til)
      *
     c     rlbalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlbalu_levr
      *
      * Key for oppdatering av leverandør-søketekst (til)
      *
     c     rlsolu_key    klist
     c                   kfld                    w_firt
     c                   kfld                    rlsolu_slev
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_firt = p_firt
      *
     c                   endsr
