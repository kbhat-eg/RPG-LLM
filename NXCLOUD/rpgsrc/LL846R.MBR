      /TITLE  ASLAGR: Sletter lagerposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LL846R
      *  Beskrivelse . . : Sletter lagerposter, slette historikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  -------------------------------------------------
      *
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhisl1    if   e           k disk    rename(vhispfr:vhisl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvhislu    uf   e           k disk    rename(vhispfr:vhislur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_val1                        1  0 overlay(d_prec)
     d  d_val2                        1  0 overlay(d_prec:2)
     d  d_val3                        1  0 overlay(d_prec:3)
     d  d_val4                        1  0 overlay(d_prec:4)
     d  d_lage                        2  0 overlay(d_prec:5)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vhisl1_lage     s                   like(vhlage)
     d vhisl1_vare     s                   like(vhvare)
     d vvarl1_vare     s                   like(vvvare)
     d vhislu_lage     s                   like(vhlage)
     d vhislu_vare     s                   like(vhvare)
     d vhislu_dato     s                   like(vhdato)
     d vhislu_time     s                   like(vhtime)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_parm          s             64
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        d_val4 <> 1
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      vhisl1_vare = *blank
     c                   eval      vhisl1_lage = d_lage
     c     vhisl1_key    setll     vhisl1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vhisl1r                                90
     c                   dow       *in90 = *off
      *
     c                   if        vhfirm <> w_firm
     c                   goto      end_pgm
     c                   endif
      *
     c                   if        vhlage <> vhisl1_lage
     c                   goto      les_neste
     c                   endif
      *
      *  Sjekk om sletting av alle poster i lager
      *
     c                   if        d_val3 = 1
     c                   goto      slett_post
     c                   endif
      *
      *  Sjekk om bare sletting poster som ikke er er varetype L
      *
     c                   if        d_val1 = 1
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    vhfirm
5.70 c                   parm                    vhvare
5.70 c                   parm                    vhlage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp <> 'L'
     c                   goto      slett_post
     c                   endif
     c                   endif
      *
      *  Sjekk om bare sletting poster som ikke finnes i vareregister
      *
     c                   if        d_val2 = 1
     c                   eval      vvarl1_vare = vhvare
     c     vvarl1_key    chain     vvarl1                             93
     c                   if        *in93 = *on
     c                   goto      slett_post
     c                   endif
     c                   endif
      *
     c                   goto      les_neste
      *
      *  Sletter lagerpost på valgt record
      *
     c     slett_post    tag
      *
     c                   eval      vhislu_vare = vhvare
     c                   eval      vhislu_lage = vhlage
     c                   eval      vhislu_dato = vhdato
     c                   eval      vhislu_time = vhtime
     c     vhislu_key    chain     vhislu                             94
     c                   if        *in94 = *off
     c                   delete    vhislur
     c                   endif
     c     les_neste     tag
     c                   read      vhisl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGERBOK
      *
     c     vhisl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl1_lage
     c                   kfld                    vhisl1_vare
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for oppdatering av LAGERBOK
      *
     c     vhislu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhislu_lage
     c                   kfld                    vhislu_vare
     c                   kfld                    vhislu_dato
     c                   kfld                    vhislu_time
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
