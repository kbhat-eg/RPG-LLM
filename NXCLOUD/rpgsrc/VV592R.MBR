     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VV592R
      * Beskrivelse . . : Vare, pris/etikett - radioterminal
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111000 HKO  Nytt program
      *       141200 HKO  Lagt inn "field exit" og bedret logikk ved
      *                   bytting av enhet
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
      *       080306 KOB  Går ut av programmet etter utskrift av etikett
5.70  * PRGR  041108 bof  Utvidet med prisgruppe
6.10  * R6.10 191109 bsa  Merker riktig enhet ifølge gtin nr.
6.11  * R6.11 300910 bsa  Hvis ikke enhet via gtin kode, bruk slagsenhet 1
6.nu  * R6.30 160614 bof  Endret ihht. nummerutvidelse
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fvv592d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_kund          s              6  0
     d p_vare          s                   like(vvvare)
6.10 d p_enhe          s                   like(veenhe)
      *
      * Datastruktur - parametre inn til VP900R
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Datastruktur - parametre ut fra VP900R
      *
     d                 ds
     d horec                         80
     d  hokpri                        1    overlay(horec:2)
     d  hosapr                        9  2 overlay(horec:12)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variabler i nøkler
      *
     d vvenl2_saen     s                   like(vesaen)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
     d w_saim          s              9  2
     d x               s              1  0
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_enhe          s                   like(veenhe)
6.10 d w_xenhe         s                   like(veenhe)
      *
     d w_enhe_gml      s                   like(veenhe)
      *
8.01 d w_prgr          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1BLD Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
      *
      * Henter vareinformasjon
      *
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b1vare = vvvare
      *
     c                   eval      vvtek1 = %trim(vvtek1)
     c                   eval      b1tek1 = %subst(vvtek1:1:19)
     c                   eval      b1tek2 = %subst(vvtek1:20:16)
      *
     c                   eval      b1lnav = *blank
      *
     c                   if        vvldor <> 0
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      b1lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Henter tre salgsenheter
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      b1enh1 = *blank
     c                   eval      b1enh2 = *blank
     c                   eval      b1enh3 = *blank
      *
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      b1enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      b1enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      b1enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
6.11  * Hvis enhet via gtin er blank. Brusk salgsenhet 1
6.11 c                   if        w_xenhe = *blank
6.11 c                   eval      w_xenhe = b1enh1
6.11 c                   endif
6.10  * Merk riktig enhet
6.10 c                   if        w_xenhe <> *blank
6.10 c                   select
6.10 c                   when      w_xenhe = b1enh1
6.10 c                   eval      *in80  = *on
6.10 c                   eval      *in81  = *off
6.10 c                   eval      *in82  = *off
6.10 c                   when      w_xenhe = b1enh2
6.10 c                   eval      *in80  = *off
6.10 c                   eval      *in81  = *on
6.10 c                   eval      *in82  = *off
6.10 c                   when      w_xenhe = b1enh3
6.10 c                   eval      *in80  = *off
6.10 c                   eval      *in81  = *off
6.10 c                   eval      *in82  = *on
6.10 c                   endsl
6.10 c                   endif
6.10  *
6.10 c**                 eval      w_enhe = b1enh1
6.10 c                   eval      w_enhe = w_xenhe
     c                   eval      b1anta = 0
      *
6.10 c**                 eval      w_enhe_gml = b1enh1
6.10 c                   eval      w_enhe_gml = w_xenhe
      *
      * Finner pris, priskode og rabatt for kunde på enhet
      *
     c                   exsr      hent_pris
      *
      * Kalkulerer pris etter antall, og viser bildet
      *
     c     b1tagb        tag
      *
     c                   if        b1anta <> 0
     c                   eval(h)   b1saim = w_saim * b1anta
     c                   endif
      *
     c                   exfmt     b1bld
      *
     c                   if        *inkc
     c                   goto      avslutt
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   exsr      bytt_enhet
     c                   goto      b1tagb
     c                   endif
      *
      * F2=Etikett utskrift
      *
     c                   if        *inkb
     c                   call      'FP250C'
     c                   parm                    p_vare
      *
     c                   goto      avslutt
     c                   endif
      *
      * F5=Forny
      *
     c                   if        *inke
     c                   goto      b1taga
     c                   endif
      *
     c                   goto      b1tagb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for hetning av pris, priskode og rabatt for kunde/enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      w_saim = 0
     c                   eval      b1saim = 0
     c                   eval      b1rab1 = 0
     c                   eval      b1rab2 = 0
     c                   eval      b1kpri = *blank
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = p_kund
     c                   eval      hikpro = 0
     c                   eval      hivare = w_vare
     c                   eval      hilety = 0
     c                   eval      hienhe = w_enhe
     c                   eval      hidato = *loval
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hitilb = *blank
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hiinlr = *blank
      *
     c                   time                    hidato
     c                   time                    hitime
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   eval      w_saim = hosapr
     c                   eval      b1kpri = hokpri
     c                   eval      b1rab1 = horab1
     c                   eval      b1rab2 = horab2
      *
     c                   if        horab1 > 0
     c                   eval(h)   w_saim = w_saim - (w_saim * horab1 / 100)
     c                   endif
     c                   if        horab2 > 0
     c                   eval(h)   w_saim = w_saim - (w_saim * horab2 / 100)
     c                   endif
      *
     c                   if        w_saim <> 0
     c                   eval      w_kode = 4
     c                   eval      w_saim = w_saim * w_mvat
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    w_saim
     c                   endif
      *
     c                   eval      b1saim = w_saim
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulerer og henter antall mellom enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bytt_enhet    begsr
      *
     c                   select
     c                   when      *in80 = *on and
     c                             b1enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        b1enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      w_enhe = b1enh1
     c                   when      *in81 = *on
     c                   eval      w_enhe = b1enh2
     c                   when      *in82 = *on
     c                   eval      w_enhe = b1enh3
     c                   endsl
      *
     c                   if        w_enhe <> w_enhe_gml
      *
     c                   select
     c                   when      w_enhe_gml = b1enh1
     c                   eval(h)   b1anta = b1anta / w_omr2
     c                   when      w_enhe_gml = b1enh2
     c                   if        *in82 = *on
     c                   eval(h)   b1anta = b1anta * w_omr2 / w_omr3
     c                   else
     c                   eval(h)   b1anta = b1anta * w_omr2 / w_omr1
     c                   endif
     c                   when      w_enhe_gml = b1enh3
     c                   eval(h)   b1anta = b1anta * w_omr3 / w_omr1
     c                   endsl
      *
     c                   endif
      *
     c                   eval      w_enhe_gml = w_enhe
      *
     c                   exsr      hent_pris
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      *in31 = *on
     c                   goto      b1tagb
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_vare
6.10 c                   parm                    p_enhe
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
6.10 c                   eval      w_xenhe = p_enhe
      *
      * Finner moms-sats
      *
     c                   time                    w_udat
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c                   endsr
