      /TITLE  Utskrift av varebevegelsesliste
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : MB601R
      * Beskrivelse . . : Utskrift av ABC analyse detajer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           250804  Nytt program                             BØ
      *           160307  Lagt på nye parametere                   BØ
      *           281107  Feil-retting for å få ut lista           BØ
6.10  * R6.10     171109  Tar hensyn til inntastet lager           BSA
6.20  * R6.20     061110  Utvidet med lager på sortiment           BOF
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmabdl5    if   e           k disk    rename(mabdpfr:mabdl5r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
6.20 fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
      *
     fmb601p    o    e             printer
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fabc                        2    overlay(p_prec)
     d  p_tabc                        2    overlay(p_prec:3)
     d  p_fogr                        2  0 overlay(p_prec:5)
     d  p_togr                        2  0 overlay(p_prec:7)
     d  p_fhgr                        2  0 overlay(p_prec:9)
     d  p_thgr                        2  0 overlay(p_prec:11)
     d  p_fugr                        3  0 overlay(p_prec:13)
     d  p_tugr                        3  0 overlay(p_prec:16)
     d  p_levn                        6  0 overlay(p_prec:19)
     d  p_vsor                       10    overlay(p_prec:25)
     d  p_mbeh                        1  0 overlay(p_prec:35)
     d  p_tell                        1  0 overlay(p_prec:36)
     d  p_lagk                        2    overlay(p_prec:37)
      *
      * Datastrukturer for testing på varegrupper
      *
     d                 ds
     d f_vgrp                         7
     d  f_ogrp                        2  0 overlay(f_vgrp)
     d  f_hgrp                        2  0 overlay(f_vgrp:3)
     d  f_ugrp                        3  0 overlay(f_vgrp:5)
      *
     d                 ds
     d t_vgrp                         7
     d  t_ogrp                        2  0 overlay(t_vgrp)
     d  t_hgrp                        2  0 overlay(t_vgrp:3)
     d  t_ugrp                        3  0 overlay(t_vgrp:5)
      *
     d                 ds
     d v_vgrp                         7
     d  v_ogrp                        2  0 overlay(v_vgrp)
     d  v_hgrp                        2  0 overlay(v_vgrp:3)
     d  v_ugrp                        3  0 overlay(v_vgrp:5)
      *
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
6.20 d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
      *
      * Definering av variable
      *
     d w_parm          s            128
     d w_firm          s                   like(vvfirm)
     d w_lagk          s                   like(mblagk)
     d w_vsor          s                   like(mbvsor)
     d w_ogrp          s                   like(mbogrp)
     d w_hgrp          s                   like(mbhgrp)
     d w_ugrp          s                   like(mbugrp)
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_vare          s              8  0
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_tell          s              3  0
      *
     d b_feil          s              1    inz(*off)
     d b_init          s              1    inz(*on)
      *
      * - - - - - - - - - -
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * A2HDR  - Lager heading
      * A3HDR  - Overgruppe heading
      * A4HDR  - Hovedgruppe heading
      * A5HDR  - Undergruppe heading
      * D1DET  - Detail  D1 på rapport
      *
      * Skriv første heading
      *
     c                   write     a1hdr                                30
      *
      * Lese file
      *
      *
readec                   read      mabdl5
readec                   dow       not %eof(mabdl5)
      *
      * Utskrift av detaljer
      *
     c                   movel     mbvare        d1vare
     c                   movel     mbvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      d1text = vvtek1
     c                   else
     c                   eval      d1text = 'Ukjent vare'
     c                   endif
      *
      * sjekker abc-kode
      *
     c                   if        mbkabc < p_fabc or
     c                             mbkabc > p_tabc
     c                   goto      neste
     c                   endif
      *
      * sjekker varegruppe
      *
      *   overgruppe
     c                   if        f_ogrp <> 0
     c                   if        vvogrp < f_ogrp
     c                             or vvogrp > t_ogrp
     c                   goto      neste
     c                   endif
     c                   endif
      *   hovedgruppe
     c                   if        f_hgrp <> 0
     c                   if        vvhgrp < f_hgrp
     c                             or vvhgrp > t_hgrp
     c                   goto      neste
     c                   endif
     c                   endif
      *   undergruppe
     c                   if        f_ugrp <> 0
     c                   if        vvugrp < f_ugrp
     c                             or vvugrp > t_ugrp
     c                   goto      neste
     c                   endif
     c                   endif
      *
      * sjekker leverandør
     c                   if        p_levn <> 0 and
     c                             vvldor <> p_levn
     c                   goto      neste
     c                   endif
6.10  *
6.10  * sjekker lagerkoden
6.10 c                   if        p_lagk <> *blank and
6.10 c                             p_lagk <> mblagk
6.10 c                   goto      neste
6.10 c                   endif
      *
      * sjekker varesortiment
     c                   if        p_vsor <> *blank
     c                   eval      vsdtl1_vsor = p_vsor
     c                   eval      vsdtl1_ldor = vvldor
     c                   eval      vsdtl1_ogrp = vvogrp
     c                   eval      vsdtl1_hgrp = vvhgrp
     c                   eval      vsdtl1_ugrp = vvugrp
     c                   eval      vsdtl1_modn = 0
     c                   eval      vsdtl1_vare = vvvare
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ldor = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ogrp = 0
     c                   eval      vsdtl1_ldor = vvldor
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ldor = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ldor = 0
     c                   eval      vsdtl1_vare = vvvare
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   goto      neste
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * sjekker antall tellet
      *
     c                   eval      d1atel = 0
     c                   eval      d1behl = 0
     c                   if        P_tell = 1 or
     c                             P_mbeh = 1
     c                   exsr      ant_tellet
     c                   endif
      *
     c                   eval      d1kabc = mbkabc
     c                   eval      d1anta = mbanta
     c                   eval      d1anto = mbanto
     c                   eval      d1omse = mbomse
6.10 c                   eval      d1lagk = mblagk
      *
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c     neste         tag
      *
readec                   read      mabdl5
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av lagerregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ant_tellet    begsr
      *
     c                   eval      d1atel = 0
      *
     c                   eval      vlagl1_vare = d1vare
     c     vlagl1_key    setll     vlagl1                                 90
     c     vlagl1_key    reade     vlagl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        p_tell = 1
     c                   eval      d1atel = d1atel + vlatel
     c                   endif
     c                   if        p_mbeh = 1
     c                   eval      d1behl = d1behl + vlbehl
     c                   endif
      *
     c     vlagl1_key    reade     vlagl1                                 90
     c                   enddo
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      * Key for oppslag på varesortiment
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
6.20 c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
6.20  *
6.20  * Key for oppslag på varesortiment  hode
6.20  *
6.20 c     vshel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
     c                   time                    w_dato
     c                   time                    w_time
     c                   eval      w_firm = l_firm
      *
      * Redigere heading
      *
     c     *dmy          move      w_dato        a1date
     c     *hms          move      w_time        a1time
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1ogrp = p_fogr
     c                   eval      a1hgrp = p_fhgr
     c                   eval      a1ugrp = p_fugr
     c                   eval      w_ogrp = p_fogr
     c                   eval      w_hgrp = p_fhgr
     c                   eval      w_ugrp = p_fugr
     c                   eval      w_vsor = p_vsor
     c                   eval      w_lagk = p_lagk
      *
      * Bygg opp varegryppe fra
      *
     c                   eval      f_ogrp = p_fogr
     c                   eval      f_hgrp = p_fhgr
     c                   eval      f_ugrp = p_fugr
      *
      * Bygg opp varegryppe til
      *
     c                   eval      t_ogrp = p_togr
     c                   eval      t_hgrp = p_thgr
     c                   eval      t_ugrp = p_tugr
     c                   eval      a1vsor = p_vsor
     c                   eval      a1ogrp = p_fogr
     c                   eval      a1hgrp = p_fhgr
     c                   eval      a1ugrp = p_fugr
      *
6.20 c                   if        p_vsor <> *blank
6.20 c                   eval      vshel1_vsor = p_vsor
6.20 c                   if        p_lagk <> *blank
6.20 c                   move      p_lagk        vshel1_lage
6.20 c                   else
6.20 c                   eval      vshel1_lage = 0
6.20 c                   endif
6.20 c     vshel1_key    chain     vshel1
6.20 c                   if        %found(vshel1)
6.20 c                   eval      vsdtl1_lage = vshel1_lage
6.20 c                   else
6.20 c                   eval      vsdtl1_lage = 0
6.20 c                   endif
6.20 c                   endif
      *
     c                   endsr
