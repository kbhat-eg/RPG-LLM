     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF795R
      * Beskrivelse . . : Endring av kunde-paramaterfil, Oppdateringen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.30 200904 KOB  Fjernet rabattgruppe
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fskpml1    if   f  512    25aidisk    keyloc(1)
     fskpmlu    uf   e           k disk    rename(skpmpfr:skpmlur)
      *
     fvahgl1    if   e           k disk    rename(vahgpfr:vahgl1r)
     fvaugl1    if   e           k disk    rename(vaugpfr:vaugl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fra11pf    if   e           k disk
     fra06pf    if   e           k disk
     fra08pf    if   e           k disk
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fra12pf    if   e           k disk
     fvotyl1    if   e           k disk    rename(votypfr:votyp1r)
     fra07pf    if   e           k disk
     fra10pf    if   e           k disk
     fra26l1    if   e           k disk    rename(ra26pfr:ra26l1r)
     fvltpl1    if   e           k disk
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
      * Definering av variabler i nøkler
      *
     d skpmlu_type     s                   like(sptype)
     d skpmlu_kode     s                   like(spkode)
     d skpmlu_valg     s                   like(spvalg)
      *
     d vahgl1_hgrp     s                   like(vfhhgr)
     d vaugl1_hgrp     s                   like(vfuhgr)
     d vaugl1_ugrp     s                   like(vfuugr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d ra11pf_kkod     s                   like(rakkod)
     d ra06pf_fkod     s                   like(rafkod)
     d ra08pf_hkod     s                   like(rahkod)
     d ra09l1_ikod     s                   like(raikod)
     d ra12pf_lkod     s                   like(ralkod)
     d votyl1_aork     s                   like(vaotyp)
     d ra07pf_gkod     s                   like(ragkod)
     d ra10pf_jkod     s                   like(rajkod)
     d ra26l1_zsak     s                   like(razsak)
     d vltpl1_lety     s                   like(vylety)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(spfirm)
      *
     d w_slet          s              1  0
     d w_text          s             30
      *
      * Nøkkel Parameter-fil
      *
     d                 ds
     d skpml1_key                    25
     d skpml1_firm                    3  0 overlay(skpml1_key:01)
     d skpml1_type                   10    overlay(skpml1_key:04)
     d skpml1_kode                    2  0 overlay(skpml1_key:14)
     d skpml1_valg                   10    overlay(skpml1_key:16)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     iskpml1    ns  01   14 c0   15 c0
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
      *                                                    Alt. Hovedgruppe
     i          ns  01   14 c1   15 c1
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17 0spahgr
      *                                                    Alt. Undergruppe
     i          ns  01   14 c1   15 c2
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17 0spahgr
     i                                 18   20 0spaugr
      *                                                    Rabattgruppe
     i          ns  01   14 c1   15 c3
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   22 0sprgrp
      *                                                    Overgruppe
     i          ns  01   14 c1   15 c4
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17 0spogrp
      *                                                    Hovedgruppe
     i          ns  01   14 c1   15 c5
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17 0spogrp
     i                                 18   19 0sphgrp
      *                                                    Undergruppe
     i          ns  01   14 c1   15 c6
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17 0spogrp
     i                                 18   19 0sphgrp
     i                                 20   22 0spugrp
      *                                                    Kundekategori
     i          ns  01   14 c2   15 c1
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   19 0spkkat
      *                                                    Rabattkategori
     i          ns  01   14 c2   15 c2
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   18 0sprkat
      *                                                    Distrikt
     i          ns  01   14 c2   15 c3
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17 0spdist
      *                                                    Selger
     i          ns  01   14 c2   15 c4
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   19 0spselg
      *                                                    Land
     i          ns  01   14 c2   15 c5
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   18  spland
      *                                                    Varenummer
     i          ns  01   14 c3   15 c1
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 61  210  spfvar
     i                                211  360  sptvar
      *                                                    Kundenummer
     i          ns  01   14 c3   15 c2
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 61  120  spfkun
     i                                121  180  sptkun
      *                                                    Kundeprosjekt
     i          ns  01   14 c3   15 c3
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 61  120  spflad
     i                                121  180  sptlad
      *                                                    Periode
     i          ns  01   14 c4   15 c1
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 61   66 0spfper
     i                                 67   72 0sptper
      *                                                    Fakturanummer
     i          ns  01   14 c4   15 c2
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 61   66 0spffak
     i                                 67   72 0sptfak
     i                                 73   78 0spfkre
     i                                 79   84 0sptkre
      *                                                    Fakturadato
     i          ns  01   14 c4   15 c3
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 61   68 0spffda
     i                                 69   76 0spftda
      *                                                    Kampanje
     i          ns  01   14 c4   15 c4
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 61   65  spkamp
      *                                                    Ordretype
     i          ns  01   14 c5   15 c1
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17  spotyp
      *                                                    Avdeling
     i          ns  01   14 c5   15 c2
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   19 0spavde
      *                                                    Lager
     i          ns  01   14 c5   15 c3
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   17 0splage
      *                                                    Produktansvarlig
     i          ns  01   14 c5   15 c4
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   19  sppran
      *                                                    Leveringstype
     i          ns  01   14 c5   15 c5
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   16 0splety
      *                                                    Leverandør
     i          ns  01   14 c5   15 c6
     i                                  1    3 0spfirm
     i                                  4   13  sptype
     i                                 14   15 0spkode
     i                                 16   30  spvalg
     i                                 16   2100splevr
     i          ns  02
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Arbeide med handlinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Posisjonering
      *
     c                   exsr      posisjoner
      *
      * Les statistikkregisteret og oppdater
      *
     c                   exsr      oppdat
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese og oppdatere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
     c                   dou       *in15  = *on
      *
     c                   read      skpml1                                 15
      *
     c                   if        *in15                                        EOF
     c                   goto      end_oppdat
     c                   endif
      *
     c                   if        spfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_oppdat
     c                   endif
      *
      * Kaller opp subrutiner avhengig av valg
      *
     c                   move      *zero         w_slet
     c                   move      *blank        w_text
      *
      * Hovedgruppe
      *
     c                   if        spkode = 11
     c                   exsr      SP111R
     c                   goto      opdate
     c                   endif
      *
      * Undergruppe
      *
     c                   if        spkode = 12
     c                   exsr      SP112R
     c                   goto      opdate
     c                   endif
      *
      * Overgruppe
      *
     c                   if        spkode = 14
     c                   exsr      SP114R
     c                   goto      opdate
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        spkode = 15
     c                   exsr      SP115R
     c                   goto      opdate
     c                   endif
      *
      * Undergruppe
      *
     c                   if        spkode = 16
     c                   exsr      SP116R
     c                   goto      opdate
     c                   endif
      *
      * Kundekategori
      *
     c                   if        spkode = 21
     c                   exsr      SP121R
     c                   goto      opdate
     c                   endif
      *
      * Rabattkategori
      *
     c                   if        spkode = 22
     c                   exsr      SP122R
     c                   goto      opdate
     c                   endif
      *
      * Distrikt
      *
     c                   if        spkode = 23
     c                   exsr      SP123R
     c                   goto      opdate
     c                   endif
      *
      * Selger
      *
     c                   if        spkode = 24
     c                   exsr      SP124R
     c                   goto      opdate
     c                   endif
      *
      * Land
      *
     c                   if        spkode = 25
     c                   exsr      SP125R
     c                   goto      opdate
     c                   endif
      *
      * Varenummer
      *
     c                   if        spkode = 31
     c                   iter
     c                   endif
      *
      * Kundenummer
      *
     c                   if        spkode = 32
     c                   iter
     c                   endif
      *
      * Leveringsadresse
      *
     c                   if        spkode = 33
     c                   iter
     c                   endif
      *
      * Periode
      *
     c                   if        spkode = 41
     c                   move      1             w_slet
     c                   goto      opdate
     c                   endif
      *
      * Faktura/kreditnotanr.
      *
     c                   if        spkode = 42
     c                   iter
     c                   endif
      *
      * Fakturadato
      *
     c                   if        spkode = 43
     c                   move      1             w_slet
     c                   goto      opdate
     c                   endif
      *
      * Kampanje
      *
     c                   if        spkode = 44
     c                   iter
     c                   endif
      *
      * Ordretype
      *
     c                   if        spkode = 51
     c                   exsr      SP151R
     c                   goto      opdate
     c                   endif
      *
      * Avdeling
      *
     c                   if        spkode = 52
     c                   exsr      SP152R
     c                   goto      opdate
     c                   endif
      *
      * Lager
      *
     c                   if        spkode = 53
     c                   exsr      SP153R
     c                   goto      opdate
     c                   endif
      *
      * Produktansvarlig
      *
     c                   if        spkode = 54
     c                   exsr      SP154R
     c                   goto      opdate
     c                   endif
      *
      * Leveringstype
      *
     c                   if        spkode = 55
     c                   exsr      SP155R
     c                   goto      opdate
     c                   endif
      *
      * Leverandør
      *
     c                   if        spkode = 56
     c                   exsr      SP156R
     c                   goto      opdate
     c                   endif
      *
      * Hopper over oppdatering
      *
     c                   iter
      *
      * Oppdater hvis endring
      *
     c     opdate        tag
      *
     c                   eval      skpmlu_type = sptype
     c                   eval      skpmlu_kode = spkode
     c                   eval      skpmlu_valg = spvalg
     c     skpmlu_key    chain     skpmlu                             90
     c     *in90         ifeq      *off
     c     w_slet        ifeq      *zero
     c     w_text        ifne      *blank
     c                   movel     w_text        sprest
     c                   endif
     c                   update    skpmlur
     c                   else
     c                   delete    skpmlur
     c                   endif
     c                   endif
      *
     c                   enddo
      *
     c     end_oppdat    tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på år/periode
      *
     c                   eval      skpml1_type = *blank
     c                   eval      skpml1_kode = *zeros
     c                   eval      skpml1_valg = *blank
     c     skpml1_key    setll     skpml1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra ALTERNATIV HOVEDGRUPPE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp111r        begsr
      *
     c                   eval      vahgl1_hgrp = spahgr
     c     vahgl1_key    chain     vahgl1                             90
     c                   eval      w_text = vfhtxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra ALTERNATIV UNDERGRUPPE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp112r        begsr
      *
     c                   eval      vaugl1_hgrp = spahgr
     c                   eval      vaugl1_ugrp = spaugr
     c     vaugl1_key    chain     vaugl1                             90
     c                   eval      w_text = vfutxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra OVERGRUPPE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp114r        begsr
      *
     c                   eval      vogrl1_oogr = spogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   eval      w_text = vgotxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra HOVEDGRUPPE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp115r        begsr
      *
     c                   eval      vhgrl1_hogr = spogrp
     c                   eval      vhgrl1_hhgr = sphgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   eval      w_text = vghtxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra UNDERGRUPPE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp116r        begsr
      *
     c                   eval      vugrl1_uogr = spogrp
     c                   eval      vugrl1_uhgr = sphgrp
     c                   eval      vugrl1_uugr = spugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   eval      w_text = vgutxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra KUNDEKATEGORI-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp121r        begsr
      *
     c                   eval      ra11pf_kkod = spkkat
     c     ra11pf_key    chain     ra11pf                             90
     c                   eval      w_text = raktxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra RABATTKATEGORI-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp122r        begsr
      *
     c                   eval      ra06pf_fkod = sprkat
     c     ra06pf_key    chain     ra06pf                             90
     c                   eval      w_text = raftxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra DISTRIKT-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp123r        begsr
      *
     c                   eval      ra08pf_hkod = spdist
     c     ra08pf_key    chain     ra08pf                             90
     c                   eval      w_text = rahtxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra SELGER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp124r        begsr
      *
     c                   eval      ra09l1_ikod = spselg
     c     ra09l1_key    chain     ra09l1                             90
     c                   eval      w_text = raitxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra LAND-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp125r        begsr
      *
     c                   eval      ra12pf_lkod = spland
     c     ra12pf_key    chain     ra12pf                             90
     c                   eval      w_text = raltxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra ORDRETYPE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp151r        begsr
      *
     c                   eval      votyl1_aork = spotyp
     c     votyl1_key    chain     votyl1                             90
     c                   eval      w_text = vaotxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra AVDELING-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp152r        begsr
      *
     c                   eval      ra07pf_gkod = spavde
     c     ra07pf_key    chain     ra07pf                             90
     c                   eval      w_text = ragtxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra LAGER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp153r        begsr
      *
     c                   eval      ra10pf_jkod = splage
     c     ra10pf_key    chain     ra10pf                             90
     c                   eval      w_text = rajtxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra PRODUKTANSVARLIG-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp154r        begsr
      *
     c                   eval      ra26l1_zsak = sppran
     c     ra26l1_key    chain     ra26l1r
     c                   eval      w_text = raztxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra LEVERINGSTYPE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp155r        begsr
      *
     c                   eval      vltpl1_lety = splety
     c     vltpl1_key    chain     vltpl1                             90
     c                   eval      w_text = vybesk
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger fra LEVERANDØR-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp156r        begsr
      *
     c                   eval      rlevl1_levr = splevr
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      w_text = rlnavn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på år
      *
     c     skpmlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    skpmlu_type
     c                   kfld                    skpmlu_kode
     c                   kfld                    skpmlu_valg
      *
      * Key for oppslag på alt.hovedgruppe
      *
     c     vahgl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vahgl1_hgrp
      *
      * Key for oppslag på alt.undergruppe
      *
     c     vaugl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vaugl1_hgrp
     c                   kfld                    vaugl1_ugrp
      *
      * Key for oppslag på Overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på Hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på Undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på Kundekategori
      *
     c     ra11pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11pf_kkod
      *
      * Key for oppslag på Rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
      *
      * Key for oppslag på Distrikt
      *
     c     ra08pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra08pf_hkod
      *
      * Key for oppslag på Selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på Land
      *
     c     ra12pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra12pf_lkod
      *
      * Key for oppslag på Ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_aork
      *
      * Key for oppslag på Avdeling
      *
     c     ra07pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07pf_gkod
      *
      * Key for oppslag på Lager
      *
     c     ra10pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10pf_jkod
      *
      * Key for oppslag på Produktansvarlig
      *
     c     ra26l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra26l1_zsak
      *
      * Key for oppslag på Leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på Leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Henter firmakode fra local
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
