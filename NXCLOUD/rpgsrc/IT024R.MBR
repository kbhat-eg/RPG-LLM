     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT024R
      * Beskrivelse . . : Leser kid fil, sjekker åpne poster og skriver kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251115 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fakidl1    if   e           k disk    rename(akidpfr:akidl1r)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
      *
     fit024d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d akidl1_kidr     s                   like(akkidr)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
      *
      *  Definering av datastrukturer for KID
      *
     d d_kidl          ds
     d  d_kfir                        3  0
     d  d_kfil                        3  0
     d  d_kkun                        6  0
     d  d_kbil                        8  0
     d    d_bila                      8    overlay(d_kbil:1)
     d  d_ksjk                        1  0
     d  d_krst                        4
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_firm                        3  0
     d  d_kkid                        9
     d  d_kund                       20
     d  d_biln                       20
     d  d_lkid                       25
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_kidr          s              9  0
     d w_aarr          s              4  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_rpgp          s             10
     d w_tdat          s               d   datfmt(*iso)
      *
      *  Konstanter
      *
     d c_digits        c                   '0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og skriver til kø
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger ut parameterbilde og henter år/datoinformasjon
      *
     c                   exfmt     a1bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      slutt
     c                   endif
      *
     c     *dmy          move      a1fdat        w_tdat
     c                   eval      w_aarr = %subdt(w_tdat:*years)
      *
      *
      * Leser alle poster på akidl1
      *
     c     akidl1_key    setll     akidl1
      *
     c                   read      akidl1
     c                   dow       not %eof and
     c                             akfirm = w_firm
      *
      * Sjekker om kidfelt er numerisk - hvis ikke, droppes det.
      *
     c                   eval      d_kidl = akkidd
     c                   if        %check(c_digits : d_bila) > 0
     c                   goto      neste_kid
     c                   endif
      *
      * Sjekker om bilag finnes i fakturaregister
      *
     c                   eval      w_kidr = (akkidr * 10) + akksif
     c                   eval      sohel1_aarr = w_aarr
     c                   eval      sohel1_binr = d_kbil
6.21  *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1
     c                   dow       not %eof
     c                   if        w_kidr = sokidr and
     c                             sofkda > w_tdat
     c                   goto      skriv_kid
     c                   endif
     c     sohel1_key    reade     sohel1
     c                   enddo
     c                   goto      neste_kid
      *
      * Redigerer parm og kaller program for å skrive til kø
      *
     c     skriv_kid     tag
     c                   eval      d_firm = w_firm
     c                   eval      d_kkid = %editw(akkidr : '        ') +
     c                                      %editw(akksif : ' ')
     c     ' ':'0'       xlate     d_kkid        d_kkid
     c*                  eval      d_kund = %triml(%editw(d_kkun : '     0'))
     c*                  eval      d_biln = %triml(%editw(d_kbil : '       0'))
     c                   eval      d_kund = %triml(%editc(d_kkun : 'X'))
     c                   eval      d_biln = %triml(%editc(d_kbil : 'X'))
     c                   eval      d_lkid = akkidd
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c     neste_kid     tag
     c                   read      akidl1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for kid-register
      *
     c     akidl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    akidl1_kidr
      *
      * Key for oppslag på fakturanummer
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      akidl1_kidr = 0
     c                   eval      w_prog = 'NexstepImportKIDtran'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT024R'
      *
     c                   endsr
