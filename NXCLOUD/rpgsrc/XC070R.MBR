     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASCOMP
      * Program . . . . : Xc070R
      * Beskrivelse . . : Compello Integrasjon, justering av feiltraner
      *
      * spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211211 sst  Nytt program
6.30  * 6.30  240117 sst  Endret register rcoml2 til RCOML2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.30 frcoml2    if   e           k disk    rename(rcompfr:rcoml2r)
     frcomlr    if   e           k disk    rename(rcompfr:rcomlrr)
      *
     fxc070d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_asp                 911    913
     d l_fgrp                931    933
     d l_firm                944    946
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
6.30 d rcoml2_firm     s                   like(rcfirm)
6.30 d rcoml2_biln     s                   like(rcbiln)
     d rcomlr_huid     s                   like(rchuid)
      *
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_fgrp          s              3
     d w_firm          s              3
     d s_fgrp          s              3
     d w_f10           s             10
      *
      * Fetler fra XC100r vedr kontroll
      *
     d i               s              3  0
     d j               s              3  0
     d w_b             s             11
     d wdat            s              6  0
     d waar            s              2  0
     d w_dat5          s              5
     d w_dat6          s              6
     d w2x             s              2
     d w4x             s              4
     d w6x             s              6
     d w_dmy           s              6
     d w_dmyn          s              6  0
     d w_kr            s             13
     d w_or            s              2
     d w_krn           s             13  0
     d w_orn           s              2  0
     d w_bilk          s              2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     d w_aaaa          s              4  0
     d w_aar           s              4
     d w_dag           s              2
     d w_mnd           s              2
     d w_raar          s              4  0
     d w_raarx         s              4
     d w_rper          s              2  0
     d w_rperx         s              2
     d wuaa            s              4  0
     d wuaax           s              4
     d wtst            s              3  0
     d w_feil          s             30
     d w_tell          s              6  0
     d w_sts           s              1
     d w_last_biln     s                   like(rcbiln)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(15)
      *
      * Forklaring på firmielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c*                  if        b2fgrp <> *blank or
     c                   if        b2firm <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'K'
6.30 c                   eval      rcoml2_firm = b1firm
6.30 c                   eval      rcoml2_biln = b1biln
6.30 c     rcoml2_key    setll     rcoml2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på filgruppe
      *
     c                   if        b2firm <> *blank
     c                   eval      w_seqe = 'K'
6.30 c                   eval      rcoml2_firm = b2firm
6.30 c                   eval      rcoml2_biln = b2biln
6.30 c     rcoml2_key    setll     rcoml2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2firm = *blank
     c                   eval      b2biln = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = '5'
     c                   eval      c2firm = b1firm
     c                   eval      c2biln = b1biln
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c*                  eval      rcomlr_fgrp = b1fgrp
     c                   eval      rcomlr_huid = b1huid
     c     rcomlr_key    chain     rcomlr
      *
     c                   if        %found
     c*    '-':'0'       xlate     rcbelo        rcbelo
     c*    '-':'0'       xlate     rcvalb        rcvalb
     c                   eval      c2firm = rcfirm
     c                   eval      c2biln = rcbiln
     c                   eval      c2bilk = rcbilk
     c                   eval      c2rper = rcrper
     c                   eval      c2bdat = rcbdat
     c                   eval      c2belo = rcbelo
     c                   if        rcbiln = '100014'
     c                   eval      rcbiln = rcbiln
     c                   endif
     c                   eval      c2davd = rcdavd
     c                   eval      c2dkto = rcdkto
     c                   eval      c2dsps = rcdsps
     c                   eval      c2dmva = rcdmva
     c                   eval      c2cavd = rccavd
     c                   eval      c2ckto = rcckto
T5.30c                   eval      c2csps = rccsps
T5.30c                   eval      c2cmva = rccmva
T5.60c                   eval      c2fdat = rcfdat
T5.60c                   eval      c2valb = rcvalb
T5.60c                   eval      c2valk = rcvalk
T5.60c                   eval      c2text = rctext
T5.60c                   eval      c2lfak = rclfak
     c                   eval      c2kidi = rckidi
     c                   eval      c2pros = rcpros
     c                   eval      c2akti = rcakti
     c                   eval      c2huid = rchuid
     c                   eval      c2edat = rcedat
     c                   eval      c2etim = rcetim
     c                   eval      c2feil = rcfeil
     c                   eval      rcfeil = *blank
     c                   endif
      *
      *
     c                   if        b1valg = '5'
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
T5.20c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      *
      * Oppdater subfile
      *
     c     c2_sub        tag
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'K'
6.30 c                   read      rcoml2                                 15
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *   Dersom ikke ASP-bruker vises bare egen filgruppe
     c                   if        rcfirm <> w_firm and
     c                             l_asp  <> 'ASP'
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   endsl
      *
     c                   eval      b1valg = *blank
     c                   eval      b1firm = rcfirm
     c                   eval      b1biln = rcbiln
     c                   eval      b1huid = rchuid
     c*                  eval      b1raar = rcraar
     c                   eval      b1rper = rcrper
     c                   eval      b1bdat = rcbdat
     c                   eval      b1dkto = rcdkto
     c                   eval      b1kkto = rcckto
     c                   eval      b1belo = rcbelo
     c                   if        %subst(b1belo:1:1)  = '0'    and
     c                             b1dkto              = *blank and
     c                             b1kkto             <> *blank
     c                   eval      %subst(b1belo:1:1)  = '-'
     c                   endif
     c                   if        %subst(b1belo:1:1) <> '-'
     c                   eval      %subst(b1belo:2:14) = %subst(b1belo:1:14)
     c                   eval      %subst(b1belo:1:1)  = ' '
     c                   endif
     c                   if        rcbiln <> w_last_biln
     c                   if        *in41 = *on
     c                   eval      *in41 = *off
     c                   else
     c                   eval      *in41 = *on
     c                   endif
     c                   endif
     c                   eval      w_last_biln = rcbiln
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'K'
6.30 c     rcoml2_key    setgt     rcoml2
6.30 c                   readp     rcoml2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'K'
6.30 c                   readp     rcoml2
     c                   endsl
      *
     c                   if        %eof             or
     c                             rcfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'K'
6.30 c     rcoml2_key    setll     rcoml2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les/posisjonering på bilag
      *
6.30 c     rcoml2_key    klist
6.30 c                   kfld                    rcoml2_firm
6.30 c                   kfld                    rcoml2_biln
      *
      * Key for les på fgrpo
      *
     c     rcomlr_key    klist
     c                   kfld                    rcomlr_huid
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   move      uyear         wuaa
     c                   eval      wuaa = wuaa + 2000
      *
     c                   eval      *in22 = *on
     c                   eval      *in31 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'K'
     c                   if        l_asp = 'ASP'
     c                   eval      *in31 = *off
     c                   endif
6.30 c                   eval      rcoml2_firm = *blank
6.30 c                   eval      rcoml2_biln = *blank
6.30 c     rcoml2_key    setll     rcoml2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
