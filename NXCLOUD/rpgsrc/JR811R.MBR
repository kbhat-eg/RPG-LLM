     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR829R
      * Beskrivelse . . : Leser fil m/format TJÆRALIN
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   leverandørens varenummer.
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050510 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvarl4    if   e           k disk    rename(jvarpfr:jvarl4r)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - Ahlsell
      *
     d                 ds
     d d_inpu                       135
     d  d_lvnr                       14    overlay(d_inpu:1)
     d  d_vtxt                       52    overlay(d_inpu:16)
     d  d_enhe                        3    overlay(d_inpu:69)
     d  d_rabk                        4    overlay(d_inpu:73)
     d  d_forp                        9    overlay(d_inpu:78)
     d   d_forp_kr                    6  0 overlay(d_forp:1)                    Salgspris
     d   d_forp_Øre                   2  2 overlay(d_forp:8)                    Salgspris
     d  d_raba                        4    overlay(d_inpu:88)
     d  d_netp                        9    overlay(d_inpu:93)
     d   d_netp_kr                    6  0 overlay(d_netp:1)                    Salgspris
     d   d_netp_Øre                   2  2 overlay(d_netp:8)                    Salgspris
     d  d_pasl                        4    overlay(d_inpu:103)
     d  d_saum                        9    overlay(d_inpu:108)
     d   d_saum_kr                    6  0 overlay(d_saum:1)                    Salgspris
     d   d_saum_Øre                   2  2 overlay(d_saum:8)                    Salgspris
     d  d_eann                       13    overlay(d_inpu:117)
     d  d_eannn                      13  0 overlay(d_eann:1)
      *
      * Definering av variable i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvarld_ean1     s                   like(jvean1)
     d jvarl4_lvar     s                   like(jvlvar)
     d jvarlu_vare     s                   like(jvvare)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_ok            s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_ugrx          s              3
     d w_prix          s              9
     d w_prihx         s              6
     d w_pridx         s              2
     d w_prihn         s              6  0
     d w_pridn         s              2  2
     d w_inpr          s              9  2
     d w_leng          s              2  0
     d w_posi          s              2  0
      *
     d w_eann          s                   like(jvean1)
     d w_eanx          s             13
     d w_stat          s                   like(jvstat)
      *
      * Definering av konstanter
      *
     d c_digi          s             15    inz('0123456789 ')
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Leser fil.
      *
     c                   read      jrvapf
     c                   dow       not %eof
     c                   exsr      behandling
     c                   read      jrvapf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
     c                   if        d_lvnr = *blank or b_ok = *off
     c                   goto      end_oppdat
     c                   endif
      *
      * Starter oppdatering
      *
      * finner varen på grunnlag av eann
     c                   if        d_eannn <> 0
     c                   eval      jvarld_ean1 = d_eannn
     c     jvarld_key    chain     jvarld
     c                   if        %found(jvarld)
     c                   exsr      oppd_pris
     c                   else
     c                   eval      jvarl4_lvar = d_lvnr
     c     jvarl4_key    chain     jvarl4
     c                   if        %found(jvarl4)
     c                   exsr      oppd_pris
     c                   endif
     c                   endif
     c                   endif
      *
     c     end_oppdat    endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
      * Sletter priser på leverandør
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
     c                   clear                   jvprlur
      *
     c                   evalr     w_prix = %trim(d_netp)
     c                   eval      w_prix = %xlate(' ':'0':w_prix)
      *
     c                   if        %subst(w_prix:7:1) <> ',' or
     c                             %check(c_digi : %subst(w_prix:1:6)) > 0 or
     c                             %check(c_digi : %subst(w_prix:8:2)) > 0
     c                   eval      jxpris = *zero
     c                   else
     c                   eval      w_prihx = %subst(w_prix:1:6)
     c                   move      w_prihx       w_prihn
     c                   eval      w_pridx = %subst(w_prix:8:2)
     c                   move      w_pridx       w_pridn
     c                   eval(h)   w_inpr = w_prihn + w_pridn
     c                   eval      jxpris = w_inpr
     c                   endif
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
     c                   eval      jxlvar = jvlvar
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
      *
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     jrvrad        jrvrad
     c                   eval      b_ok = *off
      *
      * Lev. varenr
      *
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_lvnr = %trim(%subst(jrvrad:1:w_posi-1))
      *
     c                   if        %check(c_digi : d_lvnr) > *zero  or
     c                             d_lvnr = *blank
     c                   leavesr
     c                   endif
      *
      * Varetekst
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_vtxt = %trim(%subst(jrvrad:1:w_posi-1))
      *
      * Enhet
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_enhe = %trim(%subst(jrvrad:1:w_posi-1))
      *
      *  nettopris
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_netp = %trim(%subst(jrvrad:1:w_posi-1))
      *
      * EAN
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   if        w_posi = 0
     c                   eval      d_eann = %trim(%subst(jrvrad:1:14))
     c                   else
     c                   eval      d_eann = %trim(%subst(jrvrad:1:w_posi-1))
     c                   endif
      *
      * Utleder  nettopris
      *
     c*                  if        d_netp <> *blank
      *
     c*                  eval      d_netp = %trim(d_netp)
     c*                  eval      w_posi = %scan('.':d_netp)
     c*                  eval      w_leng = %len(%trim(d_netp))
      *
     c*                  if        w_posi = 0
     c*                  eval      w_posi = w_leng + 1
     c*                  eval      d_netp = %trim(d_netp) + '.00'
     c*                  endif
      *
     c*    ' ':'0'       xlate     d_netp        d_netp
      *
     c*                  if        (w_posi-1) < 6
     c*                  eval      d_netp = %subst(c_null:1:6-w_posi+1) +
     c*                                     %subst(d_netp:1:w_posi-1) +
     c*                                     '.' +
     c*                                     %subst(d_netp:w_posi+1:2)
     c*                  endif
      *
     c*                  else
     c*                  eval      d_netp = '0000000.00'
     c*                  endif
      *
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare m/ean-nr
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      * Key for oppslag på vare m/lar
      *
     c     jvarl4_key    klist
     c                   kfld                    jvarl4_lvar
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
