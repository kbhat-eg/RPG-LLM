     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK601R                                        *
      * Beskrivelse . . : Utskrift av dubletter i kunderegisteret       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      * V5.40     120705  Nytt program                             KOB  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     f                                     prefix(x:1)
     frk601p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av Local data area
      *
     d                uds
T5.00d l_navn                300    300
T5.00d l_gate                301    301
T5.00d l_ponr                302    302
T5.00d l_tlfn                303    303
T5.00d l_mobn                304    304
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d ant             s              2  0
      *
     d w_ikke          s              1
      *
     d w_anta          s              5  0
     d w_firm          s                   like(l_firm)
     d w_kund          s                   like(rkkund)
     d w_alfa          s                   like(rkalfa)
     d w_navn          s                   like(rknavn)
     d w_gate          s                   like(rkgate)
     d w_ponr          s                   like(rkponr)
     d w_tlfn          s                   like(rktlfn)
     d w_mobn          s                   like(rkmobn)
      *
      * Les kunderegister
      *
     c     rkunl2_key    setll     rkunl2                                 90                 .
     c     rkunl2_key    reade     rkunl2                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rkalfa = w_alfa
     c                   exsr      xsrkont
      *
     c                   if        w_ikke = ' '
     c                   eval      ant = ant + 1
     c                   else
     c                   eval      ant = 1
     c                   endif
      *
     c                   else
      *
     c                   eval      ant = 1
     c                   endif
      *
     c                   select
     c                   when      ant = 1
     c                   eval      w_kund = rkkund
     c                   eval      w_navn = rknavn
     c                   eval      w_gate = rkgate
     c                   eval      w_ponr = rkponr
     c                   eval      w_tlfn = rktlfn
     c                   eval      w_mobn = rkmobn
      *
     c                   when      ant = 2
     c                   eval      d1kund = w_kund
     c                   exsr      utskrift
     c                   eval      d1kund = rkkund
     c                   exsr      utskrift
      *
     c                   when      ant > 2
     c                   eval      d1kund = rkkund
     c                   exsr      utskrift
     c                   endsl
      *
     c                   eval      w_alfa = rkalfa
      *
     c     rkunl2_key    reade     rkunl2                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   eval      rkunl1_kund = d1kund
     c     rkunl1_key    chain     rkunl1                             90
      *
     c                   eval      d1navn = xknavn
     c                   eval      d1gate = xkgate
     c                   eval      d1sted = xksted
     c                   eval      d1tlfn = xktlfn
     c                   eval      d1mobn = xkmobn
      *
     c                   write     d1det
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsrkont       begsr
      *
     c                   eval      w_ikke = ' '
      *
      * Sjekk navn
      *
     c                   if        l_navn = 'J' and
     c                             rknavn <> w_navn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk gateadresse
      *
     c                   if        l_gate = 'J' and
     c                             rkgate <> w_gate
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk postnummer
      *
     c                   if        l_ponr = 'J' and
     c                             rkponr <> w_ponr
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk telefonnummer
      *
     c                   if        l_tlfn = 'J' and
     c                             rktlfn <> w_tlfn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk mobilnummer
      *
     c                   if        l_mobn = 'J' and
     c                             rkmobn <> w_mobn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Key for kunderegister
      *
     c     rkunl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      w_alfa = ' '
     c                   eval      w_kund = 0
     c                   eval      ant = 0
      *
      * Skriv heading på første side
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
