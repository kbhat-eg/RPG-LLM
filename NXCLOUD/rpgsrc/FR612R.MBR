     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FR612R
      * Beskrivelse . . : Rabattmatrise, kundebrev
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130599 HKO  Nytt program
      *       200300 HKO  Lagt inn leverandør som nivå i rabattmatrisen
      *       180204 HKO  Erstattet rabattgruppe med varegruppe og modul
      *       110304 HKO  Dersom rabatt på varenivå og det ikke gis rabatt
      *                   på varens priskode vises ikke rabattlinjen.
6.31  *       300315 bof  Utvidet med prisgruppe i nøkkel
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffprwl1    if   e           k disk    rename(fprwpfr:fprwl1r)
     ffprwl2    if   e           k disk    rename(fprwpfr:fprwl2r)
     ffrawl1    if   e           k disk    rename(frawpfr:frawl1r)
     ffrawl2    if   e           k disk    rename(frawpfr:frawl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
      *
     ffr612p    o    e             printer
      *
      * Local data area (LDA)
      *
     d                uds
     d l_ruti                800    809
      *
      * Definering av parametre
      *
     d p_firm          s                   like(frfirm)
     d p_kund          s                   like(frkund)
     d p_kpro          s                   like(frkpro)
     d p_rdat          s               d   datfmt(*iso)
     d p_txt1          s             60
     d p_txt2          s             60
     d p_txt3          s             60
     d p_valg          s              1  0
     d p_prgr          s                   like(frprgr)
      *
      * Definering av variable i nøkler
      *
6.31 d fprwl1_prgr     s                   like(fpprgr)
     d fprwl1_rkat     s                   like(fprkat)
     d fprwl1_kkat     s                   like(fpkkat)
     d fprwl1_kund     s                   like(fpkund)
     d fprwl1_kpro     s                   like(fpkpro)
     d fprwl1_vare     s                   like(fpvare)
     d fprwl1_enhe     s                   like(fpenhe)
     d fprwl1_lety     s                   like(fplety)
     d frawl1_prgr     s                   like(frprgr)
     d frawl1_rkat     s                   like(frrkat)
     d frawl1_kkat     s                   like(frkkat)
     d frawl1_kund     s                   like(frkund)
     d frawl1_kpro     s                   like(frkpro)
     d frawl1_ogrp     s                   like(frogrp)
     d frawl1_hgrp     s                   like(frhgrp)
     d frawl1_ugrp     s                   like(frugrp)
     d frawl1_ldor     s                   like(frldor)
     d frawl1_modn     s                   like(frmodn)
     d frawl1_vare     s                   like(frvare)
     d frawl1_enhe     s                   like(frenhe)
     d frawl1_lety     s                   like(frlety)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vpkol1_pkod     s                   like(vapkod)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
     d rlevl1_levr     s                   like(rllevr)
     d aforl1_ruti     s                   like(afruti)
     d apospf_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
      *
     d w_firm          s                   like(frfirm)
     d w_prgr          s                   like(frprgr)
     d w_rkat          s                   like(frrkat)
     d w_kkat          s                   like(frkkat)
     d w_kund          s                   like(frkund)
     d w_kpro          s                   like(frkpro)
     d w_opda          s                   like(flopda)
     d w_bupr          s                   like(fpsapr)
     d w_kopr          s                   like(fpkopr)
     d w_inpr          s                   like(fpkopr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_rkat = 0
     c                   eval      w_kkat = 0
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
     c                   eval      w_prgr = p_prgr
      *
     c                   time                    w_opda
      *
      * Finner rabattkategori og kundekategori for kunde.
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      w_rkat = rkrabk
     c                   eval      w_kkat = rkkatg
     c                   endif
      *
      * Finner eventuelle overstyringer på kundeprosjekt
      *
     c                   if        p_kpro <> 0
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
     c                   if        florka <> 0
     c                   eval      w_rkat = florka
     c                   endif
     c                   if        flokun <> 0
     c                   eval      w_kund = flokun
     c                   endif
     c                   if        flokpr <> 0
     c                   eval      w_kpro = flokpr
     c                   endif
     c                   if        flokun <> 0
     c                   eval      rkunl1_kund = flokun
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      w_rkat = rkrabk
     c                   eval      w_kkat = rkkatg
     c                   endif
     c                   endif
     c                   if        flopda <> *loval
     c                   eval      w_opda = flopda
     c                   endif
     c                   endif
     c                   endif
      *
      * Avbryter dersom kunde ikke ble funnet
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
      * Avbryter dersom det ikke finnes spesialpriser eller rabatter for
      * rabattkategori, kundekategori, kunde eller kundeprosjekt.
      *
6.31 c                   eval      fprwl1_prgr = w_prgr
     c                   eval      fprwl1_rkat = w_rkat
     c                   eval      fprwl1_kkat = w_kkat
     c                   eval      fprwl1_kund = w_kund
     c                   eval      fprwl1_kpro = w_kpro
     c     fprwl1_key2   chain     fprwl1                             90
     c                   if        *in90 = *on
     c                   eval      frawl1_prgr = w_prgr
     c                   eval      frawl1_rkat = w_rkat
     c                   eval      frawl1_kkat = w_kkat
     c                   eval      frawl1_kund = w_kund
     c                   eval      frawl1_kpro = w_kpro
     c     frawl1_key2   chain     frawl1                             90
     c                   endif
      *
     c                   if        *in90 = *on and
     c                             w_kpro <> 0 and
     c                             p_valg = 0
     c                   goto      avslutt
     c                   endif
      *
6.31 c                   if        *in90 = *on
6.31 c                   eval      fprwl1_prgr = *blank
6.31 c     fprwl1_key2   chain     fprwl1                             90
     c                   if        *in90 = *on
     c                   eval      fprwl1_kpro = 0
     c     fprwl1_key2   chain     fprwl1                             90
     c                   if        *in90 = *on
     c                   eval      fprwl1_kund = 0
     c     fprwl1_key2   chain     fprwl1                             90
     c                   if        *in90 = *on
     c                   eval      fprwl1_kkat = 0
     c     fprwl1_key2   chain     fprwl1                             90
     c                   if        *in90 = *on
     c                   eval      frawl1_kpro = 0
     c     frawl1_key2   chain     frawl1                             90
     c                   if        *in90 = *on
     c                   eval      frawl1_kund = 0
     c     frawl1_key2   chain     frawl1                             90
     c                   if        *in90 = *on
     c                   eval      frawl1_kkat = 0
     c     frawl1_key2   chain     frawl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
6.31 c                   endif
      *
      * Skriver brev-hode
      *
     c                   exsr      hode
      *
      * Utplukk til detalj-linjer
      *
     c                   exsr      utplukk
      *
      * Skriver bunnlinje
      *
     c                   exsr      bunnlinje
      *
      * Avslutter prorgram
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
     c                   eval      a1date = udate
      *
      * Kunde
      *
     c                   eval      a1kund = p_kund
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      a1knav = rknavn
     c                   eval      a1gate = rkgate
     c                   eval      a1kpos = rksted
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   eval      a1kpro = p_kpro
     c                   eval      a1pnav = *blank
     c                   eval      a1adr1 = *blank
     c                   eval      a1lpon = 0
     c                   eval      a1lpos = *blank
      *
     c                   if        p_kpro <> 0
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
     c                   eval      a1pnav = flnavn
     c                   eval      a1adr1 = fladr1
     c                   eval      a1lpon = flponr
     c                   if        flponr <> 0
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf                             90
     c                   if        *in90 = *off
     c                   eval      a1lpos = apsted
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * Henter beskrivelse av priskode 'N' og 'T'
      *
     c                   eval      vpkol1_pkod = 'N'
     c     vpkol1_key    chain     vpkol1                             90
     c                   if        *in90 = *off
     c                   eval      a3nbes = vaptxt
     c                   endif
      *
     c                   eval      vpkol1_pkod = 'T'
     c     vpkol1_key    chain     vpkol1                             90
     c                   if        *in90 = *off
     c                   eval      a3tbes = vaptxt
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utplukk til detaljlinje for spesialpriser og rabatter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utplukk       begsr
      *
      * Leser spesialpriser
      * Det sjekkes om det finnes overstyringer på et høyere nivå før
      * pris skrives ut
      *
     c     fprwl2_key    setll     fprwl2
     c     fprwl2_key    reade     fprwl2                                 90
     c                   dow       *in90 = *off
      *
     c                   if        fprkat <> 0 and
     c                             fprkat <> w_rkat
     c                   goto      neste_pris
     c                   endif
      *
     c                   if        fpkkat <> 0 and
     c                             fpkkat <> w_kkat
     c                   goto      neste_pris
     c                   endif
      *
     c                   if        fpkund <> 0 and
     c                             fpkund <> w_kund
     c                   goto      neste_pris
     c                   endif
      *
     c                   if        w_kpro = 0 and
     c                             fpkpro <> 0
     c                   goto      neste_pris
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             fpkpro <> 0 and
     c                             fpkpro <> w_kpro
     c                   goto      neste_pris
     c                   endif
      *
     c                   if        fpfdat <> *loval
     c                   if        p_rdat < fpfdat or
     c                             p_rdat > fptdat
     c                   goto      neste_pris
     c                   endif
     c                   endif
      *
     c                   if        fpkund = 0
6.31 c                   eval      fprwl1_prgr = fpprgr
     c                   eval      fprwl1_rkat = w_rkat
     c                   eval      fprwl1_kkat = w_kkat
     c                   eval      fprwl1_kund = w_kund
     c                   eval      fprwl1_kpro = 0
     c                   eval      fprwl1_vare = fpvare
     c                   eval      fprwl1_enhe = fpenhe
     c                   eval      fprwl1_lety = fplety
     c     fprwl1_key    chain     fprwl1                             91
     c                   if        *in91 = *off
     c                   goto      neste_pris
     c                   endif
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             fpkpro = 0
6.31 c                   eval      fprwl1_prgr = fpprgr
     c                   eval      fprwl1_rkat = w_rkat
     c                   eval      fprwl1_kkat = w_kkat
     c                   eval      fprwl1_kund = w_kund
     c                   eval      fprwl1_kpro = w_kpro
     c                   eval      fprwl1_vare = fpvare
     c                   eval      fprwl1_enhe = fpenhe
     c                   eval      fprwl1_lety = fplety
     c     fprwl1_key    chain     fprwl1                             91
     c                   if        *in91 = *off
     c                   goto      neste_pris
     c                   endif
     c                   endif
      *
     c                   exsr      detalj_pris
     c                   eval      b_pris = *on
      *
     c     neste_pris    tag
      *
     c     fprwl2_key    reade     fprwl2                                 90
     c                   enddo
      *
      * Skriver blank linje før rabatter skrives
      *
     c                   if        b_pris = *on
     c                   write     d1spc                                30
     c                   exsr      overflow
     c                   endif
      *
      * Leser rabattmatrise
      * Det sjekkes om det finnes overstyringer på et høyere nivå før
      * rabatt skrives ut
      *
     c     frawl2_key    setll     frawl2
     c     frawl2_key    reade     frawl2                                 90
     c                   dow       *in90 = *off
      *
     c                   if        frrkat <> 0 and
     c                             frrkat <> w_rkat
     c                   goto      neste_rabatt
     c                   endif
      *
     c                   if        frkkat <> 0 and
     c                             frkkat <> w_kkat
     c                   goto      neste_rabatt
     c                   endif
      *
     c                   if        frkund <> 0 and
     c                             frkund <> w_kund
     c                   goto      neste_rabatt
     c                   endif
      *
     c                   if        w_kpro = 0 and
     c                             frkpro <> 0
     c                   goto      neste_rabatt
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             frkpro <> 0 and
     c                             frkpro <> w_kpro
     c                   goto      neste_rabatt
     c                   endif
      *
     c                   if        frkund = 0
     c                   eval      frawl1_prgr = w_prgr
     c                   eval      frawl1_rkat = w_rkat
     c                   eval      frawl1_kkat = w_kkat
     c                   eval      frawl1_kund = w_kund
     c                   eval      frawl1_kpro = 0
     c                   eval      frawl1_ogrp = frogrp
     c                   eval      frawl1_hgrp = frhgrp
     c                   eval      frawl1_ugrp = frugrp
     c                   eval      frawl1_ldor = frldor
     c                   eval      frawl1_modn = frmodn
     c                   eval      frawl1_vare = frvare
     c                   eval      frawl1_enhe = frenhe
     c                   eval      frawl1_lety = frlety
     c     frawl1_key    chain     frawl1                             91
     c                   if        *in91 = *off
     c                   goto      neste_rabatt
     c                   endif
     c                   endif
      *
     c                   if        w_kpro <> 0 and
     c                             frkpro = 0
     c                   eval      frawl1_prgr = w_prgr
     c                   eval      frawl1_rkat = w_rkat
     c                   eval      frawl1_kkat = w_kkat
     c                   eval      frawl1_kund = w_kund
     c                   eval      frawl1_kpro = w_kpro
     c                   eval      frawl1_ogrp = frogrp
     c                   eval      frawl1_hgrp = frhgrp
     c                   eval      frawl1_ugrp = frugrp
     c                   eval      frawl1_ldor = frldor
     c                   eval      frawl1_modn = frmodn
     c                   eval      frawl1_vare = frvare
     c                   eval      frawl1_enhe = frenhe
     c                   eval      frawl1_lety = frlety
     c     frawl1_key    chain     frawl1                             91
     c                   if        *in91 = *off
     c                   goto      neste_rabatt
     c                   endif
     c                   endif
      *
     c                   exsr      detalj_rabatt
      *
     c     neste_rabatt  tag
      *
     c     frawl2_key    reade     frawl2                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av detaljlinje - spesialpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj_pris   begsr
      *
     c                   eval      d1vare = fpvare
     c                   eval      d1text = fptek1
     c                   eval      d1lety = fplety
     c                   eval      d1enhe = fpenhe
     c                   eval      d1sapr = fpsapr
      *
      * Beregner salgspris
      *
      * Dersom salgspris ikke er utfylt, kalles program som henter priser
      * på vare
      *
     c                   if        d1sapr = 0
     c                   eval      b_inlr = *off
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    fpvare
5.70 c                   parm                    fpprgr
     c                   parm                    fpenhe
     c                   parm                    fplety
     c                   parm                    w_opda
     c                   parm                    b_inlr
     c                   parm                    d1sapr
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
     c                   endif
      *
      * Finner salgspris etter rabatt
      *
     c                   if        fprab1 <> 0
     c                   eval(h)   d1sapr = d1sapr - (d1sapr * fprab1 / 100)
     c                   endif
      *
     c                   if        fprab2 <> 0
     c                   eval(h)   d1sapr = d1sapr - (d1sapr * fprab2 / 100)
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av detaljlinje - rabattmatrise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj_rabatt begsr
      *
     c                   if        frdekn <> 0 or
     c                             frdekd <> 0
     c                   leavesr
     c                   endif
      *
     c                   eval      d1vgrp = *blank
     c                   eval      d1sapr = 0
      *
     c                   eval      d1ldor = frldor
     c                   eval      d1vare = frvare
     c                   eval      d1text = frtek1
     c                   eval      d1enhe = frenhe
     c                   eval      d1lety = frlety
      *
     c                   if        frogrp <> 0
     c                   move      frogrp        w_ogrp_alf
     c                   move      frhgrp        w_hgrp_alf
     c                   move      frugrp        w_ugrp_alf
     c                   select
     c                   when      frugrp <> 0
     c                   eval      d1vgrp = w_ogrp_alf + w_hgrp_alf +
     c                                      w_ugrp_alf
     c                   when      frhgrp <> 0
     c                   eval      d1vgrp = w_ogrp_alf + w_hgrp_alf
     c                   other
     c                   eval      d1vgrp = w_ogrp_alf
     c                   endsl
     c                   endif
      *
      * Henter tekst-linje
      *
     c                   if        frvare = *blank and
     c                             frogrp <> 0
      *
     c                   select
     c                   when      frugrp <> 0
     c                   eval      vugrl1_uogr = frogrp
     c                   eval      vugrl1_uhgr = frhgrp
     c                   eval      vugrl1_uugr = frugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      d1text = vgutxt
     c                   endif
     c                   when      frhgrp <> 0
     c                   eval      vhgrl1_hogr = frogrp
     c                   eval      vhgrl1_hhgr = frhgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      d1text = vghtxt
     c                   endif
     c                   other
     c                   eval      vogrl1_oogr = frogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      d1text = vgotxt
     c                   endif
     c                   endsl
      *
     c                   endif
      *
     c                   if        frvare = *blank and
     c                             frldor <> 0
     c                   eval      rlevl1_levr = frldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   if        frogrp <> 0
     c                   eval      d1text = %trim(d1text) + ' - ' + rlnavn
     c                   else
     c                   eval      d1text = rlnavn
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        frvare = *blank and
     c                             frmodn <> 0
     c                   movel     frmodn        d1vare
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    frmodn
     c                   parm                    d1text
     c                   endif
      *
     c                   if        frvare = *blank and
     c                             frogrp = 0 and
     c                             frldor = 0 and
     c                             frmodn = 0
     c                   eval      d1text = 'GENERELL RABATT ALLE VARER'
     c                   endif
      *
      * Finner rabatter i.h.t. dato
      *
     c                   if        frra1d <> 0 and
     c                             p_rdat >= frra1f and
     c                             p_rdat <= frra1t
     c                   eval      d1rab1 = frra1d
     c                   else
     c                   eval      d1rab1 = frrab1
     c                   endif
      *
     c                   if        frra2d <> 0 and
     c                             p_rdat >= frra2f and
     c                             p_rdat <= frra2t
     c                   eval      d1rab2 = frra2d
     c                   else
     c                   eval      d1rab2 = frrab2
     c                   endif
      *
     c                   if        frrand <> 0 and
     c                             p_rdat >= frranf and
     c                             p_rdat <= frrant
     c                   eval      d1rabn = frrand
     c                   else
     c                   eval      d1rabn = frrabn
     c                   endif
      *
     c                   if        frratd <> 0 and
     c                             p_rdat >= frratf and
     c                             p_rdat <= frratt
     c                   eval      d1rabt = frratd
     c                   else
     c                   eval      d1rabt = frrabt
     c                   endif
      *
      * Test på vare og priskode
      *
     c                   if        frvare <> *blank and
     c                             d1rab1 <> 0 and
     c                             d1rabt = 0
     c                   eval      vvarl1_vare = frvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   if        vvkpri = 'N' and
     c                             d1rabn = 0
     c***                          vvkpri = 'B' and
     c***                          d1rabb = 0
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av bunnlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bunnlinje     begsr
      *
     c                   eval      b1txt1 = p_txt1
     c                   eval      b1txt2 = p_txt2
     c                   eval      b1txt3 = p_txt3
     c                   eval      b1mld1 = afmld1
     c                   eval      b1mld2 = afmld2
     c                   eval      b1mld3 = afmld3
      *
     c                   write     b1bot                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Paramtre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_rdat
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_valg
     c                   parm                    p_prgr
      *
      * Key for oppslag og les på spesialpris arbeidsfil
      *
     c     fprwl1_key    klist
     c                   kfld                    w_firm
6.31 c                   kfld                    fprwl1_prgr
     c                   kfld                    fprwl1_rkat
     c                   kfld                    fprwl1_kkat
     c                   kfld                    fprwl1_kund
     c                   kfld                    fprwl1_kpro
     c                   kfld                    fprwl1_vare
     c                   kfld                    fprwl1_enhe
     c                   kfld                    fprwl1_lety
      *
     c     fprwl1_key2   klist
     c                   kfld                    w_firm
6.31 c                   kfld                    fprwl1_prgr
     c                   kfld                    fprwl1_rkat
     c                   kfld                    fprwl1_kkat
     c                   kfld                    fprwl1_kund
     c                   kfld                    fprwl1_kpro
      *
     c     fprwl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på rabattmatrise arbeidsfil
      *
     c     frawl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frawl1_prgr
     c                   kfld                    frawl1_rkat
     c                   kfld                    frawl1_kkat
     c                   kfld                    frawl1_kund
     c                   kfld                    frawl1_kpro
     c                   kfld                    frawl1_ogrp
     c                   kfld                    frawl1_hgrp
     c                   kfld                    frawl1_ugrp
     c                   kfld                    frawl1_ldor
     c                   kfld                    frawl1_modn
     c                   kfld                    frawl1_vare
     c                   kfld                    frawl1_enhe
     c                   kfld                    frawl1_lety
      *
     c     frawl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    frawl1_prgr
     c                   kfld                    frawl1_rkat
     c                   kfld                    frawl1_kkat
     c                   kfld                    frawl1_kund
     c                   kfld                    frawl1_kpro
      *
     c     frawl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppslag på priskode
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter informasjon fra rutine-registeret
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1                             90
      *
     c                   endsr
