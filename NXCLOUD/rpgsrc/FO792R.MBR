      /TITLE  Kontroll av ordre på kunde
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOFAK                                              *
      * Program . . . . : FO792R
      * Beskrivelse . . : Kontroll av ordre på kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  -------------------------------------------------
T3.30 * V3.30      020702 Mangler RETURN, pgm. gikk i loop.        HFL
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KA = F1  = Valg av firma
      *       KC = F3  = Exit
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelk    if   e           k disk    rename(fohepfr:fohelkr)
      *
      * Definering av variable i nøkler
      *
     d fohelk_kund     s                   like(fokund)
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d w_firm          s                   like(fofirm)
     d p_firm          s              3  0
     d p_kund          s                   like(fokund)
     d p_stat          s              1
      *
      * Sjekk om det finnes ordre på kunde
      *
     c                   if        p_stat = ' '
     c                   eval      fohelk_kund = p_kund
     c     fohelk_key    chain     fohelk                             91
     c                   if        *in91 = *off
     c                   eval      p_stat = 'X'
     c                   endif
     c                   endif
      *
     c     slutt         tag
      *
     c                   if        p_stat = 'S'
     c                   eval      *inlr = *on
     c                   endif
T3.30c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_stat
      *
      * Key for oppslag på ordehode
      *
     c     fohelk_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelk_kund
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
      *
