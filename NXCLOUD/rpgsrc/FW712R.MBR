      /title  ASOFAK Lev.vare, henter vareregister - Ole Moe Engros
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW712R
      * Beskrivelse . . : Danning av leverandør vareregister, Ole Moe Engros
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250200 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Ole Moe Engros
      *
     d                 ds
     d d_inpu                       173
     d  d_vare                       13    overlay(d_inpu:1)                    Varenr
     d  d_vgrp                        4    overlay(d_inpu:14)                   Varegruppe
     d  d_tek1                       30    overlay(d_inpu:19)                   Varetekst-1
     d  d_enhe                        3    overlay(d_inpu:50)                   Enhet
     d  d_tek2                       30    overlay(d_inpu:54)                   Varetekst-2
     d  d_anta                        5    overlay(d_inpu:85)                   Antall i pakning
     d  d_sapr                       10    overlay(d_inpu:91)                   Salgspris
     d   d_sapr_kr                    7  0 overlay(d_sapr:1)                     - kroner
     d   d_sapr_Øre                   2  2 overlay(d_sapr:9)                     - øre
     d  d_inpr                       10    overlay(d_inpu:102)                  Innkjøpspris
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)                     - kroner
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)                     - øre
     d  d_grpr                       10    overlay(d_inpu:113)                  Grunnpris 1
     d  d_eann                       13  0 overlay(d_inpu:124)                  Ean-nummer
     d  d_grp2                       10    overlay(d_inpu:137)                  Grunnpris 2
     d  d_rgrp                        5  0 overlay(d_inpu:143)                  Rabattgruppe
     d  d_gtxt                       20    overlay(d_inpu:154)                  Varegruppe-tekst
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_vtxt          s             71
     d w_posi          s              2  0
     d w_leng          s              1  0
     d w_bmen_alf      s             11
      *
      * Definering av konstanter
      *
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = %trim(d_tek1)
     c                   eval      fwtek2 = %trim(d_tek2)
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = d_eann
     c                   eval      fwnobb = 0
     c                   eval      fwdato = *loval
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
     c                   eval      fwsapr = d_sapr_kr + d_sapr_Øre
     c                   eval      fwbmen = 0
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
      * Konkatinerer varetekst-1 og varetekst-2
      *
     c                   exsr      konkatiner
      *
      * Konverterer enhet
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
      * Bestillingsmengde
      *
     c                   if        d_anta <> *blank
     c     digits        check     d_anta                                 91
     c                   if        *in91 = *off
     c                   eval      d_anta = %trim(d_anta)
     c                   eval      w_leng = %len(%trim(d_anta))
     c                   eval      w_bmen_alf = *blank
     c                   eval      w_bmen_alf = %subst(w_bmen_alf:1:8-w_leng) +
     c                                          d_anta + '000'
     c                   move      w_bmen_alf    fwbmen
     c                   endif
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konkatinering av varetekst-1 og varetekst-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konkatiner    begsr
      *
     c                   eval      w_vtxt = *blank
      *
      * Trimmer varetekst-1
      *
     c     ' '           scan      fwtek1:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_vtxt = %trim(w_vtxt) + ' ' +
     c                                      %subst(fwtek1:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      fwtek1 = %trim(
     c                                      %subst(fwtek1:w_posi:36-w_posi))
      *
     c     ' '           scan      fwtek1:1      w_posi                   92
     c                   enddo
      *
      * Trimmer varetekst-2, og konkatinerer denne med varetekt-1
      *
     c                   if        fwtek2 <> *blank
      *
     c     ' '           scan      fwtek2:1      w_posi                   92
     c                   dow       w_posi <> 1
      *
     c                   if        *in92 = *off
     c                   eval      w_posi = 36
     c                   endif
      *
     c                   eval      w_vtxt = %trim(w_vtxt) + ' ' +
     c                                      %subst(fwtek2:1:w_posi-1)
      *
     c                   if        w_posi >= 35
     c                   leave
     c                   endif
      *
     c                   eval      fwtek2 = %trim(
     c                                      %subst(fwtek2:w_posi:36-w_posi))
      *
     c     ' '           scan      fwtek2:1      w_posi                   92
     c                   enddo
      *
     c                   endif
      *
      * Splitter konkatinert varetekst i varetekst-1 og varetekst-2
      *
     c                   eval      fwtek1 = %trim(%subst(w_vtxt:1:35))
     c                   eval      fwtek2 = %trim(%subst(w_vtxt:36:35))
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
