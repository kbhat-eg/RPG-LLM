     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK697R                                              *
      * Beskrivelse . . : Utskrift av generalnota kopi                        *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.50 100108 HFL  Nytt program                                  *
6.nu  * R6.30 140814 nho  Endring vedr kid, sett etter FO616R                 *
6.nu  * R6.20     280515  Utvidelse av kort kid                          NHO  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frktrl5    if   e           k disk    rename(rktrpfr:rktrl5r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     frgirpf    o  a e           k disk
     frk694p    o    e             printer infds(d_infds)
      *
     d rkunl1_kund     s                   like(rnkund)
     d rktrl5_samf     s                   like(rnsamf)
     d aforl1_ruti     s                   like(afruti)
      *
     d                 ds
     d wkwrec                  2    192
     d  wkfirm                        3  0 overlay(wkwrec)
     d  wkkund                        6  0 overlay(wkwrec:4)
     d  wknavn                       30    overlay(wkwrec:10)
      *
     d wksort                        64
     d  wksfir                        3  0 overlay(wksort:40)                   Firma
     d  wksknr                        6  0 overlay(wksort:43)                   Kunden
6.NU d  wksamf                        8  0 overlay(wksort:49)
     d                 ds
     d wgkkey                  2     65
     d  wgfirm                        2  0 overlay(wgkkey)
     d  wgkund                        6  0 overlay(wgkkey:3)
6.NU d  wgbiln                        8  0 overlay(wgkkey:9)
      *
     d                 ds
     d w_udato                        8  0
     d  w_aar                         4  0 overlay(w_udato)
     d  w_mnd                         2  0 overlay(w_udato:5)
     d  w_dag                         2  0 overlay(w_udato:7)
      *
     d                 ds
     d wudato                         6  0
     d  wudag                         2  0 overlay(wudato:1)
     d  wumnd                         2  0 overlay(wudato:3)
     d  wuaar                         2  0 overlay(wudato:5)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
     d pgenf                 300    305  0
     d pgent                          6  0
     d praar                          4  0
     d pudato                         8  0
      *
     d l_ruti                800    809
     d l_hex1                760    779
     d l_hex2                780    799
     d l_sfir                845    845
     d l_alin                856    858  0
     d l_oflw                860    862  0
     d l_kidk                883    883  0
     d l_ocrk                885    885  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d p_stat          s              1
     d p_ckod          s              2  0
     d p_ctxt          s             20
     d p_ctx2          s             20
     d p_fkda          s               d   datfmt(*iso)
     d p_ffda          s               d   datfmt(*iso)
     d p_rda1          s               d   datfmt(*iso)
     d p_rda2          s               d   datfmt(*iso)
     d p_rda3          s               d   datfmt(*iso)
      *
      * Informasjon fra printer-file
      *
     d d_infds         ds
     d  d_linnbr             367    368B 0
     d  d_pagnbr             369    372B 0
      *
      * Datastruktur for kid-feltet
      *
     d                 ds
     d d_kkid                        21
     d  d_kkus                       20    overlay(d_kkid)
     d   d_kfir                       3  0 overlay(d_kkus)
     d   d_kkod                       1  0 overlay(d_kkus:4)
6.nu d   d_ktyp                       1  0 overlay(d_kkus:5)
6.nu d   d_kled                       1  0 overlay(d_kkus:6)
6.nu d   d_kkto                       6  0 overlay(d_kkus:7)
6.nu d   d_kbil                       8  0 overlay(d_kkus:13)
     d  d_kmod                        1  0 overlay(d_kkid:21)
      *
     d akr             s              1    dim(9)
     d n               s              2  0
      *
      * Datastruktur  bankgiro fra
      *
     d                 ds
     d dsktoa                        11  0
     d  dskta1                        4  0 overlay(dsktoa)
     d  dskta2                        2  0 overlay(dsktoa:5)
     d  dskta3                        5  0 overlay(dsktoa:7)
      *
      * Datastruktur  bankgiro til
      *
     d                 ds
     d dsktob                        13
     d  dsktb1                        4    overlay(dsktob)
     d  dsblk1                        1    overlay(dsktob:5)
     d  dsktb2                        2    overlay(dsktob:6)
     d  dsblk2                        1    overlay(dsktob:8)
     d  dsktb3                        5    overlay(dsktob:9)
      *
      * Definering av formater                                          *
      *
      * P1GIR  - Bankgiro hode beløpet gjelder
      * P2GIR  - Bankgiro hode gebyr
      * P3GIR  - Bankgiro hode rabatt
      * P4GIR  - Bankgiro hode navn/adresse
      * P5GIR  - Bankgiro beløpslinje
      *
6.nu d*w_kidr          s              6  0
6.nu d w_kidr          s              8  0
     d w_ksif          s              1  0
     d w_kkus          s             24
     d w_kkid          s             21
5.49 d w_kki2          s             24
      *
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_tbnk          s             10
     d w_fbgi          s             13
      *
     d w_firm          s                   like(rnfirm)
     d w_avde          s              4  0
     d w_aprt          s              1  0
     d w_bdat          s              6  0
     d w_belØ          s             11  2
     d w_tbel          s             11  2
     d s_samf          s                   like(rnsamf)
      *
      /eject
      *
      * Leser kundetransaksjoner ######################## START ######
      *
     c                   move      pgenf         rktrl5_samf
     c     rktrl5_key    setll     rktrl5
     c     rktrl5_key    reade     rktrl5                                 97
      *
     c                   dow       *in97 = *off
      *
      * Er generalnota innen valgt intervall?
      *
     c                   if        rnsamf > pgent
     c*                  iter
     c                   goto      nyles
     c                   endif
      *
      * Generalnota på valgt år?
      *
     c                   if        rnraar <> praar
     c                   goto      nyles
     c                   endif
      *
     c                   exsr      beh_post
      *
     c     nyles         tag
     c     rktrl5_key    reade     rktrl5                                 97
     c                   enddo
      *
     c     slutt         tag
      *
     c                   exsr      brd_gennot
     c                   write(e)  totsam
      *
     c                   eval      *inlr = *on
      *
      /eject
      *
      * Subrutine for behandling av posteringer
      *
     c     beh_post      begsr
      *
      * Test brudd på generalnotanr.
      *
     c                   if        rnsamf <> s_samf
     c                   if        w_belØ <> *zero
      * Avslutt generalnota
     c                   exsr      brd_gennot
     c                   eval      a1side = 1
     c                   endif
      *
      * Start ny generalnota
     c                   eval      page = 0
     c                   eval      w_belØ = *zeros
     c                   move      rnkund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1                             91
      *
      *  Hent inn avd. opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   if        afprfi = 2
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = rkavde
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
     c                   eval      a4fnav = w_fnav
     c                   eval      a4fad1 = w_fad1
     c                   eval      a4fad2 = w_fad2
     c                   eval      a4fste = w_fste
     c                   eval      a5fbgi = w_fbgi
     c                   eval      a6fbgi = w_fbgi
      *
      * Skal firmanavn droppes på faktura/bankgiro ?
      *
     c                   if        w_aprt = 1
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   eval      a4fnav = *blank
     c                   eval      a4fad1 = *blank
     c                   eval      a4fad2 = *blank
     c                   eval      a4fste = *blank
     c                   endif
      *
     c                   if        w_aprt = 2
     c                   eval      a4fnav = *blank
     c                   eval      a4fad1 = *blank
     c                   eval      a4fad2 = *blank
     c                   eval      a4fste = *blank
     c                   endif
      *
     c                   endif
      *
      * Klargjør kundedata for utskrift
      *
     c                   move      rkkund        a1kund
     c                   move      rknavn        a1navn
     c                   move      rkgate        a1gate
     c                   move      rkadr2        a1adr2
     c                   move      rksted        a1sted
      *
     c     *dmy          move      rnfdat        a1fdat
     c                   move      wudato        a1dato
      *
     c                   eval      w_4sam = rnsamf
     c                   eval      a1side = 1
      *
     c                   write(e)  a1hdr
      *
     c                   endif
     c                   eval      s_samf = rnsamf
      *
     c     *dmy          move      rnbdat        w_bdat
      *
     c                   write(e)  linje
      *
      * Er det overflow ?
      *
     c                   if        %error = '1'
     c                   eval      a1side = a1side + 1
     c                   write(e)  a1hdr
     c                   endif
      *
      * Akkumuler til total
      *
     c                   eval      w_belØ = w_belØ + rnbelØ
     c                   eval      w_tbel = w_tbel + rnbelØ
      *
     c                   endsr
      *
      /eject
      *                                                                 *
      * Subrutine for brudd på generalnota                              *
      *                                                                 *
     c     brd_gennot    begsr
      *
      *
      * Klargjør kidd
      *
     c                   exsr      xkidd
      *
      * Skriv bankgiro-fil
      *
     c                   exsr      skr_giro
      *
      * Skriv totalinje
      *
     c                   write(e)  total
      *
      * Er det overflow ?
      *
     c                   if        %error = '1'
     c                   write(e)  a1hdr
     c                   endif
      *
      * Skrive bankgiro
      *
     c                   eval      a1txt1 = 'Beløpet gjelder nota'
      *
     c                   move      w_4sam        a1biln
     c                   eval      a1belØ = w_belØ
     c                   move      pudato        a1dato
     c                   eval      a2rent = 0
      *
      * Navn/Adresse
      *
     c                   move      rkkund        a4kund
     c                   move      rknavn        a4navn
     c                   move      rkgate        a4gate
     c                   move      rkadr2        a4adr2
     c                   move      rksted        a4sted
      *
      * Beregne modulus 10 på beløpsfeltet
      *
     c                   move      *blank        d_kkus
     c                   move      w_belØ        w_kkus
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   move      w_ksif        a5bsif
      *
      * Splitte kr/øre
      *
     c                   movel     w_belØ        a5kron
     c                   move      w_belØ        a5Ører
      *
      * Fjerne foranstilte nuller
      * i et alfanummerisk felt
      *
     c                   movea     a5kron        akr
     c                   z-add     1             n
      *
     c                   dow       akr(n) = '0'
     c                   move      ' '           akr(n)
     c                   add       1             n
     c     n             cabgt     9             xsorti
     c                   enddo
      *
     c     xsorti        tag
      *
     c                   movea     akr(1)        a5kron
      *
     c                   exsr      xprint
      *
     c                   endsr
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Skriv bankgirofil                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     skr_giro      begsr
      *
     c                   move      rkfirm        rgfirm
     c                   move      rkfirm        wgfirm
     c                   move      rkkund        rgkund
     c                   move      rkkund        wgkund
     c                   move      w_4sam        rgbiln
     c                   move      w_4sam        wgbiln
     c                   eval      rgrabb = *zeros
     c                   move      w_belØ        rgbelØ
     c                   move      rknavn        rgnavn
     c                   move      rkgate        rggate
     c                   move      rkponr        rgponr
     c                   move      rksted        rgsted
     c                   move      rkpers        rgpers
     c                   move      rktlfn        rgtlfn
     c                   move      rktlfx        rgtlfx
     c                   move      rkbgir        rgbgir
     c                   move      rklagr        rglagr
     c                   move      rkslgr        rgslgr
     c                   move      rkdist        rgdist
     c                   move      rkkatg        rgkatg
     c                   move      rkbetb        rgbetb
     c                   move      rkbetm        rgbetm
     c                   move      rkfrie        rgfrie
     c                   move      rkavde        rgavde
     c                   move      rkftnr        rgftnr
     c                   move      pudato        rgbdat
     c                   move      rnfdat        rgfdat
     c                   move      'P'           rgakti
     c                   move      rkadr2        rgadr2
     c                   eval      rgkkid = w_kkid
      *
     c                   move      wgkkey        rgkkey
     c                   write     rgirpfr
      *
     c                   endsr
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XKIDD  Klargjør kidd                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xkidd         begsr
      *
      * Løsning for å kunne benytte kortere kidd-nummer,
      *
      * Bygger opp kiddfelt (lang versjon)
      *
     c                   eval      d_kfir = w_firm
E5.20c                   eval      d_kkod = 1                                   Rentenota i kid
     c                   eval      d_kled = *zero
6.nu c                   eval      d_ktyp = *zero
     c                   eval      d_kkto = rkkund
     c                   eval      d_kbil = w_4sam
     c                   eval      w_kkus = d_kkus
     c                   eval      w_ksif = *zero
      *
      * Beregning av sjekksiffer på kiddfelt (lang versjon)
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   eval      d_kmod = w_ksif
     c                   eval      w_kkid = d_kkid
      *
      * Oppdatering av kidd-referanse-fil
      *
     c                   select
     c                   when      l_kidk = 0
     c                   move      *blank        a5kkid
T5.30c                   eval      w_kkid = *blanks
T5.30c                   eval      *in88  = *on
      *
     c                   when      l_kidk = 1
     c                   eval      a5kkid = %trim(w_kkid)
      *
     c                   when      l_kidk = 2
     c                   call      'AK700R'
     c                   parm                    w_firm
     c                   parm                    w_kkid
     c                   parm                    w_kidr
     c                   parm                    w_ksif
5.49 c                   parm                    w_kki2
      *
     c                   move      *blank        d_kkid
     c                   move      w_kidr        d_kkus
     c                   move      w_ksif        d_kmod
     c                   eval      w_kkid = d_kkid
     c                   eval      a5kkid = %trim(w_kkid)
      *
     c                   endsl
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XPRINT Skriver bankgiro                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xprint        begsr
      *
      * Skriv beløpet gjelder
      *
     c                   write(e)  p1gir
      *
      * Skriv gebyrlinje
      *
     c                   if        a2rent > *zero
     c                   write(e)  p2gir
     c                   endif
      *
      * Skriv rabattlinje
      *
     c                   if        a3rabb > *zero
     c                   write(e)  p3gir
     c                   endif
      *
      * Skriv firmanavn
      *
     c                   write(e)  p4gir
      *
      * Skrive giro H-linje dersom ikke OCR
      *
     c                   if        l_ocrk = 0
     c                   write(e)  p5gir
     c                   endif
      *
      * Skrive giro H-linje dersom OCR og 4224
      *
     c                   if        l_ocrk = 1
     c                   write(e)  p6gir
     c                   endif
      *
      * Skrive giro H-linje dersom OCR og Axix-boks
      *
     c                   if        l_ocrk = 2
     c                   move      l_hex1        p1hex1
     c                   move      l_hex2        p1hex2
     c                   write(e)  p7gir
     c                   endif
      *
      * Skrive bankgiro H-linje dersom 5224 skriver
      *
     c                   if        l_ocrk = 3
     c                   write(e)  p8gir
     c                   endif
      *
      * Skrive bankgiro H-linje dersom OCR og Nett-print
      *
     c                   if        l_ocrk = 4
     c                   move      l_hex1        p1hex1
     c                   move      l_hex2        p1hex2
     c                   write(e)  p9gir
     c                   endif
      *
     c                   endsr
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     rktrl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl5_samf
     c*                  kfld                    rktrl5_bdat
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Setter startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      wksort = *blank
     c                   eval      wksfir = l_firm
      *
      * Hente inn formular-register
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r                            69
      *
     c                   if        not%found
     c                   eval      afprfi = 1
     c                   endif
      *
      * Hente inn formular-register
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        not%found
     c                   eval      afinst = 1
     c                   eval      afprfi = 1
     c                   endif
      *
      * Instillinger av blankett
      *
     c                   if        afinst = 1
     c                   write(e)  a0hdr
     c                   endif
      *
      *  Hent inn firma opplysninger dersom
      *  kode for det er satt i rutineregister
      *
      *  NB NB NB
      *  Når parameteret w_aprt er tilgjengelig via rutineregisteret
      *  må koden nedenfor tilpasses
      *
     c                   if        afprfi = 1 or
NB   c                             afprfi = 0
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = rkavde
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
NB   c                   if        afprfi = 1
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
     c                   eval      a4fnav = w_fnav
     c                   eval      a4fad1 = w_fad1
     c                   eval      a4fad2 = w_fad2
     c                   eval      a4fste = w_fste
     c                   eval      a5fbgi = w_fbgi
     c                   eval      a6fbgi = w_fbgi
NB   c                   endif
      *
      * Skal firmanavn droppes på rentenota ?
      *
     c                   if        w_aprt = 1
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   eval      a4fnav = *blank
     c                   eval      a4fad1 = *blank
     c                   eval      a4fad2 = *blank
     c                   eval      a4fste = *blank
     c                   endif
      *
     c                   if        w_aprt = 2
     c                   eval      a4fnav = *blank
     c                   eval      a4fad1 = *blank
     c                   eval      a4fad2 = *blank
     c                   eval      a4fste = *blank
     c                   endif
      *
     c                   endif
      *
     c                   move      pudato        w_udato
     c                   move      w_aar         wuaar
     c                   move      w_mnd         wumnd
     c                   move      w_dag         wudag
     c                   eval      pdato = wudato
      *
     c                   endsr
