      /TITLE  ASLAGR Faktura, spørring
     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : SH401R
      * Beskrivelse . . : Faktura, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
      * R5.30 241104 KOB  Lagt inn leverandør i parameter for SH410R
      * R5.60 240308 KOB  Lagt inn spørring mot arkiv
5.61  * R5.60 050609 bof  Lagt inn en test på dato
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.20  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.nu  *  6.30     040614  Sjekket mtp. nummerutvidelse                   bof  *
6.30  * R6.20     040714  Endret metode for oppslag mot arkiv            bsa  *
7.xx  * 7.xx      180516  Endringer reltart til ny GUI. NB Kan kun gjelde BSA
7.02  * 7.00  280319 bkv  Frisøk
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsihelr    if   e           k disk    rename(sihepfr:sihelrr)
     fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
     fsihel3    if   e           k disk    rename(sihepfr:sihel3r)
     fsihel4    if   e           k disk    rename(sihepfr:sihel4r)
     fsihel5    if   e           k disk    rename(sihepfr:sihel5r)
     fsihela    if   e           k disk    rename(sihepfr:sihelar)
     fsihel6    if   e           k disk    rename(sihepfr:sihel6r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fsh401d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(shfirm)
     d p_aarr          s                   like(shaarr)
     d p_otyp          s                   like(shotyp)
     d p_levr          s                   like(shldor)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sihelr_binr     s                   like(shbinr)
     d sihel1_binr     s                   like(shbinr)
     d sihel1_numm     s                   like(shnumm)
     d sihel3_numm     s                   like(shnumm)
     d sihel4_otyp     s                   like(shotyp)
     d sihel5_fkda     s                   like(shfkda)
     d sihela_lena     s                   like(shlena)
     d sihela_navn     s                   like(shnavn)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(shfirm)
     d w_levr          s                   like(shldor)
     d w_aarr          s                   like(shaarr)
6.30 d w_type          s             50
pdf  d w_modu          s             10    inz('PROA    ')
5.60 d*w_modu*pdf      s             10    inz('MSI     ')
5.60 d w_disp          s              1    inz('1')
5.60 d w_tilg          s              1
5.60  *
5.60  * Definering av parametre MultiArcive
5.60  *
5.60 d w_pgrp          s              2
5.60 d w_aara          s              4
6.nu d w_bila          s              8
5.60 d w_afir          s              3
6.30 d w_refn          s             50
7.02 d b_open_sok      s              1    inz(*off)
7.02 d sqlquery        s           4096
7.02 d sqlwhere        s           2096
7.02 d sql_fri_s       s             50
7.02 d sql_fri_sl      s             50
7.02 d x               s              1  0
7.02 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
7.02 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
7.02 d w_ste1          s             35
7.02 d w_ste2          s             35
7.02 d w_ste3          s             35
7.02 d w_ste4          s             35
7.02 d w_ste5          s             35
7.02 d w_fsok          s                   like(b2fsok)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      /eject
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
     C/End-Exec
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
7.02  *
7.02  * F21=Hent søk
7.02  *
7.02 c                   when      *inkv
7.02 c                   eval      b2fsok = w_fsok
7.02 c                   eval      *in22 = *on
7.02 c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2binr <> 0 or
     c                             b2numm <> 0 or
     c                             b2otyp <> *blank or
     c                             b2fkda <> 0 or
     c                             b2leva <> *blank or
7.02 c                             b2fsok <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c                   eval      b2fsok = *blank
7.02 c                   eval      b2fso2 = *blank
7.02 c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'O'
     c     sihel3_key    setll     sihel3
     c                   when      w_seqe = 'T'
     c     sihel4_key    setll     sihel4
     c                   when      w_seqe = 'D'
     c     sihel5_key    setll     sihel5
     c                   when      w_seqe = 'K'
     c     sihela_key    setll     sihela
     c                   other
     c     sihel1_key    setll     sihel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på fakturanummer
      *
     c                   if        b2binr <> 0
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = ' '
     c                   eval      sihel1_binr = b2binr
     c                   eval      sihel1_numm = b2numm
     c     sihel1_key    setll     sihel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2numm <> 0
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'O'
     c                   eval      sihel3_numm = b2numm
     c     sihel3_key    setll     sihel3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordretype
      *
     c                   if        b2otyp <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'T'
     c                   eval      sihel4_otyp = b2otyp
     c     sihel4_key    setll     sihel4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på fakturadato
      *
     c                   if        b2fkda <> 0
7.02 c                   eval      b2fso2 = *blank
      *
     c                   eval      w_seqe = 'D'
     c                   eval      sihel5_fkda = *loval
      *
     c     *dmy          test(d)                 b2fkda                 90
     c                   if        *in90 = *off
     c     *dmy          move      b2fkda        sihel5_fkda
     c                   endif
      *
     c     sihel5_key    setll     sihel5
     c                   goto      end_pos
      *
     c                   endif
      *
      * Posisjoner startverdi på alfasøk leverandør
      *
     c                   if        b2leva <> *blank
7.02 c                   eval      b2fso2 = *blank
     c                   eval      w_seqe = 'K'
     c                   eval      sihela_lena = b2leva
     c     sihela_key    setll     sihela
     c                   goto      end_pos
     c                   endif
7.02  *
7.02  * Posisjoner startverdi på frisøk
7.02  *
7.02 c                   if        b2fsok <> *blank
7.02 c                   eval      b2fso2 = b2fsok
7.02 c                   eval      w_fsok = b2fsok
7.02 c                   eval      b_open_sok = *on
7.02 c                   eval      w_seqe = 'S'
7.02 c                   goto      end_pos
7.02 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2binr = 0
     c                   eval      b2numm = 0
     c                   eval      b2otyp = *blank
     c                   eval      b2fkda = 0
     c                   eval      b2leva = *blank
7.02 c                   eval      b2fsok = *blank
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
7.02 c                   if        *in22 = *off
7.02 c                   eval      *in22 = *on
7.02 c                   else
7.02 c                   eval      *in22 = *off
7.02 c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
7.02 c                   eval      *in22 = *off
     c                   eval      wvisrn = w_srrn
      *
      * Vise faktura
      *
     c                   if        b1valg = '5'
     c                   call      'SH410R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1ldor
     c                   parm                    b1binr
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv fakturakopi
      *
     c                   if        b1valg = '6'
     c                   call      'LO681R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1binr
     c                   parm                    b1otyp
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
5.60  *
5.60  * Vis arkiv
5.60  *
5.60 c                   if        b1valg = '9'
5.60  *
5.60  * Kontrollerer gyldig tilgang
5.60  *
5.60 c                   call      'AX700R'
5.60 c                   parm                    w_modu
5.60 c                   parm                    w_disp
5.60 c                   parm                    w_tilg
5.60  *
5.60 c                   if        w_tilg = '0'
5.60  *
pdf  c                   eval      w_type = 'FAKTURA'
5.60 c                   move      w_firm        w_afir
5.60 c                   eval      w_pgrp = '30'
5.60 c                   eval      w_aara = %char(w_aarr)
5.60 c                   eval      w_bila = %char(b1binr)
6.30 c                   eval      w_refn = *blank
6.30 c                   eval      w_refn = %char(w_aarr) + %trim(%char(b1binr))
5.60  *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.30 c                   parm                    w_refn
6.30 c**                 parm                    w_aara
6.30 c**                 parm                    w_bila
6.30 c**                 parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
5.60  *
5.60 c                   endif
5.60  *
5.60 c                   eval      b1valg = *blank
5.60 c                   update    b1sfl
5.60 c                   iter
5.60 c                   endif
      *
      * Vise ordrehode
      *
     c                   if        b1valg = '7'
     c                   call      'SH415R'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    b1binr
     c                   parm                    b1numm
     c                   parm                    b1suff
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
7.02  * Initierer SQL for søk
7.02  *
7.02 c                   if        b_open_sok = *on
7.02  *
7.02 c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c/exec sql
7.02 c+ close STMT
7.02 c/end-exec
7.02 c
7.02 c                   eval      sqlwhere = ''
7.02 c                   if        b2fsok <> *blanks
7.02 c     lo:up         xlate     b2fsok        b2fsok
7.02 c                   call      'AS700R'
7.02 c                   parm                    b2fsok
7.02 c                   parm                    w_ste1
7.02 c                   parm                    w_ste2
7.02 c                   parm                    w_ste3
7.02 c                   parm                    w_ste4
7.02 c                   parm                    w_ste5
7.02 c
7.02 c                   eval      w_ste1 = %ScanRpl('%' : ' ' : w_ste1)
7.02 c     1             do        5             x
7.02 c                   select
7.02 c                   when      x=1
7.02 c                   if        w_ste1 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste1)+'%'''
7.02 c                   when      x=2
7.02 c                   if        %trim(w_ste2) = ''
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste2)+'%'''
7.02 c                   when      x=3
7.02 c                   if        w_ste3 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste3)+'%'''
7.02 c                   when      x=4
7.02 c                   if        w_ste4 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste4)+'%'''
7.02 c                   when      x=5
7.02 c                   if        w_ste5 = *blanks
7.02 c                   leave
7.02 c                   endif
7.02 c                   eval      sql_fri_s = '''%'+%trim(w_ste5)+'%'''
7.02 c                   endsl
7.02 c
7.02 c     up:lo         xlate     sql_fri_s     sql_fri_sl
7.02 c                   eval      sql_fri_sl = %ScanRpl('%' : '' : sql_fri_sl)
7.02 c
7.02 c                   eval      sqlwhere  = %trim(sqlwhere) + ' +
7.02 c                             and ( +
7.02 c                                a.shlena like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.shnavn like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or b.rlnavn like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or b.rllevr like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.shnumm like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.shadr1 like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.shadr2 like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.shsted like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or a.shbinr like ' +%trim(sql_fri_s)+ ' +
7.02 c                             or UPPER(a.shdref) like '+
7.02 c                                %trim(sql_fri_s)+ ' +
7.02 c                             or UPPER(a.shrekv) like '+
7.02 c                                %trim(sql_fri_s)+ ' +
7.02 c                              or lower(DAYNAME(a.shbdat)) +
7.02 c                                  = '+sql_fri_sl+ ' +
7.02 c                             or ((select count(*) from sidtpf +
7.02 c                             where slfirm=a.shfirm and slnumm= +
7.02 c                              a.shnumm and slsuff = a.shsuff and +
7.02 c                             sltek1 like '+%trim(sql_fri_s)+ ' ) <> 0)   +
7.02 c                             or ((select count(*) from sidtpf +
7.02 c                             where slfirm=a.shfirm and slnumm= +
7.02 c                             a.shnumm and slsuff = a.shsuff and +
7.02 c                             slvare LIKE '+%trim(sql_fri_s)+ ' ) <> 0)   +
7.02 c                               )'
7.02 c                   enddo
7.02 c                   endif
7.02 c
7.02 c                   eval      sqlquery  = 'select +
7.02 c                               a.shfirm +
7.02 c                              ,a.shaarr +
7.02 c                              ,a.shbinr +
7.02 c                              ,a.shnumm +
7.02 c                              ,a.shsuff +
7.02 c                              ,a.shotyp +
7.02 c                              ,a.shnavn +
7.02 c                              ,a.shfkda +
7.02 c                              ,a.shldor +
7.02 c                              ,a.shfsum +
7.02 c                               from   sihepf a +
7.02 c                               left outer join rlevpf b on b.rlfirm = +
7.02 c                                 a.shfirm and b.rllevr = a.shldor +
7.02 c                               '
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               where  a.shfirm = ? and a.shaarr = ? '
7.02 c                   eval      sqlquery  = %trim(sqlquery) +' '+
7.02 c                                            %trim(sqlwhere)
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               order by +
7.02 c                               a.shdate desc +
7.02 c                               , a.shtime desc +
7.02 c                               , a.shnumm +
7.02 c                               , a.shsuff +
7.02 c                               '
7.02 c                   eval      sqlquery  = %trim(sqlquery) + ' +
7.02 c                               for read only +
7.02 c                               '
7.02 c
7.02 c/exec sql
7.02 c+ PREPARE SQL_STMT FROM :sqlquery
7.02 c/end-exec
7.02 c/exec sql
7.02 c+ DECLARE STMT CURSOR FOR SQL_STMT
7.02 c/end-exec
7.02 c
7.02 c                   if        sqlcod = 100 or sqlstt = '02000'
7.02 c                   eval      *in15 = *on
7.02 c                   leavesr
7.02 c                   endif
7.02 c
7.02 c/exec sql
7.02 c+ OPEN STMT using :w_firm, :w_aarr
7.02 c/end-exec
7.02 c
7.02 c                   endsl
7.02 c                   endif                                                  b_open_sok = *on
      *
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
7.02 c                   when      w_seqe = 'S'
7.02 c/exec sql
7.02 c+ fetch STMT
7.02 c+ into  :shfirm,:shaarr,:shbinr,:shnumm,:shsuff,:shotyp,
7.02 c+       :shnavn,:shfkda,:shldor,:shfsum
7.02 c/end-exec
7.02 c                   if        sqlcod = 100 or sqlstt = '02000'
7.02 c                   eval      *in15 = *on
7.02 c                   endif
     c                   when      w_seqe = 'O'
     c                   read      sihel3                                 15
     c                   when      w_seqe = 'T'
     c                   read      sihel4                                 15
     c                   when      w_seqe = 'D'
     c                   read      sihel5                                 15
     c                   when      w_seqe = 'K'
     c                   read      sihela                                 15
     c                   other
     c                   read      sihel1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        shfirm <> w_firm or
     c                             shaarr <> w_aarr
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   if        p_otyp <> *blank and
     c                             shotyp <> p_otyp
     c                   iter
     c                   endif
      *
     c                   if        p_levr <> 0 and
     c                             shldor <> p_levr
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   eval      b1binr = shbinr
     c                   eval      b1numm = shnumm
     c                   eval      b1suff = shsuff
     c                   eval      b1otyp = shotyp
     c                   eval      b1navn = shnavn
     c                   eval      b1ldor = shldor
      *
5.61 c                   if        shfkda <> *loval
     c     *dmy          move      shfkda        b1fkda
5.61 c                   else
5.61 c                   eval      b1fkda = 0
5.61 c                   endif
      *
     c                   eval      rlevl1_levr = shldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      b1navn = rlnavn
     c                   endif
      *
      * Henter fakturasum fra siste ordre på fakura
      *
     c                   eval      sihelr_binr = shbinr
     c     sihelr_key    setll     sihelr
     c     sihelr_key    reade     sihelr                                 90
     c                   dow       *in90 = *off
     c     sihelr_key    reade     sihelr                                 90
     c                   enddo
      *
     c                   eval      b1fsum = shfsum
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
7.02 c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_otyp
     c                   parm                    p_levr
      *
      * Key for les på fakturaregister
      *
     c     sihelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sihelr_binr
      *
      * Key for posisjonering på fakturanummer
      *
     c     sihel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sihel1_binr
     c                   kfld                    sihel1_numm
      *
      * Key for posisjonering på ordrenummer
      *
     c     sihel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sihel3_numm
      *
      * Key for posisjonering på ordretype
      *
     c     sihel4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sihel4_otyp
      *
      * Key for posisjonering på fakturadato
      *
     c     sihel5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sihel5_fkda
      *
      * Key for posisjonering på leverandør alfasøk
      *
     c     sihela_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    sihela_lena
     c                   kfld                    sihela_navn
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firmakode og år fra parameter
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      b2firm = w_firm
     c                   eval      w_aarr = p_aarr
      *
      * Henter verdi til skjermbilde
      *
     c                   eval      b2aarr = w_aarr
     c                   eval      b2oty1 = p_otyp
     c                   eval      b2levr = p_levr
      *
      * Fyller subfile
      *
     c                   eval      *in22 = *on
      *
     c     sihel1_key    setll     sihel1
      *
7.02 c                   eval      w_seqe = 'S'
7.02 c                   eval      b_open_sok = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
