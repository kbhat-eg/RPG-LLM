     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV130R
      * Beskrivelse . . : Oppdatering av vareinfo til EVR, parameterprog
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020796 HKO  Nytt program
6.10  * R6.10 191109 bof  Ønske om at man går ut av bildet når ferdig
7.xx  * 7.00  040316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fjv130d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller overføringsprogram
      *
     c                   call      'JV730R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
      *
6.10 c*                  goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'JL500R'
     c                   parm                    b1ldor
     c                   parm                    b1navn
     c                   goto      end_spØrr
     c                   endif                                                           .....
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1navn = *blank
     c                   eval      b1utxt = *blank
      *
      * Leverandør
      *
     c                   if        b1ldor = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1navn = jlnavn
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0 and
     c                             b1hgrp = 0 and
     c                             b1ugrp = 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0 and
     c                             b1ugrp = 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
