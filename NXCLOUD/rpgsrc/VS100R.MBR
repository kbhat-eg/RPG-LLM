     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VS100R
      * Beskrivelse . . : Søketekst, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110199 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvsokl1    if   e           k disk    rename(vsokpfr:vsokl1r)
     fvsoklr    if   e           k disk    rename(vsokpfr:vsoklrr)
     fvsoklu    uf a e           k disk    rename(vsokpfr:vsoklur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvs100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vsfirm)
     d p_vare          s                   like(vsvare)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vsoklu_stek     s                   like(vsstek)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_dupl          s              1    inz(*off)
     d b_oppd          s              1    inz(*off)
      *
     d w_firm          s                   like(vsfirm)
     d w_vare          s                   like(vsvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(9)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      * Oppdater vare-søketekst-register med endringer
      *
     c                   if        b_oppd = *on
     c                   call      'VV750R'
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     vsokl1_key    setll     vsokl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
     c                   if        b1stek = b1work
     c                   iter
     c                   endif
      *
      * Sletting av søketekst
      *
     c                   if        b1stek = *blank
     c                   eval      vsoklu_stek = b1work
     c     vsoklu_key    chain     vsoklur                            91
     c                   if        *in91 = *off
     c                   delete    vsoklur
     c                   endif
     c                   eval      b1work = *blank
     c                   eval      b_oppd = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Oppretting eller endring av søketekst
      *
      * Dersom søketeksten er ny eller endret sjekkes det om søketekst
      * finnes fra før
      *
     c                   eval      b_dupl = *off
      *
     c     vsoklr_key    setll     vsoklr
     c     vsoklr_key    reade     vsoklr                                 91
     c                   dow       *in91 = *off
     c                   if        vsstek <> b1work and
     c                             vsstek = b1stek
     c                   eval      b_dupl = *on
     c                   leave
     c                   endif
     c     vsoklr_key    reade     vsoklr                                 91
     c                   enddo
      *
     c                   if        b_dupl = *on
     c                   eval      *in31 = *on
     c                   iter
     c                   endif
      *
      * Sletter eventuelt endret søketekst
      *
     c                   if        b1work <> *blank
     c                   eval      vsoklu_stek = b1work
     c     vsoklu_key    chain     vsoklur                            91
     c                   if        *in91 = *off
     c                   delete    vsoklur
     c                   endif
     c                   endif
      *
      * Henter vareinformasjon
      *
     c     vvarl1_key    chain     vvarl1                             91
      *
      * Oppretter søketekst
      *
     c                   eval      vsfirm = w_firm
     c                   eval      vsvare = w_vare
     c                   eval      vsstek = b1stek
      *
     c                   eval      b_oppd = *on
     c                   write     vsoklur
      *
     c                   iter
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     vsokl1_key    reade     vsokl1
      *
     c                   if        %eof = *off
     c                   eval      b1work = vsstek
     c                   eval      b1stek = vsstek
     c                   else
     c                   eval      *in15 = *on
     c                   eval      b1work = *blank
     c                   eval      b1stek = *blank
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for les på søketekstregister
      *
     c     vsokl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på søketekstregister
      *
     c     vsoklr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av søketekstregister
      *
     c     vsoklu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vsoklu_stek
      *
      * Key for oppsalg på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vsokl1_key    setll     vsokl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
