6.32 h option(*nodebugio) datedit(*dmy-) decedit('0.')
6.32 h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
6.32  /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF501R
      * Beskrivelse . . : Salgsstatistikk, spørring på salg til kunde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250698 HKO  Nytt program
5.30  * R5.30 141004 AMD  Endring pga bongnummer fra 6 num til 12 alfa
5.31  * R5.31 200505 AMD  Endring for å skille på utskrift av faktura
5.31  *                   da bongnummer skal være med som nøkkel dersom
5.31  *                   faktura er kjørt i butikk
      *       131005 HKO  Utvidet antall rader i subfilen, og skrevet
      *                   programmet om til ny standard (m/rollback)
      * V5.50 110607 KOB  Lagt ut bto.salgspris i stedet for antall i subfile
      * V5.50 111007 KOB  Lagt tilbake antall etter protester fra flere kunder
6.11  * R6.11 201010 AMD  Program feilet pga. mangler i parameterliste
6.30  * 6.30  280115 bsa  Skriver til datakø for å trigge java sniffer
6.32  * V6.30 190115 bsa  Lagt inn mulighet for eksport til excel
6.33  * 6.30  050615 bsa  Nullstill minne for brukj av XML
7.xx  * 7.00  180216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 6.30  180517 bsa  Lagt inn oppslag mot arkiv.
7.02  * 6.30  050220 bsa  Blir rot i liste ved kundeprosjekt.
7.03  *K000   240820 bsa  Blir rot i liste.
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal3    if   e           k disk    rename(sstapfr:sstal3r)
     fsstal4    if   e           k disk    rename(sstapfr:sstal4r)
     fsstal5    if   e           k disk    rename(sstapfr:sstal5r)
     fsstal6    if   e           k disk    rename(sstapfr:sstal6r)
     fsstal7    if   e           k disk    rename(sstapfr:sstal7r)
     fsstal8    if   e           k disk    rename(sstapfr:sstal8r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
6.32 fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.32 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
7.01 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
     fsf501d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sffirm)
     d p_paar          s                   like(sfpaar)
     d p_vkun          s                   like(sfvkun)
     d p_kpro          s                   like(sfkpro)
     d p_bida          s                   like(sfbida)
      *
      * Definering av Local data area
      *
     d                uds
6.32 d l_poff                584    584
6.32 d l_utqm                585    594
6.32 d l_bach                595    635
6.32 d l_arch                636    636  0
6.32 d l_skje                637    637  0
6.32 d l_land                638    638  0
6.32 d l_exlp                639    639  0
6.32 d l_mail                640    640  0
p.3x d l_prfi                750    750  0
6.32 d l_outq                810    819
6.32 d l_copy                838    843  0
6.32 d l_ruti                800    809
6.32 d l_wsid                901    910
     d l_user                911    920
6.32 d l_filg                931    933
     d l_firm                944    946  0
6.32 d l_avde                947    950  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d sstal3_vare     s                   like(sfvare)
     d sstal3_kpro     s                   like(sfkpro)
     d sstal3_bida     s                   like(sfbida)
     d sstal4_tek1     s                   like(sftek1)
     d sstal5_kpro     s                   like(sfkpro)
     d sstal6_bida     s                   like(sfbida)
     d sstal7_binr     s                   like(sfbinr)
     d sstal8_ornr     s                   like(sfornr)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_wsid     s                   like(bowsid)
5.30 d bohel1_bong     s                   like(bobong)
6.32 d aforl1_ruti     s                   like(afruti)
7.01 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d w_tek1          s             35
     d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(sffirm)
     d w_paar          s                   like(sfpaar)
     d w_vkun          s                   like(sfvkun)
     d w_bida          s                   like(sfbida)
     d w_vare          s                   like(sfvare)
     d w_pnav          s                   like(flnavn)
     d w_adr1          s                   like(fladr1)
     d w_adr2          s                   like(fladr2)
     d w_sted          s                   like(fladr1)
     d w_kprs          s                   like(flkprs)
     d w_tlfa          s                   like(fltlfa)
     d w_tlfp          s                   like(fltlfp)
     d w_tlfm          s                   like(fltlfm)
     d w_mail          s                   like(flmail)
     d w_orka          s                   like(florka)
     d w_okun          s                   like(flokun)
     d w_okpr          s                   like(flokpr)
     d w_opda          s                   like(flopda)
     d w_proj          s                   like(flproj)
6.11 d w_kjor          s                   like(flkjor)
6.11 d w_kjnr          s                   like(flkjnr)
6.32 d w_fmai          s                   like(aamail)
6.32 d w_weba          s                   like(aaweba)
6.32 d w_kpro          s                   like(sfkpro)
6.32 d w_bdat          s                   like(sfbida)
      *
6.32 d w_ddmy          s              6  0
6.32 d w_prfi          s              1  0
6.32 d w_ffir          s              3  0
6.32 d w_fnav          s             30
6.32 d w_fad1          s             30
6.32 d w_fad2          s             30
6.32 d w_fste          s             30
6.32 d w_fftx          s             30
6.32 d w_tfax          s              8
6.32 d w_ftlf          s             11
6.32 d w_ffax          s             11
6.32 d w_head          s             75
6.32 d w_kode          s              1
6.32 d w_fell          s              6
6.32 d w_type          s              2
6.32 d w_fast          s             10
6.32 d w_ruti          s             10
6.32 d w_numm          s              8  0
6.32 d w_pdf           s              1  0
6.32 d w_jnav          s             15
6.32 d w_jkey          s             41
6.32 d w_jtxt          s             35
6.32 d w_pdf_ds        s            512
6.32 d w_mtxt          s            300
6.32 d w_jstat         s              2  0
6.32 d w_jtext         s            200
6.32 d w_kknav         s             50
6.32 d w_kpnav         s             50
6.32 d h_valu          s             15
6.32 d d_valu          s             50
6.32 d d_type          s             10
6.32 d w_dato2         s             12  0
6.32 d w_tids          s              6  0
6.32 d w_txt1          s             75
6.32 d w_mime          s             24
6.32 d w_date          s               d   datfmt(*iso)
6.32 d w_1fnav         s             50
6.32 d w_1fad1         s             50
6.32 d w_1fad2         s             50
6.32 d w_avde          s              4  0
6.32 d w_rtyp          s             50
6.32 d w_fdat          s              6  0
6.32 d w_neto          s             13  2
6.32 d w_knav          s                   like(rknavn)
7.01 d w_modu          s             10    inz('PROA    ')
7.01 d w_disp          s              1    inz('1')
7.01 d w_tilg          s              1
7.01 d w_dtyp          s             50
7.01 d w_refn          s             50
7.01 d w_aarr          s              4  0
      *
6.32 d p_lr            s              1
6.32 d p_str_in        s            512
6.32 d p_str_out       s            512
6.32 d b_pdfp          s              1    inz(*off)
6.30 d p_filg          s             10
6.30 d p_xmlf          s            256
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(12)
      *
6.32 d XML_Output      s            256a   Varying
6.32 d XML_path        s            256a   Varying
6.32 d                                     Inz('/home/asp/print/adb')
6.32 d jasper_mal      s            256a
6.32 d jasper_logo     s            256a   varying
6.32 d tmpdate         s               D
6.32 d tmptime         s               T
6.32 d crlf            c                   const(X'0d25')
6.32  *
6.32 d d_pdf_ds        ds
6.32 d  d_xmlm                       75
6.32 d  d_jasm                       75
6.32 d  d_logo                       75
6.32 d  d_path                      100
6.33 d  d_emal                       50
6.30 d  d_fifo                       75
6.30 d  d_fkop                        1
6.30 d  d_dtaq                        1
6.30 d  d_sign                        1
6.32  /copy prototypeb
6.32  /copy usec
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
6.32 c                   when      *inki
6.32 c                   exsr      exl_eksport
6.32 c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vare <> 0 or
     c                             b2tek1 <> *blank or
     c                             b2kpro <> 0 or
     c                             b2bida <> 0 or
     c                             b2binr <> 0 or
     c                             b2ornr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      sstal4_tek1 = b1tek1
     c     sstal4_key    setll     sstal4
     c                   when      w_seqe = 'A'
     c                   eval      sstal5_kpro = b1kpro
     c     sstal5_key    setll     sstal5
     c                   when      w_seqe = 'D'
     c                   eval      sstal6_bida = %date(b1bida:*dmy)
     c     sstal6_key    setll     sstal6
     c                   when      w_seqe = 'F'
     c                   eval      sstal7_binr = b1binr
     c     sstal7_key    setll     sstal7
     c                   when      w_seqe = 'O'
     c                   eval      sstal8_ornr = b1ornr
     c     sstal8_key    setll     sstal8
     c                   other
     c                   eval      sstal3_vare = b1vare
     c                   eval      sstal3_kpro = b1kpro
     c                   eval      sstal3_bida = %date(b1bida:*dmy)
     c     sstal3_key    setll     sstal3
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Vare
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = *blank
     c                   movel     b2vare        sstal3_vare
     c                   eval      sstal3_kpro = b2kpro
     c                   eval      sstal3_bida = *loval
     c                   if        b2bida <> 0
     c     *dmy          test(d)                 b2bida                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2bida        sstal3_bida
     c                   endif
     c                   endif
     c     sstal3_key    setll     sstal3
     c                   goto      end_pos
     c                   endif
      *
      * Varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      sstal4_tek1 = b2tek1
     c     sstal4_key    setll     sstal4
     c                   goto      end_pos
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      sstal5_kpro = b2kpro
     c     sstal5_key    setll     sstal5
     c                   goto      end_pos
     c                   endif
      *
      * Fakturadato/bilagsdato
      *
     c                   if        b2bida <> 0
     c                   eval      w_seqe = 'D'
     c                   eval      sstal6_bida = *loval
     c     *dmy          test(d)                 b2bida                 31
     c                   if        *in31 = *off
     c     *dmy          move      b2bida        sstal6_bida
     c                   endif
     c     sstal6_key    setll     sstal6
     c                   goto      end_pos
     c                   endif
      *
      * Fakturanummer
      *
     c                   if        b2binr <> 0
     c                   eval      w_seqe = 'F'
     c                   eval      sstal7_binr = b2binr
     c     sstal7_key    setll     sstal7
     c                   goto      end_pos
      *
     c                   endif
      *
      * Ordrenummer
      *
     c                   if        b2ornr <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      sstal8_ornr = b2ornr
     c     sstal8_key    setll     sstal8
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = 0
     c                   eval      b2tek1 = *blank
     c                   eval      b2kpro = 0
     c                   eval      b2bida = 0
     c                   eval      b2binr = 0
     c                   eval      b2ornr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise salgsdetaljer
      *
     c                   if        b1valg = 5
     c     *dmy          move      b1bida        w_bida
     c                   call      'SF510R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1binr
     c                   parm                    b1line
     c                   parm                    w_bida
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv fakturakopi
      *
     c                   if        b1valg = 6
     c                   exsr      skriv
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise faktura/bong
      *
     c                   if        b1valg = 7
     c                   exsr      vise
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
7.01  *
7.01  * Slå opp i arkiv.
7.01  *
7.01 c                   if        b1valg = 9
7.01  *
7.01  * Kontrollerer gyldig tilgang
7.01  *
7.01 c                   call      'AX700R'
7.01 c                   parm                    w_modu
7.01 c                   parm                    w_disp
7.01 c                   parm                    w_tilg
7.01  *
7.01 c                   if        w_tilg = '0'
7.01  *
7.01  * Hent hvilken kategori ordretype vi sjekker mot.
7.01  *
7.01 c                   eval      w_dtyp = *blanks
7.01 c                   eval      votyl1_otyp = b1otyp
7.01 c     votyl1_key    chain     votyl1
7.01 c                   if        not %found
7.01 c                   eval      vaoakk = 9
7.01 c                   endif
7.01  *
7.01 c                   if        vaosys = 0 and
7.01 c                             vaoakk = 3
7.01 c     *dmy          move      b1bida        w_date
7.01 c                   eval      w_aarr = %subdt(w_date:*years)
7.01 c                   eval      w_dtyp = 'FAKTURA'
7.01 c                   eval      w_refn = *blank
7.01 c                   eval      w_refn = %char(w_aarr) + %trim(%char(b1binr))
7.01  *
7.01 c                   call      'AP622C'
7.01 c                   parm                    w_dtyp
7.01 c                   parm                    w_refn
7.01 c                   endif
7.01  *
7.01 c                   endif
7.01  *
7.01 c                   eval      b1valg = 0
7.01 c                   update    b1sfl
7.01 c                   iter
7.01 c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   read      sstal4
     c                   when      w_seqe = 'A'
     c                   read      sstal5
     c                   when      w_seqe = 'D'
     c                   read      sstal6
     c                   when      w_seqe = 'F'
     c                   read      sstal7
     c                   when      w_seqe = 'O'
     c                   read      sstal8
     c                   other
     c                   read      sstal3
     c                   endsl
      *
     c                   if        %eof or                                      EOF
     c                             sffirm <> w_firm or
     c                             sfpaar <> w_paar or
     c                             sfvkun <> w_vkun
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        p_kpro <> 0 and
     c                             sfkpro <> p_kpro
     c                   iter
     c                   endif
      *
     c                   if        p_bida <> *loval and
     c                             sfbida <> p_bida
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      sstal4_tek1 = sftek1
     c                   when      w_seqe = 'A'
     c                   eval      sstal5_kpro = sfkpro
     c                   when      w_seqe = 'D'
     c                   eval      sstal6_bida = sfbida
     c                   when      w_seqe = 'F'
     c                   eval      sstal7_binr = sfbinr
     c                   when      w_seqe = 'O'
     c                   eval      sstal8_ornr = sfornr
     c                   other
     c                   eval      sstal3_vare = sfvare
     c                   eval      sstal3_kpro = sfkpro
     c                   eval      sstal3_bida = sfbida
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = sfline
     c                   eval      b1wsid = sfwsid
     c                   eval      b1vare = sfvare
     c                   eval      b1tek1 = sftek1
     c                   eval      b1binr = sfbinr
     c                   eval      b1ornr = sfornr
     c                   eval      b1bong = sfbong
     c                   eval      b1otyp = sfotyp
     c                   eval      b1anta = sfanta
     c                   eval      b1kpro = sfkpro
      *
     c                   eval      b1bida = 0
7.02 c                   eval      b1pnav = *blank
      *
     c                   if        sfbida <> *loval
     c     *dmy          move      sfbida        b1bida
     c                   endif
      *
     c                   eval      fkprl1_kund = sfvkun
     c                   eval      fkprl1_kpro = sfkpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
7.03 c                   eval      b1pnav = flnavn
7.03 c**                 eval      b1pnav = %char(sfkpro) + ' ' + flnavn
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'T'
     c     sstal4_key    setgt     sstal4
     c                   readp     sstal4
     c                   when      w_seqe = 'A'
     c     sstal5_key    setgt     sstal5
     c                   readp     sstal5
     c                   when      w_seqe = 'D'
     c     sstal6_key    setgt     sstal6
     c                   readp     sstal6
     c                   when      w_seqe = 'F'
     c     sstal7_key    setgt     sstal7
     c                   readp     sstal7
     c                   when      w_seqe = 'O'
     c     sstal8_key    setgt     sstal8
     c                   readp     sstal8
     c                   other
     c     sstal3_key    setgt     sstal3
     c                   readp     sstal3
     c                   endsl
      *
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   readp     sstal4
     c                   when      w_seqe = 'A'
     c                   readp     sstal5
     c                   when      w_seqe = 'D'
     c                   readp     sstal6
     c                   when      w_seqe = 'F'
     c                   readp     sstal7
     c                   when      w_seqe = 'O'
     c                   readp     sstal8
     c                   other
     c                   readp     sstal3
     c                   endsl
      *
     c                   if        %eof or                                      EOF
     c                             sffirm <> w_firm or
     c                             sfpaar <> w_paar or
     c                             sfvkun <> w_vkun
     c                   leave
     c                   endif
      *
     c                   if        p_kpro <> 0 and
     c                             sfkpro <> p_kpro
     c                   iter
     c                   endif
      *
     c                   if        p_bida <> *loval and
     c                             sfbida <> p_bida
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      sstal4_tek1 = sftek1
     c                   when      w_seqe = 'A'
     c                   eval      sstal5_kpro = sfkpro
     c                   when      w_seqe = 'D'
     c                   eval      sstal6_bida = sfbida
     c                   when      w_seqe = 'F'
     c                   eval      sstal7_binr = sfbinr
     c                   when      w_seqe = 'O'
     c                   eval      sstal8_ornr = sfornr
     c                   other
     c                   eval      sstal3_vare = sfvare
     c                   eval      sstal3_kpro = sfkpro
     c                   eval      sstal3_bida = sfbida
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'T'
     c     sstal4_key    setll     sstal4
     c                   when      w_seqe = 'A'
     c     sstal5_key    setll     sstal5
     c                   when      w_seqe = 'D'
     c     sstal6_key    setll     sstal6
     c                   when      w_seqe = 'F'
     c     sstal7_key    setll     sstal7
     c                   when      w_seqe = 'O'
     c     sstal8_key    setll     sstal8
     c                   other
     c     sstal3_key    setll     sstal3
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Vare
      *
     c                   if        w_afld = 'B2VARE'
     c                   movel     b2vare        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2vare
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
      *
     c                   if        p_bida <> *loval
     c                   eval      w_dato = p_bida
     c                   else
     c                   time                    w_dato
     c                   endif
      *
     c                   CALL      'FL501R'
     c                   parm                    w_firm
     c                   parm                    b2vkun
     c                   parm                    w_dato
     c                   parm                    b2kpro
     c                   parm                    w_pnav
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_sted
     c                   parm                    w_kprs
     c                   parm                    w_tlfa
     c                   parm                    w_tlfp
     c                   parm                    w_tlfm
     c                   parm                    w_mail
     c                   parm                    w_orka
     c                   parm                    w_okun
     c                   parm                    w_okpr
     c                   parm                    w_opda
     c                   parm                    w_proj
6.11 c                   parm                    w_kjor
6.11 c                   parm                    w_kjnr
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise faktura eller butikk-bong
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vise          begsr
      *
      * Dersom rad er faktura, kalles program for å vise faktura
      *
     c                   eval      sohel1_aarr = w_paar
     c                   eval      sohel1_binr = b1binr
     c                   eval      sohel1_numm = b1ornr
     c     sohel1_key    chain     sohel1
     c                   if        %found
      *
     c                   call      'SO510R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    p_vkun
     c                   parm                    b1binr
      *
     c                   leavesr
     c                   endif
      *
      * Dersom rad er bong/butikkfaktura, kalles program for å vise
      * bong/butikkfaktura
      *
     c                   eval      bohel1_aarr = w_paar
     c                   eval      bohel1_wsid = b1wsid
5.30 c                   eval      bohel1_bong = b1bong
     c     bohel1_key    chain     bohel1
     c                   if        %found
      *
     c                   call      'BO310R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1wsid
     c                   parm                    b1bong
      *
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive fakturakopi
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv         begsr
      *
      * Dersom rad er faktura, kalles program for å skrive fakturakopi
5.31  * For faktura kjørt i butikk skal bongnummer benyttes i stedet for
5.31  * ordrenummer dersom salget er gjort fra gammelt butikksystem
      *
     c                   eval      sohel1_aarr = w_paar
     c                   eval      sohel1_binr = b1binr
     c                   eval      sohel1_numm = b1ornr
5.31  *
     c                   if        b1bong <> *blank and
     c                             %scan('-':b1bong) = 0
5.31 c                   movel     b1bong        sohel1_numm
     c                   endif
5.31  *
     c     sohel1_key    chain     sohel1
     c                   if        %found
      *
     c                   call      'FO681R'
     c                   parm                    w_firm
     c                   parm                    w_paar
     c                   parm                    b1binr
     c                   parm                    b1otyp
      *
     c                   leavesr
     c                   endif
      *
     c                   endsr
5.31  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å eksportere data til excel
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     exl_eksport   begsr
6.32  * Kan ikke hvis ikke proa er aktivert
6.32 c                   if        b_pdfp = *on
6.32  *
6.32  * Hent inn xml templaten
6.32  *
6.32 c                   callp     GetHTMLIFS(d_xmlm :'<AS400>')
6.33 c                   callp     ClrHtmlBuffer
6.32  *
6.32  * Finn hvor xml'en skal legges
6.32  *
6.32 c                   eval      XML_path = *blanks
6.32 c                   eval      XML_path = %trim(d_path)
6.32  *
6.32  * Generer batchid
6.32  *
6.32 c                   exsr      pdf_batchno
6.32 c                   time                    w_date
6.32  *
6.32  * Vi logger ar eksport har startet
6.32  *
6.32 c                   eval      w_jstat = 10
6.32 c                   eval      w_jtext = 'Eksport av varestatistikk ha+
6.32 c                                       r startet'
6.32 c                   call      'AB705R'
6.32 c                   parm                    l_bach
6.32 c                   parm                    w_jstat
6.32 c                   parm                    w_jtext
6.32  * Vi skriver xml for miljø formater
6.32 c                   exsr      xml_envm
6.32 c                   exsr      xml_head
6.32  *
6.32  * Les alle postene vi skal
6.32  *
6.32 c     sstal3_key2   setll     sstal3
6.32 c                   read      sstal3
6.32  *
6.32 c                   dow       not %eof
6.32 c                   if        sffirm <> w_firm or
6.32 c                             sfpaar <> w_paar or
6.32 c                             sfvkun <> w_vkun
6.32 c                   leave
6.32 c                   endif
6.32  *
6.32 c*                  if        p_vkun <> 0 and
6.32 c*                            sfvkun <> p_vkun
6.32 c*                  leave
6 3x c*                  endif
6.32  *
6.32 c                   if        p_kpro <> 0 and
6.32 c                             sfkpro <> p_kpro
6.32 c                   leave
6.32 c                   endif
6.32  *
6.32 c                   if        p_bida <> *loval and
6.32 c                             sfbida <> p_bida
6.32 c                   leave
6.32 c                   endif
6.32  *
6.32 c                   exsr      xml_detal
6.32  *
6.32 c                   read      sstal3
6.32  *
6.32 c                   enddo
6.32  *
6.32  * Skriv xml fil til disk
6.32  *
6.32 c                   exsr      xml_end
6.32  *
6.32  * Kall forhåndsvisning av av pdf, for å få tilbake excel regneark
6.32  *
6.32 c                   exsr      pdf_preview
6.32  *
6.32 c                   endif
6.32  *
6.32 c                   endsr
6.32  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for etablering av batchnummer
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     pdf_batchno   begsr
6.32  *
6.32  * Vi starter med logging av jobb, og får tilbake unik jobbid.
6.32  * Først få jobbnummer
6.32  *
6.32 c                   eval      w_kode = '0'
6.32 c                   eval      w_firm = l_firm
6.32 c                   eval      w_fell = 'NXTKLI'
6.32 c                   eval      w_type = *blank
6.32 c                   eval      w_fast = *blank
6.32 c                   eval      w_numm = *zero
6.32  *
6.32 c                   call      'AS100R'
6.32 c                   parm                    w_kode
6.32 c                   parm                    w_firm
6.32 c                   parm                    w_fell
6.32 c                   parm                    w_type
6.32 c                   parm                    w_fast
6.32 c                   parm                    w_numm
6.32  *
6.32 c                   eval      w_jnav = 'PDF' + %char(w_numm)
6.32 c                   eval      w_jtxt = 'Utskrift av PDF'
6.32 c                   call      'AB700R'
6.32 c                   parm                    w_jnav
6.32 c                   parm                    w_jkey
6.32 c                   parm                    w_jtxt
6.32 c                   eval      l_bach = w_jkey
6.32  *
6.32 c     end_batchno   endsr
6.32  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32  * Subrutiner for XML/jasper integrasjon - Miljø variabler         *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32 c     xml_envm      begsr
6.32  *
6.32 c                   eval      w_rtyp = 'XsltCommand'
6.32 c                   eval      w_mime = 'application/vnd.ms-excel'
6.32 c                   eval      d_jasm = *blank
6.32  *
6.32  /Free
6.32       // Finne nytt batch nummer
6.32           UpdHTMLVar('BatchNo':               w_jkey);
6.32           UpdHTMLVar('Batchtype':            'EXCEL');
6.32           UpdHTMLVar('Username':              l_user);
6.32           WrtSection('Topp');
6.32       // Finn report command variabler
6.32           UpdHTMLVar('User':                        l_user);
6.32           UpdHTMLVar('PW':                             ' ');
6.32           UpdHTMLVar('IPAdr':                          ' ');
6.32           UpdHTMLVar('Schema':                'ADB'+l_filg);
6.32           UpdHTMLVar('Companyno':            %char(l_firm));
6.32           WrtSection('Credential');
6.32       // Finn report command variabler
6.32           UpdHTMLVar('Reportcommandtype':           w_rtyp);
6.32      //   UpdHTMLVar('Jaspertemplate':              d_jasm);
6.32           UpdHTMLVar('Logo':                           ' ');
6.32           UpdHTMLVar('Keepspool':                   'true');
6.32           UpdHTMLVar('Xsltschemapath':                 ' ');
6.32           UpdHTMLVar('Mimetype':                    w_mime);
6.32           WrtSection('ReportCommand');
6.32  /End-free
6.32  *
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32  * Subrutiner for XML integrasjon - Heading                        *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32 c     xml_head      begsr
6.32  *
6.32 c                   eval      aforl1_ruti = 'SF502    '
6.32 c     aforl1_key    chain     aforl1r
6.32  * Scann av rutinetekst
6.32 c                   eval      p_str_in = *blank
6.32 c                   eval      p_lr = '0'
6.32 c                   movel     aftxt1        p_str_in
6.32 c                   call      'AP613R'
6.32 c                   parm                    p_str_in
6.32 c                   parm                    p_str_out
6.32 c                   parm                    p_lr
6.32 c                   movel     p_str_out     w_txt1
6.32  *
6.32  * Vi må oversette aktuelle tekster til "xml" tegn - Firma
6.32 c                   eval      w_1fnav = *blank
6.32 c                   eval      w_1fad1 = *blank
6.32 c                   eval      w_1fad2 = *blank
6.32  *
6.32 c                   eval      p_str_in = *blank
6.32 c                   eval      p_lr = '0'
6.32 c                   movel     w_fnav        p_str_in
6.32 c                   call      'AP613R'
6.32 c                   parm                    p_str_in
6.32 c                   parm                    p_str_out
6.32 c                   parm                    p_lr
6.32 c                   movel     p_str_out     w_1fnav
6.32  *
6.32 c                   eval      p_str_in = *blank
6.32 c                   eval      p_lr = '0'
6.32 c                   movel     w_fad1        p_str_in
6.32 c                   call      'AP613R'
6.32 c                   parm                    p_str_in
6.32 c                   parm                    p_str_out
6.32 c                   parm                    p_lr
6.32 c                   movel     p_str_out     w_1fad1
6.32  *
6.32 c                   eval      p_str_in = *blank
6.32 c                   eval      p_lr = '0'
6.32 c                   movel     w_fad2        p_str_in
6.32 c                   call      'AP613R'
6.32 c                   parm                    p_str_in
6.32 c                   parm                    p_str_out
6.32 c                   parm                    p_lr
6.32 c                   movel     p_str_out     w_1fad2
6.32  *
6.32  /Free
6.32
6.32       // Skriv firmavariabler
6.32           UpdHTMLVar('Reporttext':            w_txt1);
6.32           UpdHTMLVar('Subheader':                ' ');
6.32           UpdHTMLVar('CompanyName':          w_1fnav);
6.32           UpdHTMLVar('Companyadress1':       w_1fad1);
6.32           UpdHTMLVar('Companyadress2':       w_1fad2);
6.32           UpdHTMLVar('Companypostalcode':        ' ');
6.32           UpdHTMLVar('Companypostoffice':     w_fste);
6.32           UpdHTMLVar('Companyphone':          w_ftlf);
6.32           UpdHTMLVar('Companyfax':            w_ffax);
6.32           UpdHTMLVar('Companyorgno':   %char(aafftn));
6.32           UpdHTMLVar('Companygiro':    %char(aafbgi));
6.32           UpdHTMLVar('Companyemail':          aamail);
6.32           UpdHTMLVar('Companywebpage': %trim(aaweba));
6.32
6.32           test(de) *dmy w_ddmy;
6.32           if %error;
6.32           tmpdate = *loval;
6.32           else;
6.32           tmpdate = %date(w_ddmy : *dmy);
6.32           endif;
6.32           UpdHTMLVar('Creationdate':   %char(tmpdate : *iso));
6.32
6.32           test(te) *hms w_tids;
6.32           if %error;
6.32           tmptime = *loval;
6.32           else;
6.32           tmptime = %time(w_tids : *hms);
6.32           endif;
6.32           UpdHTMLVar('Creationtime': %char(tmptime : *hms));
6.32           WrtSection('DocumentInformation');
6.32
6.32           WrtSection('FreedefHeader');
6.32
6.32  /End-free
6.32  *
6.32 c                   eval      h_valu='Kunde'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Kundenavn'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Prosjekt'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Navn'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Dato'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Fakturanr'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Ordrenr'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Ordretype'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Varenr'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Varetekst'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Antall'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Kost'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Sum brt'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Rabatt1'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Rabatt2'
6.32 c                   exsr      xml_head_val
6.32 c                   eval      h_valu='Sum nto'
6.32 c                   exsr      xml_head_val
6.32  *
6.32  /Free
6.32
6.32       // Avslutt headervalues
6.32           WrtSection('HeaderLineEnd');
6.32
6.32  /End-free
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32  * Subrutiner for XML integrasjon - Heading                        *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32 c     xml_head_val  begsr
6.32  *
6.32  /Free
6.32
6.32       // Skriv headervalue
6.32           UpdHTMLVar('Headervalue':  %trim(h_valu));
6.32           WrtSection('HeaderLineValue');
6.32
6.32  /End-free
6.32  *
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32  * Subrutiner for XML integrasjon - detaljlinjer                   *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32 c     xml_detal     begsr
6.32  *
6.32  /Free
6.32
6.32       // Skriv detaljer
6.32           WrtSection('DetailLineStart');
6.32
6.32           WrtSection('FreedefLine');
6.32
6.32  /End-free
6.32  *
6.32  * Så skriver vi de foskjellige linjene
6.32  *
6.32  * Kunde
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfvkun)
6.32 c                   exsr      xml_line_val
6.32  * Kundenavn
6.32  *
6.32 c                   eval      w_knav = *blanks
6.32 c                   eval      rkunl1_kund = sfvkun
6.32 c     rkunl1_key    chain     rkunl1
6.32 c                   if        %found
6.32 c                   eval      w_kknav = rknavn
6.32 c                   endif
6.32  *
6.32 c                   eval      p_str_in = *blank
6.32 c                   eval      p_lr = '0'
6.32 c                   movel     w_kknav       p_str_in
6.32 c                   call      'AP613R'
6.32 c                   parm                    p_str_in
6.32 c                   parm                    p_str_out
6.32 c                   parm                    p_lr
6.32 c                   movel     p_str_out     w_kknav
6.32  *
6.32 c                   eval      d_type='Char'
6.32 c                   eval      d_valu=%trim(w_kknav)
6.32 c                   exsr      xml_line_val
6.32  * Kundeprosjekt
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfkpro)
6.32 c                   exsr      xml_line_val
6.32  * Prosjektnavn
6.32 c                   eval      w_kpnav = *blanks
6.32 c                   eval      fkprl1_kund = sfvkun
6.32 c                   eval      fkprl1_kpro = sfkpro
6.32 c     fkprl1_key    chain     fkprl1
6.32 c                   if        %found
6.32 c                   eval      w_kpnav = flnavn
6.32 c                   endif
6.32  *
6.32 c                   eval      p_str_in = *blank
6.32 c                   eval      p_lr = '0'
6.32 c                   movel     w_kpnav       p_str_in
6.32 c                   call      'AP613R'
6.32 c                   parm                    p_str_in
6.32 c                   parm                    p_str_out
6.32 c                   parm                    p_lr
6.32 c                   movel     p_str_out     w_kpnav
6.32  * Navn
6.32 c                   eval      d_type='Char'
6.32 c                   eval      d_valu=%trim(w_kpnav)
6.32 c                   exsr      xml_line_val
6.32  * Dato
6.32 c                   if        sfbida <> *loval
6.32 c     *dmy          move      sfbida        w_fdat
6.32 c                   endif
6.32 c                   eval      d_type='Date'
6.32  /Free
6.32
6.32           UpdHTMLVar('type':         %trim(d_type));
6.32           test(de) *dmy w_fdat;
6.32           if %error;
6.32           tmpdate = *loval;
6.32           else;
6.32           tmpdate = %date(w_fdat : *dmy);
6.32           endif;
6.32           UpdHTMLVar('Linevalue':   %char(tmpdate : *iso));
6.32           WrtSection('DetailLineValue');
6.32
6.32  /End-free
6.32  *
6.32  * Fakturanr
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfbinr)
6.32 c                   exsr      xml_line_val
6.32  * Ordrenr
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfornr)
6.32 c                   exsr      xml_line_val
6.32  * Ordretype
6.32 c                   eval      d_type='Char'
6.32 c                   eval      d_valu=%trim(sfotyp)
6.32 c                   exsr      xml_line_val
6.32  * Varenummer
6.32 c                   eval      d_type='Char'
6.32 c                   eval      d_valu=%trim(sfvare)
6.32 c                   exsr      xml_line_val
6.32  * Varetekst
6.32 c                   eval      d_type='Char'
6.32 c                   eval      d_valu=%trim(sftek1)
6.32 c                   exsr      xml_line_val
6.32  * Antall
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfanta)
6.32 c                   exsr      xml_line_val
6.32  * Kost
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfsumk)
6.32 c                   exsr      xml_line_val
6.32  * Sum brutto
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfsumb)
6.32 c                   exsr      xml_line_val
6.32  * Rabatt 1
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfrab1)
6.32 c                   exsr      xml_line_val
6.32  * Rabatt 2
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(sfrab2)
6.32 c                   exsr      xml_line_val
6.32  * Netto
6.32 c                   eval      w_neto = sfsumb - sfrab1 - sfrab2 - sfrabo
6.32 c                   eval      d_type='Numeric'
6.32 c                   eval      d_valu=%char(w_neto)
6.32 c                   exsr      xml_line_val
6.32  *
6.32  /Free
6.32
6.32       // Skriv avslutning
6.32
6.32           WrtSection('DetailLineEnd');
6.32
6.32  /End-free
6.32  *
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32  * Subrutiner for XML integrasjon - Linjer                         *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32 c     xml_line_val  begsr
6.32  *
6.32  /Free
6.32
6.32       // Skriv linevalue
6.32           UpdHTMLVar('type':         %trim(d_type));
6.32           UpdHTMLVar('Linevalue':    %trim(d_valu));
6.32           WrtSection('DetailLineValue');
6.32
6.32  /End-free
6.32  *
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32  * Subrutiner for XML integrasjon - Slutt                          *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32 c     xml_end       begsr
6.32  *
6.32  /Free
6.32           WrtSection('EndExcelLines');
6.32  /End-free
6.32  *
6.32  /Free
6.32           WrtSection('Footer');
6.32  /End-free
6.32  *
6.32 c                   eval      XML_Output = XML_path + l_filg +'/'+
6.32 c                             'SF502_'+ %trim(w_jkey) + '.xml'
6.32  *
6.32 c                   callp     WrtHtmlToStmf(XML_Output: 819)
6.30  * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
6.30 c                   if        d_dtaq = '1'
6.30 c                   eval      p_xmlf = xml_output
6.30 c                   eval      p_filg = 'ADB'+l_filg
6.30 c                   call      'AP629C'
6.30 c                   parm                    p_filg
6.30 c                   parm                    p_xmlf
6.30 c                   endif
6.32  *
6.32 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.32 c     pdf_preview   begsr
6.32  * Vi kaller program for launch av PDF i browser
6.32 c                   call      'AP621R'
6.32 c                   parm                    l_bach
6.32  *
6.32 c                   endsr
6.32  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_paar
     c                   parm                    p_vkun
     c                   parm                    p_kpro
     c                   parm                    p_bida
      *
      * Key for posisjonering på kundenummer
      *
     c     sstal3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal3_vare
     c                   kfld                    sstal3_kpro
     c                   kfld                    sstal3_bida
6.32  *
6.32  * Key for posisjonering på statistikk
6.32  *
6.32 c     sstal3_key2   klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    w_paar
6.32 c                   kfld                    w_vkun
6.32 c                   kfld                    w_vare
6.32 c                   kfld                    w_kpro
6.32 c                   kfld                    w_bida
      *
      * Key for posisjonering på varetekst
      *
     c     sstal4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal4_tek1
      *
      * Key for posisjonering på kundeprosjekt
      *
     c     sstal5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal5_kpro
      *
      * Key for posisjonering på fakturadato
      *
     c     sstal6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal6_bida
      *
      * Key for posisjonering på fakturanummer
      *
     c     sstal7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal7_binr
      *
      * Key for posisjonering på ordrenummer
      *
     c     sstal8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    w_vkun
     c                   kfld                    sstal8_ornr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
      *
      * Key for oppslag på bong-hode-register
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
     c                   kfld                    bohel1_wsid
5.30 c                   kfld                    bohel1_bong
6.32  *
6.32  * Key for oppslag på firma
6.32  *
6.32 c     afirl1_key    klist
6.32 c                   kfld                    w_firm
6.32  *
6.32  * Key for rutineregisteret
6.32  *
6.32 c     aforl1_key    klist
6.32 c                   kfld                    aforl1_ruti
7.01  *
7.01  *  Key les av ordretype
7.01  *
7.01 c     votyl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    votyl1_otyp
      *
      * Henter firmakode, år og varekunde fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_paar = p_paar
     c                   eval      w_vkun = p_vkun
6.32 c                   eval      w_vare = *blanks
6.32 c                   eval      w_kpro = p_kpro
6.32 c                   eval      w_bdat = p_bida
      *
      * Henter verdi til skjermbilde
      *
     c                   eval      b2paar = w_paar
     c                   eval      b2vkun = p_vkun
     c                   eval      b2lad1 = p_kpro
      *
     c                   eval      rkunl1_kund = p_vkun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b2knav = rknavn
     c                   endif
      *
     c                   if        p_bida <> *loval
     c     *dmy          move      p_bida        b2bid1
     c                   endif
      *
      * Fyller subfile
      *
     c                   eval      *in22 = *on
6.32 c                   eval      *in40 = *off
      *
     c                   eval      sstal3_vare = *blank
     c                   eval      sstal3_kpro = 0
     c                   eval      sstal3_bida = *loval
     c     sstal3_key    setll     sstal3
      *
     c                   exsr      crt_subfile
6.32  *
6.32  * Sjekk om vi skal bruke JASPER og XML generering av dokumenter
6.32 c**                 if        l_poff = '1'
6.32 c                   eval      b_pdfp = *off
6.32 c                   eval      w_pdf = 0
6.32 c                   eval      w_pdf_ds = *blanks
6.32 c                   eval      w_ruti = 'SF502    '
6.32  *
6.32 c                   call      'AP609R'
6.32 c                   parm                    w_ruti
6.32 c                   parm                    w_pdf
6.32 c                   parm                    w_pdf_ds
6.32 c                   eval      d_pdf_ds = w_pdf_ds
6.32  * Vi skal kjøre med jasper
6.32 c                   if        w_pdf = 1 and
6.32 c                             %trim(d_xmlm) <> *blanks
6.32 c                   eval      b_pdfp = *on
6.32 c                   eval      *in40 = *on
6.32 c                   else
6.32 c                   eval      b_pdfp = *off
6.32 c                   endif
6.32  *
6.32  *  Hent inn firma/avd. opplysninger dersom
6.32  *  kode for det er satt i rutineregister
6.32  *
6.32 c**                 if        l_prfi = 1 or
6.32 c**                           l_prfi = 2
6.32 c**                 eval      w_prfi = l_prfi
6.32 c                   eval      w_prfi = 1
6.32 c                   eval      w_ffir = w_firm
6.32 c                   eval      w_avde = l_avde
6.32  *
6.32 c                   call      'FÅ707R'
6.32 c                   parm                    w_prfi
6.32 c                   parm                    w_ffir
6.32 c                   parm                    w_avde
6.32 c                   parm                    w_fnav
6.32 c                   parm                    w_fad1
6.32 c                   parm                    w_fad2
6.32 c                   parm                    w_fste
6.32 c                   parm                    w_fftx
6.32 c                   parm                    w_tfax
6.32 c                   parm                    w_ftlf
6.32 c                   parm                    w_ffax
6.32 c                   parm                    w_fmai
6.32 c                   parm                    w_weba
6.32 c**                 endif
6.32  *
6.32 c                   time                    w_dato2
6.32 c                   movel     w_dato2       w_tids            6 0
6.32 c                   move      w_dato2       w_ddmy
6.32  *
6.32 c**                 else
6.32 c**                 eval      b_pdfp = *off
6.32 c**                 endif
      *
     c                   endsr
