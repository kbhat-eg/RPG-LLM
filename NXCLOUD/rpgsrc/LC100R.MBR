      /TITLE  ASLAGR Lagertelling
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LC100R
      * Beskrivelse . . : Batch, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161199 ASK  Nytt program
      *       181108 ASK  Hennter ut tellelistenummer ved oppretting av Batc
      * 6.10  150609 BSA  Oppdatert med sporingsinformasjon
6.20  *       021214 NHO  Fra-til dato må tastes inn ved kopiering
6.20  *                   Sperrekode skal ikke kopieres
6.nu  * 6.30  170614 bof  endret ihht. nummerutvidelse
6.30  * 6.30  310317 bsa  Legger inn lager for filtrering
      *
7.xx  * 7.00  210117 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * K000  180520 bsa  Legger "DIVTEL" som default teller for diverse
7.01  *                   varenummer.
7.02  *K000   090321 ask  Lagt inn sjekk på at post finnes i sideregister (LBC1) før sletting
      *
8.01  *K0000  080323 bsa  Switchstyring av lager for filtrering.
8.02  *K0000  140623 bsa  Switchstyring av lager for filtrering. Skal også styre default
8.02  *K0000              setting av lagerverdi basrt på brukers lager.
8.03  * 800   141024 ask  Lagt inn filter funksjonalitet med eget bilde for utvalg (NXS-20133)
      *                   - switchstyring fra 8.01/8.02 brukes fortsatt
8.04  * 800   161024 ask  Lagt inn funksjoner
      *                   for kjøre hele telleprosessen fra dette bilde (NXS-20132)
8.05  * 800   221024 ask  Lagt inn test på at alle tellebunter er overført før div. valg
8.06  * 800   301024 ask  Lagt inn direkte tilgang til lagerverdiliste med batch
8.07  * 800   071124 ask  Lagt inn tilgangskontroll pr. valg
8.08  * 800   211124 ask  Viser bunter på tellebatc
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fanuml1    if   e           k disk    rename(anumpfr:anuml1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
8.02 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     flbchlr    if   e           k disk    rename(lbchpfr:lbchlrr)
     flbchlu    uf a e           k disk    rename(lbchpfr:lbchlur)
6.30 flbc1lu    uf a e           k disk    rename(lbc1pfr:lbc1lur)
6.30 flbc1l1    if   e           k disk    rename(lbc1pfr:lbc1l1r)
6.30 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
     flc100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(4) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
8.04 d lda            uds
     d l_user                911    920
8.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
8.04 d l_batc               1006   1015
8.04 d l_type               1016   1016
8.06  *
8.06  *  Parameter-rec
8.06  *
8.06 d                 ds
8.06 d p_prec                       128
8.06 d  p_fvar                        8  0 overlay(p_prec)
8.06 d  p_tvar                        8  0 overlay(p_prec:9)
8.06 d  p_flag                        2  0 overlay(p_prec:17)
8.06 d  p_tlag                        2  0 overlay(p_prec:19)
8.06 d  p_fogr                        2  0 overlay(p_prec:21)
8.06 d  p_togr                        2  0 overlay(p_prec:23)
8.06 d  p_fhgr                        2  0 overlay(p_prec:25)
8.06 d  p_thgr                        2  0 overlay(p_prec:27)
8.06 d  p_fugr                        3  0 overlay(p_prec:29)
8.06 d  p_tugr                        3  0 overlay(p_prec:32)
8.06 d  p_levn                        6  0 overlay(p_prec:35)
8.06 d  p_sakb                        4    overlay(p_prec:41)
8.06 d  p_sort                        1  0 overlay(p_prec:45)
8.06 d  p_type                        1  0 overlay(p_prec:46)
8.06 d  p_vlin                        1  0 overlay(p_prec:47)
8.06 d  p_batc                       10    overlay(p_prec:48)
8.06 d  p_dath                        6  0 overlay(p_prec:58)
8.06 d  p_gjko                        1  0 overlay(p_prec:64)
8.06 d  p_dumm                       10    overlay(p_prec:65)
      *
      * Definering av variabler i nøkler
      *
     d anuml1_fast     s                   like(anfast)
     d anuml1_numm     s                   like(anfell)
     d anuml1_type     s                   like(antype)
     d lusrl1_user     s                   like(lbuser)
     d lbchl1_batc     s                   like(lcbatc)
     d lbchlr_batc     s                   like(lcbatc)
     d lbchlu_batc     s                   like(lcbatc)
6.30 d ra10l1_jkod     s                   like(rajkod)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(lcfirm)
     d w_bdat          s                   like(lcbdat)
     d w_edat          s                   like(lcedat)
     d w_sist          s                   like(ansist)
     d w_type          s              2
     d w_fast          s             10
     d w_kode          s              1
6.nu d w_numm          s              8  0
     d w_fell          s              6
     d w_time_6        s              6  0
     d w_batc_gml      s                   like(lcbatc)
6.30 d w_ltxt          s             20
8.02 d w_lage          s              2  0
8.08 d w_buntot        s              5  0
8.08 d w_bunovf        s              5  0
8.06 d w_parm          s            128
8.05 d b_bunt          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Eksternt program
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   DCL-PR CO402R EXTPGM('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
8.01     p_verdi2            char(2048);
8.01   END-PR;
      *
     d u_lage          s              1    inz(*off)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1WIN  - C1 Vindu for å opprette Batch
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  A1 Sjekker om denne brukeren kan legge opp batchnummer         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.07  *
8.07  *  Henter opplysninger fra BRUKER-TABELL LAGR   - sjekk tilgang
8.07  *
8.07 c                   eval      lusrl1_user = l_user
8.07 c     lusrl1_key    chain     lusrl1                             90
8.07 c                   if        *in90 = *on
8.07 c                   eval      lbtans = 0
8.07 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1win
     c                   exsr      forny
     c                   goto      b2taga
8.03 c                   when      *inka
8.03 c                   exsr      xh1win
8.03 c                   eval      lbchl1_batc = *blank
8.03 c     lbchl1_key    setll     lbchl1
8.03 c                   exsr      forny
8.03 c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2batc <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
8.04 c                   eval      l_batc = '          '
8.04 c                   eval      l_type = ' '
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     lbchl1_key    setll     lbchl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på batch
      *
     c                   if        b2batc <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      lbchl1_batc = b2batc
     c     lbchl1_key    setll     lbchl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2batc = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
8.07 c                   if        lbtans < 2
8.07 c                   exfmt     a1win
8.07 c                   else
     c                   eval      c1batc = b1batc
     c                   exsr      xc2bld
8.07 c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = 3
8.07 c                   if        lbtans < 2
8.07 c                   exfmt     a1win
8.07 c                   else
     c                   eval      k1batc = b1batc
     c                   exsr      xk1win
     c                   exsr      xc2bld
8.07 c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
8.07 c                   if        lbtans < 2
8.07 c                   exfmt     a1win
8.07 c                   else
     c                   eval      d1batc = b1batc
     c                   exsr      xd1win
8.07 c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c1batc = b1batc
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
8.04  *
8.04  * Skrive tellelister
8.04  *
8.04 c                   if        b1valg = 6
8.07 c                   if        lbtans < 1
8.07 c                   exfmt     a1win
8.07 c                   else
8.04 c                   eval      l_batc = b1batc
8.04 c                   out       *dtaara
8.04 c                   call      'LE600R'
8.07 c                   endif
8.04 c                   eval      b1valg = 0
8.04 c                   update    b1sfl
8.04 c                   iter
8.04 c                   endif
8.04  *
8.04  * Arbeide med tellebunter
8.04  *
8.04 c                   if        b1valg = 7
8.04 c                   eval      l_batc = b1batc
8.04 c                   out       *dtaara
8.04 c                   call      'LU100R'
8.04 c                   eval      b1valg = 0
8.08 c                   eval      b_forn = *on
8.04 c                   update    b1sfl
8.04 c                   iter
8.04 c                   endif
8.04  *
8.04  * Vedlikehold telle liste
8.04  *
8.04 c                   if        b1valg = 8
8.07 c                   if        lbtans < 1
8.07 c                   exfmt     a1win
8.07 c                   else
8.04 c                   eval      l_batc = b1batc
8.04 c                   out       *dtaara
8.04 c                   call      'LE100R'
8.07 c                   endif
8.04 c                   eval      b1valg = 0
8.04 c                   update    b1sfl
8.04 c                   iter
8.04 c                   endif
8.04  *
8.04  * Utskrift av lageravvik
8.04  *
8.04 c                   if        b1valg = 9
8.07 c                   if        lbtans < 1
8.07 c                   exfmt     a1win
8.07 c                   else
8.05 c                   exsr      sjk_bunt
8.05 c                   if        b_bunt = *off
8.04 c                   eval      l_batc = b1batc
8.04 c                   out       *dtaara
8.04 c                   call      'LE610R'
8.05 c                   endif
8.07 c                   endif
8.04 c                   eval      b1valg = 0
8.04 c                   update    b1sfl
8.04 c                   iter
8.04 c                   endif
8.04  *
8.04  * Utskrift av lagerverdi
8.04  *
8.04 c                   if        b1valg = 10
8.07 c                   if        lbtans < 1
8.07 c                   exfmt     a1win
8.07 c                   else
8.05 c                   exsr      sjk_bunt
8.05 c                   if        b_bunt = *off
8.04 c                   eval      l_batc = b1batc
8.04 c                   eval      l_type = '2'
8.04 c                   out       *dtaara
8.06 c                   call      'LL605C'
8.05 c                   endif
8.07 c                   endif
8.04 c                   eval      b1valg = 0
8.04 c                   update    b1sfl
8.04 c                   iter
8.04 c                   endif
8.04  *
8.04  * Utskrift av diversevarer
8.04  *
8.04 c                   if        b1valg = 11
8.07 c                   if        lbtans < 1
8.07 c                   exfmt     a1win
8.07 c                   else
8.04 c                   eval      l_batc = b1batc
8.04 c                   out       *dtaara
8.04 c                   call      'LE620C'
8.07 c                   endif
8.04 c                   eval      b1valg = 0
8.04 c                   update    b1sfl
8.04 c                   iter
8.04 c                   endif
8.04  *
0.04  * Overføring til lager
8.04  *
8.04 c                   if        b1valg = 12
8.07 c                   if        lbtans < 2
8.07 c                   exfmt     a1win
8.07 c                   else
8.05 c                   exsr      sjk_bunt
8.05 c                   if        b_bunt = *off
8.04 c                   eval      l_batc = b1batc
8.04 c                   out       *dtaara
8.04 c                   call      'LE710R'
8.05 c                   endif
8.07 c                   endif
8.04 c                   eval      b1valg = 0
8.04 c                   update    b1sfl
8.04 c                   iter
8.04 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1win        begsr
      *
8.07 c                   if        lbtans < 2
8.07 c                   exfmt     a1win
8.07 c                   goto      end_c1win
8.07 c                   endif
      *
     c                   eval      c1batc = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1win
     c                   endsl
      *
      * Validering
      *
      *
     c                   if        c1batc = *blank
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      lbchlr_batc = c1batc
     c     lbchlr_key    chain     lbchlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      *in33 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   exsr      xc2bld
      *
     c     end_c1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
6.30  * Hent inn informasjon fra sidetabell (filtreringslager)
6.30 c                   eval      lbchlr_batc = c1batc
6.30 c     lbchlr_key    chain     lbc1l1
6.30  *
6.30 c                   if        %found
6.30 c                   eval      c2lage = l1lage
6.30 c                   else
6.30 c                   eval      c2lage = 0
8.02 c                   if        u_lage = *on and
8.02 c                             w_lage <> 0
8.02 c                   eval      c2lage = w_lage
8.02 c                   endif
6.30 c                   endif
      *
     c                   eval      lbchlr_batc = c1batc
     c     lbchlr_key    chain     lbchlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2nyen = a_meld(2)
     c                   eval      c2batc = lcbatc
     c                   eval      c2ansv = lcansv
6.20 c**                 eval      c2sper = lcsper
6.20 c                   if        b1valg = 3
6.20 c                   eval      lcsper = 0
6.20 c                   endif
     c                   eval      c2sper = lcsper
     c                   eval      c2stel = lcstel
     c                   eval      c2numm = lcnumm
     c                   eval      c2antb = lcantb
     c                   eval      c2ahte = lcahte
     c                   eval      c2lope = lclope
      *
     c                   eval      c2bdat = 0
     c                   eval      c2btim = 0
     c                   eval      c2edat = 0
     c                   eval      c2etim = 0
      *
     c                   if        lcbdat <> *loval
     c     *dmy          move      lcbdat        c2bdat
     c                   endif
      *
     c     *hms          move      lcbtim        w_time_6
     c                   eval      c2btim = w_time_6 / 100
      *
     c                   if        lcedat <> *loval
     c     *dmy          move      lcedat        c2edat
     c                   endif
      *
     c     *hms          move      lcetim        w_time_6
     c                   eval      c2etim = w_time_6 / 100
      *
     c                   else
      *
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c2batc = c1batc
     c                   eval      c2ansv = *blank
     c                   eval      c2bdat = *zero
     c                   eval      c2btim = *zero
     c                   eval      c2edat = *zero
     c                   eval      c2etim = *zero
     c                   eval      c2sper = *zero
     c                   eval      c2stel = *zero
     c                   eval      c2numm = *zero
     c                   eval      c2antb = *zero
     c                   eval      c2ahte = *zero
7.01 c*                  eval      c2lope = *blank
7.01 c                   eval      c2lope = 'DIVTEL'
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c1batc = k1batc
     c                   eval      c2batc = k1batc
6.20 c                   eval      c2bdat = k1bdat
6.20 c                   eval      c2edat = k1edat
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      c2nyen = a_meld(3)
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
6.30  * Sjekk om spørring
6.30 c                   if        *inkd and
6.30 c                             wactfe = 'C2LAGE'
6.30 c                   call      'RA510R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    c2lage
6.30 c                   parm                    w_ltxt
9.01 c                   goto      c2tagb
6.30 c                   endif
      *
      * Validering av input
      *
8.01 c                   if        u_lage = *on and
8.01 c                             c2lage = 0
8.01 c                   eval      *in43 = *on
8.01 c                   goto      c2tagb
8.01 c                   endif
      *
6.30 c                   if        c2lage <> 0
6.30 c                   eval      ra10l1_jkod = c2lage
6.30 c     ra10l1_key    chain     ra10l1
6.30 c                   if        not %found
6.30 c                   eval      *in42 = *on
6.30 c                   goto      c2tagb
6.30 c                   endif
6.30 c                   endif
      *
     c                   if        c2ansv = *blank
     c                   eval      *in40 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c     *dmy          test(d)                 c2bdat                 34
     c                   if        *in34 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      w_time_6 = c2btim * 100
     c     *hms          test(t)                 w_time_6               36
     c                   if        *in36 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c     *dmy          test(d)                 c2edat                 35
     c                   if        *in35 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      w_time_6 = c2etim * 100
     c     *hms          test(t)                 w_time_6               37
     c                   if        *in37 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2edat <> *zero
     c                   eval      w_bdat = *loval
     c                   if        c2bdat <> 0
     c     *dmy          move      c2bdat        w_bdat
     c                   endif
     c     *dmy          move      c2edat        w_edat
     c                   if        w_bdat > w_edat
     c                   eval      *in38 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Sjekk nummerserie for diversevarer
      *
     c                   eval      anuml1_numm = c2lope
     c                   eval      anuml1_type = *blank
     c                   eval      anuml1_fast = *blank
      *
     c     anuml1_key    setll     anuml1
     c                   read      anuml1                                 97
      *
     c                   if        anfell <> c2lope
     c                   eval      *in41 = *on
     c                   goto      c2tagb
     c                   endif
      *
      *
     c                   if        c2sper > 1
     c                   eval      *in39 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   lbchlur
      *
     c                   eval      lbchlu_batc = c1batc
     c     lbchlu_key    chain     lbchlur                            90
      *
      * Hent tellelistenummer ved ny batch
      *
     c                   if        *in90 = *on
     c                   eval      w_fell = laatel
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
     c                   eval      lcnumm = w_numm
     c                   endif
      *
     c                   eval      lcansv = c2ansv
     c                   eval      lcsper = c2sper
     c                   eval      lcstel = c2stel
     c                   eval      lcahte = c2ahte
     c                   eval      lclope = c2lope
      *
     c                   eval      lcbdat = *loval
     c                   eval      lcbtim = *loval
     c                   eval      lcedat = *loval
     c                   eval      lcetim = *loval
      *
     c                   if        c2bdat <> 0
     c     *dmy          move      c2bdat        lcbdat
     c                   endif
      *
     c                   eval      w_time_6 = c2btim * 100
     c     *hms          move      w_time_6      lcbtim
      *
     c                   if        c2edat <> 0
     c     *dmy          move      c2edat        lcedat
     c                   endif
      *
     c                   eval      w_time_6 = c2etim * 100
     c     *hms          move      w_time_6      lcetim
      *
6.10 c                   time                    lceeda
6.10 c                   time                    lceeti
6.10 c                   eval      lceusr = l_user
     c                   if        *in90 = *off
6.10 c                   time                    lceeda
6.10 c                   time                    lceeti
6.10 c                   eval      lceusr = l_user
     c                   update    lbchlur
     c                   else
     c                   eval      lcfirm = w_firm
     c                   eval      lcbatc = c1batc
6.10 c                   time                    lcodat
6.10 c                   time                    lcotim
6.10 c                   eval      lcousr = l_user
6.10 c                   time                    lceeda
6.10 c                   time                    lceeti
6.10 c                   eval      lceusr = l_user
     c                   write     lbchlur
     c                   endif
      *
      * Oppdater subfile
      *
6.30  * Oppdater sidetabell
6.30 c                   clear                   lbc1lur
6.30  *
6.30 c                   eval      lbchlu_batc = c1batc
6.30 c     lbchlu_key    chain     lbc1lur
6.30  *
6.30 c                   if        %found
6.30 c                   eval      l1lage = c2lage
6.30 c                   time                    l1edat
6.30 c                   time                    l1etim
6.30 c                   eval      l1eusr = l_user
6.30 c                   update    lbc1lur
6.30 c                   else
6.30 c                   eval      l1firm = w_firm
6.30 c                   eval      l1batc = c1batc
6.30 c                   eval      l1lage = c2lage
6.30 c                   time                    l1odat
6.30 c                   time                    l1otim
6.30 c                   eval      l1ousr = l_user
6.30 c                   time                    l1edat
6.30 c                   time                    l1etim
6.30 c                   eval      l1eusr = l_user
6.30 c                   write     lbc1lur
6.30 c                   endif
     c                   if        b1valg = 2
     c                   eval      b1batc = c2batc
     c                   eval      b1ansv = c2ansv
     c                   eval      b1bdat = c2bdat
     c                   eval      b1edat = c2edat
     c                   eval      b1sper = c2sper
     c                   eval      b1numm = lcnumm
     c                   eval      b1antb = ' '
     c                   eval      b1stel = c2stel
     c                   endif
      *
     c                   if        b1valg = 1 or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      lbchlr_batc = d1batc
     c     lbchlr_key    chain     lbchlr                             90
      *
     c                   eval      d1batc = lcbatc
     c                   eval      d1ansv = lcansv
     c                   eval      d1sper = lcsper
     c                   eval      d1numm = lcnumm
     c                   eval      d1antb = lcantb
      *
     c                   eval      d1bdat = 0
     c                   eval      d1btim = 0
     c                   eval      d1edat = 0
     c                   eval      d1etim = 0
      *
     c                   if        lcbdat <> *loval
     c     *dmy          move      lcbdat        d1bdat
     c                   endif
      *
     c     *hms          move      lcbtim        w_time_6
     c                   move      w_time_6      d1btim
      *
     c                   if        lcedat <> *loval
     c     *dmy          move      lcedat        d1edat
     c                   endif
      *
     c     *hms          move      lcetim        w_time_6
     c                   move      w_time_6      d1etim
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      lbchlu_batc = d1batc
     c     lbchlu_key    chain     lbchlur                            90
     c                   delete    lbchlur
6.30  *
6.30  * Sletter rad også i sidetabell
6.30  *
6.30 c                   eval      lbchlu_batc = d1batc
6.30 c     lbchlu_key    chain     lbc1lur                            90
7.02 c                   if        %found(lbc1lu)
6.30 c                   delete    lbc1lur
7.02 c                   endif
      *
      * Sletter rader i relaterte tabeller
      *
     c                   call      'LC400R'
     c                   parm                    w_firm
     c                   parm                    d1batc
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_batc_gml = k1batc
      *
      * Fyller kopierings-bildet
      *
     c                   eval      lbchlr_batc = k1batc
     c     lbchlr_key    chain     lbchlr                             90
      *
     c                   eval      k1batc = *blank
     c                   eval      k1ansv = lcansv
6.20 c**                 eval      k1sper = lcsper
     c                   eval      k1batf = w_batc_gml
      *
     c                   eval      k1bdat = 0
     c                   eval      k1btim = 0
     c                   eval      k1edat = 0
     c                   eval      k1etim = 0
      *
     c                   if        lcbdat <> *loval
6.20 c**   *dmy          move      lcbdat        k1bdat
     c                   endif
      *
     c     *hms          move      lcbtim        w_time_6
     c                   move      w_time_6      k1btim
      *
     c                   if        lcedat <> *loval
6.20 c**   *dmy          move      lcedat        k1edat
     c                   endif
      *
     c     *hms          move      lcetim        w_time_6
     c                   move      w_time_6      k1etim
      *
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1batc = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
6.20  *
6.20  * Sjekk dato fra/til
6.20  *
6.20 c     *dmy          test(d)                 k1bdat                 34
6.20 c                   if        *in34 = *on
6.20 c                   goto      k1tagb
6.20 c                   endif
6.20  *
6.20  *
6.20  *
6.20 c     *dmy          test(d)                 k1edat                 35
6.20 c                   if        *in35 = *on
6.20 c                   goto      k1tagb
6.20 c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      lbchlr_batc = k1batc
     c     lbchlr_key    chain     lbchlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1batc = w_batc_gml
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  H1 Behandle bilde for filter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
      * Henter inn filterinformasjon
      *
     c                   exfmt     h1win
      *
      * F3=Avslutt subrutinen
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      lbchl1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        lcfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
8.03  *
8.03  *  Sjekk filter
8.03  *
8.03 c                   if        h1lage <> 0
8.03 c                   eval      lbchlr_batc = lcbatc
8.03 c     lbchlr_key    chain     lbc1l1
8.03 c                   if        %found and
8.03 c                             l1lage <> 0
8.03 c                   if        h1lage <> l1lage
8.03 c                   iter
8.03 c                   endif
8.03 c                   endif
8.03 c                   endif
8.03  *
8.03 c                   if        h1open = 1 and
8.03 c                             lcsper = 1
8.03 c                   iter
8.03 c                   endif
8.03  *
8.03 c                   if        h1sykl = 1 and
8.03 c                             lcstel <> 1
8.03 c                   iter
8.03 c                   endif
8.03  *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1batc = lcbatc
     c                   eval      b1ansv = lcansv
     c                   eval      b1sper = lcsper
     c                   eval      b1stel = lcstel
8.08 c*                  eval      b1antb = lcantb
8.08 c                   exsr      tel_bunt
     c                   eval      b1numm = lcnumm
     c                   eval      b1bdat = 0
     c                   eval      b1edat = 0
      *
     c                   if        lcbdat <> *loval
     c     *dmy          move      lcbdat        b1bdat
     c                   endif
      *
     c                   if        lcedat <> *loval
     c     *dmy          move      lcedat        b1edat
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
8.05  *
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  *  Sjekk at alle tellebunter er overført på denne batch
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  *
8.05 c     sjk_bunt      begsr
8.05  *
8.05 c                   eval      b_bunt = *off
8.05         exec sql
8.05           select count(*)
8.05               into :w_buntot
8.05             from lbhepf
8.05             where lubatc = :b1batc and
8.05                   lukode = 0;
8.05
8.05 c                   if        w_buntot > 0
8.05 c                   exfmt     a2win
8.05 c                   eval      b_bunt = *on
8.05 c                   endif
8.05  *
8.05 c                   endsr
8.08  *
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  *  Teller bunter og overførte bunter på batc
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  *
8.08 c     tel_bunt      begsr
8.08  *
8.08         exec sql
8.08           select count(*)
8.08               into :w_buntot
8.08             from lbhepf
8.08             where lubatc = :b1batc;
8.08
8.08         exec sql
8.08           select count(*)
8.08               into :w_bunovf
8.08             from lbhepf
8.08             where lubatc = :b1batc and
8.08                   lukode = 1;
8.08
8.08         b1antb = %trim(%char(w_bunovf)) + ' av ' + %trim(%char(w_buntot));
8.08  *
8.08 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.04  *
8.04  * Definisjon av local data area for output
8.04  *
8.04 c     *dtaara       define    *lda          lda
      *
      * Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      * Key for les av BATCH
      *
     c     lbchlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchlr_batc
      *
      * Key for oppdatering av BATCH
      *
     c     lbchlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchlu_batc
      *
      *  Key for les av NUMMER
      *
     c     anuml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anuml1_numm
     c                   kfld                    anuml1_type
     c                   kfld                    anuml1_fast
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BRUKER
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
6.30  *
6.30  *  Key for les av lager
6.30  *
6.30 c     ra10l1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ra10l1_jkod
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      *  Hent opplysninger fra LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
      *  Hent opplysninger fra NUMMER
      *
     c                   eval      anuml1_numm = laatbu
     c                   eval      anuml1_type = *blank
     c                   eval      anuml1_fast = *blank
     c     anuml1_key    chain     anuml1                             90
     c                   if        *in97 = *off
     c                   eval      w_sist = ansist
     c                   endif
8.01  *
8.01  * Endring 8.01
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'LC100_LAGERFILTER':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_lage = *on;
8.01   endif;
      *
8.03  *
8.03  *  Henter opplysninger fra BRUKER-TABELL
8.03  *
8.03 c                   eval      w_lage = 0
8.03 c     l_user        chain     ausrl1
8.03 c                   if        %found
8.03 c                   eval      w_lage = ablagr
8.03 c                   endif
      *
      *
      * Posisjonerer databasen, og fyller opp subfile
8.03  * Setter startverdier for filter bilde
      *
8.03 c                   eval      h1lage = w_lage
8.03 c                   eval      h1sykl = 0
8.03 c                   eval      h1open = 1
     c                   eval      lbchl1_batc = *blank
     c     lbchl1_key    setll     lbchl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
