     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSHOP
      * Program . . . . : BS510R
      * Beskrivelse . . : ButikkLinjeSlettet, vis
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       021104 HKO  Nytt program
7.xx  * 7.00  200516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbbsli2    if   e           k disk    rename(bbslst:bbsli2r)
     fbbsli3    if   e           k disk    rename(bbslst:bbsli3r)
     fbbsli4    if   e           k disk    rename(bbslst:bbsli4r)
     fbbsli5    if   e           k disk    rename(bbslst:bbsli5r)
      *
     fbs510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d bbsli2_sdat     s                   like(bssdat)
     d bbsli3_susr     s                   like(bssusr)
     d bbsli4_ornr     s                   like(bsornr)
     d bbsli5_tref     s                   like(bstref)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(bsfirm)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(15)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2sdat <> 0 or
     c                             b2susr <> *blank or
     c                             b2ornr <> 0 or
     c                             b2tref <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'U'
     c                   eval      bbsli3_susr = b1susr
     c     bbsli3_key    setll     bbsli3
     c                   when      w_seqe = 'O'
     c                   eval      bbsli4_ornr = b1ornr
     c     bbsli4_key    setll     bbsli4
     c                   when      w_seqe = 'T'
     c                   eval      bbsli5_tref = b1tref
     c     bbsli5_key    setll     bbsli5
     c                   other
     c     *dmy          move      b1sdat        bbsli2_sdat
     c     bbsli2_key    setll     bbsli2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Slettet dato
      *
     c                   if        b2sdat <> 0
     c                   eval      w_seqe = *blank
     c     *dmy          test(d)                 b2sdat                 40
     c                   if        *in40 = *off
     c     *dmy          move      b2sdat        bbsli2_sdat
     c     bbsli2_key    setll     bbsli2
     c                   endif
     c                   goto      end_pos
     c                   endif
      *
      * Slette av
      *
     c                   if        b2susr <> *blank
     c                   eval      w_seqe = 'U'
     c                   eval      bbsli3_susr = b2susr
     c     bbsli3_key    setll     bbsli3
     c                   goto      end_pos
     c                   endif
      *
      * Ordrenr
      *
     c                   if        b2ornr <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      bbsli4_ornr = b2ornr
     c     bbsli4_key    setll     bbsli4
     c                   goto      end_pos
     c                   endif
      *
      * Terminal-referanse
      *
     c                   if        b2tref <> 0
     c                   eval      w_seqe = 'T'
     c                   eval      bbsli5_tref = b2tref
     c     bbsli5_key    setll     bbsli5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2sdat = 0
     c                   eval      b2susr = *blank
     c                   eval      b2ornr = 0
     c                   eval      b2tref = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'U'
     c                   read      bbsli3
     c                   when      w_seqe = 'O'
     c                   read      bbsli4
     c                   when      w_seqe = 'T'
     c                   read      bbsli5
     c                   other
     c                   read      bbsli2
     c                   endsl
      *
     c                   if        %eof or
     c                             bsfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'U'
     c                   eval      bbsli3_susr = bssusr
     c                   when      w_seqe = 'O'
     c                   eval      bbsli4_ornr = bsornr
     c                   when      w_seqe = 'T'
     c                   eval      bbsli5_tref = bstref
     c                   other
     c                   eval      bbsli2_sdat = bssdat
     c                   endsl
      *
     c     *dmy          move      bssdat        b1sdat
     c                   eval      b1susr = bssusr
     c                   eval      b1stim = bsstim
     c                   eval      b1kass = bskass
     c                   eval      b1anta = bsanta
     c                   eval      b1enhe = bsenhe
     c                   eval      b1ntpr = bsntpr
     c                   eval      b1ornr = bsornr
     c                   eval      b1tref = bstref
     c                   eval      b1kode = 'S'
      *
     c                   if        bsrant <> 0
     c                   eval      b1kode = 'A'
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'U'
     c     bbsli3_key    setgt     bbsli3
     c                   readp     bbsli3
     c                   when      w_seqe = 'O'
     c     bbsli4_key    setgt     bbsli4
     c                   readp     bbsli4
     c                   when      w_seqe = 'T'
     c     bbsli5_key    setgt     bbsli5
     c                   readp     bbsli5
     c                   other
     c     bbsli2_key    setgt     bbsli2
     c                   readp     bbsli2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'U'
     c                   readp     bbsli3
     c                   when      w_seqe = 'O'
     c                   readp     bbsli4
     c                   when      w_seqe = 'T'
     c                   readp     bbsli5
     c                   other
     c                   readp     bbsli2
     c                   endsl
      *
     c                   if        %eof or
     c                             bsfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'U'
     c                   eval      bbsli3_susr = bssusr
     c                   when      w_seqe = 'O'
     c                   eval      bbsli4_ornr = bsornr
     c                   when      w_seqe = 'T'
     c                   eval      bbsli5_tref = bstref
     c                   other
     c                   eval      bbsli2_sdat = bssdat
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'U'
     c     bbsli3_key    setll     bbsli3
     c                   when      w_seqe = 'O'
     c     bbsli4_key    setll     bbsli4
     c                   when      w_seqe = 'T'
     c     bbsli5_key    setll     bbsli5
     c                   other
     c     bbsli2_key    setll     bbsli2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på slettet dato
      *
     c     bbsli2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbsli2_sdat
      *
      * Key for posisjonering på slettet av
      *
     c     bbsli3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbsli3_susr
      *
      * Key for posisjonering på ordrenr
      *
     c     bbsli4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbsli4_ornr
      *
      * Key for posisjonering på terminal-referanse
      *
     c     bbsli5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbsli5_tref
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      bbsli2_sdat = *hival
     c     bbsli2_key    setll     bbsli2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
