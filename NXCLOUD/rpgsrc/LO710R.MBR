     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO710R
      *  Beskrivelse . . : Vise informasjon om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  ------------------------------------------------
6.nu  *  R6.30     120614  Sjekket mtp. nummerutvidelse            BOF
7.00  *  K00       220420  Utvidet skjermbile                      bsa  *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flo710d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  X1 Behandler opplysning om hvem som holder ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      x1dumy = *blank
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c     lohel1_key    chain     lohel1r
B001 c                   if        %found
 001  *
 001 c                   eval      x1kous = lokous
 001 c                   eval      x1kows = lokows
 001  *
B002 c                   if        lokoda <> *loval
 002 c     *dmy          move      lokoda        x1koda
E002 c                   endif
 001  *
 001 c     *hms          move      lokoti        x1koti
 001  *
E001 c                   endif
      *
     c                   exfmt     x1bld
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Key for les av BESTILLINGS-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
      *
     c                   endsr
