     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LD512R
      * Beskrivelse . . : Spørrring på ordre/bestilling (Datokontroll)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.20  * R6.20 270410 bof  Tester om bruker har lov til å endre bestilling
6.nu  * R6.30 030614 bsa  Sjekket mht. nummerutvidelse
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtll    if   e           k disk    rename(fodtpfr:fodtllr)
     flodtlc    if   e           k disk    rename(lodtpfr:lodtlcr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fld512d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
6.20  * Definering av array
6.20  *
6.20 d a_meld          s             50    dim(1) ctdata perrcd(1)
      *
      * Definering av parametre
      *
     d p_kund          s                   like(fdkund)
6.nu d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
      *
      * Definering av Local data area
      *
     d                uds
6.20 d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fodtll_kund     s                   like(fdkund)
     d fodtll_numm     s                   like(fdnumm)
     d fodtll_suff     s                   like(fdsuff)
     d fodtll_line     s                   like(fdline)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d lodtlc_ornu     s                   like(ldornu)
     d lodtlc_orsu     s                   like(ldorsu)
     d lodtlc_orli     s                   like(ldorli)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d rkunl1_kund     s                   like(rkkund)
6.20 d fusrl1_user     s                   like(fbuser)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(fdfirm)
     d w_kund          s                   like(fdkund)
6.nu d w_numm          s                   like(fdnumm)
     d w_suff          s                   like(fdsuff)
     d w_pnum          s                   like(fdnumm)
     d w_psuf          s                   like(fdsuff)
     d w_pval          s              2  0
6.20 d w_mld1          s             50
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *             størrelse og utseende.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
      *                  if        b2numm <> 0
      *                  exsr      posisjoner
      *                  goto      b2tagb
      *                  endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overgang til bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bestil        begsr
      *
     c                   eval      w_pnum = b1numm_b
     c                   eval      w_psuf = b1suff_b
     c                   if        w_pnum <> 0
      *
     c                   if        b1valg = '2'
6.20 c                   if        fbko15 = 0
6.20 c                   eval      w_mld1 = a_meld(1)
6.20 c                   call      'AA005R'
6.20 c                   parm                    w_mld1
6.20 c                   leavesr
6.20 c                   endif
     c                   eval      w_pval = 02
     c                   call      'LO110R'
     c                   parm                    w_pval
     c                   parm                    w_pnum
     c                   parm                    w_psuf
     c                   endif
      *
     c                   if        b1valg = '5'
     c                   call      'LO505R'
     c                   parm                    w_firm
     c                   parm                    w_pnum
     c                   parm                    w_psuf
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     fodtll_key    setll     fodtll
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *    posisjoner    begsr
      *
      * Posisjoner startverdi på ordrenummer
      *
      *                  if        b2numm <> 0
      *                  eval      fodtll_numm = b2numm
      *    fodtll_key    setll     fodtll
      *                  goto      end_pos
      *                  endif
      *
      *    end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c*                  eval      *in22 = *off
      *
      *                  exsr      clr_subfile
      *                  exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
      *                  eval      b2numm = 0
      *
      *                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Overgang til bestilling
      *
     c                   if        b1valg = '2' or
     c                             b1valg = '5'
     c                   exsr      bestil
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   eval      *in31  = *off
     c                   eval      *in32  = *off
     c                   eval      *in33  = *off
     c                   read      fodtll                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        fdfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        fdkund <> w_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        fdnumm <> w_numm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        fdsuff <> w_suff
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        fdvare = *blank
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
      * Hente inn ordrehode-opplysninger
      *
     c                   eval      fohel1_numm = fdnumm
     c                   eval      fohel1_suff = fdsuff
     c     fohel1_key    chain     fohel1                             90
     c                   if        *in90
     c                   eval      fobdat = *loval
     c                   eval      foldat = *loval
     c                   eval      footyp = *blank
     c                   eval      fonavn = *blank
     c                   eval      foadr1 = *blank
     c                   eval      fosted = *blank
     c                   eval      fodref = *blank
     c                   endif
      *
      * Snu dato
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        b1bdat_o
     c                   endif
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        b1ldat_o
     c                   endif
      *
     c                   if        fdldat <> *loval
     c     *dmy          move      fdldat        b1ldat_o
     c                   endif
      *
      * Ta vare på leveringsadresse og kundens ref,
      * dersom det er første post i subfile som behandles
      *
     c     w_srrn        ifeq      1
     c                   eval      b3navn = fonavn
     c                   eval      b3gate = foadr1
     c                   eval      b3sted = fosted
     c                   eval      b2dref = fodref
     c                   eval      b2otyp = footyp
     c                   endif
      *
     c                   eval      b1valg = *blank
     c                   eval      b1vare = fdvare
     c                   eval      b1anta = fdanta
      *
      * Hent inn bestilling dersom den finnes
      *
     c                   exsr      hent_best
      *
      * Sjekke / varsle dato
      *
     c                   if        foldat <> *loval
     c                   if        loldat <> *loval
     c                   if        loldat >  foldat
     c                   eval      *in31   = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        foldat <> *loval
     c                   if        lomoda <> *loval
     c                   if        lomoda >  foldat
     c                   eval      *in32   = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_best     begsr
      *
      * Slå opp mot bestillings-linje-tabell
      *
     c                   eval      b1otyp_b = *blank
     c                   eval      b1numm_b = *zero
     c                   eval      b1suff_b = *zero
     c                   eval      b1bdat_b = *zero
     c                   eval      b1ldat_b = *zero
     c                   eval      b1moda_b = *zero
      *
     c                   eval      lodtlc_ornu = fdnumm
     c                   eval      lodtlc_orsu = fdsuff
     c                   eval      lodtlc_orli = fdline
      *
     c     lodtlc_key    chain     lodtlcr                            90
     c                   if        *in90
     c                   goto      end_best
     c                   endif
      *
      *
      * Slå opp mot bestillings-hode-tabell
      *
     c                   eval      lohel1_numm = ldnumm
     c                   eval      lohel1_suff = ldsuff
      *
     c     lohel1_key    chain     lohel1r                            90
     c                   if        *in90
     c                   goto      end_best
     c                   endif
      *
     c                   move      lonumm        b1numm_b
     c                   move      losuff        b1suff_b
     c                   move      lootyp        b1otyp_b
      *
      * Snu dato
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        b1bdat_b
     c                   endif
      *
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        b1ldat_b
     c                   endif
      *
     c                   if        lomoda <> *loval
     c     *dmy          move      lomoda        b1moda_b
     c                   endif
      *
     c                   if        ldldat <> *loval
     c     *dmy          move      ldldat        b1moda_b
     c                   endif
      *
      * Sjekk om bestillingsnummer matcher riktig
      *
     c                   if        ldnumm <> fdbenr
     c                   eval      *in33  =  *on
     c                   endif
      *
     c     end_best      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kund
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for lesing av ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for posisjonering på ordrenummer
      *
     c     fodtll_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtll_kund
     c                   kfld                    fodtll_numm
     c                   kfld                    fodtll_suff
     c                   kfld                    fodtll_line
      *
      * Key for oppslag på bestillings-linje-register
      *
     c     lodtlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlc_ornu
     c                   kfld                    lodtlc_orsu
     c                   kfld                    lodtlc_orli
      *
      * Key for oppslag på bestillings-hode-register
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for lesing av kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
6.20  *
6.20  * Key for brukerregister
6.20  *
6.20 c     fusrl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    fusrl1_user
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_kund = p_kund
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      b2kund = p_kund
     c                   eval      b2numm = p_numm
     c                   eval      b2suff = p_suff
     c                   eval      *in22  = *on
6.20  *
6.20  * Hente inn brukerregister
6.20  *
6.20 c                   eval      fusrl1_user = l_user
6.20 c     fusrl1_key    chain     fusrl1r                            90
6.20 c                   if        fbko15 =  1
6.20 c                   eval      *in50  = *on
6.20 c                   endif
      *
      * Henter inn kundeopplysninger
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90
     c                   eval      rknavn = *blank
     c                   eval      rkgate = *blank
     c                   eval      rksted = *blank
     c                   eval      rktlfn = *blank
     c                   endif
      *
     c                   eval      b2navn = rknavn
     c                   eval      b2gate = rkgate
     c                   eval      b2sted = rksted
     c                   eval      b2tlfn = rktlfn
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fodtll_kund = p_kund
     c                   eval      fodtll_numm = p_numm
     c                   eval      fodtll_suff = p_suff
     c                   eval      fodtll_line = 0
     c     fodtll_key    setll     fodtll
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**     M e l d i n g e r
Bruker har ikke lov til å endre bestilling        <---
