     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FA931R
      * Beskrivelse . . : Oppslag mot Bisnode med webservice - private
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290916 ASK  Nytt program
8.01  * 800   251020 ASK  Fjernet splitting av for og etternavn
8.02  * 800   021221 ASK  Setter kreditgrenser basert på koder i koderegister
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d p_pnum          s             11
8.01 d p_navn          s             30
     d p_adr1          s             30
     d p_adr2          s             30
     d p_ponr          s              4  0
     d p_pste          s             30
     d p_tele          s             11
     d p_mobi          s             11
8.02 d p_kgre          s              8  0
     d p_fdat          s             10
      *
     d XML_Input       s            256a   Varying
     d XML_Output1     s            256a   Varying
     d XML_Output2     s            256a   Varying
     d XML_Output3     s            256a   Varying
     d XML_path        s            256a
     d w_xmlp          s            100
     d options         s            200a   Varying
      *
     d w_url           s            200
     d w_reqf          s            100
     d w_rspf          s            100
     d w_xmlf          s            100    Varying
     d w_usrn          s             20
     d w_pass          s             50
     d w_usrk          s             20
     d w_pask          s             50
     d w_susr          s             10
     d w_likn          s             25
     d w_stat          s              1  0
     d w_autt          s              5
     d w_intr          s             15
     d w_navn          s             50
     d w_adrs          s             50
     d w_ponr          s              4
     d w_pste          s             25
     d w_besl          s             20
     d w_scor          s              2
     d w_scorn         s              2  0
     d w_grav          s              2
     d w_grgk          s              2
     d w_innt          s             10
     d w_fdat          s             10
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_leng          s              2  0
     d w_start         s              2  0
     d w_slutt         s              2  0
     d w_tegn          s              1
      *
     d w_firm          s              3  0
8.02 d w_ksc1          s              3  0
8.02 d w_ksc2          s              3  0
8.02 d w_ksc3          s              3  0
8.02 d w_ksc4          s              3  0
8.02 d w_ksc5          s              3  0
8.02 d w_kgr1          s              8  0
8.02 d w_kgr2          s              8  0
8.02 d w_kgr3          s              8  0
8.02 d w_kgr4          s              8  0
8.02 d w_kgr5          s              8  0
      *
      *
      * Definering av konstanter
      *
      *
      /copy prototypeb
      /copy usec
      *
      * Datastruktur for XML fra PersonSok WS
      *
     d Personalia      ds                  qualified
     d   Internref                   15a
     d   StatusDato                  10a
     d   Fodselsdato                 10a
     d   Navn                        50a
     d   Adresse                     50a
     d   Postnr                       4a
     d   Poststed                    25a
     d   Kommune                     30a
     d   Fylke                       20a
     d   Alder                        3a
     d   Kjonn                        6a
      *
      * Datastruktur for XML fra PersonInfo WS
      *
     d Score           ds                  qualified
     d   Internref                   15a
     d   Beslutning                  20a
     d   Score                        2a
     d   GrenseAvslag                 2a
     d   GrenseGodkjent...
     d                                2a
     d   Inntekt                     10a
      *
     d Ligning         ds                  qualified
     d   Beslutning                  20a
      *
      * Oppslag mot switch register for å hente info
      *
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;

8.02
8.02 C/Exec SQL
8.02 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
8.02 C/End-Exec
8.02
      *
      * Hovedprogram:
      *
      *  - slår opp med personnummer
      *  - mottar internreferanse og person data
      *  - henter likning og rating basert på internreferanse
      *  - beregner kridit grense ut i fra mottatt informasjon
      *
      * ------------------------------------------------------------------------
      * Initierer, bygger requestfiler fra PersonSokMal og kjører webservice
      * ------------------------------------------------------------------------
      *
      * Params file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/PersonSokMal.txt.params'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
     c                   eval      XML_Output1 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/PersonSok' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt.params'
      /Free
           // Ingen parametre på denne filen
               WrtSection('PsokParams');

      /End-free
      *
     c                   callp     WrtHtmlToStmf(XML_Output1: 819)
      *
      * Headers file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/PersonSokMal.txt.headers'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   eval      w_usrn = w_usrk
     c                   eval      w_pass = w_pask
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
      /Free
           // Legge inn personnumer fra parameter
               UpdHTMLVar('usrid':                  w_usrn);
               UpdHTMLVar('passw':                  w_pass);
               WrtSection('PsokHeader');

      /End-free
      *
     c                   eval      XML_Output2 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/PersonSok' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt.headers'
      *
     c                   callp     WrtHtmlToStmf(XML_Output2: 819)
      *
      * Body file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/PersonSokMal.txt'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
      /Free
           // Legge inn personnumer fra parameter
               UpdHTMLVar('pnumm':                  p_pnum);
               WrtSection('Psok');

      /End-free
      *
     c                   eval      XML_Output3 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/PersonSok' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt'
     c                   eval      XML_Input  = '/home/' +
     c                                          l_figr +
     c                                          '/Bisnode/Bisnode_ws' +
     c                                          '/PersonSokResp' +
     c                                          %trim(%char(w_numm)) +
     c                                          '.txt'
      *
     c                   callp     WrtHtmlToStmf(XML_Output3: 819)
      *
      * Initierer og kaller webservice for PersonSok
      *
     c                   eval      w_url = 'https://dbonline.no' +
     c                                     '/webservices/service/PersonInfo'
     c                   eval      w_usrn = *blank
     c                   eval      w_pass = *blank
     c                   eval      w_autt = *blank
     c                   eval      w_reqf = XML_Output3
     c                   eval      w_rspf = XML_Input
     c                   eval      w_stat = 0
      *
     c                   call      'AW703C'
     c                   parm                    w_url
     c                   parm                    w_reqf
     c                   parm                    w_rspf
     c                   parm                    w_usrn
     c                   parm                    w_pass
     c                   parm                    w_autt
     c                   parm                    w_stat

     c                   callp     unlink(XML_Output1)
     c                   callp     unlink(XML_Output2)
     c                   callp     unlink(XML_Output3)
      *
      * ------------------------------------------------------------------------
      * Leser mottatt svar på PersonSok og tar vare på mottatte data
      * ------------------------------------------------------------------------
      *
      /Free
             options  = 'doc=file +
                                path=PersonSokResponse/Personalia +
                                case=any +
                                ns=remove +
                                allowextra=yes +
                                allowmissing=yes';
             w_xmlf = %trim(w_rspf);

                   xml-into Personalia %xml(w_xmlf: options);

      /End-free
      *
      /Free

             w_intr  = Personalia.Internref;
             w_navn  = Personalia.Navn;
             w_adrs  = Personalia.Adresse;
             w_ponr  = Personalia.Postnr;
             w_pste  = Personalia.Poststed;
             w_fdat  = Personalia.Fodselsdato;

      /End-free
      *
      * Editerer parameter felter for retur til hovedprogram
      *
8.01 c                   eval      p_navn = w_navn
     c                   eval      p_adr1 = w_adrs
     c                   eval      p_adr2 = *blank
     c                   eval      p_ponr = %dec(w_ponr:4:0)
     c                   eval      p_pste = w_pste
     c                   eval      p_tele = *blank
     c                   eval      p_mobi = *blank
     c                   eval      p_fdat = w_fdat
      *
     c                   callp     unlink(XML_Input)
      *
      * ------------------------------------------------------------------------
      * Initierer, bygger requestfiler fra PersoninfoMal og kjører webservice
      * ------------------------------------------------------------------------
      *
      * Params file
      *
     c     testut        tag
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/Personinfomal.txt.params'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
     c                   eval      XML_Output1 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/Personinfo' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt.params'
      *
      /Free
           // Ingen parametre på denne filen
               WrtSection('PinfoParams');

      /End-free
      *
     c                   callp     WrtHtmlToStmf(XML_Output1: 819)
      *
      * Headers file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/PersoninfoMal.txt.headers'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
     c                   eval      XML_Output2 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/Personinfo' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt.headers'
      *
     c                   eval      w_usrn = w_usrk
     c                   eval      w_pass = w_pask
      *
      /Free
           // Legge inn personnumer fra parameter
               UpdHTMLVar('usrid':                  w_usrn);
               UpdHTMLVar('passw':                  w_pass);
               WrtSection('PinfoHeader');

      /End-free
      *
     c                   callp     WrtHtmlToStmf(XML_Output2: 819)
      *
      *
      * Body file
      *
     c                   eval      XML_path = '/home/' +
     c                                        l_figr +
     c                                        '/Bisnode/Bisnode_mal' +
     c                                        '/PersoninfoMal.txt'
      *
     c                   eval      w_xmlp = %trim(XML_path)
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
      *
      /Free
           // Legge inn internref fra forrige kall
               UpdHTMLVar('internref':       %trim(w_intr));
               UpdHTMLVar('scoremod':               w_scor);
               UpdHTMLVar('ligning':                w_likn);
               WrtSection('Score');

      /End-free
      *
     c                   eval      XML_Output3 = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/Personinfo' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt'
     c                   eval      XML_Input   = '/home/' +
     c                                           l_figr +
     c                                           '/Bisnode/Bisnode_ws' +
     c                                           '/PersoninfoResp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.txt'
      *
     c                   callp     WrtHtmlToStmf(XML_Output3: 819)
      *
      * Initierer og kaller webservice for Personinfo
      *
     c                   eval      w_url = 'https://dbonline.no' +
     c                                     '/webservices/service/PersonInfo'
     c                   eval      w_usrn = *blank
     c                   eval      w_pass = *blank
     c                   eval      w_autt = *blank
     c                   eval      w_reqf = XML_Output3
     c                   eval      w_rspf = XML_Input
     c                   eval      w_stat = 0
      *
     c                   call      'AW703C'
     c                   parm                    w_url
     c                   parm                    w_reqf
     c                   parm                    w_rspf
     c                   parm                    w_usrn
     c                   parm                    w_pass
     c                   parm                    w_autt
     c                   parm                    w_stat
      *
     c                   callp     unlink(XML_Output1)
     c                   callp     unlink(XML_Output2)
     c                   callp     unlink(XML_Output3)
      *
      * ------------------------------------------------------------------------
      * Leser mottatt svar på Personinfo og beregner kreditgrense
      * ------------------------------------------------------------------------
      *
      /Free
             options  = 'doc=file +
                                path=HentPersonResponse/Scoring +
                                case=any +
                                ns=remove +
                                allowextra=yes +
                                allowmissing=yes';
             w_xmlf = %trim(w_rspf);

                   xml-into Score %xml(w_xmlf: options);

      /End-free
      *
      /Free

             w_intr  = Score.Internref;
             w_besl  = Score.Beslutning;
             w_scor  = Score.Score;
             w_grav  = Score.GrenseAvslag;
             w_grgk  = Score.GrenseGodkjent;
             w_innt  = Score.Inntekt;

      /End-free
      *
      *
     c                   callp     unlink(XML_Input)
      *
      * Beregner kreditgrense
      *
     c                   eval      w_scorn = %dec(w_scor:2:0)

8.02  /FREE
8.02
8.02  // Henter kredittgrenser fra privat
8.02
8.02       exec sql
8.02       select  rkksc1, rkkgr1, rkksc2, rkkgr2, rkksc3, rkkgr3,
8.02               rkksc4, rkkgr4, rkksc5, rkkgr5
8.02         into :w_ksc1, :w_kgr1, :w_ksc2, :w_kgr2, :w_ksc3, :w_kgr3,
8.02              :w_ksc4, :w_kgr4, :w_ksc5, :w_kgr5
8.02        from ra33st
8.02         where rkktyp = 'PRIVAT';
8.02
8.02           if sqlcod = 100;
8.02             p_kgre = 0;
8.02            else;
8.02             if w_ksc1 > 0 and
8.02                w_scorn > w_ksc1;
8.02                  p_kgre = w_kgr1;
8.02             endif;
8.02             if w_ksc2 > 0 and
8.02                w_scorn > w_ksc2;
8.02                  p_kgre = w_kgr2;
8.02             endif;
8.02             if w_ksc3 > 0 and
8.02                w_scorn > w_ksc3;
8.02                  p_kgre = w_kgr3;
8.02             endif;
8.02             if w_ksc4 > 0 and
8.02                w_scorn > w_ksc4;
8.02                  p_kgre = w_kgr4;
8.02             endif;
8.02             if w_ksc5 > 0 and
8.02                w_scorn > w_ksc5;
8.02                  p_kgre = w_kgr5;
8.02             endif;
8.02           endif;
8.02  /END-FREE
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved ikke-treff mot Bisnode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
8.01 c                   eval      p_navn = 'Person ikke funnet'
     c                   eval      p_adr1 = *blank
     c                   eval      p_adr2 = *blank
     c                   eval      p_ponr = 0
     c                   eval      p_pste = *blank
     c                   eval      p_tele = *blank
     c                   eval      p_mobi = *blank
     c                   eval      p_kgre = 0
     c                   goto      avslutt
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_pnum
8.01 c                   parm                    p_navn
     c                   parm                    p_adr1
     c                   parm                    p_adr2
     c                   parm                    p_ponr
     c                   parm                    p_pste
     c                   parm                    p_tele
     c                   parm                    p_mobi
     c                   parm                    p_kgre
     c                   parm                    p_fdat
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Henter inn løpenummer teller for Bisnode
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'BISTEL'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
      * Tilordner faste verdier
      *
     c                   eval      w_susr = l_user
     c                   eval      w_scor = '1'
     c                   eval      w_likn = 'Ligning'
      *
      *
      * Eksternt kundesøk  hent påloggingsinformasjon Bisnode
      *
       CallP CO402R(l_filg:w_firm:0:'BISNODE - EKSTERNT_KUNDESØK':
                       co402_verdi1:co402_verdi2);

      * Brukerid
           w_start = 1;
           dou  w_tegn <> ' ';
            w_tegn = %subst(CO402_verdi2 : w_start : 1);
            w_start= w_start + 1;
           enddo;
           w_start= w_start - 1;
           w_slutt = %scan(' ' : co402_verdi2 : w_start);
           w_leng = w_slutt - w_start;

           w_usrk = %subst(CO402_verdi2 :w_start : w_leng);

      * Passord
           w_start = w_slutt;
           dou  w_tegn <> ' ';
            w_tegn = %subst(CO402_verdi2 : w_start : 1);
            w_start= w_start + 1;
           enddo;
           w_start= w_start - 1;
           w_slutt = %scan(' ' : co402_verdi2 : w_start);
           w_leng = w_slutt - w_start;

           w_pask = %subst(CO402_verdi2 :w_start : w_leng);
      *
     c                   endsr
