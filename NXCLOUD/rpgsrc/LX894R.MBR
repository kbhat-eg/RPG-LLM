     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX893R
      * Beskrivelse . . : Oppdaterer varetype på lager fra vareregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221208 bof  Nytt program
      *
6.11  *       041010 bof  Hvis lager ikke kjøres, settes varetype lik
      *                   den på vare.
6.20  *       310812 bof  Nytt felt, endringsdato for faste data som vises
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vlaglu_firm     s                   like(vlfirm)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d ra10l1_firm     s                   like(vvfirm)
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        faalag = 0
      * kjører ikke lager, velger varetype fra vare
      *
     c                   read      vlagl1
     c                   dow       not %eof(vlagl1)
      *
     c                   eval      vvarlr_firm = vlfirm
     c                   eval      vvarlr_vare = vlvare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr) and
     c                             (vvvtyp = 'L' or
     c                             vvvtyp = 'S')
     c                   exsr      upd_lager
     c                   endif
      *
     c                   read      vlagl1
     c                   enddo
      *
     c                   else
      * kjører lager
      * da leses alle varer, hvis lager finnes fra før oppdateres koden
      * ihht. vare, hvis den ikke finnes fra før, dannes lager med
      * varetype=S
     c                   read      vvarl1
     c                   dow       not %eof(vvarl1)
      *
     c                   if        vvvtyp = 'L' or
     c                             vvvtyp = 'S'
      *
     c
     c                   eval      ra10l1_firm = vvfirm
     c     ra10l1_key    setll     ra10l1
     c     ra10l1_key    reade     ra10l1
     c                   dow       not %eof(ra10l1)
      *
     c                   eval      vlaglu_firm = vvfirm
     c                   eval      vlaglu_vare = vvvare
     c                   eval      vlaglu_lage = rajkod
     c     vlaglu_key    chain     vlaglu
     c                   if        %found(vlaglu)
     c                   if        vlvtyp = *blank
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
6.20 c                   time                    vlfeda
6.20 c                   time                    vlfeti
6.20 c                   eval      vlfeus = l_user
     c                   eval      vlvtyp = vvvtyp
     c                   update    vlaglur
     c                   endif
     c                   else
     c                   exsr      dann_lager
     c                   endif
      *
     c     ra10l1_key    reade     ra10l1
     c                   enddo
      *
     c                   endif
      *
     c     les_neste     tag
     c                   read      vvarl1
     c                   enddo
      *
     c                   endif
      *
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_lager    begsr
      *
     c                   clear                   vlaglur
     c                   eval      vlfirm = vvfirm
     c                   eval      vlvare = vvvare
     c                   eval      vllage = rajkod
     c                   eval      vlvtyp = 'S'
     c                   time                    vlodat
     c                   time                    vlotim
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vlousr = l_user
     c                   eval      vleusr = l_user
     c                   write     vlaglur
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdaterer lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_lager     begsr
      *
     c                   eval      vlaglu_firm = vlfirm
     c                   eval      vlaglu_vare = vlvare
     c                   eval      vlaglu_lage = vllage
     c     vlaglu_key    chain     vlaglu
     c                   if        %found(vlaglu)
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
     c                   eval      vlvtyp = vvvtyp
     c                   update    vlaglur
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    vlaglu_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Key for les av vare
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
      *
      *  Key for les av lager
      *
     c     ra10l1_key    klist
     c                   kfld                    ra10l1_firm
      *
      *  Key for les av statusreg
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma
      *
     c                   eval      w_firm = l_firm
      *
     c     fstsl1_key    chain     fstsl1
      *
     c                   endsr
