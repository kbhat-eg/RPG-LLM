     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASNAVIOKO
      * Program . . . . : RN011R
      * Beskrivelse . . : Leser linjer fra FOMK til NAV transaksjonsfil
      *
      * Spesialversjon. : OBS!!! Det finnes et "søsterprgram" RN001R
      *                   Endringer må også gjøres der
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       230921 ask  Nytt program
8.01  *       290921 ask  Dropper overføring av poster med bilagssum = 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffomkl1    if   e           k disk    rename(fomkpfr:fomkl1r)
     fiordst    o    e           k disk    rename(iordst:iordstr)
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fikonir    if   e           k disk    rename(ikonst:ikonstr)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d fomkl1_peri     s                   like(ffperi)
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d ikonir_bfra     s                   like(ikbfra)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_bilk          s              2  0
     d w_kidr          s              9  0
     d w_aarr          s              4  0
     d w_kidd          s                   like(sokidd)
     d w_kida          s              9
     d w_obil          s                   like(faabil)
     d w_sign          s              1
     d w_type          s              4

     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser poster fra fomkL1 og skriver til IORDST
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      fomkl1_peri = 0
     c     fomkl1_key    setll     fomkl1
     c                   read      fomkl1
     c                   dow       not %eof
8.01  *
8.01  * Sjekker om "0" - bilag
8.01  *
8.01 c                   if        ffbelp = 0
8.01 c                   goto      les_neste
8.01 c                   endif
      *
      * Konverterer bilagskoder for øreavrunding
      *
     c                   if        ffbilk <> w_obil
     c                   eval      w_bilk = ffbilk
     c                   else
     c                   eval      ikonir_bfra = w_bilk
     c     ikonir_key    chain     ikonir
     c                   if        %found
     c                   eval      ffbilk = ikbtil
     c                   endif
     c                   endif
      *
     c                   exsr      finn_kid
     c                   eval      w_kida = %EditC(w_kidr :'X')
      *
     c                   exsr      finn_type
     c                   eval      iotype = w_type
     c                   eval      iobetb = sobetb
      *
     c                   eval      iofirm = fffirm
     c                   eval      iobiln = ffbiln
     c                   eval      iobdat = ffbdat
     c                   eval      iofdat = fffdat
     c                   eval      iobilk = ffbilk
     c                   eval      iotext = fftext
     c                   eval      iodavd = ffdavd
     c                   eval      iodkto = ffdkto
     c                   eval      iodsps = ffdsps
     c                   eval      iodmom = ffdmom
     c                   eval      iocavd = ffcavd
     c                   eval      iockto = ffckto
     c                   eval      iocsps = ffcsps
     c                   eval      iocmom = ffcmom
     c                   if        ffckto > 0
     c                   eval      iobelp = ffbelp * -1
     c                   else
     c                   eval      iobelp = ffbelp
     c                   endif
     c                   eval      iomvag = ffmvag
     c                   eval      iokund = ffkund
     c                   eval      iokref = ffkref
     c                   eval      iokidi = w_kidd
      *
     c                   eval      w_kida = %EditC(w_kidr :'X')
     c*                  movel     ffkidr        w_kidr
     c*                  move      ffksif        w_kidr
     c                   eval      iokidr = w_kida
      *
     c                   eval      iovalk = ffvalk
     c                   eval      iovalb = ffvalb
     c                   eval      ioproj = ffproj
     c                   eval      ioakti = ffakti
     c                   eval      ioodat = w_date
     c                   eval      iootim = w_time
     c                   eval      ioousr = l_user
      *
     c                   write     iordstr
      *
8.01 c     les_neste     tag
     c                   read      fomkl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente kid-informasjon fra fakturaregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kid      begsr
      *
     c                   eval      w_kidd = *blank
     c                   eval      w_kidr = 0
     c                   eval      w_aarr = %subdt(ffbdat:*years)
     c                   eval      sohel1_aarr = w_aarr
     c                   eval      sohel1_binr = ffbiln
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1
     c                   dow       not %eof
     c                   if        sokidd <> *blanks or
     c                             sokidr <> 0
     c                   eval      w_kidd = sokidd
     c                   eval      w_kidr = sokidr
     c                   goto      end_kid
     c                   endif
     c     sohel1_key    reade     sohel1
     c                   enddo
      *
     c     end_kid       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente transaksjonstype fra fakturaregister.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_type     begsr
      *
       /FREE
          w_sign = ' ';
          w_type = '   ';

          exec sql
            select vaokre
            into :w_sign
            from votypf
             where vaofir = :w_firm and vaotyp = :sootyp
                       fetch first 1 rows only;

         if w_sign = '-';
           w_type = 'KRED';
          else;
           w_type = 'FAKT';
         endif;

      /END-FREE
      *
     c     end_type      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på posteringsfil
      *
     c     fomkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fomkl1_peri
      *
      * Key for oppslag på fakturanummer
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
      *
      * Key for les på konverteringstabell
      *
     c     ikonir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ikonir_bfra
      *
      * Key for les på status-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Klargjør for øreavrundings konvertering
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
     c     fstsl1_key    chain     fstsl1
     c                   eval      w_obil = faabil
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
