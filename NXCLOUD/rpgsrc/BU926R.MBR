     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU925R
      * Beskrivelse . . : Butikk, henter kalulert pris og rabatt for angitt enhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200918 bsa  Nytt program
6.30  *       260918 bsa  Utvidet parameterliste med feilindikator
6.31  *       161118 bsa  Endret program som kalles pga trelast-
6.31  *                   sortimentet.
6.32  *       160119 bsa  Bruk leverandør fra BL710
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.30 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.30 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
      *
      * Definering av nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
6.30 d vvarl1_vare     s                   like(vvvare)
6.30 d vvenl1_vare     s                   like(vevare)
6.30 d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_vare          s             15
     d p_enhe          s              3
     d p_lety          s              1  0
     d p_lagr          s              2  0
     d p_dato          s               d   datfmt(*iso)
      *
     d p_sapr          s              9  2
     d p_rab1          s              5  2
     d p_rab2          s              5  2
     d p_brr1          s              5  2
     d p_brr2          s              5  2
     d p_kpri          s              1
     d p_kamp          s              5
     d p_netp          s              9  2
     d p_kopr          s              9  2
6.30 d p_stat          s              1  0
      *
      * Definering av datastrukturer
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
     d  hinumm                        8  0 overlay(hirec:56)
     d  hikode                        1    overlay(hirec:64)
     d  hidekn                        5  2 overlay(hirec:65)
     d  hiavgf                        1  0 overlay(hirec:70)
     d  hipriv                        1  0 overlay(hirec:71)
     d  hitilb                        1    overlay(hirec:72)
     d  hiskaf                        1    overlay(hirec:73)
     d  hildor                        6  0 overlay(hirec:74)
     d  hisapr                        9  2 overlay(hirec:80)
     d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_ldor          s              6  0
     d w_utda          s               d   datfmt(*iso)
     d w_vtyp          s              1
8.01 d w_prgr          s              2
     d w_avde          s              4  0
     d w_sapr          s              9  2
     d w_raba          s              9  2
     d w_sumr          s              9  2
      *
     d b_inlr          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30  * Sjekk om varen finnes ??
6.30  *
6.30 c                   eval      vvarl1_vare = p_vare
6.30 c     vvarl1_key    chain     vvarl1
6.30 c                   if        not %found
6.30 c                   eval      p_stat = 1
6.30 c                   goto      avslutt
6.30 c                   endif
6.30  *
6.30  * Sjekk om kombinasjon vare/enhet finnes ??
6.30  *
6.30 c                   eval      vvenl1_vare = p_vare
6.30 c                   eval      vvenl1_enhe = p_enhe
6.30 c     vvenl1_key    chain     vvenl1
6.30 c                   if        not %found
6.30 c                   eval      p_stat = 2
6.30 c                   goto      avslutt
6.30 c                   endif
      *
      * Finn aventuell kundens rabattkategori
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      rkrabk = 0
     c                   endif
      *
      * Fyller opp datastruktur for kall av program
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = rkrabk
     c                   eval      hikund = p_kund
     c                   eval      hikpro = p_kpro
     c                   eval      hivare = p_vare
     c                   eval      hilety = p_lety
     c                   eval      hienhe = p_enhe
     c                   eval      hidato = p_dato
     c**                 time                    hidato
     c**                 eval      hitime = *loval
     c                   time                    hitime
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hitilb = *off
     c                   eval      hiskaf = *off
6.32 c**                 eval      hildor = 0
6.32 c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinlr = *off
     c                   eval      hiprgr = w_prgr
      *
      *
      * Kaller program
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
      * Henter ouput-parametre
      *
     c                   eval      p_kpri = hokpri
     c                   eval      p_sapr = hosapr
     c                   eval      p_kopr = hokopr
     c                   eval      p_rab1 = horab1
     c                   eval      p_rab2 = horab2
     c                   eval      p_kamp = hokamp
     c                   eval      p_brr1 = hobrr1
     c                   eval      p_brr2 = hobrr2
      *
      * Så skal vi beregne nettoprris
      *
     c                   eval      w_sapr = p_sapr
     c                   eval      w_raba = 0
     c                   eval      w_sumr = 0
      * Rabatt 1
     c                   if        p_rab1 <> 0
     c                   eval      w_raba = (w_sapr - w_sumr) * p_rab1/100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      * Rabatt 2
     c                   if        p_rab2 <> 0
     c                   eval      w_raba = (w_sapr - w_sumr) * p_rab2/100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      * Bruksrettsrabatt 1
     c                   if        p_brr1 <> 0
     c                   eval      w_raba = (w_sapr - w_sumr) * p_rab1/100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      * Bruksrettsrabatt 2
     c                   if        p_brr2 <> 0
     c                   eval      w_raba = (w_sapr - w_sumr) * p_rab2/100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      * Nettopris
     c                   eval      p_netp = w_sapr - w_sumr
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
      * Brukes inn
     c                   parm                    p_firm                         firma
     c                   parm                    p_kund                         kunde
     c                   parm                    p_kpro                         kundeprosjekt
     c                   parm                    p_vare                         varenummer
     c                   parm                    p_enhe                         enhet
     c                   parm                    p_lety                         leveringstype
     c                   parm                    p_lagr                         lagr
     c                   parm                    p_dato                         prisdato
      * Sender ut
     c                   parm                    p_sapr                         salgspris
     c                   parm                    p_rab1                         rabatt 1
     c                   parm                    p_rab2                         rabatt 2
     c                   parm                    p_brr1                         bruksrettsrabatt 1
     c                   parm                    p_brr2                         bruksrettsrabatt 2
     c                   parm                    p_kpri                         priskode
     c                   parm                    p_kamp                         kampanjekode
     c                   parm                    p_netp                         nettopris
     c                   parm                    p_kopr                         kostpris
6.30 c                   parm                    p_stat                         feilindikator
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
6.30  *
6.30  * Key for oppslag på vare
6.30  *
6.30 c     vvarl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vvarl1_vare
6.30  *
6.30  * Key for oppslag på vare/enhet
6.30  *
6.30 c     vvenl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vvenl1_vare
6.30 c                   kfld                    vvenl1_enhe
      *
     c                   move      p_firm        w_firm
6.30 c                   eval      p_stat = 0
      *
      * henter prisgruppe
      *
     c                   eval      w_avde = 0
     c                   call      'VL712R   '
     c                   parm                    w_firm
     c                   parm                    p_lagr
     c                   parm                    w_avde
     c                   parm                    w_prgr
      *
      * henter varetype (Primært for å finne leverandør)
      *
6.31 c**                 call      'VL710R'
6.31 c                   call      'BL710R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    p_lagr
     c                   parm                    w_vtyp
     c                   parm                    w_utda
     c                   parm                    w_ldor
     c                   parm                    b_inlr
      * Sjekk input
     c                   test(e)                 p_dato
     c                   if        %error
     c**                 eval      p_dato = *loval
     c                   time                    p_dato
     c                   endif
      *
     c                   endsr
