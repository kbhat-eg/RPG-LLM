      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FL751R
      * Beskrivelse . . : Kundeprosjekt, oppdaterer søketekst på alle
      *                   kundeprosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110499 HKO  Nytt program
3.11  * R3.11 220900 AMD  Selgernr er utvidet fra 2 til 4 posisjoner
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
7.01  *K000   101220 bsa  Lagt inn eksternt prosjektnr i søkefelt
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprl2    if   e           k disk    rename(fkprpfr:fkprl2r)
     ffkpslu    uf a e           k disk    rename(fkpspfr:fkpslur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d fkpslu_skun     s                   like(flskun)
     d fkpslu_skpr     s                   like(flskpr)
     d rkunl1_kund     s                   like(rkkund)
     d apospf_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d w_ponr_alf      s              4
3.11 d w_selg_alf      s              4
     d w_kund_alf      s              6
     d w_kpro_alf      s              6
      *
     d w_firm          s                   like(flfirm)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fkprl2_key    setll     fkprl2
     c     fkprl2_key    reade     fkprl2                                 90
     c                   dow       *in90 = *off
      *
     c                   exsr      oppdater
      *
     c     fkprl2_key    reade     fkprl2                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Sletter eksisterende søketekst
      *
     c                   eval      fkpslu_skun = flkund
     c                   eval      fkpslu_skpr = flkpro
     c     fkpslu_key    chain     fkpslur                            91
     c                   if        *in91 = *off
     c                   delete    fkpslur
     c                   endif
      *
      * Oppretter ny søketekst....
      *
     c                   eval      flsfir = w_firm
     c                   eval      flskun = flkund
     c                   eval      flskpr = flkpro
      *
      * Navn
      *
     c     Lo:Up         xlate     flnavn        flnavn
      *
     c                   eval      flstek = flnavn
      *
      * Kundenavn
      *
     c                   eval      rkunl1_kund = flkund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c     Lo:Up         xlate     rknavn        rknavn
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      %trim(rknavn)
     c                   endif
      *
      * Adresse-1
      *
     c                   if        fladr1 <> *blank
     c     Lo:Up         xlate     fladr1        fladr1
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      %trim(fladr1)
     c                   endif
      *
      * Adresse-2
      *
     c                   if        fladr2 <> *blank
     c     Lo:Up         xlate     fladr2        fladr2
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      %trim(fladr2)
     c                   endif
      *
      * Postnummer/poststed
      *
     c                   if        flponr <> 0
      *
     c                   move      flponr        w_ponr_alf
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      w_ponr_alf
      *
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf                             91
     c                   if        *in91 = *off
     c     Lo:Up         xlate     apsted        apsted
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      %trim(apsted)
     c                   endif
      *
     c                   endif
      *
      * Kontaktaktperson (fra kunde)
      *
     c                   if        flkprs <> *blank
     c     Lo:Up         xlate     flkprs        flkprs
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      %trim(flkprs)
     c                   endif
      *
      * Selgernummer
      *
     c                   if        flselg <> 0
     c                   move      flselg        w_selg_alf
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      w_selg_alf
     c                   endif
      *
      * Kundenummer
      *
     c                   move      flkund        w_kund_alf
      *
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      w_kund_alf
      *
      * Prosjektnummer
      *
     c                   move      flkpro        w_kpro_alf
      *
     c                   eval      flstek = %trimr(flstek) + ' ' +
     c                                      w_kpro_alf
7.01  *
7.01  * Eksternt kundeprosjektnavn
7.01  *
7.01 c                   if        flepro <> *blank
7.01 c                   eval      flstek = %trimr(flstek) + ' ' +
7.01 c                                      %trim(flepro)
7.01 c                   endif
      *
      * Oppretter søketekst
      *
6.10 c                   time                    flsoda
6.10 c                   time                    flsoti
6.10 c                   eval      flsous = l_user
6.10 c                   time                    flseda
6.10 c                   time                    flseti
6.10 c                   eval      flseus = l_user
     c                   write     fkpslur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på kunde-prosjekt-register (sortert alfabetisk)
      *
     c     fkprl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av søketekst-register
      *
     c     fkpslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkpslu_skun
     c                   kfld                    fkpslu_skpr
      *
      * Key for oppslag på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
