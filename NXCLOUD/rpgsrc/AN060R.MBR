     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AN060R
      * Beskrivelse . . : Spørring på historikk ved nattjobb
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.10  * R6.10 200613 Amd  Lagt inn mulighet for å vedlikeholde nattjobb
6.10  *                   historikken med koden OK mauelt. (SST)
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KC = F3  = Exit
      *       KE = F5  = Forny
      *       KL = F12 = Annuller
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fahstl1    if   e           k disk    rename(ahstpfr:ahstl1r)
     fahstlu    uf   e           k disk    rename(ahstpfr:ahstlur)
     fan060d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
      *
      * Definisjon av array's
      *
     d mld             s             11    dim(4) ctdata perrcd(1)               MELDINGER
      *
      * Definisjon av data strukturer
      *                                                         Filedato
     d                 ds
     d d_fdat                         8  0
     d  d_faar                        4  0 overlay(d_fdat)
     d  d_fmnd                        2  0 overlay(d_fdat:5)
     d  d_fdag                        2  0 overlay(d_fdat:7)
      *                                                         Skjermdato
     d                 ds
     d d_sdat                         8  0
     d  d_sdag                        2  0 overlay(d_sdat)
     d  d_smnd                        2  0 overlay(d_sdat:3)
     d  d_saar                        4  0 overlay(d_sdat:5)
      *                                                                     -
      * Definisjon av variable i nøkler
      *                                                                     -
     d ahstl1_dato     s                   like(ahdato)
     d ahstl1_ttid     s                   like(ahttid)
      *                                                                     -
      * Definisjon av variable (Ikke standardisert enda)
      *                                                                     -
     d sfrn01          s                   like(srrn01)
     d spge01          s              3  0
     d srrn01          s              4  0
     d ssrn01          s                   like(srrn01)
     d wactli          s              2  0
     d wactpo          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Definering av felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * WACTLI - Actual line, angir startlinje for vindu.
      * WACTPO - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * D1WIN  - D1 Vindu for slette (Delete)
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene D1WIN og K1WIN
      *             er referert til dette.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      xdsp01                                       DSP SFL
      *
      * Avslutt program
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Forny
      *
     c                   if        *inke = *on                                           .....
     c                   if        sfrn01 <> *zero                                           .
     c     sfrn01        chain     b1sfl                              60                     .
     c                   endif                                                               .
     c                   eval      d_sdat = b1dato                                           .
     c                   exsr      xdatsf                                                    .
     c                   eval      ahstl1_dato = d_fdat                                      .
     c                   eval      ahstl1_ttid = b1ttid                                      .
     c     ahstl1_key    setll     ahstl1                                                    .
     c                   exsr      xclr01                                       TØM SFL      .
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * Roll
      *
     c                   if        *in10 = *on                                           .....
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * Posisjoner
      * startverdi
      *
     c                   if        b2valg = *blank                                       .....
     c                   if        b2dato <> *zero                                           .
     c                   eval      d_sdat = b2dato                                           .
     c                   exsr      xdatsf                                                    .
     c                   eval      ahstl1_dato = d_fdat                                      .
     c                   eval      ahstl1_ttid = b2ttid                                      .
     c     ahstl1_key    setll     ahstl1                                                    .
     c                   exsr      xclr01                                       TØM SFL      .
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   eval      b2dato = *zero                                            .
     c                   eval      b2ttid = *zero                                            .
     c                   goto      b2tagb                                                    .
     c                   endif                                                               .
     c                   endif                                                           .....
      *
      * Varsle om
      * gyldig konto
      *
     c                   if        b2valg <> ' '                                         .....
     c                   if        b2dato = *zero                                            .
     c                   eval      *in31 = *on                                               .
     c                   goto      b2tagb                                                    .
     c                   endif                                                               .
     c                   endif                                                           .....
      *
      * Slå opp
      * i filen
      *
     c                   eval      d_sdat = b2dato
     c                   exsr      xdatsf
     c                   eval      ahstl1_dato = d_fdat
     c                   eval      ahstl1_ttid = b2ttid
     c     ahstl1_key    chain     ahstl1                             60
      *
      * Post finnes
      * ikke
      *
     c                   if        *in60 = *on                                           .....
     c                   if        b2valg = '4'                                              .
     c                   eval      *in32 = *on                                               .
     c                   goto      b2tagb                                                    .
     c                   endif                                                               .
     c                   endif                                                           .....
      *
      * Slette
      *
     c                   if        b2valg = '4'                                          .....
     c                   exsr      xd1win                                                    .
      *                    GOTO B2TAGB
     c                   endif                                                           .....
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     c                   do        ssrn01                                                .....
     c                   readc     b1sfl                                  16                 .
     c                   if        *in16 = *on                                               .
     c                   leave                                                               .
     c                   endif                                                               .
      *
      * Slette post
      *
     c                   if        b1valg = '4'                                              .
     c                   eval      d_sdat = b1dato                                           .
     c                   exsr      xdatsf                                                    .
     c                   eval      ahstl1_dato = d_fdat                                      .
     c                   eval      ahstl1_ttid = b1ttid                                      .
     c                   exsr      xd1win                                                    .
     c                   iter                                                                .
     c                   endif                                                               .
6.10  *
6.10  * Godkjenne at nattjobb har feilet      sven
6.10  *
6.10 c                   if        b1valg = '7'
6.10 c                   eval      d_sdat = b1dato
6.10 c                   exsr      xdatsf
6.10 c                   eval      ahstl1_dato = d_fdat
6.10 c                   eval      ahstl1_ttid = b1ttid
6.10 c     ahstl1_key    chain     ahstlur
6.10  *
6.10 c                   if        %found(ahstlu)
6.10 c                   eval      %subst(ahtxt1:27:3) = '*OK'
6.10 c                   update    ahstlur
6.10 c                   endif
6.10 c                   eval      b1valg = *blank
6.10 c                   update    b1sfl
6.10 c                   iter
6.10 c                   endif
      *
     c                   enddo                                                           .....
      *
     c                   goto      b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1WIN Behandle bilde for slette
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
     c     ahstl1_key    chain     ahstl1                             60
      *
      * Snu dato
      *
     c                   eval      d_fdat = ahdato
     c                   exsr      xdatfs
     c                   eval      d1dato = d_sdat
      *
     c                   z-add     4             wactli
     c                   z-add     18            wactpo
     c                   write     s1win
      *
     c                   exfmt     d1win
      *
     c     *inkc         cabeq     *on           d1tag1
     c     *inkl         cabeq     *on           d1tag1
      *
     c     ahstl1_key    chain     ahstlur                            60
      *
     c                   if        *in60 = *off                                          .....
     c                   delete    ahstlur                                                   .
     c                   endif                                                           .....
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '4'                                          .....
     c                   eval      b1snav = *blank                                           .
     c                   movel     mld(4)        b1snav                                      .
     c                   endif                                                           .....
      *
     c     d1tag1        tag
      *
     c                   if        b1valg = '4'                                          .....
     c                   eval      b1valg = *blank                                           .
     c                   update    b1sfl                                                     .
     c                   endif                                                           .....
      *
     c                   eval      b2valg = *blank
     c                   eval      b2dato = *zero
     c                   eval      b2ttid = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Snu dato fra skjerm til file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xdatsf        begsr
     c                   eval      d_fdag = d_sdag
     c                   eval      d_fmnd = d_smnd
     c                   eval      d_faar = d_saar
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Snu dato fra file til skjerm
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xdatfs        begsr
     c                   eval      d_sdag = d_fdag
     c                   eval      d_smnd = d_fmnd
     c                   eval      d_saar = d_faar
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tøm subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xclr01        begsr
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   z-add     0             srrn01
     c                   z-add     0             sfrn01
     c                   z-add     0             ssrn01
     c                   z-add     0             spge01
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xcrt01        begsr
     c                   add       10            spge01                         SFL END
     c                   z-add     ssrn01        srrn01
      *
     c                   dou       srrn01 = spge01                                       .....
     c                   readp     ahstl1                                 15                 .
     c     *in15         cabeq     *on           xend01                         EOF          .
     c                   add       1             srrn01                                      .
     c                   eval      d_fdat = ahdato                                           .
     c                   exsr      xdatfs                                                    .
     c                   eval      b1dato = d_sdat                                           .
     c                   eval      b1txt1 = ahtxt1                                           .
     c                   eval      b1snav = ahsnav                                           .
     c                   eval      b1ttid = ahttid                                           .
     c                   write     b1sfl                                                     .
     c                   enddo                                                           .....
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01                                       .....
     c     ssrn01        add       1             sfrn01                                      .
     c                   endif                                                           .....
      *
     c                   z-add     srrn01        ssrn01
     c                   z-add     sfrn01        svrn01
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Display subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0                                           .....
     c                   eval      *in12 = *on                                               .
     c                   endif                                                           .....
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   z-add     0             spge01
     c                   z-add     0             srrn01
     c                   z-add     0             wactli
     c                   z-add     0             wactpo
      *
      * Key for file nummerisk
      *
     c     ahstl1_key    klist
     c                   kfld                    ahstl1_dato
     c                   kfld                    ahstl1_ttid
      *
     c                   z-add     99999999      ahstl1_dato
     c                   z-add     999999        ahstl1_ttid
     c     ahstl1_key    setll     ahstl1
     c                   exsr      xcrt01
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
