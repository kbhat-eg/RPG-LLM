     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RC650R                                              *
      * Beskrivelse . . : Utskrift av inkassovarsel - parameter               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.30     030205  Purrenr større enn, ble ikke lagt ut i LDA     HFL  *
 9.01 *           200314  Lagt inn blank i valg av utskrift på parameter ASK  *
 9.01 *                   bilde. Fil blir likevel lagt ut (RC653C)            *
 9.02 *           200314  Økt kontrollgrensen på antall løpedager til 50 ASK  *
 9.03 *K00 sst 210616     Ev hente opp igjen lagret parameter                 *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frca0l1    if   e           k disk    rename(rca0pfr:rca0l1r)
     frkwapf    uf a e           k disk
     frc650d    cf   e             workstn
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  ppros                461    463  1
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pparam               300    490
      *
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     d n               s              2  0
     d waar            s              4  0
     d wc0cnk          s                   like(rc0cnk)
     d wkun            s              7  0
     d wmnd            s              2  0
     d wpakod          s              2  0
     d wpdat1          s              6  0
     d p_in03          s              1
     d w_firm          s                   like(l_firm)
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
      *
      /eject
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
     c                   eval      *in11 = *off
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in14 = *off
     c                   eval      *in15 = *off
     c                   eval      *in16 = *off
     c                   eval      *in17 = *off
     c                   eval      *in18 = *off
     c                   eval      *in19 = *off
     c                   eval      *in20 = *off
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
     c                   eval      *in38 = *off
     c                   eval      *in39 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on
     c                   eval      p_in03 = '1'
     c                   goto      xslutt
     c                   endif
      *
      * Sjekke om kjøredato er gyldig
      *
     c                   if        a2pkda = *zero
     c                   move      udate         a2pkda
     c                   endif
      *
     c     *dmy          test(d)                 a2pkda                 31
     c                   if        *in31 = *on
     c                   goto      a2taga
     c                   endif
      *
      * Sjekke om Purre-/Inkasso-dato er gyldig
      *
     c                   if        a2pfda = *zero
     c                   move      udate         a2pfda
     c                   endif
      *
     c     *dmy          test(d)                 a2pfda                 32
     c                   if        *in32 = *on
     c                   goto      a2taga
     c                   endif
      *
      *  Kontroll av LØPEDAGER
      *
9.02 c                   if        a2plop > 50
     c                   move      *on           *in34
     c                   goto      a2taga
     c                   endif
      *
      *  Kontroll av Purring-/Inkasso
      *
     c                   if        a2pkod <> 'I' and
     c                             a2pkod <> 'P'
     c                   move      *on           *in36
     c                   goto      a2taga
     c                   endif
      *
      *  Kontroll av KVITTERING SKRIVES
      *
     c                   if        a2pskr <> 'J'
     c                   if        a2pskr <> ' '
     c                   move      *on           *in37
     c                   goto      a2taga
     c                   endif
     c                   endif
      *
      *  Kontroll avdeling
      *
     c                   if        a2pavf > a2pavt
     c                   move      *on           *in43
     c                   goto      a2taga
     c                   endif
      *
      *  Kontroll kundekategori
      *
     c                   if        a2pkaf > a2pkat
     c                   move      *on           *in42
     c                   goto      a2taga
     c                   endif
      *
      *  Kontroll selger
      *
     c                   if        a2psef > a2pset
     c                   move      *on           *in44
     c                   goto      a2taga
     c                   endif
      *
      *  Kontroll av kundenr. fra - til
      *
     c                   if        a2pknf > a2pknt
     c                   move      *on           *in48
     c                   goto      a2taga
     c                   endif
      *
      *  Kontroll av enkelt-kunder
      *
     c                   move      a2pkn1        pknr1
     c                   move      a2pkn2        pknr2
     c                   move      a2pkn3        pknr3
     c                   move      a2pkn4        pknr4
     c                   move      a2pkn5        pknr5
     c                   move      a2pkn6        pknr6
     c                   move      a2pkn7        pknr7
     c                   move      a2pkn8        pknr8
     c                   move      a2pkn9        pknr9
     c                   move      a2pkn0        pknr0
      *
     c                   xfoot     kun           wkun
     c                   eval      n = 1
     c                   if        wkun <> 0
      *                                                                 .
     c                   dou       n > 10
      *                                                               . .
     c                   if        kun(n) <> *zeros
     c                   eval      rkunl1_kund = kun(n)
     c     rkunl1_key    chain     rkunl1                             61
      *                                                               . .
     c   61              do
     c                   move      *on           *in47
     c                   eval      *in11 = n = 1
     c                   eval      *in12 = n = 2
     c                   eval      *in13 = n = 3
     c                   eval      *in14 = n = 4
     c                   eval      *in15 = n = 5
     c                   eval      *in16 = n = 6
     c                   eval      *in17 = n = 7
     c                   eval      *in18 = n = 8
     c                   eval      *in19 = n = 9
     c                   eval      *in20 = n = 10
     c                   goto      a2taga
     c                   enddo
      *                                                               . .
     c                   endif
      *                                                               . .
     c                   eval      n = n + 1
      *                                                               . .
     c                   enddo
      *                                                                 .
     c                   endif
      *
      *  Kontroll OM SAKS-NR. MANGLER
      *
     c***                if        wc0cnk = *blanks
     c***                move      *on           *in49
     c***                goto      a2taga
     c***                endif
      *
      * Skriv parameter (LDA)
      *
     c                   move      a2pkda        pkdat
     c                   move      a2pfda        pfdat
     c                   move      a2plop        plope
T5.30c                   move      a2ppnr        ppunr
     c                   move      a2pavf        pavdf
     c                   move      a2pavt        pavdt
     c                   move      a2pkaf        pkatf
     c                   move      a2pkat        pkatt
     c                   move      a2psef        pself
     c                   move      a2pset        pselt
     c                   move      a2pknf        pknrf
     c                   move      a2pknt        pknrt
     c                   move      a2pskr        pbskr
     c                   move      a2pkod        ppkod
      *
      * Skriv parameter (RKWAPF)
      *
     c                   eval      rwkkey = *blanks
     c                   movel     pfirm         rwkkey
      *
     c     kwakey        chain     rkwapf                             99
     c                   move      pparam        rwkrec
      *
     c                   if        *in99 = *on
     c                   write     rkwapfr
     c                   else
     c                   update    rkwapfr
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
     c                   eval      wpdat1 = 0
9.03  *   Ev. hente opp forrige parameter
9.03 c                   eval      rwkkey = *blanks
9.03 c                   movel     l_firm        rwkkey
9.03  *
9.03 c     kwakey        chain     rkwapf
9.03 c                   if        %found(rkwapf)
9.03 c                   move      rwkrec        pparam
9.03 c                   move      pkdat         a2pkda
9.03 c                   move      pfdat         a2pfda
9.03 c                   move      plope         a2plop
9.03 c                   move      ppunr         a2ppnr
9.03 c                   move      pavdf         a2pavf
9.03 c                   move      pavdt         a2pavt
9.03 c                   move      pkatf         a2pkaf
9.03 c                   move      pkatt         a2pkat
9.03 c                   move      pself         a2psef
9.03 c                   move      pselt         a2pset
9.03 c                   move      pknrf         a2pknf
9.03 c                   move      pknrt         a2pknt
9.03 c                   move      pbskr         a2pskr
9.03 c                   move      ppkod         a2pkod
9.03 c                   move      pknr1         a2pkn1
9.03 c                   move      pknr2         a2pkn2
9.03 c                   move      pknr3         a2pkn3
9.03 c                   move      pknr4         a2pkn4
9.03 c                   move      pknr5         a2pkn5
9.03 c                   move      pknr6         a2pkn6
9.03 c                   move      pknr7         a2pkn7
9.03 c                   move      pknr8         a2pkn8
9.03 c                   move      pknr9         a2pkn9
9.03 c                   move      pknr0         a2pkn0
9.03 c                   else
      *
      * Legg inn firma-nr. og navn + std.verdier for parameter
      *
     c                   move      l_firm        w_firm
     c                   eval      a2plop = 5
     c                   eval      a2pavf = *zeros
     c                   eval      a2pavt = 9999
     c                   eval      a2pkaf = *zeros
     c                   eval      a2pkat = 9999
     c                   eval      a2psef = *zeros
     c                   eval      a2pset = 9999
     c                   eval      a2pknf = *zeros
     c                   eval      a2pknt = 999999
     c                   eval      a2pkn1 = *zeros
     c                   eval      a2pkn2 = *zeros
     c                   eval      a2pkn3 = *zeros
     c                   eval      a2pkn4 = *zeros
     c                   eval      a2pkn5 = *zeros
     c                   eval      a2pkn6 = *zeros
     c                   eval      a2pkn7 = *zeros
     c                   eval      a2pkn8 = *zeros
     c                   eval      a2pkn9 = *zeros
     c                   eval      a2pkn0 = *zeros
     c                   move      udate         a2pkda
     c                   move      udate         a2pfda
9.01 c                   eval      a2pskr = ' '
     c                   eval      a2pkod = 'I'
      *
     c                   eval      pfirm = l_firm
     c                   eval      pnavn = l_fnav
     c                   eval      pkdat = *zeros
     c                   eval      pfdat = *zeros
     c                   eval      pdfgg = *zeros
     c                   eval      plope = *zeros
     c                   eval      ppros = *zeros
     c                   eval      pgrns = *zeros
     c                   eval      pmini = *zeros
     c                   eval      praar = *zeros
     c                   eval      prper = *zeros
     c                   eval      ptext = *zeros
     c                   eval      ppunr = *zeros
     c                   eval      pavdf = *zeros
     c                   eval      pavdt = *zeros
     c                   eval      pkatf = *zeros
     c                   eval      pkatt = *zeros
     c                   eval      pself = *zeros
     c                   eval      pselt = *zeros
     c                   eval      pknrf = *zeros
     c                   eval      pknrt = *zeros
     c                   eval      pknr1 = *zeros
     c                   eval      pknr2 = *zeros
     c                   eval      pknr3 = *zeros
     c                   eval      pknr4 = *zeros
     c                   eval      pknr5 = *zeros
     c                   eval      pknr6 = *zeros
     c                   eval      pknr7 = *zeros
     c                   eval      pknr8 = *zeros
     c                   eval      pknr9 = *zeros
     c                   eval      pknr0 = *zeros
9.03 c                   endif
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for statusopplysninger
      *
     c     kwakey        klist
     c                   kfld                    rwkkey
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   endsr
      /eject
