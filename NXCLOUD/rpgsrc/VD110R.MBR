     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD110R
      * Beskrivelse . . : Varesortiment-linje, utplukk leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100903 HKO  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  051110 bof  Utvidet med lager
6.3q  *       260515 bof  Datoformat sql
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skvdrmindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlsopf    if   e           k disk
     frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
      *
     fvd110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vdfirm)
     d p_vsor          s                   like(vdvsor)
6.20 d p_lage          s                   like(vdlage)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d rlevlr_levr     s                   like(rllevr)
     d rlevl1_levr     s                   like(rllevr)
     d rlevl2_alfa     s                   like(rlalfa)
     d vvarl5_ldor     s                   like(vvldor)
     d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_subf          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
      *
     d s_stek          s             35
      *
     d w_firm          s                   like(vdfirm)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skvdrmbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skvdrmbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gvdnta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2alfa <> *blank or
     c                             b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'L'
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    setll     rlevl1
     c                   other
     c                   eval      rlevl2_alfa = b1alfa
     c     rlevl2_key    setll     rlevl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Navn
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      rlevl2_alfa = b2alfa
     c     rlevl2_key    setll     rlevl2
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      rlevl1_levr = b2ldor
     c     rlevl1_key    setll     rlevl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2alfa = *blank
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Legg til sortiment
      *
     c                   if        b1valg = 1
     c                   exsr      opprett
     c                   move      '->'          b1navn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Varegrupper
      *
     c                   if        b1valg = 7
     c                   call      'VG560R'
     c                   parm                    p_firm
     c                   parm                    p_vsor
5.20 c                   parm                    p_lage
     c                   parm                    b1ldor
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
     c+                  select rlsfir,
     c+                         rlstek,
     c+                         rlslev
     c+                  from   rlsopf
     c+                  where  rlsfir = :w_firm and
     c+                         rlstek like :w_ste1
     c+                  order by rlsfir, rlstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :rlsfir,
     c+                        :rlstek,
     c+                        :rlslev
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   eval      rlevlr_levr = rlslev
     c     rlevlr_key    chain     rlevlr
     c                   if        not %found
     c                   iter
     c                   endif
      *
     c                   when      w_seqe = 'L'
     c                   read      rlevl1                                 15
     c                   other
     c                   read      rlevl2                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   eval      vvarl5_ldor = rllevr
     c     vvarl5_key    chain     vvarl5
     c                   if        not %found
     c                   iter
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      rlevl1_levr = rllevr
     c                   other
     c                   eval      rlevl2_alfa = rlalfa
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldor = rllevr
     c                   eval      b1alfa = rlalfa
     c                   eval      b1navn = rlnavn
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'S'
     c                   exsr      forny
     c                   goto      end_bck
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     rlevl1_key    setgt     rlevl1
     c                   readp     rlevl1
     c                   other
     c     rlevl2_key    setgt     rlevl2
     c                   readp     rlevl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     rlevl1
     c                   other
     c                   readp     rlevl2
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      vvarl5_ldor = rllevr
     c     vvarl5_key    chain     vvarl5
     c                   if        not %found
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      rlevl1_levr = rllevr
     c                   other
     c                   eval      rlevl2_alfa = rlalfa
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     rlevl1_key    setll     rlevl1
     c                   other
     c     rlevl2_key    setll     rlevl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   eval      b_open_sok = *off
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette i varesortimnet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c                   eval      vsdtl1_vsor = p_vsor
6.20 c                   eval      vsdtl1_lage = p_lage
     c                   eval      vsdtl1_ldor = b1ldor
     c                   eval      vsdtl1_ogrp = 0
     c                   eval      vsdtl1_hgrp = 0
     c                   eval      vsdtl1_ugrp = 0
     c                   eval      vsdtl1_modn = 0
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        %found
     c                   goto      end_opprett
     c                   endif
      *
      * Oppretter varesortiment-linje
      *
     c                   clear                   vsdtlur
      *
     c                   eval      vdvsor = p_vsor
6.20 c                   eval      vdlage = p_lage
     c                   eval      vdldor = b1ldor
      *
     c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
     c                   eval      vdeusr = l_user
      *
     c                   write     vsdtlur
      *
     c     end_opprett   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vsor
6.20 c                   parm                    p_lage
      *
      * Key for oppslag på leverandør
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlr_levr
      *
      * Key for posisjonering på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for posisjonering på alfa
      *
     c     rlevl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl2_alfa
      *
      * Key for oppslag på vare
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl5_ldor
      *
      * Key for oppslag på varesortiment-linje
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
6.20 c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2vsor = p_vsor
6.20 c                   eval      b2lage = p_lage
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rlevl2_alfa = *blank
     c     rlevl2_key    setll     rlevl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
