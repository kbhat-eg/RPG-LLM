     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP201R
      * Beskrivelse . . : prosjekt-rapport kopiere rapporter
      *
      * Spesialversjon. :             Prosk.leder
      * Tilpasset for . :             opprettet dato
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280616 sst  Nytt program
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frprrlr    if   e           k disk    rename(rprrpfr:rprrlrr)
     frprrlu    uf a e           k disk    rename(rprrpfr:rprrlur)
     f
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rprrlr_firm     s                   like(rphfir)
     d rprrlr_rapp     s                   like(rphrap)
     d rprrlr_linj     s                   like(rphlin)
     d rprrlr_ulin     s                   like(rphuli)
     d rprrlu_firm     s                   like(rphfir)
     d rprrlu_rapp     s                   like(rphrap)
     d rprrlu_linj     s                   like(rphlin)
     d rprrlu_ulin     s                   like(rphuli)
      *
      * Definering av variable
      *
     d p_filg          s                   like(l_filg)
     d p_firm          s              3  0
     d p_frap          s                   like(rphrap)
     d p_trap          s                   like(rphrap)
     d p_sts           s              3
     d s_filg          s                   like(l_filg)
     d s_firm          s                   like(l_firm)
      *
     d w_sts           s              3
      *
      * Sjekker om rapport finnes fera før
      *
     c                   eval      rprrlu_firm = p_firm
     c                   eval      rprrlu_rapp = p_trap
     c                   eval      rprrlu_linj = *zero
     c                   eval      rprrlu_ulin = *zero
     c     rprrlu_key    setll     rprrlu
     c                   read      rprrlu
     c                   if        not %eof and
     c                             rphrap = p_trap
      *                                          Rapport finnes fra før
     c                   eval      p_sts = 'RFF'
     c                   goto      end
     c                   endif
      *
      * Fyller lese key
      *
     c                   eval      rprrlr_firm = l_firm
     c                   eval      rprrlr_rapp = p_frap
     c                   eval      rprrlr_linj = *zero
     c                   eval      rprrlr_ulin = *zero
     c     rprrlr_key    setll     rprrlr
     c                   read      rprrlr
      *
     c                   dow       not %eof(rprrlr) and
     c                             rphfir = l_firm  and
     c                             rphrap = p_frap
      *
     c                   eval      rphfir = p_firm
     c                   eval      rphrap = p_trap
     c                   write     rprrlur
     c                   read      rprrlr
     c                   enddo
      *
     c     end           tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
     c     *entry        plist
     c                   parm                    p_filg
     c                   parm                    p_firm
     c                   parm                    p_frap
     c                   parm                    p_trap
     c                   parm                    p_sts
      *
      * Key for les på rapport
      *
     c     rprrlr_key    klist
     c                   kfld                    rprrlr_firm
     c                   kfld                    rprrlr_rapp
     c                   kfld                    rprrlr_linj
     c                   kfld                    rprrlr_ulin
      *
      * Key for oppdatering av rappjket
      *
     c     rprrlu_key    klist
     c                   kfld                    rprrlu_firm
     c                   kfld                    rprrlu_rapp
     c                   kfld                    rprrlu_linj
     c                   kfld                    rprrlu_ulin
      *
     c                   endsr
