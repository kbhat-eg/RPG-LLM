     h datedit(*dmy)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : BO126R
      * Beskrivelse . . : Utskrift av kontroll/avstem., Dekning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.30  * R5.30 131004 HFL  I bonghode og linje er bongnr. 12A.
5.31  * R5.31 221204 ASK  Lagt inn riktig mva-behandling ihht. vare
5.32  * R5.32 110405 ASK  Lagt inn korreksjoner for almenningsløsning
pdf   * R6.10 081010 BSA  Innført PDF utskrift. Innebærer primært å finne
pdf   *                   generert spool, og kall av AP620R med dette som
pdf   *                   parametre.
6.10  * 6.10  250112 bof  Overstyrt kostpris skal ikke brukes på div.varer
6.20  * 6.20  080312 bof  Utvidet med parameter VL710r
6.21  * 6.20  031212 bsa  Tillegg for å berike metatagger
6.22  * R6.20 041013 BSA  Endret logikk rundt refn
6.23  * R6.20 070714 bof  Trekke vekk depositum fra lista + linje for dep.
6.30  * R6.30 161115 bsa  Lagt ut mer info på kvitteringsliste og arkiv.
6.31  * R6.20 251115 BSA  Fjernet '-' i type dokument, da dette skaper trøbbel
6.31  *                   for arkivklienten.
6.32  * R6.30 270616 bof  Trekker fra rabatt i kolonne 2 (linjesum eks mva)
6.33  * R6.30 280616 bof  Omskriving pga. nye felt i bodtpf(innkl.mva fra POS)
6.33  *                   All kode fra 6.32 er fjernet.
6.34  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.35  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.nu  * 6.30  171019 bkv  Nummerserie rettelse ldlavs
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbpsolc    ip   e           k disk    rename(bpsopfr:bpsolcr)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fbbudl1    uf   e           k disk    rename(bbudpfr:bbudl1r)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
5.31 fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
6.35 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fbo126p    o    e             printer
pdf  f                                     usropn
      *
      * Definering av datastruktur
      *
     d                 ds
     d ldprec                       256
     d  ldotal                        4    overlay(ldprec)
     d  ldot01                        2    overlay(ldprec:5)
     d  ldot02                        2    overlay(ldprec:7)
     d  ldot03                        2    overlay(ldprec:9)
     d  ldot04                        2    overlay(ldprec:11)
     d  ldot05                        2    overlay(ldprec:13)
     d  ldot06                        2    overlay(ldprec:15)
     d  ldot07                        2    overlay(ldprec:17)
     d  ldot08                        2    overlay(ldprec:19)
     d  ldot09                        2    overlay(ldprec:21)
     d  ldot10                        2    overlay(ldprec:23)
     d  ldwsgr                        6    overlay(ldprec:25)
     d  ldwsal                       10    overlay(ldprec:31)
     d  ldws01                       10    overlay(ldprec:41)
     d  ldws02                       10    overlay(ldprec:51)
     d  ldws03                       10    overlay(ldprec:61)
     d  ldws04                       10    overlay(ldprec:71)
     d  ldws05                       10    overlay(ldprec:81)
     d  ldws06                       10    overlay(ldprec:91)
     d  ldws07                       10    overlay(ldprec:101)
     d  ldws08                       10    overlay(ldprec:111)
     d  ldws09                       10    overlay(ldprec:121)
     d  ldws10                       10    overlay(ldprec:131)
     d  ldavst                        1  0 overlay(ldprec:142)
     d  lddato                       10d   datfmt(*iso)
     d                                     overlay(ldprec:143)
     d  ldperi                        6  0 overlay(ldprec:153)
     d  lddekn                        1  0 overlay(ldprec:159)
     d  ldvdag                        2  0 overlay(ldprec:160)
     d  lddeta                        1  0 overlay(ldprec:162)
     d  ldtota                        1  0 overlay(ldprec:163)
     d  ldfrad                       10d   datfmt(*iso)
     d                                     overlay(ldprec:164)
     d  ldtild                       10d   datfmt(*iso)
     d                                     overlay(ldprec:174)
     d  ldaarr                        4  0 overlay(ldprec:184)
6.nu d  ldlavs                        8  0 overlay(ldprec:188)
6.nu d  ldldet                        6  0 overlay(ldprec:196)
6.nu d  ldtime                        6  0 overlay(ldprec:202)
6.nu d  ldkred                        1  0 overlay(ldprec:208)
      *
      * Definering av local data area
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_utqm                585    594
pdf  d l_bach                595    635
pdf  d l_arch                636    636  0
pdf  d l_skje                637    637  0
pdf  d l_land                638    638  0
pdf  d l_exlp                639    639  0
pdf  d l_mail                640    640  0
     d l_ruti                800    809
pdf  D l_outq                810    819
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_parm          s            256
pdf  d p_btyp          s            100
6.22 d w_year          s              4  0
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.21 d p_tagg0         s             20
6.21 d p_tagg0val      s             50
6.21 d p_tagg1         s             20
6.21 d p_tagg1val      s             50
6.21 d p_tagg2         s             20
6.21 d p_tagg2val      s             50
6.21 d p_tagg3         s             20
6.21 d p_tagg3val      s             50
6.21 d p_tagg4         s             20
6.21 d p_tagg4val      s             50
6.21 d p_tagg5         s             20
6.21 d p_tagg5val      s             50
6.21 d p_tagg6         s             20
6.21 d p_tagg6val      s             50
6.21 d p_tagg7         s             20
6.21 d p_tagg7val      s             50
6.21 d p_tagg8         s             20
6.21 d p_tagg8val      s             50
6.21 d p_tagg9         s             20
6.21 d p_tagg9val      s             50
6.21 d p_refn          s             50
6.21 d p_dspl          s            100
      *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(vgofir)
6.20 d w_udat          s               d   datfmt(*iso)
6.20 d w_ldor          s              6  0
6.20 d b_inlr          s              1    inz(*off)
6.35 d w_lv_nobb       s                   like(vvlnob)
6.35 d w_lv_modn       s                   like(vvlmod)
6.35 d w_lv_lldo       s                   like(vvlnle)
6.35 d w_lv_llva       s                   like(vvllva)
     d bbudl1_aarr     s                   like(buaarr)
     d bbudl1_mmnd     s                   like(bummnd)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_wsid     s                   like(bdwsid)
5.30 d bodtl1_numm     s                   like(bdbong)
     d bodtl1_line     s                   like(bdline)
5.31 d bohel1_aarr     s                   like(boaarr)
5.31 d bohel1_bong     s                   like(bobong)
5.31 d bohel1_wsid     s                   like(bowsid)
     d vltyl1_ltyp     s                   like(valtyp)
     d votyl1_otyp     s                   like(vaotyp)
     d aforl1_ruti     s                   like(afruti)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vogrl1_oogr     s                   like(vgoogr)
      *
      * Definering av variable
      *
     d w_dato_alf      s             10
     d w_mda           s              5
     d wdbelp          s                   like(bdsums)
     d wddekn          s                   like(bdsums)
     d wdgrax          s             13  4
     d wdtotg          s                   like(bdrab1)
     d wpqmva          s              1
     d wpretk          s              1
     d wqperi          s                   like(ldperi)
     d wsaar           s              4  0
     d wsdag           s              2  0
     d wsdato          s              8  0
     d wsmda           s              4  0
     d wsmnd           s              2  0
     d wwtotd          s                   like(bdsums)
     d wwtote          s                   like(bdsums)
     d wwtotg          s                   like(bdrab1)
     d wwtoti          s                   like(bdsums)
     d wwtotk          s                   like(bdsums)
6.23 d wwdepo          s                   like(bdsums)
6.10 d w_vtyp          s              1
6.32 d w_sums          s             11  2
      *
     ibpsolcr       01
     i                                          bsofir        l6
     i                                          bsooty        l4
     i                                          bsoogr        l2
     i                                          bsohgr        l1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av Levelbrudd Input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        *inl6 = *on
     c                   exsr      l6inn
     c                   endif
     c                   if        *inl4 = *on
     c                   exsr      l4inn
     c                   endif
     c                   if        *inl2 = *on
     c                   exsr      l2inn
     c                   endif
     c                   if        *inl1 = *on
     c                   exsr      l1inn
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av BONG-LINJE-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent opplysninger fra BONG-LINJE-REGISTER
      *
     c                   eval      w_firm = bsofir
     c                   eval      bodtl1_aarr = bsoaar
     c                   eval      bodtl1_wsid = bsowsd
     c                   eval      bodtl1_numm = bsonum
     c                   eval      bodtl1_line = bsolin
     c     bodtl1_key    chain     bodtl1r                            90
      *
      * Hent opplysninger fra LINJETYPE-REGISTER
      *
     c                   eval      vltyl1_ltyp = bdltyp
     c     vltyl1_key    chain     vltyl1r                            90
      *
      *  Hent opplysninger fra BONG-HODE-REGISTER
      *
     c                   eval      bohel1_aarr = bdaarr
     c                   eval      bohel1_wsid = bdwsid
     c                   eval      bohel1_bong = bdbong
     c     bohel1_key    chain     bohel1r
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r                            90
      *
      * Tester om kredit omsetning er valgt utelatt.
      *
     c                   if        ldkred = 1 and
     c                             vaoakk < 3
     c                   goto      xslutt
     c                   endif
6.23  *
6.23  * Depositum skal ikke være med på lista
6.23  *
6.23 c                   if        bdvare = baadva
6.23 c                   eval      wwdepo = wwdepo + bdsums
6.23 c                   goto      xslutt
6.23 c                   endif
6.10  *
6.10  * Hente varetype
6.10  *
6.10 c                   call      'VL710R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    bdvare
6.10 c                   parm                    bolage
6.10 c                   parm                    w_vtyp
6.20 c                   parm                    w_udat
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.35 c                   parm                    w_lv_nobb
6.35 c                   parm                    w_lv_modn
6.35 c                   parm                    w_lv_lldo
6.35 c                   parm                    w_lv_llva
      *
      * Bruk selvkost dersom selvkost er utfylt
      *
6.10 c                   if        bdselk <> *zero  and
6.10 c                             w_vtyp <> 'D'
     c                   z-add     bdselk        bdkopr
     c                   endif
      *
      *
      * Snur fortegn på beløp dersom negativ ordre-type
      *
     c                   if        vaokre = '-'
     c                   mult      -1            bdsums
6.33 c                   mult      -1            bdsumi
     c                   mult      -1            bdkopr
     c                   endif
      *
      * Snur fortegn på beløp dersom negativ linje-type
      *
     c                   if        valkre = '-'
     c                   mult      -1            bdkopr
     c                   endif
      *
      * Beregning av sum eksl. mva.
      *
6.32  * finner nettosum (uten rabatter)
6.32 c                   eval      w_sums = bdsums
6.32 c                                      - bdrbe1 - bdrbe2 - bdorbe
6.32 c                   z-add     w_sums        wwtote
6.33  *
6.33  * bruker sumi innkl. mva fra POS, denne er fratrukket rabatt
6.33  *
6.33 c                   eval      wwtoti = bdsumi
      *
      * Beregning av kostverdi
      *
     c     bdanta        mult      bdkopr        wwtotk
      *
      * Beregning av dekningsbidrag
      *
     c     wwtote        sub       wwtotk        wwtotd
      *
      * Addering sammen til totaler for alle nivåer (levels)
      *
     c                   add       wwtoti        l1toti
     c                   add       wwtote        l1tote
     c                   add       wwtotk        l1totk
     c                   add       wwtotd        l1totd
      *
     c                   add       wwtoti        l2toti
     c                   add       wwtote        l2tote
     c                   add       wwtotk        l2totk
     c                   add       wwtotd        l2totd
      *
     c                   add       wwtoti        l4toti
     c                   add       wwtote        l4tote
     c                   add       wwtotk        l4totk
     c                   add       wwtotd        l4totd
      *
     c                   add       wwtoti        l6toti
     c                   add       wwtote        l6tote
     c                   add       wwtotk        l6totk
     c                   add       wwtotd        l6totd
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
      * Behandling av level-brudd output
      *
     cl1                 exsr      l1ut
     cl2                 exsr      l2ut
     cl4                 exsr      l4ut
     cl6                 exsr      l6ut
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.34 clr                 close     bo126p
6.34 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  clr                 parm                    l_bach
pdf  clr                 endif
pdf   *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L6INN     Behandling av brudd på L6 inn  (Firma)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l6inn         begsr
      *
      * Nullstilling av totaler
      *
     c                   eval      l6toti = *zero
     c                   eval      l6tote = *zero
     c                   eval      l6totk = *zero
     c                   eval      l6totd = *zero
     c                   eval      l6totg = *zero
6.23 c                   eval      l7depo = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L4INN     Behandling av brudd på L4 inn  (Ordretype)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l4inn         begsr
      *
      * Nullstilling av totaler
      *
     c                   eval      l4toti = *zero
     c                   eval      l4tote = *zero
     c                   eval      l4totk = *zero
     c                   eval      l4totd = *zero
     c                   eval      l4totg = *zero
      *
      * Skriver ny heading, Hver gang ny ordretype
      *
     c                   if        *in32 = *on
     c                   write     a1hdr                                30
     c                   endif
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bsooty
     c     votyl1_key    chain     votyl1r                            90
      *
      * Legger inn opplysninger i utskriftsfelter
      *
     c                   eval      b1otyp = bsooty
     c                   eval      b1otxt = vaotxt
      *
      * Skriv
      *
     c                   if        *in32 = *on
     c                   write     b1det                                30
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L2INN     Behandling av brudd på L2 inn  (Overgruppe)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l2inn         begsr
      *
      * Nullstilling av totaler
      *
     c                   eval      l2toti = *zero
     c                   eval      l2tote = *zero
     c                   eval      l2totk = *zero
     c                   eval      l2totd = *zero
     c                   eval      l2totg = *zero
      *
      * Legger inn opplysninger i utskriftsfelter
      *
     c                   eval      c1ogrp = bsoogr
      *
      * Hent inn opplysninger fra Overgruppe
      *
     c                   eval      vhgrl1_hogr = bsoogr
     c     vogrl1_key    chain     vogrl1r                            90
     c                   eval      c1otxt = *blank
     c                   if        *in90 = *off
     c                   movel     vgotxt        c1otxt
     c                   endif
      *
      * Skriv
      *
     c                   if        *in32 = *on
     c                   write     c1det                                30
     c                   endif
     c                   if        *in32 = *on
     c                             and *in30 = *on
     c                   exsr      skhead
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L1IN      Behandling av brudd på L1 inn  (Hovedgruppe)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     l1inn         begsr
      *
      * Nullstilling av totaler
      *
     c                   eval      l1toti = *zero
     c                   eval      l1tote = *zero
     c                   eval      l1totk = *zero
     c                   eval      l1totd = *zero
     c                   eval      l1totg = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L1UT      Behandling av brudd på L1 ut   (Hovedgruppe)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     l1ut          begsr
      *
      * Kaller opp subrutine for beregning av DB-%
      *
     c                   z-add     l1tote        wdbelp
     c                   z-add     l1totd        wddekn
     c                   exsr      dekn
     c                   z-add     wdtotg        l1totg
      *
      * Legger inn opplysninger i utskriftsfelter
      *
     c                   eval      l1hgrp = bsohgr
      *
      * Hent inn opplysninger fra Hovedgruppe-register
      *
     c                   eval      vhgrl1_hogr = bsoogr
     c                   eval      vhgrl1_hhgr = bsohgr
     c     vhgrl1_key    chain     vhgrl1r                            90
     c                   if        *in90 = *off
     c                   eval      l1htxt = vghtxt
     c                   endif
      *
      * Skriv
      *
     c                   if        *in32 = *on
     c                   write     l1tot                                30
     c                   endif
     c                   if        *in32 = *on
     c                             and *in30 = *on
     c                   exsr      skhead
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L2UT      Behandling av brudd på L2 ut   (Overgruppe)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l2ut          begsr
      *
      * Kaller opp subrutine for beregning av DB-%
      *
     c                   z-add     l2tote        wdbelp
     c                   z-add     l2totd        wddekn
     c                   exsr      dekn
     c                   z-add     wdtotg        l2totg
      *
      * Legger inn opplysninger i utskriftsfelter
      *
     c                   eval      l2ogrp = bsoogr
      *
      * Skriv
      *
     c                   if        *in32 = *on
     c                   write     l2tot                                30
     c                   endif
     c                   if        *in32 = *on
     c                             and *in30 = *on
     c                   exsr      skhead
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L4UT      Behandling av brudd på L4 ut   (Ordretype)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l4ut          begsr
      *
      * Kaller opp subrutine for beregning av DB-%
      *
     c                   z-add     l4tote        wdbelp
     c                   z-add     l4totd        wddekn
     c                   exsr      dekn
     c                   z-add     wdtotg        l4totg
      *
      * Legger inn opplysninger i utskriftsfelter
      *
     c                   eval      l4otyp = bsooty
      *
      * Skriv
      *
     c                   if        *in32 = *on
     c                   write     l4tot                                30
     c                   endif
     c                   if        *in32 = *on
     c                             and *in30 = *on
     c                   exsr      skhead
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * L6UT      Behandling av brudd på L6 ut   (Firma)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     l6ut          begsr
      *
      * Kaller opp subrutine for beregning av DB-%
      *
     c                   z-add     l6tote        wdbelp
     c                   z-add     l6totd        wddekn
     c                   exsr      dekn
     c                   z-add     wdtotg        l6totg
      *
      * Hent opplysninger fra BUDSJETT-REGISTER
      *
     c     bbudl1_key    chain     bbudl1r                            90
     c                   add       l6tote        busalg
     c                   add       l6totk        bukost
      *
      * Oppdatering av BUDSJETT-REGISTER
      *
     c                   if        ldavst = 1
     c                   if        *in90 = *off
     c                   update    bbudl1r
     c                   endif
     c                   endif
      *
      * Legger inn budsjett i utfelt
      *
     c                   z-add     bukron        l6bude
      *
      * Beregning av sum eksl. mva.
      *
     c                   z-add     busalg        l6hite
      *
      * Beregning av sum inkl. mva.
      *
     c*    busalg        mult(h)   mvatil        l6hiti
     c                   eval      l6hiti = 0
      *
      * Beregning av kostverdi
      *
     c                   z-add     bukost        l6hitk
      *
      * Beregning av dekningsbidrag
      *
     c     l6hite        sub       l6hitk        l6hitd
      *
      * Kaller opp subrutine for beregning av DB-%
      *
     c                   z-add     l6hite        wdbelp
     c                   z-add     l6hitd        wddekn
     c                   exsr      dekn
     c                   z-add     wdtotg        l6hitg
      *
      * Beregner Budsjett hittil i perioden
      *
     c                   eval      l6bude = *zero
     c                   if        buvdag <> *zero
     c     bukron        div       buvdag        l6bude
     c                   mult      ldvdag        l6bude
     c                   endif
      *
     c     l6hite        sub       l6bude        l6avie
      *
      * Kaller opp subrutine for beregning av DB-%
      *
     c                   z-add     l6bude        wdbelp
     c                   z-add     l6hite        wddekn
     c                   exsr      dekn
     c                   z-add     wdtotg        l6oppe
      *
      * Legger inn opplysninger i utfelter
      *
     c                   eval      l6firm = l_firm
     c                   eval      l6peri = wqperi
     c                   eval      l6vdag = ldvdag
      *
     c                   if        lddato <> *loval
     c     *dmy          move      lddato        l6dat1
     c     *dmy          move      lddato        l6dat2
     c     *dmy          move      lddato        l6dat3
     c                   endif
6.23  *
6.23 c                   eval      l7depo = wwdepo
      *
      * Skriv
      *
     c                   if        *in32 = *on
     c                   write     l6tot                                30
6.23 c                   write     l7tot                                30
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * DEKN      Beregning av dekningsgrad
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dekn          begsr
      *
      * Legger inn opplysninger i utfelter
      *
     c                   if        wdbelp <> *zero
     c     wddekn        div       wdbelp        wdgrax
     c     wdgrax        mult      100           wdtotg
     c                   else
     c                   eval      wdtotg = *zero
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * SKHEAD    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skhead        begsr
      *
      * Sjekker om omsetning er eks. eller inkl. kreditsalg
      *
     c                   if        ldkred = 1
     c                   eval      *in42 = *on
     c                   else
     c                   eval      *in42 = *off
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Snu dato          (Dato inkl. hundrår)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     snudat        begsr
      *
     c                   movel     wsdato        wsaar
     c                   move      wsdato        wsmda
     c                   movel     wsmda         wsmnd
     c                   move      wsmda         wsdag
     c                   movel     wsdag         wsmda
     c                   eval      wsmda = wsmnd
     c                   movel     wsmda         wsdato
     c                   eval      wsdato = wsaar
      *
     c                   endsr
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.31 c**                 eval      p_btyp = 'DAGSOPPGJØR-DEKNING'
6.31 c                   eval      p_btyp = 'DAGSOPPGJØR DEKNING'
6.21 c                   eval      p_dspl = 'Avstemmingsliste - dekning '+
6.30 c                                      %char(a1list) +
6.30 c                                      ' Periode ' + %char(ldperi)  +
6.30 c                                      '  Gruppe ' + %trim(a1wsgr)
6.22 c**                 eval      p_refn = 'BO126-'+%char(a1list)
6.22 c                   eval      p_refn = %char(w_year)+%char(a1list)
6.21 c                   eval      p_tagg0 = 'NUMMER'
6.21 c                   eval      p_tagg0val = %char(a1list)
6.21 c                   eval      p_tagg1 = 'PERIODE'
6.21 c                   eval      p_tagg1val = %char(a1peri)
6.21 c                   eval      p_tagg2 = 'TOTALSUM'
6.21 c                   eval      p_tagg2val = %char(l6hitg)
6.21 c                   eval      p_tagg3 = *blanks
6.21 c                   eval      p_tagg3val = *blanks
6.21 c                   eval      p_tagg4 = *blanks
6.21 c                   eval      p_tagg4val = *blanks
6.21 c                   eval      p_tagg5 = *blanks
6.21 c                   eval      p_tagg5val = *blanks
6.21 c                   eval      p_tagg6 = *blanks
6.21 c                   eval      p_tagg6val = *blanks
6.21 c                   eval      p_tagg7 = *blanks
6.21 c                   eval      p_tagg7val = *blanks
6.21 c                   eval      p_tagg8 = *blanks
6.21 c                   eval      p_tagg8val = *blanks
6.21 c                   eval      p_tagg9 = *blanks
6.21 c                   eval      p_tagg9val = *blanks
pdf   *
6.21 c**                 call      'AP620R'
6.21 c                   call      'AP627R'
pdf  c                   PARM                    splfname
pdf  c                   PARM                    jobname
pdf  c                   PARM                    username
pdf  c                   PARM                    jobnbr
pdf  c                   PARM                    splfnbr
pdf  c                   PARM                    splfname2
pdf  c                   PARM                    jobname2
pdf  c                   PARM                    username2
pdf  c                   PARM                    jobnbr2
pdf  c                   PARM                    splfnbr2
pdf  c                   PARM                    splfname3
pdf  c                   PARM                    jobname3
pdf  c                   PARM                    username3
pdf  c                   PARM                    jobnbr3
pdf  c                   PARM                    splfnbr3
6.21 c                   parm                    p_tagg0
6.21 c                   parm                    p_tagg0val
6.21 c                   parm                    p_tagg1
6.21 c                   parm                    p_tagg1val
6.21 c                   parm                    p_tagg2
6.21 c                   parm                    p_tagg2val
6.21 c                   parm                    p_tagg3
6.21 c                   parm                    p_tagg3val
6.21 c                   parm                    p_tagg4
6.21 c                   parm                    p_tagg4val
6.21 c                   parm                    p_tagg5
6.21 c                   parm                    p_tagg5val
6.21 c                   parm                    p_tagg6
6.21 c                   parm                    p_tagg6val
6.21 c                   parm                    p_tagg7
6.21 c                   parm                    p_tagg7val
6.21 c                   parm                    p_tagg8
6.21 c                   parm                    p_tagg8val
6.21 c                   parm                    p_tagg9
6.21 c                   parm                    p_tagg9val
pdf  c                   PARM                    l_bach
pdf  c                   PARM                    p_btyp
6.21 c                   parm                    p_refn
6.21 c                   parm                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wpretk = *blank
     c                   eval      wsdato = *zero
     c                   eval      wsaar = *zero
     c                   eval      wsmda = *zero
     c                   eval      wsmnd = *zero
     c                   eval      wsdag = *zero
      *
     c                   eval      w_dato_alf = *blank
     c                   eval      w_mda = *blank
      *
     c                   eval      wdgrax = *zero
      *
      * Key for file : RUTINE-REGISTER
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for file : Overgruppe-register
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for file : Hovedgruppe-register
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : LINJETYPE-REGISTER
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for file : BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : BUDSJETT-REGISTER
      *
     c     bbudl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbudl1_aarr
     c                   kfld                    bbudl1_mmnd
      *
      * Key for file : BONG-LINJE-REGISTER
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_numm
     c                   kfld                    bodtl1_line
5.31  *
5.31  *  Key for les av BONG-HODE
5.31  *
5.31 c     bohel1_key    klist
5.31 c                   kfld                    w_firm
5.31 c                   kfld                    bohel1_aarr
5.31 c                   kfld                    bohel1_wsid
5.31 c                   kfld                    bohel1_bong
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Legger inn parameter-record i hjelpefelter i programmet
      *
     c                   eval      ldprec = p_parm
pdf  c                   open      bo126p
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      a1firm = l_firm
      *
      * Sjekk, Er dette avstemmingsliste
      *
     c                   move      *off          *in31
     c                   if        ldavst = 1
     c                   move      *on           *in31
     c                   endif
      *
      * Sjekk, Skal denne listen skrives ut
      *
     c                   move      *off          *in32
     c                   if        lddekn = 1
     c                   move      *on           *in32
     c                   endif
      *
      * Snur periode og kjøredato til utfelter
      *
     c                   movel     ldperi        wsdato
     c                   exsr      snudat
     c                   move      wsdato        wqperi
      *
      * Legger inn opplysninger i utfelter
      *
     c                   eval      a1wsid = l_wsid
     c                   eval      a1user = l_user
     c                   eval      a1fnav = l_fnav
      *
     c                   eval      a1date = udate
6.22 c                   eval      w_year = *year
     c                   time                    a1time
     c                   eval      a1peri = wqperi
      *
     c                   eval      a1list = ldlavs
     c                   eval      l6list = ldlavs
6.30 c                   eval      a1wsgr = ldwsgr
      *
      * Legger inn periode i budsjett-nøkkel
      *
     c                   movel     ldperi        bbudl1_aarr
     c                   move      ldperi        bbudl1_mmnd
      *
     c                   if        bbudl1_aarr = *zero
     c                   move      lddato        w_dato_alf
     c                   movel     w_dato_alf    bbudl1_aarr
     c                   move      w_dato_alf    w_mda
     c                   movel     w_mda         bbudl1_mmnd
     c                   endif
      *
      * Hent opplysninger fra BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    chain     bstsl1r                            90
      *
      * Hent opplysninger fra RUTINE-REGISTER
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r                            90
     c                   if        *in90 = *off
     c                   eval      l6mld1 = afmld1
     c                   endif
      *
     c                   endsr
