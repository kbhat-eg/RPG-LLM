     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU727R
      * Beskrivelse . . : Butikk, eksport av vareliste - linje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210404 HKO  Nytt program
5.70  * PRGR  121208 bof  Utvidet med prisgruppe
6.21  * 6.20  190511 b√∏   Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvawpf    if   e           k disk
      *
     fboldst    uf a e           k disk    rename(boldst:boldstr)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vsor          s             10
      *
      * Definering av variable
      *
     d b_svgr          s              1
     d b_sldo          s              1
     d b_stek          s              1
6.21 d b_inlr          s              1    inz(*off)
      *
     d w_lety          s              1  0
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_ldor          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
     d w_firm          s                   like(vvfirm)
      *
     d s_vare          s                   like(vvvare)
      *
5.70 d                uds
5.70 d l_user                911    920
5.70 d l_sfir                944    946  0
5.70 d l_savd                947    950  0
5.70 d l_lagr               1001   1002  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kaller utplukksprogram
      *
     c                   eval      b_svgr = *off
     c                   eval      b_sldo = *off
     c                   eval      b_stek = *on
      *
     c                   call      'VD700R'
     c                   parm                    w_firm
     c                   parm                    p_vsor
     c                   parm                    b_svgr
     c                   parm                    b_sldo
     c                   parm                    b_stek
     c                   parm                    w_lage
      *
      * Leser utplukksfil som inneholder alle varer i sortimentet
      *
     c                   eval      s_vare = *blank
      *
     c                   read      vvawpf
     c                   dow       not %eof
      *
     c                   if        vvvare <> s_vare
     c                   exsr      behandling
     c                   endif
      *
     c                   eval      s_vare = vvvare
      *
     c                   read      vvawpf
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av linje-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   clear                   boldstr
      *
     c                   eval      firm = w_firm
     c                   eval      list = p_vsor
     c                   eval      tek1 = vvtek1
     c                   eval      vare = vvvare
6.30 c                   if        w_lv_nobb > 0
6.30 c                   eval      nobb = w_lv_nobb
6.30 c                   eval      lvar = w_lv_llva
6.30 c                   else
     c                   eval      nobb = vvnobb
     c                   eval      lvar = vvlvar
6.30 c                   endif
     c                   eval      enhe = *blank
     c                   eval      sapr = 0
     c                   eval      kpri = vvkpri
     c                   eval      vtyp = w_vtyp
     c                   eval      odat = vvodat
     c                   eval      edat = vvedat
     c                   eval      etim = vvetim
     c                   eval      eusr = vveusr
      *
      * Henter pris og enhet
      *
     c                   eval      w_lety = 0
      *
     c                   time                    w_dato
     c                   time                    w_time
      *
     c                   call      'VP710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_time
     c                   parm                    enhe
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
      *
     c                   select
     c                   when      w_tipr <> 0
     c                   eval      sapr = w_tipr
     c                   when      w_bupr <> 0
     c                   eval      sapr = w_bupr
     c                   other
     c                   eval      sapr = w_sapr
     c                   endsl
      *
     c                   write     boldstr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vsor
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
5.70 c                   eval      w_lage = l_lagr
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c                   endsr
