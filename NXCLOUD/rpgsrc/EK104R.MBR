     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EK104R
      * Beskrivelse . . : Kunde bonus, vedlikehold - bonustyper
      *
      *                   Velger kunde fra liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170611  bsa Nytt program
6.20  * 6.20  060312  bsa Viser evnt negative bonuser. Endringer i skjermbildet
6.21  * 6.20  280513  bsa Legger ut periode ved rekalkulering av bonustype
6.22  * 6.20  261113  bsa Legger ut fra periode ved bonusberegning
6.23  * 6.20  140116  bsa Returnerer til feil tag.
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekbtir    if   e           k disk    rename(ekbtst:ekbtirr)
     fekbtiu    uf a e           k disk    rename(ekbtst:ekbtiur)
     fekbti1    uf a e           k disk    rename(ekbtst:ekbti1r)
     fekbeiu    uf a e           k disk    rename(ekbest:ekbeiur)
     fekboiu    uf a e           k disk    rename(ekbost:ekboiur)
     fekboic    uf   e           k disk    rename(ekbost:ekboicr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
      *
     fek104d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d  l_dato               101    108
     d  l_tek1               201    270
     d  l_tek2               301    370
     d  l_tek3               401    470
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vbotl1_tbot     s                   like(vatbot)
     d rkunlr_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
     d rkunl2_alfa     s                   like(rkalfa)
     d ekbtir_tbku     s                   like(ektbku)
     d ekbtiu_tbku     s                   like(ektbku)
     d ekbtiu_tbot     s                   like(ektbot)
     d ekbti1_tbku     s                   like(ektbku)
     d ekbti1_tbot     s                   like(ektbot)
      *
     d ekbeiu_ebku     s                   like(ekebku)
     d ekbeiu_erka     s                   like(ekerka)
     d ekbeiu_ekun     s                   like(ekekun)
     d ekbeiu_ekpr     s                   like(ekekpr)
     d ekbeiu_eogr     s                   like(ekeogr)
     d ekbeiu_ehgr     s                   like(ekehgr)
     d ekbeiu_eugr     s                   like(ekeugr)
     d ekbeiu_emod     s                   like(ekemod)
     d ekbeiu_eldo     s                   like(ekeldo)
     d ekbeiu_evar     s                   like(ekevar)
     d ekbeiu_eenh     s                   like(ekeenh)
     d ekbeiu_elet     s                   like(ekelet)
     d ekbeiu_ebot     s                   like(ekebot)
      *
     d ekboiu_bkun     s                   like(ekbkun)
      *
     d ekboic_bkun     s                   like(ekbkun)
     d ekboic_boty     s                   like(ekboty)
      *
     d p_kund          s                   like(ektbku)
6.21 d w_peri          s              4  0
6.22 d w_fper          s              4  0
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_kund_alf      s              6
     d w_paar_alf      s              4
     d w_m1ar          s              4  0
     d w_naar          s              4  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
     d w_firm          s                   like(ektfir)
     d w_navn          s                   like(rknavn)
     d w_bkun          s                   like(ektbku)
     d w_tbot          s                   like(ektbot)
     d w_tbes          s                   like(vatbes)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * K1WIN  - K1 Vindu for kopiere
      * O1WIN  - O1 Vindu for starte beregnign av årsbonus for kunde
      * M1WIN  - M1 Vindu for innlegging av prosenter for masseoppdatering
      * L1WIN  - L1 Vindu for innlegging av tidsrom for masseoppdatering
      * P1WIN  - P1 Vindu for starte beregnign av årsbonus for alle kunder
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
6.23 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F23=Vis alle / kun med bonus
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
     c*                  if        b2alfa <> *blank or
     c*                            b2kund <> 0
     c*                  exsr      posisjoner
     c*                  goto      b2tagb
     c*                  endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1bkun = b2bkun
     c                   eval      c1bkna = b2bkna
     c                   eval      c1tbot = *blanks
     c                   eval      c1tbes = *blanks
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    w_tbot
     c                   parm                    w_tbes
      *
     c                   if        w_tbot <> *blank
     c                   eval      c1tbot = w_tbot
     c                   eval      c1tbes = w_tbes
     c                   endif
      *
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1tbot = *blank
     c                   goto      end_c1bld
     c                   endif
      * Sjekk at intastet type er gyldig
     c                   exsr      sjk_input
      *
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      c2tbot = c1tbot
     c                   eval      c2tbes = c1tbes
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c**                 eval      b2tbot = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ekbti1_tbot = c1tbot
     c                   eval      ekbti1_tbku = p_kund
     c     ekbti1_key    chain     ekbti1
      *
     c                   if        %found
     c                   unlock    ekbti1
     c                   eval      c2tsbo = ektsbo
     c                   eval      c2tbgr = ektbgr
     c                   eval      c2tbda = ektbda
     c                   eval      c2teus = ekteus
     c                   if        ekteda <> *loval
     c     *dmy          move      ekteda        c2teda
     c                   endif
     c                   if        ektoda <> *loval
     c     *dmy          move      ektoda        c2toda
     c                   endif
     c                   else
     c                   eval      c2tbgr = 0
     c                   eval      c2tsbo = 0
     c                   eval      c2tbda = *loval
     c                   eval      c2teus = *blank
     c                   eval      c2teda = 0
     c                   eval      c2toda = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c*                  if        c2tbot = *blank
     c*                  eval      *in31 = *on
     c*                  goto      c2taga
     c*                  endif
      *
      * Oppdatering
      *
     c                   clear                   ekbtiur
      *
     c                   eval      ekbtiu_tbku = p_kund
     c                   eval      ekbtiu_tbot = c2tbot
     c     ekbtiu_key    chain     ekbtiur
      *
     c                   time                    ekteda
     c                   time                    ekteti
     c                   eval      ekteus = l_user
     c                   eval      ektkka = rkkatg
      *
     c                   if        %found
     c                   update    ekbtiur
     c                   else
     c                   eval      ektfir = w_firm
     c                   eval      ektbot = c1tbot
     c                   eval      ektbku = p_kund
     c                   time                    ektoda
     c                   time                    ektoti
     c                   eval      ektous = l_user
     c                   write     ekbtiur
     c                   endif
      *
      * Oppdater subfile
      *
     c*                  if        b1valg = 2
     c*                  eval      b1tbes = vatbes
     c*                  endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1tbot = b1tbot
     c                   eval      d1tbes = b1tbes
     c                   eval      d1kund = b2bkun
     c                   movel     b2bkna        d1bkna
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      * Så tar vi alle underliggende linjer på EKBEST og EKBOST
     c                   exsr      slett_bonus
      *
      * Sletter rad
      *
     c                   eval      ekbtiu_tbku = d1kund
     c                   eval      ekbtiu_tbot = d1tbot
     c     ekbtiu_key    chain     ekbtiur
     c                   delete    ekbtiur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_input     begsr
      *
     c                   eval      b_feil = *off
     c                   eval      *in31 = *off
      *
     c                   eval      vbotl1_tbot = c1tbot
     c     vbotl1_key    chain     vbotl1
      *
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c*                  select
     c*                  when      w_seqe = 'K'
     c*                  eval      rkunl1_kund = b1kund
     c*    rkunl1_key    setll     rkunl1
     c*                  other
     c*                  eval      rkunl2_alfa = b1alfa
     c*    rkunl2_key    setll     rkunl2
     c*                  endsl
     c                   eval      ekbtir_tbku = p_kund
     c     ekbtir_key    setll     ekbtir
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på navn/alfa
      *
     c*                  if        b2alfa <> *blank
     c*                  eval      w_seqe = *blank
     c*                  eval      rkunl2_alfa = b2alfa
     c*    rkunl2_key    setll     rkunl2
     c*                  goto      end_pos
     c*                  endif
      *
      * Posisjoner startverdi på nummer
      *
     c*                  if        b2kund <> 0
     c*                  eval      w_seqe = 'K'
     c*                  eval      rkunl1_kund = b2kund
     c*    rkunl1_key    setll     rkunl1
     c*                  goto      end_pos
     c*                  endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c*                  eval      b2alfa = *blank
     c*                  eval      b2kund = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise
      *
     c                   if        b1valg = 5
     c                   eval      c2tbot = b1tbot
     c                   eval      c1tbot = b1tbot
     c                   eval      c2tbes = b1tbes
     c                   eval      c1bkun = b2bkun
     c                   eval      c1bkna = b2bkna
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Bonus
      *
     c                   if        b1valg = 7
     c                   call      'EK101R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    b1tbot
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endre bonustid på enkeltkunde
      *
     c                   if        b1valg = 8
     c                   exsr      endre_tid
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kalkuler årsbonus
      *
     c                   if        b1valg = 9
     c                   exsr      kalk_bonus
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for slette bonus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_bonus   begsr
      *
      * Sletter bonus - les fra elementene
      *
     c                   eval      l1tbot = *blank
     c                   eval      l1stda = 0
     c                   eval      l1slda = 0
     c     ekboic_key    setll     ekboic
     c     ekboic_key    reade     ekboic
     c                   dow       not %eof(ekboic)
      * Slett først underliggende bonusdetaljer
     c                   eval      ekebku = ekbkun
     c                   eval      ekerka = ekrkat
     c                   eval      ekekun = ekkund
     c                   eval      ekekpr = ekkpro
     c                   eval      ekeogr = ekogrp
     c                   eval      ekehgr = ekhgrp
     c                   eval      ekeugr = ekugrp
     c                   eval      ekeldo = ekldor
     c                   eval      ekemod = ekmodn
     c                   eval      ekevar = ekvare
     c                   eval      ekeenh = ekenhe
     c                   eval      ekelet = eklety
     c                   eval      ekebot = ekboty
     c     ekbeiu_key    setll     ekbeiu
     c     ekbeiu_key    reade     ekbeiu
     c                   dow       not %eof(ekbeiu)
     c                   delete    ekbeiu
     c     ekbeiu_key    reade     ekbeiu
     c                   enddo
      * Slett så elementet
     c                   delete    ekboic
     c     ekboic_key    reade     ekboic
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for masse endring av bonus tidsrom
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endre_tid     begsr
      *
      * Fyller bildet
      *
     c                   eval      l1tbot = *blank
     c                   eval      l1stda = 0
     c                   eval      l1slda = 0
     c                   eval      l1tbot = b1tbot
     c                   eval      l1kund = b2bkna
      *
     c     l1taga        tag
     c                   exfmt     l1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Leser alle poster i bonusfile og oppdaterer
      *
     c                   eval      ekboiu_bkun = b2bkun
      *
     c     ekboiu_key    setll     ekboiu
     c                   read      ekboiu
      *
     c                   dow       not %eof(ekboiu)
     c                   if        ekbkun <> b2bkun
     c                   goto      end_tid
     c                   endif
      * Hvis lest bonustype ikke er lik lest bonustype, les ny.
6.11 c                   if        l1tbot <> *blank and
6.11 c                             l1tbot <> ekboty
6.11 c                   goto      ny_les
6.11 c                   endif
      *
      * Leser tilhørende bonuselementer
      *
     c                   eval      ekbeiu_ebku = ekbkun
     c                   eval      ekbeiu_erka = ekrkat
     c                   eval      ekbeiu_ekun = ekkund
     c                   eval      ekbeiu_ekpr = ekkpro
     c                   eval      ekbeiu_eogr = ekogrp
     c                   eval      ekbeiu_ehgr = ekhgrp
     c                   eval      ekbeiu_eugr = ekugrp
     c                   eval      ekbeiu_eldo = ekldor
     c                   eval      ekbeiu_emod = ekmodn
     c                   eval      ekbeiu_evar = ekvare
     c                   eval      ekbeiu_eenh = ekenhe
     c                   eval      ekbeiu_elet = eklety
     c                   eval      ekbeiu_ebot = ekboty
     c     ekbeiu_key    setll     ekbeiu
     c     ekbeiu_key    reade     ekbeiur
      *
     c                   dow       not %eof(ekbeiu)
      *
      * Oppdaterer tidsrom på bonuselement
      *
     c                   eval      ekefda = %date(l1stda:*dmy)
     c                   eval      eketda = %date(l1slda:*dmy)
     c                   eval      ekeeda = w_date
     c                   eval      ekeeti = w_time
     c                   eval      ekeeus = l_user
     c                   update    ekbeiur
      *
     c     ekbeiu_key    reade     ekbeiur
     c                   enddo
      *
     c                   eval      ekfdat = %date(l1stda:*dmy)
     c                   eval      ektdat = %date(l1slda:*dmy)
     c                   eval      ekedat = w_date
     c                   eval      eketim = w_time
     c                   eval      ekeusr = l_user
     c                   update    ekboiur
      *
     c     ny_les        tag
      *
     c                   read      ekboiu
     c                   enddo
      *
     c     end_tid       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulering av årsbonus for kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_bonus    begsr
      *
      * Fyller bildet
      *
     c                   eval      o1tbot = *blank
     c                   eval      o1kund = b2bkun
     c                   eval      o1knav = b2bkna
     c                   eval      o1tbot = b1tbot
      *
     c                   time                    w_udat
     c     *iso0         move      w_udat        w_udat_8
     c                   movel     w_udat_8      o1paar
      *
6.21 c                   if        umonth-1 > 9
6.21 c                   eval      o1peri = %char(umonth-1)
6.21 c                   else
6.21 c                   eval      o1peri = '0' + %char(umonth-1)
6.21 c                   endif
6.22 c                   eval      o0peri = '01'
      *
     c     o1tagb        tag
      *
     c                   exfmt     o1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekker input
      *
     c                   if        o1paar = 0
     c                   movel     w_udat_8      o1paar
     c                   endif
      *
     c                   if        o1paar < 2000
     c                   eval      *in31 = *on
     c                   goto      o1tagb
     c                   endif
6.22  * Periode
6.22 c                   move      o0peri        w_peri
6.22 c                   if        w_peri < 1   or
6.22 c                             w_peri > 12
6.22 c                   eval      *in32 = *on
6.22 c                   goto      o1tagb
6.22 c                   endif
6.21  * Periode
6.21 c                   move      o1peri        w_peri
6.21 c                   if        w_peri < 1   or
6.21 c                             w_peri > 12
6.22 c                   eval      *in32 = *on
6.21 c                   goto      o1tagb
6.21 c                   endif
6.22  * Periodesjekk
6.22 c                   if        %int(o0peri) > %int(o1peri)
6.22 c                   eval      *in33 = *on
6.22 c                   goto      o1tagb
6.22 c                   endif
      *
      * Kaller kalkulasjonsprogram
      *
     c                   movel     w_firm        w_firm_alf
     c                   movel     b2bkun        w_kund_alf
     c                   movel     o1paar        w_paar_alf
     c                   eval      w_tbot = o1tbot
      *
     c                   call      'EK710C'
     c                   parm                    w_firm_alf
     c                   parm                    w_kund_alf
     c                   parm                    w_paar_alf
     c                   parm                    w_tbot
6.22 c                   parm                    o0peri
6.21 c                   parm                    o1peri
      *
      * Oppdaterer subfile
      *
     c**                 eval      b1sbon = 0
     c**                 eval      b1bdat = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c*                  select
     c*                  when      w_seqe = 'K'
     c*                  read      rkunl1
     c*                  other
     c                   read      ekbtir
     c*                  endsl
      *
     c                   if        %eof or
     c                             ektfir <> w_firm or
     c                             ektbku <> p_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c*                  select
     c*                  when      w_seqe = 'K'
     c*                  eval      rkunl1_kund = rkkund
     c*                  other
     c*                  eval      rkunl2_alfa = rkalfa
     c*                  endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1tbot = ektbot
     c                   eval      b1tsbo = ektsbo
     c                   eval      b1tbgr = ektbgr
     c                   if        ektbda <> *loval
     c     *dmy          move      ektbda        b1tbda
     c                   else
     c                   eval      b1tbda = 0
     c                   endif
      *
     c                   eval      vbotl1_tbot = ektbot
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   eval      b1tbes = vatbes
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c*                  if        w_seqe = 'K'
     c*    rkunl1_key    setgt     rkunl1
     c*                  readp     rkunl1
     c*                  else
     c*    rkunl2_key    setgt     rkunl2
     c                   readp     ekbtir
     c*                  endif
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c*                  if        w_seqe = 'K'
     c*                  readp     rkunl1
     c*                  else
     c                   readp     ekbtir
     c*                  endif
      *
     c                   if        %eof or
     c                             ektfir <> w_firm or
     c                             ektbku <> p_kund
     c                   leave
     c                   endif
      *
     c*                  eval      ekboi1_bkun = rkkund
     c*    ekboi1_key    chain     ekboi1
     c*                  if        not %found and
     c*                            *in80 = *off
     c*                  iter
     c*                  endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c*                  if        w_seqe = 'K'
     c*                  eval      rkunl1_kund = rkkund
     c*                  else
     c*                  eval      rkunl2_alfa = rkalfa
     c*                  endif
      *
     c                   enddo
      *
     c                   if        %eof
     c*                  if        w_seqe = 'K'
     c*    rkunl1_key    setll     rkunl1
     c*                  else
     c     ekbtir_key    setll     ekbtir
     c                   endif
     c*                  endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_kund
      *
      * Key for oppslag på bonustype
      *
     c     ekbtir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbtir_tbku
      *
      * Key for oppslag på bonustype
      *
     c     ekbti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti1_tbku
     c                   kfld                    ekbti1_tbot
      *
      * Key for oppslag på bonustype
      *
     c     ekbtiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbtiu_tbku
     c                   kfld                    ekbtiu_tbot
      *
      * Key for oppdatering av kunde bonuselement
      *
     c     ekbeiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbeiu_ebku
     c                   kfld                    ekbeiu_erka
     c                   kfld                    ekbeiu_ekun
     c                   kfld                    ekbeiu_ekpr
     c                   kfld                    ekbeiu_eogr
     c                   kfld                    ekbeiu_ehgr
     c                   kfld                    ekbeiu_eugr
     c                   kfld                    ekbeiu_eldo
     c                   kfld                    ekbeiu_emod
     c                   kfld                    ekbeiu_evar
     c                   kfld                    ekbeiu_eenh
     c                   kfld                    ekbeiu_elet
     c                   kfld                    ekbeiu_ebot
      *
      * Key for oppdatering av bonus
      *
     c     ekboiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboiu_bkun
      *
      * Key for oppdatering av bonus
      *
     c     ekboic_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboic_bkun
     c                   kfld                    ekboic_boty
      *
      * Key for posisjonering på kundenr
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på kundenr
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *off
      *
     c                   eval      b2bkun = p_kund
     c                   eval      b2bkna = *blanks
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in80 = *on
     c                   else
     c                   eval      b2bkna = rknavn
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ekbtir_tbku = p_kund
     c     ekbtir_key    setll     ekbtir
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
