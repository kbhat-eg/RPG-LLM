     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MP100R
      * Beskrivelse . . : Parameter pr. lev. - Bestillingsforslag.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210404 bø   Nytt program
5.51  * 5.51  190508 tsv  Viser lager på subfila
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  040909 bof  Utvidet med styring på kjedesortiment
6.20  * 6.10  130511 bsa  Utvidet med styring av jobb for varsling av innkjøper
6.30  * 6.30  280814 bof  Sjekk om leverandør er pasiv/sperret
      *
7.xx  * 7.00  050617 bkv  GUI tilpassinger
7.01  * 7.00  041119 bkv  Vasker vekk probl Newlook tegn
7.02  * 7.00  131219 bkv  Søk på leverandør
7.03  * 7.00  310120 bsa  Feil i feltverdier etter retur fra F taster
7.04  * 7.00  120220 bkv  Mulighet til å filtrere på levnr,selger og lager
7.05  *K0000  110920 bsa  Overgang til vedlikehold av lev. hvis bestforslag
7.05  *K0000              kode er satt til 2. (div koder)
7.06  * 7.00  160221 sst  Legge inn nytt felt for start tidspunkt.
      *
8.01  *       281223 bof  Logge i SLOGPF hvis sletting
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmppal1    if   e           k disk    rename(mppapfr:mppal1r)
     fmppal2    if   e           k disk    rename(mppapfr:mppal2r)
     fmppal3    if   e           k disk    rename(mppapfr:mppal3r)
     fmppal4    if   e           k disk    rename(mppapfr:mppal4r)
     fmppalr    if   e           k disk    rename(mppapfr:mppalrr)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     fmppalu    uf a e           k disk    rename(mppapfr:mppalur)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
6.11 flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
7.05 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
7.06 fxtillu    uf a e           k disk    rename(xtilpfr:xtillur)
8.01 fsloglu    uf a e           k disk    rename(slogpfr:sloglur)
      *
     fmp100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
     d p_ldor          s                   like(mpldor)
     d p_lnum          s                   like(mplnum)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d mppal1_ldor     s                   like(mpldor)
     d mppal2_lsor     s                   like(mplsor)
     d mppal3_selg     s                   like(mpselg)
     d mppal4_besk     s                   like(mpbesk)
     d mppalr_ldor     s                   like(mpldor)
     d mppalr_lnum     s                   like(mplnum)
     d mppalu_ldor     s                   like(mpldor)
     d mppalu_lnum     s                   like(mplnum)
     d ausrl1_user     s                   like(abuser)
     d ra09l1_kod      s                   like(raikod)
7.06 d xtillu_firm     s                   like(xtfirm)
7.06 d xtillu_regi     s                   like(xtregi)
7.06 d xtillu_felt     s                   like(xtfelt)
7.06 d xtillu_xkey     s                   like(xtxkey)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(mpfirm)
     d w_retk          s              1
     d w_levr          s                   like(mpldor)
     d w_navn          s             30
     d w_jkod          s                   like(mplage)
     d w_stat          s              1
     d w_jtxt          s             30
     d w_zkod          s                   like(mppran)
     d w_ztxt          s             30
     d w_ikod          s                   like(mpselg)
     d w_itxt          s             30
     d w_ogrp          s                   like(mpogrf)
     d w_hgrp          s                   like(mphgrf)
     d w_ugrp          s                   like(mpugrf)
     d w_ldor          s                   like(mpldor)
     d w_lnum          s                   like(mplnum)
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
7.04 d w_lnav          s             20
      *
     d s_ldor          s                   like(mpldor)
     d s_lnum          s                   like(mplnum)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
7.04 c                   when      *inka
7.04 c                   exsr      xh1win
7.04 c                   exsr      forny
7.04 c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
7.05 c                   when      *inkg
7.05 c                   exsr      xc7bld
7.05 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0 or
     c                             b2lsor <> *blank or
     c                             b2selg <> 0 or
     c                             b2besk <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      mppal4_besk = b1besk
     c     mppal4_key    setll     mppal4
     c                   when      w_seqe = 'C'
     c                   eval      mppal3_selg = b1selg
     c     mppal3_key    setll     mppal3
     c                   when      w_seqe = 'B'
     c                   eval      mppal2_lsor = b1lnav
     c     mppal2_key    setll     mppal2
     c                   other
     c                   eval      mppal1_ldor = b1ldor
     c     mppal1_key    setll     mppal1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leverandørnr.
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      mppal1_ldor = b2ldor
     c     mppal1_key    setll     mppal1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2lsor <> *blank
     c                   eval      w_seqe = 'B'
     c                   movel     b2lsor        mppal2_lsor
     c     mppal2_key    setll     mppal2
     c                   goto      end_pos
     c                   endif
      *
      * Selger
      *
     c                   if        b2selg <> 0
     c                   eval      w_seqe = 'C'
     c                   eval      mppal3_selg = b2selg
     c     mppal3_key    setll     mppal3
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2besk <> *blank
     c                   eval      w_seqe = 'D'
     c                   eval      mppal4_besk = b2besk
     c     mppal4_key    setll     mppal4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
     c                   eval      b2lsor = *blank
     c                   eval      b2selg = 0
     c                   eval      b2besk = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lnum = b1lnum
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ldor = b1ldor
     c                   eval      k1lnum = b1lnum
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ldor = b1ldor
     c                   eval      d1lnum = b1lnum
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lnum = b1lnum
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 7
      * nullstiller dato, slik at den kan kjøres med en gang.
     c                   eval      mppalu_ldor = b1ldor
     c                   eval      mppalu_lnum = b1lnum
     c     mppalu_key    chain     mppalur
     c                   if        %found(mppalu)
     c                   eval      mpsdat = *loval
6.10 c                   time                    mpedat
6.10 c                   time                    mpetim
6.10 c                   eval      mpeusr = l_user
     c                   update    mppalur
     c                   endif
      *
     c                   eval      p_ldor = b1ldor
     c                   eval      p_lnum = b1lnum
     c                   call      'MP703R  '
     c                   parm                    p_ldor
     c                   parm                    p_lnum
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1lnum = 0
     c                   eval      c1ldor = b2ldor
      *
     c     c1taga        tag
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1ldor = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   eval      w_retk = *blank
     c                   eval      w_levr = c1ldor
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
6.30  *
6.30 c                   if        w_retk = 'P' or
6.30 c                             w_retk = 'S'
6.30 c                   eval      *in33  = *on
6.30 c                   goto      c1taga
6.30 c                   endif
      *
     c                   if        w_retk <> *blank
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1lnum = 0
     c                   eval      w_ldor = c1ldor
     c                   exsr      finn_num
     c                   eval      c1lnum = w_lnum
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      mppalr_ldor = c1ldor
     c     mppalr_key    chain     mppalr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ldor = 0
     c                   eval      c1lnum = 0
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ldor = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      mppalr_ldor = c1ldor
     c                   eval      mppalr_lnum = c1lnum
     c     mppalr_key    chain     mppalr
      *
     c                   if        %found
     c                   eval      w_retk = *blank
     c                   eval      w_levr = mpldor
     c                   eval      w_navn = *blank
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
     c                   movel     w_navn        c1lnav
     c                   eval      c2ogrf = mpogrf
     c                   eval      c2ogrt = mpogrt
     c                   eval      c2hgrf = mphgrf
     c                   eval      c2hgrt = mphgrt
     c                   eval      c2ugrf = mpugrf
     c                   eval      c2ugrt = mpugrt
     c                   eval      c2varf = mpvarf
     c                   eval      c2vart = mpvart
     c                   eval      c2pran = mppran
     c                   eval      c2thor = mpthor
     c                   eval      c2lage = mplage
     c                   eval      c2vval = mpvval
     c                   eval      c2sats = mpsats
     c                   eval      c2aval = mpaval
     c                   eval      c2besk = mpbesk
     c                   eval      c3hypp = mphypp
     c                   eval      c3dag1 = mpdag1
     c                   eval      c3dag2 = mpdag2
     c                   eval      c3dag3 = mpdag3
     c                   eval      c3dag4 = mpdag4
     c                   eval      c3dag5 = mpdag5
     c                   eval      c3dag6 = mpdag6
     c                   eval      c3dag7 = mpdag7
     c                   eval      c2gvek = mpgvek
     c                   eval      c2gbel = mpgbel
     c                   eval      c2mail = mpmail
     c                   eval      c2selg = mpselg
     c                   eval      c2rebe = mprebe
     c                   eval      c2kora = mpkora
6.11 c                   eval      c2ksor = mpksor
     c                   eval      c2user = mpuser
6.20 c                   eval      c4rapp = mprapp
6.20 c                   eval      c4ab01 = mpab01
6.20 c                   eval      c4ab02 = mpab02
6.20 c                   eval      c4ab03 = mpab03
6.20 c                   eval      c4ab04 = mpab04
6.20 c                   eval      c4ab05 = mpab05
6.20 c                   eval      c4ab06 = mpab06
6.20 c                   eval      c4ab07 = mpab07
6.20 c                   eval      c4ab08 = mpab08
6.20 c                   eval      c4ab09 = mpab09
     c     *dmy          move      mpodat        c2odat
     c     *dmy          move      mpedat        c2edat
     c                   if        mpsdat <> *loval
     c     *dmy          move      mpsdat        c2sdat
     c                   else
     c                   eval      c2sdat = 0
     c                   endif
7.06  *
7.06  *  Henter tilleggs felt fra XTILPF
7.06  *
7.06 c                   eval      xtillu_firm = w_firm
7.06 c                   eval      xtillu_regi = 'MPPAPF'
7.06 c                   eval      xtillu_felt = 'START_TID'
7.06 c                   eval      xtillu_xkey = %editc(w_firm:'X') +
7.06 c                                           %editc(c1ldor:'X') +
7.06 c                                           %editc(c1lnum:'X')
7.06 c     xtillu_key    chain     xtillu
7.06 c                   if        %found(xtillu)
7.06 c                   eval      c3_ttt = %dec(%subst(xtalfa:1:2):2:0)
7.06 c                   eval      c3_tmm = %dec(%subst(xtalfa:3:2):2:0)
7.06 c                   else
7.06 c                   eval      c3_ttt = *zero
7.06 c                   eval      c3_tmm = *zero
7.06 c                   endif
7.06  *
     c                   else
     c                   eval      w_retk = *blank
     c                   eval      w_levr = c1ldor
     c                   eval      w_navn = *blank
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
     c                   movel     w_navn        c1lnav
     c                   eval      c2ogrf = 0
     c                   eval      c2ogrt = 0
     c                   eval      c2hgrf = 0
     c                   eval      c2hgrt = 0
     c                   eval      c2ugrf = 0
     c                   eval      c2ugrt = 0
     c                   eval      c2varf = *blank
     c                   eval      c2vart = *blank
     c                   eval      c2pran = *blank
     c                   eval      c2thor = 0
     c                   eval      c2sdat = 0
     c                   eval      c2lage = 0
     c                   eval      c2vval = 0
     c                   eval      c2sats = 0
     c                   eval      c2aval = 1
     c                   eval      c2besk = *blank
     c                   eval      c3hypp = *blank
     c                   eval      c3dag1 = *blank
     c                   eval      c3dag2 = *blank
     c                   eval      c3dag3 = *blank
     c                   eval      c3dag4 = *blank
     c                   eval      c3dag5 = *blank
     c                   eval      c3dag6 = *blank
     c                   eval      c3dag7 = *blank
7.04 c                   eval      c3_ttt = *zero
7.04 c                   eval      c3_tmm = *zero
     c                   eval      c2gvek = 0
     c                   eval      c2gbel = 0
     c                   eval      c2mail = *blank
     c                   eval      c2selg = 0
     c                   eval      c2user = *blank
     c                   eval      c2rebe = *blank
     c                   eval      c2kora = *blank
6.20 c                   eval      c4rapp = *blank
6.20 c                   eval      c4ab01 = *blank
6.20 c                   eval      c4ab02 = *blank
6.20 c                   eval      c4ab03 = *blank
6.20 c                   eval      c4ab04 = *blank
6.20 c                   eval      c4ab05 = *blank
6.20 c                   eval      c4ab06 = *blank
6.20 c                   eval      c4ab07 = *blank
6.20 c                   eval      c4ab08 = *blank
6.20 c                   eval      c4ab09 = *blank
6.11 c                   if        laakso <> *blank
6.11 c                   eval      c2ksor = 1
6.11 c                   else
6.11 c                   eval      c2ksor = 0
6.11 c                   endif
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ldor = k1ldor
     c                   eval      w_retk = *blank
     c                   eval      w_levr = c1ldor
     c                   eval      w_navn = *blank
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
     c                   movel     w_navn        c1lnav
     c                   eval      c1lnum = k1lnum
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
7.03 c     c2taga        tag
      *
7.xx  *  GUI FIX -
7.xx c                   if        c2rebe = '1'
7.xx c                   eval      c2rebe = 'J'
7.xx c                   endif
7.xx c                   if        c2kora = '1'
7.xx c                   eval      c2kora = 'J'
7.xx c                   endif
7.xx  *
7.03 c***  c2taga        tag
      *
     c                   exfmt     c2bld
      *
7.xx  *  GUI FIX -
7.xx c                   if        c2rebe = 'J'
7.xx c                   eval      c2rebe = '1'
7.xx c                   endif
7.xx c                   if        c2kora = 'J'
7.xx c                   eval      c2kora = '1'
7.xx c                   endif
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      c2taga
     c                   endif
      *
      * Kaller bilde for kjøremønster
      *
     c                   if        *inki
     c                   exsr      xc3win
     c                   goto      c2taga
     c                   endif
6.20  *
6.20  * Kaller bilde for varsling
6.20  *
6.20 c                   if        *inkj
6.20 c                   exsr      xc4win
6.20 c                   goto      c2taga
6.20 c                   endif
      *
      * Validering av input
      *
      * selger
     c                   if        c2selg <> 0
     c                   eval      w_stat = *blank
     c                   eval      w_ikod = c2selg
     c                   eval      w_itxt = *blank
     c                   call      'RS709R  '
     c                   parm                    w_stat
     c                   parm                    w_ikod
     c                   parm                    w_itxt
     c                   if        w_stat <> *blank
     c                   eval      *in44 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      * beskrivelse
     c                   if        c2besk = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c*                  if        c2pran = *blank
     c*                  eval      *in32 = *on
     c*                  goto      c2taga
     c*                  endif
      *
      * produktansvarlig
     c                   if        c2pran <> *blank
     c                   eval      w_stat = *blank
     c                   eval      w_zkod = c2pran
     c                   call      'RS726R  '
     c                   parm                    w_stat
     c                   parm                    w_zkod
     c                   parm                    w_ztxt
      *
     c                   if        w_stat <> *blank
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *  sjekker fra til
     c                   if        c2ogrf > c2ogrt
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2hgrf > c2hgrt
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2ugrf > c2ugrt
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2varf > c2vart
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
      *
     c*                  if        c2thor = 0
     c*                  eval      *in37 = *on
     c*                  goto      c2taga
     c*                  endif
      *
     c*                  if        c2lage = 0
     c*                  eval      *in38 = *on
     c*                  goto      c2taga
     c*                  endif
      * lager
     c                   eval      w_stat = *blank
     c                   eval      w_jkod = c2lage
     c                   call      'RS710R  '
     c                   parm                    w_stat
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
      *
     c                   if        w_stat <> *blank
     c                   eval      *in38 = *on
     c                   goto      c2taga
     c                   endif
      *
      * sist kjørt dato
     c                   if        c2sdat <> 0
     c     *dmy          test (d)                c2sdat                 42
     c                   if        *in42 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2rebe <> *blank and
     c                             c2rebe <> '1'
     c                   eval      *in41 = *on
     c                   goto      c2taga
     c                   endif
      *
      * sist kjørt dato
     c                   if        c2kora <> *blank and
     c                             c2kora <> '1'
     c                   eval      *in43 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2ogrf = 0 and
     c                             c2ogrt = 0
     c                   eval      c2ogrt = *hival
     c                   endif
      *
     c                   if        c2hgrf = 0 and
     c                             c2hgrt = 0
     c                   eval      c2hgrt = *hival
     c                   endif
      *
     c                   if        c2ugrf = 0 and
     c                             c2ugrt = 0
     c                   eval      c2ugrt = *hival
     c                   endif
      *
     c                   if        c2varf = *blank and
     c                             c2vart = *blank
     c                   eval      c2vart = '999999999999999'
     c                   endif
      *
     c                   if        b_oppr = *on
     c                   exsr      xc3win
     c                   endif
     c                   if        c3hypp = *blank
     c                   eval      c3hypp = 'DG'
     c                   endif
      *
      *
      * Oppdatering
      *
     c                   clear                   mppalur
      *
     c                   eval      mppalu_ldor = c1ldor
     c                   eval      mppalu_lnum = c1lnum
     c     mppalu_key    chain     mppalur
      *
     c                   movel     c1lnav        mplsor
     c                   eval      mpogrf = c2ogrf
     c                   eval      mpogrt = c2ogrt
     c                   eval      mphgrf = c2hgrf
     c                   eval      mphgrt = c2hgrt
     c                   eval      mpugrf = c2ugrf
     c                   eval      mpugrf = c2ugrf
     c                   eval      mpugrt = c2ugrt
     c                   eval      mpvarf = c2varf
     c                   eval      mpvart = c2vart
     c                   eval      mppran = c2pran
     c                   eval      mpthor = c2thor
     c                   eval      mplage = c2lage
     c                   eval      mpvval = c2vval
     c                   eval      mpsats = c2sats
     c                   eval      mpaval = c2aval
     c                   eval      mpbesk = c2besk
     c                   eval      mphypp = c3hypp
     c                   eval      mpdag1 = c3dag1
     c                   eval      mpdag2 = c3dag2
     c                   eval      mpdag3 = c3dag3
     c                   eval      mpdag4 = c3dag4
     c                   eval      mpdag5 = c3dag5
     c                   eval      mpdag6 = c3dag6
     c                   eval      mpdag7 = c3dag7
     c                   eval      mpgvek = c2gvek
     c                   eval      mpgbel = c2gbel
     c                   eval      mpmail = c2mail
     c                   eval      mpselg = c2selg
     c                   eval      mprebe = c2rebe
     c                   eval      mpkora = c2kora
6.11 c                   eval      mpksor = c2ksor
6.20 c                   eval      mprapp = c4rapp
6.20 c                   eval      mpab01 = c4ab01
6.20 c                   eval      mpab02 = c4ab02
6.20 c                   eval      mpab03 = c4ab03
6.20 c                   eval      mpab04 = c4ab04
6.20 c                   eval      mpab05 = c4ab05
6.20 c                   eval      mpab06 = c4ab06
6.20 c                   eval      mpab07 = c4ab07
6.20 c                   eval      mpab08 = c4ab08
6.20 c                   eval      mpab09 = c4ab09
     c                   if        c2sdat <> 0
     c     *dmy          move      c2sdat        mpsdat
     c                   else
     c                   eval      mpsdat = *loval
     c                   endif
      *
     c                   time                    mpedat
     c                   time                    mpetim
     c                   eval      mpuser = l_user
      *
     c                   if        %found
6.10 c                   time                    mpedat
6.10 c                   time                    mpetim
6.10 c                   eval      mpeusr = l_user
     c                   update    mppalur
     c                   else
     c                   eval      mpfirm = w_firm
     c                   eval      mpldor = c1ldor
     c                   eval      mplnum = c1lnum
     c                   time                    mpodat
6.10 c                   time                    mpotim
6.10 c                   eval      mpuser = l_user
6.10 c                   time                    mpedat
6.10 c                   time                    mpetim
6.10 c                   eval      mpeusr = l_user
     c                   write     mppalur
     c                   endif
7.06  *
7.06  *  Oppdatering av tilleggs felt i XTILPF
7.06  *
7.06 c                   eval      xtillu_firm = w_firm
7.06 c                   eval      xtillu_regi = 'MPPAPF'
7.06 c                   eval      xtillu_felt = 'START_TID'
7.06 c                   eval      xtillu_xkey = %editc(w_firm:'X') +
7.06 c                                           %editc(c1ldor:'X') +
7.06 c                                           %editc(c1lnum:'X')
7.06 c     xtillu_key    chain     xtillu
7.06 c                   if        %found(xtillu)
7.06 c                   eval      xtalfa = %editc(c3_ttt:'X') +
7.06 c                                      %editc(c3_tmm:'X')
7.06 c                   time                    xtdato
7.06 c                   update    xtillur
7.06 c                   else
7.06 c                   if        c3_ttt <> *zero
7.06 c                   eval      xtfirm = w_firm
7.06 c                   eval      xtregi = xtillu_regi
7.06 c                   eval      xtfelt = xtillu_felt
7.06 c                   eval      xtxkey = xtillu_xkey
7.06 c                   eval      xtalfa = %editc(c3_ttt:'X') +
7.06 c                                      %editc(c3_tmm:'X')
7.06 c                   time                    xtdato
7,06 c                   write     xtillur
7.06 c                   endif
7.06 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1selg = mpselg
     c                   eval      b1besk = mpbesk
5.51 c                   eval      b1lage = mplage
     c                   if        mpsdat = *loval
     c                   eval      b1sdat  = 0
     c                   else
     c     *dmy          move      mpsdat        b1sdat
     c                   endif
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av bilde for kjøremønster (C3win)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3win        begsr
      *
     c     c3taga        tag
      *
7.xx  * versjon < 700 brukte 1 istedet for J
7.xx  * koden under konverter fra 1 til J
7.xx c                   if        c3dag1 = '1'
7.xx c                   eval      c3dag1 = 'J'
7.xx c                   endif
7.xx c                   if        c3dag2 = '1'
7.xx c                   eval      c3dag2 = 'J'
7.xx c                   endif
7.xx c                   if        c3dag3 = '1'
7.xx c                   eval      c3dag3 = 'J'
7.xx c                   endif
7.xx c                   if        c3dag4 = '1'
7.xx c                   eval      c3dag4 = 'J'
7.xx c                   endif
7.xx c                   if        c3dag5 = '1'
7.xx c                   eval      c3dag5 = 'J'
7.xx c                   endif
7.xx c                   if        c3dag6 = '1'
7.xx c                   eval      c3dag6 = 'J'
7.xx c                   endif
7.xx c                   if        c3dag7 = '1'
7.xx c                   eval      c3dag7 = 'J'
7.xx c                   endif
     c                   exfmt     c3win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
     c                   if        c3hypp <> 'DG' and
     c                             c3hypp <> '14' and
     c                             c3hypp <> 'UK' and
     c                             c3hypp <> 'MD'
     c                   eval      *in39 = *on
     c                   goto      c3taga
     c                   endif
7.xx  * versjon < 700 brukte 1 istedet for J
7.xx  * koden under konverter fra 1 til J
7.xx c                   if        c3dag1 = 'J'
7.xx c                   eval      c3dag1 = '1'
7.xx c                   endif
7.xx c                   if        c3dag2 = 'J'
7.xx c                   eval      c3dag2 = '1'
7.xx c                   endif
7.xx c                   if        c3dag3 = 'J'
7.xx c                   eval      c3dag3 = '1'
7.xx c                   endif
7.xx c                   if        c3dag4 = 'J'
7.xx c                   eval      c3dag4 = '1'
7.xx c                   endif
7.xx c                   if        c3dag5 = 'J'
7.xx c                   eval      c3dag5 = '1'
7.xx c                   endif
7.xx c                   if        c3dag6 = 'J'
7.xx c                   eval      c3dag6 = '1'
7.xx c                   endif
7.xx c                   if        c3dag7 = 'J'
7.xx c                   eval      c3dag7 = '1'
7.xx c                   endif
      *
     c                   endsr
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for behandling av bilde for varsling (c4win)
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     xc4win        begsr
6.20  *
6.20 c     c4taga        tag
6.20  *
6.20 c                   exfmt     c4win
6.20  *
6.20 c                   if        *inkc or *inkl
6.20 c                   leavesr
6.20 c                   endif
6.20 c**                 if        c3hypp <> 'DG'
6.20 c**                 eval      *in39 = *on
6.20 c**                 goto      c4taga
6.20 c**                 endif
6.20  *
6.20 c                   endsr
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      mppalr_ldor = d1ldor
     c                   eval      mppalr_lnum = d1lnum
     c     mppalr_key    chain     mppalr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      mppalu_ldor = d1ldor
     c                   eval      mppalu_lnum = d1lnum
     c     mppalu_key    chain     mppalur
      *
     c                   delete    mppalur
      *
8.01  *  Logg hendelse
8.01 c                   clear                   sloglur
8.01 c                   eval      elfirm = w_firm
8.01 c                   eval      elregi = 'MPPAPF'
8.01 c                   eval      elfore = %char(d1ldor) + '-' +
8.01 c                                      %char(d1lnum)
8.01 c                   eval      elfort = %char(mpselg) + '-' +
8.01 c                                      %char(mplage)
8.01 c                   time                    elrdat
8.01 c                   time                    elrtim
8.01 c                   eval      eluser = l_user
8.01 c                   time                    eldato
8.01 c                   eval      elfrav = %char(d1ldor) + '-' +
8.01 c                                      %char(d1lnum)
8.01 c                   eval      eltilv = '0 - Slettet'
8.01 c                   write     sloglur
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ldor = k1ldor
     c                   eval      s_lnum = k1lnum
      *
      * Fyller kopierings-bildet
      *
     c                   eval      mppalr_ldor = k1ldor
     c                   eval      mppalr_lnum = k1lnum
     c     mppalr_key    chain     mppalr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
7.02  *
7.02  * Spørring
7.02  *
7.02 c                   if        *inkd
7.02 c                   exsr      spØrring
7.02 c                   goto      k1taga
7.02 c                   endif
      *
      * Sjekk om utfylt
      *
     c                   eval      w_retk = *blank
     c                   eval      w_levr = k1ldor
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
      *
     c                   if        w_retk <> *blank
     c                   eval      *in31  = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1lnum = 0
     c                   eval      w_ldor = k1ldor
     c                   exsr      finn_num
     c                   eval      k1lnum = w_lnum
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      mppalr_ldor = k1ldor
     c                   eval      mppalr_lnum = k1lnum
     c     mppalr_key    chain     mppalr
     c                   if        %found
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = s_ldor
     c                   eval      c1lnum = s_lnum
     c                   exsr      xc2bld
      *
     c                   endsr
      *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Overgang til vedlikeholde lev. til bestillingsforslag
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     xc7bld        begsr
7.05  *
7.05 c                   call      'MP110R  '
7.05  *
7.05 c                   endsr
7.05  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   read      mppal4
     c                   when      w_seqe = 'C'
     c                   read      mppal3
     c                   when      w_seqe = 'B'
     c                   read      mppal2
     c                   other
     c                   read      mppal1
     c                   endsl
      *
     c                   if        %eof or
     c                             mpfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
7.04  * Uaktuell leverandør ?
7.04  *
7.04 c                   if        p1ldor <> 0 and
7.04 c                             p1ldor <> mpldor
7.04 c                   iter
7.04 c                   endif
7.04  *
7.04  *
7.04  * Uaktuell selger ?
7.04  *
7.04 c                   if        p1selg <> 0 and
7.04 c                             p1selg <> mpselg
7.04 c                   iter
7.04 c                   endif
7.04  *
7.04  * Uaktuelt lager ?
7.04  *
7.04 c                   if        p1lage <> 0 and
7.04 c                             p1lage <> mplage
7.04 c                   iter
7.04 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      mppal4_besk = mpbesk
     c                   when      w_seqe = 'C'
     c                   eval      mppal3_selg = mpselg
     c                   when      w_seqe = 'B'
     c                   eval      mppal2_lsor = mplsor
     c                   other
     c                   eval      mppal1_ldor = mpldor
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldor = mpldor
     c                   eval      b1lnum = mplnum
     c                   eval      w_retk = *blank
     c                   eval      w_levr = mpldor
     c                   eval      w_navn = *blank
     c                   call      'RS760R  '
     c                   parm                    w_retk
     c                   parm                    w_levr
     c                   parm                    w_navn
     c                   if        w_retk = *blank
     c                   movel     w_navn        b1lnav
     c                   else
     c                   eval      b1lnav = *blank
     c                   endif
     c                   eval      b1selg = mpselg
5.51 c                   eval      b1lage = mplage
     c                   movel     mpbesk        b1besk
     c                   if        mpsdat <> *loval
     c     *dmy          move      mpsdat        b1sdat
     c                   else
     c                   eval      b1sdat = 0
     c                   endif
      *
7.01 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'D'
     c     mppal4_key    setgt     mppal4
     c                   readp     mppal4
     c                   when      w_seqe = 'C'
     c     mppal3_key    setgt     mppal3
     c                   readp     mppal3
     c                   when      w_seqe = 'B'
     c     mppal2_key    setgt     mppal2
     c                   readp     mppal2
     c                   other
     c     mppal1_key    setgt     mppal1
     c                   readp     mppal1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   readp     mppal4
     c                   when      w_seqe = 'C'
     c                   readp     mppal3
     c                   when      w_seqe = 'B'
     c                   readp     mppal2
     c                   other
     c                   readp     mppal1
     c                   endsl
      *
     c                   if        %eof or
     c                             mpfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'D'
     c                   eval      mppal4_besk = mpbesk
     c                   when      w_seqe = 'C'
     c                   eval      mppal3_selg = mpselg
     c                   when      w_seqe = 'B'
     c                   eval      mppal2_lsor = mplsor
     c                   other
     c                   eval      mppal1_ldor = mpldor
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'D'
     c     mppal4_key    setll     mppal4
     c                   when      w_seqe = 'C'
     c     mppal3_key    setll     mppal3
     c                   when      w_seqe = 'B'
     c     mppal2_key    setll     mppal2
     c                   other
     c     mppal1_key    setll     mppal1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandlnrg av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
7.04  * Leverandør
7.04  *
7.04 c                   if        wactfe = 'P1LDOR'
7.04 c                   call      'RL500R'
7.04 c                   parm                    w_firm
7.04 c                   parm                    p1ldor
7.04 c                   parm                    w_navn
7.04 c                   goto      end_spØrr
7.04 c                   endif
7.04  * Selger
7.04  *
7.04 c                   if        wactfe = 'P1SELG'
7.04 c                   call      'RA509R'
7.04 c                   parm                    w_firm
7.04 c                   parm                    p1selg
7.04 c                   parm                    w_navn
7.04 c                   goto      end_spØrr
7.04 c                   endif
7.04  *
7.04  * Lager
7.04  *
7.04 c                   if        wactfe = 'P1LAGE'
7.04 c                   call      'RA510R'
7.04 c                   parm                    w_firm
7.04 c                   parm                    p1lage
7.04 c                   parm                    w_lnav
7.04 c                   goto      end_spØrr
7.04 c                   endif
      *
      * levnr.
     c                   if        w_afld = 'C1LDOR'
     c                   eval      w_firm      = l_firm
     c                   eval      w_levr      = c1ldor
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'RL500R  '
     c                   PARM                    w_firm
     c                   PARM                    w_levr
     c                   PARM                    w_navn
     c                   eval      c1ldor      = w_levr
     c                   goto      end_spØrr
     c                   endif
7.02 c                   if        w_afld = 'K1LDOR'
7.02 c                   eval      w_firm      = l_firm
7.02 c                   eval      w_levr      = k1ldor
7.02 c                   eval      w_navn      = *blank
7.02 c                   CALL      'RL500R  '
7.02 c                   PARM                    w_firm
7.02 c                   PARM                    w_levr
7.02 c                   PARM                    w_navn
7.02 c                   eval      k1ldor      = w_levr
7.02 c                   goto      end_spØrr
7.02 c                   endif
      *
      * produktansvarlig
     c                   if        w_afld = 'C2PRAN'
     c                   eval      w_firm      = l_firm
     c                   eval      w_zkod      = c2pran
     c                   eval      w_ztxt      = *blank
4.86 c                   CALL      'RA526R  '
     c                   PARM                    w_firm
     c                   PARM                    w_zkod
     c                   PARM                    w_ztxt
     c                   eval      c2pran      = w_zkod
     c                   goto      end_spØrr
     c                   endif
      *
      * lager
     c                   if        w_afld = 'C2LAGE'
     c                   eval      w_firm      = l_firm
     c                   eval      w_jkod      = c2lage
     c                   eval      w_jtxt      = *blank
4.86 c                   CALL      'RA510R  '
     c                   PARM                    w_firm
     c                   PARM                    w_jkod
     c                   PARM                    w_jtxt
     c                   eval      c2lage      = w_jkod
     c                   goto      end_spØrr
     c                   endif
      *
      * overgruppe   fra
     c                   if        w_afld = 'C2OGRF'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = c2ogrf
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG510R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_navn
     c                   eval      c2ogrf      = w_ogrp
     c                   goto      end_spØrr
     c                   endif
      *
      * overgruppe  til
     c                   if        w_afld = 'C2OGRT'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = c2ogrt
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG510R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_navn
     c                   eval      c2ogrt      = w_ogrp
     c                   goto      end_spØrr
     c                   endif
      *
      * hovedgruppe fra
     c                   if        w_afld = 'C2HGRF'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = 0
     c                   eval      w_hgrp      = c2hgrf
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG511R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_navn
     c                   eval      c2hgrf      = w_hgrp
     c                   goto      end_spØrr
     c                   endif
      *
      * hovedgruppe til
     c                   if        w_afld = 'C2HGRT'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = 0
     c                   eval      w_hgrp      = c2hgrt
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG511R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_navn
     c                   eval      c2hgrt      = w_hgrp
     c                   goto      end_spØrr
     c                   endif
      *
      * undergruppe fra
     c                   if        w_afld = 'C2UGRF'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = 0
     c                   eval      w_hgrp      = 0
     c                   eval      w_ugrp      = c2ugrf
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG512R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_ugrp
     c                   PARM                    w_navn
     c                   eval      c2ugrf      = w_ugrp
     c                   goto      end_spØrr
     c                   endif
      *
      * undergruppe til
     c                   if        w_afld = 'C2UGRT'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ogrp      = 0
     c                   eval      w_hgrp      = 0
     c                   eval      w_ugrp      = c2ugrt
     c                   eval      w_navn      = *blank
4.86 c                   CALL      'VG512R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ogrp
     c                   PARM                    w_hgrp
     c                   PARM                    w_ugrp
     c                   PARM                    w_navn
     c                   eval      c2ugrt      = w_ugrp
     c                   goto      end_spØrr
     c                   endif
      *
      * selger
     c                   if        w_afld = 'C2SELG'
     c                   eval      w_firm      = l_firm
     c                   eval      w_ikod      = c2selg
     c                   eval      w_itxt      = *blank
4.86 c                   CALL      'RA509R  '
     c                   PARM                    w_firm
     c                   PARM                    w_ikod
     c                   PARM                    w_itxt
     c                   eval      c2selg      = w_ikod
     c                   eval      ra09l1_kod  = w_ikod
     c     ra09l1_key    chain     ra09l1
     c                   if        %found(ra09l1)
     c                   eval      c2mail = raiema
     c                   endif
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine finner siste løpenr. for leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_num      begsr
      *
     c                   eval      mppalr_ldor =  w_ldor
     c                   eval      mppalr_lnum =  *hival
     c     mppalr_key    setll     mppalr
     c                   readp     mppalr                                 60
     c                   if        *in60      =  *on  or
     c                             mpldor    <>  mppalr_ldor
     c                   eval      w_lnum     =  1
     c                   else
     c                   eval      w_lnum     =  mplnum  +  1
     c                   endif
      *
     c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01  /FREE
7.01
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 5 ;
7.01       w_len = %len(%trim(b1besk));
7.01       if (w_len > 0);
7.01       b1besk = %ScanRpl('.':'':b1besk:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1besk));
7.01       if (w_len > 0);
7.01       b1besk = %ScanRpl(':':'':b1besk:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1lnav));
7.01       if (w_len > 0);
7.01       b1lnav = %ScanRpl('.':'':b1lnav:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1lnav));
7.01       if (w_len > 0);
7.01       b1lnav = %ScanRpl(':':'':b1lnav:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01     endfor ;
7.01   endsr;
7.01  /END-FREE
      *
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  * xh1win  Bilde for behandling av bla-alternativer
7.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.04  *
7.04 c     xh1win        begsr
7.04  *
7.04 c     h1taga        tag
7.04  *
7.04 c                   write     h1win
7.04 c                   exfmt     h1win
7.04  *
7.04  * Behandling av funksjonstaster
7.04  *
7.04 c                   select
7.04 c                   when      *inkc
7.04 c                   goto      end_h1win
7.04 c                   when      *inkd
7.04 c                   exsr      spØrring
7.04 c                   goto      h1taga
7.04 c                   endsl
7.04  *
7.04 c     end_h1win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på parameter pr. lev.
      *
     c     mppal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppal1_ldor
      *
      * Key for posisjonering på Beskrivelse
      *
     c     mppal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppal2_lsor
      *
      * Key for posisjonering på selger
      *
     c     mppal3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppal3_selg
      *
      * Key for posisjonering på beskrivelse
      *
     c     mppal4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppal4_besk
      *
      * Key for les på parameter pr. lev.
      *
     c     mppalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppalr_ldor
     c                   kfld                    mppalr_lnum
      *
      * Key for oppdatering av parameter pr. lev.
      *
     c     mppalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mppalu_ldor
     c                   kfld                    mppalu_lnum
      *
      * Oppslag mot bruker register
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      * Oppslag mot selgerregister
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_kod
6.11  *
6.11  * Lager - statuskoder
6.11  *
6.11 c     lstsl1_key    klist
6.11 c                   kfld                    w_firm
7.05  *
7.05  * Ordre - statuskoder
7.05  *
7.05 c     fstsl1_key    klist
7.05 c                   kfld                    w_firm
7.04  *
7.04  * Std, Tillegsregister
7.04  *
7.04 c     xtillu_key    klist
7.04 c                   kfld                    xtillu_firm
7.04 c                   kfld                    xtillu_regi
7.04 c                   kfld                    xtillu_felt
7.04 c                   kfld                    xtillu_xkey
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.11 c     lstsl1_key    chain     lstsl1
      *
7.05 c                   eval      *in50 = *off
7.05 c     fstsl1_key    chain     fstsl1
7.05 c                   if        %found
7.05 c                   if        faabfs = 2
7.05 c                   eval      *in50 = *on
7.05 c                   endif
7.05 c                   endif
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      mppal1_ldor = 0
     c     mppal1_key    setll     mppal1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
