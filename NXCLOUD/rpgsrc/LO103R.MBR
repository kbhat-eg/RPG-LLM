      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO103R                                       *
      *  Beskrivelse . . : Registrering av Best./Innkjøp, Opplysninger  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *   6.10     180609  Oppdatert med sporingsinformasjon       BSA
6.nu  *   6.30     280514  Utvidet mtp. nummerutvidelse             bof
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FANUML1    IF   E           K DISK    RENAME(ANUMPFR:ANUML1R)
      *
     FLSTSL1    IF   E           K DISK    RENAME(LSTSPFR:LSTSL1R)
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLOHELO    IF   E           K DISK    RENAME(LOHEPFR:LOHELOR)
     FLOHELU    UF   E           K DISK    RENAME(LOHEPFR:LOHELUR)
     FSIHEL1    IF   E           K DISK    RENAME(SIHEPFR:SIHEL1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
      *
     FLO103D    CF   E             WORKSTN
      *
     D                 DS
     D  DSLBTX                 1     75
     D  DSLTX1                 1     25
     D  DSLTX2                26     50
     D  DSLTX3                51     75
      *
     D                UDS
6.10 d  l_user               911    920
     D  l_firm               944    946  0
     D  l_fnav               951    980
      *
      * Definering av variabler i nøkler
      *
     d lohelo_firm     s                   like(lofirm)
     d lohelo_otyp     s                   like(lootyp)
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandler vedlikehold av faste opplysninger           II    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     D1TAGA        TAG
      *
      *  Hent opplysninger fra BESTILLINGS-HODE-REGISTER
      *
     C     HEDKEY        CHAIN     LOHEL1R                            90
     C     *IN90         IFEQ      *OFF
      *
      *  Hent opplysninger fra ORDRE-TYPE-REGISTER
      *
     c                   eval      d1fkda = 0
     c                   eval      d1ffda = 0
      *
     C                   MOVE      LOOTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
      *
     C     LOBINR        IFNE      *ZERO
      *
     c                   if        lofkda <> *loval
     c     *dmy          move      lofkda        d1fkda
     c                   endif
      *
     c                   if        loffda <> *loval
     c     *dmy          move      loffda        d1ffda
     c                   endif
      *
     C                   MOVE      LOBINR        D1BINR
      *
     C     VAOKRE        IFEQ      '-'
     C                   EVAL      D1TOTF = LOTOTF * -1
     C                   ELSE
     C                   EVAL      D1TOTF = LOTOTF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler skjermbilde  D1                                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Send alt
      *
     C     D1TAGB        TAG
     C                   EXFMT     D1BLD
      *
      *  F3=Gå tilbake til forrige bilde
      *
     C     *INKC         IFEQ      *ON
     C                   MOVE      '9'           WP110D
     C                   GOTO      XSLUTT
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler sjekking av opplysninger                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Sjekk,  Er bilagsnummer utfylt
      *
     C     D1BINR        IFEQ      *ZERO
     C                   MOVE      *ON           *IN35
     C                   GOTO      D1TAGB
     C                   ENDIF
      *
      *  Sjekk,  Er bilagsnummer innenfor grenser
      *
     C     D1BINR        IFLT      WSTART
     C     D1BINR        ORGT      WSLUTT
     C                   MOVE      *ON           *IN31
     C                   GOTO      D1TAGB
     C                   ENDIF
      *
      *  Sjekk,  Er bilagsnummer brukt før.
      *
     C                   MOVEL     LODATE        HISALF
     C                   MOVEL     HISALF        HISAAR
     C                   MOVE      D1BINR        HISBNR
     C                   Z-ADD     0             HISNUM
     C                   Z-ADD     0             HISSUF
      *
     C     HISKEY        SETLL     SIHEL1
     C                   READ      SIHEL1                                 61
     C     *IN61         CABEQ     *ON           BINOK
     C     SHFIRM        CABNE     HISFIR        BINOK
     C     SHAARR        CABNE     HISAAR        BINOK
     C     SHBINR        CABNE     HISBNR        BINOK
      *
     C                   MOVE      *ON           *IN34
     C                   GOTO      D1TAGB
      *
     C     BINOK         TAG
      *
      *  Sjekk mot registrerte - ikke fakturerte.
      *
     c                   eval      lohelo_otyp = 'IF'
     c     lohelo_key    setll     lohelo
     c     lohelo_key    reade     lohelo                                 90
     c                   dow       *in90 = *off
      *
     c                   if        lobinr = d1binr
     c                   eval      *in34 = *on
     c                   goto      d1tagb
     c                   endif
      *
     c     lohelo_key    reade     lohelo                                 90
     c                   enddo
      *
      *  Sjekk,  Fakturadato
      *
     C     D1FKDA        IFNE      *ZERO
     c     *dmy          test(d)                 d1fkda                 32
     c                   if        *in32 = *on
     c                   goto      d1tagb
     c                   endif
     C                   END
      *
      *  Sjekk,  Forfallsdato
      *
     C     D1FFDA        IFNE      *ZERO
     c     *dmy          test(d)                 d1ffda                 33
     c                   if        *in33 = *on
     c                   goto      d1tagb
     c                   endif
     C                   END
      *
      *  Oppdaterer BESTILLINGS-HODE-REGISTER
      *
     C     HEDKEY        CHAIN     LOHELUR                            90
     C     *IN90         IFEQ      *OFF
      *
     c                   eval      lofkda = *loval
     c                   eval      loffda = *loval
      *
     c                   if        d1fkda <> 0
     c     *dmy          move      d1fkda        lofkda
     c                   endif
      *
     c                   if        d1ffda <> 0
     c     *dmy          move      d1ffda        loffda
     c                   endif
      *
     C                   MOVE      D1TOTF        LOTOTF
     C                   MOVE      D1BINR        LOBINR
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     C                   UPDATE    LOHELUR
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WPRETK            1
     C                   MOVE      *BLANK        WP110D            1
     C                   MOVE      *BLANK        HISALF           10
      *
     C                   MOVE      *ZERO         HISAAR            4 0
      *
     C                   MOVE      *ZERO         WPKONT            5 0
      *
     C                   MOVE      *ZERO         WPSPES            2 0
     C                   MOVE      *ZERO         WPCBDA            8 0
     C                   MOVE      *ZERO         WPCFDA            8 0
     C                   MOVE      *ZERO         WPCRAB            4 2
     C                   MOVE      *ZERO         WPPDAT            8 0
     C                   MOVE      *ZERO         WPPOMR           10 6
      *
     C                   MOVE      *ZERO         WSTART            6 0
     C                   MOVE      *ZERO         WSLUTT            6 0
      *
      *  Redefinering av felter
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    LOFIRM        WPFIRM
     C     *LIKE         DEFINE    LONUMM        WPNUMM
     C     *LIKE         DEFINE    LOSUFF        WPSUFF
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    ANFIRM        NUMFIR
     C     *LIKE         DEFINE    ANFELL        NUMFEL
     C     *LIKE         DEFINE    ANTYPE        NUMTYP
     C     *LIKE         DEFINE    ANFAST        NUMFAS
      *
     C     *LIKE         DEFINE    LAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    SHFIRM        HISFIR
     C     *LIKE         DEFINE    SHBINR        HISBNR
     C     *LIKE         DEFINE    SHNUMM        HISNUM
     C     *LIKE         DEFINE    SHSUFF        HISSUF
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
      *  Key for file : NUMMER-REGISTER
      *
     C     NUMKEY        KLIST
     C                   KFLD                    NUMFIR
     C                   KFLD                    NUMFEL
     C                   KFLD                    NUMTYP
     C                   KFLD                    NUMFAS
      *
      *  Key for file : STATUS-KODER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      *  Key for file : BESTILLINGS-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for  BESTILLINGS-HODE  -Ordretype
      *
     C     lohelo_key    klist
     C                   kfld                    lohelo_firm
     C                   kfld                    lohelo_otyp
      *
      * Key for Historisk-fakturaregister
      *
     C     HISKEY        KLIST
     C                   KFLD                    HISFIR
     C                   KFLD                    HISAAR
     C                   KFLD                    HISBNR
     C                   KFLD                    HISNUM
     C                   KFLD                    HISSUF
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WP110D
     C                   PARM                    WPFIRM
     C                   PARM                    WPNUMM
     C                   PARM                    WPSUFF
      *
      *  Legger inn firmanummer i nøklene
      *
     C                   MOVE      WPFIRM        NUMFIR
     C                   MOVE      WPFIRM        STSFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        HISFIR
     C                   MOVE      WPFIRM        OTYFIR
     c                   eval      lohelo_firm = wpfirm
      *
      *  Legger inn bestillingsnummer og suffix i nøkklene
      *
     C                   MOVE      WPNUMM        HEDNUM
     C                   MOVE      WPSUFF        HEDSUF
      *
      *  Henter statusopplysninger fra STATUS-REGISTER
      *
     C     STSKEY        CHAIN     LSTSL1R                            90
      *
      *  Henter statusopplysninger fra BRUKER-REGISTERENE
      *
     C                   MOVE      LAABIN        NUMFEL
     C                   MOVE      *BLANK        NUMTYP
     C                   MOVE      *BLANK        NUMFAS
     C     NUMKEY        CHAIN     ANUML1                             90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      ANNFOM        WSTART
     C                   MOVE      ANNTOM        WSLUTT
     C                   ELSE
     C                   MOVE      *ZERO         WSTART
     C                   MOVE      *ZERO         WSLUTT
     C                   END
      *
     CSR                 ENDSR
