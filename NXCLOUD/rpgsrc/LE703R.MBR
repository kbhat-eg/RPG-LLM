     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LE700R
      * Beskrivelse . . : Oppdatering av telle-liste utfra telle-bunt - overskriv
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270122 ASK  Nytt program
      *                   Dette er en variant av LE700R som overskriver post i telleliste
      *                   i stedet for summerer opp. Valg 10 i LU100R.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     flbhelu    uf   e           k disk    rename(lbhepfr:lbhelur)
     flbdtl2    if   e           k disk    rename(lbdtpfr:lbdtl2r)
     flbdtlu    uf   e           k disk    rename(lbdtpfr:lbdtlur)
     fltellu    uf a e           k disk    rename(ltelpfr:ltellur)
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     fltell2    if   e           k disk    rename(ltelpfr:ltell2r)
     fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Datastrukturer
      *
      *  Parameter-inn
     d                 ds
     d p_prec                        20
     d p_firm                         3  0 overlay(p_prec)
     d p_batc                        10    overlay(p_prec:4)
     d p_numm                         6  0 overlay(p_prec:14)
     d p_kode                         1    overlay(p_prec:20)
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
     d  hinumm                        6  0 overlay(hirec:56)
     d  hikode                        1    overlay(hirec:62)
     d  hidekn                        5  2 overlay(hirec:63)
     d  hiavgf                        1  0 overlay(hirec:68)
     d  hipriv                        1  0 overlay(hirec:69)
     d  hitilb                        1    overlay(hirec:70)
     d  hiskaf                        1    overlay(hirec:71)
     d  hildor                        6  0 overlay(hirec:72)
     d  hisapr                        9  2 overlay(hirec:78)
     d  hikopr                        9  2 overlay(hirec:87)
     d  hiprgr                        1    overlay(hirec:96)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  hokpri                        1    overlay(horec:2)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
      *
      *  Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      *  Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d lbdtl2_batc     s                   like(lvbatc)
     d lbdtl2_numm     s                   like(lvnumm)
     d lbdtlu_batc     s                   like(lvbatc)
     d lbdtlu_numm     s                   like(lvnumm)
     d lbdtlu_line     s                   like(lvline)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
     d ltell1_batc     s                   like(lebatc)
     d ltell1_numm     s                   like(lenumm)
     d ltell1_suff     s                   like(lesuff)
     d ltell1_line     s                   like(leline)
     d ltellu_batc     s                   like(lebatc)
     d ltellu_numm     s                   like(lenumm)
     d ltellu_suff     s                   like(lesuff)
     d ltellu_line     s                   like(leline)
     d ltell2_batc     s                   like(lebatc)
     d ltell2_lage     s                   like(lelage)
     d ltell2_lope     s                   like(ledlop)
     d ltell2_numm     s                   like(lenumm)
     d ltell2_suff     s                   like(lesuff)
     d ltell2_vare     s                   like(levare)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
      *
      *  Definering av variable
      *
     d w_tell          s              6  0
     d w_firm          s                   like(vvfirm)
     d w_parm          s             20
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_numm          s              8  0
     d w_type          s              2
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_vtyp          s                   like(vvvtyp)
     d w_siva          s                   like(lvvare)
     d w_inlr          s              1
     d w_lety          s              1  0
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_inpr          s              9  2
     d w_kopr          s              9  2
     d w_utda          s                   like(vvudat)
     d w_ldor          s                   like(vvldor)
     d b_inlr          s              1    inz(*off)
     d w_lv_nobb       s                   like(vvlnob)
     d w_lv_modn       s                   like(vvlmod)
     d w_lv_lldo       s                   like(vvlnle)
     d w_lv_llva       s                   like(vvllva)
      *
     d w_prgr          s              1
     d w_avde          s              4  0
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hent inn opplysninger fra LAGERBUNT-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lbhelu_key    chain     lbhelu                             90
     c                   eval      lukode = 1
     c                   if        *in90 = *off
     c                   time                    luedat
     c                   time                    luetim
     c                   eval      lueusr = l_user
     c                   update    lbhelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese bunt-telle-linjer og oppdatere hovedtelle-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lbdtl2_key    setll     lbdtl2
     c     lbdtl2_key    reade     lbdtl2                                 91
      *
     c                   dow       *in91 = *off
      *
      *  Sjekk om ugyldig vare
      *
     c                   eval      vvarl1_vare = lvvare
     c     vvarl1_key    chain     vvarl1r                            96
      *
      *  Ikke oppdatering dersom antall er null eller ugyldig vare
      *
     c*                  if        lvantt <> 0 and
     c                   if        *in96 = *off
     c                   exsr      opptel
     c                   endif
      *
     c     lbdtl2_key    reade     lbdtl2                                 91
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-TELLE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opptel        begsr
      *
      *  Legger inn løpenummer, dersom DIVERSE-VARER
      *
     c                   if        lutype = 'DIVERSE'
     c                   if        lvdlop = 0
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
     c                   eval      lvdlop = w_numm
     c                   endif
     c                   endif
      *
      *  Hent innformasjon fra LAGER-TELLE-VARE-REGISTER
      *
     c                   eval      ltell2_vare = lvvare
     c                   eval      ltell2_lage = lulage
     c                   eval      ltell2_lope = lvdlop
     c     ltell2_key    chain     ltell2                             90
     c                   if        *in90 = *off
     c                   eval      ltellu_line = leline
     c                   endif
     c                   if        *in90 = *on
     c                   exsr      neste
     c                   eval      ltellu_line = w_tell
     c                   endif
      *
     c                   exsr      hent_prisgr
      *
      *  Oppdaterer LAGER-TELLE-REGISTER
      *
     c     ltellu_key    chain     ltellu                             90
      *
      *  Nullstiller felter ved nye record
      *
     c                   if        *in90 = *on
     c                   eval      leogrp = 0
     c                   eval      lehgrp = 0
     c                   eval      leugrp = 0
     c                   eval      lelage = 0
     c                   eval      leantt = 0
     c                   eval      leantl = 0
     c                   eval      leantb = 0
     c                   eval      lespny = 0
     c                   eval      lekpny = 0
     c                   eval      letek1 = *blank
     c                   eval      leenh1 = *blank
     c                   endif
      *
      *  Legger over felter som er spesielle for DIVERSE-VARER
      *
     c                   if        lutype = 'DIVERSE'
     c                   eval      letek1 = lvtek1
     c                   eval      lespny = lvspny
     c                   eval      lekpny = lvkpny
     c                   eval      leenh1 = lvenh1
     c                   endif
      *
      *  Legger inn felter som er felles for alle varer
      *
     c                   eval      lelage = lulage
     c                   eval      ledlop = lvdlop
      *
      *  Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = lvvare
     c     vvarl1_key    chain     vvarl1r                            96
      *
      * Kaller program for henting av varetype
      *
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    lulage
     c                   parm                    w_vtyp
     c                   parm                    w_utda
     c                   parm                    w_ldor
     c                   parm                    b_inlr
     c                   parm                    w_lv_nobb
     c                   parm                    w_lv_modn
     c                   parm                    w_lv_lldo
     c                   parm                    w_lv_llva
      *
      *  Lagerenhet i vare-register skal benyttes.
      *  Telle-listen må alltid ha alle varer i samme enhet
      *  Men, diversevarer må kunne overføre enhet
      *
     c                   exsr      omr_antall
      *
      *
      *  Legger inn antallsfelter i TELLE-REGISTER
      *
      * Sjekker om vare lik forrige - skal da akkumulere
      *
     c                   if        lvvare = w_siva
     c                   eval      leantt = leantt + lvantt
     c                   eval      leantb = leantb + lvantt
     c                   else
     c                   eval      leantt = lvantt
     c                   eval      leantb = lvantt
     c                   endif
      *
     c                   eval      w_siva = lvvare
     c                   eval      letims = lvtims
      *
      *  Legger over Overgruppe/Hovedgruppe/Undergruppe, hvis ikke utfylt
      *
     c                   if        leogrp = 0
     c                   eval      leogrp = vvogrp
     c                   endif
     c                   if        lehgrp = 0
     c                   eval      lehgrp = vvhgrp
     c                   endif
     c                   if        leugrp = 0
     c                   eval      leugrp = vvugrp
     c                   endif
      *
      *  Legger tekst og enhet fra vareregister, hvis ikke utfylt
      *
     c                   if        letek1 = *blank
     c                   eval      letek1 = vvtek1
     c                   endif
     c                   if        leenh1 = *blank
     c                   eval      leenh1 = vvenhl
     c                   endif
     c                   if        leenh1 = *blank
     c                   eval      leenh1 = vvenhe
     c                   endif
      *
      *  Henter inn priser dersom ikke DIVERSE-VARER
      *
     c                   if        lutype <> 'DIVERSE'
     c                   exsr      hntpri
     c                   if        lespny = 0
     c**                 eval      lespny = hosapr
     c                   eval      lespny = w_sapr
     c                   endif
     c                   if        lekpny = 0
     c**                 eval      lekpny = hokopr
     c                   eval      lekpny = w_kopr
     c                   endif
     c                   endif
      *
      *  Oppdaterer register
      *
     c                   if        *in90 = *off
     c                   time                    leedat
     c                   time                    leetim
     c                   eval      leeusr = l_user
     c                   update    ltellur
     c                   else
     c                   eval      levare = lvvare
     c                   eval      lefirm = w_firm
     c                   eval      lebatc = ltellu_batc
     c                   eval      lenumm = ltellu_numm
     c                   eval      lesuff = ltellu_suff
     c                   eval      leline = ltellu_line
     c                   time                    leodat
     c                   time                    leotim
     c                   eval      leousr = l_user
     c                   time                    leedat
     c                   time                    leetim
     c                   eval      leeusr = l_user
     c                   write     ltellur
     c                   endif
      *
      *  Oppdaterer LAGER-BUNT-LINJE-REGISTER
      *
     c                   eval      lbdtlu_line = lvline
     c     lbdtlu_key    chain     lbdtlu                             90
     c                   eval      lvtlin = ltellu_line
     c                   if        *in90 = *off
     c                   time                    lvedat
     c                   time                    lvetim
     c                   eval      lveusr = l_user
     c                   update    lbdtlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hent sist brukte linjenummer og legg inn i teller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     neste         begsr
      *
     c                   eval      w_tell = 0
      *
     c                   eval      ltell1_line = 999999
     c     ltell1_key    setll     ltell1r
     c                   eval      *in96 = *off
     c                   readp     ltell1r                                96
     c                   if        *in96 = *off
     c                   if        lefirm = w_firm
     c                   if        lebatc = ltell1_batc
     c                   if        lenumm = ltell1_numm
     c                   if        lesuff = ltell1_suff
     c                   eval      w_tell = leline
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall fra opptalt til lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Dersom opptalt enhet er lik lagerenhet eller diversevare
      * beholdes antall som registrert
      *
     c                   if        lvenh1 = vvenhl
     c                   goto      end_omr
     c                   endif
      *
     c                   if        w_vtyp = 'D'
     c                   goto      end_omr
     c                   endif
      *
      * Finner omregningsfaktor volum for lagerenhet og for opptalt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = lvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 91
     c                   dow       *in91 = *off
     c                   if        lvenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 91
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl <> 0
     c                   eval      lvantt = lvantt * w_omrl / w_omrs
     c                   else
     c                   eval      lvantt = 0
     c                   endif
     c                   eval      leenh1 = vvenhl
      *
     c     end_omr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = lclope
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter opplysninger om varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntpri        begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = 0
     c                   eval      hikpro = 0
     c                   eval      hivare = lvvare
     c                   eval      hilety = 0
     c                   eval      hienhe = leenh1
     c                   eval      hidato = ludato
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiprgr = w_prgr
      *
     c**                 call      'VP900R'
     c**                 parm                    hirec
     c**                 parm                    horec
      *
     c                   eval      w_lety = 0
     c                   eval      w_inlr = ' '
     c                   eval      w_sapr = 0
     c                   eval      w_bupr = 0
     c                   eval      w_kopr = 0
     c                   eval      w_inpr = 0
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    lvvare
     c                   parm                    w_prgr
     c                   parm                    leenh1
     c                   parm                    w_lety
     c                   parm                    ludato
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente prisgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_prisgr   begsr
      *
     c                   eval      w_avde = 0
     c                   call      'VL712R'
     c                   parm                    w_firm
     c                   parm                    lulage
     c                   parm                    w_avde
     c                   parm                    w_prgr
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      *  Key for les av STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Key for oppdatering LAGERTELLEBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for les av LAGERTELLELINJE
      *
     c     lbdtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl2_batc
     c                   kfld                    lbdtl2_numm
      *
      *  Key for oppdatering av LAGERTELLELINJE
      *
     c     lbdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtlu_batc
     c                   kfld                    lbdtlu_numm
     c                   kfld                    lbdtlu_line
      *
      *  Key for les av LAGER-TELLE
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
     c                   kfld                    ltell1_numm
     c                   kfld                    ltell1_suff
     c                   kfld                    ltell1_line
      *
      *  Key for oppdatering av LAGER-TELLE
      *
     c     ltellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltellu_batc
     c                   kfld                    ltellu_numm
     c                   kfld                    ltellu_suff
     c                   kfld                    ltellu_line
      *
      *  Key for les av LAGER-TELLE-VARE
      *
     c     ltell2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell2_batc
     c                   kfld                    ltell2_numm
     c                   kfld                    ltell2_suff
     c                   kfld                    ltell2_vare
     c                   kfld                    ltell2_lage
     c                   kfld                    ltell2_lope
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
     c                   eval      p_prec = w_parm
      *
      *  Sett startverdier
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      lbchl1_batc = p_batc
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbdtl2_batc = p_batc
     c                   eval      lbdtlu_batc = p_batc
     c                   eval      ltellu_batc = p_batc
     c                   eval      ltell1_batc = p_batc
     c                   eval      ltell2_batc = p_batc
      *
     c                   eval      lbhelu_numm = p_numm
     c                   eval      lbdtl2_numm = p_numm
     c                   eval      lbdtlu_numm = p_numm
     c                   eval      ltellu_suff = 0
     c                   eval      ltell1_suff = 0
     c                   eval      ltell2_suff = 0
     c                   eval      ltellu_line = 0
     c                   eval      ltell1_line = 0
     c                   eval      lbdtlu_line = 0
      *
     c                   eval      w_tell = 0
     c                   eval      w_kode = *blank
     c                   eval      w_fell = *blank
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = 0
      *
      *  Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
      *  Hent opplysninger fra BATCH
      *
     c     lbchl1_key    chain     lbchl1r                            90
     c                   eval      ltellu_numm = lcnumm
     c                   eval      ltell1_numm = lcnumm
     c                   eval      ltell2_numm = lcnumm
      *
     c                   endsr
