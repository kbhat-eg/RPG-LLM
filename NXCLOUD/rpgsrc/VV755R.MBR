     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV755R
      * Beskrivelse . . : Vare, bytter navn på enhet
      *
      * NB! Dersom det legges inn bytting av navn på enhet i flere tabeller
      *     må også jobben som bytter navn på enhet i andre filgrupper
      *     endres m.h.p. overriding av tabeller mot databibliotek !!!
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290304 HKO  Nytt program
      *       141104 HKO  Lagt inn bytting av navn på enhet i
      *                   ekstern ordre-linje
5.70  * PRGR  281008 bof  Utvidet med prisgruppe
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  070110 bof  Utvidet med vveppf
6.20  * 6.20  010411 bof  Utvidet med vvprpf med prisgiver
6.21  * 6.21  020511 bof  Utvidet tilbudspris med varegr. /lev.nr
      *
8.01  * 8.xx  120422 bsa  Kan forårsake dupkey i vvenlu
8.02  * 8.xx  150223 bsa  Kan forårsake dupkey i vveplu
8.03  * 8.xx  200224 bof  Kan forårsake dupkey i vvprlu
8.02  *PRG2   140622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *PRG2               Endringer består kun i kompilering, da alle
8.02  *PRG2               referanser er eksternt definert.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     fvvenlu    uf   e           k disk    rename(vvenpfr:vvenlur)
8.01 fvvenl1    uf   e           k disk    rename(vvenpfr:vvenl1r)
8.01 f                                     prefix(xx:2)
6.11 fvveplu    uf   e           k disk    rename(vveppfr:vveplur)
8.02 fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
8.02 f                                     prefix(xx:2)
     fvvprlu    uf   e           k disk    rename(vvprpfr:vvprlur)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
8.03 fvvprlr    if   e           k disk    rename(vvprpfr:vvprlrr)
8.03 f                                     prefix(xx:2)
6.21 fvtill9    uf   e           k disk    rename(vtilpfr:vtill9r)
     fvetilu    uf   e           k disk    rename(vetipfr:vetilur)
     ffrabla    uf   e           k disk    rename(frabpfr:frablar)
     ffpril5    uf   e           k disk    rename(fpripfr:fpril5r)
     ffodtlv    uf   e           k disk    rename(fodtpfr:fodtlvr)
     fnodtl4    uf   e           k disk    rename(nodtpfr:nodtl4r)
     fsodtlv    uf   e           k disk    rename(sodtpfr:sodtlvr)
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_enhg          s                   like(vvenhe)
     d p_enhn          s                   like(vvenhe)
      *
      * Definering av variabler i nøkler
      *
     d vvenlu_enhe     s                   like(veenhe)
8.01 d vvenl1_enhe     s                   like(veenhe)
6.11 d vveplu_penh     s                   like(vepenh)
8.02 d vvepl1_penh     s                   like(vepenh)
6.20 d vvprlu_ldor     s                   like(vpldor)
6.20 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
6.20 d vvprlu_lety     s                   like(vplety)
6.20 d vvprlu_pdat     s                   like(vppdat)
8.03 d vvprlr_ldor     s                   like(vpldor)
8.03 d vvprlr_prgr     s                   like(vpprgr)
8.03 d vvprlr_enhe     s                   like(vpenhe)
8.03 d vvprlr_lety     s                   like(vplety)
8.03 d vvprlr_pdat     s                   like(vppdat)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
5.70 d w_prgr          s                   like(vpprgr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Vare
      *
     c     key01         chain     vvarlur
      *
     c                   if        vvenhe = p_enhg
     c                   eval      vvenhe = p_enhn
     c                   endif
     c                   if        vvenhl = p_enhg
     c                   eval      vvenhl = p_enhn
     c                   endif
     c                   if        vvenhs = p_enhg
     c                   eval      vvenhs = p_enhn
     c                   endif
      *
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarlur
      *
      * Vare-enhet
      *
     c                   eval      vvenlu_enhe = p_enhg
     c     vvenlu_key    setll     vvenlu
     c     vvenlu_key    reade     vvenlur
     c                   dow       not %eof
8.01 c                   eval      vvenl1_enhe = p_enhn
8.01 c     vvenl1_key    chain     vvenl1
8.01 c                   if        not %found
8.01  *
     c                   eval      veenhe = p_enhn
6.10 c                   time                    veedat
6.10 c                   time                    veetim
6.10 c                   eval      veeusr = l_user
     c                   update    vvenlur
8.01  *
8.01 c                   else
8.01 c                   unlock    vvenlu
8.01 c                   endif
     c     vvenlu_key    reade     vvenlur
     c                   enddo
6.11  *
6.11  * Vare-enhet-prisgiver
6.11  *
6.11 c                   eval      vveplu_penh = p_enhg
6.11 c     vveplu_key    setll     vveplu
6.11 c     vveplu_key    reade     vveplur
6.11 c                   dow       not %eof
8.02 c                   eval      vvepl1_penh = p_enhn
8.02 c     vvepl1_key    chain     vvepl1
8.02 c                   if        not %found
8.01  *
6.11 c                   eval      vepenh = p_enhn
6.11 c                   time                    vepeda
6.11 c                   time                    vepeti
6.11 c                   eval      vepeus = l_user
6.11 c                   update    vveplur
8.02  *
8.02 c                   else
8.02 c                   unlock    vveplu
8.02 c                   endif
6.11 c     vveplu_key    reade     vveplur
6.11 c                   enddo
      *
6.20  * Vare-pris
6.20  * leser alle vare-pris pr. vare og tester om enhet, så bytter på disse
6 20  *
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1r
6.20 c                   dow       not %eof(vvprl1)
8.03  * sjekk om ny enhet allerede finnes
8.03 c                   eval      vvprlr_ldor = vpldor
8.03 c                   eval      vvprlr_enhe = p_enhn
8.03 c                   eval      vvprlr_prgr = vpprgr
8.03 c                   eval      vvprlr_lety = vplety
8.03 c                   eval      vvprlr_pdat = vppdat
8.03 c     vvprlr_key    chain     vvprlr
8.03 c                   if        not %found(vvprlr)
8.03  *
6.20 c                   if        vpenhe = p_enhg
6.20 c                   eval      vvprlu_ldor = vpldor
6.20 c                   eval      vvprlu_enhe = vpenhe
8.03 c                   eval      vvprlu_prgr = vpprgr
6.20 c                   eval      vvprlu_lety = vplety
6.20 c                   eval      vvprlu_pdat = vppdat
6.20 c     vvprlu_key    chain     vvprlu
6.20 c                   if        %found(vvprlu)
6.20 c                   eval      vpenhe = p_enhn
6.20 c                   time                    vpedat
6.20 c                   time                    vpetim
6.20 c                   eval      vpeusr = l_user
6.20 c                   update    vvprlur
6.20 c                   endif
6.20 c                   endif
8.03 c                   endif
6.20 c     vvprl1_key    reade     vvprl1r
6.20 c                   enddo
      *
      * Tilbud
      *
6.21 c     key01         setll     vtill9
6.21 c     key01         reade     vtill9r
     c                   dow       not %eof
      *
     c                   if        vtenh1 = p_enhg
     c                   eval      vtenh1 = p_enhn
     c                   endif
     c                   if        vtenh2 = p_enhg
     c                   eval      vtenh2 = p_enhn
     c                   endif
     c                   if        vtenh3 = p_enhg
     c                   eval      vtenh3 = p_enhn
     c                   endif
      *
6.10 c                   time                    vtedat
6.10 c                   time                    vtetim
6.10 c                   eval      vteusr = l_user
6.21 c                   update    vtill9r
      *
6.21 c     key01         reade     vtill9r
     c                   enddo
      *
      * Etikett
      *
     c     key01         setll     vetilu
     c     key01         reade     vetilur
     c                   dow       not %eof
      *
     c                   if        vmenhe = p_enhg
     c                   eval      vmenhe = p_enhn
     c                   endif
      *
6.10 c                   time                    vmedat
6.10 c                   time                    vmetim
6.10 c                   eval      vmeusr = l_user
     c                   update    vetilur
      *
     c     key01         reade     vetilur
     c                   enddo
      *
      * Rabattmatrise
      *
     c     key01         setll     frabla
     c     key01         reade     frablar
     c                   dow       not %eof
      *
     c                   if        frenhe = p_enhg
     c                   eval      frenhe = p_enhn
     c                   endif
      *
     c                   update    frablar
      *
     c     key01         reade     frablar
     c                   enddo
      *
      * Spesialpriser
      *
     c     key01         setll     fpril5
     c     key01         reade     fpril5r
     c                   dow       not %eof
      *
     c                   if        fpenhe = p_enhg
     c                   eval      fpenhe = p_enhn
     c                   endif
      *
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fpril5r
      *
     c     key01         reade     fpril5r
     c                   enddo
      *
      * Ordre-linje
      *
     c     key01         setll     fodtlv
     c     key01         reade     fodtlvr
     c                   dow       not %eof
      *
     c                   if        fdenh1 = p_enhg
     c                   eval      fdenh1 = p_enhn
     c                   endif
      *
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlvr
      *
     c     key01         reade     fodtlvr
     c                   enddo
      *
      * Ekstern ordre-linje
      *
     c     key01         setll     nodtl4
     c     key01         reade     nodtl4r
     c                   dow       not %eof
      *
     c                   if        ndenhe = p_enhg
     c                   eval      ndenhe = p_enhn
     c                   endif
      *
6.10 c                   time                    ndedat
6.10 c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
     c                   update    nodtl4r
      *
     c     key01         reade     nodtl4r
     c                   enddo
      *
      * Faktura-linje
      *
     c     key01         setll     sodtlv
     c     key01         reade     sodtlvr
     c                   dow       not %eof
      *
     c                   if        sdenh1 = p_enhg
     c                   eval      sdenh1 = p_enhn
     c                   endif
      *
6.10 c                   time                    sdedat
6.10 c                   time                    sdetim
6.10 c                   eval      sdeusr = l_user
     c                   update    sodtlvr
      *
     c     key01         reade     sodtlvr
     c                   enddo
      *
      * Lager/innkjøp
      *
     c                   call      'LL755R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    p_enhg
     c                   parm                    p_enhn
      *
      * Butikk
      *
     c                   call      'BD755R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    p_enhg
     c                   parm                    p_enhn
      *
      * Statistikk
      *
     c                   call      'SF755R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    p_enhg
     c                   parm                    p_enhn
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhg
     c                   parm                    p_enhn
      *
      * Key for oppdatering etter oppslag på vare
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av vare-enhet
      *
     c     vvenlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenlu_enhe
8.01  *
8.01  * Key for oppdatering av vare-enhet
8.01  *
8.01 c     vvenl1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    w_vare
8.01 c                   kfld                    vvenl1_enhe
6.11  *
6.11  * Key for oppdatering av vare-enhet-prisgiver
6.11  *
6.11 c     vveplu_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    w_vare
6.11 c                   kfld                    vveplu_penh
8.02  *
8.02  * Key for oppdatering av vare-enhet-prisgiver
8.02  *
8.02 c     vvepl1_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    w_vare
8.02 c                   kfld                    vvepl1_penh
6.20  *
6.20  * Key for les av vare-pris
6.20  *
6.20 c     vvprl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    w_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    vvprlu_ldor
6.20 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
6.20 c                   kfld                    vvprlu_lety
6.20 c                   kfld                    vvprlu_pdat
8.03 c
8.03  *
8.03  * Key for les for å finne om pris finnes fra før
8.03  *
8.03 c     vvprlr_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    w_vare
8.03 c                   kfld                    vvprlr_ldor
8.03 c                   kfld                    vvprlr_prgr
8.03 c                   kfld                    vvprlr_enhe
8.03 c                   kfld                    vvprlr_lety
8.03 c                   kfld                    vvprlr_pdat
      *
      *
      * Henter firma og vare fra parmeter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   eval      w_prgr = *blank
5.70 c                   call      'VL711R  '
5.70 c                   parm                    w_prgr
      *
      *
     c                   endsr
