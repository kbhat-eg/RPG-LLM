      /TITLE  ASOFAK Innkjøps-statistikk, bytter enhet på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SI750R
      * Beskrivelse . . : Innkjøps-statistikk, bytter enhet på vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151199 ASK  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsistl2    if   e           k disk    rename(sistpfr:sistl2r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fsistl1    uf   e           k disk    rename(sistpfr:sistl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sifirm)
     d p_vare          s                   like(sivare)
     d p_enhe          s                   like(sivenh)
      *
      * Definering av variable i nøkler
      *
     d sistl2_vare     s                   like(sivare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d sistl1_paar     s                   like(sipaar)
     d sistl1_binr     s                   like(sibinr)
     d sistl1_line     s                   like(siline)
     d sistl1_bida     s                   like(sibida)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sifirm)
     d w_omrs          s                   like(veomre)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finner omregningsfaktor volum for ny statistikkenhet
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1                             90
     c                   if        *in90 = *on or
     c                             veomre = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_omrs = veomre
      *
      * Leser alle statistikkrader for vare, og omregner antall i.h.t.
      * statistikkenhet
      *
     c                   eval      sistl2_vare = p_vare
     c     sistl2_key    setll     sistl2
     c     sistl2_key    reade     sistl2                                 90
     c                   dow       *in90 = *off
      *
     c                   if        sivenh <> p_enhe
      *
     c                   eval      vvenl1_enhe = sioenh
     c     vvenl1_key    chain     vvenl1                             91
     c                   if        *in91 = *off and
     c                             veomre <> 0
      *
     c                   eval      sistl1_paar = sipaar
     c                   eval      sistl1_binr = sibinr
     c                   eval      sistl1_line = siline
     c                   eval      sistl1_bida = sibida
     c     sistl1_key    chain     sistl1r                            91
     c                   if        *in91 = *off
     c                   eval      sivenh = p_enhe
     c                   eval(h)   sianta = sioant * veomre / w_omrs
     c                   update    sistl1r
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c     sistl2_key    reade     sistl2                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhe
      *
      * Key for les på statistikk
      *
     c     sistl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistl2_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppdatering av statistikk
      *
     c     sistl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistl1_paar
     c                   kfld                    sistl1_binr
     c                   kfld                    sistl1_line
     c                   kfld                    sistl1_bida
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
