     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VP611R
      * Beskrivelse . . : Vare-pris, utskrift av prisbok kunder - styreprog.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270599 HKO  Nytt program
      *       240101 HKO  Lagt inn membernavn ved kall til utskriftsprogram
      *       270606 HKO  Henter leveringstype fra kunde distribusjonsliste
      *                   dersom leveringstype ikke er valgt
5.70  * prgr  280509 HKO  Lagt inn valg av lager i forb. prisgruppe
7.01  *k00  030620 BOF NXS-1195  utvidet med parametere
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkdil1    if   e           k disk    rename(fkdipfr:fkdil1r)
      *
      * Definering av datastrukturer
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_rkat                        3  0 overlay(d_list:4)
     d  d_kund                        6  0 overlay(d_list:7)
     d  d_kpro                        6  0 overlay(d_list:13)
     d  d_vsor                       10    overlay(d_list:19)
     d  d_ogrp                        2  0 overlay(d_list:29)
     d  d_hgrp                        2  0 overlay(d_list:31)
     d  d_ugrp                        3  0 overlay(d_list:33)
     d  d_ldor                        6  0 overlay(d_list:36)
     d  d_sval                        1  0 overlay(d_list:42)
     d  d_tval                        1  0 overlay(d_list:43)
     d  d_2val                        1  0 overlay(d_list:44)
     d  d_rval                        1  0 overlay(d_list:45)
     d  d_lval                        1  0 overlay(d_list:46)
     d  d_lety                        1  0 overlay(d_list:47)
     d  d_dato                         d   overlay(d_list:48) datfmt(*iso)
5.70 d  d_lage                        2  0 overlay(d_list:60)
      *
      * Definering av parametre
      *
     d p_parm          s             64
      *
      * Definering av variabler i nøkler
      *
     d fkdil1_kund     s                   like(fhkund)
     d fkdil1_kpro     s                   like(fhkpro)
      *
      * Definering av variable
      *
     d w_vsor          s             10
     d w_ogrp          s              2  0
     d w_hgrp          s              2  0
     d w_ugrp          s              3  0
     d w_ldor          s              6  0
     d w_sval          s              1  0
     d w_tval          s              1  0
     d w_2val          s              1  0
     d w_rval          s              1  0
     d w_lval          s              1  0
     d w_lety          s              1  0
     d w_dato          s               d   datfmt(*iso)
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_memb          s             10
5.70 d w_lage          s              2  0
      *
     d w_firm          s                   like(fhfirm)
     d w_kund          s                   like(fhkund)
     d w_kpro          s                   like(fhkpro)
     d w_btis          s                   like(fhbtis)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 c                   eval      w_sval = d_sval
7.01 c                   eval      w_tval = d_tval
      * Behandling avhengig av valgte parametre
      *
     c                   select
     c                   when      d_rkat <> 0
     c                   exsr      behandl_rkat
     c                   when      d_kpro <> 0
     c                   exsr      behandl_kpro
     c                   when      d_kund <> 0
     c                   exsr      behandl_kund
     c                   other
     c                   exsr      behandl_tot
     c                   endsl
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på rabattkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_rkat  begsr
      *
      * Leser alle kunder med valgt rabattkategori
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1                                 60
      *
     c                   dow       *in60 = *off
      *
     c                   if        rkrabk <> d_rkat
     c                   goto      neste_kunde
     c                   endif
      *
      * Leser alle kombinasjoner av kunde/kundeprosjekt for kunde.
      * Prisbok skrives kun dersom 'Prisbok' er valgt i distribusjonslisten.
      *
     c                   eval      fkdil1_kund = rkkund
     c     fkdil1_key2   setll     fkdil1
     c     fkdil1_key2   reade     fkdil1                                 61
      *
     c                   dow       *in61 = *off
      *
     c                   if        fhpbok = 1
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
     c                   eval      w_kund = fhkund
     c                   eval      w_kpro = fhkpro
     c                   if        d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   else
     c                   eval      w_vsor = d_vsor
     c                   endif
     c                   if        d_lety = 0
     c                   eval      w_lety = fhlety
     c                   else
     c                   eval      w_lety = d_lety
     c                   endif
     c                   exsr      utskrift
     c                   endif
      *
     c     fkdil1_key2   reade     fkdil1                                 61
     c                   enddo
      *
     c     neste_kunde   tag
      *
     c     rkunl1_key    reade     rkunl1                                 60
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på kundeprosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_kpro  begsr
      *
     c                   eval      w_kund = d_kund
     c                   eval      w_kpro = d_kpro
      *
     c                   eval      fkdil1_kund = d_kund
     c                   eval      fkdil1_kpro = d_kpro
     c     fkdil1_key1   chain     fkdil1                             60
      *
     c                   eval      w_vsor = d_vsor
7.01 c                   if        %found(fkdil1)
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
7.01 c                   endif
     c                   if        *in60  = *off and
     c                             d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   endif
      *
     c                   eval      w_lety = d_lety
     c                   if        *in60  = *off and
     c                             d_lety = 0
     c                   eval      w_lety = fhlety
     c                   endif
      *
     c                   exsr      utskrift
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_kund  begsr
      *
     c                   eval      w_kund = d_kund
      *
     c                   eval      fkdil1_kund = d_kund
     c     fkdil1_key2   setll     fkdil1
     c     fkdil1_key2   reade     fkdil1                                 60
      *
      * Behandling dersom distribusjonsliste ikke finnes for kunde
      *
     c                   if        *in60 = *on
     c                   eval      w_kpro = 0
     c                   eval      w_vsor = d_vsor
     c                   eval      w_lety = d_lety
     c                   exsr      utskrift
     c                   endif
      *
      * Behandling dersom distribusjonsliste finnes for kunde
      *
     c                   dow       *in60 = *off
      *
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
     c                   eval      w_kpro = fhkpro
     c                   if        d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   else
     c                   eval      w_vsor = d_vsor
     c                   endif
     c                   if        d_lety = 0
     c                   eval      w_lety = fhlety
     c                   else
     c                   eval      w_lety = d_lety
     c                   endif
     c                   exsr      utskrift
      *
     c     fkdil1_key2   reade     fkdil1                                 60
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hele distribusjonslisten
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_tot   begsr
      *
     c     fkdil1_key3   setll     fkdil1
     c     fkdil1_key3   reade     fkdil1                                 60
      *
     c                   dow       *in60 = *off
      *
     c                   if        fhpbok = 1
7.01 c                   eval      w_tval = fhtval
7.01 c                   eval      w_sval = fhsval
     c                   eval      w_kund = fhkund
     c                   eval      w_kpro = fhkpro
     c                   if        d_vsor = *blank
     c                   eval      w_vsor = fhvsor
     c                   else
     c                   eval      w_vsor = d_vsor
     c                   endif
     c                   if        d_lety = 0
     c                   eval      w_lety = fhlety
     c                   else
     c                   eval      w_lety = d_lety
     c                   endif
     c                   exsr      utskrift
     c                   endif
      *
     c     fkdil1_key3   reade     fkdil1                                 60
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av prisbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
     c                   if        d_ogrp <> 0
     c                   eval      w_vsor = *blank
     c                   endif
      *
     c                   eval      w_ogrp = d_ogrp
     c                   eval      w_hgrp = d_hgrp
     c                   eval      w_ugrp = d_ugrp
     c                   eval      w_ldor = d_ldor
7.01 c*                   eval      w_sval = d_sval
7.01 c*                   eval      w_tval = d_tval
     c                   eval      w_2val = d_2val
     c                   eval      w_rval = d_rval
     c                   eval      w_lval = d_lval
     c                   eval      w_dato = d_dato
      *
     c                   time                    w_tmst
     c     *iso0         move      w_tmst        w_tmst_20
      *
     c                   eval      w_memb = 'P' + %subst(w_tmst_20:9:9)
      *
     c                   call      'VP612C'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    w_vsor
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_ldor
     c                   parm                    w_sval
     c                   parm                    w_tval
     c                   parm                    w_2val
     c                   parm                    w_rval
     c                   parm                    w_lval
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_btis
5.70 c                   parm                    w_lage
     c                   parm                    w_memb
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for oppslag på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på kunde distribusjonsregister
      *
     c     fkdil1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
     c                   kfld                    fkdil1_kpro
      *
     c     fkdil1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
      *
     c     fkdil1_key3   klist
     c                   kfld                    w_firm
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      d_list = p_parm
      *
     c                   eval      w_firm = d_firm
      *
      * Henter timestamp, for å benytte denne som unik nøkkel på
      * forsendelsen.
      *
     c                   time                    w_btis
5.70 c                   eval      w_lage = d_lage
      *
     c                   endsr
