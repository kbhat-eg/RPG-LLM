     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : ND102R
      * Beskrivelse . . : Vise ordrreferanse
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120318 bsa  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fedhelr    if   e           k disk    rename(edhepfr:edhelrr)
     fnohel1    if   e           k disk    rename(nohepfr:nohel1r)
     fnkeolr    if   e           k disk    rename(nkeopfr:nkeolrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
      *
     fnd102d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d edhelr_tell     s                   like(eotell)
     d edhelr_fsys     s                   like(eofsys)
     d edhelr_ldor     s                   like(eoldor)
     d edhelr_ldat     s                   like(eoldat)
     d nkeolr_kode     s                   like(nkkode)
     d rlevl1_levr     s                   like(rllevr)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(eofirm)
     d w_ldor          s                   like(eoldor)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(eofirm)
     d p_fsys          s                   like(eofsys)
     d p_tell          s                   like(eotell)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     edhelr_key    setll     edhelr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise tilhørende linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   call      'ND103R'
     c                   parm                    w_firm
     c                   parm                    p_fsys
     c                   parm                    p_tell
     c                   parm                    b1ldor
     c                   parm                    b1ledt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     edhelr_key2   reade     edhelr
      *
     c                   if        %eof or
     c                             eofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1fsys = eofsys
     c                   eval      b1tell = eotell
     c                   eval      b1numm = eonumm
     c                   eval      b1suff = eosuff
     c                   eval      b1otyp = *blanks
     c                   eval      fohel1_numm = eonumm
     c                   eval      fohel1_suff = eosuff
     c     fohel1_key    chain     fohel1
     c                   if        %found
     c                   eval      b1otyp = footyp
     c                   eval      b1bnum = fobenr
     c                   endif
      *
     c                   eval      b1ldor = eoldor
     c                   eval      b1lnav = *blanks
     c                   eval      rlevl1_levr = eoldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
     c                   eval      b1ledt = eoldat
     c                   eval      b1ldat = 0
     c                   if        eoldat <> *loval
     c     *dmy          move      eoldat        b1ldat
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     edhelr_key    setgt     edhelr
     c     edhelr_key2   readpe    edhelr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c     edhelr_key2   readpe    edhelr
      *
     c                   if        %eof or
     c                             eofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     edhelr_key    setll     edhelr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_fsys
     c                   parm                    p_tell
      *
      * Key for oppdatering av ekstern ordre-hode
      *
     c     edhelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    edhelr_fsys
     c                   kfld                    edhelr_tell
     c                   kfld                    edhelr_ldor
     c                   kfld                    edhelr_ldat
      *
      * Key for oppdatering av ekstern ordre-hode
      *
     c     edhelr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    edhelr_fsys
     c                   kfld                    edhelr_tell
      *
      * Key for henting av ekstern ordrekode
      *
     c     nkeolr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeolr_kode
      *
      * Key for henting av ekstern ordrekode
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for henting av ekstern ordrekode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på ekstern ordre-hode
      *
     c     nohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    p_fsys
     c                   kfld                    p_tell
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      b2fsys = p_fsys
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
     c                   eval      nkeolr_kode = p_fsys
     c     nkeolr_key    chain     nkeolr
     c                   eval      b2besk = nkbesk
      *
     c     nohel1_key    chain     nohel1
      *
     c                   eval      b2nref = nonref
     c                   eval      b2knav = noknav
     c                   eval      b2pnav = nopnav
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      edhelr_fsys = p_fsys
     c                   eval      edhelr_tell = p_tell
     c                   eval      edhelr_ldor = 0
     c                   eval      edhelr_ldat = *loval
     c     edhelr_key    setll     edhelr
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
