     h option(*nodebugio)
      /title  ASOFAK Lev.vare, henter vareregister - Emco
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW724R
      * Beskrivelse . . : Danning av leverandør vareregister, Emco
      *
      * Spesialversjon. : Tilpasset innlesing internt for Emco
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240601 HKO  Nytt program
      *       290304 GRO  Lagt til leveringstype
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Emco
      *
     d                 ds
     d d_inpu                       256
     d  d_evar                       15    overlay(d_inpu:1)                    Eget varenr
     d  d_vare                       15    overlay(d_inpu:16)                   Lev.varenr/Varenr
     d  d_tek1                       35    overlay(d_inpu:31)                   Varetekst-1
     d  d_tek2                       35    overlay(d_inpu:66)                   Varetekst-2
     d  d_nobb                        8    overlay(d_inpu:101)                  Nobb-nr
     d  d_eann                       13    overlay(d_inpu:109)                  EAN-nr
     d  d_enhe                        3    overlay(d_inpu:122)                  Enhet
     d  d_inpr                       10    overlay(d_inpu:125)                  Innkjøpspris
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)                    Innkjøpspris - kr
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)                    Innkjøpspris - øre
     d  d_sapr                       10    overlay(d_inpu:135)                  Salgspris
     d   d_sapr_kr                    7  0 overlay(d_sapr:1)                    Salgspris - kr
     d   d_sapr_Øre                   2  2 overlay(d_sapr:9)                    Salgspris - øre
     d  d_anta                       12    overlay(d_inpu:145)                  Antall/annbrekk
     d   d_anta_hel                   8  0 overlay(d_anta:1)
     d   d_anta_des                   3  3 overlay(d_anta:10)
     d  d_vekt                       10    overlay(d_inpu:157)                  Vekt
     d   d_vekt_hel                   6  0 overlay(d_vekt:1)
     d   d_vekt_des                   3  3 overlay(d_vekt:8)
     d  d_lety                        2    overlay(d_inpu:167)                  Enhet
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fwfirm)
     d w_lety          s              1
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = fwirad
      *
     c                   if        %check(digits : d_evar) = 0
     c                   exsr      oppd_pris
     c                   endif
      *
     c                   read      flvwpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av prislinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
      * Oppdaterer bare hvis salgspris-felt er utfyllt
      *
     c                   if        d_sapr = *blank
     c                   leavesr
     c                   endif
      *
      * Oversetter til numeriske felt
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
     c     ' ':'0'       xlate     d_sapr        d_sapr
     c     ' ':'0'       xlate     d_anta        d_anta
     c     ' ':'0'       xlate     d_vekt        d_vekt
      *
      * Oversetter tekster til ascii og store bokstaver
      *
     c                   eval      d_evar = %trim(d_evar)
     c                   eval      d_vare = %trim(d_vare)
     c                   eval      d_tek1 = %trim(d_tek1)
     c                   eval      d_tek2 = %trim(d_tek2)
      *
     c     X'46':'Æ'     xlate     d_vare        d_vare
     c     X'A0':'æ'     xlate     d_vare        d_vare
     c     X'77':'Ø'     xlate     d_vare        d_vare
     c     X'90':'ø'     xlate     d_vare        d_vare
     c     X'2A':'Å'     xlate     d_vare        d_vare
     c     X'EF':'å'     xlate     d_vare        d_vare
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
     c     X'46':'Æ'     xlate     d_tek2        d_tek2
     c     X'A0':'æ'     xlate     d_tek2        d_tek2
     c     X'77':'Ø'     xlate     d_tek2        d_tek2
     c     X'90':'ø'     xlate     d_tek2        d_tek2
     c     X'2A':'Å'     xlate     d_tek2        d_tek2
     c     X'EF':'å'     xlate     d_tek2        d_tek2
      *
     c     Lo:Up         xlate     d_tek1        d_tek1
     c     Lo:Up         xlate     d_tek2        d_tek2
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwldor = p_ldor
     c                   eval      fwlvar = d_vare
     c                   eval      fwenhe = d_enhe
      *
     c                   move      d_nobb        fwnobb
      *
      * Henter event. lev./lev.varenummer fra eget varenummer
      *
     c                   if        d_evar <> *blank
     c                   eval      vvarl1_vare = d_evar
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      fwldor = vvldor
     c                   eval      fwlvar = vvlvar
     c                   eval      fwnobb = vvnobb
     c                   if        fwenhe = *blank
     c                   eval      fwenhe = vvenhe
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = d_tek2
     c                   eval      fweann = 0
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
     c                   eval      fwsapr = d_sapr_kr + d_sapr_Øre
     c                   eval      fwdato = *loval
     c                   eval      fwvekt = d_vekt_hel + d_vekt_des
     c                   eval      fwbmen = d_anta_hel + d_anta_des
     c                   eval      fwlvae = *blank
     c                   eval      fwtvar = *blank
     c                   eval      fwhkod = 1
      *
     c                   eval      fwlety = 0
     c                   if        %len(%trim(d_lety)) = 1
     c                   eval      w_lety = %trim(d_lety)
     c                   if        %check(digits : w_lety) = 0
     c                   move      w_lety        fwlety
     c                   endif
     c                   endif
      *
     c                   eval      fwline = fwline + 1
      *
     c                   move      d_eann        fweann
      *
     c                   time                    fwdato
      *
      * Konverterer enhet
      *
     c                   eval      fenkl1_enhf = fwenhe
     c     fenkl1_key    chain     fenkl1
     c                   if        %found
     c                   eval      fwenhe = feenht
     c                   endif
      *
      * Skriver til fil
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
