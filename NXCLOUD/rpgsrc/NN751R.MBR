     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NN751R
      * Beskrivelse . . : Nettbutikk, eksport av ordrebekref. - danner fil
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221002 HKO  Nytt program
6.20  * 6.20  210813 bsa  Rekompilert grunnet endret ANBLPF
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.31  * 6.30  080219 bof  Rekompilert grunnet endret ANBLPF
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
      *
     fnetores   uf a e           k disk
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_nref          s              6  0
6.nu d p_numm          s              8  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_avde                947    950  0
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         16
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                 9    overlay(d_bgm: 5) inz('IMLORDRES')
     d  d_bgmfill2                    2    overlay(d_bgm:14) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:16) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - ordrebekreftelse
      *
     d                 ds
6.nu d d_ore                         19
     d  d_orercd                      3    overlay(d_ore:  1) inz('ORE')
     d  d_orefill1                    1    overlay(d_ore:  4) inz(';')
     d  d_oreref                      6  0 overlay(d_ore:  5)
     d  d_orefill2                    1    overlay(d_ore: 11) inz(';')
6.nu d  d_orenum                      8  0 overlay(d_ore: 12)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d anbll3_lfgr     s                   like(aolfgr)
     d anbll3_lfir     s                   like(aolfir)
     d anbll3_lavd     s                   like(aolavd)
      *
      * Definering av variable
      *
     d w_tmst          s               z
     d w_tell          s             12  0
     d w_ponr          s              4
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netoresr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netoresr
      *
      * Organisasjon
      * Henter informasjon fra nettbutikk.
      * Avbryter dersom nettbutikk ikke ble funnet
      *
     c                   eval      anbll3_lfgr = l_fgrp
     c                   eval      anbll3_lfir = l_firm
     c                   eval      anbll3_lavd = l_avde
     c     anbll3_key    chain     anbll3
     c                   if        not %found
     c                   eval      anbll3_lavd = 0
     c     anbll3_key    chain     anbll3
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c                   eval      d_orgid = aoleie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aolnav
      *
     c                   eval      data = d_org
     c                   write     netoresr
      *
      * Ordrebekreftelse
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_oreref = p_nref
6.nu c                   eval      d_orenum = p_numm
      *
     c                   eval      data = d_ore
     c                   write     netoresr
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netoresr
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_nref
6.nu c                   parm                    p_numm
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_lfgr
     c                   kfld                    anbll3_lfir
     c                   kfld                    anbll3_lavd
      *
     c                   endsr
