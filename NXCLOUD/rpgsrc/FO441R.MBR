      /TITLE  ASOFAK: Fjerning fra historisk fakturaregister hovedproram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO441R
      * Beskrivelse . . : Fjerning fra historisk fakturaregister -
      *                                hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohel5    if   e           k disk    rename(sohepfr:sohel5r)
     fsohelu    uf   e           k disk    rename(sohepfr:sohelur)
     fsodtlu    uf   e           k disk    rename(sodtpfr:sodtlur)
      *
      * Definering av parametre
      *
     d p_afir          s              3
     d p_dato          s               d   datfmt(*iso)
      *
      * Definering av variable i nøkler
      *
     d sohelu_aarr     s                   like(soaarr)
     d sohelu_binr     s                   like(sobinr)
     d sohelu_numm     s                   like(sonumm)
     d sohelu_suff     s                   like(sosuff)
     d sodtlu_aarr     s                   like(sdaarr)
     d sodtlu_binr     s                   like(sdbinr)
     d sodtlu_numm     s                   like(sdnumm)
     d sodtlu_suff     s                   like(sdsuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sofirm)
     d w_date          s                   like(sofkda)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hovedprogram                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sohel5_key    setll     sohel5
     c                   read      sohel5                                 60
      *
     c                   dow       *in60 = *off
     c                             and sofkda < p_dato
      *
     c                   exsr      oppdat                                       Backup/slett
      *
     c                   read      sohel5                                 60
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av fakturahode og fakturalinje tabeller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
     c                   eval      sohelu_aarr = soaarr
     c                   eval      sohelu_binr = sobinr
     c                   eval      sohelu_numm = sonumm
     c                   eval      sohelu_suff = sosuff
      *
     c                   eval      sodtlu_aarr = soaarr
     c                   eval      sodtlu_binr = sobinr
     c                   eval      sodtlu_numm = sonumm
     c                   eval      sodtlu_suff = sosuff
      *
     c     sohelu_key    chain     sohelu                             61
     c                   if        *in61 = *off
      *
     c                   delete    sohelur                                      Slette hode
      *
     c     sodtlu_key    setll     sodtlu
     c     sodtlu_key    reade     sodtlu                                 61
      *
     c                   dow       *in61 = *off
     c                   delete    sodtlur                                      Slette linje
     c     sodtlu_key    reade     sodtlu                                 61
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_afir
     c                   parm                    p_dato
      *
      * Key for les på faktura-hode -tabell
      *
     c     sohel5_key    klist
     c                   kfld                    w_firm
      *
      * Key for sletting fra faktura-hode -tabell
      *
     c     sohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohelu_aarr
     c                   kfld                    sohelu_binr
     c                   kfld                    sohelu_numm
     c                   kfld                    sohelu_suff
      *
      * Key for sletting fra faktura-linje -tabell
      *
     c     sodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtlu_aarr
     c                   kfld                    sodtlu_binr
     c                   kfld                    sodtlu_numm
     c                   kfld                    sodtlu_suff
      *
      * Initierer nøkkel-verdier
      *
     c                   move      p_afir        w_firm
      *
     c                   endsr
