      /TITLE  ASOFAK Lev.vare, innlesing av varer - styreprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW102R
      * Beskrivelse . . : Innlesing av varer/priser fra leverandør,
      *                   styreprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       280408 bof  Utvidet med generelt SDV format (T.M.Bergersen)
5.61  *       090309 bof  Utvidet med generelt Integ.dat format
      *       010909 ask  Endret kall til CL for prisfil innlesning
      *       210909 bof  Laget også en variant for qdls
5.62  *       110810 bof  Lagt inn valg på kontroll-liste (IFS)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvalu    uf   e           k disk    rename(flvapfr:flvalur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fwfirm)
     d p_ldor          s                   like(fwldor)
      *
     d p_file          s             12
     d p_fldr          s             20
     d p_prog          s             10
     d p_feil          s              1
     d p_valg          s              2  0
      *
      * Definering av variabler i nøkler
      *
     d flvalu_ldor     s                   like(fwldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fwfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kaller program for kopiering av fil fra leverandør over til
      * arbeidsfil
5.62 c                   if         p_valg = 27 or
5.62 c                              p_valg = 29
      *
      * ASK: Endret call til FW103C som bruker IFS i stenden for QDLS
      *
     c                   call      'FW103C'
     c                   parm                    p_file
     c                   parm                    p_fldr
     c                   parm                    p_feil
      *
     c                   else
      *
     c                   call      'FW102C'
     c                   parm                    p_file
     c                   parm                    p_fldr
     c                   parm                    p_feil
      *
     c                   endif
      *
      * Avbryter dersom feil
      *
     c                   if        p_feil = *on
     c                   goto      avslutt
     c                   endif
      *
      * Tømmer leverandør-vareregister for gamle varer/priser fra
      * leverandør
      *
     c                   eval      flvalu_ldor = p_ldor
     c     flvalu_key    setll     flvalu
     c     flvalu_key    reade     flvalur                                90
     c                   dow       *in90 = *off
     c                   delete    flvalur
     c     flvalu_key    reade     flvalur                                90
     c                   enddo
      *
      * Hvis Gen.SDV format - behandles dette annerledes
     c                   if        p_prog = 'FW725R   '
     c                   call      p_prog
     c                   goto      avslutt
     c                   endif
      *
      * Hvis Gen Integ.dat  - behandles dette annerledes
     c                   if        p_prog = 'FW727R   '
     c                   call      'FW727C '
     c                   parm                    p_ldor
     c                   goto      avslutt
     c                   endif
5.62  *
5.62  * Hvis Kontroll-liste -  behandles dette annerledes
5.62 c                   if        p_prog = 'FW729R   '
5.62 c                   call      'FW729C '
5.62 c                   goto      avslutt
5.62 c                   endif
      *
      * Kaller program for lesing av fil fra leverandør over til
      * leverandør-vareregister.
      *
     c                   call      p_prog
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_file
     c                   parm                    p_fldr
     c                   parm                    p_prog
     c                   parm                    p_ldor
     c                   parm                    p_valg
     c                   parm                    p_feil
      *
      * Key for oppdatering av leverandør vareregister
      *
     c     flvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    flvalu_ldor
      *
      *
      * Henter firmanummer fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
