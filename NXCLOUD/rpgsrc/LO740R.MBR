     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LO740R
      * Beskrivelse . . : Styreprogram - sende hva/hvor
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
5.21  *    R5.21  060204  Lagt inn tillegg for generering av ordre
5.21  *                   fra en bestilling  (RingAlm-løsningen)
      *
      *    R5.52  110907  Koder om NORGROS/WEB for flatfilformat    TSV
      *    R5.53  170907  Lagt på beh. bilde/tekst for kampanje.    TSV
      *    6.10   180609  Oppdatert med sporingsinformasjon         BSA
6.11  *    6.11   240112  Legger inn ekstra sjekker hvis leverandør BSA
6.11  *                   er NEB sertfisert.                        BSA
6.12  *    6.10   070213  Flagger flere feilstatuser fra LI653      BSA
6.13  *    6.10   110213  Endrer feilmelding ved overføring         BSA
6.20  *    R6.20  240310  Hvis overføring ikke går bra, sett status bof
6.21  *    R6.20  240912  Utvidet LLDIPF med Neb-godkjent
6.22  *    R6.20  110414  Endrer måten feilmelding sendes pga jacada
6.23  *  6.20 291215 nho  Sjekk leveringsmåte
6.24  *  6.20 040115 nho  Må sjekke om leveringsmåte er et krav
6.25  *  6.20     050116  Hvis intern bestilling og vi kommer fra
6.25  *                   ordre, sjekk om vi allerede har FO100R i
6.25  *                   program stack (rekursivt kall).
6.nu  *    6.30   050614  Utvidelser mtp. nummerutvidelse           bof
6.30  *  6.30 230616 bsa  Sjekk om overføring allerede pågår.
6.31  *  6.30 230616 bsa  Hvis feilkode på varelinjer, vis disse med feilkode
6.32  *  6.30 210217 bsa  Sjekker mot feil navn på overføringsanmodning
7.01  *  7.00 061021 bof  Bruke AA005R for feilmeldinger
      *
8.01  *  800  180522 ask  Lagt inn sending av kopi til distribusjonssenter
      *                   dersom switch "BEST_RDS" er satt på.
8.02  *  800  280822 ask  Sjekker om internbestilling til RDS eller kopi til RDS
      *                   Styrer sendinger basert på dette.
      *                   Har også fjernet valg "sending til IBM"
8.03  *  800  201022 bsa  Nytt valg fra leverandørditribusjon skal flagge
8.03  *  800              at bestilling er sendt, og sette loggkode 2.
8.04  *  800  011122 ask  Lagt inn kopi til RDS ved NGN_EDI der det er aktuelt
8.05  *  800  011122 ask  Passivisert funksjon for internbestilling - erstattet av NGN_EDI
8.06  *  800  161122 ask  Lagt inn test for å unngå kopi på intern bestilling
8.07  *  800  211122 ask  Endret nøkkelbegrep på eksdternt lager fra N(2 0) til A(20)
8.08  *  800  151222 bsa  Skal endre leveringsdato også hvis NGN_EDI er valgt.
8.08  *  800              Ref 8.05 i LO760R.
8.09  * K000  100123 bsa  Send transportrequest hvis tilhørende ordre oppfyller
8.09  * K000              krav til kjøring.
8.10  *K000   180123 bsa  Flagg om skaffetekst i transportlogg.
8.11  *K000   060223 bsa  Flagg om sjekk på loggkode i transportlogging
8.12  *  800  120923 bsa  Forsøker unngå unødige ENTER trykk.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KC = F3  = Exit
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
6.21 f*ldnlr    if   e           k disk    rename(lldnpfr:lldnlrr)
     flldilu    uf   e           k disk    rename(lldipfr:lldilur)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
8.08 flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.24 favall1    if   e           k disk    rename(avalpfr:avall1r)
8.09 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
      *
     flo740d    cf   e             workstn
      *
      * Datastruktur
      *
     d                 ds
     d d_urec                        80
     d  d_firm                        3  0 overlay(d_urec)
6.nu d  d_numm                        8  0 overlay(d_urec:4)
6.nu d  d_suff                        2  0 overlay(d_urec:12)
6.nu d  d_kode                        1    overlay(d_urec:14)
6.nu d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:33)
6.nu d  d_wsid                       10    overlay(d_urec:43)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
8.01 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d lldilu_ldor     s                   like(lpldor)
     d lldilu_lage     s                   like(lplage)
     d rlevl1_levr     s                   like(rllevr)
6.24 d avall1_apgm     s                   like(avapgm)
8.08 d lodtlu_numm     s                   like(ldnumm)
8.08 d lodtlu_suff     s                   like(ldsuff)
8.08 d lodtlu_line     s                   like(ldline)
8.09 d fohel1_numm     s                   like(fonumm)
8.09 d fohel1_suff     s                   like(fosuff)
      *
      * Definering av variable i parametre
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
6.20 d p_stat          s              1
     d p_otyp          s              4
     d p_akod          s              2  0
     d p_aktk          s              2
     d p_luke          s              4  0
5.53 d p_text          s             35
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_mld1          s             50    inz(*blank)
     d w_stmp          s               z
     d w_stmp_alf      s             26
     d w_memb          s             10
     d w_lknr          s             10
     d w_numm_alf      s              6
     d w_leng          s              2  0
     d w_stat          s              1
     d w_parm          s             80
6.11 d w_bsts          s              1
6.11 d w_numm          s                   like(lonumm)
6.11 d w_suff          s                   like(losuff)
6.30 d w_navn          s             10
6.30 d w_osta          s              1
8.02 d w_rdsk          s              1
8.02 d w_lagd          s                   like(lolage)
8.02 d w_lmat          s                   like(lolmat)
8.07 d w_ekst          s             20
8.09 d w_reqs          s              5
8.10 d w_skaf          s              1  0
8.11 d w_logg          s              1  0
      *
6.25 d r_prog          s             10
6.25 d r_stat          s              1
      *
     d w_feil          s              1    inz(*off)
8.06 d w_int           s              1    inz(*off)
8.01 d u_rds           s              1    inz(*off)
8.09 d u_flat          s              1    inz(*on)
      *
      * Definering av konstanter
      *
     d c_null          s              6    inz('000000')
8.01  *
8.01  * Definering av eksterne prg
8.01  *
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   DCL-PR CO402R EXTPGM('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
8.01     p_verdi2            char(2048);
8.04   END-PR;
8.04  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * A1BLD  - A1 Bilde for overføringsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram - legg ut informasjon og start overføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finn ordre
      *
     c                   eval      a1stat = *blank
     c                   eval      lohelr_numm = p_numm
     c                   eval      lohelr_suff = p_suff
     c     lohelr_key    chain     lohelrr
     c                   if        not %found
     c                   exsr      feil_ut
     c                   goto      pgm_end
     c                   endif
      *
     c                   if        lobsen = 1
     c                   if        lobsda <> *loval
     c     *dmy          move      lobsda        a1sida
     c                   endif
     c                   eval      a1sius = lobsav
     c                   eval      a1stat = 'BEST. SENDT FØR - F10=OMKJØRING'
     c                   eval      *in31 = *on
     c                   endif
      *
      * Finn leverandør på ordre
      *
     c                   eval      a1ltxt = *blank
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   exsr      feil_ut
     c                   goto      pgm_end
     c                   endif
     c                   eval      a1ldor = loldor
     c                   eval      a1ltxt = rlnavn
      *
      * Finn forsendelsesinfo
      *
     c                   eval      *in41 = *on
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      lldilr_lage = *blank
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   exsr      feil_ut
     c                   goto      pgm_end
     c                   endif
     c                   endif
6.11  *
6.11  * Sjekk om det skal sjekkes etter NEB standard
6.11  *
6.11 c                   eval      *in42 = *on
6.11 c**                 eval      a1nebg = '1'
6.21 c*                  move      lolage        lldilr_lage
6.21 c*    lldilr_key    chain     lldnlr
6.21 c*                  if        not %found
6.21 c*                  eval      lldilr_lage = *blank
6.21 c*    lldilr_key    chain     lldnlr
6.21 c*                  if        not %found
6.21 c*                  eval      *in42 = *off
6.21 c**                 eval      a1nebg = *blanks
6.21 c*                  else
6.21 c                   if        lpnebg <> '1'
6.11 c                   eval      *in42 = *off
6.11 c**                 eval      a1nebg = *blanks
6.11 c                   endif
6.21 c*                  endif
6.21 c*                  else
6.21 c*                  if        lpnebg <> '1'
6.21 c*                  eval      *in42 = *off
6.21 c**                 eval      a1nebg = *blanks
6.21 c*                  endif
6.21 c*                  endif
      *
     c                   eval      a1type = lptype
     c                   eval      a1form = lpform
     c                   eval      a1mott = lpmott
8.02  *
8.02  * Sjekk om kopi til RDS eller bestilling fra RDS
8.02  *
8.02 c                   eval      w_rdsk = ' '
8.02 c                   if        u_rds = *on
8.02 c                   exsr      sjk_rds_bes
8.02  *
8.06 c                   if        w_rdsk = ' ' and
8.06 c                             w_int = *off
8.02 c                   exsr      sjk_rds_kop
8.02 c                   endif
8.02 c                   endif
      *
     c     a1bld_ut      tag
5.52  *
     c                   exfmt     a1bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on
     c                   goto      pgm_end
     c                   endif
6.24  *
6.24  * Firmastyrt validering av utvalgte felter
6.24  *
6.24  *
6.24 c                   eval      avall1_apgm = 'LO102R'
6.24 c     avall1_key    setll     avall1
6.24 c     avall1_key    reade     avall1
6.24 c                   dow       not %eof(avall1)
6.24 c                   if        avaflt = 'LOLMAT'  and
6.24 c                             avaval = 1
6.23 C                   IF        LOLMAT = 0
7.01 C*                  MOVE      *ON           *IN38
7.01 c                   eval      w_mld1 = 'Forsendelsesmåte ble ikke +
7.01 c                                       funnet.'
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
6.23 C                   goto      a1bld_ut
6.23 C                   ENDIF
6.24 c                   endif
6.24 c     avall1_key    reade     avall1
6.24 c                   enddo
      *
     c                   if        lobsen = 1 and
     c                             *inkj = *off
     c                   goto      pgm_end
     c                   endif
6.13  * Sjekk om det ligger GLN nummer på leverandøren
8.03 c                   if        %trim(lpform) = 'EDI' or
8.03 c                             %trim(lpform) = 'NGN_EDI'
6.13 c                   if        rleanl = 0
7.01 c*                  eval      *in44 = *on
7.01 c                   eval      w_mld1 = 'Leverandør mangler info+
7.01 c                                       . Må rettes.'
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
6.13 c                   goto      a1bld_ut
6.13 c                   endif
6.13 c                   endif
6.11  *
6.11  * Sjekk kvaliteten på varelinjene hvis NEB standard
6.11  *
6.11 c                   if        *in42 = *on
6.11 c                   eval      *in43 = *off
6.11 c                   eval      w_bsts = *blank
6.11 c                   eval      w_numm = p_numm
6.11 c                   eval      w_suff = p_suff
6.11 c                   call      'LI653R'
6.11 c                   parm                    w_numm
6.11 c                   parm                    w_suff
6.11 c                   parm                    w_bsts
6.11  * Hvis feilkode kommer i retur, send feilmelding
6.11 c                   if        w_bsts = '1'
6.31 c                   call      'LO741R'
6.31 c                   parm                    w_numm
6.31 c                   parm                    w_suff
6.31 c**                 eval      *in43 = *on
7.01 c                   eval      w_mld1 = 'Varer på bestilling +
7.01 c                                       ikke NEB Std. '
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
6.11 c                   goto      a1bld_ut
6.11 c                   endif
6.12  * Hvis feilkode kommer i retur, send feilmelding (leverandørinfo)
6.12 c                   if        w_bsts = '2'
7.01 c*                  eval      *in44 = *on
7.01 c                   eval      w_mld1 = 'Leverandør mangler info+
7.01 c                                       . Må rettes.'
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
6.12 c                   goto      a1bld_ut
6.12 c                   endif
6.11 c                   endif
6.25  *
6.25  * Hvis det er snakk om intern bestilling, sjekk om vi
6.25  * vi allerede har FO100R i program stack. Gi feilmelding
6.25  *
6.25 c                   if        lpform = 'INTERN    ' and
6.25 c                             lptype = 'INTERN   '
6.25 c                   eval      r_stat = *blanks
6.25 c                   eval      r_prog = 'FO100R'
6.25 c                   call      'AP650R'
6.25 c                   parm                    r_prog
6.25 c                   parm                    r_stat
6.25  * Hvis programmet allerede finnes i stack, kan vi ikke kalle det på nytt
6.25 c                   if        r_stat = '1'
6.25 c                   eval      w_mld1 = 'Prog vil feile. Gå t+
6.25 c                                       il meny, prøv på nytt'
6.25 c                   call      'AA005R'
6.25 c                   parm                    w_mld1
6.25 c                   goto      a1bld_ut
6.25 c                   endif
6.25 c                   endif
6.30  *
6.30  * Sjekk om overfølring allerede pågår.
6.30  * Webtrade
6.30 c                   eval      *in45  = *off
6.30 c                   eval      w_navn = *blanks
6.30 c                   eval      w_osta = *blanks
6.30 c                   if        %trim(lpform) = 'WEBTRADE' and
6.30 c                             %trim(lptype) = 'FTP'
6.30 c                   eval      w_navn = 'JERNIABEST'
6.30 c                   endif
6.30  * NBS
6.30 c                   if        %trim(lpform) = 'EDI' and
6.30 c                             %trim(lptype) = 'FTP' and
6.30 c                             %trim(lpmott) = 'NBS'
6.30 c                   eval      w_navn = 'NBSEDISND'
6.30 c                   endif
6.30  * FARVERINGEN
6.30 c                   if        %trim(lpform) = 'FARVERING' and
6.30 c                             %trim(lptype) = 'FTP'
6.30 c                   eval      w_navn = 'FARVERING'
6.30 c                   endif
6.30  * EFONELFO / HEIDENREICH
6.30 c                   if        %trim(lpform) = 'EFONELFO' and
6.30 c                             %trim(lptype) = 'FTP'
6.30 c                   eval      w_navn = 'HEIDENBEST'
6.30 c                   endif
6.30  * LØVENSKIOLD
6.30 c                   if        %trim(lpform) = 'LOVENSKIOL' and
6.30 c                             %trim(lptype) = 'FTP'
6.32 c                   eval      w_navn = 'LOVENSBEST'
6.30 c                   endif
6.30  * NORGROS
6.30 c                   if        %trim(lpform) = 'NORGROS' and
6.30 c                             %trim(lptype) = 'FTP'
6.30 c                   eval      w_navn = 'NBSEDISND'
6.30 c                   endif
6.30  *
6.30 c                   if        w_navn <> *blanks
6.30 c                   call      'AF712R'
6.30 c                   parm                    w_navn
6.30 c                   parm                    w_osta
6.30  * Overføring pågår ??
6.30 c                   if        w_osta <> *blanks
7.01 c*                  eval      *in45 = *on
7.01 c                   eval      w_mld1 = 'Overføring pågår. Vent litt +
7.01 c                                       og forsøk igjen'
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
6.30 c                   goto      a1bld_ut
6.30 c                   endif
6.30  *
6.30  * Overføring pågår alikevel - sjekk mot LWEDUT
6.30  *
6.30 c                   call      'AF713C'
6.30 c                   parm                    w_osta
6.30  *
6.30 c                   if        w_osta <> *blanks
7.01 c*                  eval      *in45 = *on
7.01 c                   eval      w_mld1 = 'Overføring pågår. Vent litt +
7.01 c                                       og forsøk igjen'
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
6.30 c                   goto      a1bld_ut
6.30 c                   endif
6.30  *
6.30 c                   endif
      *
      *  Dersom WEBTRADE format - hent inn tilleggsinformasjon til Jernia
      *
     c                   if        lpform = 'WEBTRADE  '
      *
     c                   eval      a2otyp = 'LAG '
     c                   eval      a2akod = lolage
      *
     c     a2bld_ut      tag
     c                   exfmt     a2bld
      *
     c                   if        *inkc = *on
     c                   goto      a1bld_ut
     c                   endif
      *
     c                   if        a2otyp <> 'LAG ' and
     c                             a2otyp <> 'HAS ' and
     c                             a2otyp <> 'FOE '
     c                   eval      *in31 = *on
     c                   goto      a2bld_ut
     c                   endif
      *
     c                   if        a2otyp = 'FOE ' and
     c                             a2aktk = *blank
     c                   eval      *in32 = *on
     c                   goto      a2bld_ut
     c                   endif
      *
     c                   if        a2otyp = 'FOE ' and
     c                             a2luke = 0
     c                   eval      *in33 = *on
     c                   goto      a2bld_ut
     c                   endif
     c                   endif
      *
      *  Dersom NORGROS format - hent inn tilleggsinformasjon til Norgros
      *
     c                   if        lpform = 'NORGROS   '
5.53 c     a3bld_ut      tag
 .   c                   eval      a3text = *blanks
 .   c                   exfmt     a3bld
 .    *
 .   c                   if        *inkc = *on
 .   c                   goto      a1bld_ut
 .   c                   endif
5.53 c                   eval      p_text = a3text
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Valg av format og forsendelsesmetode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     data_ut       tag
     c                   exsr      crt_memb
      *
      * JERNIA/WEBTRADE
      *
     c                   if        lpform = 'WEBTRADE '
     c                   eval      p_otyp = a2otyp
     c                   eval      p_akod = a2akod
     c                   eval      p_aktk = a2aktk
     c                   eval      p_luke = a2luke
     c                   CALL      'LO750C'
     c                   parm                    w_memb
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_otyp
     c                   parm                    p_akod
     c                   parm                    p_aktk
     c                   parm                    p_luke
      *
     c                   if        lptype = 'SNA      '
     c                   CALL      'LO751C'
     c                   parm                    w_memb
     c                   parm                    w_feil
      *
     c                   if        w_feil = *off
     c                   eval      a1stat = 'BESTILLING ER SENDT JERNIA!'
     c                   else
     c                   eval      a1stat = 'BESTILLING IKKE SENDT!'
     c                   endif
     c                   exsr      overf_stat
     c                   endif
      *
     c                   if        lptype = 'FTP      '
     c                   move      p_numm        w_numm_alf
     c                   CALL      'LO752C'
     c                   parm                    w_numm_alf
     c                   parm                    w_memb
     c                   parm                    p_otyp
     c                   parm                    w_feil
      *
     c                   if        w_feil = *off
     c                   eval      a1stat = 'BESTILLING ER SENDT JERNIA!'
     c                   else
     c                   eval      a1stat = 'BESTILLING IKKE SENDT!'
     c                   endif
     c                   exsr      overf_stat
     c                   endif
     c                   endif
      *
      * EDI
      *
     c                   if        lpform = 'EDI      '
8.02  *
8.02 c                   if        w_rdsk = 'B'
8.02 c                   CALL      'LO768C'
8.02 c                   parm                    w_memb
8.02 c                   parm                    p_numm
8.02 c                   parm                    p_suff
8.02  *
8.02 c                   else
     c                   CALL      'LO760C'
     c                   parm                    w_memb
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *
     c                   if        lptype = 'FTP      ' and
     c                             lpmott = 'NBS            '
     c                   CALL      'LO763C'
     c                   parm                    w_memb
     c                   parm                    w_feil
6.13  * Returnerer med 'E' hvis det har gått feil
6.13 c                   if        w_feil = 'E'
6.13 c                   eval      w_feil = *on
0.13 c                   else
6.13 c                   eval      w_feil = *off
6.13 c                   endif
      *
     c                   if        w_feil = *off
     c                   eval      a1stat = 'BESTILLING ER SENDT NBS!'
     c                   else
6.13 c**                 eval      a1stat = 'BESTILLING ER IKKE SENDT'
6.13 c                   eval      a1stat = 'FEIL I OVERFØRING TIL NBS !'
     c                   endif
8.02 c                   endif
     c                   endif
8.01  *
8.01  * Overfører kopi av bestilling til distribusjonssenter hvis koden er satt på
8.01  *
8.02 c                   if        w_rdsk = 'C'
8.01 c                   CALL      'LO768C'
8.01 c                   parm                    w_memb
8.01 c                   parm                    p_numm
8.01 c                   parm                    p_suff
8.01 c                   endif
      *
     c                   exsr      overf_stat
5.52 c                   endif
      *
      * FARVERINGEN
      *
     c                   if        lpform = 'FARVERING'
     c                   CALL      'LO770C'
     c                   parm                    w_memb
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
     c                   if        lptype = 'FTP      '
     c                   move      p_numm        w_numm_alf
     c                   eval      w_lknr = %subst(lplknr:1:5) + '     '
      *
     c                   CALL      'LO771C'
     c                   parm                    w_numm_alf
     c                   parm                    w_memb
     c                   parm                    w_lknr
     c                   parm                    w_feil
      *
     c                   if        w_feil = *off
     c                   eval      a1stat = 'BESTILLING ER SENDT FARVERINGEN'
     c                   else
     c                   eval      a1stat = 'BESTILLING ER IKKE SENDT!'
     c                   endif
      *
     c                   exsr      overf_stat
     c                   endif
     c                   endif
      *
      * EFONELFO / HEIDENREICH
      *
     c                   if        lpform = 'EFONELFO'
     c                   if        lptype = 'FTP      '
     c                   CALL      'LO775C'
     c                   parm                    w_memb
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
     c                   if        w_feil = *off
     c                   eval      a1stat = 'BESTILLING ER SENDT HEIDENREICH'
     c                   else
     c                   eval      a1stat = 'BESTILLING ER IKKE SENDT!'
     c                   endif
      *
     c                   exsr      overf_stat
     c                   endif
     c                   endif
      *
      * LØVENSKIOLD
      *
     c                   if        lpform = 'LOVENSKIOL'
     c                   if        lptype = 'FTP      '
     c                   CALL      'LO780C'
     c                   parm                    w_memb
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
     c                   if        w_feil = *off
     c                   eval      a1stat = 'BESTILLING ER SENDT LØVENSKIOLD'
     c                   else
     c                   eval      a1stat = 'BESTILLING ER IKKE SENDT!'
     c                   endif
      *
     c                   exsr      overf_stat
     c                   endif
     c                   endif
      *
      * NORGROS
      *
     c                   if        lpform = 'NORGROS   '
5.53 c                   if        lptype = 'FTP      '
      *
 .   c                   CALL      'LO790C'
 .   c                   parm                    w_memb
 .   c                   parm                    p_numm
5.52 c                   parm                    p_suff
      *
5.53 c                   parm                    p_text
      *
 .   c                   CALL      'LO763C'
 .   c                   parm                    w_memb
 .   c                   parm                    w_feil
 .    *
 .   c                   if        w_feil = *off
 .   c                   eval      a1stat = 'BESTILLING ER SENDT NBS!'
 .   c                   else
 .   c                   eval      a1stat = 'BESTILLING ER IKKE SENDT'
 .   c                   endif
 .   c                   exsr      overf_stat
     c                   endif
     c                   endif
      *
      * INTERNT FRA BESTILLING TIL ORDRE
      *
5.21 c                   if        lpform = 'INTERN    '
5.21 c                   if        lptype = 'INTERN   '
      *
      * Rediger intert kundenummer
      *
     c                   eval      lplknr = %trim(lplknr)
     c                   eval      w_leng = %len(%trim(lplknr))
     c                   if        w_leng < 6
     c                   eval      lplknr = %subst(c_null:1:6-w_leng) +
     c                                      %subst(lplknr:1:w_leng)
     c                   endif
      *
5.21 c                   CALL      'NO124R'
5.21 c                   parm                    w_firm
5.21 c                   parm                    p_numm
5.21 c                   parm                    p_suff
5.21 c                   parm                    lplknr
5.21 c                   parm                    rlavde
5.21 c                   parm                    rllagr
5.21  *
5.21 c                   if        w_feil = *off
5.21 c                   eval      a1stat = 'INTERN ER BEHANDLET            '
5.21 c                   else
5.21 c                   eval      a1stat = 'INTERN ER IKKE BEHANDLET!'
5.21 c                   endif
5.21  *
5.21 c                   exsr      overf_stat
5.21 c                   endif
5.21 c                   endif
8.03  *
8.03  * NGN_EDI - sett loggkode 2
8.03  *
8.03 c                   if        %trim(lpform) = 'NGN_EDI' and
8.03 c                             %trim(lptype) = 'NGN_EDI' and
8.03 c                             %trim(lpmott) = 'NGN_EDI'
8.08 c                   exsr      logg_levdat
8.03 c                   exsr      overf_stat
8.04  *
8.04  * Overfører kopi av bestilling til distribusjonssenter hvis koden er satt på
8.04  *
8.04 c                   if        w_rdsk = 'C'
8.04 c                   CALL      'LO768C'
8.04 c                   parm                    w_memb
8.04 c                   parm                    p_numm
8.04 c                   parm                    p_suff
8.04 c                   endif
8.03 c                   endif
8.03  *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for feilhåndtering - eksternt feilbilde.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     feil_ut       begsr
      *
     c                   eval      w_mld1 = 'Distribusjonsinformasjon ' +
     c                                      'ikke funnet'
     c                   call      'AA005R'
     c                   parm                    w_mld1
6.20  *
6.20 c                   eval      p_stat = 'F'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bygging av file-member
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_memb      begsr
      *
     c                   time                    w_stmp
      *
     c                   move      w_stmp        w_stmp_alf
     c                   eval      w_memb = 'W' +
     c                                      %subst(w_stmp_alf:12:2) +
     c                                      %subst(w_stmp_alf:15:2) +
     c                                      %subst(w_stmp_alf:18:2) +
     c                                      %subst(w_stmp_alf:21:3)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kvittering av overført
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overf_stat    begsr
      *
     c                   eval      *in31 = *off
     c                   if        w_feil = *on
     c                   goto      overf_feil
     c                   endif
      *
      * Logger i loghistorikk
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_numm = p_numm
     c                   eval      d_suff = p_suff
     c                   eval      d_kode = '2'
     c                   eval      d_date = *loval
     c                   eval      d_time = *loval
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
     c                   eval      w_parm = d_urec
      *
5.51 c                   call      'LM900R'
5.51 c                   parm                    w_stat
5.51 c                   parm                    w_parm
5.01  *
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_suff
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lobsen = 1
     c                   time                    lobsda
     c                   eval      lobsav = l_user
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
     c                   eval      lldilu_ldor = lldilr_ldor
     c                   eval      lldilu_lage = lldilr_lage
     c     lldilu_key    chain     lldilu
     c                   if        %found
     c                   time                    lpsikl
     c                   eval      lpsibe = p_numm
     c                   update    lldilur
     c                   endif
8.09  *
8.09  * Hvis flåtestyring, sjekk om vi skal sende transportrequest. NB Gjelder ikke
8.09  * ikke NGN_EDI, hvor disse går rett til transportformidler.
8.09  *
8.09 c                   if        u_flat = *on and
8.09 c                             loornr <> 0
8.09  *
8.09 c                   if        %trim(lpform) <> 'NGN_EDI' and
8.09 c                             %trim(lptype) <> 'NGN_EDI' and
8.09 c                             %trim(lpmott) <> 'NGN_EDI'
8.09  *
8.09 c                   eval      w_reqs = *blanks
8.10 c                   eval      w_skaf = 1
8.11 c                   eval      w_logg = 1
8.09 c                   call      'AP058R '
8.09 c                   parm                    fofirm
8.09 c                   parm                    loornr
8.09 c                   parm                    loorsu
8.10 c                   parm                    w_skaf
8.11 c                   parm                    w_logg
8.09 c                   parm                    w_reqs
8.09  *
8.09 c                   endif
8.09  *
8.09 c                   endif
8.09  *
     c     overf_feil    tag
8.12 c                   if        w_feil = *on
     c                   exfmt     a1bld
8.12 c                   endif
      *
     c                   endsr
      *
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  * Subrutine for oppdatering av leveriungsdato baset på hodet
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08 c     logg_levdat   begsr
8.08  *
8.08 c                   eval      lohelr_numm = p_numm
8.08 c                   eval      lohelr_suff = p_suff
8.08 c     lohelr_key    chain     lohelr
8.08 c                   if        %found
8.08  *
8.08 c                   eval      lodtlu_numm = lonumm
8.08 c                   eval      lodtlu_suff = losuff
8.08 c                   eval      lodtlu_line = 0
8.08 c     lodtlu_key    setll     lodtlu
8.08 c     lodtlu_key2   reade     lodtlu
8.08 c                   dow       not %eof(lodtlu)
8.08  * Oppdater med mer er funnet, oppdater bestillingslinje med info
8.08 c                   if        ldldat = *loval and
8.08 c                             loldat <> *loval
8.08 c                   eval      ldldat = loldat
8.08 c                   time                    ldedat
8.08 c                   time                    ldetim
8.08 c                   eval      ldeusr = l_user
8.08 c                   update    lodtlur
8.08 c                   else
8.08 c                   unlock    lodtlu
8.08 c                   endif
8.08  *
8.08 c     lodtlu_key2   reade     lodtlu
8.08 c                   enddo
8.08  *
8.08 c                   endif
8.08  *
8.08 c                   endsr
      *
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      // Subrutine for å sjekke bestilling direkte til distribusjonslager
      //  (Denne er passivisert og erstattes med NGN_EDI, kan aktiviseres med w_rdsk = 'B')
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

         begsr sjk_rds_bes;

          w_lagd = 0;

      // Sjekk om leverandør er distribusjonslager

           exec SQL
8.07        select distinct rkeela into :w_ekst
              from ra37st
              where rkefir = :w_firm
                and rkelev = :loldor;

          if sqlcod = 0;
8.05  //   w_rdsk = 'B';
8.05       w_rdsk = ' ';
8.06       w_int = *on;
          endif;

         endsr;

      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      // Subrutine for å sjekke om kopi skal sendes til distribusjonslager
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

         begsr sjk_rds_kop;

          w_lagd = 0;
          w_lmat = 0;
8.07      w_ekst = ' ';

      // Finn lager og leveringsmåte på ordre

          if loornr > 0;
           exec sql
            select folage, folmat
             into :w_lagd, :w_lmat
            from fohepf
             where fofirm = :w_firm and fonumm = :loornr and fosuff = :loorsu;
          endif;


      // Finn finn kobling til distribusjonslager

          if w_lmat > 0 and
             w_lagd > 0;
           exec SQL
8.07        select rkrela into :w_ekst
              from ra36st
              where rkrfir = :w_firm
                and rkrfsk = :w_lmat
                and rkrsla = :w_lagd;
          endif;

          if sqlcod = 0 and
8.07         w_ekst <> ' ';
           w_rdsk = 'C';
          endif;


         endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør distribusjon
      *
6.24  *
6.24  * Key for file : Firmastyrt validering
6.24  *
6.24 c     avall1_key    klist
6.24 c                   kfld                    w_firm
6.24 c                   kfld                    avall1_apgm
6.24  *
     c     lldilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      * Key for oppdatering av leverandør distribusjon
      *
     c     lldilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldilu_ldor
     c                   kfld                    lldilu_lage
      *
      * Key for oppslag på ordre/suffix
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for oppdatering på ordre/suffix
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
8.08  *
8.08  * Key for oppdatering på ordre/suffix
8.08  *
8.08 c     lodtlu_key    klist
8.08 c                   kfld                    w_firm
8.08 c                   kfld                    lodtlu_numm
8.08 c                   kfld                    lodtlu_suff
8.08 c                   kfld                    lodtlu_line
8.08  *
8.08  * Key for oppdatering på ordre/suffix
8.08  *
8.08 c     lodtlu_key2   klist
8.08 c                   kfld                    w_firm
8.08 c                   kfld                    lodtlu_numm
8.08 c                   kfld                    lodtlu_suff
8.09  *
8.09  * Key for oppdatering på ordre/suffix
8.09  *
8.09 c     fohel1_key    klist
8.09 c                   kfld                    w_firm
8.09 c                   kfld                    fohel1_numm
8.09 c                   kfld                    fohel1_suff
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
6.20 c                   parm                    p_stat
      *
     c                   eval      w_firm = l_firm
6.20 c                   eval      p_stat = *blank
      *
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'BEST_RDS':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_rds = *on;
8.01   endif;
8.09  *
8.09  * Endring 8.08 - Koblet til ekstern flåtestyring
8.09  *
8.09   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.09                    co402_verdi1:co402_verdi2);
8.09   if co402_verdi1 = '1';
8.09     u_flat = *on;
8.09   endif;
     c                   endsr
