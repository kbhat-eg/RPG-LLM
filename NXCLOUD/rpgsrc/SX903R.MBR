     h option(*nodebugio) datedit(*dmy-) decedit('0.')
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SX903R
      * Beskrivelse . . : Legger ut varer til Cobuilder
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150318 bsa  Nytt program
7.01  *       300518 bsa  Utvidet med mail info. Sendes til fast
7.01  *                   mailadresse. (NB De er hardkodet)
7.02  *       200618 bsa  Slår opp og henter mailadresser, samt logger
7.02  *                   sist kjørt dato. Ønsker å redusere mengden
7.02  *                   varer som overføres.
7.03  *       160120 bsa  Div justeringer.
7.04  *       100921 bof  Flyttet henting av afpspf, slik at info var tilgj.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
7.03 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
7.02 fscoblu    uf   e           k disk    rename(scobpfr:scoblur)
7.02 fscobl1    if   e           k disk    rename(scobpfr:scobl1r)
7.02 fscovlu    uf a e           k disk    rename(scovpfr:scovlur)
7.02 fscovl1    if   e           k disk    rename(scovpfr:scovl1r)
      *
      * Definering av array
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_poff                584    584
     d l_utqm                585    594
     d l_bach                595    635
     d l_arch                636    636  0
     d l_skje                637    637  0
     d l_exlp                639    639  0
     d l_mail                640    640  0
     d l_prfi                750    750  0
     d l_outq                810    819
     d l_copy                838    843  0
     d l_ruti                800    809
     d l_wsid                901    910
     d l_firm                944    946  0
     d l_filg                931    933
     d l_user                911    920
     d l_avde                947    950  0

      *
      * Definering av datastrukturer
      *
      *
      * Definering av parametre
      *
     d p_dato          s               d   datfmt(*iso)
     d p_inkl          s              1  0
      *
      * Definering av variabler i nøkler
      *
     d jvpkl3_gtin     s                   like(jwgtin)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d aforl1_ruti     s                   like(afruti)
7.02 d scovlu_nobb     s                   like(svnobb)
7.02 d scovl1_nobb     s                   like(svnobb)
7.03 d afpslr_ufil     s                   like(l_filg)
7.03 d afpslr_uide     s                   like(afuide)
      *
      * Definering av variable
      *
     d b_excl          s              1    inz(*off)
     d b_eof           s              1    inz(*off)
7.02 d b_nobb          s              1    inz(*off)
      *
     d x               s              2  0
     d x_eann          s             14  0
      *
     d w_firm          s                   like(aaffir)
     d w_lnavn         s             50
     d w_tek1          s            100
     d w_tek2          s            100
     d w_ant           s              9  0
     d w_dato          s                   like(aaodat)
7.02 d w_dats          s                   like(aaodat)
     d w_aarr          s              4  0
     d w_gtin          s             14  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_ruti          s             10
     d w_numm          s              8  0
     d w_eann          s             14  0
     d w_1fnav         s             50
     d w_1fad1         s             50
     d w_1fad2         s             50
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_mail          s             50
     d w_weba          s             50
     d w_avde          s              4  0
      *
     d p_stat          s              1
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_numm          s              8  0
     d p_suff          s              2  0
     d p_selg          s              4  0
     d p_serv          s             35
     d p_kule          s              1
     d p_navn          s             30
     d p_emal          s             50
     d p_ema2          s             50
     d p_ema3          s             50
     d p_kopi          s             50
     d p_emne          s             50
     d p_txt1          s             50
     d p_txt2          s             50
     d p_txt3          s             50
     d p_txt4          s             50
     d p_pers          s             50
     d p_inif          s             40
     d p_in03          s              1
     d p_btyp          s            100
     d p_ok            s              1
     d p_lr            s              1
     d p_str_in        s            512
     d p_str_out       s            512
     d p_filg          s             10
     d p_xmlf          s            256
     d w_text          s             50
     d w_dspl          s            100
     d w_jkey          s             41
     d w_jnav          s             15
     d w_jtxt          s             35
     d w_jstat         s              2  0
     d w_jtext         s            200
     d w_pdf           s              1  0
     d w_pdf_ds        s            512
     d w_rtyp          s             50
     d w_dato2         s             12  0
     d w_ddmy          s              6  0
     d w_tids          s              6  0
     d w_txt1          s             75
     d w_date          s               d   datfmt(*iso)
     d w_pemne         s             75
     d w_ptxt1         s             75
     d w_ptxt2         s             75
     d w_ptxt3         s             75
     d w_ptxt4         s             75
     d w_emal          s             50
     d w_ema1          s             50
     d w_ema2          s             50
     d w_ema3          s             50
     d w_besk          s             75
     d w_lnav          s             75
     d w_ppers         s             75
     d w_mtxt          s            300
     d h_valu          s             15
     d d_valu          s            100
     d d_type          s             10
     d w_mime          s             24
      *
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a   Varying
     d                                     Inz('/home/asp/print/adb')
     d jasper_mal      s            256a
     d jasper_logo     s            256a   varying
     d tmpdate         s               D
     d tmptime         s               T
     d crlf            c                   const(X'0d25')
      *
     d d_pdf_ds        ds
     d  d_xmlm                       75
     d  d_jasm                       75
     d  d_logo                       75
     d  d_path                      100
     d  d_emal                       50
     d  d_fifo                       75
     d  d_fkop                        1
     d  d_dtaq                        1
     d  d_sign                        1
      /copy prototypeb
      /copy usec
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO
     C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling
      *
     c                   if        b_excl = *on
     c                   exsr      behandling
      *
     c                   if        w_ant > 0
     c                   exsr      xml_end
     c                   if        1 = 2
     c                   exsr      pdf_preview
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
7.02 c**                 eval      w_aarr = 2015
     c                   eval      w_eann = 1
      *
     c/exec sql
     c+ declare varer cursor for
     c+ select distinct sfeann
     c+ from   sstapf
     c+ where  sffirm =  :w_firm and
7.02 c+        sfbida >= :w_dats and
     c+        sfeann >= :w_eann
     c+ for read only
     c/end-exec
     c/exec sql
     c+ open varer
     c/end-exec
      *
     c                   dow       b_EOF = *off
      *
     c/exec sql
     c+ fetch varer
     c+ into  :x_eann
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c/exec sql
     c+  close varer
     c/end-exec
     c                   eval      b_eof = *on
     c                   leavesr
     c                   endif
      *
     c                   exsr      detalj
      * For test
     c**                 if        w_ant > 10
     c**                 goto      end_sr
     c**                 endif
      *
     c                   enddo
      *
     c     end_sr        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje (brudd på hovedgruppe/undergruppe)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      * Her finner vi vare via JVPK basert på GTIN nummer
     c                   eval      jvpkl3_gtin = x_eann
     c     jvpkl3_key    chain     jvpkl3
     c                   if        %found
     c                   eval      jvarl1_vare = jwvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
7.02  * Er denne varen lagt ut tidligere ?
7.02 c                   eval      b_nobb = *off
7.02 c                   eval      scovl1_nobb = jvnobb
7.02 c     scovl1_key    chain     scovl1
7.02 c                   if        not %found
7.02 c                   eval      b_nobb = *on
7.02 c                   endif
7.02  * Sjekk om varen skal være med basert på parameter
7.02 c                   if        p_inkl = 1 or
7.02 c                             b_nobb = *on
     c                   exsr      xml_detal
7.02 c                   exsr      logg_nobb
     c                   eval      w_Ant = w_ant + 1
7.02 c                   endif
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML/jasper integrasjon - Miljø variabler         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_envm      begsr
      *
     c                   eval      w_rtyp = 'XsltCommand'
     c                   eval      w_mime = 'application/vnd.ms-excel'
     c                   eval      d_jasm = *blank
      *
      /Free
           // Finne nytt batch nummer
               UpdHTMLVar('BatchNo':               w_jkey);
               UpdHTMLVar('Batchtype':            'EXCEL');
               UpdHTMLVar('Username':              l_user);
               WrtSection('Topp');
           // Finn report command variabler
               UpdHTMLVar('User':                l_user);
               UpdHTMLVar('PW':                     ' ');
               UpdHTMLVar('IPAdr':                  ' ');
               UpdHTMLVar('Schema':        'ADB'+l_filg);
               UpdHTMLVar('Companyno':    %char(l_firm));
               WrtSection('Credential');
           // Finn report command variabler
               UpdHTMLVar('Reportcommandtype':   w_rtyp);
               UpdHTMLVar('Jaspertemplate':      d_jasm);
               UpdHTMLVar('Logo':                   ' ');
               UpdHTMLVar('Keepspool':           'true');
               UpdHTMLVar('Xsltschemapath':         ' ');
               UpdHTMLVar('Mimetype':            w_mime);
               WrtSection('ReportCommand');
      /End-free
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML integrasjon - Heading                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_head      begsr
      *
     c                   eval      aforl1_ruti = w_ruti
     c     aforl1_key    chain     aforl1r
      *
      * Hvis mail skal skrives, må vi hente mail informasjon
      *
     c                   if        l_mail = 1
     c                   eval      p_kule = ' '
     c                   eval      p_kund = *zeros
     c                   eval      p_kpro = *zeros
     c                   eval      p_numm = *zeros
     c                   eval      p_suff = *zeros
     c                   eval      p_selg = *zeros
     c                   eval      p_in03 = *off
     c                   eval      p_emne = *blank
     c                   eval      p_txt1 = *blank
     c                   eval      p_txt2 = *blank
     c                   eval      p_txt3 = *blank
     c                   eval      p_txt4 = *blank
     c                   eval      p_pers = *blank
      * Henter ut mailserver
     c                   eval      p_serv = *blank
     c                   eval      p_stat = *blank
     c                   call      'AM705C'
     c                   parm                    p_serv
     c                   parm                    p_stat
7.02  * Hent informasjon fra loggregister
7.02 c     scoblu_key    chain     scoblu
7.02 c                   if        %found
7.02 c                   eval      sckdat = p_dato
7.02 c                   time                    scudat
7.02 c                   time                    scutid
7.02 c                   time                    scedat
7.02 c                   time                    scetim
7.02 c                   eval      sceusr = l_user
7.02 c                   update    scoblur
      *
7.02 c                   eval      p_emal = %trim(scmai1)
7.02 c                   eval      p_ema2 = %trim(scmai2)
7.02 c                   eval      p_ema3 = %trim(scmai3)
      *
7.02 c                   eval      p_emne = %trim(scsubj)
7.02 c                   eval      p_txt1 = %trim(sctxt1)
7.02 c                   eval      p_txt2 = %trim(sctxt2)
7.02 c                   eval      p_txt3 = %trim(sctxt3)
7.02 c                   eval      p_txt4 = %trim(scsend)
7.03 c                   eval      p_pers = *blanks
      *
      * Scann av strenger
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_emne        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_pemne
      * Scann av strenger
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_ptxt1
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt2        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_ptxt2
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt3        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_ptxt3
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_txt4        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_ptxt4
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     p_pers        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_ppers
      *
     c                   eval      w_mtxt = %trim(w_ptxt1) + crlf +
     c                                      %trim(w_ptxt2) + crlf +
     c                                      %trim(w_ptxt3) + crlf +
     c                                      %trim(w_ptxt4) + crlf +
     c                                      %trim(w_ppers)
      *
7.03 c*                  if        p_kopi = *blanks
7.03 c*                  eval      p_kopi = 'noreply@mestergruppen.no'
7.03 c*                  endif
      *
7.01 c**                 if        p_in03 = *off
      *
      /Free
           // Skriv mailinformasjon
               WrtSection('MailCommandStart');
          //   UpdHTMLVar('Receivers':           p_emal);
               if %Trim(p_emal)<> *blanks;
               UpdHTMLVar('Receivers':           p_emal);
               WrtSection('MailReceiver');
               endif;
               if %Trim(p_ema2)<> *blanks;
               UpdHTMLVar('Receivers':           p_ema2);
               WrtSection('MailReceiver');
               endif;
               if %Trim(p_ema3)<> *blanks;
               UpdHTMLVar('Receivers':           p_ema3);
               WrtSection('MailReceiver');
               endif;
               UpdHTMLVar('CC':                     ' ');
               UpdHTMLVar('BCC':                 p_kopi);
               UpdHTMLVar('Sender':              p_kopi);
               UpdHTMLVar('Subject':             w_pemne);
               UpdHTMLVar('Message':             w_mtxt);
               UpdHTMLVar('SMTPServer':          p_serv);
               UPDHTMLVar('SMTPUser':               ' ');
               UPDHTMLVar('SMTPPw':                 ' ');
               WrtSection('MailCommandEnd');
      /End-free
7.01 c**                 endif
7.02 c                   endif
      *
     c                   endif
      *
      * Legg på FTP informasjon
      *
7.01 c                   if        1=2
      *
      /Free
           // Skriv FTP informasjon
              UpdHTMLVar('Host':             '172.22.30.10');
              UpdHTMLVar('User':               'g0xegbaasa');
              UpdHTMLVar('Password':             'mars2010');
              UpdHTMLVar('Folder': '/home/adbg0x/cobuilder');

              UpdHTMLVar('Filename':        'Cobuilder.csv');
              UpdHTMLVar('Usetempsuffix':           'false');
              UpdHTMLVar('Sendonlyattachements':     'true');

               WrtSection('FTPCommand');
      /End-free
      *
7.01 c                   endif
      *
      * Scann av strenger
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     aftxt1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_txt1
      *
      * Vi må oversette aktuelle tekster til "xml" tegn - Firma
     c                   eval      w_1fnav = *blank
     c                   eval      w_1fad1 = *blank
     c                   eval      w_1fad2 = *blank
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     w_fnav        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_1fnav
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     w_fad1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_1fad1
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     w_fad2        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_1fad2
      *
      /Free

           // Skriv firmavariabler
               UpdHTMLVar('Reporttext':            w_txt1);
               UpdHTMLVar('Subheader':                ' ');
               UpdHTMLVar('CompanyName':          w_1fnav);
               UpdHTMLVar('Companyadress1':       w_1fad1);
               UpdHTMLVar('Companyadress2':       w_1fad2);
               UpdHTMLVar('Companypostalcode':        ' ');
               UpdHTMLVar('Companypostoffice':     w_fste);
               UpdHTMLVar('Companyphone':          w_ftlf);
               UpdHTMLVar('Companyfax':            w_ffax);
               UpdHTMLVar('Companyorgno':   %char(aafftn));
               UpdHTMLVar('Companygiro':    %char(aafbgi));
               UpdHTMLVar('Companyemail':          aamail);
               UpdHTMLVar('Companywebpage': %trim(aaweba));

               test(de) *dmy w_ddmy;
               if %error;
               tmpdate = *loval;
               else;
               tmpdate = %date(w_ddmy : *dmy);
               endif;
               UpdHTMLVar('Creationdate':   %char(tmpdate : *iso));

               test(te) *hms w_tids;
               if %error;
               tmptime = *loval;
               else;
               tmptime = %time(w_tids : *hms);
               endif;
               UpdHTMLVar('Creationtime': %char(tmptime : *hms));
               WrtSection('DocumentInformation');

               WrtSection('FreedefHeader');

      /End-free
      *
     c                   eval      h_valu='Org_nr'
     c                   exsr      xml_head_val
     c                   eval      h_valu='Org_name'
     c                   exsr      xml_head_val
     c                   eval      h_valu='gtin'
     c                   exsr      xml_head_val
     c                   eval      h_valu='Nobbnr'
     c                   exsr      xml_head_val
     c                   eval      h_valu='spn'
     c                   exsr      xml_head_val
     c                   eval      h_valu='Ppn'
     c                   exsr      xml_head_val
     c                   eval      h_valu='Nrfnr'
     c                   exsr      xml_head_val
     c                   eval      h_valu='Elnr'
     c                   exsr      xml_head_val
     c                   eval      h_valu='ptext_1'
     c                   exsr      xml_head_val
     c                   eval      h_valu='ptext_2'
     c                   exsr      xml_head_val
      *
      /Free

           // Avslutt headervalues
               WrtSection('HeaderLineEnd');

      /End-free
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML integrasjon - Heading                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_head_val  begsr
      *
      /Free

           // Skriv headervalue
               UpdHTMLVar('Headervalue':  %trim(h_valu));
               WrtSection('HeaderLineValue');

      /End-free
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML integrasjon - detaljlinjer                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_detal     begsr
      *
      /Free

           // Skriv detaljer
               WrtSection('DetailLineStart');

               WrtSection('FreedefLine');

      /End-free
      *
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1r
     c                   if        not %found
     c                   eval      jlnavn = *blanks
     c                   endif
      * Leverandørens navn
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     jlnavn        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_lnavn
      * Varetekst 1 fra statestikk
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     jvtek1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_tek1
      * Varetekst 2 fra statestikk
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     jvtek2        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_tek2
      *
      * Så skriver vi de forskjellige linjene
      *
      * Orgnr leverandør
     c                   eval      d_type='Numeric'
     c                   eval      d_valu=%char(jlorgn)
     c                   exsr      xml_line_val
      * Leverandør  navn
     c                   eval      d_type='Char'
     c                   eval      d_valu=%trim(w_lnavn)
     c                   exsr      xml_line_val
      * GTIN nummer
     c                   eval      d_type='Numeric'
     c                   eval      d_valu=%char(x_eann)
     c                   exsr      xml_line_val
      * NOBB nummer
     c                   eval      d_type='Numeric'
     c                   eval      d_valu=%char(jvnobb)
     c                   exsr      xml_line_val
      * Leverandørens varenummer
     c                   eval      d_type='Char'
     c                   eval      d_valu=%trim(jvlvar)
     c                   exsr      xml_line_val
      * Produsentens varenummer
     c                   eval      d_type='Char'
     c                   eval      d_valu=%trim(jvpvar)
     c                   exsr      xml_line_val
      * NRF nummer
     c                   eval      d_type='Numeric'
     c                   eval      d_valu='0'
     c                   exsr      xml_line_val
      * Elnr
     c                   eval      d_type='Numeric'
     c                   eval      d_valu='0'
     c                   exsr      xml_line_val
      * Varetekst 1
     c                   eval      d_type='Char'
     c                   eval      d_valu=%trim(w_tek1)
     c                   exsr      xml_line_val
      * Varetekst 2
     c                   eval      d_type='Char'
     c                   eval      d_valu=%trim(w_tek2)
     c                   exsr      xml_line_val
      /Free
           // Skriv avslutning

               WrtSection('DetailLineEnd');

      /End-free
      *
     c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML integrasjon - Linjer                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_line_val  begsr
      *
      /Free

           // Skriv linevalue
               UpdHTMLVar('type':         %trim(d_type));
               UpdHTMLVar('Linevalue':    %trim(d_valu));
               WrtSection('DetailLineValue');

      /End-free
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML integrasjon - Slutt                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_end       begsr
      *
      /Free
               WrtSection('EndExcelLines');
      /End-free
      *
      * Hvis denne ikke skal arkiveres, lages ikke archive tagg.
      *
     c                   if        l_arch = 1
      * Lag tag for vising i arkivklienten
     c                   eval      w_dspl = *blank

      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     w_dspl        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_dspl
      /Free
           // Skriv metattags

               UpdHTMLVar('Displaytag':     %trim(w_dspl));
               WrtSection('ArchiveCommandStart');

               UpdHTMLVar('Metavalue':             l_user);
               UpdHTMLVar('Metakey':             'BRUKER');
               WrtSection('MetaTag');

               UpdHTMLVar('Metavalue': %char(w_date:*iso));
               UpdHTMLVar('Metakey':               'DATO');
               WrtSection('MetaTag');

               UpdHTMLVar('Metavalue':             w_txt1);
               UpdHTMLVar('Metakey':         'RUTINETEKST');
               WrtSection('MetaTag');

               WrtSection('ArchiveCommandEnd');
      /End-free
      *
     c                   endif
      *
      /Free
               WrtSection('Footer');
      /End-free
      *
     c                   eval      XML_Output = XML_path + l_filg +'/'+
     c                             'CobXls_'+ %trim(w_jkey) + '.xml'
      *
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
      * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
     c                   if        d_dtaq = '1'
     c                   eval      p_xmlf = xml_output
     c                   eval      p_filg = 'ADB'+l_filg
     c                   call      'AP629C'
     c                   parm                    p_filg
     c                   parm                    p_xmlf
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     pdf_preview   begsr
      *
      * Vi kaller program for launch av PDF i browser
     c                   call      'AP621R'
     c                   parm                    w_jkey
      *
     c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.02  * Subrutiner for å legge vare i loggregister                      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.02 c     logg_nobb     begsr
7.02  * Vi legger ut vare hvis den ikke finnes fra før
7.02 c                   eval      scovlu_nobb = jvnobb
7.02 c     scovlu_key    chain     scovlu
7.02 c                   if        not %found
7.02 c                   eval      svfirm = w_firm
7.02 c                   eval      svnobb = jvnobb
7.02 c                   eval      svtek1 = jvtek1
7.02  *
7.02 c                   time                    svodat
7.02 c                   time                    svotim
7.02 c                   eval      svousr = l_user
7.02 c                   time                    svedat
7.02 c                   time                    svetim
7.02 c                   eval      sveusr = l_user
7.02 c                   write     scovlur
7.02 c                   else
7.02 c                   unlock    scovlu
7.02 c                   endif
7.02  *
7.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_dato
     c                   parm                    p_inkl
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
7.02  *
7.02  * Key for oppslag på loggregister
7.02  *
7.02 c     scoblu_key    klist
7.02 c                   kfld                    w_firm
7.02  *
7.02  * Key for oppslag på loggregister
7.02  *
7.02 c     scobl1_key    klist
7.02 c                   kfld                    w_firm
7.02  *
7.02  * Key for oppslag på loggregister
7.02  *
7.02 c     scovlu_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    scovlu_nobb
7.02  *
7.02  * Key for oppslag på loggregister
7.02  *
7.02 c     scovl1_key    klist
7.02 c                   kfld                    w_firm
7.02 c                   kfld                    scovl1_nobb
      *
      * Key for oppslag på rutineregister
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for oppslag på GTIN nummer
      *
     c     jvpkl3_key    klist
     c                   kfld                    jvpkl3_gtin
      *
      * Key for oppslag på vareregister
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandørregister
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
7.03  *
7.03  * Key for properties
7.03  *
7.03 c     afpslr_key    klist
7.03 c                   kfld                    afpslr_ufil
7.03 c                   kfld                    afpslr_uide
     c
      *
      * Sjekk om vi skal bruke XML generering av dokumenter
      * NB KUN hvis EXCEL rapport.
      *
     c                   time                    w_dato
7.02 c                   eval      w_dats = p_dato
     c                   eval      w_firm = l_firm
7.01 c                   eval      l_mail = 1
     c                   eval      l_arch = 0
     c                   eval      b_excl = *off
     c                   eval      w_pdf = 0
     c                   eval      w_pdf_ds = *blanks
     c                   eval      w_ruti = 'SX903'
7.04  * flytter afpspf hit
7.04  *
7.04  **
7.04  * henter mailadresse fra
7.04 c                   eval      afudss = *blank
7.04 c                   eval      afpslr_ufil = l_filg
7.04 c                   eval      afpslr_uide = 'NOMAIL   '
7.04 c     afpslr_key    chain     afpslr
7.04 c                   if        not %found
7.04 c                   eval      p_kopi = *blanks
7.04 c                   else
7.04 c                   eval      p_kopi = %trim(afudss)
7.04 c                   endif
7.04  *
      * Vi starter med logging av jobb, og får tilbake unik jobbid.
      * Først få jobbnummer
      *
     c                   eval      w_kode = '0'
     c                   eval      w_firm = l_firm
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
     c                   eval      w_jnav = 'PDF' + %char(w_numm)
     c                   eval      w_jtxt = 'Lage EXCEL liste'
     c                   call      'AB700R'
     c                   parm                    w_jnav
     c                   parm                    w_jkey
     c                   parm                    w_jtxt
      *
     c                   call      'AP609R'
     c                   parm                    w_ruti
     c                   parm                    w_pdf
     c                   parm                    w_pdf_ds
     c                   eval      d_pdf_ds = w_pdf_ds
      *
      * PROA er aktivert
      *
     c                   if        %trim(d_xmlm) <> *blanks
     c                   eval      b_excl = *on
      *
      * Hent inn xml templaten
      *
     c                   callp     GetHTMLIFS(d_xmlm :'<AS400>')
     c                   callp     ClrHtmlBuffer
      *
      * Finn hvor xml'en skal legges
      *
     c                   eval      XML_path = *blanks
     c                   eval      XML_path = %trim(d_path)
      *
     c                   time                    w_date
     c                   time                    w_dato2
     c                   movel     w_dato2       w_tids
     c                   move      w_dato2       w_ddmy
      *
      * Vi logger at utskriftprogrammet har startet
      *
     c                   eval      w_jstat = 10
     c                   eval      w_jtext = 'Excel av cobuildergrunnlag +
     c                              har startet'
     c                   call      'AB705R'
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
      *
      * Splitter datastruktur i parametre og variable
      *
      *  Hent inn firma/avd. opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   eval      w_prfi = 1
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = l_avde
      *
     c                   call      'FÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_mail
     c                   parm                    w_weba
      *
      *
      * Vi skriver xml for miljø formater
     c                   exsr      xml_envm
     c                   exsr      xml_head
      *
     c                   else
      * Vi logger at noe er galt med parametrene
     c                   eval      w_jstat = 20
     c                   eval      w_jtext = 'Finner ikke path til xml !!! +
     c                             Sjekk rutineregister !'
     c                   call      'AB705R'
     c                   parm                    l_bach
     c                   parm                    w_jstat
     c                   parm                    w_jtext
      *
     c                   endif
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
7.02  *
7.02  * Hent informasjon fra loggregister
7.02  *
7.02 c     scobl1_key    chain     scobl1
7.02 c                   if        %found and
7.02 c                             w_dats = *loval
7.02 c                   eval      w_dats = scudat
7.02 c                   eval      p_dato = scudat
7.02 c                   endif
7.04  **
7.04  ** henter mailadresse fra
7.04 c*                   eval      afudss = *blank
7.04 c*                   eval      afpslr_ufil = l_filg
7.04 c*                   eval      afpslr_uide = 'NOMAIL   '
7.04 c*     afpslr_key    chain     afpslr
7.04 c*                   if        not %found
7.04 c*                   eval      p_kopi = *blanks
7.04 c*                   else
7.04 c*                   eval      p_kopi = %trim(afudss)
7.04 c*                   endif
      *
     c                   endsr
