      /title  ASSHOP Henter inn fra andre systemer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BOHNTR
      * Beskrivelse . . : Henter inn fra andre systemer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       031100 HKO  Lagt inn henting av ekspedisjon fra håndterminal
      * V5.30 131004 HFL  Bare comp.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbohntd    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_retu          s                   like(w_retu)
     d p_firm          s              3  0
     d p_aarr          s                   like(w_aarr)
     d p_wsid          s                   like(w_wsid)
      *
      * Definering av variable
      *
     d w_retu          s              1
     d w_wsid          s             10
     d w_aarr          s              4  0
     d w_numm          s              6  0
     d w_suff          s              2  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1 Behandler inntasting av valg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
      * Send Alt
      *
     c     a1tagb        tag
     c                   exfmt     a1win
      *
      * Behandling av standard funksjonstaster
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   eval      p_retu = '9'
     c                   goto      avslutt
     c                   endif
      *
      * Hent fra ordre-register
      *
     c                   if        a1valg = 1
     c                   exsr      hent_ordre
     c                   goto      avslutt
     c                   endif
      *
      * Hent fra butikk-bong-register
      *
     c                   if        a1valg = 2
     c                   exsr      hent_bong
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      a1tagb
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_ordre    begsr
      *
     c                   eval      w_retu = *blank
      *
      * Spørring
      *
     c                   call      'BOHHOR'
     c                   parm                    w_retu
     c                   parm                    p_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
      *
      * Henting
      *
     c                   if        w_retu <> '9'
     c                   call      'BOHNOR'
     c                   parm                    w_retu
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av butikk-bong
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_bong     begsr
      *
     c                   eval      w_retu = *blank
      *
      * Spørring
      *
     c                   call      'BOHHBR'
     c                   parm                    w_retu
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    w_aarr
     c                   parm                    w_wsid
     c                   parm                    w_numm
      *
      * Henting
      *
     c                   if        w_retu = *blank
     c                   call      'BOHNBR'
     c                   parm                    w_retu
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    w_aarr
     c                   parm                    w_wsid
     c                   parm                    w_numm
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_retu
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
      *
     c                   endsr
