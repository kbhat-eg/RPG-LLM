     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : FP270R
      * Beskrivelse . . : Vedlikehold av parametre. Utskrift av kollietikett
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210319 bsa  Nytt program
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     fqp270d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtlr_numm     s                   like(fonumm)
     d fodtlr_suff     s                   like(fosuff)
     d fodtlr_line     s                   like(foline)
     d fodtl1_numm     s                   like(fonumm)
     d fodtl1_suff     s                   like(fosuff)
     d fodtl1_line     s                   like(foline)
     d fodtlu_numm     s                   like(fonumm)
     d fodtlu_suff     s                   like(fosuff)
     d fodtlu_line     s                   like(foline)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Linje med kolliinformasjon
      *
     d d_line          ds
     d  d_ktyp                        8
     d  d_fil0                        1
     d  d_kana                        3
     d  d_kann                        3  0 overlay(d_kana)
     d  d_ktxt                       24
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_line          s                   like(fdline)
     d w_anta          s              2  0
     d w_type          s              8
     d w_text          s             24
     d w_numm          s                   like(fonumm)
     d w_lpro          s              1
      *
      * Definering av variable
      *
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d s_numm          s                   like(fonumm)
     d s_suff          s                   like(fosuff)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1BLD Behandle bildet for valg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1taga        tag
      *
      *  Hent opplysninger fra synkroniserings tabell
      *
     c                   eval      a1numm = *zeros
     c                   eval      a1suff = *zeros
     c                   eval      a1kund = *zeros
     c                   eval      a1navn = *blank
     c                   eval      a1adr1 = *blank
     c                   eval      a1adr2 = *blank
     c                   eval      a1sted = *blank
     c                   eval      a1lnav = *blank
     c                   eval      a1lad1 = *blank
     c                   eval      a1lad2 = *blank
     c                   eval      a1lstd = *blank
      *
     c     fohel1_key    chain     fohel1
     c                   if        %found
     c                   eval      a1numm = fonumm
     c                   eval      a1suff = fosuff
     c                   eval      a1kund = fokund
     c                   eval      a1lnav = fonavn
     c                   eval      a1lad1 = foadr1
     c                   eval      a1lad2 = foadr2
     c                   eval      a1lstd = fosted
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      a1navn = rknavn
     c                   eval      a1adr1 = rkgate
     c                   eval      a1adr2 = rkadr2
     c                   eval      a1sted = %char(rkponr) + ' ' + rksted
     c                   endif
      * Finn eventuelle kolliinformasjon som finnes på linjene allerede
     c                   eval      fodtlr_numm = fonumm
     c                   eval      fodtlr_suff = fosuff
     c                   eval      fodtlr_line = 9000
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
     c                   dow       not %eof
      *
     c                   if        fdline = 9000
     c                   eval      d_line = fdtek1
     c                   eval      d_kann = %int(%trim(d_kana))
     c                   eval      a1atre = d_kann
     c                   eval      a1ttxt = %trim(d_ktxt) + %trim(fdtek2)
     c                   elseif    fdline = 9001
     c                   eval      d_line = fdtek1
     c                   eval      d_kann = %int(%trim(d_kana))
     c                   eval      a1apal = d_kann
     c                   eval      a1ptxt = %trim(d_ktxt) + %trim(fdtek2)
     c                   elseif    fdline = 9002
     c                   eval      d_line = fdtek1
     c                   eval      d_kann = %int(%trim(d_kana))
     c                   eval      a1aand = d_kann
     c                   eval      a1atxt = %trim(d_ktxt) + %trim(fdtek2)
     c                   endif
      *
     c     fodtlr_key2   reade     fodtlr
     c                   enddo
     c                   endif
      *
      *  Vis bilde
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc = *on
     c                   goto      end_pgm
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd
      *
      *  Hent inn ordrenummer
      *
     c*                  call      'FO5xxR'
     c*                  parm                    w_firm
     c*                  parm                    w_numm
     c*                  parm                    w_suff
     c*                  goto      a1tagb
     c*                  endif
      *
     c                   endif
      *
      * Start ordrebehandling - brukes for å søke etter riktig ordrenummer
      *
     c                   if        *inkj
      *
      *  Hent inn ordrenummer
      *
     c                   eval      w_numm = 0
     c                   eval      w_lpro = *blanks
      *
     c                   call      'FO500R'
     c                   parm                    w_numm
     c                   parm                    w_lpro
     c                   goto      a1tagb
      *
     c                   endif
      *
      *  Test på gyldige valg
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
      *
     c                   if        a1numm = 0 and
     c                             s_numm <> 0
     c                   eval      fohel1_numm = 0
     c                   eval      fohel1_suff = 0
     c                   eval      s_numm = 0
     c                   eval      s_suff = 0
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1numm <> 0
     c                   if        a1numm <> s_numm or
     c                             a1suff <> s_suff
     c                   eval      fohel1_numm = a1numm
     c                   eval      fohel1_suff = a1suff
     c                   eval      s_numm = a1numm
     c                   eval      s_suff = a1suff
     c                   goto      a1taga
     c                   endif
     c                   endif
      *  Sjekk om det er lagt inn antall og eller tekst på
     c                   if        %trim(a1ttxt) <> *blank and
     c                             a1atre = 0
     c                   eval      *in31 = *on
     c                   goto      a1tagb
     c                   endif
      *
     c                   if        %trim(a1ptxt) <> *blank and
     c                             a1apal = 0
     c                   eval      *in32 = *on
     c                   goto      a1tagb
     c                   endif
      *
     c                   if        %trim(a1atxt) <> *blank and
     c                             a1aand = 0
     c                   eval      *in33 = *on
     c                   goto      a1tagb
     c                   endif
      *
      * F6=Skriv kollietiketter
      *
     c                   if        *inkf
      *
      *  Legg ut nye/oppdater linjer på ordrelinjeregisteret
      *
     c                   eval      fodtlu_numm = a1numm
     c                   eval      fodtlu_suff = a1suff
      * Trelast
     c                   if        a1atre > 0
     c                   eval      fodtlu_line = 9000
     c     fodtlu_key    chain     fodtlur
     c                   eval      w_line = 9000
     c                   eval      w_anta = a1atre
     c                   eval      w_type = 'Trelast:'
     c                   eval      w_text = a1ttxt
     c                   if        not %found
     c                   exsr      ny_linje
     c                   else
     c                   exsr      opd_linje
     c                   endif
     c                   endif
      * Pall
     c                   if        a1apal > 0
     c                   eval      fodtlu_line = 9001
     c     fodtlu_key    chain     fodtlur
     c                   eval      w_line = 9001
     c                   eval      w_anta = a1apal
     c                   eval      w_type = 'Pall   :'
     c                   eval      w_text = a1ptxt
     c                   if        not %found
     c                   exsr      ny_linje
     c                   else
     c                   exsr      opd_linje
     c                   endif
     c                   endif
      * Andre
     c                   if        a1aand > 0
     c                   eval      fodtlu_line = 9002
     c     fodtlu_key    chain     fodtlur
     c                   eval      w_line = 9002
     c                   eval      w_anta = a1aand
     c                   eval      w_type = 'Andre  :'
     c                   eval      w_text = a1atxt
     c                   if        not %found
     c                   exsr      ny_linje
     c                   else
     c                   exsr      opd_linje
     c                   endif
     c                   endif
      *
      * Da skriver vi ut etiketter
      *
     c                   call      'FP270C'
     c                   parm                    w_firm
     c                   parm                    a1numm
     c                   parm                    a1suff
      *
     c                   else
     c                   goto      a1tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for opprette ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_linje      begsr
      *
     c                   clear                   fodtlur
      *
     c                   eval      fdfirm = fofirm
     c                   eval      fdnumm = fonumm
     c                   eval      fdsuff = fosuff
     c                   eval      fdline = w_line
     c                   eval      fdotyp = footyp
     c                   eval      fdltyp = 'TX'
     c                   eval      fdlage = folage
     c                   eval      fdkund = fokund
     c                   eval      fdavde = foavde
     c                   if        w_anta > 9
     c                   eval      fdtek1 = w_type+' '+%char(w_anta)+' '+w_text
     c                   else
     c                   eval      fdtek1 = w_type+'  '+%char(w_anta)+' '+w_text
     c                   endif
      *
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for oppdatere ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opd_linje     begsr
      *
     c                   if        w_anta > 9
     c                   eval      fdtek1 = w_type+' '+%char(w_anta)+' '+w_text
     c                   else
     c                   eval      fdtek1 = w_type+'  '+%char(w_anta)+' '+w_text
     c                   endif
      *
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   update    fodtlur
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Key for les av ordrehodet
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      *  Key for les av ordrelinjer
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
     c                   kfld                    fodtlr_line
      *
      *  Key for les av ordrelinjer
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      *  Key for les av ordrelinjer
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      *  Key for les av ordrelinjer
      *
     c     fodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
      *
      *  Key for les av kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c                   eval      s_numm = p_numm
     c                   eval      s_suff = p_suff
      *
     c                   endsr
