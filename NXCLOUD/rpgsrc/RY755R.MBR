     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : RY750R
      * Beskrivelse . . : FIX: Leser CHRIST-fil, og frigir varer i.h.t. kjøp
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131099 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av recordbeskrivelse - Christ Engebretsen
      *
     d                 ds
     d d_inpu                       160
     d  d_vare                       13    overlay(d_inpu:13)                   Artikkelnr
      *
      * Definering av variable i nøkler
      *
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jvdtlu_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_lvar          s             15
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser CHRIST-fil, og frigir varer
      *
     c                   read      jrvapf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   eval      w_lvar = %trim('0' + %subst(d_vare:8:6))
      *
     c                   eval      jvarl8_ldor = 009998
     c                   eval      jvarl8_lvar = w_lvar
     c     jvarl8_key    chain     jvarl8                             91
     c                   if        *in91 = *off
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur                            92
     c                   if        *in92 = *off
     c                   eval      jvdhko = 0
     c                   update    jvdtlur
     c                   endif
     c                   endif
      *
     c                   read      jrvapf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
