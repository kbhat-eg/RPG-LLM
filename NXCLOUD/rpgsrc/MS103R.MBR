     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : MS103R
      * Beskrivelse . . : Syklisk telling - starte ny telling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130809 ask  Nytt program
      * 6.20  050511 ask  Lagt inn nye valg for telleliste
6.nu  * 6.30  170614 bof  Endring ihht. nummerutvidelse
6.31  * 6.30  020216 bof  Feilretting vedr.nummerutvidelse
6.32  * 6.30  201216 bsa  Bygger opp id for syklisk telling basert på
6.32  *                   lager, år og løpenummer.
6.33  * 6.30  300317 bsa  Legger ut filtreringslager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flbchlu    uf a e           k disk    rename(lbchpfr:lbchlur)
6.32 flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     fmthhlu    uf a e           k disk    rename(mthhpfr:mthhlur)
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.33 flbc1lu    uf a e           k disk    rename(lbc1pfr:lbc1lur)
      *
     fms103d    cf   e             workstn
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av parametre
      *
5.70 d p_lage          s              2  0
5.70 d p_anta          s              6  0
      *
      * Datastruktur for file-member
      *
     d                 ds
     d d_memb                        10
     d  d_mbok                        1    overlay(d_memb)
     d  d_mfir                        3  0 overlay(d_memb:2)
     d  d_mnum                        6  0 overlay(d_memb:5)
6.32  *
6.32  * Datastruktur for batchid
6.32  *
6.32 d                 ds
6.32 d d_bgen                        10
6.32 d  d_lagr                        2  0 overlay(d_bgen)
6.32 d  d_faid                        3    overlay(d_bgen:3)
6.32 d  d_aarr                        2  0 overlay(d_bgen:6)
6.32 d  d_week                        2  0 overlay(d_bgen:8)
6.32 d  d_tell                        1  0 overlay(d_bgen:10)
      *
      * Datastruktur for parameterliste
      *
     d                 ds
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fvar                       15    overlay(d_prec:11)
     d  d_tvar                       15    overlay(d_prec:26)
     d  d_fogr                        2  0 overlay(d_prec:41)
     d  d_togr                        2  0 overlay(d_prec:43)
     d  d_fhgr                        2  0 overlay(d_prec:45)
     d  d_thgr                        2  0 overlay(d_prec:47)
     d  d_fugr                        3  0 overlay(d_prec:49)
     d  d_tugr                        3  0 overlay(d_prec:52)
     d  d_fpla                        8    overlay(d_prec:55)
     d  d_tpla                        8    overlay(d_prec:63)
     d  d_flag                        2  0 overlay(d_prec:71)
     d  d_tlag                        2  0 overlay(d_prec:73)
     d  d_list                        1  0 overlay(d_prec:75)
     d  d_hovd                        1  0 overlay(d_prec:76)
     d  d_val1                        1  0 overlay(d_prec:77)
     d  d_val2                        1  0 overlay(d_prec:78)
     d  d_val3                        1  0 overlay(d_prec:79)
     d  d_opda                        6  0 overlay(d_prec:80)
     d  d_vtyp                        1    overlay(d_prec:86)
     d  d_pans                        4    overlay(d_prec:87)
     d  d_levr                        6  0 overlay(d_prec:92)
     d  d_sykl                        1  0 overlay(d_prec:98)
      *
      * Definering av variabler i nøkler
      *
     d lbchlu_batc     s                   like(lcbatc)
6.32 d lbchl1_batc     s                   like(lcbatc)
     d ltell1_batc     s                   like(lebatc)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lcfirm)
     d w_batc          s                   like(lcbatc)
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_type          s              2
6.31 d w_numm          s              6  0
6.31 d w_num8          s              8  0
     d w_numa          s              6
     d w_time_6        s              6  0
     d w_qnnn          s              1
6.32 d w_aarr          s              4  0
6.32 d w_date          s               d   datfmt(*iso)
6.32 d w_week          s              2  0
6.32 d w_stat          s              1
6.32 d w_tell          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b2tagb        tag
      *
     c                   exsr      xc1bld
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å starte syklisk telling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      c1anta = p_anta
      *
     c     c1tagb        tag
      *
     c                   exfmt     c1bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_xc1
     c                   endif
      *
      * Validering av input
      *
     c                   if        c1ansv = *blank
     c                   eval      *in40 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c     *dmy          test(d)                 c1bdat                 41
     c                   if        *in41 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   eval      w_time_6 = c1btim * 100
     c     *hms          test(t)                 w_time_6               42
     c                   if        *in42 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c     *dmy          test(d)                 c1edat                 43
     c                   if        *in43 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   eval      w_time_6 = c1etim * 100
     c     *hms          test(t)                 w_time_6               44
     c                   if        *in44 = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   lbchlur
      *
      * Bygg nytt batcnummer
      *
6.32  * Sjekk først om kan benytte liste etter ny metode
      *
6.32 c                   eval      w_batc = *blanks
6.32 c                   eval      w_tell = 1
6.32 c     ny            tag
6.32 c                   if        w_tell < 10
6.32 c                   eval      d_tell = w_tell
6.32 c                   eval      lbchl1_batc = d_bgen
6.32 c     lbchl1_key    chain     lbchl1
6.32 c                   if        %found
6.32 c                   eval      w_tell = w_tell + 1
6.32 c                   goto      ny
6.32 c                   else
6.32 c                   eval      w_batc = d_bgen
6.32 c                   endif
6.32 c                   endif
      *
6.32 c                   if        w_batc = *blanks
     c                   eval      w_fell = 'SYKTEL'
     c                   exsr      hntnum
     c                   move      w_numm        w_numa
     c                   eval      w_batc = 'SYKL' + w_numa
6.32 c                   endif
      *
     c                   eval      lbchlu_batc = w_batc
     c     lbchlu_key    chain     lbchlur
      *
      * Hent tellelistenummer ved ny batch
      *
     c                   if        not %found
     c                   eval      w_fell = laatel
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
     c                   eval      lcnumm = w_numm
      *
     c                   eval      lcansv = c1ansv
     c                   eval      lcsper = 0
     c                   eval      lcstel = 1
     c                   eval      lcahte = 0
     c                   eval      lclope = 'DIVTEL'
      *
     c     *dmy          move      c1bdat        lcbdat
     c                   eval      w_time_6 = c1btim * 100
     c     *hms          move      w_time_6      lcbtim
     c     *dmy          move      c1edat        lcedat
     c                   eval      w_time_6 = c1etim * 100
     c     *hms          move      w_time_6      lcetim
      *
6.10 c                   time                    lcodat
6.10 c                   time                    lcotim
6.10 c                   eval      lcousr = l_user
     c                   eval      lcfirm = w_firm
     c                   eval      lcbatc = w_batc
     c                   write     lbchlur
6.33  *
6.33  * Legg ut filtreringslager på tilleggstabell
6.33  *
6.33 c     lbchlu_key    chain     lbc1lur
6.33  *
6.33 c                   if        not %found
6.33 c                   eval      l1firm = w_firm
6.33 c                   eval      l1batc = w_batc
6.33 c                   eval      l1lage = p_lage
6.33 c                   time                    l1odat
6.33 c                   time                    l1otim
6.33 c                   eval      l1ousr = l_user
6.33 c                   time                    l1edat
6.33 c                   time                    l1etim
6.33 c                   eval      l1eusr = l_user
6.33 c                   write     lbc1lur
6.33 c                   endif
      *
     c                   endif
      *
     c                   exsr      lag_tel
     c                   exsr      lag_hh
      *
     c     end_xc1       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller program for å danne telle liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_tel       begsr
      *
      *  Legger inn opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_batc = w_batc
     c                   eval      d_sykl = 1
     c                   eval      d_list = 1
     c                   eval      d_hovd = 1
6.20 c                   eval      d_val1 = c1antl
     c                   eval      d_val2 = 0
6.20 c                   eval      d_val3 = c1sort
     c                   eval      d_opda = 0
     c                   eval      d_vtyp = 'L'
      *
     c                   movel     '00000000'    d_fvar
     c                   movel     '99999999'    d_tvar
     c                   eval      d_fogr = 0
     c                   eval      d_togr = 99
     c                   eval      d_fhgr = 0
     c                   eval      d_thgr = 99
     c                   eval      d_fugr = 0
     c                   eval      d_tugr = 999
     c                   eval      d_fpla = '        '
     c                   eval      d_tpla = '99999999'
     c                   eval      d_pans = *blank
     c                   eval      d_levr = 0
      *
     c                   eval      d_flag = p_lage
     c                   eval      d_tlag = p_lage
      *
      *  Henter siste nummer for File-member
      *
     c                   call      'LÅ900R'
     c                   parm                    w_qnnn
     c                   parm                    w_numm
      *
     c                   eval      d_mbok = 'K'
     c                   eval      d_mfir = w_firm
     c                   eval      d_mnum = w_numm
      *
      *  Kaller opp program for videre behandling
      *
     c                   call      'LE601C'
     c                   parm                    d_prec
     c                   parm                    d_memb
      *
     c     end_tel       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Legger ut telling på håndholdt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_hh        begsr
      *
      * Sjekk om HH-transer skal legges ut
      *
     c                   if        c1brk1 = *blank and
     c                             c1brk2 = *blank and
     c                             c1brk3 = *blank and
     c                             c1brk4 = *blank and
     c                             c1brk5 = *blank
     c                   goto      end_hh
     c                   endif
      *
     c                   eval      ltell1_batc = w_batc
     c     ltell1_key    setll     ltell1
     c     ltell1_key    reade     ltell1r
      *
     c                   dow       not %eof
     c                   eval      mffirm = w_firm
     c                   eval      mfbatc = w_batc
     c                   eval      mfplas = leplas
     c                   eval      mfvare = levare
6.10 c                   time                    mfodat
6.10 c                   time                    mfotim
6.10 c                   eval      mfuser = l_user
      *
      * Legger ut telleoppdrag pr. bruker
      *
     c                   if        c1brk1 <> *blank
     c                   eval      mfhhus = c1brk1
     c                   write     mthhlur
     c                   endif
      *
     c                   if        c1brk2 <> *blank
     c                   eval      mfhhus = c1brk2
     c                   write     mthhlur
     c                   endif
      *
     c                   if        c1brk3 <> *blank
     c                   eval      mfhhus = c1brk3
     c                   write     mthhlur
     c                   endif
      *
     c                   if        c1brk4 <> *blank
     c                   eval      mfhhus = c1brk4
     c                   write     mthhlur
     c                   endif
      *
     c                   if        c1brk5 <> *blank
     c                   eval      mfhhus = c1brk5
     c                   write     mthhlur
     c                   endif
      *
     c     ltell1_key    reade     ltell1r
     c                   enddo
      *
     c     end_hh        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
6.31 c                   eval      w_num8 = *zero
     c                   eval      w_kode = '0'
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.31 c                   parm                    w_num8
6.31 c                   eval      w_numm = w_num8
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av BATCH
      *
     c     lbchlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchlu_batc
6.32  *
6.32  * Key for oppdatering av BATCH
6.32  *
6.32 c     lbchl1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    lbchl1_batc
      *
      *  Key for les av LAGER-TELLE
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Parametre
      *
5.70 c     *entry        plist
5.70 c                   parm                    p_lage
5.70 c                   parm                    p_anta
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c     lstsl1_key    chain     lstsl1r
6.32  *
6.32  * Genererer id for telleliste
6.32  *
6.32 c                   time                    w_date
6.32 c                   eval      w_stat = *blanks
6.32 c                   eval      d_lagr = p_lage
6.32 c                   eval      d_faid = 'SYK'
6.32 c                   eval      d_aarr = %subdt(w_date:*years) - 2000
6.32  *
6.32 c                   call      'AX711R'
6.32 c                   parm                    w_date
6.32 c                   parm                    w_aarr
6.32 c                   parm                    w_week
6.32 c                   parm                    w_stat
6.32  *
6.32 c                   if        w_week > 52
6.32 c                   eval      w_week = 52
6.32 c                   endif
6.32 c                   eval      d_week = w_week
      *
     c                   endsr
