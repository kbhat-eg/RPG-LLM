      /TITLE  ASLAGR Slette lagervare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LL762R
      * Beskrivelse . . : Administrerer lager og lagerbok ved sletting
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       061201 ASK  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
7.01  * 7.xx  141019 BSA  Legger inn unik time
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhislu    uf a e           k disk    rename(vhispfr:vhislur)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vhfirm)
     d p_vare          s                   like(vhvare)
      *
      * Definering av variable i nøkler
      *
     d vhislu_lage     s                   like(vhlage)
     d vhislu_vare     s                   like(vhvare)
     d vhislu_dato     s                   like(vhdato)
     d vhislu_time     s                   like(vhtime)
     d vlagl1_vare     s                   like(vlvare)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      slett_lager
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å slette alle poster på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finner evt. gamle lagerposter på dette varenummer og sletter disse.
      * Oppretter en logg-post i historisk lagerbok pr. brukt lager.
      *
     c     slett_lager   begsr
      *
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1                                 91
      *
     c                   dow       *in91 = *off
     c                   eval      vlaglu_lage = vllage
     c     vlaglu_key    chain     vlaglu                             92
     c                   if        *in92 = *off
     c                   exsr      lagerbok
     c                   delete    vlaglur
     c                   endif
     c     vlagl1_key    reade     vlagl1                                 91
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette post i historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Logger sletting av vare i historisk-lagerbok, dette gjøres på alle
      * brukte lager.
      *
     c     lagerbok      begsr
      *
     c                   eval      vhfirm = w_firm
     c                   eval      vhvare = vhislu_vare
     c                   eval      vhlage = vllage
     c                   eval      vhdato = vhislu_dato
7.01 c**                 eval      vhtime = vhislu_time
7.01 c                   time                    vhtime
     c                   eval      vhuser = l_user
     c                   eval      vhsyst = *blank
     c                   eval      vhkode = *blank
     c                   eval      vhtype = 'U'
     c                   eval      vhrefr = 'Slettet  '
     c                   eval      vhbehl = 0
     c                   eval      vhanta = 0
6.10 c                   time                    vhodat
6.10 c                   time                    vhotim
6.10 c                   eval      vhousr = l_user
6.10 c                   time                    vhedat
6.10 c                   time                    vhetim
6.10 c                   eval      vheusr = l_user
      *
     c                   write     vhislur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av historisk lagerbok
      *
     c     vhislu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhislu_lage
     c                   kfld                    vhislu_vare
     c                   kfld                    vhislu_dato
     c                   kfld                    vhislu_time
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      * Key for oppdatering på lagerregister
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Legger inn verdier i key-felter
      *
     c                   eval      w_firm = p_firm
     c                   eval      vhislu_vare = p_vare
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlaglu_vare = p_vare
     c                   time                    vhislu_dato
     c                   time                    vhislu_time
      *
     c                   endsr
