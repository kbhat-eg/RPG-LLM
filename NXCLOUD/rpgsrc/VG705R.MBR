     h option(*nodebugio) datedit(*dmy)
     h dftactgrp(*no)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG705R
      * Beskrivelse . . : Leser innkjnøpsstatistikk og danner gjennomsnittlig kostpris
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240820 ASK  Nytt program
      * 8.00  160921 ask  Programmet er kraftig omskrevet og endringer er ikke merket
      *                   Ses på som nytt program
      *
8.01  * 800   160222 ask  Lagt inn mulighet for nullstilling av kostpris register
      *                   og oppdatering av SIVIDA i innkjøpsstatistikk.
8.02  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsisttmp   if   e           k disk    rename(sistpfr:sisttmpr)
     fvgkpiu    uf a e           k disk    rename(vgkpst:vgkpiur)
     fvgkpi1    if   e           k disk    rename(vgkpst:vgkpi1r)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvg705d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastruktur for kostprisfaktorer
      *
     d d_frec          ds
     d  d_fty1                       30
     d  d_fsi1                        1
     d  d_fak1                        8  6
     d  d_kto1                        6  0
     d  d_mot1                        6  0
     d  d_fty2                       30
     d  d_fsi2                        1
     d  d_fak2                        8  6
     d  d_kto2                        6  0
     d  d_mot2                        6  0
     d  d_fty3                       30
     d  d_fsi3                        1
     d  d_fak3                        8  6
     d  d_kto3                        6  0
     d  d_mot3                        6  0
     d  d_fty4                       30
     d  d_fsi4                        1
     d  d_fak4                        8  6
     d  d_kto4                        6  0
     d  d_mot4                        6  0
     d  d_fty5                       30
     d  d_fsi5                        1
     d  d_fak5                        8  6
     d  d_kto5                        6  0
     d  d_mot5                        6  0
      *
      * Definering av variabler i nøkler
      *
     d sisttmp_vida    s                   like(sivida)
     d vvarlr_vare     s                   like(vvvare)
     d vlaglr_vare     s                   like(vlvare)
     d vlaglr_lage     s                   like(vllage)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d vgkpiu_lage     s              2  0
     d vgkpiu_hkod     s                   like(vghkod)
     d vgkpiu_vare     s                   like(vgvare)
     d vgkpi1_lage     s              2  0
     d vgkpi1_hkod     s                   like(vghkod)
     d vgkpi1_vare     s                   like(vgvare)
     d vgkpi1_gdat     s                   like(vggdat)
     d vgkpi1_gtim     s                   like(vggtim)
      *
      * Definering av parametre
      *
8.02 d p_prgr          s              2
     d p_lety          s              1  0
     d p_kopr          s              9  2
     d p_inpr          s              9  2
     d p_aogr          s              2  0
     d p_ahgr          s              2  0
     d p_augr          s              3  0
     d p_aldo          s              6  0
     d p_amod          s              8  0
     d p_aart          s              8  0
     d p_apro          s              3  1
     d p_inlr          s              1
      **
      *
      * Definering av variable
      *
     d w_firm          s                   like(sifirm)
     d w_sign          s              1  0
     d w_til1          s              9  2
     d w_til2          s              9  2
     d w_til3          s              9  2
     d w_til4          s              9  2
     d w_til5          s              9  2
     d w_ipny          s             11  2
     d w_kpny          s             11  2
     d w_inkp          s             11  2
     d w_avpv          s             12  2
     d w_avpp          s             12  2
     d w_omrf          s              4  2
     d w_nylv          s             12  2
     d w_gmlv          s             12  2
     d w_dato          s              8  0
     d w_behl          s             12  3
     d w_gbeh          s             12  3
     d w_feik          s              2  0
     d w_feil          s            100
     d w_hdat          s               d   datfmt(*iso)
     d w_htim          s               t   timfmt(*iso)
     d w_sdat          s               d   datfmt(*iso)
     d w_frec          s            300
     d w_cost          s                   like(vgcost)
     d w_gjkp          s                   like(vgcost)
     d w_nykp          s                   like(vgcost)
     d w_antb          s                   like(sianta)
     d w_anta          s                   like(sianta)
     d w_sumb          s                   like(sisumb)
     d w_sumk          s                   like(sisumk)
     d w_anth          s                   like(sianta)
     d w_ldor          s                   like(sildor)
     d w_ofak          s             12  6
     d s_ornr          s                   like(siornr)
     d s_vida          s                   like(sivida)
     d s_bida          s                   like(sibida)
     d s_vare          s                   like(sivare)
     d s_lage          s                   like(silage)
     d b_avvik         s              1    inz(*off)

       DCL-PR VG707R EXTPGM('VG707R');
         p_aogr      packed(2:0);
         p_ahgr      packed(2:0);
         p_augr      packed(3:0);
         p_aldo      packed(6:0);
         p_amod      packed(8:0);
         p_aart      packed(8:0);
         p_apro      packed(3:1);
         p_inlr      char(1);
       END-PR;


      *
      * HOVEDPROGRAM
      *
      * Behandling av parameterbilde - sjekk gyldige valg
      *
     c/exec sql
     c+ SET OPTION DatFmt=*ISO,
     c+            SRTSEQ=*LANGIDUNQ,
     c+            LANGID=NOR,
     c+            commit=*none
     c/end-exec

     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk startdato
      *
     c                   if        c2sdat = 0
     c                   eval      *in41 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk priskode
      *
     c                   if        c2kprk = *blank
     c                   eval      *in42 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
8.01  *
8.01  * Skal kostprisregister nullstilles?
8.01  *
8.01 c                   if        c2null = 1
8.01 c     *ymd          move      c2sdat        w_sdat
8.01 c                   exsr      null_regi
8.01 c                   endif
8.01  *
8.01  * Skal virkelig mottattdato i innkjøpsstatistikk justeres
8.01  *
8.01 c                   if        c2stat = 1
8.01 c                   exsr      oppd_stat
8.01 c                   endif
      *
      * Leser i gjennom innkjøpsstatistikk fra startpunkt
      *
     c     *dmy          move      c2sdat        sisttmp_vida
     c     sisttmp_key   setll     sisttmp
      *
      *
     c                   read      sisttmp
     c                   dow       not %eof
      *
      * Skal bare noen lager inkluderes?
      *
     c                   if        c2lag0 = 0
     c                   if        c2lagu = 0
     c                   if        silage <> c2lag1 and
     c                             silage <> c2lag2 and
     c                             silage <> c2lag3 and
     c                             silage <> c2lag4 and
     c                             silage <> c2lag5 and
     c                             silage <> c2lag6 and
     c                             silage <> c2lag7 and
     c                             silage <> c2lag8 and
     c                             silage <> c2lag9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Skal noen lager ekskluderes?
      *
     c                   if        c2lag0 = 0
     c                   if        c2lagu = 1
     c                   if        silage = c2lag1 or
     c                             silage = c2lag2 or
     c                             silage = c2lag3 or
     c                             silage = c2lag4 or
     c                             silage = c2lag5 or
     c                             silage = c2lag6 or
     c                             silage = c2lag7 or
     c                             silage = c2lag8 or
     c                             silage = c2lag9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Skal bare noen ordretyper inkluderes?
      *
     c                   if        c2oty0 = 0
     c                   if        c2otyu = 0
     c                   if        siotyp <> c2oty1 and
     c                             siotyp <> c2oty2 and
     c                             siotyp <> c2oty3 and
     c                             siotyp <> c2oty4 and
     c                             siotyp <> c2oty5 and
     c                             siotyp <> c2oty6 and
     c                             siotyp <> c2oty7 and
     c                             siotyp <> c2oty8 and
     c                             siotyp <> c2oty9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Skal noen ordretyper ekskluderes?
      *
     c                   if        c2oty0 = 0

     c                   if        c2otyu = 1
     c                   if        siotyp = c2oty1 or
     c                             siotyp = c2oty2 or
     c                             siotyp = c2oty3 or
     c                             siotyp = c2oty4 or
     c                             siotyp = c2oty5 or
     c                             siotyp = c2oty6 or
     c                             siotyp = c2oty7 or
     c                             siotyp = c2oty8 or
     c                             siotyp = c2oty9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Direktelevert skal ikke oppdatere
      *
     c                   if        silety = 1
     c                   goto      les_neste
     c                   endif
TST   *
TST  c*                  if        sivare <> '24372518'
TST  c*                  goto      les_neste
TST  c*                  endif
      *
      * Sjekker om varen er gyldig vare
      *
     c                   eval      vvarlr_vare = sivare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker om varen er lagervare
      *
     c                   eval      vlaglr_vare = sivare
     c                   eval      vlaglr_lage = silage
     c     vlaglr_key    chain     vlaglr
     c                   if        not %found(vlaglr) or
     c                             vlvtyp <> 'L'
     c                   goto      les_neste
     c                   endif
      *
      * Sjekk om bestilling/vare er behandlet før
      *
     c                   if        siornr = s_ornr and
     c                             sivida = s_vida and
     c                             sivare = s_vare and
     c                             silage = s_lage
     c                   goto      les_neste
     c                   else
     c                   eval      s_ornr = siornr
     c                   eval      s_vida = sivida
     c                   eval      s_bida = sibida
     c                   eval      s_vare = sivare
     c                   eval      s_lage = silage
     c                   endif
      *
      * Beregn sum på innkjøp - 0 eller negativ i antall skal ikke oppdatere
      *
     c                   exsr      sum_best
     c                   if        w_antb < 0.001
     c                   eval      w_feik = 01
     c                   eval      w_feil = 'Sum innkjøp er 0 eller negativt'
     c                   exsr      logg_feil
     c                   goto      les_neste
     c                   endif
      *
      * Beregn sum i lagerbok 0 eller negativ i antall skal ikke oppdatere
      *
     c                   exsr      sum_hist
     c                   if        w_anth < 0.001
     c                   eval      w_feik = 02
     c                   eval      w_feil = 'Sum lagerbok er 0 eller negativt'
     c                   exsr      logg_feil
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker om varen er bestilt i lagerenhet - hvis ikke, regn om til lagerenhet
      * Beregn avvik
      *
     c                   eval      w_ofak = 0
     c                   if        sivenh <> vvenhl
     c                   eval      w_anta = w_antb
     c                   exsr      beregn_pris
     c                   eval      w_antb = w_anta
     c                   endif
      *
     c                   eval(h)   w_avpv = %abs(((w_antb-w_anth)*100)/w_anth)
     c                   if        w_avpv > 1
     c                   eval      w_feik = 03
     c                   eval      w_feil = 'Avvik mellom innkjøps transaksjon +
     c                                       og lagerbok'
     c                   exsr      logg_feil
     c                   goto      les_neste
     c                   endif
      *
      * Gjør beregning av ny glidende gj.sn. kostpris
      *
     c                   exsr      oppd_kostpris
      *
     c     les_neste     tag
      *
     c                   read      sisttmp
     c                   enddo
      *
     c                   goto      c2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av gjennomsnittlig kostpris på en vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_kostpris begsr
      *
      * Beregner priser for oppdatering
      *
     c                   eval(h)   w_ipny = w_sumb/w_antb
     c                   eval      w_kpny = w_sumk/w_antb
      *
      * Sjekker om innkjøpspris eller kostpris skal brukes
      *
     c                   if        c2kprk = 'I'
     c                   eval      w_inkp = w_ipny
     c                   else
     c                   eval      w_inkp = w_kpny
     c                   endif
      *
      * Hent eventuelle kostprisfaktorer og beregn tillegg/fradrag
      *
     c                   eval      w_ldor = sifaln
     c                   call      'VG703R'
     c                   parm                    s_vare
     c                   parm                    s_lage
     c                   parm                    w_ldor
     c                   parm                    w_frec
     c                   eval       d_frec = w_frec
      *
     c                   exsr      finn_kostt
     c                   eval      w_nykp = w_inkp + w_til1 + w_til2 + w_til3
     c                                             + w_til4 + w_til5
      *
      * Sjekk om innpris er %-vis usansynlig sammenlignet mot kalkulert kostpris.
      *
     c                   eval      b_avvik = *off
     c                   exsr      kost_kontroll
      *
     c                   if        b_avvik = *off
     c                   if        c2kprk = 'I'
     c                   eval      w_nykp = w_nykp * w_omrf
     c                   endif
     c                   else
     c                   eval      w_feik = 04
     c                   eval      w_feil = 'Avvik mellom transaksjon pris +
     c                                       og kalk. kostpris for stort'
     c                   exsr      logg_feil
     c                   goto      end_kostpris
     c                   endif
      *
      * Beregn lagerverdi på nytt innkjøp
      *
     c                   eval(h)   w_nylv = w_nykp * w_antb
      *
      * Hent gjeldende gjennomsnittlig kostpris og beregn eksisterende lagerverdi
      *
     c                   eval      w_cost = 0
     c                   eval      w_dato = %dec(s_vida:*iso)
     c                   call      'VG701R'
     c                   parm                    s_vare
     c                   parm                    s_lage
     c                   parm                    w_dato
     c                   parm                    w_cost
      *
      *
      * Beregn lagerverdi på beholdning før  innkjøp
      *
     c                   exsr      finn_behl
     c                   eval(h)   w_gmlv = w_cost * w_gbeh
      *
      * Bergn ny glidende gjennomsnittlig kostpris
      *
     c                   if        w_antb > 0 and
     c                             w_gmlv > 0
     c                   eval(h)   w_gjkp = (w_nylv + w_gmlv)
     c                                       / w_behl
     c                   else
     c                   eval      w_gjkp = w_nykp
     c                   endif
      *
      * Sjekk om kostpris er 0 eller negativ
      *
     c                   if        w_gjkp <= 0
     c                   eval      w_feik = 05
     c                   eval      w_feil = 'Ny kalkulet glidende kostpris +
     c                                       er null eller negativ'
     c                   exsr      logg_feil
     c                   goto      end_kostpris
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      vgkpiu_hkod = *blank
     c                   movel     s_vare        vgkpiu_vare
     c                   eval      vgkpiu_lage = s_lage
     c     vgkpiu_key    chain     vgkpiu
      *
     c                   if        %found
     c                   exsr      pris_hist
     c                   end
      *
     c                   clear                   vgkpiur
     c                   eval      vgfirm = w_firm
     c                   eval      vghkod = ' '
     c                   eval      vgvare = s_vare
     c                   eval      vglage = s_lage
     c                   eval      vggdat = w_hdat
     c                   eval      vggtim = w_htim
     c                   eval      vgenhe = vvenhl
     c                   eval      vgtype = 'I'
     c                   eval      vgcost = w_gjkp
      *
     c                   eval      vgbest = siornr
     c                   eval      vgsuff = siorsu
     c                   eval      vgline = siorli
     c                   eval      vggmkp = w_cost
     c                   eval      vginkp = w_nykp
     c                   eval      vgpkod = c2kprk
     c                   eval      vganta = w_antb
     c                   eval      vgbehl = w_gbeh
      *
     c                   time                    vgttim
     c                   time                    vgbdat
      *
     c                   write     vgkpiur
      *
     c     end_kostpris  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for beregning av kost- og innkjøps-pris i lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_pris   begsr
      *
      * Finn omregningsfaktor på innkjøpsenhet og beregn priser i forhold til basisenhet
      *
     c                   eval      vvenlr_vare = sivare
     c                   eval      vvenlr_enhe = sivenh
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_ipny = w_ipny / veomre
     c                   eval(h)   w_kpny = w_kpny / veomre
     c                   eval      w_anta = w_anta * veomre
     c                   endif
      *
      * Finn omregningsfaktor på lagerenhet og beregn priser dersom lagerenhet er ulik basisenhet
      *
     c                   if        vvenhe <> vvenhl
     c                   eval      vvenlr_enhe = vvenhl
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_ipny = w_ipny * veomre
     c                   eval(h)   w_kpny = w_kpny * veomre
     c                   eval      w_anta = w_anta / veomre
     c                   eval      w_ofak = veomre
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for feilmeldinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for avvikskontroll mot kalkulert kostpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr kost_kontroll;

          p_prgr = *blanks;
          p_lety = 0;
          p_kopr = 0;
          p_inpr = 0;
          w_omrf = 1;
          w_avpp = 0;

       // Finner avviksprosent i egen tabell

          p_aogr = siogrp;
          p_ahgr = sihgrp;
          p_augr = siugrp;
          p_aldo = sifaln;
          p_amod = 0;
          p_aart = 0;
          p_apro = 0;
          p_inlr = *blank;

          VG707R(p_aogr:p_ahgr:p_augr:p_aldo:p_amod:p_aart:p_apro:p_inlr);

       // Finner prisgruppe på lager

          exec SQL
           select vpgprg into :p_prgr
             from vpgrpf
             where vpglag = :s_lage;

       // Finner kalkulert kostpris
       //  - prøver leverandør fra innkjøp, preferert leverandør på lager, leverandør fra varekort

          if sifaln > 0;
            w_ldor = sifaln;
          else;
            w_ldor = sildor;
          endif;
          exsr finn_kost;
          if p_kopr = 0 or p_inpr = 0;
             w_ldor = vlbldo;
             exsr finn_kost;
             if p_kopr = 0 or p_inpr = 0;
               w_ldor = vvldor;
             exsr finn_kost;
          endif;
          endif;

       // Sjekker ny innpris mot kalkulert kostpris/innkjøpspris

          if p_kopr > 0 and p_inpr > 0;
           w_avpp = %abs(((w_nykp-p_kopr)*100)/p_kopr);
             if w_avpp > p_apro;
               w_avpp = %abs(((w_nykp-p_inpr)*100)/p_inpr);
               if w_avpp > p_apro;
                 b_avvik = *on;
               endif;
             endif;
           if p_kopr > p_inpr;
             w_omrf = p_kopr / p_inpr;
           endif;
          endif;

       endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne kalkulertkostpris på valgt leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
        begsr finn_kost;

          exec SQL
            select vpkopr, vpinpr
             into :p_kopr, :p_inpr
              from vvprpf where
               vpfirm = :w_firm and
               vpvare = :s_vare and
               vpprgr = :p_prgr and
               vplety = :p_lety and
               vpldor = :w_ldor and
               vpenhe = :vvenhl and
               vppdat <= :s_bida
              order by vppdat desc
               fetch first row only;

        endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å summere alle linjer på dato/bestilling/vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_best      begsr
      *
      /FREE
          w_antb = 0;
          w_sumk = 0;
          w_sumb = 0;

            exec sql
            select
              sum(sianta),
              sum(sisumk),
              sum(sisumb - sirab1 - sirab2 - sirabo)
             into :w_antb, :w_sumk, :w_sumb
            from sistpf
             where sifirm = :w_firm and siornr = :s_ornr and
                   sivida = :s_vida and sivare = :s_vare and
                   silage = :s_lage;

      /END-FREE
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å summere alle linjer i historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_hist      begsr
      *
      /FREE

          w_anth = 0;

            exec sql
            select sum(vhanta) into :w_anth
            from vhispf
             where vhfirm = :w_firm and vhonum = :s_ornr and
                   vhdato = :s_vida and vhvare = :s_vare and
                   vhlage = :s_lage;
            if sqlcod < 0;
              w_anth = 0;
            endif;

      /END-FREE
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne gammel beholdning fra historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_behl     begsr
      *
      /FREE

          w_behl = 0;
          w_gbeh = 0;

            exec sql
            select vhbehl, vhdato, vhtime
             into :w_behl, :w_hdat, :w_htim
            from vhispf
             where vhfirm = :w_firm and vhonum = :s_ornr and
                   vhdato = :s_vida and vhvare = :s_vare and
                   vhlage = :s_lage
             order by vhdato, vhtime desc, vhsekv desc
               fetch first rows only;

          w_gbeh = w_behl - w_antb;

      /END-FREE
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for logging av avvik i transaksjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr logg_feil;

      // Nullstiller verdier avhengig av avbruddspunkt

         if w_feik = 1;
          w_anth = 0;
          w_gbeh = 0;
          w_avpv = 0;
          w_avpp = 0;
          p_apro = 0;
          p_kopr = 0;
          w_nykp = 0;
          w_cost = 0;
          w_ofak = 0;
         endif;

         if w_feik = 2;
          w_gbeh = 0;
          w_avpv = 0;
          w_avpp = 0;
          p_apro = 0;
          p_kopr = 0;
          w_nykp = 0;
          w_cost = 0;
          w_ofak = 0;
         endif;

         if w_feik = 3;
          w_gbeh = 0;
          w_avpp = 0;
          p_apro = 0;
          p_kopr = 0;
          w_nykp = 0;
          w_cost = 0;
         endif;

         if w_feik = 4;
          w_nykp = 0;
          w_cost = 0;
         endif;

             exec sql
            insert into vgkest
              (vefirm,velage,vevare,veenhe,vetdat,
               vebest,vesuff,veinkp,vesumb,vesumh,
               vebehl,veavpv,veavpp,veavpt,vepkod,
               vegkpr,venkpr,vefeik,vefeil,vetype,
               vebusr,vekalp,veldor,veenhb,veomrf,
               veantb)
            values(:w_firm,:silage,:sivare,:vvenhl,:sibida,
                   :siornr,:siorsu,:w_inkp,:w_sumb,:w_anth,
                   :w_gbeh,:w_avpv,:w_avpp,:p_apro,:c2kprk,
                   :p_kopr,:w_nykp,:w_feik,:w_feil,'I',
                   :l_user,:w_cost,:w_ldor,:sivenh,:w_ofak,
                   :w_antb);
       endsr;
      /END-FREE

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å beregne kostpris tillegg og fradrag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kostt    begsr
      *
     c                   if        d_fty1 <> *blank
     c                   if        d_fsi1 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til1  = ((w_inkp * d_fak1)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty2 <> *blank
     c                   if        d_fsi2 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til2  = ((w_inkp * d_fak2)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty3 <> *blank
     c                   if        d_fsi3 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til3  = ((w_inkp * d_fak3)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty4 <> *blank
     c                   if        d_fsi4 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til4  = ((w_inkp * d_fak4)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty5 <> *blank
     c                   if        d_fsi5 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til5  = ((w_inkp * d_fak5)/100) * w_sign
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kostpris historikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pris_hist     begsr
      *
      * Sletter opprinnelig post
      *
     c                   delete    vgkpiur
      *
      * Oppretter historikk post - sjekker om historikk post allerede finnes
      *
     c                   eval      vgkpi1_lage = vglage
     c                   eval      vgkpi1_hkod = 'H'
     c                   eval      vgkpi1_vare = vgvare
     c                   eval      vgkpi1_gdat = vggdat
     c                   eval      vgkpi1_gtim = vggtim
     c     vgkpi1_key    chain     vgkpi1
      *
     c                   if        %found
     c                   goto      end_hist
     c                   end
     c                   eval      vghkod = 'H'
      *
     c                   write     vgkpiur
      *
     c     end_hist      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å nullstille kostypris register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr null_regi;

             exec sql
               delete from vgkpst
                where vgfirm = :w_firm and
                      vggdat >= :w_sdat;
       endsr;
      /END-FREE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å legge inn manglende info i innkjøpsstatistikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr oppd_stat;

             exec sql
               update sistpf
                set sivida = sildat
                 where sifirm = :w_firm and
                       sivida = '0001-01-01';
       endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av innkjøpsstatistikk
      *
     c     sisttmp_key   klist
     c                   kfld                    w_firm
     c                   kfld                    sisttmp_vida
      *
      * Key for posisjonering på lager via lagernummer
      *
     c     vlaglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglr_vare
     c                   kfld                    vlaglr_lage
      *
      *  Key for oppdslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      *  Key for oppdslag på vare/enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for oppslag av kostpris
      *
     c     vgkpiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpiu_lage
     c                   kfld                    vgkpiu_hkod
     c                   kfld                    vgkpiu_vare
      *
     c     vgkpi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpi1_lage
     c                   kfld                    vgkpi1_hkod
     c                   kfld                    vgkpi1_vare
     c                   kfld                    vgkpi1_gdat
     c                   kfld                    vgkpi1_gtim
      *
     c                   eval      w_firm = l_firm
      *
      * Henter kode informasjon
      *
     c     vgkkir_key    chain     vgkkir
      *
     c                   endsr
