      /title  ASSHOP Henter inn fra håndterminal til butikk-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BOHNHR
      * Beskrivelse . . : Henter inn fra håndterminal til butikk-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190600 HKO  Nytt program
      *       280900 HKO  Tilpasset ny håndterminal ordre-fil
      *       121200 HKO  Oppdaterer butikk-linje med referansenummer fra
      *                   håndterminal
      *       210301 HKO  Oppdaterer kundeprosjekt navn og adresse,
      *                   leveringsadrsse, kundens ref. og rekv.nummer ved
      *                   innlesing fra håndterminal-hode
      *       300501 HKO  Henter kode for undertrykt fakturagebyr fra
      *                   kundereg.
3.31  *       281102 AMD  Tar med selgernummer til butikk-hode-register
5.01  *       290403 AMD  Oppdatering av alfafelt i ordrehode (korrigering)
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       230508 ASK  Håndterer fakturakunde på radioterminal
5.70  * PRGR  121208 bof  Utvidet med prisgruppe
6.21  * 6.21  260412 AMD  Korrigering av feil :
6.21  *                   Betalingsmåte ble lagt over fra kundereg til
6.21  *                   butikkhode i stedet for forsendelsesmåte
6.21  *                   I tillegg :
6.21  *                   Dersom kjørekontorløsning er aktivert, skal
6.21  *                   leveringsmåte settes lik 99= Lev. fra kasse
6.22  * R6.20 030815 bsa  Sjekker om det er krav til utfylt leveringsmåte
6.22  *                   via AVALPF.
6.30  * 6.30  150914 BKV  Henter overstyrt info fra kundeprosjekt
6.31  * 6.30  210815 bof  feilretting i parameter til VP900R
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhohelu    uf   e           k disk    rename(hohepfr:hohelur)
     fhodtl1    if   e           k disk    rename(hodtpfr:hodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkrpl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
     fbwsdl1    if   e           k disk    rename(bwsdpfr:bwsdl1r)
6.21 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fbmhelu    uf a e           k disk    rename(bmhepfr:bmhelur)
     fbmdtlu    uf a e           k disk    rename(bmdtpfr:bmdtlur)
6.22 favall1    if   e           k disk    rename(avalpfr:avall1r)
      *
      *
8.01 d w_prgr          s              2
      *
      * Definering av datastrukturer
      *
      *  Datastruktur - parametre inn til VP900R
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.31 d  hinumm                        8  0 overlay(hirec:56)
6.31 d  hikode                        1    overlay(hirec:64)
6.31 d  hidekn                        5  2 overlay(hirec:65)
6.31 d  hiavgf                        1  0 overlay(hirec:70)
6.31 d  hipriv                        1  0 overlay(hirec:71)
6.31 d  hitilb                        1    overlay(hirec:72)
6.31 d  hiskaf                        1    overlay(hirec:73)
6.31 d  hildor                        6  0 overlay(hirec:74)
6.31 d  hisapr                        9  2 overlay(hirec:80)
6.31 d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      *  Datastruktur - parametre ut fra VP900R
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
8.01 d  hoprgr                        2    overlay(horec:73)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(bnfirm)
     d p_aarr          s                   like(bnaarr)
     d p_wsid          s                   like(bnwsid)
     d p_refn          s                   like(hdrefn)
     d p_line          s                   like(bmline)
     d p_tots          s                   like(bntots)
      *
      * Definering av variable i nøkler
      *
     d hohelu_refn     s                   like(horefn)
     d hodtl1_refn     s                   like(hdrefn)
     d vvarl1_vare     s                   like(vvvare)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d apospf_ponr     s                   like(apponr)
     d bwsdl1_wsid     s                   like(bwwsid)
6.22 d avall1_apgm     s                   like(avapgm)
      *
      * Definering av variable
      *
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
      *
     d w_firm          s                   like(bnfirm)
     d w_aarr          s                   like(bnaarr)
     d w_wsid          s                   like(bnwsid)
     d w_numm          s                   like(bnnumm)
6.21 d w_retk          s              1
6.21 d w_qtxt          s             20
6.22 d u_lmat          s              1    inz(*off)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter dagens dato og klokkeslett
      *
     c                   time                    w_udat
     c                   time                    w_utim
      *
      * Initierer parametre ved kall av VP900R
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hilety = 0
     c                   eval      hidato = w_udat
     c                   eval      hitime = w_utim
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hitilb = *off
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinlr = *off
5.70 c                   eval      hiprgr = w_prgr
      *
      * Henter info fra kasse-register
      *
     c                   eval      bwsdl1_wsid = w_wsid
     c     bwsdl1_key    chain     bwsdl1                             90
      *
      * Leser håndterminal hode/linje, og oppdaterer butikk-linje med
      * salget, vareinfo. og pris for kunde
      *
     c                   eval      hohelu_refn = p_refn
     c     hohelu_key    chain     hohelur                            90
     c                   if        *in90 = *off
      *
     c                   eval      hostat = 1
     c                   update    hohelur
      *
     c                   eval      hodtl1_refn = p_refn
     c     hodtl1_key    setll     hodtl1
     c     hodtl1_key    reade     hodtl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      vvvare = *blank
     c                   eval      vvtek1 = *blank
      *
     c***                eval      vvarl1_vare = hdvare
     c                   movel     hdvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1r                            92
     c                   if        *in92 = *off
     c                   eval      hikund = hokund
     c                   eval      hikpro = hokpro
     c                   eval      hivare = vvvare
     c                   eval      hienhe = hdenhe
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
     c                   endif
      *
     c                   eval      p_line = p_line + 10
      *
     c                   eval      bmfirm = w_firm
     c                   eval      bmaarr = w_aarr
     c                   eval      bmwsid = w_wsid
     c                   eval      bmnumm = w_numm
     c                   eval      bmline = p_line
      *
     c                   eval      bmltyp = *blank
     c                   eval      bmvare = vvvare
     c                   eval      bmenh1 = hdenhe
     c                   eval      bmtek1 = vvtek1
     c                   eval      bmtek2 = vvtek2
     c                   eval      bmanta = hdanta
     c                   eval      bmkpri = hokpri
     c                   eval      bmoppr = hooppr
     c                   eval      bmsapr = hosapr
     c                   eval      bmkopr = hokopr
     c                   eval      bmselk = 0
     c                   eval      bmrab1 = horab1
     c                   eval      bmrab2 = horab2
      *
     c                   if        bmkopr = 0
     c                   eval      bmkopr = bmsapr * hopros
     c                   endif
      *
     c                   eval      bmsums = hosapr - (hosapr * horab1 / 100)
     c                   eval      bmsums = bmsums - (bmsums * horab2 / 100)
     c                   eval      bmsums = bmsums * bmanta
      *
     c                   eval      bmrefn = hdrefn
      *
     c                   write     bmdtlur
      *
     c                   eval      p_tots = p_tots + bmsums
      *
     c     hodtl1_key    reade     hodtl1                                 91
     c                   enddo
      *
     c                   endif
      *
      * Oppretter butikk-hode
      * Butikk-hode opprettes ikke dersom det finnes fra før, eller dersom
      * kunde ikke er oppgitt eller ikke ble funnet
      *
     c     bmhelu_key    chain     bmhelur                            90
     c                   if        *in90 = *off
     c                   goto      avslutt
     c                   endif
      *
     c                   if        hokund = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      rkunl1_kund = hokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      bnfirm = w_firm
     c                   eval      bnaarr = w_aarr
     c                   eval      bnwsid = w_wsid
     c                   eval      bnnumm = w_numm
      *
     c                   eval      bnotyp = *blank
     c                   eval      bnlage = 0
     c                   eval      bnbdat = w_udat
     c                   eval      bnfkda = *loval
     c                   eval      bnffda = *loval
3.31 c                   eval      bnselg = hoselg
     c                   eval      bnavde = bwavde
     c                   eval      bnkgfa = rkfagb
     c                   eval      bntotx = 0
     c                   eval      bntotb = 0
     c                   eval      bnmott = 0
     c***                eval      bntots = p_tots
     c                   eval      bndate = w_udat
     c                   eval      bntime = w_utim
     c                   eval      bnbinr = 0
     c                   eval      bnkref = 0
     c                   eval      bnldat = *loval
     c                   eval      bnbetb = rkbetb
     c                   eval      bndist = rkdist
6.21 c                   eval      bnlmat = rkform
     c                   eval      bnlbet = rklbet
     c                   eval      bnvalk = rkvalk
     c                   eval      bnrkat = rkrabk
     c                   eval      bnsped = rksped
     c                   eval      bnkjor = rkkjØr
     c                   eval      bnmkod = rkmkod
     c                   eval      bnlkun = 0
     c                   eval      bnkund = hokund
5.01 c                   movel     rkalfa        bnkuna
     c                   eval      bnkpro = hokpro
     c                   eval      bndref = hodref
     c                   eval      bnrekv = horekv
      *
     c                   eval      bnnavn = honavn
     c                   eval      bnadr1 = *blank
     c                   eval      bnadr2 = *blank
     c                   eval      bnsted = *blank
6.21  *
6.21  * Dersom kjørekontorløsning er aktivert
6.21  * og det ikke er oppgitt leveringsmåte
6.21  * settes leveringsmåte = 99
6.21  *
6.21 c                   if        bnlmat = 0 and
6.21 c                             faakjk = 1
6.21 c                   eval      bnlmat = 99
6.21 c                   exsr      sbform
6.21 c                   endif
6.22  *
6.22  * Dersom krav til utfylt leveringsmåte
6.22  * og det ikke er leveringsmåte på kunde
6.22  * settes leveringsmåte = 99
6.22  *
6.22 c                   if        bnlmat = 0 and
6.22 c                             u_lmat = *on
6.22 c                   eval      bnlmat = 99
6.22 c                   exsr      sbform
6.22 c                   endif
6.21  *
      *
     c                   if        hokpro <> 0
     c                   eval      fkprl1_kund = hokund
     c                   eval      fkprl1_kpro = hokpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
      *
6.30 c                   eval      bnbetb = flbetb
6.30 c                   eval      bnmkod = flmkod
6.30 c                   eval      bnkgfa = flfagb
      *
     c                   if        bnnavn = *blank
     c                   eval      bnnavn = flnavn
     c                   endif
      *
     c                   eval      bnadr1 = fladr1
     c                   eval      bnadr2 = fladr2
      *
     c                   if        flponr <> 0
     c                   movel     flponr        bnsted
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf                             91
     c                   if        *in91 = *off
     c                   eval      bnsted = %trim(bnsted) + ' ' + apsted
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Sjekker om fakturakunde finnes - bytter verdier
      *
     c                   if        rkfaku <> 0
     c                   eval      bnnavn = rknavn
     c                   eval      bnadr1 = rkgate
     c                   eval      bnadr2 = rkadr2
     c                   eval      bnsted = rksted
      *
     c                   eval      rkunl1_kund = rkfaku
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      bnlkun = hokund
     c                   eval      bnkund = rkkund
5.01 c                   movel     rkalfa        bnkuna
     c                   endif
     c                   endif
      *
     c                   write     bmhelur
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Sjekk/Hent tekst for leveringsmåte                RS717R
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sbform        begsr
6.21  *
6.21 c                   eval      bnlmtx = *blank
6.21 c                   call      'RS717R'
6.21 c                   parm                    w_retk
6.21 c                   parm                    bnlmat
6.21 c                   parm                    w_qtxt
6.21  *
6.21 c                   if        w_retk = *blank
6.21 c                   movel     w_qtxt        bnlmtx
6.21 c                   endif
6.21  *
6.21 c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    p_refn
     c                   parm                    p_line
     c                   parm                    p_tots
      *
      * Key for oppdatering av håndterminal ordre-hode
      *
     c     hohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohelu_refn
      *
      * Key for les på håndterminal ordre-linje
      *
     c     hodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hodtl1_refn
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppslag på kasse-register
      *
     c     bwsdl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bwsdl1_wsid
      *
      * Key for oppdatering av butikk-hode
      *
     c     bmhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_wsid
     c                   kfld                    w_numm
6.21  *
6.21  * Key for oppslag på Ofak status register
6.21  *
6.21 c     fstsl1_key    klist
6.21 c                   kfld                    w_firm
6.22  *
6.22  * Key for file : Firmastyrt validering
6.22  *
6.22 c     avall1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    avall1_apgm
      *
      * Henter firma, år og ws-id fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_aarr = p_aarr
     c                   eval      w_wsid = p_wsid
     c                   eval      w_numm = 0
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
6.21  *
6.21  * Henter inn status-register i OFAK
6.21  *
6.21 c     fstsl1_key    chain     fstsl1
6.22  *
6.22  * Sjekk om det er lagt opp krav til leveringsmåte
6.22  *
6.22 c                   eval      avall1_apgm = 'FO101R'
6.22 c     avall1_key    setll     avall1
6.22 c     avall1_key    reade     avall1
6.22 c                   dow       not %eof(avall1)
6.22 c                   if        avaflt = 'FOLMAT'  and
6.22 c                             avaval = 1
6.22 c                   eval      u_lmat = *on
6.22 c                   endif
6.22 c     avall1_key    reade     avall1
6.22 c                   enddo
6.22  *
6.22  * Sjekk om det er lagt opp krav til leveringsmåte
6.22  *
6.22 c                   eval      avall1_apgm = 'FO102R'
6.22 c     avall1_key    setll     avall1
6.22 c     avall1_key    reade     avall1
6.22 c                   dow       not %eof(avall1)
6.22 c                   if        avaflt = 'FOLMAT'  and
6.22 c                             avaval = 1
6.22 c                   eval      u_lmat = *on
6.22 c                   endif
6.22 c     avall1_key    reade     avall1
6.22 c                   enddo
6.22  * Sjekk om det er lagt opp krav til leveringsmåte
6.22  *
6.22 c                   eval      avall1_apgm = 'FO103R'
6.22 c     avall1_key    setll     avall1
6.22 c     avall1_key    reade     avall1
6.22 c                   dow       not %eof(avall1)
6.22 c                   if        avaflt = 'FOLMAT'  and
6.22 c                             avaval = 1
6.22 c                   eval      u_lmat = *on
6.22 c                   endif
6.22 c     avall1_key    reade     avall1
6.22 c                   enddo
      *
     c                   endsr
