     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FM501R
      * Beskrivelse . . : Kort/fakt.avtaler, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       171110 bsa  Nytt program
6.10  *       221210 bsa  Legger over koden uansett
6.11  *       030211 bsa  Feil i korttype. Henter over feil felt
6.12  *       070211 bsa  Håndtere Maxbo kort på samme måte som BM Kort
6.13  *       181011 bsa  Endret ledetekst og felter
6.14  *       300312 bsa  Innført GE kort, håndteres på samme måte som BM og Maxbo
6.15  *       060812 bsa  Hvis det ikke er fullmakt, skal kortet ikke vises
6.20  *       090914 bsa  Kun kreditkort sjekkes mot fullmakt
6.21  *       090914 bsa  Skal ta det kortet som er valgt.
8.00  *  8.00 250522 sst  Endret fra 'GE Kort' til Santander
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukrl1    if   e           k disk    rename(rukrpfr:rukrl1r)
6.10 frukrl5    if   e           k disk    rename(rukrpfr:rukrl5r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     ffm501d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rukfir)
     d p_kund          s                   like(rukkun)
     d p_kpro          s                   like(rukpro)
     d p_kftp          s                   like(ruktyp)
     d p_ktsq          s                   like(ruktsq)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rukrl1_kkun     s                   like(rukkun)
6.10 d rukrl5_kkun     s                   like(rukkun)
6.10 d rukrl5_ktyp     s                   like(ruktyp)
6.10 d rukrl5_kpro     s                   like(rukpro)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rukfir)
     d w_kund          s                   like(rukkun)
     d w_kpro          s                   like(rukpro)
     d w_kort          s             16
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
6.10  * Skal uansett sjekk om det finnes BM Kort på kunden. Hvis så, skal ordren merkes.
6.10 c                   exsr      sjk_kort
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
      * F6 er gjort ugyldig i skerjmbildet. Lar koden ligge så lenge. Kommer til
      * å finnes andre vedlikeholdsprogrammer.
     c                   when      *inkf
     c                   call      'FM100R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rukrl1_kkun = b1kund
     c     rukrl1_key    setll     rukrl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_kftp = b1kftp
     c                   eval      p_ktsq = b1ktsq
     c**                 eval      p_kpro = b1kpro
     c                   eval      b_valg_ok = *on
      *
6.12 c                   if        p_kftp = 5 or
6.14 c                             p_kftp = 4 or
6.14 c                             p_kftp = 3
     c                   leave
6.10 c                   endif
     c                   endif
6.10  *
6.21 c***                exsr      sjk_kort
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rukrl1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        rukfir <> w_firm or
     c                             rukkun <> w_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
6.15  *
6.20 c                   if        ruktyp > 2 and
6.20 c                             ruktyp < 6
6.20 c**                           ruktyp <> 5
6.15 c                   if        rukobl = 0
6.15 c                   iter
6.15 c                   endif
6.15 c                   endif
      *
      * Skriver til subfile
      *
     c                   if        rukpro = *zero or
     c                             w_kpro = rukpro
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rukrl1_kkun = rukkun
      *
     c                   eval      b1valg = 0
     c                   eval      b1kund = rukkun
     c**                 eval      b1kpro = rukpro
     c                   eval      b1kftp = ruktyp
     c                   eval      b1ktsq = ruktsq
      *
     c                   exsr      avt_type
      *
     c                   if        rukknr <> 0
     c                   move      rukknr        w_kort
6.13 c**                 eval      b1kort = '*' +
6.13 c**                           %subst(w_kort:11:6)
6.13 c                   movel     w_kort        b1kort
     c                   else
     c                   eval      b1kort = *blank
     c                   endif
      *
     c                   movel     rukhld        b1navn
     c                   move      rukobl        b1obli
6.13 c**                 eval      b1kexp = rukexp
      *
     c                   write     b1sfl
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_type      begsr
      *
     c                   if        ruktyp = 1
     c                   eval      b1type = 'VISA Purchas.'
     c                   elseif    ruktyp = 2
     c                   eval      b1type = 'Europay Purc.'
     c                   elseif    ruktyp = 3
     c                   eval      b1type = 'MAXBO Kort   '
     c                   elseif    ruktyp = 4
8.00 c                   eval      b1type = 'Santander    '
     c                   elseif    ruktyp = 5
     c                   eval      b1type = 'BM Kort (DNB)'
     c                   elseif    ruktyp = 6
     c                   eval      b1type = 'NBBL Fordelk.'
     c                   else
     c                   eval      b1type = 'Ukjent'
     c                   endif
      *
     c                   endsr
      *
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rukrl1_key    setgt     rukrl1
     c                   readp     rukrl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rukrl1
      *
     c                   if        %eof or
     c                             rukfir <> w_firm or
     c                             rukkun <> w_kund
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rukrl1_kkun = rukkun
      *
     c                   enddo
      *
     c                   if        %eof
     c     rukrl1_key    setll     rukrl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * Subrutine for initiering av program
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  *
6.10 c     sjk_kort      begsr
6.10  * BM Kort
6.10 c                   eval      rukrl5_ktyp = 5
6.10 c                   eval      rukrl5_kkun = w_kund
6.10 c                   eval      rukrl5_kpro = 0
6.10 c     rukrl5_key    chain     rukrl5
6.10 c                   if        %found(rukrl5) and
6.10 c                             rukspr <> 'J'  and
6.10 c                             rukobl = 1
6.11 c                   eval      p_kftp = ruktyp
6.11 c                   eval      p_ktsq = ruktsq
6.10 c                   eval      b_valg_ok = *on
6.10 c                   endif
6.12  * Maxbo kort
6.12 c                   eval      rukrl5_ktyp = 3
6.12 c                   eval      rukrl5_kkun = w_kund
6.12 c                   eval      rukrl5_kpro = 0
6.12 c     rukrl5_key    chain     rukrl5
6.12 c                   if        %found(rukrl5) and
6.12 c                             rukspr <> 'J'  and
6.12 c                             rukobl = 1
6.12 c                   eval      p_kftp = ruktyp
6.12 c                   eval      p_ktsq = ruktsq
6.12 c                   eval      b_valg_ok = *on
6.12 c                   endif
8.00  * Santander
6.14 c                   eval      rukrl5_ktyp = 4
6.14 c                   eval      rukrl5_kkun = w_kund
6.14 c                   eval      rukrl5_kpro = 0
6.14 c     rukrl5_key    chain     rukrl5
6.14 c                   if        %found(rukrl5) and
6.14 c                             rukspr <> 'J'  and
6.14 c                             rukobl = 1
6.14 c                   eval      p_kftp = ruktyp
6.14 c                   eval      p_ktsq = ruktsq
6.14 c                   eval      b_valg_ok = *on
6.14 c                   endif
6.12  *
6.12 c                   endsr
6.12  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_kftp
     c                   parm                    p_ktsq
      *
      * Key for posisjonering på kunde
      *
     c     rukrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrl1_kkun
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
6.10  *
6.10  * Key for oppslag på kortregister
6.10  *
6.10 c     rukrl5_key    klist
6.10 c                   kfld                    w_firm
6.10 c                   kfld                    rukrl5_ktyp
6.10 c                   kfld                    rukrl5_kkun
6.10 c                   kfld                    rukrl5_kpro
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rukrl1_kkun = w_kund
     c     rukrl1_key    setll     rukrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
