     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : FO617R
      * Beskrivelse . . : Danner kostpristransaksjoner fra salgsstatistikk
      *
      * Spesialversjon. : SWITCHSTYRT fra FO616C
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201020 ASK  Nytt program
8.01  * 800   070722 ASK  Lagt inn logging til "RLOG"
8.02  * 800   211022 ASK  Lagt sjekk mot fakturakunde i statistikk
8.03  * 800   150323 ask  Lagt inn nye felter i utvodet logregister for regnskapstranser
8.04  * 800   130423 ask  Henter forfallsdato fra tidligere loggede transer på bilagsnummer
8.05  * 800   160523 ask  Lagt inn sjekk på at ikke poster fra butikk blir med
8.06  * 800   131023 ask  Lagt inn oppdatering av buntsummer etter utlegg av kosttranser
8.07  * 800   171023 ask  Lagt les av parameterfil - erstatter SFERAP
8.08  * 800   261023 ask  Snur fortegen dersom det er negativ transaksjon
8.09  * 800   011123 ask  Sjekker om lagervare -hvis ikke så dropp kostpostering
8.10  * 800   091123 ask  Lagt inn oppdatering av sum bunt etter utlegg av kosttranser
8.11  * 800   190224 ask  Lagt inn switchstyring på om alle varer
      *                   eller bare lagervarer skal ha kostføring
8.12  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
8.13  * 800   041024 ask  Lagt inn ny funksjonalitet for føring varekost i regnskap
8.14  * 800   180225 ask  Omskrevet for å sikre riktig summering i RLOG tabeller
8.15  * 800   250225 ask  Utvidet nøkkel for oppslag på kontohenvisninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.07 fsovki1    uf   e           k disk    rename(sovkst:sovki1r)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
8.07 fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
8.06 frlobi1    uf   e           k disk    rename(rlobst:rlobi1r)
8.14 frlodi1    if   e           k disk    rename(rlodst:rlodi1r)
     ffovfpf    o  a e             disk
      *
      * Definering av Local data area
      *
     d                uds
8.01 d l_tmst                751    776
8.11 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
8.07 d sovki1_vopd     s                   like(sovopd)
8.07 d sstal1_paar     s                   like(sfpaar)
8.07 d sstal1_binr     s                   like(sfbinr)
8.07 d sstal1_line     s                   like(sfline)
     d votyl1_otyp     s                   like(vaotyp)
     d vkhvl1_kkod     s                   like(vakkod)
8.15 d vkhvl1_klty     s                   like(vaklty)
8.15 d vkhvl1_koty     s                   like(vakoty)
8.06 d rlobi1_bts1     s                   like(rlbts1)
8.14 d rlodi1_dts1     s                   like(rldts1)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
     d w_peri          s                   like(ffperi)
     d w_belp          s                   like(sfsumk)
8.13 d w_kost          s                   like(sfsumk)
8.02 d w_kund          s                   like(sfvkun)
8.04 d w_fdat          s                   like(fffdat)
8.04 d w_bunt          s                   like(ffbunt)
8.01 d w_logg          s               z
8.01 d w_lintel        s              8  0
8.01 d w_dumkki        s              8  0
8.01 d w_dumkkk        s              1  0
8.03 d w_dumd20        s              2  0
8.01 d w_dumaki        s             25
8.01 d w_dumpru        s              6
8.06 d w_dsum          s                   like(rlbsd1)
8.06 d w_ksum          s                   like(rlbsk1)
8.06 d w_antd          s                   like(rlban1)
8.06 d w_antk          s                   like(rlban1)
8.09 d w_vtyp          s              1
8.13 d w_ktyp          s              1
      *
     d b_feil          s              1    inz(*off)
     d b_negp          s              1    inz(*off)
8.11 d u_alle          s              1    inz(*off)
      *
8.11   dcl-s co402_verdi1  char(1);
8.11   dcl-s co402_verdi2  char(2048);
8.11   dcl-pr co402r extpgm('CO402R');
8.11     p_filgrp            char(3)     const;
8.11     p_firm              packed(3:0) const;
8.11     p_lager             packed(2:0) const;
8.11     p_nokkel            char(50)    const;
8.11     p_verdi1            char(1);
8.11     p_verdi2            char(2048);
8.11   end-pr;
8.01  *
8.01 c/Exec SQL
8.01 c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
8.01 c+             commit=*none
8.01 c/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.07  *
8.07  * Leser parameterfil for oppslag mot statistikk
8.07  *
8.07 c     sovki1_key    reade     sovki1
8.07 c                   dow       not %eof
8.07 c                   eval      b_feil = *off
      *
      * Leser salgsstatistikk, og danner kostpristransaksjoner
      *
8.07 c                   eval      sstal1_paar = sovaar
8.07 c                   eval      sstal1_binr = sovbil
8.07 c                   eval      sstal1_line = sovbli
8.07 c     sstal1_key    chain     sstal1r
8.07 c                   if        not %found
8.07 c                   eval      b_feil = *on
8.07 c                   goto      mrk_stat
8.07 c                   endif
      *
8.05  * Hopper over statistikkposter fra butikk
8.05  *
8.05 c                   if        sfbong <> ' '
8.05 c                   goto      les_neste
8.05 c                   endif
8.09  *
8.11 c                   if        u_alle = *off
8.09 c                   exsr      sjekk_vtyp
8.09 c                   if        b_feil = *on
8.09 c                   goto      mrk_stat
8.09 c                   endif
8.11 c                   endif
8.05  *
     c                   exsr      sjekk_otyp
     c                   if        b_feil = *on
     c                   goto      mrk_stat
     c                   endif
      *
     c                   exsr      finn_kost
     c                   if        b_feil = *on
     c                   goto      mrk_stat
     c                   endif
      *
8.02 c                   if        sffkun <> sfvkun and
8.02 c                             sffkun <> 0
8.02 c                   eval      w_kund = sffkun
8.02 c                   else
8.02 c                   eval      w_kund = sfvkun
8.02 c                   endif
8.02  *
     c                   exsr      bygg_post
      *
      * Merker salget med at det er rapportert
      * Merking: 1=Trans ok 9=Utlegg av postering feilet
      *
     c     mrk_stat      tag
     c                   if        b_feil = *on
8.07 c                   eval      sovopd = '9'
     c                   else
8.07 c                   eval      sovopd = '1'
     c                   endif
      *
8.07 c                   update    sovki1r
      *
8.05 c     les_neste     tag
8.07 c     sovki1_key    reade     sovki1
      *
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
8.06 c                   exsr      upd_bilag
8.10 c                   exsr      upd_bunt
8.01 c                   eval      l_tmst = ' '
     c                   eval      *inlr = *on
     c                   return
8.09  *
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  *  Sjekker varetype - bare lagervarer skal danne kostpostering
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  *
8.09 c     sjekk_vtyp    begsr
8.09
            w_vtyp = ' ';
8.09        exec sql
8.09           select vlvtyp
8.09              into :w_vtyp
8.09            from vlagpf
8.09             where vlfirm = :sffirm and
8.09                   vlvare = :sfvare and
8.09                   vllage = :sflage;
8.09        if sqlcod = 0;
8.09         if w_vtyp <> 'L';
8.09            b_feil = *on;
8.09         endif;
8.09        else;
8.09         exec sql
8.09           select vvvtyp
8.09              into :w_vtyp
8.09            from vvarpf
8.09             where vvfirm = :sffirm and
8.09                   vvvare = :sfvare;
8.09        if sqlcod < 0 or
8.09           w_vtyp <> 'L';
8.09            b_feil = *on;
8.09         endif;
8.09        endif;
8.09
8.09  *
8.09 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  *  Sjekker ordretype på statistikkpost negativ ordretype eller negativt beløp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_otyp    begsr
      *
     c                   eval      b_negp = *off
     c                   eval      votyl1_otyp = sfotyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaokre = '-'
     c                   eval      b_negp = *on
     c                   endif
     c                   else
     c                   eval      b_feil = *on
     c                   endif
8.08  *
8.08 c                   if        sfsumk < 0
8.08 c                   eval      b_negp = *on
8.08 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finner aktuell kostpris på varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kost     begsr
      *
8.13 c                   if        w_ktyp = 'G'
8.13 c                   eval      w_kost = sfgjkp
8.13 c                   else
8.13 c                   eval      w_kost = sfsumk
8.13 c                   endif
8.13 c                   if        w_kost = 0
8.13 c                   eval      b_feil = *on
8.13 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Bygger kostpris posteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_post     begsr
      *
      * Hent kostkonto fra kontohenvisning
      *
     c                   eval      vkhvl1_kkod = sfkhev
8.15 c                   eval      vkhvl1_koty = sfotyp
     c     vkhvl1_key    chain     vkhvl1
8.15 c                   if        not %found(vkhvl1)
8.15 c                   eval      vkhvl1_koty = ' '
8.15 c     vkhvl1_key    chain     vkhvl1
8.15 c                   endif
      *
      * Redigerer periode fra statistikk
      *
     c                   eval      w_peri = %dec(%char(sfpmnd) +
     c                                      %subst(%char(sfpaar):3:2):4:0)
      *
      * Fører salgskost postering
      *
     c                   eval      ffdavd = 0
     c                   eval      ffdkto = 0
     c                   eval      ffdsps = 0
     c                   eval      ffcavd = 0
     c                   eval      ffckto = 0
     c                   eval      ffcsps = 0
      *
     c                   if        b_negp = *on
     c                   eval      ffcavd = sfsavd
     c                   eval      ffckto = vgkkos
     c                   else
     c                   eval      ffdavd = sfsavd
     c                   eval      ffdkto = vgkkos
     c                   endif
8.13 c                   eval      w_belp = %abs(w_kost)
     c                   exsr      skriv_post
      *
      * Fører salg motpost
      *
     c                   eval      ffdavd = 0
     c                   eval      ffdkto = 0
     c                   eval      ffdsps = 0
     c                   eval      ffcavd = 0
     c                   eval      ffckto = 0
     c                   eval      ffcsps = 0
      *
     c                   if        b_negp = *on
     c                   eval      ffdavd = sfsavd
     c                   eval      ffdkto = vakko3
     c                   else
     c                   eval      ffcavd = sfsavd
     c                   eval      ffckto = vakko3
     c                   endif
8.13 c                   eval      w_belp = %abs(w_kost)
     c                   exsr      skriv_post
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriver hovedboks postering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_post    begsr
      *
8.04 c                   exsr      hent_forfall

     c                   eval      fffirm = w_firm
     c                   eval      ffperi = w_peri
8.04 c                   eval      ffbunt = w_bunt
     c                   eval      ffbiln = sfbinr
     c                   eval      ffbill = sfline
     c                   eval      ffbdat = sfbida
8.04 c                   eval      fffdat = w_fdat
     c                   eval      ffbilk = vgkbik
     c                   eval      fftext = 'Faktura ' + %char(sfbinr) +
     c                                      '/' + %char(sfline)
     c                   eval      ffdmom = '0'
     c                   eval      ffbelp = w_belp
     c                   eval      ffcmom = '0'
     c                   eval      ffproj = *blank
     c                   eval      ffarbo = *blank
     c                   eval      ffakti = *blank
     c                   eval      ffkref = 0
8.02 c                   eval      ffkund = w_kund
     c                   eval      ffvalk = *blank
     c                   eval      ffvalb = 0
     c                   eval      ffmngd = 0
     c                   eval      ffautx = *blank
     c                   eval      ffmvag = 0
      *
     c                   write     fovfpfr
8.01 c                   exsr      logg_linjer
      *
     c                   endsr
8.01  *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  * Legger ut posteringstrans til loggfil linjer
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01   begsr logg_linjer;
8.01
8.01     w_lintel = w_lintel + 1;
8.01     w_dumkki = 0;
8.01     w_dumkkk = 0;
8.03     w_dumd20 = 0;
8.01     w_dumaki = '  ';
8.01     w_dumpru = '  ';
8.01
8.01        exec sql
8.01        insert into rlodst
8.01          (rldfir,rldtyp,rldts1,rldlin,rldper,
8.01           rldbun,rldbin,rldbli,rldbda,rldfda,
8.01           rldbko,rldbtx,rlddav,rlddko,rlddsp,
8.01           rlddmv,rldbel,rldmvg,rldkav,rldkko,
8.01           rldksp,rldkmv,rldpro,rldpra,rldpru,
8.01           rldpao,rldkrf,rldres,rldlki,rldkki,
8.01           rldkkk,rldaki,rldvko,rldvbe,rldmko,
8.03           rldmen,rldsal,rldsfa,rldtty,rldbet,rldrem)
8.01        values(:fffirm,'FAKT',:w_logg,:w_lintel,
8.01               :ffperi,:ffbunt,:ffbiln,:ffbill,
8.01               :ffbdat,:fffdat,:ffbilk,:fftext,
8.01               :ffdavd,:ffdkto,:ffdsps,:ffdmom,
8.01               :ffbelp,:ffmvag,:ffcavd,:ffckto,
8.01               :ffcsps,:ffcmom,:ffproj,:ffakti,
8.01               :ffupro,:ffarbo,:ffkref,:ffkund,
8.01               :ffkidi,:ffkidr,:ffksif,:ffkid2,
8.01               :ffvalk,:ffvalb,:ffmngk,:ffmngd,
8.03               :ffautx,' ',' ',:w_dumd20,' ');
8.01
8.01   endsr;
8.04  *
8.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  * Henter forfallsdato fra tidligere transer på dette bilagsnummer
8.04  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.04  *
8.04   begsr hent_forfall;
8.04
8.04     w_fdat = *loval;
8.04
8.14 c                   eval      rlodi1_dts1 = w_logg
8.14 c     rlodi1_key    setll     rlodi1
8.14 c     rlodi1_key    reade     rlodi1
8.14 c                   dow       not %eof(rlodi1)
8.14 c                   if        rldbin = sfbinr
8.14 c                   eval      w_fdat = rldfda
8.14 c                   eval      w_bunt = rldbun
8.14 c                   leave
8.14 c                   endif
8.14 c     rlodi1_key    reade     rlodi1
8.14 c                   enddo
8.04
8.04   endsr;
8.06  *
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  * Oppdaterer bilagssummer etter utlegg av kosttranser
8.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.06  *
8.06 c     upd_bilag     begsr
8.06  *
8.06 c                   eval      rlobi1_bts1 = w_logg
8.06 c     rlobi1_key    setll     rlobi1
8.06 c     rlobi1_key    reade     rlobi1
8.06 c                   dow       not %eof
8.14 c                   exsr      sum_bilag
8.06 c                   eval      rlbsd1 = w_dsum
8.06 c                   eval      rlbsk1 = w_ksum
8.06 c                   eval      rlban1 = w_antd + w_antk
8.06 c                   update    rlobi1r
8.06 c     rlobi1_key    reade     rlobi1
8.06 c                   enddo
8.06  *
8.06 c                   endsr
8.14  *
8.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.14  * Summerer poster på bilag
8.14  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.14  *
8.14   begsr sum_bilag;
8.14
8.14     w_dsum = 0;
8.14     w_ksum = 0;
8.14     w_antd = 0;
8.14     w_antk = 0;
8.14  *
8.14 c                   eval      rlodi1_dts1 = w_logg
8.14 c     rlodi1_key    setll     rlodi1
8.14 c     rlodi1_key    reade     rlodi1
8.14 c                   dow       not %eof(rlodi1)
8.14 c                   if        rldbin = rlbbil
8.14 c                   if        rlddko > 0
8.14 c                   eval      w_dsum = w_dsum + rldbel
8.14 c                   eval      w_antd = w_antd + 1
8.14 c                   endif
8.14 c                   if        rldkko > 0
8.14 c                   eval      w_ksum = w_ksum + rldbel
8.14 c                   eval      w_antd = w_antd + 1
8.14 c                   endif
8.14 c                   endif
8.14 c     rlodi1_key    reade     rlodi1
8.14 c                   enddo
8.06   endsr;
8.10  *
8.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.10  * Oppdaterer bunt etter utlegg av kosttranser
8.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.10  *
8.10 c     upd_bunt      begsr
8.10  *
8.14     w_dsum = 0;
8.14  *
8.14 c                   eval      rlodi1_dts1 = w_logg
8.14 c     rlodi1_key    setll     rlodi1
8.14 c     rlodi1_key    reade     rlodi1
8.14 c                   dow       not %eof(rlodi1)
8.14 c                   eval      w_dsum = w_dsum + rldbel
8.14 c     rlodi1_key    reade     rlodi1
8.14 c                   enddo
8.10
8.10        exec sql
8.10          update rlohst
8.10            set  rlhbs1 = :w_dsum
8.10            where rlhts1 = :w_logg;
8.10  *
8.10 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.07  *
8.07  * Key for les på parameterfil
8.07  *
8.07 c     sovki1_key    klist
8.07 c                   kfld                    w_firm
8.07 c                   kfld                    sovki1_vopd
      *
      * Key for les på salgsstatistikk
      *
8.07 c     sstal1_key    klist
8.07 c                   kfld                    w_firm
8.07 c                   kfld                    sstal1_paar
8.07 c                   kfld                    sstal1_binr
8.07 c                   kfld                    sstal1_line
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      * Key for oppslag på Ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for kontohenvisninger
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
8.15 c                   kfld                    vkhvl1_klty
8.15 c                   kfld                    vkhvl1_koty
      *
      * Hent kostkonto salg og bilagskode
      *
     c                   eval      w_firm = l_firm
     c     vgkkir_key    chain     vgkkir
8.06  *
8.06  * Key for posisjonering på transaksjonslogg bilag
8.06  *
8.06 c     rlobi1_key    klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    rlobi1_bts1
8.14  *
8.14  * Key for posisjonering på transaksjonslogg linjer
8.14  *
8.14 c     rlodi1_key    klist
8.14 c                   kfld                    w_firm
8.14 c                   kfld                    rlodi1_dts1
8.01
8.01  // Bygger timestamp for logging - finner sist brukte linjenummer på bunt
8.01
8.01       w_logg = %timestamp(l_tmst);
8.01
8.01       exec sql
8.01          select linjenr into :w_lintel
8.01             from rlodst
8.01             where tid_dannet = :w_logg
8.01             order by linjenr desc
8.01           fetch first row only;
8.11  *
8.13  * Sjekk om det skal kjøres kostføring på alle varer og hvilken kostpris
8.11  *
8.13   CallP CO402R(l_filg:w_firm:0:'POST_KOST':
8.11                    co402_verdi1:co402_verdi2);
8.11   if  %subst(co402_verdi2:1:1) <> *blank;
8.11     u_alle = *on;
8.11   endif;
8.13   w_ktyp = %subst(co402_verdi2:2:1);
      *
      * Bygger nøkkel og setter startpunkt
      *
8.07 c                   eval      sovki1_vopd = ' '
8.07 c     sovki1_key    setll     sovki1
8.07  *
     c                   endsr
