     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AN020R
      * Beskrivelse . . : Generering av reorgansieringsjobb
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                        Sign.
      * --------- ------  ------------------------------------------------
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fapgmpf    ip   f  519        disk
     fqcllesrc  o  a f  096        disk
      *
      * Definering av tabeller
      *
     d rec             s             80    dim(7) ctdata perrcd(1)              Programlinjer
     d bxx             s              1    dim(10)                              Bibliotek
     d fxx             s              1    dim(10)                              Register
     d fra             s              1    dim(30)                              Hjelpe-array
      *
      * Definering av data-strukturer
      *
     d                 ds
     d d_dato                         6  0
     d  d_aaar                        2  0 overlay(d_dato)
     d  d_mmnd                        2  0 overlay(d_dato:3)
     d  d_ddag                        2  0 overlay(d_dato:5)
      *
     d                 ds
     d d_recc                        84
     d  d_flt1                       28    overlay(d_recc)
     d  d_flt2                       56    overlay(d_recc:29)
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d n               s              2  0
     d y               s              2  0
     d z               s              2  0
     d w_dato          s              6  0
     d w_recc          s             84
     d w_sys1          s              2
     d w_tell          s              6  0
     d w_bibl          s                   like(inbibl)
     d w_file          s                   like(infile)
      *
     iapgmpf    ns  01
     i                                 14   14  inbib1
     i                                 14   23  inbibl
     i                                 24   24  infil1
     i                                 24   33  infile
     i                                 34   41  inkode
     i                                 42   51  intype
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legg ut records til Cl-program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger ut første del av CL-programmet
      *
     c   01
     cann89              for       n = 1 to 4
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   eval      *in89 = *on
     c                   endfor
      *
      * Danner SUBRUTINE for reorgansiering av registre
      *
     c                   if        inbibl = 'QS36F'
     c                   goto      start
     c                   endif
      *
     c                   if        inbib1 = 'Q'
     c                   goto      slutt
     c                   endif
     c                   if        inbib1 = 'Æ'
     c                   goto      slutt
     c                   endif
      *
     c                   if        infil1 = 'Q'
     c                   goto      slutt
     c                   endif
     c                   if        infil1 = 'Æ'
     c                   goto      slutt
     c                   endif
      *
     c     start         tag
      *
     c                   if        inkode <> '*FILE'
     c                   goto      slutt
     c                   endif
      *
     c                   if        intype <> 'PF'
     c                   goto      slutt
     c                   endif
      *
     c                   add       100           w_tell
     c                   eval      w_bibl = inbibl
     c                   eval      w_file = infile
     c                   exsr      xsubr
     c                   except    xrecut
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
      *
      * Legger ut siste del av CL-programmet
      *
     clr                 for       n = 6 to 7
     clr                 add       100           w_tell
     clr                 movel     rec(n)        w_recc
     clr                 except    xrecut
     clr                 endfor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Danner rutine for BACKUP av biblioteker
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsubr         begsr
      *
     c                   eval      z = *zero
     c                   eval      fra = *blank
     c                   movel     rec(05)       w_recc
     c                   eval      d_recc = w_recc
      *
     c                   movea     w_bibl        bxx
      *
     c                   for       y = 1 to 10
     c                   if        bxx(y) <> *blank
     c                   add       1             z
     c                   eval      fra(z) = bxx(y)
     c                   endif
     c                   endfor
      *
     c                   add       1             z
     c                   eval      fra(z) = '/'
      *
     c                   movea     w_file        fxx
      *
     c                   for       y = 1 to 10
     c                   if        fxx(y) <> *blank
     c                   add       1             z
     c                   eval      fra(z) = fxx(y)
     c                   endif
     c                   endfor
      *
     c                   add       1             z
     c                   eval      fra(z) = ')'
      *
     c                   movea     fra           d_flt2
     c                   eval      w_recc = d_recc
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
     c                   eval      w_recc = *blank
     c                   eval      w_tell = *zero
     c                   eval      w_dato = *zero
     c                   eval      n = *zero
     c                   eval      y = *zero
     c                   eval      z = *zero
     c                   eval      w_sys1 = *blank
      *
     c                   eval      d_aaar = uyear
     c                   eval      d_mmnd = umonth
     c                   eval      d_ddag = uday
     c                   eval      w_dato = d_dato
      *
     c                   endsr
      *
     oqcllesrc  eadd         xrecut
     o                       w_recc              96
     o                       w_tell               6
     o                       w_dato              12
**
             PGM
             MONMSG     MSGID(CPF2981)
             MONMSG     MSGID(CPF3135)
/*                                                                   */
             RGZPFM    FILE(1234567890/1234567890)
/*                                                                   */
             ENDPGM
