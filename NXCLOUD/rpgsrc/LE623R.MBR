      /TITLE  ASLAGR: Utskrift av diversevarer, telling
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Program . . . . : LE623R                                       *
      *  Beskrivelse . . : Utskrift av diversevarer, telling            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *
     flsorl2    ip   e           k disk    rename(lsorpfr:lsorl2r)
      *
     fle623p    o    e             printer
      *
      *   Local Data Area
      *
     d                uds
     d l_ruti                800    809
      *
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      * Definering av datastrukturer
      *
     d                 ds
      *                                                      Parameter-inn
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fogr                        2  0 overlay(d_prec:11)
     d  d_togr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_thgr                        2  0 overlay(d_prec:17)
     d  d_fugr                        3  0 overlay(d_prec:19)
     d  d_tugr                        3  0 overlay(d_prec:22)
     d  d_fvar                       15    overlay(d_prec:25)
     d  d_tvar                       15    overlay(d_prec:40)
     d  d_flag                        2  0 overlay(d_prec:55)
     d  d_tlag                        2  0 overlay(d_prec:57)
     d  d_sort                        1  0 overlay(d_prec:59)
      *
      *   Definering av varible
      *
     d w_parm          s            128
      *
     ilsorl2r       01
     i                                          lwfirm        l8
     i                                          lwlage        l7
     i                                          lwvare        l2
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av opplysninger                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   if        *inl8 = *on
     c                   exsr      l8inn
     c                   endif
     c                   if        *inl7 = *on
     c                   exsr      l7inn
     c                   endif
     c                   if        *inl2 = *on
     c                   exsr      l2inn
     c                   endif
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av LAGER-RECORD-Opplysninger                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legger inn data i utskrifts-felter
      *
     c                   eval      d1vare = lwvare
     c                   eval      d1lage = lwlage
     c                   eval      l2vare = lwvare
     c                   eval      d1tek1 = lwtek1
     c                   eval      d1spny = lwspny
     c                   eval      d1kpny = lwkpny
     c                   eval      d1antt = lwantt
     c                   eval      d1enh1 = lwenh1
     c                   eval      d1numm = lwnumm
      *
     c                   eval      d1tots = lwspny * lwantt
     c                   eval      d1totk = lwkpny * lwantt
      *
      *  Nullstiller felter dersom antall ikke er utfylt
      *
     c                   if        d1antt = *zero
     c                   eval      d1enh1 = *blank
     c                   endif
      *
      *  Akkumulerer til totalfelter
      *
     c                   eval      l2antt = l2antt + d1antt
     c                   eval      l2tots = l2tots + d1tots
     c                   eval      l2totk = l2totk + d1totk
      *
     c                   eval      l7tots = l7tots + d1tots
     c                   eval      l7totk = l7totk + d1totk
      *
     c                   eval      l8tots = l8tots + d1tots
     c                   eval      l8totk = l8totk + d1totk
      *
      *  Skriver ut diverse-varer
      *
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   goto      slutt
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     slutt         tag
      *
     c*2                 if        *in72 = *on
     cl2                 exsr      l2ut
     c*2                 endif
     c*7                 if        *in76 = *on
     cl7                 exsr      l7ut
     c*7                 endif
     cl8                 exsr      l8ut
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  l8IN      Behandling av brudd på l8 inn  (Firma)               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     l8inn         begsr
      *
      *  Nullstiller totaler
      *
     c                   eval      l8tots = *zero
     c                   eval      l8totk = *zero
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  l7IN      Behandling av brudd på l7 inn  (Lager    )           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     l7inn         begsr
      *
      *  Nullstiller totaler
      *
     c                   eval      l7tots = *zero
     c                   eval      l7totk = *zero
      *
      *  Skriver ny heading for nytt lager
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  L2IN      Behandling av brudd på L2 inn  (Varenummer)          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     l2inn         begsr
      *
      *  Nullstiller totaler
      *
     c                   eval      l2antt = *zero
     c                   eval      l2tots = *zero
     c                   eval      l2totk = *zero
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  L2UT      Behandling av brudd på L2 ut   (Diversevarenummer)   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     l2ut          begsr
      *
      *  Skriver total for L2
      *
     c                   if        l2antt <> *zero
     c                             or l2tots <> *zero
     c                             or l2totk <> *zero
     c                   write     l2tot                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  L7UT      Behandling av brudd på l7 ut   (Lager)               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     l7ut          begsr
      *
      *  Legger inn opplysninger i utfelter
      *
     c                   eval      l7lage = lwlage
      *
      *  Skriver total for l7
      *
     c                   write     l7tot                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  L8UT      Behandling av brudd på l8 ut   (Firma)               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     l8ut          begsr
      *
      *  Legger inn opplysninger i utfelter
      *
     c                   eval      l8firm = lwfirm
      *
      *  Skrive totalsum
      *
     c                   write     l8tot                                30
     c                   if        *in30 = *on
     c                   exsr      ovflow
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  OVFLOW    Behandling av overflow                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     ovflow        begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
      *  Behandling av parameter-felter
      *
     c                   eval      d_prec = w_parm
      *
      *  Legger inn opplysninger i utfelter
      *
     c                   eval      a1wsid = l_wsid
     c                   eval      a1user = l_user
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
      *
     c                   eval      a1date = udate
     c                   time                    a1time
      *
     c                   endsr
