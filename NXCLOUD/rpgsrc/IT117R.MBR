     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT117R
      * Beskrivelse . . : Oppdaterer selgere fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260214 ask  Nytt program
7.01  * 7.01  070121 ask  Fjernet nuyllstilling av felt RAIRSK
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra09lu    uf a e           k disk    rename(ra09pfr:ra09lur)
      *
      * Parameter-inn
      *
     d p_firm          s                   like(raifir)
     d p_comd          s             20
     d p_parm          s           2048
     d p_user          s             10
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d ra09lu_ikod     s                   like(raikod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds          2048
     d  d_ikod                        4
     d  d_besk                       20
     d  d_mail                       50
     d  d_mobn                       11
      *
      * Definering av variable
      *
     d w_firm          s                   like(raifir)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_ikod          s                   like(raikod)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - oppdaterer informasjon på ikod., setter statusflagg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Redigerer parameterdata før behandling
      *
     c                   eval      d_parm = p_parm
     c                   exsr      red_num
     c                   exsr      red_flt
      *
      * Velger oppdatering avhengig av kommando
      *
     c                   select
     c                   when      p_comd = 'INSERT'
     c                   exsr      nyrec
     c                   when      p_comd = 'UPDATE'
     c                   exsr      oppdrec
     c                   when      p_comd = 'DELETE'
     c                   exsr      sletrec
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprette kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nyrec         begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra09lu_ikod = w_ikod
     c     ra09lu_key    chain     ra09lu
     c                   if        not %found
     c                   eval      raifir = w_firm
     c                   eval      raikod = w_ikod
     c                   eval      raitxt = d_besk
     c                   eval      raikto = 0
7.01 c*                  eval      rairsk = 0
     c                   eval      raimva = 0
     c                   eval      raiprk = 0
     c                   eval      raiblk = 0
     c                   eval      raisa2 = 0
     c                   eval      raisa3 = 0
     c                   eval      raisa4 = 0
     c                   eval      raisa5 = 0
     c                   eval      raiema = d_mail
     c                   eval      raimob = d_mobn
     c                   eval      raidat = w_date
     c                   eval      raitim = w_time
     c                   eval      raiusr = p_user
     c                   write     ra09lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatere kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra09lu_ikod = w_ikod
     c     ra09lu_key    chain     ra09lu
     c                   if        %found
     c                   eval      raitxt = d_besk
     c                   eval      raiema = d_mail
     c                   eval      raimob = d_mobn
     c                   eval      raidat = w_date
     c                   eval      raitim = w_time
     c                   eval      raiusr = p_user
     c                   update    ra09lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Slett kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sletrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra09lu_ikod = w_ikod
     c     ra09lu_key    chain     ra09lu
     c                   if        %found
     c                   delete    ra09lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Bytter ut blanke med nuller i numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_num       begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Redigerer numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_flt       begsr
      *
      * Kodefelt
     c                   eval      w_ikod = %dec(d_ikod:4:0)
      *
     c     end_flt       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_comd
     c                   parm                    p_parm
     c                   parm                    p_user
     c                   parm                    p_stat
      *
      * Key for posisjonering på valgt kode
      *
     c     ra09lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09lu_ikod
      *
      * Setter "timestamp" for postoppdateringer
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
