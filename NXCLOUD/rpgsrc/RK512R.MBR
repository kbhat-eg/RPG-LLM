     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RK512R
      * Beskrivelse . . : Kundeposteringer, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130802 KOB  Nytt program
T5.00 *       131102 HFL  Restbeløp i parameterlista.
      *       300403 KOB  Lagt inn F2=Notatark
      *       300403 KOB  Endret blafunksjon slik at oppgitt periode
      *                   angir posisjonerning
      *       210803 KOB  Lagt inn ny index for å lese åpne poster
      *       051003 KOB  Lagt inn ny funksjon for å vise hele bilaget
      *       121103 KOB  Lagt inn ny funksjon for å lese motpost
      *       150304 KOB  Endret slik at ved valg av alle poster vises
      *                   kun poster for inneværende regnskapsår
      * R5.30 040205 HFL  Lagt inn merke når notatblokk på kunden
T5.40 * R5.40 150605 HFL  Leser RKLAL2 i stedet for RKLALU.
T5.40 * R5.40 250705 HFL  Notatark på postnivå.
      * R5.40 251005 KOB  Lagt inn ny funksjon for å lese arkiv
T5.40 * R5.40 200106 HFL  Etter Vis motpost og vis bilag oppsto av
      *                   og til "Decimal data error" på B2KUND
T5.40 * R5.50 220107 KOB  Endret slik at år/periode som er valgt beholdes ved bi
T5.40 * R5.50 120307 KOB  Endret slik at memosaldo legges ut med mva (Hardkodet
      *                   dersom kunden er avgiftspliktig
T5.50 * R5.50 240907 HFL  0-stiller noen tellere.
5.60  * R5.60 130308 HFL  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
T5.40 * R5.60 200308 KOB  Endret slik at RK513R kalles med bilagsnr.
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20     140611  Memosaldo lagt ut på eget register             BSA  *
6.21  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.21  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.22  * R6.20     301112  Endret oppslag mot arkiv                       bsa  *
6.23  * R6.20     040113  Endret logikk på refnr                         bsa  *
6.24  * R6.10     260313  Henter dokumenttype basert på bilagskode       bsa  *
6.25  * R6.20     030113  Notatblokk ble ikke vist                       nho  *
7.01  * 7.00  030120 bsa  Endret måten å bygge opp streng. Samkjøres med RK512R
7.01  *                   Reverserer logikk rundt 6.22.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frktrl9    if   e           k disk    rename(rktrpfr:rktrl9r)
     frktrl8    if   e           k disk    rename(rktrpfr:rktrl8r)
     f                                     prefix(xn:2)
     frktrl2    if   e           k disk    rename(rktrpfr:rktrl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     frktrlu    if   e           k disk    rename(rktrpfr:rktrlur)
E5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
E5.40frklal3    if   e           k disk    rename(rklapfr:rklal3r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
      *
     frk512d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rktrl9_kund     s                   like(rnkund)
     d rktrl8_kund     s                   like(rnkund)
     d rktrl8_biln     s                   like(rnbiln)
     d rktrl2_kund     s                   like(rnkund)
     d rktrl2_bdat     s                   like(rnbdat)
     d rktrl2_tist     s                   like(rntist)
     d rkunl1_kund     s                   like(rnkund)
     d rktrlu_kund     s                   like(rnkund)
     d rktrlu_bdat     s                   like(rnbdat)
     d rktrlu_tist     s                   like(rntist)
E5.40d rklal2_syst     s                   like(rxsyst)
E5.40d rklal2_pros     s                   like(rxpros)
E5.40d rklal2_akti     s                   like(rxakti)
E5.40d rklal2_kund     s                   like(rxkont)
      *
T5.40d rklal3_syst     s                   like(rxsyst)
T5.40d rklal3_pros     s                   like(rxpros)
T5.40d rklal3_akti     s                   like(rxakti)
T5.40d rklal3_kund     s                   like(rxkont)
T5.40d rklal3_spes     s                   like(rxspes)
T5.40d rklal3_avde     s                   like(rxavde)
T5.40d rklal3_tist     s                   like(rxtist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rnfirm)
     d w_biln          s                   like(rnbiln)
T5.00d w_rest          s                   like(rnrest)
     d w_kund          s                   like(rnkund)
     d w_3ka1          s                   like(ra3ka1)
     d w_3ka2          s                   like(ra3ka2)
     d w_3ka3          s                   like(ra3ka3)
6.22 d w_type          s             50
     d w_valg          s              1
     d w_valg_a        s              1    inz(*off)
     d w_valg_r        s              1  0 inz(1)
     d w_valg_v        s              1    inz(*off)
      *
     d w_dato          s                   like(rnbdat)
     d w_bdat          s                   like(rnbdat)
     d w_bel0          s                   like(rnbelØ)
     d w_bel1          s                   like(rnbelØ)
     d w_bel2          s                   like(rnbelØ)
     d w_bel3          s                   like(rnbelØ)
     d w_bel4          s                   like(rnbelØ)
     d w_beln          s                   like(rnbelØ)
     d w_fodag         s              5  0
     d w_krdag         s              5  0
     d w_tran          s              7  0
     d w_dagr          s              7  0
     d w_sald          s                   like(rnbelØ)
     d w_inka          s                   like(rnbelØ)
     d w_raar          s                   like(rnraar)
     d w_rper          s                   like(rnrper)
6.22 d w_refn          s             50
6.24 d w_dtyp          s             50
6.24 d w_year          s              1
6.24 d w_ok            s              1
      *
pdf  d*w_modu          s             10    inz('MSA     ')
pdf  d w_modu          s             10    inz('PROA    ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
T5.40d p_kund          s                   like(rnkund)
      *
      * Definering av parametre MultiArcive
      *
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
      *
      * Parametre til RS205R - hent mva %
      *
5.50 d w_stat          s              1
5.50 d w_mvak          s              1
5.50 d w_mvtx          s             30
5.50 d w_mvas          s              6  2
5.50 d w_mvat          s              5  4
5.50 d w_mvaf          s             10  9
5.50 d w_bpro          s              5  2
      *
      *
     d p_stat          s              1
     d p_akod          s                   like(rnbilk)
     d p_gkod          s                   like(rkavde)
     d p_ikod          s                   like(rkslgr)
     d p_atxt          s             20
     d p_gtxt          s             20
     d p_itxt          s             20
     d p_raar          s                   like(rnraar)
     d p_biln          s                   like(rnbiln)
      *
T510  * Parameter - program RF020R
     i/COPY QCPYLESRC,RF020PAR
6.20 d*                ds
6.20 d*rxulda                       175
6.20 d* rxstat                        1    overlay(rxulda)
6.20 d* rxfirm                        3  0 overlay(rxulda:2)
6.20 d* rxsyst                        1    overlay(rxulda:5)
6.20 d* rxpros                        6  0 overlay(rxulda:6)
6.20 d* rxakti                        3    overlay(rxulda:12)
6.20 d* rxf001                        3    overlay(rxulda:15)
6.20 d* rxkont                        6  0 overlay(rxulda:18)
6.20 d* rxspes                        4  0 overlay(rxulda:24)
6.20 d* rxavde                        4  0 overlay(rxulda:28)
6.20 d* rxlinj                        4  0 overlay(rxulda:32)
6.20 d* rxtgrp                        3    overlay(rxulda:36)
6.20 d* rxtext                       78    overlay(rxulda:39)
6.20 d* rxf002                       13    overlay(rxulda:117)
6.20 d* rxvalg                        1  0 overlay(rxulda:130)
6.20 d* rxtist                         z   overlay(rxulda:131)
6.20 d* rxbiln                        6  0 overlay(rxulda:158)
6.20 d* rxrper                        2  0 overlay(rxulda:164)
6.20 d* rxraar                        4  0 overlay(rxulda:166)
6.20 d* rxonr                         6  0 overlay(rxulda:170)
T510 d rxupar          s                   like(rxulda)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
T5.30 * Finnes notatblokk på kunden?
      *
E5.40c                   eval      rklal2_syst = 'K'
6.25 c**                 eval      rklal2_pros = *zeros
6.25 c                   eval      rklal2_pros = *blanks
E5.40c                   eval      rklal2_akti = *blanks
E5.40c                   eval      rklal2_kund = b2kund
E5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.30c                   if        *in94 = *on
T5.30c                   eval      b2notb = 'NOTATARK'
T5.30c                   endif
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
T510 c                   when      *inkb
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
T510 c                   exsr      notat
T510 c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Åpne poster/Alle poster/Akkumulert
      *
     c                   if        *inkf = *on
      *
     c                   select
     c                   when      w_valg_r = 1
     c                   eval      w_valg_r = 2
      *
     c                   when      w_valg_r = 2
     c                   eval      w_valg_r = 3
     c                   eval      rktrl2_bdat = *loval
      *
     c                   when      w_valg_r = 3
     c                   eval      w_valg_r = 1
     c                   endsl
      *
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * NOK/Valuta
      *
     c                   if        *inkg = *on
     c                   if        w_valg_v = *on
     c                   eval      w_valg_v = *off
     c                   else
     c                   eval      w_valg_v = *on
     c                   endif
      *
     c                   eval      rktrl2_bdat = *loval
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Statistikk
      *
     c                   if        *inkh = *on
      *
     c                   eval      rktrl2_kund = w_kund
     c                   eval      rktrl2_bdat = *loval
     c                   eval      rktrl2_tist = *loval
     c     rktrl2_key    setll     rktrl2
      *
     c                   exsr      xe2win
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2raar <> w_raar or
     c                             b2rper <> w_rper
      *
     c                   if        b2raar <> w_raar
     c                   eval      w_raar = b2raar
     c                   endif
      *
     c                   if        b2rper <> w_rper
     c                   eval      w_rper= b2rper
     c                   endif
      *
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rktrl9_kund = w_kund
     c                   eval      rktrl2_kund = w_kund
     c                   eval      rktrl2_bdat = *loval
     c                   eval      rktrl2_tist = *loval
     c     rktrl9_key    setll     rktrl9
     c     rktrl2_key    setll     rktrl2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
     c                   eval      rktrl9_kund = b2kund
     c     rktrl9_key    setll     rktrl9
      *
     c                   eval      rktrl2_kund = b2kund
     c     rktrl2_key    setll     rktrl2
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
      * Filen er tom
      *
     c                   if        b1biln = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      w_biln = b1biln
T5.00c                   eval      w_rest = b1rest
     c                   goto      avslutt
     c                   endif
      *
      * Vis post
      *
     c                   if        b1valg = 5
     c                   eval      *in30  = *on
     c                   eval      c2nyen = '   Vis   '
     c                   exsr      xc2win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis motpost
      *
     c                   if        b1valg = 6
     c                   eval      *in30  = *on
      *
T5.40c                   eval      p_kund = b2kund
     c                   call      'RK513R'
E5.40c                   parm                    p_kund
E5.60c                   parm                    b1biln
     c                   parm                    b1bdat
     c                   parm                    b1rper
     c                   parm                    b1raar
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis bilag
      *
     c                   if        b1valg = 7
      *
T5.40c                   eval      p_kund = b2kund
     c                   call      'RF512R'
E5.40c                   parm                    p_kund
     c                   parm                    b1biln
     c                   parm                    b1bdat
     c                   parm                    b1rper
     c                   parm                    b1raar
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise faktura
      *
     c                   if        b1valg = 8
     c                   eval      *in30  = *on
     c                   eval      p_biln = b1biln
     c                   eval      w_kund = b2kund
     c                   eval      p_raar = b1raar
     c                   call      'RK110C'
     c                   parm                    w_firm
     c                   parm                    p_raar
     c                   parm                    w_kund
     c                   parm                    p_biln
      *
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
6.24  *
6.24  * Hent dokumenttype basert på bilagskoden
6.24  *
6.24 c                   eval      w_dtyp = *blanks
6.24 c                   eval      w_year = *blanks
6.24 c                   eval      w_ok = '0'
6.24  *
6.24 c                   call      'AP632R'
6.24 c                   parm                    w_firm
6.24 c                   parm                    b1bilk
6.24 c                   parm                    w_dtyp
6.24 c                   parm                    w_year
6.24 c                   parm                    w_ok
6.24  *
6.24 c                   if        w_ok = '1'
6.24 c                   eval      w_type = w_dtyp
6.24 c                   if        w_year = 'J'
6.24 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.24 c                   else
6.24 c                   eval      w_refn = %trim(%char(b1biln))
6.24 c                   endif
6.24 c                   else
6.24  * Gammel logikk hvis den ikke finnes
      *
7.01 c                   if        b1bilk = 1 or
7.01 c                             b1bilk = 3
7.01 c                   eval      w_type = 'FAKTURA'
7.01 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
7.01 c                   elseif    b1bilk = 2 or
7.01 c                             b1bilk = 4
7.01 c                   eval      w_type = 'INNGÅENDE%20FAKTURA'
7.01 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
7.01 c                   else
7.01 c                   eval      w_type = 'DIVERSEBILAG'
7.01 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
7.01 c                   endif
6.24 c                   endif
      *
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '20'
     c                   eval      w_aara = %char(b1raar)
     c                   eval      w_bila = %char(b1biln)
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.22 c                   parm                    w_refn
6.22 c**                 parm                    w_aarr
6.22 c**                 parm                    w_bila
6.22 c**                 parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      b1rest = 0
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_valg_r = 1
     c                   read      rktrl9
     c                   else
     c                   read      rktrl2
     c                   endif
      *
     c                   if        %eof or
     c                             rnfirm <> w_firm or
     c                             rnkund <> w_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Alle poster >  dato
      *
     c                   if        w_valg_r = 2
     c                   if        rnraar < b2raar or
     c                             rnraar = b2raar and
     c                             b2rper  > *zero and
     c                             rnrper < b2rper
     c                   iter
     c                   endif
     c                   endif
      *
      * Ved valg "Valuta"
      * test om valutakode = blank
      *
     c                   if        w_valg_v = *on and
     c                             rnvalk = *blank
     c                   iter
     c                   endif
      *
      * Hvis akkumulert - les fram til valgt dato
      *
     c                   if        w_valg_r = 3 and
     c                             w_raar  > 0 or
     c                             w_rper  > 0
      *
     c                   if        rnraar < w_raar or
     c                             rnraar = w_raar and
     c                             w_rper > 0      and
     c                             rnrper < w_rper
     c                   eval      b1rest = b1rest + rnbelØ
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = rnbdat
     c                   eval      b1tist = rntist
     c                   eval      b1biln = rnbiln
     c                   eval      b1raar = rnraar
     c                   eval      b1rper = rnrper
     c     *dmy          move      rnbdat        b1bida
     c                   eval      b1bilk = rnbilk
     c                   eval      b1text = rntext
      *
     c                   if        rnrefn <> *zeros
     c                   eval      b1refn = rnrefn
     c                   else
     c                   eval      b1refn = rnsamf
     c                   endif
      *
     c     *dmy          move      rnfdat        b1fdat
      *
      * NOK
      *
     c                   if        w_valg_v = *off
     c                   eval      b1belØ = rnbelØ
      *
     c                   if        w_valg_r = 3
     c                   eval      b1rest = b1rest + rnbelØ
     c                   else
     c                   eval      b1rest = rnrest
     c                   endif
      *
     c                   endif
      *
      * Valuta
      *
     c                   if        w_valg_v = *on
     c                   eval      b1belØ = rnvalb
      *
     c                   if        w_valg_a = *off
     c                   eval      b1rest = rnvalr
     c                   else
     c                   eval      b1rest = b1rest + rnvalr
     c                   endif
      *
     c                   move      '    '        b1text
     c                   move      rnvalk        b1text
     c                   endif
      *
      * Legg på farger
      *
     c                   eval      b1inko = rninko
     c                   eval      b1antp = rnantp
     c                   eval      b1rekl = rnrekl
      *
      * Purret - blå
      *
     c                   if        b1antp > 0
     c                   eval      *in71 = *on
     c                   eval      *in70 = *off
     c                   eval      *in72 = *off
     c                   endif
      *
      * Sendt til inkasso - rød
      *
     c                   if        b1inko <> ' '
     c                   eval      *in70 = *on
     c                   eval      *in71 = *off
     c                   eval      *in72 = *off
     c                   endif
      *
      * Reklamasjon - hvit
      *
     c                   if        b1rekl <> ' '
     c                   eval      *in72 = *on
     c                   eval      *in70 = *off
     c                   eval      *in71 = *off
     c                   endif
      *
T5.40 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.40c                   eval      rklal3_syst = 'K'
6.25 c**                 eval      rklal3_pros = *zeros
6.25 c                   eval      rklal3_pros = *blanks
T5.40c                   eval      rklal3_akti = *blanks
T5.40c                   eval      rklal3_kund = b2kund
T5.40c                   eval      rklal3_spes = *zero
T5.40c                   eval      rklal3_avde = *zero
T5.40c                   eval      rklal3_tist = b1tist
T5.40c     rklal3_key    setll     rklal3                                 73
      *
     c                   write     b1sfl
      *
     c                   eval      *in70 = *off
     c                   eval      *in71 = *off
     c                   eval      *in72 = *off
T5.40c                   eval      *in73 = *off
      *
     c                   enddo
      *
      * Send melding hvis ingen poster
      *
     c                   if        w_srrn = *zero
     c                   exsr      clr_subfile
      *
     c                   if        w_valg_r = 1
     c                   eval      b1text = 'Ingen åpne poster'
     c                   else
     c                   eval      b1text = 'Ingen poster'
     c                   endif
      *
     c                   if        w_valg_v = *on
     c                   eval      b1text = 'Ingen valutaposter'
     c                   endif
      *
     c                   eval      b1biln = *zeros
     c                   eval      b1bdat = *loval
     c                   eval      b1bilk = *zeros
     c                   eval      b1refn = *zeros
     c                   eval      b1fdat = *zeros
     c                   eval      b1belØ = *zeros
     c                   eval      b1rest = *zeros
     c                   eval      w_srrn = 1
     c                   write     b1sfl
      *
     c                   endif
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for vise post                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rktrlu_kund = b2kund
     c                   eval      rktrlu_bdat = b1bdat
     c                   eval      rktrlu_tist = b1tist
     c     rktrlu_key    chain     rktrlu
      *
     c                   if        not%found
     c                   goto      end_c2bld
     c                   endif
      *
     c                   move      rnbiln        c2biln
     c                   move      rnbilk        c2bilk
     c                   move      rntext        c2text
     c     *dmy          move      rnbdat        c2bdat
     c     *dmy          move      rnfdat        c2fdat
     c                   move      rnrper        c2rper
     c                   move      rnraar        c2raar
     c                   move      rnrekl        c2rekl
     c                   move      rnjour        c2jour
     c                   move      rnrefn        c2refn
     c                   move      rnsamf        c2samf
     c                   move      rnbiln        c2biln
     c                   move      rnbelØ        c2belØ
     c                   move      rnrest        c2rest
      *
     c                   move      rnantp        c2antp
      *
     c                   if        rnpdat = *loval
     c                   eval      c2pdat = 0
     c                   else
     c     *dmy          move      rnpdat        c2pdat
     c                   endif
      *
     c                   move      rnrenk        c2renk
      *
     c                   if        rnrdat = *loval
     c                   eval      c2rdat = 0
     c                   else
     c     *dmy          move      rnrdat        c2rdat
     c                   endif
      *
     c                   move      rninko        c2inko
      *
     c                   if        rnidat = *loval
     c                   eval      c2idat = 0
     c                   else
     c     *dmy          move      rnidat        c2idat
     c                   endif
      *
     c                   move      rnvalk        c2valk
     c                   move      rnvalb        c2valb
     c                   move      rnvalr        c2valr
      *
      * Hent avdelingstekst
      *
     c                   move      rnavde        c2avde
      *
     c                   if        rnavde <> *zero
     c                   move      rnavde        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   movel     p_gtxt        c2gtxt
     c                   endif
      *
      * Hent selgertekst
      *
     c                   move      rnslgr        c2slgr
      *
     c                   if        rnslgr <> *zero
     c                   move      rnslgr        p_ikod
     c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_ikod
     c                   parm                    p_itxt
      *
     c                   movel     p_itxt        c2itxt
     c                   endif
      *
T5.40 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.40c                   eval      rklal3_syst = 'K'
6.25 c**                 eval      rklal3_pros = *zeros
6.25 c                   eval      rklal3_pros = *blanks
T5.40c                   eval      rklal3_akti = *blanks
T5.40c                   eval      rklal3_kund = b2kund
T5.40c                   eval      rklal3_spes = *zero
T5.40c                   eval      rklal3_avde = *zero
T5.40c                   eval      rklal3_tist = b1tist
T5.40c     rklal3_key    setll     rklal3                                 73
T5.40c                   if        *in73 = *on
T5.40c                   eval      c2notb = 'NOTATARK'
T5.40c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
T5.40c                   eval      *in73 = *off
      *
T5.40 * Notatblokk bilag
T5.40c                   if        *inkb
T5.40c                   eval      rxtist = rntist
T5.40c                   eval      rxbiln = rnbiln
T5.40c                   eval      rxrper = rnrper
T5.40c                   eval      rxraar = rnraar
T5.40c                   eval      rxonr  = *zero
T5.40c                   exsr      notat
T5.40c                   goto      end_c2bld
T5.40c                   endif
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2WIN Behandle bilde for saldo/kjøp/statistikk                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2win        begsr
      *
     c                   eval      e2post = 0
     c                   eval      e2debe = 0
     c                   eval      e2kred = 0
     c                   eval      e2rest = 0
     c                   eval      e2rekl = 0
T5.50c                   eval      e2purr = 0
T5.50c                   eval      e2rent = 0
T5.50c                   eval      e2inka = 0
      *
     c     e2tagb        tag
      *
     c                   read      rktrl2                                 15
      *
     c     *in15         cabeq     *on           xe2d01
      *
     c                   if        rnfirm <> w_firm or
     c                             rnkund <> w_kund
     c                   move      *on           *in15
     c                   endif
      *
     c     *in15         cabeq     *on           xe2d01
      *
      *  Restbeløp = 0
      *
     c                   if        rnrest = *zero
     c                   goto      srut
     c                   endif
      *
      *  Summer til aldersfordeling
      *
     c                   eval      w_sald = w_sald + rnrest
      *
      * Summerer beløp til inkasso
      *
     c                   if        rninko = 'J'
     c                   eval      w_inka = w_inka + rnrest
     c                   endif
      *
      * Beregn antall dager fra forfallsdato
      *
     C     w_dato        subdur    rnfdat        w_fodag : *d
      *
      * Beregn antall dager fra bilagsdato
      *
     C     w_dato        subdur    rnbdat        w_krdag : *d
      *
      * Ikke saldert
      *
     c                   if        rnrest < *zeros
     c                   eval      w_beln = w_beln + rnrest
      *
     c                   else
      *
      * Beregn grunnlag for gj.snitt kredittid
      *
     c                   eval      w_dagr = w_dagr + w_krdag
     c                   eval      w_tran = w_tran + 1
      *
      * Ikke forfalt
      *
     c                   select
     c                   when      w_fodag < 0
     c                   eval      w_bel0 = w_bel0 + rnrest
      *
      * Aldersfordeling 0 - A
      *
     c                   when      w_fodag <= w_3ka1
     c                   eval      w_bel1 = w_bel1 + rnrest
      *
      * Aldersfordeling A - B
      *
     c                   when      w_fodag <= w_3ka2
     c                   eval      w_bel2 = w_bel2 + rnrest
      *
      * Aldersfordeling B - C
      *
     c                   when      w_fodag <= w_3ka3
     c                   eval      w_bel3 = w_bel3 + rnrest
      *
      * Aldersfordeling C - D
      *
     c                   other
     c                   eval      w_bel4 = w_bel4 + rnrest
     c                   endsl
      *
     c                   endif
      *
     c     srut          tag
      *
      *  Antall poster
      *
     c                   eval      e2post = e2post + 1
      *
      *  Antall debet/kredit
      *
     c                   if        rnbelØ > *zero
     c                   eval      e2debe = e2debe + 1
     c                   else
     c                   eval      e2kred = e2kred + 1
     c                   endif
      *
      *  Antall restbeløp
      *
     c                   if        rnrest <> *zero
     c                   eval      e2rest = e2rest + 1
     c                   endif
      *
      *  Antall purret
      *
     c                   if        rnantp <> *zero
     c                   eval      e2purr = e2purr + 1
     c                   endif
      *
      *  Antall renteberegnet
      *
     c                   if        rnrenk <> *zero
     c                   eval      e2rent = e2rent + 1
     c                   endif
      *
      *  Antall inkasso
      *
     c                   if        rninko = 'J'
     c                   eval      e2inka = e2inka + 1
     c                   endif
      *
      *  Antall reklamasjon
      *
     c                   if        rnrekl <> *blank
     c                   eval      e2rekl = e2rekl + 1
     c                   endif
      *
     c                   goto      e2tagb
      *
     c     xe2d01        tag
      *
     c                   move      w_3ka1        e23ka1
     c                   move      w_3ka1        e23ks2
     c                   move      w_3ka2        e23ka2
     c                   move      w_3ka2        e23ks3
     c                   move      w_3ka3        e23ka3
     c                   move      w_3ka3        e23ka4
      *
     c                   move      rksald        e2sald
     c                   move      rksaif        e2saif
     c                   move      rkkjØp        e2kjØp
     c                   move      rkkjØf        e2kjØf
     c                   move      rkgrns        e2grns
     c                   move      rksald        e2salt
5.60 c                   move      rkkjal        e2kjal
5.60 c                   move      rkmema        e2mema
      *
      * Hent mva % og beregn memosaldo inkl. mva dersom kunden er avg.pliktig
      *
5.50 c                   if        rkmkod = 1
      *
5.50 c                   eval      w_stat = *blank
     c                   eval      w_mvak = '3'
      *
5.50 c                   call      'RS205R'
5.50 c                   parm                    w_stat
5.50 c                   parm                    w_mvak
5.50 c                   parm                    w_mvtx
5.50 c                   parm                    w_dato
5.50 c                   parm                    w_mvas
5.50 c                   parm                    w_mvat
5.50 c                   parm                    w_mvaf
5.50 c                   parm                    w_bpro
      *
5.50 c                   eval(h)   e2mems = rkmems * w_mvat
     c                   else
5.50 c                   eval      e2mems = rkmems
     c                   endif
      *
      * Beregner gj.snitt kredittid pr. kunde
      *
      * Flytt felt til skjerm
      *
     c                   eval      e2ald0 = w_bel0
     c                   eval      e2ald1 = w_bel1
     c                   eval      e2ald2 = w_bel2
     c                   eval      e2ald3 = w_bel3
     c                   eval      e2ald4 = w_bel4
     c                   eval      e2aldn = w_beln
     c                   eval      e2inkb = w_inka
      *
      * Nullstill
      *
     c                   eval      w_bel0 = *zeros
     c                   eval      w_bel1 = *zeros
     c                   eval      w_bel2 = *zeros
     c                   eval      w_bel3 = *zeros
     c                   eval      w_bel4 = *zeros
     c                   eval      w_beln = *zeros
      *
     c                   eval      w_sald = *zeros
     c                   eval      w_inka = *zeros
     c                   eval      w_dagr = *zeros
     c                   eval      w_tran = *zeros
      *
     c                   exfmt     e2win
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
T510  * Subrutine for oppkall av notatark
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     notat         begsr
      *
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'K'
E5.40c                   eval      rxpros = *blank
     c                   eval      rxakti = ' '
     c                   eval      rxkont = rkkund
     c                   eval      rxspes = 0
     c                   eval      rxavde = 0
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   eval      rxvalg = *blank
     c                   move      rxulda        rxupar
      *
     c                   call      'RF020R'
     c                   parm                    rxupar
      *
     c                   move      rxupar        rxulda
      *
T510 c                   endsr
      *
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_valg
     c                   parm                    w_kund
     c                   parm                    w_biln
T5.00c                   parm                    w_rest
      *
      * Key for posisjonering på åpne kundeposter
      *
     c     rktrl9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl9_kund
      *
     c     rktrl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl8_kund
     c                   kfld                    rktrl8_biln
      *
      * Key for posisjonering på kundeposter
      *
     c     rktrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl2_kund
     c                   kfld                    rktrl2_bdat
     c                   kfld                    rktrl2_tist
      *
      * Key for les av postering
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
     c                   kfld                    rktrlu_bdat
     c                   kfld                    rktrlu_tist
      *
      * Key for kunderegister via kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
T5.30 * Key for notatblokk kontonivå
      *
E5.40c     rklal2_key    klist
T5.30c                   kfld                    w_firm
E5.40c                   kfld                    rklal2_syst
E5.40c                   kfld                    rklal2_pros
E5.40c                   kfld                    rklal2_akti
E5.40c                   kfld                    rklal2_kund
      *
T5.40 * Key for notatblokk postnivå
      *
T5.40c     rklal3_key    klist
T5.30c                   kfld                    w_firm
T5.40c                   kfld                    rklal3_syst
T5.40c                   kfld                    rklal3_pros
T5.40c                   kfld                    rklal3_akti
T5.40c                   kfld                    rklal3_kund
T5.40c                   kfld                    rklal3_spes
T5.40c                   kfld                    rklal3_avde
T5.40c                   kfld                    rklal3_tist
      *
      * Key for statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 3
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.21 c                   eval      b2firm = w_firm
      *
     c                   eval      *in22 = *on
     c                   move      *date         w_dato
      *
     c                   eval      b2raar = *year
     c                   eval      w_raar = *year
      *
      * Hent kunde
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             61
6.20  * Hent inn informasjon om memosaldo
6.20 c     rkunl1_key    chain     rkmel1                             61
      *
     c                   eval      b2kund = w_kund
     c                   eval      b2navn = rknavn
     c                   eval      b2sald = rksald
      *
      * Hent statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             61
      *
      * Hent statuskode 3
      *
     c     raa3l1_key    chain     raa3l1                             61
      *
     c                   if        *in61 = *off
     c                   move      ra3ka1        w_3ka1
     c                   move      ra3ka2        w_3ka2
     c                   move      ra3ka3        w_3ka3
     c                   else
     c                   move      30            w_3ka1
     c                   move      60            w_3ka2
     c                   move      90            w_3ka3
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rktrl9_kund = w_kund
     c                   eval      rktrl2_kund = w_kund
     c                   eval      rktrl2_bdat = *loval
     c                   eval      rktrl2_tist = *loval
      *
     c     rktrl9_key    setll     rktrl9
     c     rktrl2_key    setll     rktrl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
