      /TITLE  ASOFAK Konvertering, konvertering av enheter -varereg.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX761R
      * Beskrivelse . . : Konvertering, konvertering av enheter -varereg.
      *
      *                   Gjør oppslag i enhets-konverteringstabell, og
      *                   konverterer enheter i vareregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121099 HKO  Nytt program
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
      *
6.10  *  UDS Local Data Area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d fenkl1_firm     s                   like(fefirm)
     d fenkl1_enhf     s                   like(feenhf)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom det ikke finnes enheter i konverteringstabell
      *
     c                   read      fenkl1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
      * Behandler vare
      *
     c                   read      vvarlur                                90
     c                   dow       *in90 = *off
      *
     c                   if        vvenhe <> *blank
     c                   eval      fenkl1_firm = vvfirm
     c                   eval      fenkl1_enhf = vvenhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      vvenhe = feenht
     c                   endif
     c                   endif
      *
     c                   if        vvenhl <> *blank
     c                   eval      fenkl1_firm = vvfirm
     c                   eval      fenkl1_enhf = vvenhl
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      vvenhl = feenht
     c                   endif
     c                   endif
      *
     c                   if        vvenhs <> *blank
     c                   eval      fenkl1_firm = vvfirm
     c                   eval      fenkl1_enhf = vvenhs
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      vvenhs = feenht
     c                   endif
     c                   endif
      *
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarlur
      *
     c                   read      vvarlur                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på enhets-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    fenkl1_firm
     c                   kfld                    fenkl1_enhf
      *
     c                   endsr
