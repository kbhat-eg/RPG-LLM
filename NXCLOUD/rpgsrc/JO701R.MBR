     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO701R
      * Beskrivelse . . : Matching, Oppdater fra EVR
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290996 HKO  Nytt program
      *       191196 HKO  Endret program fra kun innlegging av nye rader
      *                   til også å oppdatere eksisterende
      *       270298 HKO  Lagt inn test på at varer > '10000000' ikke skal
      *                   oppdatere matche-registert
      *       180104 HKO  Oppdaterer matche-register med Nobbnr
6.11  * 6.10  110709 bof  Utivdet eannr til 14 og brukt std. oppslag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjmatlu    uf a e           k disk    rename(jmatpfr:jmatlur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
      *
      * Definering av variable i nøkler
      *
     d jmatlu_var1     s                   like(jovar1)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jofirm)
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser EVR vareregister og oppdaterer matche-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1                                 90
     c                   dow       *in90 = *off
      *
      * LVR-varer skal ikke oppdatere matche-registeret
      *
     c                   if        vvvare > '10000000'
     c                   goto      les_neste
     c                   endif
      *
      * EAN-nummer
      *
6.11 c                   call      'VE712R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    vvvare
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
      *
      * Leser og oppdaterer vare i matche-registeret
      *
     c                   clear                   jmatlur
      *
     c                   eval      jmatlu_var1 = vvvare
     c     jmatlu_key    chain     jmatlur                            91
      *
     c                   eval      joogrp = vvogrp
     c                   eval      johgrp = vvhgrp
     c                   eval      jougrp = vvugrp
     c                   eval      jotek1 = vvtek1
     c                   eval      jotek2 = vvtek2
     c                   eval      joldor = vvldor
     c                   eval      jolvar = vvlvar
     c                   eval      joeann = w_eann
     c                   eval      jonobb = vvnobb
      *
     c                   if        *in91 = *off
     c                   update    jmatlur
     c                   else
     c                   eval      jofirm = w_firm
     c                   eval      jovar1 = vvvare
     c                   write     jmatlur
     c                   endif
      *
     c     les_neste     tag
      *
     c     vvarl1_key    reade     vvarl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for les på vareregister EVR
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av matcheregister
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
