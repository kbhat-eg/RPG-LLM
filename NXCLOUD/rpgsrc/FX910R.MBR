      /TITLE  ASOFAK: Subprogram for avrunding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FA910R
      * Beskrivelse . . : Subprogram for avrunding
      *
      *                   Kode som input-parameter:
      *                   Kode 1 = Avrunding av innkjøpspris
      *                   Kode 2 = Avrunding av kostpris
      *                   Kode 3 = Avrunding av salgspris eksl. mva
      *                   Kode 4 = Avrunding av salgspris inkl. mva
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
9.01  *       100418 bsa  Innfører manuell lukking av filer
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
9.01 d p_inlr          s              1
      *
     d p_kode          s              1  0
     d p_bel1          s             11  2
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_Ører          s              3  2
     d w_avru          s              9  0
     d w_sist          s              1  0
     d w_spny          s             11  2
      *
     d w_firm          s                   like(faafir)
     d w_avrk          s                   like(faaink)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  * "Ekstrarunde" for å stenge åpne filer?
9.01  *
9.01 c                   if        p_inlr = *on
9.01 c                   goto      avslutt
9.01 c                   endif
      *
      * Avslutter dersom input-beløp = 0
      *
     c                   if        p_bel1 = 0
     c                   goto      avslutt
     c                   endif
      *
      * Leser statusregister
      *
     c     fstsl1_key    chain     fstsl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
      * Test om gyldig kode
      *
     c                   if        p_kode < 1 or
     c                             p_kode > 4
     c                   goto      avslutt
     c                   endif
      *
     c                   if        p_kode = 1
     c                   eval      w_avrk = faaink
     c                   if        faaink = 0
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c                   if        p_kode = 2
     c                   eval      w_avrk = faakos
     c                   if        faakos = 0
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c                   if        p_kode = 3
     c                   eval      w_avrk = faaemv
     c                   if        faaemv = 0
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c                   if        p_kode = 4
     c                   eval      w_avrk = faaimv
     c                   if        faaimv = 0
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Test om beløp skal avrundes
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *off
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90 = *off and
     c                             vguavr = 1
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Splitt beløp i kr/øre
      *
     c                   eval      w_spny = p_bel1
      *
     c                   eval      w_avru = w_spny * 1
     c                   eval      w_Ører = w_spny - w_avru
     c                   move      w_Ører        w_sist
      *
      * Under 0. grense
      * Ingen avrunding
      *
     c                   if        w_spny < faagr0
     c                   goto      tag02
     c                   endif
      *
      * Under 1. grense
      * Avrundes til 0,05
      *
     c                   if        w_spny < faagr1
     c                   if        w_avrk = 1                                   NÆRMESTE
      *
     c                   if        w_sist = 1
     c                             or w_sist = 2
     c                   move      '0'           w_spny
     c                   endif
      *
     c                   if        w_sist = 3 or
     c                             w_sist = 4 or
     c                             w_sist = 6 or
     c                             w_sist = 7
     c                   move      '5'           w_spny
     c                   endif
      *
     c                   if        w_sist = 8 or
     c                             w_sist = 9
     c                   eval      w_spny = w_spny + faabl1
     c                   move      '0'           w_spny
     c                   endif
      *
     c                   else                                                   OPPOVER
      *
     c                   if        w_sist = 1 or
     c                             w_sist = 2 or
     c                             w_sist = 3 or
     c                             w_sist = 4
     c                   move      '5'           w_spny
     c                   endif
      *
     c                   if        w_sist = 6 or
     c                             w_sist = 7 or
     c                             w_sist = 8 or
     c                             w_sist = 9
     c                   eval      w_spny = w_spny + faabl1
     c                   move      '0'           w_spny
     c                   endif
     c                   endif
      *
     c                   goto      tag02
     c                   endif
      *
      * Under 2. grense
      * Avrundes til 0,10
      *
     c                   if        w_spny < faagr2
     c                   if        w_avrk = 1                                   NÆRMESTE
      *
     c                   if        w_sist <= 4
     c                   move      '0'           w_spny
     c                   endif
      *
     c                   if        w_sist >= 5
     c                   eval      w_spny = w_spny + faabl2
     c                   move      '0'           w_spny
     c                   endif
      *
     c                   else                                                   OPPOVER
      *
     c                   if        w_sist >= 1
     c                   eval      w_spny = w_spny + faabl2
     c                   move      '0'           w_spny
     c                   endif
      *
     c                   endif
      *
     c                   goto      tag02
     c                   endif
      *
      * Under 3. grense
      * Avrundes til 0,50
      *
     c                   if        w_spny < faagr3
     c                   if        w_avrk = 1                                   NÆRMESTE
      *
     c                   if        w_Ører <= 0.24
     c                   move      '00'          w_spny
     c                   endif
      *
     c                   if        w_Ører >= 0.25 and
     c                             w_Ører <= 0.74
     c                   move      '50'          w_spny
     c                   endif
      *
     c                   if        w_Ører >= 0.75
     c                   eval      w_spny = w_spny + faabl3
     c                   move      '00'          w_spny
     c                   endif
      *
     c                   else                                                   OPPOVER
      *
     c                   if        w_Ører >= 0.01 and
     c                             w_Ører <= 0.49
     c                   eval      w_spny = w_spny + faabl3
     c                   move      '50'          w_spny
     c                   endif
      *
     c                   if        w_Ører >= 0.51
     c                   eval      w_spny = w_spny + faabl3
     c                   move      '00'          w_spny
     c                   endif
      *
     c                   endif
      *
     c                   goto      tag02
     c                   endif
      *
      * Under 4. grense
      * Avrundes til 1,00
      *
     c                   if        w_spny < faagr4
     c                   if        w_avrk = 1                                   NÆRMESTE
      *
     c                   if        w_Ører <= 0.49
     c                   move      '00'          w_spny
     c                   endif
      *
     c                   if        w_Ører >= 0.50
     c                   eval      w_spny = w_spny + faabl4
     c                   move      '00'          w_spny
     c                   endif
      *
     c                   else                                                   OPPOVER
      *
     c                   if        w_Ører >= 0.01
     c                   eval      w_spny = w_spny + faabl4
     c                   move      '00'          w_spny
     c                   endif
      *
     c                   endif
     c                   goto      tag02
     c                   endif
      *
      * Over  4. grense
      * Avrundes til 10,00
      *
     c                   if        w_spny >= faagr4
      *
      * Legg inn siste siffer i kr.beløp
      * i første siffer i "ører"
      *
     c                   move      w_avru        w_sist
     c                   movel     w_sist        w_Ører
      *
     c                   if        w_avrk = 1                                   NÆRMESTE
      *
     c                   if        w_Ører <= 4.99
     c                   move      '000'         w_spny
     c                   endif
      *
     c                   if        w_Ører >= 5.00
     c                   eval      w_spny = w_spny + faabl5
     c                   move      '000'         w_spny
     c                   endif
      *
     c                   else                                                   OPPOVER
      *
     c                   if        w_Ører >= 0.01
     c                   eval      w_spny = w_spny + faabl5
     c                   move      '000'         w_spny
     c                   endif
      *
     c                   endif
      *
     c                   goto      tag02
     c                   endif
      *
     c     tag02         tag
      *
     c                   eval      p_bel1 = w_spny
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
9.01 c                   if        p_inlr = *on
     c                   eval      *inlr = *on
9.01 c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_kode
     c                   parm                    p_bel1
9.01 c                   parm                    p_inlr
      *
      * Key for oppslag på status-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe-register
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Henter firmanummer fra parameter
      *
     c                   move      p_firm        w_firm
      *
     c                   endsr
