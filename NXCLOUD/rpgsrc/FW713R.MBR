      /title  ASOFAK Lev.vare, henter vareregister - Coward
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW713R
      * Beskrivelse . . : Danning av leverandør vareregister, Coward
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280200 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_po            s              3  0
     d w_st            s              3  0
     d w_ln            s              3  0
     d w_belo          s              9
     d w_Ører          s              2
     d w_eann          s             13
     d w_bmen          s             11
     d w_desi          s              3
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
      *
     d w_vare          s              6                                         Varenr
     d w_vtxt          s             40
     d w_enhe          s              3                                         Enhet
     d w_inpr          s             10                                         Innkjøpspris
     d w_saim          s             10                                         Salgspris
     d w_plun          s             14                                         Ean-nummer
     d w_anta          s             11                                         Antall i pakning
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      * Hopper over første rad
      *
     c                   read      flvwpf                                 90
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
      * Splitter rad i enkelt-variable
      *
     c                   exsr      splitt
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = %trim(%subst(w_vtxt:1:35))
     c                   eval      fwtek2 = %trim(%subst(w_vtxt:36:5))
     c                   eval      fwlvar = w_vare
     c                   eval      fweann = 0
     c                   eval      fwnobb = 0
     c                   eval      fwdato = *loval
     c                   eval      fwenhe = w_enhe
     c                   eval      fwinpr = 0
     c                   eval      fwsapr = 0
     c                   eval      fwbmen = 0
     c                   eval      fwtvar = %trim('00' + w_vare)
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
      * Konverterer enhet
      *
     c                   eval      fenkl1_enhf = w_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
      * Innkjøpspris
      *
     c                   eval      w_belo = '000000000'
     c                   eval      w_Ører = '00'
      *
     c     '.'           scan      w_inpr:1      w_po                     92
      *
     c                   if        *in92 = *on
     c                   eval      w_Ører = %subst(w_inpr:w_po+1:2)
     c                   eval      w_inpr = %subst(w_inpr:1:w_po-1)
     c                   endif
      *
     c                   eval      w_ln = %len(%trim(w_inpr))
      *
     c                   eval      w_belo = %subst(w_belo:1:7-w_ln) +
     c                                      %subst(w_inpr:1:w_ln)
     c                   move      w_Ører        w_belo
      *
     c                   move      w_belo        fwinpr
      *
      * Salgspris
      *
     c                   eval      w_belo = '000000000'
     c                   eval      w_Ører = '00'
      *
     c     '.'           scan      w_saim:1      w_po                     92
      *
     c                   if        *in92 = *on
     c                   eval      w_Ører = %subst(w_saim:w_po+1:2)
     c                   eval      w_saim = %subst(w_saim:1:w_po-1)
     c                   endif
      *
     c                   eval      w_ln = %len(%trim(w_saim))
      *
     c                   eval      w_belo = %subst(w_belo:1:7-w_ln) +
     c                                      %subst(w_saim:1:w_ln)
     c                   move      w_Ører        w_belo
      *
     c                   move      w_belo        fwsapr
      *
     c                   eval      fwsapr = fwsapr * w_mvaf
      *
      * EAN-nummer
      *
     c                   eval      w_eann = '0000000000000'
      *
     c                   eval      w_ln = %len(%trim(w_plun))
      *
     c                   if        w_ln > 13
     c                   move      w_plun        w_eann
     c                   else
     c                   eval      w_eann = %subst(w_eann:1:13-w_ln) +
     c                                      %subst(w_plun:1:w_ln)
     c                   endif
      *
     c                   move      w_eann        fweann
      *
      * Antall i pakning (bestillingsmende)
      *
     c                   eval      w_bmen = '00000000000'
     c                   eval      w_desi = '000'
      *
     c     '.'           scan      w_anta:1      w_po                     92
      *
     c                   if        *in92 = *on
     c                   eval      w_desi = %subst(w_anta:w_po+1:2) + '0'
     c                   eval      w_anta = %subst(w_anta:1:w_po-1)
     c                   endif
      *
     c                   eval      w_ln = %len(%trim(w_anta))
      *
     c                   eval      w_bmen = %subst(w_bmen:1:8-w_ln) +
     c                                      %subst(w_anta:1:w_ln)
     c                   move      w_desi        w_bmen
      *
     c                   move      w_bmen        fwbmen
      *
      * Skriver til fil
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for splitting av innlest rad i enkeltvariable
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     splitt        begsr
      *
     c                   eval      w_vare = *blank
     c                   eval      w_vtxt = *blank
     c                   eval      w_enhe = *blank
     c                   eval      w_inpr = *blank
     c                   eval      w_saim = *blank
     c                   eval      w_plun = *blank
     c                   eval      w_anta = *blank
      *
      * Varenummer
      *
     c                   eval      w_vare = %subst(fwirad:1:6)
      *
      * Varetekst
      *
     c     '",'          scan      fwirad:9      w_po                     92
     c                   if        *in92 = *off
     c                   goto      end_splitt
     c                   endif
      *
     c                   eval      w_vtxt = %subst(fwirad:9:w_po-9)
      *
     c     X'46':'Æ'     xlate     w_vtxt        w_vtxt
     c     X'A0':'æ'     xlate     w_vtxt        w_vtxt
     c     X'77':'Ø'     xlate     w_vtxt        w_vtxt
     c     X'90':'ø'     xlate     w_vtxt        w_vtxt
     c     X'2A':'Å'     xlate     w_vtxt        w_vtxt
     c     X'EF':'å'     xlate     w_vtxt        w_vtxt
      *
      * Enhet
      *
     c                   eval      w_po = w_po + 2
     c     ','           scan      fwirad:w_po   w_po                     92
      *
     c                   eval      w_po = w_po + 1
     c     ','           scan      fwirad:w_po   w_po                     92
      *
     c                   eval      w_st = w_po + 1
     c     ','           scan      fwirad:w_st   w_po                     92
      *
     c                   eval      w_enhe = %subst(fwirad:w_st:w_po-w_st)
      *
      * Innkjøpspris
      *
     c                   eval      w_st = w_po + 1
     c     ','           scan      fwirad:w_st   w_po                     92
      *
     c                   eval      w_inpr = %subst(fwirad:w_st:w_po-w_st)
      *
      * Salgspris inkl. mva
      *
     c                   eval      w_st = w_po + 1
     c     ','           scan      fwirad:w_st   w_po                     92
      *
     c                   eval      w_saim = %subst(fwirad:w_st:w_po-w_st)
      *
      * EAN-nummer
      *
     c                   eval      w_po = w_po + 1
     c     ','           scan      fwirad:w_po   w_po                     92
      *
     c                   eval      w_st = w_po + 1
     c     ','           scan      fwirad:w_st   w_po                     92
      *
     c                   eval      w_plun = %subst(fwirad:w_st:w_po-w_st)
      *
      * Antall i pakning (bestillingsmengde)
      *
     c                   eval      w_st = w_po + 1
     c     ','           scan      fwirad:w_st   w_po                     92
      *
     c                   eval      w_anta = %subst(fwirad:w_st:w_po-w_st)
      *
     c     end_splitt    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   endsr
