      /TITLE  ASLAGR: Sletting av bestilling etter utskrift
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO401R
      *  Beskrivelse . . : Sletting av bestilling etter utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            021299  Nytt program                            ASK
6.20  *  6.20      250713  Slett logg når bestillings slettes      bof
6.nu  *  6.30      050614  Gjennomgått mtp. nummerutvidelse        bof
      *
8.01  *K0800   271022 bsa  Slette også oppføring i tilleggsregister
8.02  *K000    290824 bsa  Logger linjer også ved sleting av bestillinger
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
6.20 flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
     flotxl1    if   e           k disk    rename(lotxpfr:lotxl1r)
     flotxlu    uf   e           k disk    rename(lotxpfr:lotxlur)
6.20 fllogl1    if   e           k disk    rename(llogpfr:llogl1r)
6.20 flloglu    uf   e           k disk    rename(llogpfr:lloglur)
8.01 flotii2    uf   e           k disk    rename(lotist:lotii2r)
8.02 fldeslu    uf a e           k disk    rename(ldespfr:ldeslur)
8.02 fldellu    uf a e           k disk    rename(ldelpfr:ldellur)
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
6.20 d lohel1_numm     s                   like(lonumm)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d lotxl1_numm     s                   like(ltnumm)
     d lotxl1_suff     s                   like(ltsuff)
     d lotxl1_line     s                   like(ltline)
     d lotxlu_numm     s                   like(ltnumm)
     d lotxlu_suff     s                   like(ltsuff)
     d lotxlu_line     s                   like(ltline)
6.20 d llogl1_numm     s                   like(lmnumm)
6.20 d llogl1_suff     s                   like(lmsuff)
6.20 d lloglu_numm     s                   like(lmnumm)
6.20 d lloglu_suff     s                   like(lmsuff)
6.20 d lloglu_kode     s                   like(lmkode)
6.20 d lloglu_date     s                   like(lmdate)
6.20 d lloglu_time     s                   like(lmtime)
8.02 d ldeslu_numm     s                   like(lrnumm)
8.02 d ldeslu_suff     s                   like(lrsuff)
8.02 d ldeslu_line     s                   like(lrline)
8.02 d ldellu_aarr     s                   like(lyaarr)
8.02 d ldellu_numm     s                   like(lynumm)
8.02 d ldellu_suff     s                   like(lysuff)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
8.02 d p_type          s              4
8.02 d p_kom1          s             30
8.02 d p_kom2          s             30
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
8.02 d w_year          s              4  0
8.02 d w_dato          s               d   datfmt(*iso)
8.02 d w_time          s               t   timfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestilling   BESTILLING-TEKST
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *in90 = *off
     c     lotxl1_key    setll     lotxl1r
     c     lotxl1_key    reade     lotxl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      lotxlu_line = ltline
     c     lotxlu_key    chain     lotxlur                            91
     c                   if        *in91 = *off
     c                   delete    lotxlur
     c                   endif
      *
     c     lotxl1_key    reade     lotxl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestilling   BESTILLING-LINJE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *in90 = *off
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      lodtlu_line = ldline
     c     lodtlu_key    chain     lodtlur                            91
     c                   if        *in91 = *off
     c                   delete    lodtlur
     c                   endif
8.02  *
8.02  * Legger linja ut i sletteregister
8.02  *
8.02 c                   exsr      del_line
8.02  *
      *
     c     lodtl1_key    reade     lodtl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av bestilling   BESTILLING-HODE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     lohelu_key    chain     lohelur                            92
     c                   if        *in92 = *off
     c                   delete    lohelur
     c                   endif
8.01  *
8.01 c     lohelu_key    chain     lotii2
8.01 c                   if        %found
8.01 c                   delete    lotii2r
8.01 c                   endif
8.02  *
8.02  *  Oppdaterer BESTILLING-SLETTET-REGISTER med ny bestilling
8.02  *  Opplysninger fra slettet bestilling blir lagt over
8.02  *
8.02 c                   if        p_type <> *blank
8.02 c     ldellu_key    chain     ldellur
8.02 c                   if        not %found
8.02 c                   eval      lyfirm = w_firm
8.02 c                   eval      lyaarr = ldellu_aarr
8.02 c                   eval      lynumm = ldellu_numm
8.02 c                   eval      lysuff = ldellu_suff
8.02 c                   eval      lyotyp = lootyp
8.02 c                   eval      lybdat = lobdat
8.02 c                   eval      lyinnk = loinnk
8.02 c                   eval      lyvalk = lovalk
8.02 c                   eval      lylele = lolele
8.02 c                   eval      lyldor = loldor
8.02 c                   eval      lylena = lolena
8.02 c                   eval      lynavn = lonavn
8.02 c                   eval      lyadr1 = loadr1
8.02 c                   eval      lyadr2 = loadr2
8.02 c                   eval      lysted = losted
8.02 c                   eval      lyvref = lovref
8.02 c                   eval      lydref = lodref
8.02 c                   eval      lyrekv = lorekv
8.02 c                   eval      lytoti = lototi
8.02 c                   eval      lytotr = lototr
8.02 c                   eval      lytotk = lototk
8.02 c                   eval      lyornr = loornr
8.02 c                   eval      lyorsu = loorsu
8.02 c                   eval      lywsid = lowsid
8.02 c                   eval      lyuser = louser
8.02  *
8.02  *  Legger inn dagens dato - mangler felter for når slettet.
8.02  *
8.02 c*                  eval      lydate = w_dato
8.02 c*                  eval      lytime = w_time
8.02 c*                  eval      lyuser = l_user
8.02  *
8.02 c                   eval      lytype = p_type
8.02 c                   eval      lykom1 = p_kom1
8.02 c                   eval      lykom2 = p_kom2
8.02  *
8.02 c                   eval      lydate = lodate
8.02 c                   eval      lytime = lotime
8.02 c                   eval      lyuser = louser
8.02 c                   eval      lyedat = loedat
8.02 c                   eval      lyetim = loetim
8.02 c                   eval      lyeusr = loeusr
8.02  *
8.02 c                   write     ldellur
8.02 c                   endif
8.02 c                   endif
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *  Sletting av logg knyttet til bestilling
6.20  *  sjekker først at det ikke ligger igjen best.hode med en eller annen
6.20  *  suffix.
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c                   eval      lohel1_numm = p_numm
6.20 c     lohel1_key    chain     lohel1
6.20 c                   if        not %found(lohel1)
6.20  *
6.20 c     llogl1_key    setll     llogl1r
6.20 c     llogl1_key    reade     llogl1r
6.20  *
6.20 c                   dow       not %eof(llogl1)
6.20  *
6.20 c                   eval      lloglu_numm = lmnumm
6.20 c                   eval      lloglu_suff = lmsuff
6.20 c                   eval      lloglu_kode = lmkode
6.20 c                   eval      lloglu_date = lmdate
6.20 c                   eval      lloglu_time = lmtime
6.20 c     lloglu_key    chain     lloglu
6.20 c                   if        %found(lloglu)
6.20 c                   delete    lloglur
6.20 c                   endif
6.20  *
6.20 c     llogl1_key    reade     llogl1r
6.20 c                   enddo
6.20  *
6.20 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *  Legger ut linjer på sletteregister
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     del_line      begsr
8.02  *
8.02 c                   eval      ldeslu_numm = ldnumm
8.02 c                   eval      ldeslu_suff = ldsuff
8.02 c                   eval      ldeslu_line = ldline
8.02  *
8.02 c     ldeslu_key    chain     ldeslu
8.02 c                   if        not %found
8.02 c                   eval      lrfirm = w_firm
8.02 c                   eval      lraarr = w_year
8.02 c                   eval      lrnumm = ldnumm
8.02 c                   eval      lrsuff = ldsuff
8.02 c                   eval      lrline = ldline
8.02 c                   eval      lrotyp = lootyp
8.02 c                   eval      lrltyp = ldltyp
8.02 c                   eval      lrlety = ldlety
8.02 c                   eval      lrvare = ldvare
8.02 c                   eval      lrenh1 = ldenh1
8.02 c                   eval      lrlage = ldlage
8.02 c                   eval      lrldor = ldldor
8.02 c                   eval      lrldat = ldldat
8.02 c                   eval      lravde = ldavde
8.02 c                   eval      lrkont = ldkont
8.02 c                   eval      lrspes = ldspes
8.02 c                   eval      lrakti = ldakti
8.02 c                   eval      lrlvar = ldlvar
8.02 c                   eval      lrnobb = ldnobb
8.02 c                   eval      lreann = ldeann
8.02 c                   eval      lrornu = ldornu
8.02 c                   eval      lrorsu = ldorsu
8.02 c                   eval      lrorli = ldorli
8.02 c                   eval      lrtek1 = ldtek1
8.02 c                   eval      lrtek2 = ldtek2
8.02 c                   eval      lranta = ldanta
8.02 c                   eval      lripny = ldipny
8.02 c                   eval      lrkpny = ldkpny
8.02 c                   eval      lrrab1 = ldrab1
8.02 c                   eval      lrrab2 = ldrab2
8.02 c                   eval      lrsumi = ldsumi
8.02 c                   eval      lrsumr = ldsumr
8.02 c                   eval      lrsumk = ldsumk
8.02 c                   eval      lromva = ldomva
8.02 c                   eval      lrlvre = ldlvre
8.02 c                   eval      lrogrp = ldogrp
8.02 c                   eval      lrhgrp = ldhgrp
8.02 c                   eval      lrugrp = ldugrp
8.02 c                   eval      lrkplj = ldkplj
8.02 c                   eval      lrvari = ldvari
8.02  *
8.02 c                   eval      lrslda = w_dato
8.02 c                   eval      lrslti = w_time
8.02 c                   eval      lrspgm = 'LO400R'
8.02 c                   eval      lrslws = l_wsid
8.02 c                   eval      lrslus = l_user
8.02  *
8.02 c                   eval      lrodat = ldodat
8.02 c                   eval      lrotim = ldotim
8.02 c                   eval      lrousr = ldousr
8.02 c                   eval      lredat = ldedat
8.02 c                   eval      lretim = ldetim
8.02 c                   eval      lreusr = ldeusr
8.02  *
8.02 c                   write     ldeslur
8.02 c                   endif
8.02  *
8.02 c                   endsr
8.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file oppdater BESTILLING-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
6.20  *
6.20  *  Key for les av BESTILLING-HODE
6.20  *
6.20 c     lohel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    lohel1_numm
      *
      *  Key for file les BESTILLING-TEKST
      *
     c     lotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotxl1_numm
     c                   kfld                    lotxl1_suff
      *
      *  Key for file oppdater BESTILLING-TEKST
      *
     c     lotxlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotxlu_numm
     c                   kfld                    lotxlu_suff
     c                   kfld                    lotxlu_line
      *
      *  Key for file les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for file oppdater BESTILLING-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
6.20  *
6.20  *  Key for les av Logg-linjer
6.20  *
6.20 c     llogl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    llogl1_numm
6.20  *
6.20  *  Key for oppdatering av Logg-linjer
6.20  *
6.20 c     lloglu_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    lloglu_numm
6.20 c                   kfld                    lloglu_suff
6.20 c                   kfld                    lloglu_kode
6.20 c                   kfld                    lloglu_date
6.20 c                   kfld                    lloglu_time
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
8.02 c                   parm                    p_type
8.02 c                   parm                    p_kom1
8.02 c                   parm                    p_kom2
8.02  *
8.02  * Key for oppdatering av Bestilling-linje-register
8.02  *
8.02 c     ldeslu_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    w_year
8.02 c                   kfld                    ldeslu_numm
8.02 c                   kfld                    ldeslu_suff
8.02 c                   kfld                    ldeslu_line
8.02  *
8.02  *  Key for file : BESTILLING-SLETTET-REGISTER
8.02  *
8.02 c     ldellu_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    ldellu_aarr
8.02 c                   kfld                    ldellu_numm
8.02 c                   kfld                    ldellu_suff
8.02  *
8.02  *  Legger verdier i tid/datofelter
8.02  *
8.02 c                   time                    w_dato
8.02 c                   time                    w_time
8.02 c                   extrct    w_dato:*y     w_year
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      lohelu_numm = p_numm
     c                   eval      lohelu_suff = p_suff
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c                   eval      lodtlu_numm = p_numm
     c                   eval      lodtlu_suff = p_suff
      *
     c                   eval      lotxl1_numm = p_numm
     c                   eval      lotxl1_suff = p_suff
     c                   eval      lotxlu_numm = p_numm
     c                   eval      lotxlu_suff = p_suff
      *
6.20 c                   eval      llogl1_numm = p_numm
6.20 c                   eval      llogl1_suff = p_suff
      *
8.02 c                   eval      ldellu_aarr = w_year
8.02 c                   eval      ldellu_numm = p_numm
8.02 c                   eval      ldellu_suff = p_suff
      *
      *  Hent opplysninger fra LAGER-KODE-REGISTER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
