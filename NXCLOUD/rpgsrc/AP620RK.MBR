      /TITLE  XML utlegg for vanlige utskrifter
     H DECEDIT('0.') DATEDIT(*DMY.) option(*nodebugio)
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      *
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AP620RK
      * Beskrivelse . . : Genererer XML for vanlige utkrifter fra klienten
      *                   NB Ved kompilering kreves CGIDEV2 i bibliotekslista
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- - - - - - - - - - - - - - - - - - - - - - - - - -
      *       070610 BSA  Nytt program
6.10  * R6.10 280910 BSA  Legt ut tagg for visning i klienten
6.13  * R6.10 240111 BSA  Egen tagg for frislipp av utskrifter. Legges dft "false"
6.14  * R6.10 110211 BSA  Frislipp til outq styres fra java.
6.20  * R6.20 210312 BSA  Endret tagger for arkiv seksjon. Justert ifht behov
6.20  *                   i klienten.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
      * Definering av parametre
      *
     d p_spfnam        s             10a
     d p_jobnam        s             10a
     d p_usrnam        s             10a
     d p_jobnbr        s              6a
     d p_spfnbr        s             10i 0
      *
     d p_spfnam2       s             10a
     d p_jobnam2       s             10a
     d p_usrnam2       s             10a
     d p_jobnbr2       s              6a
     d p_spfnbr2       s             10i 0
      *
     d p_spfnam3       s             10a
     d p_jobnam3       s             10a
     d p_usrnam3       s             10a
     d p_jobnbr3       s              6a
     d p_spfnbr3       s             10i 0
      *
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_numm          s              6  0
     d p_selg          s              4  0
     d p_serv          s             35
     d p_stat          s              1
     d p_kule          s              1
     d p_navn          s             30
     d p_emal          s             50
     d p_ema2          s             50
     d p_emne          s             50
     d p_txt1          s             50
     d p_txt2          s             50
     d p_txt3          s             50
     d p_txt4          s             50
     d p_pers          s             50
     d p_inif          s             40
     d p_in03          s              1
     d p_jkey          s             41
      *
      * Definering av variabler i nøkler
      *
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_jnav          s             15
     d w_jkey          s             41
     d w_lstat         s              2  0
     d w_ltxt          s            200
     d w_kode          s              1
     d w_firm          s              3  0
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_ruti          s                   like(l_ruti)
     d w_numm          s              6  0
     d w_jtxt          s             35
     d w_mtxt          s            200
     d w_jstat         s              2  0
     d w_jtext         s            200
     d w_path          s            256
6.10 d w_dspl          s            100
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a   Varying
     d                                     Inz('/home/asp/print/adb')
     d XML_dftspl      s            256a   Varying
     d                                     Inz('/home/asp/print/maler/+
     d                                     Spool.xml')
     d jasper_logo     s            256a   Varying
     d tmpdate         s               D
     d tmptime         s               T
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     d                uds
     d l_poff                584    584
     d l_utqm                585    594
     d l_bach                595    635
     d l_ruti                800    809
     d l_outq                810    819
     d l_wsid                901    910
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      /copy prototypeb
      /copy usec
      *
      * Definering av variable
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Unikt jobbnummer finnes allerede.                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Vi logger at vi genererer xml for utskriften
      *
     c                   eval      w_jstat = 10
     c                   if        l_poff = '1'
     c                   eval      w_jtext = 'Generering av xml for ' + w_jkey
     c                   else
     c                   eval      w_jtext = 'PDF gen. er deaktivert!' + w_jkey
     c                   endif
      *
     c                   call      'AB705R'
     c**                 parm                    l_bach
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
      *
      * Skriver xml for videre utskrift håndtering
      *
     c                   if        l_poff = '1'
     c                   exsr      xml_print
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML/PDF print integrasjon                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xml_print     begsr
      *
      * Skriv xmlfila til disk
      /Free
           // Finne nytt batch nummer
               UpdHTMLVar('BatchNo':                  w_jkey);
               UpdHTMLVar('Batchtype':               'SPOOL');
               UpdHTMLVar('Username':                 l_user);
               WrtSection('Topp');

           // Finne credentials
               UpdHTMLVar('User':                      'xxx');
               UpdHTMLVar('PW':                   'xxxxxxxx');
               UpdHTMLVar('IPAdr':              'xx.x.xx.xx');
               UpdHTMLVar('Schema':             'ADB'+l_filg);
               UpdHTMLVar('Companyno':         %char(l_firm));
               WrtSection('Credential');

           // Finn spool info
               UpdHTMLVar('Logo':                jasper_logo);
               UpdHTMLVar('Keepspool':                'true');
               WrtSection('SpoolReportCommandStart');

               UpdHTMLVar('Spoolfilename':          p_spfnam);
               UpdHTMLVar('Spoolfilenumber': %char(p_spfnbr));
               UpdHTMLVar('Jobname':                p_jobnam);
               UpdHTMLVar('Jobuser':                p_usrnam);
               UpdHTMLVar('Jobnumber':              p_jobnbr);
6.20           UpdHTMLVar('Removespool':             'true');
6.13           UpdHTMLVar('Releasespool':            'false');
6.20    //     UpdHTMLVar('Copytospool':             l_utqm);
               WrtSection('SpoolReportCommand');
      /End-free
      *
     c                   if        p_spfnam2 <> *blank
      /Free
               UpdHTMLVar('Spoolfilename':          p_spfnam2);
               UpdHTMLVar('Spoolfilenumber': %char(p_spfnbr2));
               UpdHTMLVar('Jobname':                p_jobnam2);
               UpdHTMLVar('Jobuser':                p_usrnam2);
               UpdHTMLVar('Jobnumber':              p_jobnbr2);
6.20           UpdHTMLVar('Removespool':             'true');
6.13           UpdHTMLVar('Releasespool':            'false');
6.20    //     UpdHTMLVar('Copytospool':             l_utqm);
               WrtSection('SpoolReportCommand');
      /End-free
     c                   endif
      *
     c                   if        p_spfnam3 <> *blank
      /Free
               UpdHTMLVar('Spoolfilename':          p_spfnam3);
               UpdHTMLVar('Spoolfilenumber': %char(p_spfnbr3));
               UpdHTMLVar('Jobname':                p_jobnam3);
               UpdHTMLVar('Jobuser':                p_usrnam3);
               UpdHTMLVar('Jobnumber':              p_jobnbr3);
6.20           UpdHTMLVar('Removespool':             'true');
6.13           UpdHTMLVar('Releasespool':            'false');
6.20    //     UpdHTMLVar('Copytospool':             l_utqm);
               WrtSection('SpoolReportCommand');
      /End-free
     c                   endif
     c                   if        1 = 2
      *
      /Free
               WrtSection('SpoolReportCommandEnd');

           // Finn print informasjon
               UpdHTMLVar('Printername':             l_utqm);
               UpdHTMLVar('Copies':                     '1');
               UpdHTMLVar('Papersource':                ' ');
               UpdHTMLVar('Duplex':                 'false');
               UpdHTMLVar('Quality':                    ' ');
               UpdHTMLVar('Sorting':                    ' ');
               UpdHTMLVar('Staple':                  'true');
               UPDHTMLVar('Alignment':           'PORTRAIT');
               WrtSection('PrintCommand');
      /End-free
      *
     c                   endif
6.10  * Lag tag for visning i arkivklienten
6.10 c                   eval      w_dspl = *blank
6.10 c                   eval      w_dspl = 'Rutine: '+%trim(l_ruti)+
6.10 c                             ' Bruker: ' +  %trim(l_user)
      *
      /Free

           // Skriv metattags
               WrtSection('SpoolReportCommandEnd');

6.10           UpdHTMLVar('Displaytag':     %trim(w_dspl));
6.20           WrtSection('ArchiveCommandStart');

               UpdHTMLVar('Metavalue':            l_user);
               UpdHTMLVar('Metakey':            'BRUKER');
               WrtSection('MetaTag');

               UpdHTMLVar('Metavalue':            l_ruti);
               UpdHTMLVar('Metakey':            'RUTINE');
               WrtSection('MetaTag');

               UpdHTMLVar('Metavalue':   %char(w_date :*iso));
               UpdHTMLVar('Metakey':              'DATO');
               WrtSection('MetaTag');
6.20           WrtSection('ArchiveCommandEnd');

               WrtSection('Footer');
      /End-free
     c**                 eval      XML_Output = XML_path + l_filg + '/'+
     c**                           %trim(l_ruti)+'_' + %trim(w_jkey) + '.xml'
     c                   eval      XML_Output = XML_path + l_filg + '/'+
     c                             %trim(w_jkey) + '.xml'
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
      *
     c                   eval      w_jtext = 'XML fil '+%trim(xml_output)+' er +
     c                             generert !'
      *
     c                   call      'AB705R'
     c**                 parm                    l_bach
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_spfnam
     c                   parm                    p_jobnam
     c                   parm                    p_usrnam
     c                   parm                    p_jobnbr
     c                   parm                    p_spfnbr
     c                   parm                    p_spfnam2
     c                   parm                    p_jobnam2
     c                   parm                    p_usrnam2
     c                   parm                    p_jobnbr2
     c                   parm                    p_spfnbr2
     c                   parm                    p_spfnam3
     c                   parm                    p_jobnam3
     c                   parm                    p_usrnam3
     c                   parm                    p_jobnbr3
     c                   parm                    p_spfnbr3
     c                   parm                    p_jkey
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
     c                   eval      w_jkey = p_jkey
     c                   eval      afpslr_ufil = l_filg
      *
      * Sjekk om PDF aktivering er på/av
      *
     c                   eval      afpslr_uide = 'STATUS    '
     c     afpslr_key    chain     afpslrr
     c                   if        not %found
     c                   eval      l_poff = '0'
     c                   else
     c                   movel     afudss        l_poff
     c                   endif
      *
     c                   if        l_poff = '1'
      *
      * Hent inn malen som skal brukes
      *
6.20 c                   eval      afpslr_uide = 'DFTXMLSPLK'
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   eval      XML_dftspl = *blanks
     c                   movel     afudss        XML_dftspl
     c                   endif
     c                   callp     GetHTMLIFS(XML_dftspl :'<AS400>')
      *
      * Finn hvor xml'en skal legges
      *
     c                   eval      afpslr_uide = 'XMLPATH   '
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   eval      XML_path = *blanks
     c                   eval      w_path = *blanks
     c                   movel     afudss        w_path
     c                   eval      XML_path = %trim(w_path)
     c                   endif
      *
      * Finn path til logo
      *
     c                   eval      afpslr_uide = 'LOGO      '
     c     afpslr_key    chain     afpslrr
     c                   if        %found
     c                   eval      jasper_logo = *blanks
     c                   movel     afudss        jasper_logo
     c                   endif
      *
     c                   endif
      *
     c                   time                    w_date
      *
     c                   endsr
      *
