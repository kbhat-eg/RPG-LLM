     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD761R
      * Beskrivelse . . : Varesortiment, innlesing fra fil - CSV
      *
      *
      * er på Nobb prisformat.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111110 BOF  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    uf   e           k disk    rename(flvwpfr:flvwpfr)
      *
     fvvarl1    if a e           k disk    rename(vvarpfr:vvarl1r)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
     fvshelu    uf a e           k disk    rename(vshepfr:vshelur)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
      *
      * Definering av Nobb prisformat
      *
     d                 ds
     d d_list                        74
     d  d_vare                       15    overlay(d_list:1)
      *
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vcfirm)
5.70 d p_vsor          s                   like(vcvsor)
     d p_lage          s                   like(vclage)
     d p_type          s                   like(vctype)
     d p_slet          s              1  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vshelu_vsor     s                   like(vcvsor)
     d vshelu_lage     s                   like(vclage)
     d vsdtlu_vsor     s                   like(vdvsor)
     d vsdtlu_lage     s                   like(vdlage)
     d vsdtlu_ldor     s                   like(vdldor)
     d vsdtlu_ogrp     s                   like(vdogrp)
     d vsdtlu_hgrp     s                   like(vdhgrp)
     d vsdtlu_ugrp     s                   like(vdugrp)
     d vsdtlu_modn     s                   like(vdmodn)
     d vsdtlu_vare     s                   like(vdvare)
      *
      * Definering av variable
      *
     d w_dato          s               d   datfmt(*iso)
     d w_posi          s              2  0
      *
     d w_firm          s                   like(vcfirm)
      *
     d b_feil          s              1    inz(*off)
      *
      * Konstanter
      *
     d c_form          s              1    inz('1')
     d c_digi          s             15    inz('0123456789 ')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * sjekker om sortimentsheading ligger der fra før
      *
     c                   clear                   vshelur
5.70 c                   eval      vshelu_vsor = p_vsor
     c                   eval      vshelu_lage = p_lage
     c     vshelu_key    chain     vshelu
     c                   if        not %found(vshelu)
     c                   eval      vcfirm = w_firm
5.70 c                   eval      vcvsor = p_vsor
     c                   eval      vclage = p_lage
     c                   eval      vctype = p_type
     c                   eval      vcbesk = 'Automatisk innlest CSV'
      *
     c                   eval      vceusr = l_user
     c                   time                    vcedat
     c                   time                    vcetim
     c                   eval      vcousr = l_user
     c                   time                    vcodat
     c                   time                    vcotim
     c                   write     vshelur
     c                   endif
      *
      * Dersom sletting er valgt, slettes eksisterende varesort.detaljer
      *
     c                   if        p_slet = 1
      *
5.70 c                   eval      vsdtlu_vsor = p_vsor
     c                   eval      vsdtlu_lage = p_lage
     c     vsdtlu_key2   setll     vsdtlu
     c     vsdtlu_key2   reade     vsdtlur
     c                   dow       not %eof
     c                   delete    vsdtlur
     c     vsdtlu_key2   reade     vsdtlur
     c                   enddo
      *
     c                   endif
      *
      * Leser og oppretter/oppdaterer spesialpriser
      *
     c                   read      flvwpfr
     c                   dow       not %eof
     c                   exsr      utled_sdv
     c                   if        b_feil = *off
     c                   exsr      oppdater
     c                   endif
     c                   read      flvwpfr
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_sdv     begsr
      *
     c                   eval      d_list = *blank
     c                   eval      b_feil = *off
      *
     c     '"':' '       xlate     fwirad        fwirad
      *
      * Varenr.
      *
     c                   eval      w_posi = %scan(';':fwirad)
     c                   if        w_posi > 0
     c                   eval      d_vare = %trim(%subst(fwirad:1:w_posi-1))
     c                   else
     c                   eval      d_vare = %trim(%subst(fwirad:1:15))
     c                   endif
     c     c_digi        check     d_vare                                 70
     c                   if        *in70 = *on
     c                   eval      b_feil = *on
     c                   goto      end_utled
     c                   endif
      *
     c     end_utled     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Avbryter dersom nobb-nummer eller pris ikke er utfylt
      *
     c                   if        d_vare = *blank
     c                   leavesr
     c                   endif
      *
      * Avbryter dersom vare ikke finnes
      *
     c                   eval      vvarl1_vare = d_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * Oppdaterer/oppretter varesortiment
      *
      *
     c                   clear                   vsdtlur
      *
5.70 c                   eval      vsdtlu_vsor = p_vsor
     c                   eval      vsdtlu_lage = p_lage
     c                   eval      vsdtlu_ldor = 0
     c                   eval      vsdtlu_ogrp = 0
     c                   eval      vsdtlu_hgrp = 0
     c                   eval      vsdtlu_ugrp = 0
     c                   eval      vsdtlu_modn = 0
     c                   eval      vsdtlu_vare = vvvare
     c     vsdtlu_key    chain     vsdtlur
      *
     c                   eval      vdfirm = w_firm
5.70 c                   eval      vdvsor = p_vsor
     c                   eval      vdlage = p_lage
     c                   eval      vdldor = 0
     c                   eval      vdogrp = 0
     c                   eval      vdhgrp = 0
     c                   eval      vdugrp = 0
     c                   eval      vdvare = vvvare
      *
     c                   eval      vdeusr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
      *
     c                   if        %found(vsdtlu)
     c                   update    vsdtlur
     c                   else
6.10 c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
     c                   write     vsdtlur
     c                   endif
      *
      * Sletter rad i arbeidsfil
      *
     c                   delete    flvwpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
5.70 c                   parm                    p_vsor
     c                   parm                    p_lage
     c                   parm                    p_type
     c                   parm                    p_slet
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vsdtlu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    vsdtlu_vsor
     c                   kfld                    vsdtlu_lage
     c                   kfld                    vsdtlu_ldor
     c                   kfld                    vsdtlu_ogrp
     c                   kfld                    vsdtlu_hgrp
     c                   kfld                    vsdtlu_ugrp
     c                   kfld                    vsdtlu_modn
     c                   kfld                    vsdtlu_vare
      *
     c     vsdtlu_key2   klist
     c                   kfld                    w_firm
5.70 c                   kfld                    vsdtlu_vsor
     c                   kfld                    vsdtlu_lage
      *
     c     vshelu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    vshelu_vsor
     c                   kfld                    vshelu_lage
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
