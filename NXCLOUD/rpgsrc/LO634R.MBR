     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO634R
      * Beskrivelse . . : Utskrift av kontroll-liste, plukker best.nr
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *                   Nytt program
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
5.61  * R5.61 200902 AHX  Lister avvik på totallistee dersom < > enn
5.61  *                   inntastet
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
6.20  * 6.20  290212 bof  Justert hente pris ihht. lev.nr på lager
6.nu  * 6.30  140614 bof  Endret ihht. nummerutvidelse
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   160622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTL1    IF   E           K DISK    RENAME(LODTPFR:LODTL1R)
     FLOTXL1    IF   E           K DISK    RENAME(LOTXPFR:LOTXL1R)
      *
     FLPLOL1    IF   E           K DISK    RENAME(LPLOPFR:LPLOL1R)
     FLPSOLU    UF A E           K DISK    RENAME(LPSOPFR:LPSOLUR)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     D                 DS
      *                                                      SORTERING
6.nu D  SORTER                 1     68
     D  SOR001                 1      8  0
     D  SOR002                 9     38
     D  SOR003                39     41
     D  SOR004                42     58
6.nu D  SOR005                59     66  0
6.nu D  SOR006                67     68  0
      *
     D                 DS
      *                                                      Parameter-inn
     D  DSPREC                 1     64
     D  DSOTYP                 1      2
6.nu D  DSFBES                 5     12  0
6.nu D  DSTBES                13     20  0
6.nu D  DSFLEV                21     26  0
6.nu D  DSTLEV                27     32  0
6.nu D  DSFDAT                33     42d   datfmt(*iso)
6.nu D  DSTDAT                43     52d   datfmt(*iso)
6.nu D  DSVAL1                53     53  0
6.nu D  DSVAL2                54     54  0
6.nu D  DSVAL3                55     55  0
6.nu D  DSVAL4                56     56  0
6.nu D  DSVAL5                57     57  0
6.nu D  DSVAL6                58     58  0
6.nu D  DSVAL7                59     59  0
6.nu D  dsvva1                60     64  2
      *
6.20 d b_inlr          s              1    inz(*off)
8.01 d w_prgr          s              2
5.70 d w_avde          s              4  0
6.20 d w_vtyp          s              1
6.20 d w_ldor          s              6  0
6.20 d w_utda          s                   like(ldedat)
6.20 d w_firm          s                   like(ldfirm)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
     D/EJECT
      * - - - - - - - - - - - - - - - - - - *
      *   Priser      :  Overføringsfelter  *
      * - - - - - - - - - - - - - - - - - - *
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
     D/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  Local Data Area                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                UDS
      *
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utplukk, Setter inn riktige parameterverdier          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      WWPARM        DSPREC
      *
     C                   MOVE      DSFBES        HEDNUM
     C                   MOVE      *ZERO         HEDSUF
     C                   MOVE      DSTBES        SLTBES
     C                   MOVE      99            SLTSUF
      *
     C     HEDKEY        SETLL     LOHEL1R
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Leser poster fra register for utplukks-test                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     START         TAG
      *
     C                   MOVE      *OFF          *IN90
     C                   READ      LOHEL1R                                90
     C     *IN90         CABEQ     *ON           SLUTT
      *
      *  Sjekk   Er bestillingsnummer innenfor grensene, Ellers hopp til Slutt
      *
     C     LONUMM        CABGT     SLTBES        SLUTT
      *
     C     LONUMM        IFEQ      SLTBES
     C     LOSUFF        IFGT      SLTSUF
     C                   GOTO      SLUTT
     C                   END
     C                   END
      *
      *  Sjekk   Er ordretype lik valgt,           Hvis nei les neste post
      *
     C     LOOTYP        CABNE     DSOTYP        START
      *
      *  Sjekk   Er leverandørnummer innenfor grensene  Hvis nei les neste post
      *
     C     LOLDOR        IFLT      DSFLEV
     C     LOLDOR        ORGT      DSTLEV
     C                   GOTO      START
     C                   END
      *
      *  Sjekk   Er Datoeneinnenfor grensene,      Hvis nei les neste post
      *
     C     LOBDAT        IFLT      DSFDAT
     C     LOBDAT        ORGT      DSTDAT
     C                   GOTO      START
     C                   END
      *
      *  Sjekk   Er Bruker lik valgt uten utplukk  Hvis nei les neste post
      *
     C     DSVAL5        IFEQ      1
     C     LOUSER        IFNE      DSUSER
     C                   GOTO      START
     C                   END
     C                   END
      *
      * henter prisgruppe
     c                   exsr      hent_prisgr
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk opplysninger i Plukk-register                            *
      *  Det skal bare sjekkes dersom valg 3=1 (Plukket record)         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     DSVAL3        IFEQ      1
      *
      *  Hent opplysninger fra BESTILLING-PLUKK-REGISTER
      *
     C                   MOVE      LONUMM        PLONUM
     C                   MOVE      LOSUFF        PLOSUF
     C     PLOKEY        CHAIN     LPLOL1R                            90
     C     *IN90         CABEQ     *ON           START
      *
      *  Sjekk   Er Arbeidsstasjon lik valgt,      Hvis nei les neste post
      *
     C     DSVAL4        IFEQ      1
     C     LKPWSD        IFNE      DSWSID
     C                   GOTO      START
     C                   END
     C                   END
      *
      *  Sjekk   Er Bruker lik valgt,              Hvis nei les neste post
      *
     C     DSVAL5        IFEQ      1
     C     LKPUSR        IFNE      DSUSER
     C                   GOTO      START
     C                   END
     C                   END
      *
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk opplysninger i Bestillings-register                      *
      *  Det skal bare sjekkes dersom valg 3=2   (Kontroll-kode)        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     DSVAL3        IFEQ      2
      *
      *  Sjekk   Er valgt kontrollkode=J,          Hvis nei les neste post
      *
     C     LOBIND        IFNE      'J'
     C                   GOTO      START
     C                   END
      *
      *  Sjekk   Er Arbeidsstasjon lik valgt,      Hvis nei les neste post
      *
     C     DSVAL4        IFEQ      1
     C     LOWSID        IFNE      DSWSID
     C                   GOTO      START
     C                   END
     C                   END
      *
      *  Sjekk   Er Bruker lik valgt,              Hvis nei les neste post
      *
     C     DSVAL5        IFEQ      1
     C     LOUSER        IFNE      DSUSER
     C                   GOTO      START
     C                   END
     C                   END
      *
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utlegging av records til Plukk/Sort-register          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legger inn bestillingsnummer og suffix i nøkklene
      *
     C                   MOVE      LONUMM        HEDNUM
     C                   MOVE      LOSUFF        HEDSUF
      *
     C                   MOVE      LONUMM        LINNUM
     C                   MOVE      LOSUFF        LINSUF
      *
     C                   MOVE      LONUMM        TXTNUM
     C                   MOVE      LOSUFF        TXTSUF
      *
     C                   MOVE      LONUMM        PSONUM
     C                   MOVE      LOSUFF        PSOSUF
      *
      *  Nullstiller sorteringsbegrep pr. ordre
      *
     C                   MOVE      *BLANK        SORTER
     C                   MOVE      *ZERO         SOR001
      *
      *  Legger inn faste verdier i sorteringsbegrep
      *
     C                   MOVEL     LONUMM        SOR005
     C                   MOVEL     LOSUFF        SOR006
      *
      *  Dersom utskrift i registreringsrekkefølge legges
      *  Dato og klokkeslett inn i sorteringsbegrepet
      *
     C     DSVAL2        IFEQ      1
     C                   MOVE      LODATE        SOR001
     C                   MOVEL     LOTIME        SOR002
     C                   END
      *
      *  Dersom utskrift i vareadresse-rekkefølge legges
      *  Leverandørnummer og vareadresse inn i sorteringsbegrepet
      *
     C     DSVAL2        IFEQ      2
     C                   MOVE      LOLDOR        SOR001
     C                   MOVEL     LONAVN        SOR002
     C                   END
      *
      *  Henter linjer/records fra de enkelte registre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *  Nøkkel for utplukks/sortert register er      *
      *                                               *
      *  LKSFIR  Firma                                *
      *  LKSSRT  Sorteringsbegrep                     *
      *  LKSONR  Bestillingsnummer                    *
      *  LKSSUF  Suffix                               *
      *  LKSTLL  Rekkefølgekode  : Hode         = 0   *
      *  LKSLIN  Linjenummer     : Detaljlinjer = 3   *
      *                          : Tekst, Foran = 2   *
      *                          : Tekst, Etter = 4   *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Blanker hjelpevariabel for test for detaljlinjer
      *
     C                   MOVE      *BLANK        TSTBYT
      *
      *  Henter detalj-linjer
      *
     C                   EXSR      LINJE
      *
      *  Henter bare Heading + tekstlinjer dersom varelinjer finnes
      *
     C     TSTBYT        IFNE      *BLANK
     C                   EXSR      OHEAD
     C                   EXSR      FORAN
     C                   EXSR      ETTER
     C                   END
      *
      *  Dersom Bestilllingen ikke lenger skal være plukket etter
      *  utskrift av kontroll-liste slettes merket
      *
     C     DSVAL6        IFEQ      1
      *
      *  Kaller opp subprogrammet for sletting av utplukk
      *  og blanking av behandlinskode i BESTILLING-HODE-REGISTER
      *
     C                   CALL      'LO402R'
     C                   PARM                    WSTATG
     C                   PARM                    HEDFIR
     C                   PARM                    HEDNUM
     C                   PARM                    HEDSUF
      *
     C                   END
      *
      *  Velg neste Bestilling
      *
     C                   GOTO      START
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     SLUTT         TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter bestilling-heading og legger ut til sorterings-register *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   OHEAD         BEGSR
      *
     C                   MOVE      *ZERO         PSOTLL
     C                   MOVE      *ZERO         PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C                   MOVE      'H'           LKSKOD
     C                   MOVEL     DSOTYP        LKSOTY
     C                   MOVE      DSVAL1        LKSALT
     C                   MOVEL     SORTER        LKSSRT
     C                   MOVEL     DSVAL7        LKSDIV
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        LKSFIR
     C                   MOVE      PSONUM        LKSONR
     C                   MOVE      PSOSUF        LKSSUF
     C                   MOVE      PSOTLL        LKSTLL
     C                   MOVE      PSOLIN        LKSLIN
     C                   WRITE     LPSOLUR
     C                   END
      *
     CSR                 ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter bestillings-linjer og legger ut til sorterings-register *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   LINJE         BEGSR
      *
     C                   MOVE      *ZERO         LINLIN
      *
     C     LINKEY        SETLL     LODTL1R
      *
     C     LINJE1        TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      LODTL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LDFIRM        IFEQ      LINFIR
     C     LDNUMM        IFEQ      LINNUM
     C     LDSUFF        IFEQ      LINSUF
      *
      *  Dersom sjekk for varelinjer kall opp subrutine
      *
     C     DSVAL7        IFEQ      1
     C                   EXSR      SJEKK
     C     WWWTST        IFEQ      *BLANK
     C                   GOTO      LINJE1
     C                   END
     C                   END
      *
     C                   Z-ADD     3             PSOTLL
     C                   Z-ADD     LDLINE        PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C                   MOVE      'X'           TSTBYT
      *
     C                   MOVE      'V'           LKSKOD
     C                   MOVEL     DSOTYP        LKSOTY
     C                   MOVE      DSVAL1        LKSALT
     C                   MOVEL     SORTER        LKSSRT
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        LKSFIR
     C                   MOVE      PSONUM        LKSONR
     C                   MOVE      PSOSUF        LKSSUF
     C                   MOVE      PSOTLL        LKSTLL
     C                   MOVE      PSOLIN        LKSLIN
     C                   WRITE     LPSOLUR
     C                   END
      *
     C                   GOTO      LINJE1
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk av linjeopplysninger for om skal med til utskrift        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   SJEKK         BEGSR
      *
      *  Blanker testfelter for om linjen skal være med
      *
     C                   MOVE      *BLANK        WWWTST
      *
      *  Sjekk   Er Innkjøpspris og rabatt forsjellig fra standard
      *
     C     DSVAL7        IFEQ      1
     C                   EXSR      HNTVAR
      *
     C     LDIPNY        IFNE      hokopr
     C     LDRAB1        ORNE      HORAB1
     C     LDRAB2        ORNE      HORAB2
     C                   MOVE      'X'           WWWTST
     C                   GOTO      SJEKK9
     C                   END
     C                   END
      *
      *  Beregner dekningsgrad for linjen
      *
     C     LDSUMI        SUB       LDSUMR        WWNETT
     C     WWNETT        SUB       LDSUMK        WWDEKN
      *
     C                   MOVE      *ZERO         WWGRAD
     C     WWNETT        IFNE      *ZERO
     C     WWDEKN        DIV       WWNETT        WWGRAD
     C                   MULT      100           WWGRAD
     C                   END
      *
      *  Sjekk   Er Dekningsgrad 100% eller mindre enn oppgitt %, skriv linjen
      *
     C     WWGRAD        IFEQ      100
     C                   MOVE      'X'           WWWTST
     C                   END
      *
     CSR   SJEKK9        ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-tekst (FORAN) og legger ut til sorterings-register*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   FORAN         BEGSR
      *
     C                   MOVE      *ZERO         TXTLIN
      *
     C     TXTKEY        SETLL     LOTXL1R
      *
     C     FORAN1        TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      LOTXL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LTFIRM        IFEQ      TXTFIR
     C     LTNUMM        IFEQ      TXTNUM
     C     LTSUFF        IFEQ      TXTSUF
     C     LTLINE        IFLE      4999
      *
     C                   Z-ADD     2             PSOTLL
     C                   Z-ADD     LTLINE        PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C                   MOVE      'K'           LKSKOD
     C                   MOVEL     DSOTYP        LKSOTY
     C                   MOVE      DSVAL1        LKSALT
     C                   MOVEL     SORTER        LKSSRT
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        LKSFIR
     C                   MOVE      PSONUM        LKSONR
     C                   MOVE      PSOSUF        LKSSUF
     C                   MOVE      PSOTLL        LKSTLL
     C                   MOVE      PSOLIN        LKSLIN
     C                   WRITE     LPSOLUR
     C                   END
      *
     C                   GOTO      FORAN1
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-tekst (ETTER) og legger ut til sorterings-register*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   ETTER         BEGSR
      *
     C                   MOVE      5000          TXTLIN
      *
     C     TXTKEY        SETLL     LOTXL1R
      *
     C     ETTER1        TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      LOTXL1R                                90
     C     *IN90         IFEQ      *OFF
     C     LTFIRM        IFEQ      TXTFIR
     C     LTNUMM        IFEQ      TXTNUM
     C     LTSUFF        IFEQ      TXTSUF
     C     LTLINE        IFLE      9999
      *
     C                   Z-ADD     4             PSOTLL
     C                   Z-ADD     LTLINE        PSOLIN
     C     PSOKEY        CHAIN     LPSOLUR                            92
      *
     C                   MOVE      'K'           LKSKOD
     C                   MOVEL     DSOTYP        LKSOTY
     C                   MOVE      DSVAL1        LKSALT
     C                   MOVEL     SORTER        LKSSRT
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    LPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        LKSFIR
     C                   MOVE      PSONUM        LKSONR
     C                   MOVE      PSOSUF        LKSSUF
     C                   MOVE      PSOTLL        LKSTLL
     C                   MOVE      PSOLIN        LKSLIN
     C                   WRITE     LPSOLUR
     C                   END
      *
     C                   GOTO      ETTER1
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter opplysninger om varen fra pris/vare subrutinen          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     HNTVAR        BEGSR
6.20  *
6.20  * henter varetype
6.20  *
6.20 c                   call      'VL710R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    ldvare
6.20 c                   parm                    ldlage
6.20 c                   parm                    w_vtyp
6.20 c                   parm                    w_utda
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   eval      hifirm = hedfir
     c                   eval      hirkat = 0
     c                   eval      hikund = 0
     c                   eval      hikpro = 0
     c                   eval      hivare = ldvare
     c                   eval      hilety = 0
     c                   eval      hienhe = ldenh1
     c                   eval      hidato = lobdat
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
6.20 c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiprgr = w_prgr
      *
     C                   CALL      'VP900R'
     C                   PARM                    HIREC
     C                   PARM                    HOREC
      *
     C                   ENDSR
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    lofirm
5.70 c                   parm                    lolage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WSTATG            1
     C                   MOVE      *BLANK        TSTBYT            1
     C                   MOVE      *BLANK        WWWTST            1
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    LDSUMI        WWNETT
     C     *LIKE         DEFINE    LDSUMI        WWDEKN
     C     *LIKE         DEFINE    LDSUMI        WWGRAD
      *
     C     *LIKE         DEFINE    LKPONR        SLTBES
     C     *LIKE         DEFINE    LKPSUF        SLTSUF
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    LDFIRM        LINFIR
     C     *LIKE         DEFINE    LDNUMM        LINNUM
     C     *LIKE         DEFINE    LDSUFF        LINSUF
     C     *LIKE         DEFINE    LDLINE        LINLIN
      *
     C     *LIKE         DEFINE    LTFIRM        TXTFIR
     C     *LIKE         DEFINE    LTNUMM        TXTNUM
     C     *LIKE         DEFINE    LTSUFF        TXTSUF
     C     *LIKE         DEFINE    LTLINE        TXTLIN
      *
     C     *LIKE         DEFINE    LKPFIR        PLOFIR
     C     *LIKE         DEFINE    LKPONR        PLONUM
     C     *LIKE         DEFINE    LKPSUF        PLOSUF
      *
     C     *LIKE         DEFINE    LKSFIR        PSOFIR
     C     *LIKE         DEFINE    LKSONR        PSONUM
     C     *LIKE         DEFINE    LKSSUF        PSOSUF
     C     *LIKE         DEFINE    LKSTLL        PSOTLL
     C     *LIKE         DEFINE    LKSLIN        PSOLIN
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : BESTILLING-LINJE-REGISTER
      *
     C     LINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINNUM
     C                   KFLD                    LINSUF
     C                   KFLD                    LINLIN
      *
      *  Key for file : BESTILLING-TEKST-REGISTER
      *
     C     TXTKEY        KLIST
     C                   KFLD                    TXTFIR
     C                   KFLD                    TXTNUM
     C                   KFLD                    TXTSUF
     C                   KFLD                    TXTLIN
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        KLIST
     C                   KFLD                    PLOFIR
     C                   KFLD                    PLONUM
     C                   KFLD                    PLOSUF
      *
      *  Key for file : PLUKK-S-LINJE-REGISTER
      *
     C     PSOKEY        KLIST
     C                   KFLD                    PSOFIR
     C                   KFLD                    PSONUM
     C                   KFLD                    PSOSUF
     C                   KFLD                    PSOTLL
     C                   KFLD                    PSOLIN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WWPARM           64
      *
      *  Legge inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        HEDFIR
     C                   MOVE      DSSFIR        LINFIR
     C                   MOVE      DSSFIR        TXTFIR
     C                   MOVE      DSSFIR        PLOFIR
     C                   MOVE      DSSFIR        PSOFIR
      *
     CSR                 ENDSR
