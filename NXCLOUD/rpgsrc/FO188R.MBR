      /TITLE  Utskrift av rapport utg. fakturafiler
     h decedit('0,') datedit(*dmy.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : FO188R
      * Beskrivelse . . : Utskrift av rapport utg. fakturafiler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140205 GRO  Nytt program
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsohelu    uf   e           k disk    rename(sohepfr:sohelur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     ffntrl1    if   e           k disk    rename(fntrpfr:fntrl1r)
      *
     ffo684p    o    e             printer
      *
      * - - - - - - - - - - - - - -
      * Datastruktur parameter-inn
      * - - - - - - - - - - - - - -
      *
     d                 ds
6.nu d dsparm                        32
     d  dsaarr                        4  0 overlay(dsparm)
     d  dsotyp                        2    overlay(dsparm:5)
6.nu d  dsffnr                        8  0 overlay(dsparm:7)
6.nu d  dstfnr                        8  0 overlay(dsparm:15)
6.nu d  dsprog                       10    overlay(dsparm:23)
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     d                uds
     d dsruti                800    809
     d dsalin                856    858  0
     d dswsid                901    910
     d dsuser                911    920
     d dsfirm                944    946  0
     d l_fnav                951    980
      *
     d fcycle          s              1  0
     d utime           s              6  0
     d wpfast          s             10
     d wpfell          s              6
     d wpkode          s              1
6.nu d wpnumm          s              8  0
6.nu d wpparm          s             32
     d wptype          s              2
     d wwaarr          s                   like(soaarr)
     d wwbinr          s                   like(sobinr)
     d wwfirm          s                   like(sofirm)
     d wwkund          s                   like(sokund)
     d wwnumm          s                   like(sonumm)
     d wwruti          s             10
     d wwsuff          s                   like(sosuff)
     d wwwsid          s                   like(sowsid)
      *
      * Definering av variabler i nøkler
      *
     d fntrl1_numm     s                   like(fnnumm)
     d fntrl1_suff     s                   like(fnsuff)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading på rapporten
      * A1DET  - Linjer  på rapporten
      * T1TOT  - Totaler på rapporten
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     c                   select
     c                   when      fcycle = *zero
     c                   exsr      xstrsr
     c                   endsl
      *
      * Behandle ordrehode
      *
     c                   dow       *in61 = *off
     c     sofirm        cabne     wwfirm        xferdi                         Nytt firma
     c     soaarr        cabne     dsaarr        xferdi                         Nytt år
      *
     c                   eval      fntrl1_numm = sonumm
     c                   eval      fntrl1_suff = sosuff
     c     fntrl1_key    chain     fntrl1r
     c                   if        not %found
     c                   goto      xtag02
     c                   endif
      *
     c                   if        dsffnr = *zero
     c                   if        dstfnr = *zero
     c                   if        sojour = 1
     c                   goto      xtag02
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        dsffnr = *zero
     c                   if        dstfnr = *zero
     c                   if        sojour = 0
     c                   goto      xtag01
     c                   endif
     c                   endif
     c                   endif
      *
     c     sobinr        cabgt     dstfnr        xferdi                         Tilnr. passert
      *
     c     xtag01        tag
      *
     c                   exsr      xajour
     c                   exsr      xprint
      *
     c     xtag02        tag
      *
     c                   read      sohel1                                 61
     c                   enddo
      *
     c     xferdi        tag
     c                   write     t1tot                                30
      *
      * Ferdig
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter/Oppdatering av NUMMER-REGISTER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     hntnum        begsr
      *
     c                   eval      wpfell = 'FJOURN'
     c                   eval      wptype = *blank
     c                   eval      wpfast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    wpkode
     c                   parm                    wwfirm
     c                   parm                    wpfell
     c                   parm                    wptype
     c                   parm                    wpfast
6.nu c                   parm                    wpnumm
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XAJOUR Klargjør data for print                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xajour        begsr
      *
     c                   eval      a1fkda = 0
     c                   eval      a1ffda = 0
      *
      * Snu fakturadato
      *
     c                   if        sofkda <> *loval
     c     *dmy          move      sofkda        a1fkda
     c                   endif
      *
      * Snu forfallsdato
      *
     c                   if        soffda <> *loval
     c     *dmy          move      soffda        a1ffda
     c                   endif
      *
      * Hente inn kunderegister
      *
     c                   eval      wwkund = sokund
     c     key04         chain     rkunl1r                            64
     c                   eval      a1navn = rknavn
      *
      * Øvrige felter til print
      *
     c                   eval      a1binr = sobinr
     c                   eval      a1numm = sonumm
     c                   eval      %subst(a1suff:2:2) = %editc(sosuff:'X')
     c                   eval      a1otyp = sootyp
     c                   eval      a1kund = sokund
     c                   eval      a2navn = sonavn
      *
     c                   z-add     sofsum        a1fsum
     c                   add       sofsum        t1fsum
      *
      * Oppdater historisk faktura-register dersom
      * fra - til - nummer ikke er oppgitt og posten
      * ikke har vært med på journal tidligere.
      *
     c                   if        dsffnr = *zero
     c                   if        dstfnr = *zero
     c                   if        sojour = *zero
     c                   eval      wwbinr = sobinr
     c                   eval      wwnumm = sonumm
     c                   eval      wwsuff = sosuff
     c     key01         chain     sohelur                            61
     c                   if        *in61 = *off
     c                   eval      sojour = 1
6.10 c                   time                    soedat
6.10 c                   time                    soetim
6.10 c                   eval      soeusr = dsuser
     c                   update    sohelur
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XPRINT Skriver fakturajournal                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xprint        begsr
      *
      * Skrive fakturalinjer
      *
     c                   write     a1det                                30
      *
      * Er det overflow ?
      *
     c                   select
     c                   when      *in30 = *on
     c                   exsr      xovrfl
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xstrsr        begsr
      *
     c                   z-add     1             fcycle
      *
      * Legger inn firma i nøkkel
      *
     c                   eval      wwfirm = dsfirm
     c                   eval      a1firm = dsfirm
      *
      * Legger inn år i nøkkel
      *
     c                   eval      wwaarr = dsaarr
      *
      * Klargjøre resterende
      * nøkkelbegrep
      *
     c                   eval      wwbinr = dsffnr
     c                   eval      wwnumm = *zero
     c                   eval      wwsuff = *zero
      *
      * Hent neste ledige nummer fra nummer-register
      *
     c                   if        dsffnr = *zero
     c                   if        dstfnr = *zero
     c                   eval      wpnumm = *zero
     c                   eval      wpkode = '0'
     c                   exsr      hntnum
     c                   eval      a1list = wpnumm
     c                   endif
     c                   endif
      *
      * Les første post
      *
     c     key01         setll     sohel1
     c                   read      sohel1                                 61
      *
      * Skriv første heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wwruti = *blank
     c                   eval      wpkode = *blank
     c                   eval      wpfell = *blank
     c                   eval      wptype = *blank
     c                   eval      wpfast = *blank
     c                   eval      wpnumm = *zero
      *
      * Key for Heading-file
      *
     c     key01         klist
     c                   kfld                    wwfirm
     c                   kfld                    wwaarr
     c                   kfld                    wwbinr
     c                   kfld                    wwnumm
     c                   kfld                    wwsuff
      *
      * Key for kunderegister
      *
     c     key04         klist
     c                   kfld                    wwfirm
     c                   kfld                    wwkund
      *
      * Key for formular-register
      *
     c     key10         klist
     c                   kfld                    wwruti
      *
      * Hente inn formular-register
      *
     c                   eval      wwruti = dsruti
      *
     c     key10         chain     aforl1r                            69
      *
      * Key for kort/fakt trans/numm
      *
     c     fntrl1_key    klist
     c                   kfld                    wwfirm
     c                   kfld                    fntrl1_numm
     c                   kfld                    fntrl1_suff
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    wpparm
      *
     c                   eval      dsaarr = *zero
     c                   eval      dsotyp = *blank
     c                   eval      dsffnr = *zero
     c                   eval      dstfnr = *zero
     c                   eval      dsparm = wpparm
      *
      * Legger inn skjermid. og bruker i faste felter
      *
     c                   eval      a1user = dsuser
     c                   eval      a1wsid = dswsid
     c                   eval      a1date = udate
     c                   time                    utime
     c                   eval      a1time = utime
     c                   movel     l_fnav        a1fnav
      *
      *
     c                   endsr
