     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK603R
      * Beskrivelse . . : Betalingsbonus - kvitteringsliste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211011 bsa  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekbti2    if   e           k disk    rename(ekbtst:ekbti2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
      *
     fek603p    o    e             printer oflind(*in30)
      *
      * Definering av parametre
      *
     d p_pfir          s              3
     d p_paar          s              4
     d p_tbot          s              2
     d w_tbot          s              2
     d b_bonu          s              1    inz(*off)
      *
      * Definering av variabler i nøkler
      *
     d ekbti2_tbot     s                   like(ektbot)
     d rkunl1_kund     s                   like(rkkund)
     d vbotl1_tbot     s                   like(vatbot)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ektfir)
     d w_kund          s              6  0
     d w_bgrl          s             11  2
     d w_aar           s              4  0
      *
      * Parameter overføringer
      *
     d                uds
      *
      *  Hva skal skrives på liste                                Valg: J/N=Ja/N
     d pabon                 330    330                                         Alle m/avtale
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980                                         Navn
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_bonu = *off
      * Skriv ut heading
     c                   eval      a1aarr = p_paar
     c                   write     a1hdr
      *
     c                   eval      t1subg = 0
     c***                eval      t1subn = 0
      * Leser alle kunde med angitt bonustype
     c                   exsr      kal_kund
      *
     c                   if        b_bonu = *on
     c                   write     t1tot
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_kund      begsr
      *
     c                   eval      w_tbot = *blank
     c                   eval      w_kund = 0
      * Hent bonustypetekst
     c                   exsr      finn_btext
      *
     c                   eval      ekbti2_tbot  =  p_tbot
     c     ekbti2_key    setll     ekbti2
     c     ekbti2_key    reade     ekbti2
      *
     c                   dow       not  %eof(ekbti2)
      *
      * Hent kundetekst
     c                   if        w_kund <> ektbku
     c                   exsr      finn_ktext
     c                   endif
      *
     c                   eval      d1bgru = ektbgr
     c**                 eval      d1sbon = ektsbo
      *
     c***                if        ektbgr = 0 and
     c***                          w_bgrl = 0
     c***                goto      nyles2
     c***                endif
      *
     c                   eval      t1subg = t1subg + ektbgr
     c**                 eval      t1subn = t1subn + ektsbo
     c                   write     d1det                                30
     c                   eval      b_bonu = *on
     c                   if        *in30 = *on
     c                   exsr      xovrfl
     c                   endif
      *
     c     ekbti2_key    reade     ekbti2
     c                   enddo
      *
     c                   endsr
      *--------------------------------------------------------------
      * Finn kundetekst
      *--------------------------------------------------------------
     c     finn_ktext    begsr
      *
     c                   eval      w_kund = ektbku
     c                   eval      d1kund = ektbku
     c                   eval      d1knvn = *blank
     c                   eval      rkunl1_kund = ektbku
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   movel     rknavn        d1knvn
     c**                 eval      d1kkat = rkkatg
     c                   endif
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Finn tekst for bonustype
      *--------------------------------------------------------------
     c     finn_btext    begsr
      *
     c                   eval      w_tbot = p_tbot
     c                   eval      d1btyp = p_tbot
     c                   eval      d1btxt = *blank
     c                   eval      vbotl1_tbot = p_tbot
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   movel     vatbes        d1btxt
     c                   endif
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Rutine for håndtering av ny side                            *
      *--------------------------------------------------------------
     c     xovrfl        begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_pfir
     c                   parm                    p_paar
     c                   parm                    p_tbot
      *
      * Key for oppslag kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti2_tbot
      *
      * Henter parameter
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
