     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JO731R
      * Beskrivelse . . : Match, maskinell på Nobbnr
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180104 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl3    if   e           k disk    rename(jmatpfr:jmatl3r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
      *
      * Definering av variable i nøkler
      *
     d jmatl3_vkod     s                   like(jovkod)
     d jmatlu_var1     s                   like(jovar1)
     d jvarl3_nobb     s                   like(jvnobb)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      jmatl3_vkod = 0
     c     jmatl3_key    setll     jmatl3
     c     jmatl3_key    reade     jmatl3
     c                   dow       not %eof
      *
     c                   if        jonobb = 0
     c                   goto      les_neste
     c                   endif
      *
     c                   if        jokode <> 0
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      jvarl3_nobb = jonobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   goto      les_neste
     c                   endif
      *
      * Oppdaterer matcheregister med matchet vare
      *
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur
     c                   if        %found
     c                   eval      jovar2 = jvvare
     c                   eval      joldo2 = jvldor
     c                   eval      jokode = 4
     c                   update    jmatlur
     c                   endif
      *
     c     les_neste     tag
      *
     c     jmatl3_key    reade     jmatl3
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for les på matcheregister
      *
     c     jmatl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl3_vkod
      *
      * Key for oppdatering av matcheregister
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
