     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV151R
      * Beskrivelse . . : Vare, bytting av priskode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151003 HKO  Nytt program
6.31  *       070217 bof  Det settes kode ut fra prisgiver
6.32  *       111017 bkv  Rettet på filter
6.33  *       260218 bof  hvis kun lev.nr i parameter, så ikke sjekk andre
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 fjvlel3    if   e           k disk    rename(jvlepfr:jvlel3r)
6.31 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarlg    if   e           k disk    rename(jvarpfr:jvarlgr)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s                   like(jvldor)
     d p_ogrp          s                   like(jvogrp)
     d p_hgrp          s                   like(jvhgrp)
     d p_ugrp          s                   like(jvugrp)
     d p_modn          s                   like(jvmodn)
     d p_pkod          s                   like(jvdpko)
     d b_ok            s              1    inz(*off)
      *
      * Definering av variable i nøkler
      *
6.31 d jvlel3_lldo     s                   like(jvlldo)
     d jvarlg_modn     s                   like(jvmodn)
6.31 d jvarl1_vare     s                   like(jvvare)
     d jvdtlu_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kaller subruiner for oppdatering av priskode
      *
     c                   select
     c                   when      p_ldor <> 0
     c                   exsr      behandl_ldor
     c                   other
     c                   exsr      behandl_modn
     c                   endsl
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av varer for leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_ldor  begsr
6.31 c                   eval      jvlel3_lldo = p_ldor
6.31 c     jvlel3_key    setll     jvlel3
6.31 c     jvlel3_key    reade     jvlel3
6.31 c                   dow       not %eof(jvlel3)
6.31 c                   eval      jvarl1_vare = jvlvnr
6.31 c     jvarl1_key    chain     jvarl1
6.31 c                   if        %found(jvarl1)
6.31  * sjekke andre parameter
6.33 c                   if        p_modn <> 0 or
6.33 c                             p_ogrp <> 0 or
6.33 c                             p_hgrp <> 0 or
6.33 c                             p_ugrp <> 0
6.31 c                   exsr      sjekk_lest
6.33 c                   else
6.33 c                   if        jvlldo = p_ldor
6.33 c                   eval      b_ok = *on
6.33 c                   endif
6.33 c                   endif
6.31 c                   if        b_ok = *on
6.31 c                   exsr      oppdater
6.31 c                   endif
6.31  *
6.31 c                   endif
6.31 c     jvlel3_key    reade     jvlel3
6.31 c                   enddo
      *
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av varer for modul
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_modn  begsr
      *
     c                   eval      jvarlg_modn = p_modn
     c     jvarlg_key    setll     jvarlg
     c     jvarlg_key    reade     jvarlg
     c                   dow       not %eof
      *
     c                   if        p_ldor = 0 or
     c                             p_ldor <> 0 and
     c                             p_ldor = jvldor
     c                   exsr      oppdater
     c                   endif
      *
     c     jvarlg_key    reade     jvarlg
     c                   enddo
      *
     c                   endsr

6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for oppdatering av priskode på vare
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_lest    begsr
6.32 c                   eval      b_ok = *off
6.31 c
6.32 c                   if        p_modn <> 0
6.32 c                   if        p_modn = jvmodn
6.31 c                   eval      b_ok = *on
6.32 c                   endif
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   if        p_ugrp <> 0 and
6.31 c                             p_hgrp <> 0 and
6.32 c                             p_ogrp <> 0
6.32 c                   if        p_ugrp = jvugrp and
6.31 c                             p_hgrp = jvhgrp and
6.31 c                             p_ogrp = jvogrp
6.31 c                   eval      b_ok = *on
6.32 c                   endif
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   if        p_hgrp <> 0 and
6.32 c                             p_ogrp <> 0
6.32 c                   if        p_hgrp = jvhgrp and
6.31 c                             p_ogrp = jvogrp
6.31 c                   eval      b_ok = *on
6.32 c                   endif
6.31 c                   leavesr
6.31 c                   endif
6.32 c                   if        p_ogrp <> 0
6.32 c                   if        p_ogrp = jvogrp
6.31 c                   eval      b_ok = *on
6.32 c                   endif
6.31 c                   leavesr
6.31 c                   endif
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av priskode på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   clear                   jvdtlur
      *
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur
      *
     c                   eval      jvdpko = p_pkod
      *
     c                   if        %found
     c                   update    jvdtlur
     c                   else
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_modn
     c                   parm                    p_pkod
      *
      * Key for les på vare m/leverandør
      *
6.31 c     jvlel3_key    klist
6.31 c                   kfld                    jvlel3_lldo
6.31  *
6.31  * Key for les på vare
6.31  *
6.31 c     jvarl1_key    klist
6.31 c                   kfld                    jvarl1_vare
      *
      * Key for les på vare m/modulnr
      *
     c     jvarlg_key    klist
     c                   kfld                    jvarlg_modn
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
