     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO736R
      * Beskrivelse . . : Plukke ordrenummer / Vise ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.31  *       201200 AMD  Sperret for plukk til lager 0 dersom dette ikke
      *                   finnes i koderegister
3.32  * R3.32 050301 AMD  Bygget inn styringen fra info-register, dersom
      *                   ordre skal stoppes før fakturering.
3.33  * R3.32 070301 AMD  Begrensning på hvem som kan kjøre faktureringm
3.34  * R3.34 080301 AMD  Lagt inn kontroll på plukking til gyldig ordretype
3.35  * R3.35 261001 AMD  Firmanummer ble ikke lagt inn i nøkkel for
3.35  *                   bruker-register (FUSRPF)
5.31  * R5.31 100104 ASK  Lagt inn endringer for lager på linjenivå.
      *                   Foreløpig er mulighet for endring av lager
      *                   ved utplukk sperret.
5.41  * R5.41 190106 AMD  Ved flere firma i samme fil, ble alle poster
      *                   vist dersom man sto inne på firmanummer med
      *                   lavest verdi.
5.51  * R5.51 300806 BOF  Sjekker mot RadioT-plukk-fil.
5.60  * R5.62 070108 bof  Utvidet nøkkel med år                        e
5.63  * R5.63 230508 bof  Utvidet med mulighet for å legge på lev.dato
5.64  * R5.64 200608 bof  Lagt på logg 0, sier at den er sendt til plukking
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  181109 bof  Fjernet F1 i D1 bilde, skal alltid være som F1
6.12  * 6.10  240511 AMD  Flere muligheter rundt kredit-alternativer
6.20  * 6.20  280212 bof  Hvis kreditsperre (rkkres=1) skal ikke kunne
      *                   skrive pakkseddel eller faktura
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * R6.30 220316 nho  Feil ved bla tilbake
7.xx  * 7.00  150916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  090519 BKV  (Kirkenes / Tana) ønsker å kunne plukke til HK
7.01  *                   selv om ordretypen har behandlingskode 2
7.02  * 7.00  301019 BKV  (Gipling) Hvis PN eller /ikke int.kund + PI :
7.02  *                   legg *H i info-type
7.03  * 7.00  301019 BKV  Legg på info kode ved overgang otyp1 til otyp2
7.04  *       050220 bkv  Newlook vask
7.05  *       190320 ask  Lagt inn fargestyring basert på kode i sortimentfeltet på VVARPF
7.05  *                   Dette er switch-styrt med koden kode "FARGE_SKAFF_SORTIMENT"
7.05  *                   Alternativt settes farge på skaffevarer
      *
7.06  * R6.30 041016 nho  Memosaldo + ordresum sjekkes mot kr.grense
7.06  *                   Eller memosaldo mot kr.grense
7.06  * MGR   050117 sst  Tilpasset bev.memosaldo og utv.kred.grense MGR
7.06  *       170117 sst  Hente parameter meosaldo fra AFPSPF
7.07  *       140918 sst  Kun tillate ordretype 'T' ved tilbudskunde
      *
7.fb  * 7.00  220217 bof  Kontantkunde - tillate hvis forhåndsbetalt
7.f1  * K000  220620 bsa  Skal ikke være mulig å plukke til pakkseddel
7.f1  * K000              hvis ikke dep er betalt.
7.f2  * K000  060121 bsa  Skal ikke stoppe hvis ikke kreditkunde/kontankunde
7.f3  * K000  151121 bof  Skal kun teste på 2 (ikke lov å kjøre pakkseddel)
      *
8.01  * K000  020222 bsa  Håndterer SQL så det ikke låses
8.02  * K000  221122 sst  Flyttet ant memo-dager og utv.kreditgrense til  UTVIDET_MEMOSALDO (nx)
8.03  * K000  280423 bsa  Hvis kontantkunde må beløpet være fullt innbetalt, for at
8.03  * K000              det skal være lov å plukke til pakkseddel.
8.04  * K000  301023 bsa  Hvis det er kontantkunde, skal det ikke kunne plukkes  til
8.04  * K000              annet enn tilbud. Justert endring 7.07. Tester ikke lenger
8.04  * K000              på hardkodet kundenummer.
8.05  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.06  *K000   090824 bsa  Etter plukking, kjør gjennoppbygging av memosaldo.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffo736d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
     f                                     sfile(d1sfl:srrn02)
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffplolu    uf a e           k disk    rename(fplopfr:fplolur)
     ffplol1    if   e           k disk    rename(fplopfr:fplol1r)
     ffplolo    if   e           k disk    rename(fplopfr:fplolor)
     ffplllu    uf a e           k disk    rename(fpllpfr:fplllur)
     ffplll1    if   e           k disk    rename(fpllpfr:fplll1r)
3.32 fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
3.33 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
3.34 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
5.51 flwpll2    if   e           k disk    rename(lwplpfr:lwpll2r)
6.12 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.06 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
8.02 f*afpslr    if   e           k disk    rename(afpspfr:afpslrr)
7.fb fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
7.fb ffst2l1    if   e           k disk    rename(fst2pfr:fst2l1r)
      *
3.32  *
3.32  * Definering av variable i nøkler
3.32  *
3.32 d vinfl1_ityp     s                   like(vaityp)
3.33 d fusrl1_user     s                   like(fbuser)
5.51 d lwpll2_numm     s                   like(mknumm)
5.51 d lwpll2_suff     s                   like(mksuff)
6.12 d rkunl1_kund     s                   like(rkkund)
8.02 d*afpslr_ufil     s                   like(l_filg)
8.02 d*afpslr_uide     s                   like(afuide)
7.fb d sdepl1_kund     s                   like(skkund)
7.fb d sdepl1_numm     s                   like(sknumm)
7.fb d sdepl1_tell     s                   like(sktell)
7.fb d fplll1_lonr     s                   like(fklonr)
7.fb d fplll1_lsuf     s                   like(fklsuf)
      *
      * Definering av variable
      *
     d w_alf1          s              1
3.32 d w_firm          s                   like(fofirm)
3.34 d w_on01          s                   like(vaon01)
3.34 d w_on02          s                   like(vaon02)
3.34 d w_on03          s                   like(vaon03)
3.34 d w_on04          s                   like(vaon04)
3.34 d w_on05          s                   like(vaon05)
3.34 d w_on06          s                   like(vaon06)
3.34 d w_onxt          s                   like(vaonxt)
5.60 d w_aarr          s              4  0
     d p_kode          s              1
5.63 d w_ldat          s                   like(foldat)
5.63 d w_stat          s              1
5.64 d p_urec          s             80
5.64 d p_stat          s              1
7.fb d b_depok         s              1
7.fb d b_d100          s              1
7.fb d w_sumi          s             11  2
7.fb d w_sumu          s             11  2
7.fb d w_avik          s             11  2
7.fb d w_mld1          s             50
8.06 d w_gogr          s              1  0
8.06 d w_nogr          s              1  0
5.64  *
5.64 d                 ds
5.64 d d_urec                        80
5.64 d  d_ufir                        3  0 overlay(d_urec)
5.64 d  d_aarr                        4  0 overlay(d_urec:4)
6.nu d  d_unum                        8  0 overlay(d_urec:8)
6.nu d  d_usuf                        2  0 overlay(d_urec:16)
6.nu d  d_ukod                        1    overlay(d_urec:18)
6.nu d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:37)
6.nu d  d_wsid                       10    overlay(d_urec:47)
      *
      *
     d                uds
     d dswsid                901    910
     d dsuser                911    920
7.00 d l_filg                931    933
     d dssfir                944    946  0
     d l_fnav                951    980
     d dssyst                999    999
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d hedfir          s                   like(fofirm)
6.NU d hednum          s                   like(fonumm)
     d hedsuf          s                   like(fosuff)
     d linfir          s                   like(fdfirm)
     d linlin          s                   like(fdline)
6.NU d linnum          s                   like(fdnumm)
     d linsuf          s                   like(fdsuff)
     d otyfir          s                   like(vaofir)
     d otytyp          s                   like(vaotyp)
     d pllfir          s                   like(fklfir)
     d plllin          s                   like(fkllin)
     d pllnum          s                   like(fklonr)
     d pllsuf          s                   like(fklsuf)
     d plofir          s                   like(fkpfir)
     d plonum          s                   like(fkponr)
     d plosuf          s                   like(fkpsuf)
     d potfir          s                   like(fofirm)
     d potnum          s                   like(fonumm)
     d pototy          s                   like(footyp)
     d potsuf          s                   like(fosuff)
     d sfrn01          s                   like(srrn01)
     d sfrn02          s                   like(srrn02)
     d spge01          s              3  0
     d spge02          s              3  0
     d srrn01          s              4  0
     d srrn02          s              4  0
     d ssrn01          s                   like(srrn01)
     d ssrn02          s                   like(srrn02)
     d wpfirm          s                   like(fofirm)
     d wpftor          s              1
     d wpipor          s              1
     d w_jkod          s              2  0
6.NU d wpnumm          s                   like(fonumm)
     d wpplor          s                   like(w_alf1)
     d w_retk          s              1
     d w_jtxt          s             20
     d wpsuff          s                   like(fosuff)
     d wseqen          s              1
     d wwoutq          s             10
     d wxnumm          s                   like(fkponr)
     d wxotyp          s                   like(fkpoty)
     d wxpdir          s              1  0
     d wxsuff          s                   like(fkpsuf)
7.03 d w_otyp1         s                   like(footyp)
7.03 d w_otyp2         s                   like(footyp)
7.03 d w_ityp          s                   like(foityp)
7.03 d w_pos           s              2  0
7.04 d w_len           s              3  0
7.04 d w_teller        s              3  0
7.05 d u_fsor          s              1    inz(*off)                            Fargekode fra sortim
7.05 d u_fskv          s              1    inz(*off)                            Farge på skaffvare
7.05 d w_fso1          s             10
7.05 d w_fso2          s             10
7.05 d w_sort          s             10
7.06 d w_mvak          s              1
7.06 d w_mvtx          s             30
7.06 d w_date          s               d   datfmt(*iso)
7.06 d w_mvas          s              6  2
7.06 d w_mvat          s              5  4
7.06 d w_mvaf          s             10  9
7.06 d w_bpro          s              5  2
7.06 d w1grns          s             11  2
7.06 d w1mems          s                   like(rkmems)
7.06 d w2mems          s                   like(rkmems)
7.06 d w3mems          s                   like(rkmems)
7.06 d w1sums          s                   like(rkmems)
7.06 d w1bere          s                   like(rkmems)
7.06 d w2bere          s                   like(rkmems)
7.06  *w_utvgrns = utvidelse av memosaldo(Mestergruppen)
7.06 d w_utvgrns       s              2  0
7.06  *w_memodagr = antall dager ordre skal med i memosaldo
7.06 d w_memodagr      s              3  0
7.06 d w_kgrn          s                   like(rksald)
7.06 d w_ksal          s                   like(rksald)
7.06 d w_3x            s              3
7.06 d p_fir           s              3
7.06 d p_kun           s              6
      *
      *---------------------------------------------------------------------
      * Stand Alone Fields - BOTTOM
      *---------------------------------------------------------------------
8.01  *
8.01 C/Exec SQL
8.01 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
8.01 C+             commit=*none
8.01 C/End-Exec
8.01  *
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
7.02 d u_702           s              1    inz(*off)
7.03 d u_703           s              1    inz(*off)
7.06 d u_706           s              1    inz(*off)
7.07 d u_707           s              1    inz(*off)
8.04 d u_til           s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Velg om det skal plukkes eller vises                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     wpnumm        cabeq     *zero         b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  A1 Behandler plukking av ordrenummer                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     a1taga        tag
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c                   move      wpnumm        hednum
     c                   move      wpsuff        hedsuf
     c     hedkey        chain     fohel1r                            90
      *
      *  Hent opplysninger fra ORDREYTYPE-REGISTER
      *
     c                   move      footyp        otytyp
     c     otykey        chain     votyl1r                            90
      *
8.06 c                   eval      w_gogr = vaoogr
6.12  *
6.12  *  Hent opplysninger fra Kunderegister
6.12  *
6.12 c                   eval      rkunl1_kund = fokund
6.12 c     rkunl1_key    chain     rkunl1r
7.06  *  Opphenting av meosaldo og beregning av ev utvidet kr.grense gjøres
7.06  *      her?  Sjekk om det er nok?
7.06  * Beregne memosaldo pr dato
7.06 c                   if        w_memodagr > 0
7.06 c                   move      w_firm        p_fir
7.06 c                   move      rkunl1_kund   p_kun
7.06 c                   call      'FO763R'
7.06 c                   parm                    p_fir
7.06 c                   parm                    p_kun
7.06 c                   endif
3.34  *
3.34  *  Ta vare på verdier fra ordretype-register
3.34  *
3.34 c                   move      vaon01        w_on01
3.34 c                   move      vaon02        w_on02
3.34 c                   move      vaon03        w_on03
3.34 c                   move      vaon04        w_on04
3.34 c                   move      vaon05        w_on05
3.34 c                   move      vaon06        w_on06
3.34 c                   move      vaonxt        w_onxt
      *
      *  Legger inn opplysningene i skjermbildefeltene
      *
     c                   move      fonumm        a1numm
     c                   move      fosuff        a1suff
     c                   move      footyp        a1foty
     c                   move      folage        a1lage
     c                   move      fokuna        a1kuna
     c                   move      vaotxt        a1ftxt
      *
     c                   move      vaonxt        a1otyp
     c                   eval      a1vlin = *zero
     c                   eval      a1till = *zero
5.63 c                   eval      a1pluk = *zero
     c                   eval      a1ptxt = *zero
5.51  *
5.51  * Sjekk om det er foretatt plukking på Radioterminal
5.51  *
5.51 c                   exsr      sjk_rt
7.fb  *
7.fb  * sjekke depositum
7.fb  *
7.fb c                   exsr      sjekk_depo
      *
      *  Lager skal bare vises i utplukksbilde
      *  dersom akkumulatortype er = 1
      *
     c                   move      *off          *in49
5.31 c*                  if        vaoakk = 1
5.31 c*                  move      *on           *in49
5.31 c*                  endif
      *
      *  Hent, Tekst for lager
      *
     c                   exsr      sblagr
      *
      *  Send Alt
      *
     c                   write     a1bld
      *
      *  Bare data
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   move      '9'           wpplor
     c                   goto      xslutt
     c                   endif
      *
      *  F4=Spørring ordretype
      *
     c                   if        *inkd = *on
     c                   if        w_arcd = 'A1BLD'
     c                   if        w_afld = 'A1OTYP'
     c                   call      'VA550R'
     c                   parm                    hedfir
     c                   parm                    a1otyp
     c                   goto      a1tagb
     c                   endif
     c                   endif
     c                   endif
      *
      *  F4=Spørring lager
      *
     c                   if        dssyst = 'A'
     c                   if        *inkd = *on
     c                   if        w_arcd = 'A1BLD'
     c                   if        w_afld = 'A1LAGE'
     c                   call      'RA510R'
     c                   parm                    wpfirm
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
     c                   move      w_jkod        a1lage
     c                   move      w_jtxt        a1latx
     c                   goto      a1tagb
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      *  Sjekk,  Finnes ordretype i ORDRETYPE-REGISTER
      *
     c                   move      a1otyp        otytyp
     c     otykey        chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   move      *on           *in32
     c                   goto      a1tagb
     c                   endif
7.07  *
7.07  * 1  MGR.  Ikke lov med annet enn ordretype 'T' på tilbudskunde
7.07  *
8.04 c**                 if        u_707 = *on
8.04 c**                 if            fokund = 59998
8.04 c                   if        u_til = *on
8.04 c                   if        fokund = faakun
7.07 c                             and a1otyp <> 'T'
7.07 c                   eval      *in68 = *on
7.07 c                   goto      a1tagb
7.07 c                   endif
7.07 c                   endif
3.34  *
3.34  *  Sjekk,  Er oppgitt til-ordretype gyldig ?
3.34  *
3.34 c                   if        faanxt =  1
3.34 c                   if        a1otyp <> w_on01 and
3.34 c                             a1otyp <> w_on02 and
3.34 c                             a1otyp <> w_on03 and
3.34 c                             a1otyp <> w_on04 and
3.34 c                             a1otyp <> w_on05 and
3.34 c                             a1otyp <> w_on06 and
3.34 c                             a1otyp <> w_onxt
3.34 c                   move      *on           *in33
3.34 c                   goto      a1tagb
3.34 c                   endif
3.34 c                   endif
7.fb  *
7.fb  * hvis depositum er 100 % og betalt, kan pakkseddel kjøres
7.fb c                   if        b_depok = *off
6.12  *
6.12  *  Sjekk,  Kontantkunde kan ikke plukkes til pakkseddel
6.12  *
7.01 c                   if        u_701 = *on
6.12 c                   if        vaoakk >  1 and
7.01 c                             rkkres =  2 and
7.01 c                             a1otyp <> 'HK'
6.12 c                   eval      *in36  =  *on
6.12 c                   goto      a1tagb
6.12 c                   endif
7.01 c                   else
7.f1 c*                  if        vaoakk >  1 and
7.f1 c*                            rkkres =  2
7.f3 c*                  if        rkkres >  0 and
7.f3 c                   if        rkkres =  2 and
7.f1 c                             vaoakk >  1
6.12 c                   eval      *in36  =  *on
6.12 c                   goto      a1tagb
6.12 c                   endif
7.01 c                   endif
6.20  *
7.fb c                   endif
6.20  *
6.20  *  Sjekk,  Kunde med kredittsprerre kan ikke plukkes til pakkseddel
6.20  *
6.20 c                   if        vaoakk >  1 and
6.20 c                             rkkres =  1
5.55 c                   exfmt     a1msg1
6.20 c                   goto      a1tagb
6.20 c                   endif
7.06  *
7.06  *  Sjekk ordresum/memosaldo mot kreditsperre
7.06  *
7.06 c                   if        u_706 = *on
7.06 c                   if        footyp = 'HO'
7.06  * Hent mva % for beregning av ordreverd inkl. mva
7.06  *
7.06 c                   eval      w_stat = *blank
7.06 c                   eval      w_mvak = '3'
7.06 c                   time                    w_date
7.06  *
7.06 c                   call      'RS205R'
7.06 c                   parm                    w_stat
7.06 c                   parm                    w_mvak
7.06 c                   parm                    w_mvtx
7.06 c                   parm                    w_date
7.06 c                   parm                    w_mvas
7.06 c                   parm                    w_mvat
7.06 c                   parm                    w_mvaf
7.06 c                   parm                    w_bpro
7.06  * Beregne memosaldo pr dato
7.06 c                   if        w_memodagr > 0
7.06 c                   move      w_firm        p_fir
7.06 c                   move      rkunl1_kund   p_kun
7.06 c                   call      'FO763R'
7.06 c                   parm                    p_fir
7.06 c                   parm                    p_kun
7.06 c                   endif
7.06  *
7.06 c     rkunl1_key    chain     rkmel1
7.06 c                   eval      w1mems = fotots * w_mvat
7.06sc                   eval      w1mems = w1mems + rksald
7.06sc                   eval      w1mems = w1mems + rkmems * w_mvat
7.06 c                   eval      w3mems = rkmems
7.06  *
7.06  *  Denne sekvensen må justeres m/Nina
7.06  *
7.06  * Sjekk, er kredittgrense overskredet
7.06  *
7.06 c                   if        rkgrns <> 0
7.06 c                   eval      w_ksal = rksald +
7.06 c                                      (rkmems * w_mvat) -
7.06 c                                      (fotots - fototr) * w_mvat
7.06  *   Utvide kreditgrense   (Mestergruppen løsning 2017_01)  sst
7.06 c                   if        w_ksal < rkgrns * 1000
7.06 c                   eval      w_kgrn = rkgrns * 1000 *
7.06 c                                      (1+w_utvgrns/100)
7.06 c                   else
7.06 c                   eval      w_kgrn = rkgrns * 1000
7.06 c                   endif
7.06 c                   eval      w_ksal = w_ksal +
7.06 c                                      (fotots - fototr) * w_mvat
7.06sc                   eval      w1grns = w_kgrn
7.06sc                   eval      w1mems = w_ksal
7.06 c                   else
7.06sc                   eval      w1grns = rkgrns*1000
7.06 c                   endif
7.06  *
7.06  *   Hit m/Nina
7.06  *
7.06 c                   if        a1vlin = '1' or
7.06 c                             a1vlin = '2'
7.06 c                   if        rkgrns <> 0
7.06 c                   if        w1mems > w1grns
7.06 c     w1mems        sub       w1grns        f2over
7.06 c                   if        rkmems < w1grns
7.06 c                   eval      f2over = 0
7.06 c                   goto      iktes1
7.06 c                   else
7.06 c                   exfmt     a1msg2
7.06 c                   endif
7.06 c                   if        *inks
7.06 c                   goto      iktes1
7.06 c                   else
7.06 c                   goto      a1tagb
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
7.06  *
7.06 c                   if        a1vlin = '0'
7.06sc                   eval      w1mems = w1mems +
7.06sc                                      (fotots - fototr) * w_mvat
7.06 c                   if        w1mems > w1grns
7.06 c     w1mems        sub       w1grns        f2over
7.06 c                   if        rkkgrk = 'J'
7.06 c                   exfmt     a1msg3
7.06 c                   goto      a1tagb
7.06 c                   endif
7.06 c                   exfmt     a1msg2
7.06 c                   if        *inks
7.06 c                   goto      iktes1
7.06 c                   endif
7.06 c                   goto      a1tagb
7.06 c                   endif
7.06 c                   endif
7.06  *
7.06 c                   endif
7.06 c                   endif
7.06  *
7.06 c     iktes1        tag
      *
      *  Sjekk,  Finnes lager i KODE-REGISTER
      *
     c                   eval      a1latx = *blank
3.31 c***                if        a1lage <> *zero
     c                   exsr      sblagr
     c                   if        w_retk <> *blank
     c                   move      *on           *in34
     c                   goto      a1tagb
     c                   endif
3.31 c***                endif
      *
      *  Dersom noen valg er endret, sendt bilde pånytt
      *
     c                   if        a1lage <> folage
     c                   move      a1lage        folage
     c                   goto      a1tagb
     c                   endif
3.32  *
3.32  * Hent inn ordre-info-kode i inforegister
3.32  *
3.32 c                   eval      vinfl1_ityp = foityp
3.32 c     vinfl1_key    chain     vinfl1r                            98
3.32 c                   if        *in98  = *on
3.32 c                   eval      vaifak = *blank
3.32 c                   endif
      *
      *  Dersom ordre har en infokode, og dette er fakturering,
3.32  *  sjekkes det om fakturering skal stoppes.
      *
     c                   if        foityp <> *blank
     c                   if        vaoakk =  3
3.32 c                   if        vaifak = '1'
     c                   eval      wpplor = '9'
     c                   goto      xslutt
     c                   endif
     c                   endif
3.32 c                   endif
3.33  *
3.33  *  Kontroll om bruker har lov til å sette igang fakturering.
3.33  *
3.33 c                   if        fbko08 <> 1
3.33 c                   if        vaoakk =  3
3.33 c                   eval      wpplor = '9'
3.33 c                   goto      xslutt
3.33 c                   endif
3.33 c                   endif
      *
      *  Oppdater ORDRE-HODE-REGISTER, Ordren er til behandling
      *
     c                   call      'FO709R'
6.NU c                   parm                    hednum
     c                   parm                    hedsuf
      *
     c     hedkey        chain     fohelur                            90
     c                   if        *in90 = *off
     c                   move      3             fokode
7.02 c                   if        u_702 = *on
7.02 c
7.02  * Legg inn hold kode hvis ordretype = PN
7.02 c                   if        a1otyp = 'PN'
7.02 c                   eval      foityp = faak98
7.02 c                   endif
7.02  * hvis kunde ikke er internkunde og PI, så hold
7.02 c                   if        rkmkod <> 1 and
7.02 c                             rkspfa <> 1 and
7.02 c                             a1otyp = 'PI'
7.02 c                   eval      foityp = faak98
7.02 c                   endif
7.02 c                   endif                                                  u_702
7.03 c                   if        u_703 = *on
7.03 c                   if        footyp = w_otyp1 and a1otyp = w_otyp2
7.03 c                   if        foityp = *blank
7.03 c                   eval      foityp = w_ityp
7.03 c                   endif
7.03 c                   endif
7.03 c                   endif                                                  u_703
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = dsuser
     c                   update    fohelur
     c                   endif
      *
8.06 c                   eval      w_nogr = vaoogr
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER med ny ordre
      *
     c                   move      wpnumm        plonum
     c                   move      wpsuff        plosuf
     c     plokey        chain     fplolur                            90
     c                   if        *in90 = *on
     c                   move      plofir        fkpfir
     c                   move      plonum        fkponr
     c                   move      plosuf        fkpsuf
     c                   move      a1otyp        fkpoty
     c                   move      a1lage        fkplag
     c                   move      dswsid        fkpwsd
     c                   move      dsuser        fkpusr
     c                   eval      fkpalt = *zero
     c                   eval      fkpdir = *zero
     c                   move      a1vlin        fkpres
     c                   move      a1ptxt        fkptxt
     c                   eval      fkpkol = *zero
     c                   eval      fkpnet = *zero
     c                   eval      fkpbru = *zero
     c                   eval      fkplmt = *blank
     c                   eval      fkpfpr = *zero
     c                   eval      fkpfte = *blank
     c                   eval      fkpepr = *zero
     c                   eval      fkpete = *blank
     c                   write     fplolur
     c                   else
     c                   update    fplolur
     c                   endif
      *
      *  Subrutinen for resting av varelinjer skal hentes opp
      *
     c                   if        a1vlin = '1'
     c                             or a1vlin = '2'
     c                   exsr      xd1bld
     c                   endif
      *
      *  Subrutinen for tilleggsopplysninger av varelinjer skal hentes opp
      *
     c                   if        a1till = '1'
     c                   exsr      xb7win
     c                   endif
5.63  *
5.63  *  Subrutinen for leveringsdato og tilleggsinfo 1 og 2
5.63  *
5.63 c                   if        a1pluk = '1'
5.63 c                   exsr      o1subr
5.63 c                   endif
      *
      *  Kaller opp utskrift-program for å sette inn alternativ
      *  og om direktevalg skal benyttes
      *
     c                   call      'FO600R'
     c                   parm                    wpipor
     c                   parm                    plofir
     c                   parm                    plonum
     c                   parm                    plosuf
      *
     c                   move      wpipor        wpplor
8.06  * Hvis plukking fra feks HO til O, må vi reberegne memosaldo
8.06 c                   if        w_gogr = 0 and
8.06 c                             w_nogr = 1
8.06 c                   move      w_firm        p_fir
8.06 c                   move      rkunl1_kund   p_kun
8.06 c                   call      'FO763R'
8.06 c                   parm                    p_fir
8.06 c                   parm                    p_kun
8.06 c                   endif
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B2 Behandler åpningsbildet samt funksjonstaster                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      *  Bare data
      *
     c     b2tagb        tag
     c                   exsr      xdsp01
      *
      *  F1=Vise bla-alternativer
      *
     c                   if        *inka = *on
     c                   exsr      xh1win
     c                   endif
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   move      '9'           wpplor
     c                   goto      xslutt
     c                   endif
      *
      *  F5=Forny
      *
     c                   if        *inke = *on
     c                   exsr      xlsp01
     c                   goto      b2tagb
     c                   endif
      *
      *  F15=Starter utskrift
      *
     c                   if        *inkp = *on
     c                   call      'FO604R'
     c                   parm                    wpftor
     c                   parm                    wpfirm
     c                   parm                    wxnumm
     c                   parm                    wxsuff
     c                   parm                    wxotyp
     c                   parm                    wxpdir
     c                   parm                    wwoutq
     c                   goto      b2tagb
     c                   endif
      *
      *  Roll
      *
     c                   if        *in10 = *on
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Posisjoner
      *  startverdi
      *  på ordrenummer, men rutinen blar bakfra
      *
     c                   if        b2numm <> *zero
     c                   if        wseqen = 'B'
     c                   move      b2numm        plonum
     c                   move      99            plosuf
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *  Posisjoner
      *  startverdi
      *  på ordretype + ordrenummer
      *
     c                   if        b2numm <> *zero
     c                   if        wseqen = 'O'
     c                   move      b2numm        potnum
     c                   eval      potsuf = *zero
     c     potkey        setll     fplolo
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      *  Posisjoner
      *  startverdi
      *  på ordrenummer
      *
     c                   if        b2numm <> *zero
     c                   move      ' '           wseqen
     c                   move      b2numm        plonum
     c                   eval      plosuf = *zero
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   goto      b2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     c                   do        ssrn01
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
      *  Ved valg 2 eller 7, Sjekk om dette er en utskrift
      *  Hvis dette er en utskrift skal den ikke kunne vedlikeholdes
      *
     c                   if        b1valg = '2 '
     c                             or b1valg = '7 '
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c                   move      b1numm        hednum
     c                   move      b1suff        hedsuf
     c     hedkey        chain     fohel1r                            90
      *
     c                   if        fokode <> 3
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   endif
      *
      *  Registrering/Endring av resting
      *
     c                   if        b1valg = '2 '
     c                   move      b1numm        hednum
     c                   move      b1suff        hedsuf
     c                   exsr      xd1bld
     c                   endif
      *
      *  Slette utplukk/Fjerne merke for at ordren er til behandling
      *
     c                   if        b1valg = '4 '
     c                   move      b1numm        plonum
     c                   move      b1suff        plosuf
     c                   exsr      xb4win
     c                   iter
     c                   endif
      *
      *  Registre/Endre tilleggsopplysninger
      *
     c                   if        b1valg = '7 '
     c                   move      b1numm        plonum
     c                   move      b1suff        plosuf
     c                   exsr      xb7win
     c                   iter
     c                   endif
      *
     c                   enddo
      *
      *  Nullstiller/blanker feltet for start f.o.m.
      *
     c                   eval      b2numm = *zero
      *
     c                   goto      b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B4 Behandle bilde for sletting av utvalg                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xb4win        begsr
      *
     c                   move      plonum        b4numm
      *
     c                   write     b4bld
     c                   exfmt     b4bld
      *
      *  F3=Avslutt
      *
     c     *inkc         cabeq     *on           b4tag1
      *
      *  Kaller opp subprogram for å slette utvalg og blanke merking
      *
     c                   call      'FO412R'
     c                   parm                    wpipor
     c                   parm                    plofir
     c                   parm                    plonum
     c                   parm                    plosuf
      *
      *  Oppdater subfile
      *
     c                   eval      b1wsid = *blank
     c                   movel     '*Slettet'    b1wsid
      *
     c     b4tag1        tag
      *
     c                   eval      b1valg = *blank
     c                   update    b1sfl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B7 Behandle bilde for innlegging av tilleggsopplysninger       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xb7win        begsr
      *
      *  Henter inn opplysninger fra UTPLUKK-PARAM-REGISTER
      *
     c     plokey        chain     fplol1                             90
     c                   if        *in90 = *off
     c                   move      fkponr        b7numm
     c                   move      fkpkol        b7koli
     c                   move      fkpnet        b7nett
     c                   move      fkpbru        b7brut
     c                   move      fkplmt        b7lmtx
     c                   move      fkpfpr        b7frpr
     c                   move      fkpfte        b7frte
     c                   move      fkpepr        b7empr
     c                   move      fkpete        b7emte
     c                   endif
      *
     c                   write     b7bld
     c                   exfmt     b7bld
      *
      *  F3=Avslutt
      *
     c     *inkc         cabeq     *on           xb7slt
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER
      *
     c     plokey        chain     fplolur                            90
     c                   if        *in90 = *off
     c                   move      b7koli        fkpkol
     c                   move      b7nett        fkpnet
     c                   move      b7brut        fkpbru
     c                   move      b7lmtx        fkplmt
     c                   move      b7frpr        fkpfpr
     c                   move      b7frte        fkpfte
     c                   move      b7empr        fkpepr
     c                   move      b7emte        fkpete
     c                   update    fplolur
     c                   endif
      *
      *  Oppdater subfile
      *
     c     xb7slt        tag
      *
     c                   if        b1valg <> *blank
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
5.63  *
5.63  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.63  * Oppdatering av tilleggsinformasjon ved kjørekontorløsning
5.63  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.63  *
5.63 c     o1subr        begsr
5.63  *
5.63  * Initierer skjermbilde
5.63  *
5.63 c                   eval      o1ldat = 0
5.63 c                   eval      o1inf1 = foinf1
5.63 c                   eval      o1inf2 = foinf2
5.63  *
5.63 c                   if        foldat <> *loval
5.63 c     *dmy          move      foldat        o1ldat
5.63 c                   endif
5.63  *
5.63  * Send bilde o1win
5.63  *
5.63 c     o1tagb        tag
5.63  *
5.63 c                   exfmt     o1win
5.63  *
5.63  * F3=Avslutt
5.63  *
5.63 c                   if        *inkc or *inkl
5.63 c                   eval      *inkc = *on
5.63 c                   leavesr
5.63 c                   endif
5.63  *
5.63  * F4=Spørring
5.63  *
5.63  *
5.63  * Validering
5.63  *
5.63  *
5.63  * Leveringsdato må oppgis
5.63  *
5.63 c                   if        o1ldat = 0
5.63 c                   eval      *in33 = *on
5.63 c                   goto      o1tagb
5.63 c                   endif
5.63  *
5.63  * Leveringsdato må være en gyldig dato
5.63  *
5.63 c     *dmy          test(d)                 o1ldat                 34
5.63 c                   if        *in34 = *on
5.63 c                   goto      o1tagb
5.63 c                   endif
5.63  *
5.63  * Leveringsdato må være større enn/lik ordre-dato
5.63  *
5.63 c     *dmy          move      o1ldat        w_ldat
5.63 c                   if        w_ldat < fobdat
5.63 c                   eval      *in35 = *on
5.63 c                   goto      o1tagb
5.63 c                   endif
5.63  *
5.63  * Oppdaterer ordre
5.63  *
5.63 c     hedkey        chain     fohelur
5.63 c                   if        %found(fohelu)
5.63 c                   eval      foldat = %date(o1ldat:*dmy)
5.63 c                   eval      foinf1 = o1inf1
5.63 c                   eval      foinf2 = o1inf2
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = dsuser
5.63 c                   update    fohelur
5.63 c                   endif
5.64  * oppdat. status
5.64 c                   eval      d_ufir = fofirm
5.64 c                   eval      d_unum = fonumm
5 64 c                   eval      d_usuf = fosuff
5.64 c                   eval      d_ukod = '0'
5.64 c                   exsr      upd_logg
      *
5.63  *
5.63 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandle bilde for PLUKKING AV LINJER                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd1bld        begsr
      *
6.11 c                   move      *on           *in21
      *
      *  Legger inn ordrenummer og suffix i nøkklene
      *
     c                   move      hednum        linnum
     c                   move      hedsuf        linsuf
     c                   move      hednum        pllnum
     c                   move      hedsuf        pllsuf
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c     hedkey        chain     fohel1r                            90
     c                   move      fonumm        d2numm
     c                   move      fosuff        d2suff
     c                   move      fokund        d2kund
     c                   move      fokuna        d2kuna
      *
      *  Hent opplysninger fra ORDRE-LINJE-REGISTER
      *
     c                   eval      linlin = *zero
     c                   eval      plllin = *zero
     c     linkey        setll     fodtl1
     c                   exsr      xclr02
     c                   exsr      xcrt02
      *
      *  Send Alt
      *
     c     d2taga        tag
     c                   write     d2cmd
      *
      *  Bare data
      *
     c     d2tagb        tag
     c                   exsr      xdsp02
      *
      *  F3=Avslutt
      *
7.fb c                   if        *inkc = *on
7.fb c                   exsr      sjekk_d100
7.fb c                   if        b_d100 = *on
7.fb c                   goto      d2tagb
7.fb c                   endif
7.fb c                   endif
      *
7.06 c                   if        u_706 = *on
7.06 c                   if        *inkc = *on and
7.06 c                             f2over <> 0 and
7.06 c                             *in17 = *on
7.06 c                   move      '9'           wpplor
7.06 c                   goto      xslutt
7.06 c                   endif
7.06 c                   endif
      *
     c     *inkc         cabeq     *on           xd1slt
      *
      *  F5=Forny
      *
     c                   if        *inke = *on
     c                   exsr      xlsp02
     c                   goto      d2tagb
     c                   endif
      *
      *  Roll
      *
     c                   if        *in10 = *on
     c                   exsr      xcrt02
     c                   goto      d2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Posisjoner
      *  startverdi
      *  på linjenummer
      *
     c                   if        d2line <> *zero
     c                   move      d2line        linlin
     c     linkey        setll     fodtl1r
     c                   exsr      xclr02
     c                   exsr      xcrt02
     c                   eval      d2line = *zero
     c                   goto      d2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     c                   do        ssrn02
     c                   readc     d1sfl
     c                   if        %eof
     c                   leave
     c                   endif
      *
      *  Sjekk alle linjer
      *
     c                   move      d1line        plllin
     c                   exsr      xd7win
      *
     c                   enddo
      *
      *  Nullstiller/blanker feltet for start f.o.m.
      *
     c                   eval      d2line = *zero
      *
     c                   exsr      xlsp02
      *
7.06 c                   if        u_706 = *on
7.06 c                   if        footyp = 'HO' and
7.06 c                             *in16 = '0'
7.06 c                   z-add     0             f2over
7.06  * Sjekker utplukkede linjer mot kredit grense
7.06sc                   eval      w2mems = (w1sums * w_mvat) + rkmems
7.06sc                   eval      w2mems = (w1sums + rkmems) * w_mvat
7.06sc                   eval      w2mems = w2mems + rksald
7.06 c                   if        w1grns <> 0
7.06 c                   if        w2mems > w1grns
7.06 c     w2mems        sub       w1grns        f2over
7.06 c     visfeil       tag
7.06 c                   if        rkkgrk = 'J'
7.06 c                   exfmt     a1msg4
7.06 c                   else
7.06 c                   exfmt     a1msg2
7.06 c                   endif
7.06 c                   if        *inks = '0' and
7.06 c                             *inkc = '0'
7.06 c                   goto      visfeil
7.06 c                   endif
7.06 c                   if        *inks
7.06 c                   eval      *in16 = '1'
7.06 c                   else
7.06 c                   if        *inkc
7.06 c                   eval      *in17 = '1'
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
      *
     c                   goto      d2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutter behandlingen av utplukk/resting                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd1slt        tag
      *
      *  Oppdaterer subfile i forrige bilde (B1) før retur
      *
     c                   if        b1valg <> *blank
     c     srrn01        chain     b1sfl                              90
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D7 Behandle bilde for OPPDATERING AV PLUKK-REGISTER            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd7win        begsr
      *
      *  Benytter Z-ADD på antall inn i nytt felt fordi feltene
      *  ikke er like store, benytter kortere feltlengde i bildet
      *
     c     pllkey        chain     fplllur                            96
      *
      *  Hverken antall eller kode er tastet, hopp ut, Slett evnt. record
      *
     c                   if        d1kode = '9'
     c                   eval      d1kode = *blank
     c                   endif
     c                   if        d1leve = *zero
     c                   if        d1kode = *blank
     c                   if        *in96 = *off
     c                   delete    fplllur
7.06 c                   if        u_706 = *on
7.06 c                   if        footyp = 'HO'
7.06  *Slett forrige inntastede, hele linja
7.06 c                   if        fklkod = '1' and
7.06 c                             d1kode = ' '
7.06 c                   sub       d1sums        w1sums
7.06 c                   endif
7.06  *Slett forrige inntastede antall med beløp
7.06 c                   if        d1kode = ' ' and
7.06 c                             fklant <> d1leve and
7.06 c                             w1sums <> 0 and
7.06 c                             fklant <> 0 and
7.06 c                             fkllin = d1line
7.06 c     d1sums        div       d1anta        w1bere
7.06 c     w1bere        mult      fklant        w2bere
7.06 c                   sub       w2bere        w1sums
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
     c                   endif
     c                   goto      xd7slt
     c                   endif
     c                   endif
      *
      *  Legg inn verdier i UTPLUKK-LINJE-REGISTER
      *
     c                   if        d1vare = *blank
     c                   if        d1leve = 1
     c                   move      '1'           d1kode
     c                   endif
     c                   if        d1leve = 3
     c                   move      '3'           d1kode
     c                   endif
     c                   if        d1leve = 4
     c                   move      '4'           d1kode
     c                   endif
     c                   eval      d1leve = *zero
     c                   endif
7.06  *Slett forrige inntastede antall med beløp
7.06 c                   if        footyp = 'HO'
7.06 c                   if        d1kode = ' ' and
7.06 c                             fklant <> d1leve and
7.06 c                             fklant <> 0 and
7.06 c                             w1sums <> 0 and
7.06 c                             fkllin = d1line
7.06 c     d1sums        div       d1anta        w1bere
7.06 c     w1bere        mult      fklant        w2bere
7.06 c                   sub       w2bere        w1sums
7.06 c                   endif
7.06 c                   endif
      *
     c                   eval      fklant = d1leve
     c                   move      d1kode        fklkod
      *
     c                   if        *in96 = *off
     c                   update    fplllur
     c                   else
     c                   move      pllfir        fklfir
     c                   move      pllnum        fklonr
     c                   move      pllsuf        fklsuf
     c                   move      plllin        fkllin
     c                   write     fplllur
     c                   endif
      *
      *  Oppdater subfile
      *
     c     xd7slt        tag
      *
      * Setter farge
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *on
7.06 c                   if        u_706 = *on
7.06 c                   if        footyp = 'HO'
7.06 c                   if        d1kode = ' ' and
7.06 c                             d1leve <> 0
7.06 c     d1sums        div       d1anta        w1bere
7.06 c     w1bere        mult      d1leve        w2bere
7.06 c                   add       w2bere        w1sums
7.06 c*9.01              z-add     w2bere        w1sums
7.06 c                   endif
7.06 c                   endif
7.06 c                   endif
     c                   select
     c                   when      d1kode = '1'
     c                   eval      *in57 = *on
7.06 c                   if        footyp = 'HO'
7.06 c                   add       d1sums        w1sums
7.06 c                   endif
     c                   eval      *in59 = *off
     c                   when      d1kode = '3'
     c                   eval      *in56 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '4'
     c                   eval      *in55 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '5'
     c                   eval      *in58 = *on
     c                   eval      *in59 = *off
     c                   endsl
      *
     c                   update    d1sfl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  H1 Behandle bilde for bla-alternativer                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xh1win        begsr
      *
      *  Send Alt
      *
     c                   write     h1bld
     c                   exfmt     h1bld
      *
     c     *inkc         cabeq     *on           xh1slt
      *
      *  Kun en bestemt ordre-type
      *
     c                   if        h1otyp <> *blank
     c                   move      'O'           wseqen
     c                   eval      potnum = *zero
     c                   eval      potsuf = *zero
     c                   move      h1otyp        pototy
     c     potkey        setll     fplolo
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   eval      h1otyp = *blank
     c                   goto      xh1slt
     c                   endif
      *
      *  Ved blaing i ordre-register skal det blas bakfra
      *
     c                   if        h1valg = '1'
     c                   move      'B'           wseqen
     c                   move      999999        plonum
     c                   move      99            plosuf
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   eval      h1valg = *blank
     c                   goto      xh1slt
     c                   endif
      *
      *  Dersom ingen er valgt, men ENTER er trykket
      *  skal startverdi settes til ordrenummer lik null
      *
     c                   move      ' '           wseqen
     c                   eval      plonum = *zero
     c                   eval      plosuf = *zero
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
      *
     c     xh1slt        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk/Hent tekst for lager                        RS710R       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sblagr        begsr
      *
     c                   call      'RS710R'
     c                   parm                    w_retk
     c                   parm                    a1lage
     c                   parm                    w_jtxt
      *
     c                   eval      a1latx = *blank
      *
     c                   if        w_retk = *blank
     c                   movel     w_jtxt        a1latx
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlsp01        begsr
      *
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1sfl                              60
     c                   endif
      *
     c                   move      b1numm        plonum
     c                   move      b1suff        plosuf
      *
     c                   if        wseqen = 'B'
     c     plokey        setll     fplol1
     c                   endif
     c                   if        wseqen = 'O'
     c     potkey        setll     fplolo
     c                   endif
     c                   if        wseqen = *blank
     c     plokey        setll     fplol1
     c                   endif
      *
     c                   exsr      xclr01
     c                   exsr      xcrt01
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   eval      spge01 = spge01 + 13
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01
      *
     c                   if        wseqen = 'B'
     c                   readp     fplol1                                 15
     c                   endif
     c                   if        wseqen = 'O'
     c                   read      fplolo                                 15
     c                   if        *in15 = *off
     c                   if        fkpoty <> pototy
     c                   move      *on           *in15
     c                   endif
     c                   endif
     c                   endif
     c                   if        wseqen = *blank
     c                   read      fplol1                                 15
     c                   endif
      *
     c     *in15         cabeq     *on           xend01
5.41  *
5.41 c                   if        fkpfir <> w_firm
5.41 c                   goto      xend01
5.41 c                   endif
5.41  *
     c                   eval      srrn01 = srrn01 + 1
     c                   move      fkponr        b1numm
     c                   move      fkpsuf        b1suff
     c                   move      fkpoty        b1otyp
     c                   move      fkpwsd        b1wsid
     c                   move      fkpusr        b1user
     c                   move      fkpalt        b1palt
     c                   move      fkptxt        b1ptxt
     c                   write     b1sfl
     c                   enddo
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01
     c                   eval      sfrn01 = ssrn01 + 1
     c                   endif
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      w_virn = sfrn01
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlsp02        begsr
      *
     c                   if        sfrn02 <> *zero
     c     sfrn02        chain     d1sfl                              90
     c                   endif
      *
6.30 c                   if        *in11 = *on
6.30 c                   eval      linlin = d1line
6.30 c                   eval      plllin = *zero
6.30 c     linkey        setll     fodtl1
6.30 c*
6.30 c                   z-add     0             tell              2 0
6.30 c     tell          doueq     14
6.30 c                   readp     fodtl1                                 15
6.30 c                   add       1             tell
6.30 c                   if        *in15 = *off
6.30 c                   if        fdnumm <> linnum
6.30 c                   move      *on           *in15
6.30 c                   eval      tell = 14
6.30 c                   endif
6.30 c                   endif
6.30 c                   if        *in15 = *off
6.30 c                   if        fdsuff <> linsuf
6.30 c                   move      *on           *in15
6.30 c                   eval      tell = 14
6.30 c                   endif
6.30 c                   endif
6.30 c*
6.30 c                   enddo
6.30 c                   endif
      *
     c                   move      d1line        linlin
6.30 c                   if        tell = 14
6.30 c                   move      fdline        linlin
6.30 c                   z-add     0             tell
6.30 c                   endif
      *
     c     linkey        setll     fodtl1
      *
     c                   exsr      xclr02
     c                   exsr      xcrt02
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp02        begsr
      *
     c                   if        srrn02 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     d2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr02        begsr
     c                   move      *on           *in14
     c                   write     d2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn02 = 0
     c                   eval      sfrn02 = 0
     c                   eval      ssrn02 = 0
     c                   eval      spge02 = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt02        begsr
7.xx c                   eval      spge02 = spge02 + 13
     c                   eval      srrn02 = ssrn02
      *
     c                   dou       srrn02 = spge02
      *
     c                   read      fodtl1                                 15
     c                   if        *in15 = *off
     c                   if        fdnumm <> linnum
     c                   move      *on           *in15
     c                   endif
     c                   endif
     c                   if        *in15 = *off
     c                   if        fdsuff <> linsuf
     c                   move      *on           *in15
     c                   endif
     c                   endif
      *
     c     *in15         cabeq     *on           xend02
      *
     c                   eval      srrn02 = srrn02 + 1
      *
     c                   eval      d1leve = 0
     c                   eval      d1kode = ' '
     c                   move      fdline        plllin
     c     pllkey        chain     fplll1                             96
     c                   if        *in96 = *off
     c                   eval      d1leve = fklant
     c                   move      fklkod        d1kode
     c                   endif
      *
7.06 c                   if        footyp = 'HO'
7.06 c                   z-add     fdsums        d1sums
7.06 c                   endif
      *
     c                   move      fdline        d1line
     c                   move      fdanta        d1anta
     c                   movel     fdvare        d1vare
     c                   move      fdtek1        d1tek1
     c                   move      fdenh1        d1enh1
      *
      * Lassnummer blir lagt inn
      * i de to siste posisjoner
      * i varenummer dersom > 0.
      *
      *    FDLASS        IFGT      0
      *                  MOVE      FDLASS        D1VARE
      *                  ENDIF
      *
      * Sette farge
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *on
     c                   select
     c                   when      d1kode = '1'
     c                   eval      *in57 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '3'
     c                   eval      *in56 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '4'
     c                   eval      *in55 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '5'
     c                   eval      *in58 = *on
     c                   eval      *in59 = *off
     c                   endsl
      *
7.04 c                   exsr      newlo_vask
7.05  *
7.05 c/exec sql
7.05 c+                  select vvsort
7.05 c+                  into :w_sort
7.05 c+                  from vvarpf
7.05 c+                  where vvfirm = :w_firm and vvvare = :fdvare
7.05 c/end-exec
7.05 c                   if        u_fsor = *on
7.05 c                   eval      *in53  = *off
7.05 c                   eval      *in54  = *off
7.05 c                   if        w_sort = w_fso1
7.05 c                   eval      *in53 = *on
7.05 c                   endif
7.05 c                   if        w_sort = w_fso2
7.05 c                   eval      *in54 = *on
7.05 c                   endif
7.05 c                   else
7.05 c                   if        u_fskv = *on
7.05 c                   eval      *in54 = *off
7.05 c                   if        fdlvre = 1
7.05 c                   eval      *in54 = *on
7.05 c                   endif
7.05 c                   endif
7.05 c                   endif
      *
     c                   write     d1sfl
     c                   enddo
      *
     c     xend02        tag
      *
     c                   if        srrn02 > ssrn02
     c                   eval      sfrn02 = ssrn02 + 1
     c                   endif
      *
     c                   eval      ssrn02 = srrn02
     c                   eval      svrn02 = sfrn02
      *
     c                   endsr
5.51  *
5.51  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.51  *  Subrutine for å sjekke om det er plukket noe på radioterminal
5.51  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.51  *
5.51 c     sjk_rt        begsr
5.51  *
5.51  *  Sjekker siste status - bare status 7 skal behandles
5.51  *
5.51 c                   eval      p_kode = *blank
5.62 c                   eval      w_aarr = %subdt(fobdat:*years)
5.51 c                   call      'FU706R '
5.62 c                   parm                    w_aarr
6.NU c                   parm                    wpnumm
5.51 c                   parm                    wpsuff
5.51 c                   parm                    p_kode
5.51  *
5.51 c                   if        p_kode <> '7'
5.51 c                   goto      end_rt
5.51 c                   endif
5.51  *
5.51 c                   eval      a1vlin = '1'
5.51  *
5.51  * Leser plukkfil på radio og oppdaterer man. plukkefil
5.51  *
5.51 c                   eval      pllnum = wpnumm
5.51 c                   eval      pllsuf = wpsuff
5.51  *
5.51 c                   eval      lwpll2_numm = wpnumm
5.51 c                   eval      lwpll2_suff = wpsuff
5.51 c     lwpll2_key    setll     lwpll2
5.51 c     lwpll2_key    reade     lwpll2
5.51  *
5.51 c                   dow       not %eof
5.51 c                   eval      plllin = mkline
5.51 c     pllkey        chain     fplllur                            96
5.51 c                   if        *in96 = *off
5.51 c                   eval      fklant = mkantp
5.51 c                   update    fplllur
5.51 c                   else
5.51 c                   move      pllfir        fklfir
5.51 c                   move      pllnum        fklonr
5.51 c                   move      pllsuf        fklsuf
5.51 c                   move      plllin        fkllin
5.51 c                   eval      fklant = mkantp
5.51 c                   write     fplllur
5.51 c                   endif
5.51 c     lwpll2_key    reade     lwpll2
5.51 c                   enddo
5.51  *
5.51 c     end_rt        endsr
5.64  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.64  * Subrutine for slette plukk-reg. på RadioTerm.
5.64  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.64  *
5.64 c     upd_logg      begsr
5.64  *
5.64  *
5.64 c                   time                    d_date
5.64 c                   time                    d_time
5.64 c                   eval      d_user = dsuser
5.64 c                   eval      d_wsid = dswsid
5.64 c                   eval      d_aarr = %subdt(fobdat:*years)
5.64 c                   eval      p_urec = d_urec
5.64 c                   call      'FU900R  '
5.64 c                   parm                    p_stat
5.64 c                   parm                    p_urec
5.64  *
5.64  *
5.64 c                   endsr
7.27  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.27  * Newlook vask av data
7.27  *  Fjerner tegn som gjør at Newlook feiltolker
7.27  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.27  *
7.27   begsr newlo_vask;
7.27     for w_teller = 1 to 25;
7.27       w_len = %len(%trim(d1tek1));
7.27       if (w_len > 0);
7.27       d1tek1 = %ScanRpl('.':'':d1tek1:w_len); // newlook tåler ikke . på slutten
7.27       endif;
7.27       w_len = %len(%trim(d1tek1));
7.27       if (w_len > 0);
7.27       d1tek1 = %ScanRpl(':':'':d1tek1:w_len); // newlook tåler ikke : på slutten
7.27       endif;
7.27     endfor ;
7.27   endsr;
      *
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Subrutine for å sjekke om linje har ubetalt depositumskrav
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  *
7.fb c     sjekk_depo    begsr
      *
7.fb c                   eval      b_depok = *off
7.fb c                   eval      *in45  = *off
7.fb c                   eval      sdepl1_numm = fonumm
7.fb c                   eval      sdepl1_kund = fokund
7.fb c                   eval      sdepl1_tell = 0
7.fb c     sdepl1_key    chain     sdepl1r
8.03 c                   if        %found(sdepl1)
      *
8.03 c**                 if        %found(sdepl1) and
7.f1 c**                           sk100p = 1  and
8.03 c**                           skdepb >= skdepo
8.03 c                   if        skdepb >= skdepo
7.fb c                   eval      b_depok = *on
7.fb c                   endif
      *
8.03 c                   if        rkkres = 2
8.03 c                   eval      b_depok = *off
8.03 c                   if        sk100p = 1 and
8.03 c                             skdepb = skdepo
8.03 c                   eval      b_depok = *on
8.03 c                   endif
8.03 c                   endif
      *
8.03 c                   endif
      *
7.fb c                   endsr
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  * Subrutine for å sjekke om linje har ubetalt depositumskrav
7.fb  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.fb  *
7.fb c     sjekk_d100    begsr
7.fb  *
7.fb c                   eval      b_d100 = *off
7.fb c                   eval      *in45  = *off
7.fb  *
7.fb  * hvis 100% så sjekk at evt avvik er innenfor
7.fb c                   if        sk100p = 1 and
7.fb c                             faatjp <> 0
7.fb c                   eval      w_sumi = 0
7.fb c                   eval      w_sumu = 0
7.fb c                   call      'FO705R '
7.fb c                   parm                    w_firm
7.fb c                   parm                    wpnumm
7.fb c                   parm                    wpsuff
7.fb c                   parm                    w_sumi
7.fb c                   parm                    w_sumu
7.fb c                   eval(h)   w_avik = skdepo * faatjp/100
7 fb  *
7.fb c                   if         w_sumi >  (w_avik + skdepo)
7.fb c                   eval      w_mld1 = 'Sum plukket er for +
7.fb c                                       høy i forhold til dep+
7.fb c                                       ositum.  '
7.fb c                   call      'AA005R'
7.fb c                   parm                    w_mld1
7.fb c                   eval      b_d100 = *on
7.fb c                   leavesr
7.fb c                   endif
7.fb c                   endif
7.fb  *
7.fb c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      wseqen = *blank
     c                   eval      w_retk = *blank
      *
     c                   eval      spge02 = 0
     c                   eval      srrn02 = 0
      *
     c                   eval      wwoutq = *blank
      *
     c                   eval      wpipor = *blank
     c                   eval      wpftor = *blank
     c                   eval      wxpdir = *zero
      *
      *  Redefinering av felter
      *
      *
      *
      *
      *
      *  Redefinering av parameterfeltene
      *
      *
      *  Redefinering av nøkler
      *
      *
      *
      *
      *
      *
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     c     otykey        klist
     c                   kfld                    otyfir
     c                   kfld                    otytyp
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     hedkey        klist
     c                   kfld                    hedfir
     c                   kfld                    hednum
     c                   kfld                    hedsuf
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     c     linkey        klist
     c                   kfld                    linfir
     c                   kfld                    linnum
     c                   kfld                    linsuf
     c                   kfld                    linlin
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     plokey        klist
     c                   kfld                    plofir
     c                   kfld                    plonum
     c                   kfld                    plosuf
      *
      *  Key for file : UTPLUKK-PARAM-ORDRETYPE-REGISTER
      *
     c     potkey        klist
     c                   kfld                    potfir
     c                   kfld                    pototy
     c                   kfld                    potnum
     c                   kfld                    potsuf
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     c     pllkey        klist
     c                   kfld                    pllfir
     c                   kfld                    pllnum
     c                   kfld                    pllsuf
     c                   kfld                    plllin
6.12  *
6.12  * Key for file : KUNDE-REGISTER
6.12  *
6.12 c     rkunl1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    rkunl1_kund
3.32  *
3.32  * Key for oppslag på ordre-info-type
3.32  *
3.32 c     vinfl1_key    klist
3.32 c                   kfld                    w_firm
3.32 c                   kfld                    vinfl1_ityp
3.33  *
3.33  * Key for brukerregister
3.33  *
3.33 c     fusrl1_key    klist
3.33 c                   kfld                    w_firm
3.33 c                   kfld                    fusrl1_user
3.34  *
3.34  * Key for ofak kode-register
3.34  *
3.34 c     fstsl1_key    klist
3.34 c                   kfld                    w_firm
5.51  *
5.51  * Key for les på radioterminal-trans
5.51  *
5.51 c     lwpll2_key    klist
5.51 c                   kfld                    w_firm
5.51 c                   kfld                    lwpll2_numm
5.51 c                   kfld                    lwpll2_suff
7.06  *
7.06  * Key for propertiesfil
7.06  *
8.02 c*    afpslr_key    klist
8.02 c*                  kfld                    afpslr_ufil
8.02 c*                  kfld                    afpslr_uide
7.fb  *
7.fb  * Key for oppslag på depositum-fil
7.fb  *
7.fb c     sdepl1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    sdepl1_numm
7.fb c                   kfld                    sdepl1_kund
7.fb c                   kfld                    sdepl1_tell
7.fb  *
7.fb  * Key for oppslag på plukkfilen
7.fb  *
7.fb c     fplll1_key    klist
7.fb c                   kfld                    w_firm
7.fb c                   kfld                    fplll1_lonr
7.fb c                   kfld                    fplll1_lsuf
7.fb  *
7.fb  * Key for oppslag på ofak-koderegister sideregister
7.fb  *
7.fb c     fst2l1_key    klist
7.fb c                   kfld                    w_firm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *entry        plist
     c                   parm                    wpplor
     c                   parm                    wpfirm
6.NU c                   parm                    wpnumm
     c                   parm                    wpsuff
      *
      *  Legger inn firma i nøkklene
      *
     c                   move      wpfirm        otyfir
     c                   move      wpfirm        hedfir
     c                   move      wpfirm        linfir
     c                   move      wpfirm        plofir
     c                   move      wpfirm        potfir
     c                   move      wpfirm        pllfir
3.35 c                   move      wpfirm        w_firm
      *
      *  Nullstiller felter som alltid skal være blanke/null
      *
     c                   eval      plonum = *zero
     c                   eval      plosuf = *zero
     c                   eval      potnum = *zero
     c                   eval      potsuf = *zero
     c                   eval      pllnum = *zero
     c                   eval      pllsuf = *zero
     c                   eval      plllin = *zero
      *
     c                   eval      wxnumm = *zero
     c                   eval      wxsuff = *zero
     c                   eval      wxotyp = *blank
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FO736R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'FO736R_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
7.03  *
7.03  * Endring 7.03
7.03  *
7.03   CallP CO402R(l_filg:w_firm:0:'FO736R_703':
7.03                    co402_verdi1:co402_verdi2);
7.03   if co402_verdi1 = '1';
7.03     w_pos = %scan(':' : co402_verdi2);
7.03     if (w_pos > 0);
7.03       w_otyp1 = %subst(co402_verdi2 : 1 : w_pos - 1);
7.03       co402_verdi2 =  %subst(co402_verdi2:w_pos+1:512-w_pos);
7.03       w_pos = %scan(':' : co402_verdi2);
7.03       w_otyp2 = %subst(co402_verdi2 : 1 : w_pos - 1);
7.03       co402_verdi2 =  %subst(co402_verdi2:w_pos+1:512-w_pos);
7.03       w_pos = %scan(':' : co402_verdi2);
7.03       w_ityp = %subst(co402_verdi2 : 1 : w_pos - 1);
7.03       if (w_otyp1 <> *blank and w_otyp2 <> *blank  and w_ityp <> *blank);
7.03  * Sjekk ordre-info-kode i inforegister
7.03         vinfl1_ityp = w_ityp;
7.03         chain vinfl1_key vinfl1r;
7.03         if %found(vinfl1);
7.03           u_703 = *on;
7.03         endif;
7.03         clear vinfl1r;
7.03       endif;
7.03     endif;
7.03   endif;
7.05  *
7.05  * Test om fargekode fra sortimentskode   7.05
7.05  *
7.05   CallP CO402R(l_filg:w_firm:0:'FARGE_SKAFF_SORTIMENT':
7.05                    co402_verdi1:co402_verdi2);
7.05   if co402_verdi1 = '1';
7.05     u_fsor = *on;
7.05     w_fso1 = %subst(co402_verdi2:3:%scan('B=':co402_verdi2) - 3);
7.05     w_fso2 = %subst(co402_verdi2:%scan('B=':co402_verdi2) + 2:10);
7.05   endif;
7.05  *
7.05  * Skal skaffevarer ha farger
7.05  *
7.05   CallP CO402R(l_filg:w_firm:0:'FARGE_SKAFFEVARE':
7.05                    co402_verdi1:co402_verdi2);
7.05   if co402_verdi1 = '1';
7.05     u_fskv = *on;
7.05   endif;
8.04  *
8.04  * Endring 8.04 Tilbudskunde
8.04  *
8.04   CallP CO402R(l_filg:w_firm:0:'FO736_707':
8.02                   co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_til = *on;
8.02   endif;
7.04  *
7.01 c                   eval      w_memodagr = 999
7.01 c                   eval      w_utvgrns  = 0
7.06  * Endring 7.06
7.06  *
7.06   CallP CO402R(l_filg:w_firm:0:'UTVIDET_MEMOSALDO':
7.06                    co402_verdi1:co402_verdi2);
7.06   if co402_verdi1 = '1';
7.06     u_706 = *on;
8.02     if  %subst(co402_verdi2:1:3) <> *blank;
8.02       w_memodagr = %dec(%subst(co402_verdi2:1:3):3:0);
8.02     endif;
8.02     if  %subst(co402_verdi2:133:2) <> *blank;
8.02       w_utvgrns = %dec(%subst(co402_verdi2:133:2):2:0);
7.01     endif;
7.06   endif;
7.07  *
7.07  * Endring 7.07
7.07  *
7.07   CallP CO402R(l_filg:w_firm:0:'FO736_707':
7.07                    co402_verdi1:co402_verdi2);
7.07   if co402_verdi1 = '1';
7.07     u_707 = *on;
7.07   endif;
      *
      *  Fyller opp skjermbildet for første visning
      *
     c     plokey        setll     fplol1
     c                   exsr      xcrt01
3.33  *
3.33  * Hente inn brukerregister
3.33  *
3.33 c                   eval      fusrl1_user = dsuser
3.33 c     fusrl1_key    chain     fusrl1r                            90
3.34  *
3.34  * Hente inn ofak kode-register
3.34  *
3.34 c     fstsl1_key    chain     fstsl1r                            90
7.fb c     fst2l1_key    chain     fst2l1r
      *
8.02  *   Hent inn % utvidelse av kreditgrense
8.02 c*                  eval      afpslr_ufil = l_filg
8.02 c*                  eval      afpslr_uide = 'KRGRNSUTV'
8.02 c*    afpslr_key    chain     afpslrr
8.02 c*                  if        %found(afpslr)
8.02 c*                  movel     afudss        w_utvgrns
8.02 c*                  endif
8.02  *
8.02  *   Hent inn ant dager fram ordre skal med i memosaldo
8.02 c*                  eval      afpslr_uide = 'MEMODAGER'
8.02 c*    afpslr_key    chain     afpslrr
8.02 c*                  if        %found(afpslr)
8.02 c*                  movel     afudss        w_3x
8.02 c*                  move      w_3x          w_memodagr
8.02 c*                  endif
      *
     c                   endsr
