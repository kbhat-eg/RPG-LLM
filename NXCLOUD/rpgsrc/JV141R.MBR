     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV141R
      * Beskrivelse . . : Overføring til EVR, guppebehandling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020996 HKO  Nytt program
      *       121202 HKO  Lagt inn grossistløsning
      *       151003 HKO  Utelater utgåtte varer ved frigjøring av nye varer
6.30  * 6.30  270314 bof  feilretting + test om varepris VA er utgått
6.31  * 6.30  140714 bof  Endret også jvprl2 til jvprl3, antakeligvis mer riktig
6.32  * 6.30  300915 bof  Justering i test på hva som skal med.
6.33  * 6.30  020816 bof  Feilretting vedr. test på til prisdato.
6.34  *       150719 bof  utvidet kall til JV141R med utgåtte varer + test på jvluda
6.35  *       270719 bof  Kriterie for utvelg,endret fra jvprpf til jvlepf
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl5    if   e           k disk    rename(jvarpfr:jvarl5r)
6.35 fjvlel3    if   e           k disk    rename(jvlepfr:jvlel3r)
6.35 fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
6.35 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.34 fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
      *
     fjv141d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_nye_varer     s              1
     d p_ldor          s                   like(jvldor)
     d p_ogrp          s                   like(jvogrp)
     d p_hgrp          s                   like(jvhgrp)
     d p_ugrp          s                   like(jvugrp)
     d p_ldop          s                   like(jvldor)
6.34 d p_utgp          s              1  0
      *
      * Definering av variable i nøkler
      *
     d jvarl5_ldor     s                   like(jvldor)
6.35 d jvlel3_lldo     s                   like(jvlldo)
6.35 d jvprl1_vare     s                   like(jxvare)
6.35 d jvprl1_ldor     s                   like(jxldor)
6.35
6.34 d jvlel1_lvnr     s                   like(jvlvnr)
6.34 d jvlel1_lldo     s                   like(jvlldo)
     d jvarl1_vare     s                   like(jvvare)
     d vvarl1_vare     s                   like(vvvare)
     d jvdtlu_dvar     s                   like(jvdvar)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d b_pris          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Funksjonstaster
      *
     c                   if        *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tag
     c                   endif
      *
      * Behandling av varer som skal holdes/frigis
      *
     c                   if        b1valh = 1 or
     c                             b1valf = 1
     c                   exsr      behandling
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1valh = 1 and
     c                             b1valf = 1
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      endsjekk
     c                   endif
      *
     c     endsjekk      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer som skal holdes/frigis
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        p_ldor <> 0
      *
     c                   eval      jvarl5_ldor = p_ldor
     c     jvarl5_key    setll     jvarl5
     c     jvarl5_key    reade     jvarl5
     c                   dow       not %eof
     c                   exsr      oppdater
     c     jvarl5_key    reade     jvarl5
     c                   enddo
      *
     c                   else
      *
6.35 c                   eval      jvlel3_lldo = p_ldop
6.35 c     jvlel3_key    setll     jvlel3
6.35 c     jvlel3_key    reade     jvlel3
     c                   dow       not %eof
6.35 c                   if        (jvluda =  *loval) or
6.35 c                             (jvluda <> *loval and
6.35 c                             jvluda >= w_udat)
6.35 c                   eval      jvarl1_vare = jvlvnr
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
6.35  * sjekk om pris er utgått, hvis parameter tilsier det.
6.35 c                   exsr      sjekk_pris
6.35 c                   if        b_pris = *on
     c                   exsr      oppdater
6.35 c                   endif
     c                   endif
6.30 c                   endif
6.32 c**                 endif
6.35 c     jvlel3_key    reade     jvlel3
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av holde-kode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   if        p_ogrp <> 0 and
     c                             p_ogrp <> jvogrp
     c                   leavesr
     c                   endif
      *
     c                   if        p_hgrp <> 0 and
     c                             p_hgrp <> jvhgrp
     c                   leavesr
     c                   endif
      *
     c                   if        p_ugrp <> 0 and
     c                             p_ugrp <> jvugrp
     c                   leavesr
     c                   endif
6.34  ** ny test på utgårdato
6.34 c*                   if        p_nye_varer = *on
6.34 c*                   if        jvstat = 4 or
6.34 c*                             jvudat <> *loval and
6.34 c*                             jvudat < w_udat
6.34 c*                   leavesr
6.34 c*                   endif
6.34 c*                   endif
6.34 c                   eval      jvlel1_lvnr = jvvare
6.34 c                   if        p_ldor <> 0
6.34 c                   eval      jvlel1_lldo = p_ldor
6.34 c                   else
6.34 c                   eval      jvlel1_lldo = p_ldop
6.34 c                   endif
6.34 c     jvlel1_key    chain     jvlel1
6.34 c                   if        not %found(jvlel1)
6.34 c                   leavesr
6.34 c                   endif
6.35 c                   if        p_utgp = 0
6.34 c                   if        jvluda <> *loval and
6.34 c                             jvluda < w_udat
6.34 c                   leavesr
6.34 c                   endif
6.35 c                   endif
      *
     c                   eval      vvarl1_vare = jvvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        p_nye_varer = *on  and %found or
     c                             p_nye_varer = *off and not %found
     c                   leavesr
     c                   endif
      *
     c                   clear                   jvdtlur
      *
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur                            91
      *
     c                   if        b1valh = 1
     c                   eval      jvdhko = 1
     c                   else
     c                   eval      jvdhko = 0
     c                   endif
      *
     c                   if        *in91 = *off
     c                   update    jvdtlur
     c                   else
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   write     jvdtlur
     c                   endif
      *
     c                   endsr
      *
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * Subrutine for å sjekke om pris er gyldig
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *
6.35 c     sjekk_pris    begsr
6.35 c                   eval      b_pris = *off
6.35 c                   if        p_utgp = 1
6.35 c                   eval      b_pris = *on
6.35 c                   leavesr
6.35 c                   endif
6.35 c                   eval      jvprl1_vare = jvvare
6.35 c                   if        p_ldor <> 0
6.35 c                   eval      jvprl1_ldor = p_ldor
6.35 c                   else
6.35 c                   eval      jvprl1_ldor = p_ldop
6.35 c                   endif
6.35 c     jvprl1_key    setll     jvprl1
6.35 c     jvprl1_key    reade     jvprl1
6.35 c                   dow       not %eof
6.35 c                   if        (jxtdat =  *loval) or
6.35 c                             (jxtdat <> *loval and
6.35 c                             jxtdat >= w_udat)
6.35 c                   eval      b_pris = *on
6.35 c                   leavesr
6.35 c                   endif
6.35 c     jvprl1_key    reade     jvprl1
6.35 c                   enddo
6.35 c                   endsr
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_nye_varer
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldop
6.34 c                   parm                    p_utgp
      *
      * Key for les på vare
      *
     c     jvarl5_key    klist
     c                   kfld                    jvarl5_ldor
      *
      * Key for les på vare-pris
      *
6.35 c     jvlel3_key    klist
6.35 c                   kfld                    jvlel3_lldo
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på EVR-vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      * Key for oppslag mot leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
6.34  *
6.34  * Key for oppslag på vare
6.34  *
6.34 c     jvlel1_key    klist
6.34 c                   kfld                    jvlel1_lvnr
6.34 c                   kfld                    jvlel1_lldo
6.34  *
6.35  *
6.35  * Key for oppslag på pris
6.35  *
6.35 c     jvprl1_key    klist
6.35 c                   kfld                    jvprl1_vare
6.35 c                   kfld                    jvprl1_ldor
6.35  *
6.35  *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      b1ogrp = p_ogrp
     c                   eval      b1hgrp = p_hgrp
     c                   eval      b1ugrp = p_ugrp
     c                   eval      b1valh = 0
     c                   eval      b1valf = 0
      *
      * Henter leverandørnavn
      *
     c                   if        p_ldor <> 0
     c                   eval      jlevl1_ldor = p_ldor
     c     jlevl1_key    chain     jlevl1
     c                   eval      b1navn = jlnavn
     c                   endif
      *
     c                   if        p_ldop <> 0
     c                   eval      jlevl1_ldor = p_ldop
     c     jlevl1_key    chain     jlevl1
     c                   eval      b1pnav = jlnavn
     c                   endif
      *
      * Henter varegruppebeskrivelse
      *
     c                   select
     c                   when      p_ugrp <> 0
     c                   eval      vugrl1_uogr = p_ogrp
     c                   eval      vugrl1_uhgr = p_hgrp
     c                   eval      vugrl1_uugr = p_ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   eval      b1utxt = vgutxt
     c                   when      p_hgrp <> 0
     c                   eval      vhgrl1_hogr = p_ogrp
     c                   eval      vhgrl1_hhgr = p_hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   eval      b1utxt = vghtxt
     c                   when      p_ogrp <> 0
     c                   eval      vogrl1_oogr = p_ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   eval      b1utxt = vgotxt
     c                   endsl
      *
     c                   time                    w_udat
      *
     c                   endsr
