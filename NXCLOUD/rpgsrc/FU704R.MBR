     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FU704R
      * Beskrivelse . . : Henter inn informasjon fra oppgitt kode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300307 AMD  Nytt program
5.60  *       070108 bof  Utvidet nøkkel med år
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflogl1    if   e           k disk    rename(flogpfr:flogl1r)
      *
      * Definering av parametre
      *
5.60 d p_aarr          s                   like(fuaarr)
6.nu d p_numm          s                   like(funumm)
     d p_suff          s                   like(fusuff)
     d p_kode          s                   like(fukode)
     d p_svar          s                   like(fukode)
     d p_date          s                   like(fudate)
     d p_time          s                   like(futime)
     d p_user          s                   like(fuuser)
     d p_wsid          s                   like(fuwsid)
      *
      * Definering av Local data area (LDA)
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
5.60 d flogl1_aarr     s                   like(fuaarr)
     d flogl1_numm     s                   like(funumm)
     d flogl1_suff     s                   like(fusuff)
     d flogl1_kode     s                   like(fukode)
     d flogl1_date     s                   like(fudate)
     d flogl1_time     s                   like(futime)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fufirm)
      *
      * --------------------------------------------------------------------
      * Hoved - logikk
      * --------------------------------------------------------------------
      *
      * Sjekker om en aktuell kode finnes
      * fra før i loggen og returnerer svaret
      *
     c                   exsr      hent_logg
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av aktuell kode i logg - filen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_logg     begsr
     c                   eval      p_svar = *blank
      *
      * Sjekker aktuell kode i logg - filen
      *
5.60 c                   eval      flogl1_aarr = p_aarr
     c                   eval      flogl1_numm = p_numm
     c                   eval      flogl1_suff = p_suff
     c                   eval      flogl1_kode = p_kode
     c                   eval      flogl1_date = *hival
     c                   eval      flogl1_time = *hival
      *
     c     flogl1_key    setll     flogl1r
     c                   readp     flogl1r
      *
     c                   if        %eof
     c                   eval      p_svar = *blank
     c                   goto      end_sr
     c                   endif
      *
5.60 c                   if        fuaarr = p_aarr and
     c                             funumm = p_numm and
     c                             fusuff = p_suff and
     c                             fukode = p_kode
     c                   eval      p_svar = 'J'
     c                   eval      p_date = fudate
     c                   eval      p_time = futime
     c                   eval      p_user = fuuser
     c                   eval      p_wsid = fuwsid
     c                   endif
      *
     c     end_sr        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
5.60 c                   parm                    p_aarr
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_kode
     c                   parm                    p_svar
     c                   parm                    p_date
     c                   parm                    p_time
     c                   parm                    p_user
     c                   parm                    p_wsid
      *
      * Key for logg - filen
      *
     c     flogl1_key    klist
     c                   kfld                    w_firm
5.60 c                   kfld                    flogl1_aarr
     c                   kfld                    flogl1_numm
     c                   kfld                    flogl1_suff
     c                   kfld                    flogl1_kode
     c                   kfld                    flogl1_date
     c                   kfld                    flogl1_time
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
