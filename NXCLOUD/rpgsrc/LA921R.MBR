     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LA921R
      * Beskrivelse . . : Oppdatering av lager-register utfra vare i best.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260902 HKO  Nytt program
      *       160507 ASK  Oppdaterer bare ut i fra inneliggende bestillinger
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.nu  * 6.30  050614 bof  Utvidelser mtp. nummerutvidelser
6.30  * 6.30  040615 bkv  Bestilling økt fra 6 til 8      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
      *
6.10  * Definering av LDA
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
     d p_vare          s                   like(ldvare)
      *
      * Datastruktur - lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d lodtlv_vare     s                   like(ldvare)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d votylr_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_stat          s              1
      *
     d w_firm          s                   like(lofirm)
6.30 d w_numa          s              8
5.02 d w_lina          s              4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer akkumulatorer i lager
      *
     c                   eval      vlaglu_vare = p_vare
     c     vlaglu_key    setll     vlaglu
     c     vlaglu_key    reade     vlaglur
     c                   dow       %eof = *off
      *
     c                   eval      vlbbes = 0
     c                   eval      vlbres = 0
      *
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
      *
     c     vlaglu_key    reade     vlaglur
     c                   enddo
      *
      * Leser alle forekomster av varen i ordre og kaller program
      * for oppdatering av akkumulatorer i lager
      *
     c                   eval      lodtlv_vare = p_vare
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
     c                   dow       %eof = *off
      *
     c                   eval      lohel1_numm = ldnumm
     c                   eval      lohel1_suff = ldsuff
     c     lohel1_key    chain     lohel1
     c                   if        %found
      *
     c                   eval      votylr_otyp = lootyp
     c     votylr_key    chain     votylr
     c                   if        vaoakk < 2
     c                   eval      hltype = 'R'
     c                   eval      hlinlr = *blank
      *
     c                   eval      hlvare = ldvare
     c                   eval      hllage = lolage
     c                   eval      hlenhe = ldenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = lootyp
     c                   eval      hlltyp = ldltyp
      *
     c                   eval      hlanta = ldanta
     c                   eval      hlkopr = ldkpny
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'L'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      ldnumm        w_numa
     c                   move      ldline        w_lina
     c                   eval      hlrefr = 'B:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = ldnumm
     c                   eval      hlolin = ldline
3.12 c                   move      ldlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c                   endif
     c                   endif
      *
     c     lodtlv_key    reade     lodtlv
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       TAG
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppdatering av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
     c     vlaglu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
      *
      * Key for les på bestilling-linje
      *
     c     lodtlv_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    lodtlv_vare
      *
      * Key for oppslag på bestilling-hode
      *
     c     lohel1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    lohel1_numm
     c                   KFLD                    lohel1_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
