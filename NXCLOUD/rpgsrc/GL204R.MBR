      /TITLE  ASOKON Legger data fra ARETPF til GOCRPF,GTGIPF og GKTOPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : BF204R                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                 *
0900  * 0900      050900  Lagt inn behandling av inkassoreturer.    JT  *
0900  *                   Innbetalinger og avslutt av sak har           *
0900  *                   informasjon i pos. 81 - 111, legges           *
0900  *                   ut som en tilleggspost på GINKPF.             *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    60-69 = Fileindikatorer chain etc.                           *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgretpf    uf   e             disk
     fgtgipf    o  a e             disk
     fgocrpf    o  a e             disk
     fgktopf    o  a e             disk
     fginkpf    o  a e             disk
      *
      *  Datastruktur for testing av returtyper på GRETPF:
      *
     d                 ds
     d dsbexx                        80
     d  dsink1                        4    overlay(dsbexx)
     d   dsrekt                       2    overlay(dsink1)
     d  dsink2                        4    overlay(dsbexx:3)
     d  dsink3                        4    overlay(dsbexx:14)
     d  dsbetf                        6    overlay(dsbexx:41)
      *
     d i               s              5  0
     d j               s              5  0
     d k               s              5  0
     d l               s              5  0
     d wink33          s             33
     d wink40          s             40
      *
      /eject
      *
      *
      *  Variabelen "I" inneholder antall OCR-poster på GRETPF:
      *  Variabelen "J" inneholder antall TELEGIRO-poster på GRETPF:
      *  Variabelen "K" inneholder antall KONTOINFO-poster på GRETPF:
      *  Variabelen "L" inneholder antall INKASSO-poster på GRETPF:
      *
     c                   eval      i = 0
     c                   eval      j = 0
     c                   eval      k = 0
     c                   eval      l = 0
      *
     c                   read      gretpf                                 60
      *
     c     *in60         cabeq     *on           xslutt
      *
     c     xttag1        tag
      *
      * Legger lest record over til datastruktur.  Denne  delen
      * inneholder informasjon som forteller bl.a. type record:
      *
     c                   movel     gdqret        dsbexx
      *
      * Tester om OCR-retur. Dette kjennetegnes ved "NY" i pos 1-2.
      * Slår testen til, skrives posten ut på GOCRPF, inn-posten
      * slettes og ny post leses.
      *
     c                   if        dsrekt = 'NY'                                ........
     c                   eval      i = i + 1                                           .
     c                   movel     dsbexx        gdqocr                                .
     c                   write     gocrpfr                                             .
     c                   delete    gretpfr                                             .
     c                   read      gretpf                                 60           .
     c     *in60         cabeq     *on           xslutt                                .
     c                   goto      xttag1                                              .
     c                   endif                                                  ........
      *
      * Tester om REM-retur. Dette kjennetegnes ved "AH" i pos 1-2
      * og BETFOR i pos 41-46.
      * Slår testen til, skrives posten ut på GTGIPF, inn-posten
      * slettes og de tre neste postene leses og legge ut uten test.
      * Dette pga. at REM-returer kommer i blokker på 4 x 80 pos.
      * Neste post for behandling leses til slutt.
      *
     c                   if        dsrekt = 'AH'                                ........
     c                   if        dsbetf = 'BETFOR'                                   .
     c                   eval      j = j + 1                                           .
     c                   movel     dsbexx        gdqtgi                                .
     c                   write     gtgipfr                                             .
     c                   delete    gretpfr                                             .
      *                                                           .
      * Leser inn de 3 neste postene for å få inn hele recorden:  .
      *                                                           .
     c                   do        3                                            ...    .
     c                   read      gretpf                                 60      .    .
     c     *in60         cabeq     *on           xslutt                           .    .
     c                   movel     gdqret        gdqtgi                           .    .
     c                   write     gtgipfr                                        .    .
     c                   delete    gretpfr                                        .    .
     c                   enddo                                                  ...    .
      *                                                           .
     c                   read      gretpf                                 60           .
     c     *in60         cabeq     *on           xslutt                                .
     c                   goto      xttag1                                              .
     c                   endif                                                         .
     c                   endif                                                  ........
      *
      * Tester om Inkasso-retur. Dette kjennetegnes ved
      * "ZXCN" i pos 1-2, pos 3-6 eller pos 14-17.
      * Slår testen til, skrives posten ut på GINKPF, inn-posten
      * slettes og ny post leses.
      *
     c                   if        dsink1 = 'ZXCN'                              ........
     c                             or dsink2 = 'ZXCN'                                  .
     c                             or dsink3 = 'ZXCN'                                  .
     c                   eval      l = l + 1                                           .
     c                   movel     dsbexx        gdqink                                .
     c                   write     ginkpfr                                             .
     c                   delete    gretpfr                                             .
0900Tc                   if        dsink3 = 'ZXCN'                              .....  .
0900Tc                   movel     dsbexx        wink33                             .  .
0900Tc                   move      '.2'          wink33                             .  .
0900Tc                   read      gretpf                                 60        .  .
0900Tc                   movel     gdqret        wink40                             .  .
0900Tc                   eval      dsbexx = *blank                                  .  .
0900Tc                   movel     wink33        dsbexx                             .  .
0900Tc                   move      wink40        dsbexx                             .  .
0900Tc                   move      dsbexx        gdqink                             .  .
0900Tc                   write     ginkpfr                                          .  .
0900Tc                   delete    gretpfr                                          .  .
0900Tc                   endif                                                  .....  .
     c                   read      gretpf                                 60           .
     c     *in60         cabeq     *on           xslutt                                .
     c                   goto      xttag1                                              .
     c                   endif                                                  ........
      *
      * Poster som ikke matcher foregående poster behandles som
      * kontoinformasjon. Det er vanskelig å gjøre konkrete tester
      * da alle banker gir forskjellig lay-out på kontoinformasjon.
      *
     c                   eval      k = k + 1                                           .
     c                   movel     dsbexx        gdqkto                                .
     c                   write     gktopfr                                             .
     c                   delete    gretpfr                                             .
     c                   read      gretpf                                 60           .
     c     *in60         cabeq     *on           xslutt                                .
     c                   goto      xttag1                                              .
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
