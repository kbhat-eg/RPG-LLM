     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO565R
      * Beskrivelse . . : Viser alle depositumsposter pr. kunde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161216 bof  Nytt program
7.xx  * 7.00  040419 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.xx  * 7.00  040419 bsa  Mulighet for bredere skjerm
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skvdrmindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsdeplu    uf   e           k disk    rename(sdeppfr:sdeplur)
     fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
      *
     fFO565d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(skfirm)
     d p_kund          s                   like(skkund)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d rkunlr_kund     s                   like(rkkund)
     d sdeplu_kund     s                   like(skkund)
     d sdeplu_numm     s                   like(sknumm)
     d sdeplu_tell     s                   like(sktell)
     d sdepl1_kund     s                   like(skkund)
     d sdepl1_numm     s                   like(sknumm)
     d sdepl1_tell     s                   like(sktell)
     d fohel1_numm     s                   like(fonumm)

      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
6.30 d b_inlr          s              1    inz(*off)
      *
      *
     d w_seqe          s              1
     d b_subf          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
     d b_funn          s              1    inz(*off)
      *
     d w_firm          s                   like(skfirm)
     d w_kund          s                   like(skkund)
     d w_numm          s                   like(sknumm)
     d w_suff          s                   like(fosuff)
     d w_sumi          s             11  2
      *
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skvdrmbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skvdrmbilde for CMD-taster
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
     c                   if        b_funn = *off
     c                   goto      avslutt
     c                   endif
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   if        b_funn = *off
     c                   goto      avslutt
     c                   endif
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Legg inn innbetaling
      *
     c                   if        b1valg = 2

     c                   exsr      xs1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Se ordre
      *
     c                   if        b1valg = 5
     c                   eval      w_suff = 0
     c                   call      'FO505R'
     c                   parm                    w_firm
     c                   parm                    b1numm
     c                   parm                    w_suff
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif

      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
     c+                  select sknumm,
     c+                         skdepo,
     c+                         skdepb
     c+                  from   sdeppf
     c+                  where  skfirm = :w_firm and
     c+                         skkund = :w_kund and
     c+                         sktell = 0    and
     c+                         skdepo <> 0 and
     c+                         skdepb < skdepo
     c+                  order by sknumm
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :sknumm,
     c+                        :skdepo,
     c+                        :skdepb
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
      *
     c                   if        skkund = p_kund
     c                   iter
     c                   endif
     c
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b_funn = *on
     c                   eval      b1valg = 0
     c                   eval      b1numm = sknumm
     c                   eval      b1depo = skdepo
     c                   eval      b1depb = skdepb
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'S'
     c                   exsr      forny
     c                   goto      end_bck
     c                   endif
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for reg av. innbetalt depositum
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     xs1win        begsr
      *
     c                   eval      s1tota = 0
     c                   eval      s1100p = 0
     c                   eval      s1depp = 0
     c                   eval      s1depo = 0
     c                   eval      s1depb = 0
     c                   eval      s1truk = 0
     c                   eval      s1iupd = 0

      * Beregn ordretotal
     c                   eval      w_sumi = 0
     c                   eval      fohel1_numm = b1numm
     c     fohel1_key    setll     fohel1
     c     fohel1_key    reade     fohel1
     c                   dow       not %eof(fohel1)
     c                   call      'FO703R '
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_sumi
     c                   eval      s1tota = s1tota  + w_sumi
     c     fohel1_key    reade     fohel1
     c                   enddo
      *
     c                   eval      sdepl1_numm = b1numm
     c                   eval      sdepl1_kund = b2kund
     c                   eval      sdepl1_tell = 0
     c     sdepl1_key    chain     sdepl1r
     c                   if        %found(sdepl1)
     c                   eval      s1iupd = skiupd
     c                   eval      s1100p = sk100p
     c                   eval      s1depp = skdepp
     c                   eval      s1depo = skdepo
     c                   eval      s1truk = skdepo - skdepb
     c                   endif
      *
      * Send bilde o1win
      *
     c     S1tagb        tag
      *
     c                   exfmt     S1win
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   eval      *inkc = *on
     c                   leavesr
     c                   endif
      *
      * Validering
      *
     c                   if        s1depb = 0
     c                   eval      *in31 = *on
     c                   goto      s1tagb
     c                   endif
      *
     c                   eval      sdeplu_numm = b1numm
     c                   eval      sdeplu_kund = b2kund
     c                   eval      sdeplu_tell = 0
     c     sdeplu_key    chain     sdeplur
     c                   if        %found(sdeplu)
     c                   eval      skiupd = s1iupd
     c                   eval      skityp = 'M'
     c                   eval      skdepb = skdepb + s1depb
     c                   time                    skedat
     c                   time                    sketim
     c                   eval      skeusr = l_user
     c                   eval      skwsid = l_wsid
     c                   update    sdeplur
     c                   endif
     c                   eval      b1depb = skdepb
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.20 c                   parm                    p_kund
      *
      * Key for oppslag på kunde
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
6.40  *
6.40  * Key for oppdat  på depositum-fil
6.40  *
6.40 c     sdeplu_key    klist
6.40 c                   kfld                    w_firm
6.40 c                   kfld                    sdeplu_numm
6.40 c                   kfld                    sdeplu_kund
6.40 c                   kfld                    sdeplu_tell
6.40  *
6.40  * Key for oppslag på depositum-fil
6.40  *
6.40 c     sdepl1_key    klist
6.40 c                   kfld                    w_firm
6.40 c                   kfld                    sdepl1_numm
6.40 c                   kfld                    sdepl1_kund
6.40 c                   kfld                    sdepl1_tell
      *
6.40  * Key for oppslag på depositum-fil
6.40  *
6.40 c     fohel1_key    klist
6.40 c                   kfld                    w_firm
6.40 c                   kfld                    fohel1_numm


      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2kund = p_kund
     c                   eval      w_kund = p_kund
      *
      *
     c                   eval      rkunlr_kund = w_kund
     c     rkunlr_key    chain     rkunlr
     c                   if        %found(rkunlr)
     c                   movel     rknavn        b2navn
     c                   endif
      *
     c                   eval      b_funn = *off
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
