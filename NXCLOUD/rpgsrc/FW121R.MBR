      /TITLE  ASOFAK Lev.vare, oppretter nye varer - behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW121R
      * Beskrivelse . . : Oppretting av nye varer, behandling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060599 HKO  Nytt program
      *       161103 HKO  Fjernet behandling av rabattgruppe
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  *       180515 BKV  Endret LVAR pga økt fra 15 til 20 tegn
7.xx  * 7.00  260618 bkv  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflval2    if   e           k disk    rename(flvapfr:flval2r)
     fflval3    if   e           k disk    rename(flvapfr:flval3r)
     fflval4    if   e           k disk    rename(flvapfr:flval4r)
     fflval5    if   e           k disk    rename(flvapfr:flval5r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fflvalr    if   e           k disk    rename(flvapfr:flvalrr)
     fflvalu    uf   e           k disk    rename(flvapfr:flvalur)
      *
     ffw121d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fwfirm)
     d p_ldor          s                   like(fwldor)
     d p_lvaf          s                   like(fwlvar)
     d p_lvat          s                   like(fwlvar)
     d p_dato          s               d   datfmt(*iso)
     d p_ipro          s              5  2
     d p_kpro          s              5  2
     d p_spro          s              5  2
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d flval3_tek1     s                   like(fwtek1)
     d flval2_lvar     s                   like(fwlvar)
     d flval4_eann     s                   like(fweann)
     d flval5_nobb     s                   like(fwnobb)
     d vvarl5_lvar     s                   like(vvlvar)
     d rlevl1_levr     s                   like(rllevr)
     d flvalr_line     s                   like(fwline)
     d flvalu_line     s                   like(fwline)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_seqe          s              1
      *
     d w_valg          s              1  0
     d w_indi          s              2  0
     d w_kvar          s              1  0
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
     d w_ogrp          s                   like(vvogrp)
     d w_hgrp          s                   like(vvhgrp)
     d w_ugrp          s                   like(vvugrp)
     d w_vtyp          s                   like(vvvtyp)
     d w_kpri          s                   like(vvkpri)
     d w_varf          s                   like(vvvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F13=Statuskoder
      *
     c                   if        *inkm = *on
     c                   call      'FW110R'
     c                   goto      b2taga
     c                   endif
      *
      * F14=Gruppebehandling
      *
     c                   if        *inkn = *on
     c                   call      'FW122R'
     c                   parm                    w_firm
     c                   parm                    p_ldor
     c                   parm                    p_lvaf
     c                   parm                    p_lvat
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * F15=Standardverdier
      *
     c                   if        *inkp = *on
     c                   call      'FW123R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_vtyp
     c                   parm                    w_kpri
     c                   parm                    w_varf
     c                   parm                    w_kvar
     c                   goto      b2taga
     c                   endif
      *
      * F20=Start overføring
      *
     c                   if        *inku = *on
      *
     c                   if        w_ogrp = 0 or
     c                             w_hgrp = 0 or
     c                             w_vtyp = *blank
     c                   eval      *in31 = *on
     c                   goto      b2tagb
     c                   endif
      *
     c                   call      'FW124R'
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_lvaf
     c                   parm                    p_lvat
     c                   parm                    p_dato
     c                   parm                    p_ipro
     c                   parm                    p_kpro
     c                   parm                    p_spro
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_vtyp
     c                   parm                    w_kpri
     c                   parm                    w_varf
     c                   parm                    w_kvar
      *
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * F23=Bytt posisjonering (Lev.varenr/EAN-nr/Nobbnr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2lvar <> *blank or
     c                             b2eann <> 0 or
     c                             b2nobb <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c     flval2_key    setll     flval2
     c                   when      w_seqe = 'E'
     c     flval4_key    setll     flval4
     c                   when      w_seqe = 'N'
     c     flval5_key    setll     flval5
     c                   other
     c     flval3_key    setll     flval3
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      flval3_tek1 = b2tek1
     c     flval3_key    setll     flval3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leverandørens varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      w_seqe = 'L'
     c                   eval      flval2_lvar = b2lvar
     c     flval2_key    setll     flval2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på EAN-nummer
      *
     c                   if        b2eann <> 0
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   eval      *in82 = *off
     c                   eval      w_seqe = 'E'
     c                   eval      flval4_eann = b2eann
     c     flval4_key    setll     flval4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på Nobbnummer
      *
     c                   if        b2nobb <> 0
     c                   eval      *in80 = *off
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   eval      w_seqe = 'N'
     c                   eval      flval5_nobb = b2nobb
     c     flval5_key    setll     flval5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2lvar = *blank
     c                   eval      b2eann = 0
     c                   eval      b2nobb = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Holde tilbake
      *
     c                   if        b1valg = 3
     c                   exsr      hold_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   call      'FW112R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    b1line
     c                   parm                    w_kvar
     c                   parm                    b1stat
     c                   parm                    b1valg
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Frigi
      *
     c                   if        b1valg = 6
     c                   exsr      frigi_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for endring av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_valg = 2
      *
     c                   call      'FW112R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    b1line
     c                   parm                    w_kvar
     c                   parm                    b1stat
     c                   parm                    w_valg
      *
      * Oppdater subfile
      *
     c                   eval      flvalr_line = b1line
     c     flvalr_key    chain     flvalr                             90
     c                   if        *in90 = *off
     c                   exsr      fyll_subfile
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å holde vare tilbake fra overføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hold_vare     begsr
      *
     c                   eval      flvalu_line = b1line
     c     flvalu_key    chain     flvalur                            90
     c                   if        *in90 = *off
     c                   eval      fwhkod = 1
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   update    flvalur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   eval      b1hkod = 'X'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å frigi vare for overføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     frigi_vare    begsr
      *
     c                   eval      flvalu_line = b1line
     c     flvalu_key    chain     flvalur                            90
     c                   if        *in90 = *off
     c                   eval      fwhkod = 0
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   update    flvalur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   eval      b1hkod = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'                                              .
     c                   read      flval2                                 15
     c                   when      w_seqe = 'E'                                              .
     c                   read      flval4                                 15
     c                   when      w_seqe = 'N'                                              .
     c                   read      flval5                                 15
     c                   other                                                               .
     c                   read      flval3                                 15                 .
     c                   endsl                                                               .
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
      * Test på om en rad vare skal være med på utplukket
      *
     c                   if        fwfirm <> w_firm or
     c                             fwldor <> p_ldor
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        p_lvaf <> *blank and
     c                             fwlvar < p_lvaf
     c                   iter
     c                   endif
      *
     c                   if        p_lvat <> *blank and
     c                             fwlvar > p_lvat
     c                   iter
     c                   endif
      *
     c                   if        fwlvar <> *blank
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5                             91
     c                   if        *in91 = *off
     c                   iter
     c                   endif
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1line = fwline
      *
     c                   exsr      fyll_subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle rad i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fyll_subfile  begsr
      *
     c                   eval      b1tek1 = fwtek1
     c                   eval      b1enhe = fwenhe
     c                   eval      b1sapr = fwsapr
     c                   eval      b1ekod = fwekod
     c                   eval      b1stat = 0
     c                   eval      b1hkod = *blank
      *
     c                   select
     c                   when      *in80 = *on
6.30 c*                  eval      b1nypo = fwlvar
6.30 c                   movel     fwlvar        b1nypo
     c                   when      *in81 = *on
     c                   eval      b1nypo = %editc(fweann : 'Z')
     c                   when      *in82 = *on
     c                   eval      b1nypo = %editc(fwnobb : 'Z')
     c                   endsl
      *
     c                   call      'FW113R'
     c                   parm                    w_firm
     c                   parm                    w_ldor
     c                   parm                    fwline
     c                   parm                    fwlvar
     c                   parm                    fwtek1
     c                   parm                    fweann
     c                   parm                    fwnobb
     c                   parm                    fwenhe
     c                   parm                    fwinpr
     c                   parm                    fwsapr
     c                   parm                    fwtvar
     c                   parm                    w_kvar
     c                   parm                    b1stat
     c                   parm                    w_indi
      *
     c                   if        fwhkod = 1
     c                   eval      b1hkod = 'X'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_lvaf
     c                   parm                    p_lvat
     c                   parm                    p_dato
     c                   parm                    p_ipro
     c                   parm                    p_kpro
     c                   parm                    p_spro
      *
      * Key for posisjonering på leverandørs varenummer
      *
     c     flval2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval2_lvar
      *
      * Key for posisjonering på varetekst-1
      *
     c     flval3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval3_tek1
      *
      * Key for posisjonering på EAN-nummer
      *
     c     flval4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval4_eann
      *
      * Key for posisjonering på Nobb-nummer
      *
     c     flval5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flval5_nobb
      *
      * Key for oppslag på vare-register med leverandørs varenummer
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppslag mot leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på leverandør vareregister
      *
     c     flvalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flvalr_line
      *
      * Key for oppdatering av leverandør vareregister
      *
     c     flvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    flvalu_line
      *
      *
      * Initierer tabell-kolonner
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
      * Initierer skjermbilde-felt
      *
     c                   eval      b2ldor = p_ldor
      *
     c                   eval      rlevl1_levr = p_ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b2navn = rlnavn
     c                   endif
      *
      * Initierer arbeidsfelt
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
      *
      * Posisjonerer i tabell, og fyller subfile
      *
     c     flval3_key    setll     flval3
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
