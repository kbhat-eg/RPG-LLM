     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JX431R
      * Beskrivelse . . : Vare, sletting av priser
      *                   -hvis til dato er lavere enn inntastet dato
      *                   -hvis lev.nr er inntastet slettes ikke prisen
      *                    hvis dette er lev.nr på varen.
6.11  * (nytt valg)       -hvis fra dato er lavere enn inntastet dato
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210810 bof  Nytt program
6.11  *       190713 bof  lagt inn valg på om man vil slette på fra dato også
6.30  *       160714 bof  Utvidelse av nøkkel vare-pris
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvprlr    if   e           k disk    rename(jvprpfr:jvprlrr)
     fjvprl2    if   e           k disk    rename(jvprpfr:jvprl2r)
     fjlevlr    if   e           k disk    rename(jlevpfr:jlevlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvprlu    uf   e           k disk    rename(jvprpfr:jvprlur)
      *
      * Definering av parametre
      *
     d p_list          s             20
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        20
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldor                        6  0 overlay(d_list:4)
     d  d_dato                        6  0 overlay(d_list:10)
     d  d_valg                        1  0 overlay(d_list:16)
6.11 d  d_vfra                        1  0 overlay(d_list:17)
6.11 d  d_vtil                        1  0 overlay(d_list:18)
      *
      * Definering av variable i nøkler
      *
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
6.30 d jvprlu_gdat     s                   like(jxgdat)
     d jvprlu_rsts     s                   like(jxrsts)
     d jvprlr_vare     s                   like(jxvare)
     d jvprl2_ldor     s                   like(jxldor)
     d jlevlr_ldor     s                   like(jlldor)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_dato          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   select
     c                   when      d_ldor <> 0
     c                   exsr      slett_ldor
     c                   other
     c                   exsr      slett_alle
     c                   endsl
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av varer for leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_ldor    begsr
      *
     c                   eval      jvprl2_ldor = d_ldor
     c     jvprl2_key    setll     jvprl2
     c     jvprl2_key    reade     jvprl2
      *
     c                   dow       not %eof
6.11 c                   if        d_vtil = 1
     c                   if        jxtdat <> *loval and
     c                             jxtdat <  w_dato
     c                   exsr      slett_pris
6.11 c                   goto      neste
     c                   endif
6.11 c                   endif
6.11 c                   if        d_vfra = 1
6.11 c                   if        jxgdat <> *loval and
6.11 c                             jxgdat <  w_dato
6.11 c                   exsr      slett_pris
6.11 c                   goto      neste
6.11 c                   endif
6.11 c                   endif
6.11 c     neste         tag
     c     jvprl2_key    reade     jvprl2
      *
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av alle varer med statuskode 4
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_alle    begsr
      *
     c                   eval      jvprlr_vare = *blank
      *
     c     jvprlr_key    setll     jvprlr
     c                   read      jvprlr
     c                   dow       not %eof
6.11 c                   if        d_vtil = 1
     c                   if        jxtdat <> *loval and
     c                             jxtdat <  w_dato
     c                   exsr      slett_pris
6.11 c                   goto      neste2
     c                   endif
6.11 c                   endif
6.11 c                   if        d_vfra = 1
6.11 c                   if        jxgdat <> *loval and
6.11 c                             jxgdat <  w_dato
6.11 c                   exsr      slett_pris
6.11 c                   endif
6.11 c                   endif
6.11 c     neste2        tag
     c                   read      jvprlr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_pris    begsr
      *
     c                   if        d_valg = 1
     c                   eval      jlevlr_ldor = jxldor
     c     jlevlr_key    chain     jlevlr
     c                   if        %found(jlevlr)
     c                   eval      vvarl1_vare = jxvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1) and
     c                             vvldor = jlenum
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
      * sletter pris
      *
     c                   eval      jvprlu_vare = jxvare
     c                   eval      jvprlu_ldor = jxldor
6.30 c                   eval      jvprlu_gdat = jxgdat
     c                   eval      jvprlu_rsts = jxrsts
     c     jvprlu_key    chain     jvprlu
     c                   if        %found(jvprlu)
     c                   delete    jvprlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på vare-pris
      *
     c     jlevlr_key    klist
     c                   kfld                    jlevlr_ldor
      *
      * Key for les på vare-pris
      *
     c     jvprlr_key    klist
     c                   kfld                    jvprlr_vare
      *
      * Key for les på vare-pris m/leverandør
      *
     c     jvprl2_key    klist
     c                   kfld                    jvprl2_ldor
      *
      * Key for slette vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
6.30 c                   kfld                    jvprlu_gdat
     c                   kfld                    jvprlu_rsts
      *
      * Key for oppslag på vare (EVR)
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter parametre og firma
      *
     c                   eval      d_list = p_list
      *
     c     *dmy          move      d_dato        w_dato
     c                   eval      w_firm = d_firm
      *
     c                   endsr
