     h/TITLE  Utskrift av autogiro - kvittering
     h option(*nodebugio)
     h decedit(',') datedit(*dmy.)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK821R                                              *
      * Beskrivelse . . : Utskrift av autogiro-kontroll                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
6.nu  * R6.30 140814 nho  Endring vedr kid, sett etter FO616R                 *
7.nx  * K000  171120 bsa  Endringer rekatert til One Nexstep                  *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkw4pf    if   e           k disk
     frkwapf    if   e           k disk
     frkunpf    if   e           k disk
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fapospf    if   e           k disk
     fraa7lu    uf a e           k disk    rename(raa7pfr:raa7lur)
     frkw4lu    o  a e           k disk    rename(rkw4pfr:rkw4lur)
     frautpf    o  a e           k disk
     frk821p    o    e             printer oflind(*in30)
      *
     d sub             s              3  0 dim(13)
      *
     d                 ds
     d d_parm                       120
     d  pfirm                         3  0 overlay(d_parm)
     d  pnavn                        30    overlay(d_parm:4)
     d  puser                        10    overlay(d_parm:34)
     d  pkdat                         6  0 overlay(d_parm:44)
     d  praar                         4  0 overlay(d_parm:50)
     d  prper                         2  0 overlay(d_parm:54)
     d  pavdf                         4  0 overlay(d_parm:56)
     d  pavdt                         4  0 overlay(d_parm:60)
     d  pkatf                         4  0 overlay(d_parm:64)
     d  pkatt                         4  0 overlay(d_parm:68)
     d  pself                         4  0 overlay(d_parm:72)
     d  pselt                         4  0 overlay(d_parm:76)
     d  pknrf                         6  0 overlay(d_parm:80)
     d  pknrt                         6  0 overlay(d_parm:86)
     d  pbilf                         6  0 overlay(d_parm:92)
     d  pbilt                         6  0 overlay(d_parm:98)
     d  pfdat                         6  0 overlay(d_parm:104)
     d  palfa                         1    overlay(d_parm:110)
      *
     d                 ds
     d ffforf                  3      8  0                                      Forfal
     d  ffford                        2  0 overlay(ffforf)                       fra
     d  ffform                        2  0 overlay(ffforf:3)                      tran
     d  ffforÅ                        2  0 overlay(ffforf:5)
     d                 ds
     d wfforf                  3      8  0                                      Forfal
     d  wfford                        2  0 overlay(wfforf)                       fra
     d  wfform                        2  0 overlay(wfforf:3)                      tran
     d  wfforÅ                        2  0 overlay(wfforf:5)
     d wsforf                 11     16  0                                      Forfal
     d  wsford                        2  0 overlay(wsforf)                       eldst
     d  wsform                        2  0 overlay(wsforf:3)                      forf
     d  wsforÅ                        2  0 overlay(wsforf:5)                       opp
     d wftorf                 19     24  0                                      Forfal
     d  wftord                        2  0 overlay(wftorf)                       tidli
     d  wftorm                        2  0 overlay(wftorf:3)                      forf
     d  wftorÅ                        2  0 overlay(wftorf:5)                       opp
     d                 ds
     d wffofd                         8  0                                      Forfal
     d  rnforÅ                        4  0 overlay(wffofd)                       fra
     d  rnform                        2  0 overlay(wffofd:5)                      tran
     d  rnford                        2  0 overlay(wffofd:7)
     d                 ds
      * - - - - - - - - - - - - - -
      * Datastruktur for kid-feltet
      * - - - - - - - - - - - - - -
     d dskkid                        21
     d  dskkus                       20    overlay(dskkid)
     d   dskfir                       3  0 overlay(dskkus)
     d   dskkod                       1  0 overlay(dskkus:4)
6.nu d   dsktyp                       1  0 overlay(dskkus:5)
6.nu d   dskled                       1  0 overlay(dskkus:6)
6.nu d   dskkto                       6  0 overlay(dskkus:7)
6.nu d   dskbil                       8  0 overlay(dskkus:13)
     d  dskmod                        1  0 overlay(dskkid:21)
     d                uds
     d dssfil                931    933
      *
     d w_parm          s            120
     d Åtte0           s              8  0
     d en              s              1
     d fir0            s              4  0
     d kÅrmnd          s                   like(seks0)
     d kidd10          s             24
     d n               s              3  0
     d pÅrmnd          s                   like(seks0)
     d pdatx           s                   like(Åtte0)
     d prdagx          s                   like(en)
     d seks0           s              6  0
     d to0             s              2  0
     d wÅÅÅÅ           s              4  0
     d wantfa          s              3  0
     d wantko          s              3  0
     d wantli          s              6  0
     d wantop          s                   like(Åtte0)
     d warb1           s              4  0
     d wa7for          s                   like(ra7for)
     d wa7onr          s                   like(ra7onr)
     d wbforp          s             11  2
     d wbfort          s             11  2
     d wbilda          s              6  0
     d wffdat          s              6  0
     d wfffda          s                   like(Åtte0)
     d wftfda          s                   like(Åtte0)
     d wkjdat          s              6  0
     d wnforÅ          s                   like(rnform)
     d wnford          s                   like(rnford)
     d wnform          s                   like(rnform)
     d wpÅr            s              2  0
     d wpÅr4           s              4  0
     d wpdag           s              2  0
     d wpkid           s             25
     d wqdatÅ          s              2  0
     d wsffda          s                   like(Åtte0)
     d wsumfa          s             10  2
     d wsumhs          s             10  2
     d wsummr          s                   like(rnbelØ)
     d wtomÅ           s                   like(fir0)
     d wtomd           s                   like(to0)
     d wtomm           s                   like(to0)
     d wtotfa          s              3  0
     d xiforf          s              6  0
     d wnkund          s              6
     d w_wdat8         s              8  0
     d w_wdat6         s              6  0
      *
      * Boolske variable til erstatning for *INxx-variable
      *
     d b_firm          s              1    inz(*off)
     d b_kund          s              1    inz(*off)
     d b_bel0          s              1    inz(*off)
     d b_bneg          s              1    inz(*off)
     d b_bilk          s              1    inz(*off)
     d b_saln          s              1    inz(*off)
     d b_rekl          s              1    inz(*off)
     d b_ok            s              1    inz(*off)
      *
     d s_firm          s                   like(rnfirm) inz(999)
     d s_kund          s                   like(rnkund) inz(999999)
      *
      * Autogiro/recordlayout BBS
      *
      * Startrecord for forsendelse - Tjenestekode = 00
     d                 ds
     d gauald                        80                                         Hele utvekslingsfelt
     d  gaafko                        2    overlay(gauald)                      Formatkode = NY
     d  gaatje                        2    overlay(gauald:3)                    Tjenestekode = 00
     d  gaafty                        2  0 overlay(gauald:5)                    Forsendelsetype = 00
     d  gaarec                        2  0 overlay(gauald:7)                    Recordtype = 10
     d  gaadav                        8  0 overlay(gauald:9)                    Dataavsender
     d  gaafor                        7  0 overlay(gauald:17)                   Forsendelsenummer
     d  gaadmo                        8  0 overlay(gauald:24)                   Datamottaker = 00008
     d  gaafil                       49    overlay(gauald:32)                   Filler = ZEROS
      *
      * Startrecord for inkasso-oppdrag - Tjenestekode = 01
     d                 ds
     d gaubld                        80                                         Hele utvekslingsfelt
     d  gabfko                        2    overlay(gaubld)                      Formatkode = NY
     d  gabtje                        2    overlay(gaubld:3)                    Tjenestekode=01
     d  gaboty                        2  0 overlay(gaubld:5)                    Oppdragstype=00
     d  gabrec                        2  0 overlay(gaubld:7)                    Recordtype = 20
     d  gabavt                        9  0 overlay(gaubld:9)                    Avtale-ID
     d  gabonr                        7  0 overlay(gaubld:18)                   Oppdragsnummer
     d  gabkto                       11  0 overlay(gaubld:25)                   Oppdragskonto
     d  gabfil                       45    overlay(gaubld:36)                   Filler = ZEROS
      *
      * Transaksjonsrecord - Tjenestekode = 01 Beløpspost 1
     d                 ds
     d gauc1l                        80                                         Hele utvekslingsfelt
     d  gacfk1                        2    overlay(gauc1l)                      Formatkode = NY
     d  gactj1                        2    overlay(gauc1l:3)                    Tjenestekode=01
     d  gactt1                        2  0 overlay(gauc1l:5)                    Transaksjonstype(02/
     d  gacrc1                        2  0 overlay(gauc1l:7)                    Recordtype = 30
     d  gacnr1                        7  0 overlay(gauc1l:9)                    Transaksjonsnummer
     d  gacdat                        6  0 overlay(gauc1l:16)                   Forfallsdato(DDMMÅÅ)
     d  gackto                       11    overlay(gauc1l:22)                   Betalers referanse/k
     d  gacbel                       17  2 overlay(gauc1l:33)                   Beløp
     d  gackid                       25    overlay(gauc1l:50)                   Ident for trans
     d  gacfil                        6    overlay(gauc1l:75)                   Filler = ZEROS
      *
      * Transaksjonsrecord - Tjenestekode = 01 Beløpspost 2
     d                 ds
     d gauc2l                        80                                         Hele utvekslingsfelt
     d  gacfk2                        2    overlay(gauc2l)                      Formatkode = NY
     d  gactj2                        2    overlay(gauc2l:3)                    Tjenestekode=01
     d  gactt2                        2  0 overlay(gauc2l:5)                    Transaksjonstype(02/
     d  gacrc2                        2  0 overlay(gauc2l:7)                    Recordtype = 31
     d  gacnr2                        7  0 overlay(gauc2l:9)                    Transaksjonsnummer
     d  gacnav                       10    overlay(gauc2l:16)                   Forkortet navn
     d  gacref                       25    overlay(gauc2l:26)                   Egenreferanse
     d  gacfrf                       25    overlay(gauc2l:51)                   Fremmedreferanse
     d  gacfi1                        5    overlay(gauc2l:76)                   Filler = ZEROS
      *
      * Navn/Adressepost - Tjenestekode = 01 Adressepost 1
     d                 ds
     d gaud1l                        80                                         Hele utvekslingsfelt
     d  gadfk1                        2    overlay(gaud1l)                      Formatkode = NY
     d  gadtj1                        2    overlay(gaud1l:3)                    Tjenestekode=01
     d  gadtt1                        2  0 overlay(gaud1l:5)                    Transaksjonstype=03
     d  gadrc1                        2  0 overlay(gaud1l:7)                    Recordtype = 40
     d  gadnr1                        7  0 overlay(gaud1l:9)                    Transaksjonsnummer
     d  gadml1                        1  0 overlay(gaud1l:16)                   Melding = 3
     d  gadnav                       30    overlay(gaud1l:17)                   Navn på mottaker
     d  gadpnr                        4  0 overlay(gaud1l:47)                   Postnummer
     d  gadpfi                        3    overlay(gaud1l:51)                   Postfiller
     d  gadste                       25    overlay(gaud1l:54)                   Poststed
     d  gadfi1                        2    overlay(gaud1l:79)                   Filler = ZEROS
      *
      * Postboks/Gate/Vei - Tjenestekode = 01 Adressepost 2
     d                 ds
     d gaud2l                        80                                         Hele utvekslingsfelt
     d  gadfk2                        2    overlay(gaud2l)                      Formatkode = NY
     d  gadtj2                        2    overlay(gaud2l:3)                    Tjenestekode=01
     d  gadtt2                        2  0 overlay(gaud2l:5)                    Transaksjonstype=03
     d  gadrc2                        2  0 overlay(gaud2l:7)                    Recordtype = 41
     d  gadnr2                        7  0 overlay(gaud2l:9)                    Transaksjonsnummer
     d  gadml2                        1  0 overlay(gaud2l:16)                   Melding = 3
     d  gadad1                       30    overlay(gaud2l:17)                   Adresse
     d  gadad2                       30  0 overlay(gaud2l:47)                   Adresse 2
     d  gadlan                        3    overlay(gaud2l:77)                   Landkode
     d  gadfi2                        1    overlay(gaud2l:80)                   Filler = ZEROS
      *
      * Spesifikasjonsrecord Tjenestekode = 01
     d                 ds
     d gaueld                        80                                         Hele utvekslingsfelt
     d  gaefko                        2    overlay(gaueld)                      Formatkode = NY
     d  gaetje                        2    overlay(gaueld:3)                    Tjenestekode=01
     d  gaetty                        2  0 overlay(gaueld:5)                    Transaksjonstype=03
     d  gaerec                        2  0 overlay(gaueld:7)                    Recordtype = 49
     d  gaetnr                        7  0 overlay(gaueld:9)                    Transaksjonsnummer
     d  gaemld                        1  0 overlay(gaueld:16)                   Melding = 3
     d  gaelin                        3  0 overlay(gaueld:17)                   Plassering/Linje
     d  gaekol                        1  0 overlay(gaueld:20)                   Plassering/Kolonne
     d  gaemsp                       40    overlay(gaueld:21)                   Meldingsspesifikasjo
     d  gaefil                       20    overlay(gaueld:61)                   Filler = ZEROS
      *
      * Sluttrecord for inkasso-oppdrag - Tjenestekode = 01
     d                 ds
     d gaufld                        80                                         Hele utvekslingsfelt
     d  gaffko                        2    overlay(gaufld)                      Formatkode = NY
     d  gaftje                        2    overlay(gaufld:3)                    Tjenestekode=01
     d  gafoty                        2  0 overlay(gaufld:5)                    Oppdragstype=00
     d  gafrec                        2  0 overlay(gaufld:7)                    Recordtype = 88
     d  gafatr                        8  0 overlay(gaufld:9)                    Antall transaksjoner
     d  gafarc                        8  0 overlay(gaufld:17)                   Antall records
     d  gafsum                       17  2 overlay(gaufld:25)                   Sum beløp
     d  gafffa                        6  0 overlay(gaufld:42)                   Første forfall
     d  gafsfa                        6  0 overlay(gaufld:48)                   Siste  forfall
     d  gaffil                       27    overlay(gaufld:54)                   Filler = ZEROS
      *
      * Startrecord for fullmaktsoppdrag - Tjenestekode = 01
     d                 ds
     d gaugld                        80                                         Hele utvekslingsfelt
     d  gagfko                        2    overlay(gaugld)                      Formatkode = NY
     d  gagtje                        2    overlay(gaugld:3)                    Tjenestekode = 01
     d  gagoty                        2  0 overlay(gaugld:5)                    Oppdragstype = 24
     d  gagrec                        2  0 overlay(gaugld:7)                    Recordtype = 20
     d  gagavt                        9  0 overlay(gaugld:9)                    Avtale-id
     d  gagonr                        7  0 overlay(gaugld:18)                   Oppdragsnummer
     d  gagokt                       11  0 overlay(gaugld:25)                   Oppdragskonto
     d  gagfil                       45    overlay(gaugld:36)                   Filler = ZEROS
      *
      * Fullmaktsrecord Fullmaktspost 1 - Tjenestekode = 01
     d                 ds
     d gauh1l                        80                                         Hele utvekslingsfelt
     d  gahfk1                        2    overlay(gauh1l)                      Formatkode = NY
     d  gahtj1                        2    overlay(gauh1l:3)                    Tjenestekode = 01
     d  gahtt1                        2  0 overlay(gauh1l:5)                    Transaksjonstype=22/
     d  gahrc1                        2  0 overlay(gauh1l:7)                    Recordtype = 70
     d  gahlØ1                        7  0 overlay(gauh1l:9)                    Fullmaktens løpenumm
     d  gahrty                        1  0 overlay(gauh1l:16)                   Registreringstype=1/
     d  gahref                       11  0 overlay(gauh1l:17)                   Betalers referansenu
     d  gahmod                        1  0 overlay(gauh1l:28)                   Moduluskontroll = 1/
     d  gahkto                       11  0 overlay(gauh1l:29)                   Betalers kontonummer
     d  gahpko                        2  0 overlay(gauh1l:40)                   Periodekode = 00-06
     d  gahbel                       17  2 overlay(gauh1l:42)                   Beløpsgrense
     d  gahfda                        6  0 overlay(gauh1l:59)                   F.o.m. dato (DDMMÅÅ)
     d  gahtda                        6  0 overlay(gauh1l:65)                   T.o.m. dato (DDMMÅÅ)
     d  gahfi1                       10    overlay(gauh1l:71)                   Filler = ZEROS
      *
      * Fullmaktsrecord Fullmaktspost 2 - Tjenestekode = 01
     d                 ds
     d gauh2l                        80                                         Hele utvekslingsfelt
     d  gahfk2                        2    overlay(gauh2l)                      Formatkode = NY
     d  gahtj2                        2    overlay(gauh2l:3)                    Tjenestekode = 01
     d  gahtt2                        2  0 overlay(gauh2l:5)                    Transaksjonstype=22/
     d  gahrc2                        2  0 overlay(gauh2l:7)                    Recordtype = 71
     d  gahlØ2                        7  0 overlay(gauh2l:9)                    Fullmaktens løpenumm
     d  gahnav                       30    overlay(gauh2l:16)                   Betalers navn
     d  gahadr                       30    overlay(gauh2l:46)                   Betalers adresse
     d  gahfi2                        5    overlay(gauh2l:76)                   Filler = ZEROS
      *
      * Fullmaktsrecord Fullmaktspost 3 - Tjenestekode = 01
     d                 ds
     d gauh3l                        80                                         Hele utvekslingsfelt
     d  gahfk3                        2    overlay(gauh3l)                      Formatkode = NY
     d  gahtj3                        2    overlay(gauh3l:3)                    Tjenestekode = 01
     d  gahtt3                        2  0 overlay(gauh3l:5)                    Transaksjonstype=22/
     d  gahrc3                        2  0 overlay(gauh3l:7)                    Recordtype = 72
     d  gahlØ3                        7  0 overlay(gauh3l:9)                    Fullmaktens løpenumm
     d  gahad2                       30    overlay(gauh3l:16)                   Betalers adresse 2
     d  gahpnr                        4  0 overlay(gauh3l:46)                   Betalers postnummer
     d  gahpfi                        3    overlay(gauh3l:50)                   Betalers postnummer
     d  gahste                       25    overlay(gauh3l:53)                   Betalers poststed
     d  gahlan                        3    overlay(gauh3l:78)                   Landkode
      *
      * Sluttrecord for fullmaktsoppdrag - Tjenestekode = 01
     d                 ds
     d gauild                        80                                         Hele utvekslingsfelt
     d  gaifko                        2    overlay(gauild)                      Formatkode = NY
     d  gaitje                        2    overlay(gauild:3)                    Tjenestekode = 01
     d  gaioty                        2  0 overlay(gauild:5)                    Oppdragstype = 24
     d  gairec                        2  0 overlay(gauild:7)                    Recordtype = 88
     d  gaiatr                        8  0 overlay(gauild:9)                    Antall transaksjoner
     d  gaiarc                        8  0 overlay(gauild:17)                   Antall records
     d  gaibel                       17  2 overlay(gauild:25)                   Sum beløp
     d  gaifil                       39    overlay(gauild:42)                   Filler = ZEROS
      *
      * Sluttrecord for forsendelse - Tjenestekode = 00
     d                 ds
     d gaujld                        80                                         Hele utvekslingsfelt
     d  gajfko                        2    overlay(gaujld)                      Formatkode = NY
     d  gajtje                        2    overlay(gaujld:3)                    Tjenestekode = 00
     d  gajfty                        2  0 overlay(gaujld:5)                    Forsendelsetype = 00
     d  gajrec                        2  0 overlay(gaujld:7)                    Recordtype = 89
     d  gajatr                        8  0 overlay(gaujld:9)                    Antall transaksjoner
     d  gajarc                        8  0 overlay(gaujld:17)                   Antall records
     d  gajbel                       17  2 overlay(gaujld:25)                   Sum beløp
     d  gajfda                        6  0 overlay(gaujld:42)                   Første dato (DDMMÅÅ)
     d  gajfil                       33    overlay(gaujld:48)                   Filler = ZEROS
      *
      *  HENT FREM OPPLYSNINGER FRA KODE-REGISTER M.M.
      *
     c     kwakey        setll     rkw4pf
     c                   read      rkw4pf
      *
     c                   if        rnfirm <> s_firm
     c                   eval      aaffir = rnfirm                              FIRMA
     c     keyfir        chain     afirl1
      *
      *  Finner faste opplysninger - Autogiro
      *
     c                   movel     rnfirm        ra7fir
     c     ra7fir        chain     raa7lu
      *
     c                   if        %found
     c     ra7anr        add       1             ra7anr
     c                   z-add     ra7for        wa7for
     c                   z-add     ra7onr        wa7onr
     c                   update    raa7lur
     c                   else
     c                   z-add     1             ra7anr
     c                   write     raa7lur
     c                   endif
      *
      *  SJEKK OM FIRMAET  SKAL BEHANDLES
      *
     c                   if        rnfirm = pfirm
     c                   eval      b_firm = *on
     c                   else
     c                   eval      b_firm = *off
     c                   endif
     c                   eval      s_firm = rnfirm
     c                   endif
      *
      *  FIRMAET  SKAL IKKE BEHANDLES
      *
     c                   if        b_firm = *off
     c                   goto      slutt
     c                   endif
      *
     c                   eval      l_nbr = ra7anr
     c                   eval      l_firm = pfirm
     c                   eval      l_navn = aafnav
     c                   write     a1hdr                                30
      *
     c                   z-add     0             wantli
      *
      *  Skriv startrecord forsendelse (Autogiro-output - rec.type 10)
      *
     c                   exsr      skriv_r10
      *
     c                   z-add     0             wsumhs
     c                   z-add     0             wtotfa
      *
      *  Skriv startrecord inkassooppdrag - rec.type 20
      *
     c                   exsr      skriv_r20
      *
     c                   dow       not %eof
      *
     c                   if        rnbelØ = 0
     c                   eval      b_bel0 = *on
     c                   endif
      *
      *  SJEKK BRUDD PÅ KUNDE
      *
     c                   if        rnkund <> s_kund
     c                   eval      b_bilk = *off
     c                   eval      b_kund = *off
     c                   if        wbforp > 0
     c                   eval      t1forp = wbforp
     c                   write     t1tot                                30
      *
      *  Skriv sluttrecord inkassooppdrag - rec.type 88
      *
     c*                  exsr      skriv_r88
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                30
     c                   endif
     c                   endif
      *
      *  BLANK UT DE FELTER SOM LISTES PÅ L2
      *
     c                   z-add     0             wbforp
     c                   z-add     0             wantko
      *
     c                   endif
      *
      *  SPLITT BILAGSDATO OG FORFALLSDATO
      *
     c                   if        rnbelØ < 0
     c                   eval      b_bneg = *on
     c                   else
     c                   eval      b_bneg = *off
     c                   endif
      *
     c                   if        b_bneg = *off
     c     *ymd          move      rnfdat        w_wdat8
     c                   move      w_wdat8       wffofd
     c                   move      rnforÅ        wnforÅ                           FFÅR
     c                   eval      wnford = rnford                                FFDAG
     c                   eval      wnform = rnform                                FFMND
     c                   eval      warb1 = rnford
     c                   movel     rnform        warb1
     c                   eval      wffdat = warb1                               FLYTT FF.DATO
     c                   movel     wnforÅ        wffdat
     c                   endif
      *
      *  NULLSTILL AUTOGBELØP - LES KUNDEREGISTER/POSTREG.
      *
     c                   if        rnkund <> s_kund
     c                   eval      rkkund = rnkund
     c                   movel     rnfirm        rkfirm
     c     rkkey         chain     rkunpf
      *
     c                   eval      apponr = rkponr
     c     apponr        chain     apospf
     c                   if        not %found
     c                   eval      apsted = *blank
     c                   endif
     c                   if        apsted = *blank
     c                   move      rksted        apsted
     c                   endif
      *
     c                   if        rksald <= 0
     c                   eval      b_saln = *on
     c                   else
     c                   eval      b_saln = *off
     c                   endif
     c                   eval      s_kund = rnkund
     c                   write     d1det                                30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                30
     c                   endif
     c                   endif
      *
     c*                  if        b_saln = *off
     c*                  if        rkf003 = 'N'
     c*                  eval      b_saln = *on
     c*                  endif
     c*                  endif
      *
      *  SJEKK OM REKLAMASJON ELLER 0 I RESTBELØP
      *
     c                   if        rnbilk = 89
     c                   eval      b_rekl = *on
     c                   else
     c                   eval      b_rekl = *off
     c                   endif
      *
     c                   if        b_rekl = *on
     c                   goto      slutt
     c                   endif
      *
     c                   if        b_bel0 = *on
     c                   goto      ut6                                          BELØP NULL
     c                   endif
      *
     c                   eval      b_ok = *on
      *
      *  HVIS SALDO NULL ELLER NEGATIV SKRIVES IKKE AUTOGIRO
      *
     c                   if        b_saln = *on
     c                   eval      b_ok = *off
     c                   endif
      *
      *  AKK.TOTALT FORFALT OG AUTOGIROGRUNNLAG
      *
     c                   if        b_ok = *on
     c                   add       rnvalr        wbforp                         FORFALT SALDO
     c                   add       rnvalr        wbfort                         TOTALLT
     c                   endif
      *
     c     ut6           tag
      *
     c     *dmy          move      rnfdat        w_wdat6
     c                   move      w_wdat6       ffforf
     c     *ymd          move      rnfdat        w_wdat8
     c                   move      w_wdat8       wffofd
      *
     c                   if        wffofd < wftfda                              Forfall < 1.forfall
     c     *dmy          move      rnfdat        w_wdat6
     c                   move      w_wdat6       wftorf
     c                   move      wffofd        wftfda
     c                   endif
      *
     c                   if        wffofd < wfffda                              Forfall < 1.forfall
     c     *dmy          move      rnfdat        w_wdat6
     c                   move      w_wdat6       wfforf
     c                   move      wffofd        wfffda
     c                   endif
      *
     c                   if        wffofd > wsffda                              Forfall > siste forf
     c     *dmy          move      rnfdat        w_wdat6
     c                   move      w_wdat6       wsforf
     c                   move      wffofd        wsffda
     c                   endif
      *
     c                   if        b_ok = *on
     c                   write     d2det                                30
      *
      *  Skriv transaksjonsrecords - rec.type 30/31
      *
     c                   exsr      skriv_r30
     c                   exsr      skriv_r31
     c*                  eval      rnstat = 'A'                                 Status
     c                   eval      rnauto = 'A'                                 Status
     c                   write     rkw4lur                                      Skriv autogiropost
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                30
     c                   endif
     c                   endif
      *
     c                   read      rkw4pf
     c                   enddo
      *
     c     slutt         tag
      *
     c                   if        wbforp > 0
     c                   eval      t1forp = wbforp
     c                   write     t1tot                                30
     c                   endif
      *
      *  Skriv sluttrecord inkassooppdrag - rec.type 88
      *
     c                   exsr      skriv_r88
      *
     c                   eval      t2fort = wbfort
     c                   write     t2tot                                30
      *
      *  Skriv sluttrecord forsendelse - rec.type 89
      *
     c                   exsr      skriv_r89
      *
      *  Oppdater felt for siste forsendelse/oppdrag
      *
     c     ra7fir        chain     raa7lu
     c                   if        %found
     c                   z-add     wa7for        ra7for
     c                   z-add     wa7onr        ra7onr
     c                   update    raa7lur
     c                   else
     c                   write     raa7lur
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriv av 'Startrecord for forsendelse' - rec.type 10
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_r10     begsr
      *
     c                   move      *zeros        gauald                         Hele rec. NU
     c                   move      'NY'          gaafko                         Formatkode
     c                   move      '00'          gaatje                         Tjenestekode
     c                   move      *zero         gaafty                         Forsendlsetype
     c                   move      10            gaarec                         Recordtype
     c                   move      ra7dav        gaadav                         Dataavsender
     c                   movel     pkdat         gaafor                         Forsendelsenummer
     c                   add       1             wa7for
     c                   move      wa7for        gaafor                         Forsendelsenummer
     c                   move      00008080      gaadmo                         Datamottaker
     c                   move      *zeros        gaafil                         Filler
     c                   add       1             wantli                         Antall rader
7.nx c**                 move      gauald        rcns80                         Hele rec.
7.nx c                   movel     gauald        rcn130                         Hele rec.
     c                   write     rautpfr                                      STARTREC. Forsendels
     c                   eval      wftfda = 99999999                            TIDLIGST forfall
     c                   move      gaafor        t2afor                         Forsendelsenummer
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriv av 'Startrecord inkasso-oppdrag' - rec.type 20
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_r20     begsr
      *
     c                   move      *zeros        gaubld                         NULL rec.
     c                   move      'NY'          gabfko                         Formatkode
     c                   move      '01'          gabtje                         Tjenestekode
     c                   move      *zero         gaboty                         Forsendlsetype
     c                   move      20            gabrec                         Recordtype
     c                   move      ra7avt        gabavt                         Avtale-ID
      *
     c                   movel     pkdat         gabonr                         Oppdragsnr.
     c                   add       1             wa7onr
     c                   move      wa7onr        gabonr                         Forsendelsenummer
      *
     c                   move      aafbgi        gabkto                         Oppdragskonto
     c                   move      *zeros        gabfil                         Filler
     c                   add       1             wantli                         Antall rader
     c                   z-add     1             wantop                         Antall rader i oppdr
7.nx c**                 move      gaubld        rcns80                         Hele rec.
7.nx c                   movel     gaubld        rcn130                         Hele rec.
     c                   write     rautpfr                                      STARTREC. Oppdrag
     c                   eval      wfffda = 99999999                            Første forfall
     c                   eval      wsffda = *zeros                              Siste forfall
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriv av 'Transaksjonrec. - Beløpspost 1' rec.30
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_r30     begsr
      *
     c                   move      *zeros        gauc1l                         NULL rec.
     c                   move      'NY'          gacfk1                         Formatkode
     c                   move      '01'          gactj1                         Tjenestekode
     c                   move      02            gactt1                         Transaksjonstype
     c                   move      30            gacrc1                         Recordtype
     c                   add       1             wantfa                         Antall fakturaer
     c                   add       1             wtotfa                         Antall fakturaer
     c                   add       1             wantli                         Antall rader
     c                   add       1             wantop                         Antall rader i oppdr
     c                   move      wantfa        gacnr1                         Transaksjonsnummer
     c                   move      ffforf        gacdat                         Forfallsdato
     c*                  move      rkbgir        gackto                         Kontonummer
     c                   evalr     gackto = %editc(rnkund : 'Z')
     c                   z-add     rnrest        gacbel                         Fakturabeløp
     c                   eval      kidd10 = *blank
     c                   eval      dskfir = pfirm
     c                   eval      dskkod = *zeros
     c                   eval      dskled = *zeros
     c                   eval      dsktyp = *zeros
     c                   eval      dskkto = rnkund
     c                   eval      dskbil = rnbiln
     c                   move      dskkus        kidd10
     c                   movel     kidd10        wpkid
      *
     c                   call      'ASMD10'                                     Beregn
     c                   parm                    wpkid                           KID-felt
      *
     c                   move      wpkid         dskkid
     c                   move      *blank        gackid                         Kundeident
     c                   move      dskkid        gackid                         Kundeident
     c                   move      *zeros        gacfil                         Filler
     c                   add       rnrest        wsumfa                         Sum fakturaer - oppd
     c                   add       rnrest        wsumhs                         Sum fakturaer - fors
7.nx c**                 move      gauc1l        rcns80                         Hele rec.
7.nx c                   movel     gauc1l        rcn130                         Hele rec.
      *
     c                   write     rautpfr                                      FAKTURA Beløpspost 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriv av 'Transaksjonrec. - Beløpspost 2' rec.31
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_r31     begsr
      *
     c                   move      *zeros        gauc2l                         NULL rec.
     c                   move      'NY'          gacfk2                         Formatkode
     c                   move      '01'          gactj2                         Tjenestekode
     c                   move      02            gactt2                         Transaksjonstype
     c                   move      31            gacrc2                         Recordtype
     c                   move      wantfa        gacnr2                         Transaksjonsnummer
     c                   movel     rknavn        gacnav                         Forkortet navn
     c     'Æ':X'46'     xlate     gacnav        gacnav
     c     'æ':X'A0'     xlate     gacnav        gacnav
     c     'Ø':X'77'     xlate     gacnav        gacnav
     c     'ø':X'90'     xlate     gacnav        gacnav
     c     'Å':X'2A'     xlate     gacnav        gacnav
     c     'å':X'EF'     xlate     gacnav        gacnav
      *
     c                   move      *blank        gacref                         Egenreferanse
     c                   move      *blank        gacfrf                         Fremmedreferanse
     c                   move      *zeros        gacfi1                         Filler
     c                   add       1             wantli                         Antall rader
     c                   add       1             wantop                         Antall rader i oppdr
7.nx c**                 move      gauc2l        rcns80                         Hele rec.
7.nx c                   movel     gauc2l        rcn130                         Hele rec.
      *
     c                   write     rautpfr                                      FAKTURA Beløpspost 2
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriv av 'Sluttrecord - inkassooppdrag' rec.88
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_r88     begsr
      *
     c                   move      *zeros        gaufld                         NULL rec.
     c                   move      'NY'          gaffko                         Formatkode
     c                   move      '01'          gaftje                         Tjenestekode
     c                   move      *zero         gafoty                         Transaksjonstype
     c                   move      88            gafrec                         Recordtype
     c                   move      wantfa        gafatr                         Antall transaksjonse
     c                   add       1             wantli                         Antall rader
     c                   add       1             wantop                         Antall rader i oppdr
     c                   move      wantop        gafarc                         Antall rader i oppdr
     c                   move      wsumfa        gafsum                         Sum fakturaer i oppd
     c                   move      wfforf        gafffa                         Første forfall
     c                   move      wsforf        gafsfa                         Siste forfall
     c                   move      *zeros        gaffil                         Filler
7.nx c**                 move      gaufld        rcns80                         Hele rec.
7.nx c                   movel     gaufld        rcn130                         Hele rec.
     c                   write     rautpfr                                      SLUTTREC. Oppdrag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriv av 'Sluttrecord - forsendelse' rec.89
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_r89     begsr
      *
     c                   move      *zeros        gaujld                         NULL rec.
     c                   move      'NY'          gajfko                         Formatkode
     c                   move      *zero         gajtje                         Tjenestekode
     c                   move      *zero         gajfty                         Forsendelsetype
     c                   move      89            gajrec                         Recordtype
     c                   move      wtotfa        gajatr                         Antall transaksjonse
     c                   add       1             wantli                         Antall records
     c                   move      wantli        gajarc                         Antall records
     c                   move      wsumhs        gajbel                         Sum fakturaer i fors
     c                   move      wftorf        gajfda                         Første dato
     c                   move      *zeros        gajfil                         Filler
7.nx c**                 move      gaujld        rcns80                         Hele rec.
7.nx c                   movel     gaujld        rcn130                         Hele rec.
     c                   write     rautpfr                                      SLUTTREC. Forsendels
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_parm = w_parm
      *
      *  DEFINISJON AV ARBEIDSFELT
      *
     c                   eval      Åtte0 = *zeros
     c                   eval      seks0 = *zeros
     c                   eval      fir0 = *zeros
     c                   eval      to0 = *zeros
     c                   eval      n = *zeros
     c                   eval      en = *blank
     c                   move      *zeros        wpkid
     c                   eval      wftorf = *zeros
     c                   eval      wfforf = *zeros
     c                   eval      wsforf = *zeros
     c     999999999     mult      100           wsummr
     c                   eval      wsummr = 99
      *
     c     rnkey         klist
     c                   kfld                    rnfirm
     c                   kfld                    rnkund
     c                   kfld                    rnbdat
     c                   kfld                    rntist
      *
     c     rkkey         klist
     c                   kfld                    rkfirm
     c                   kfld                    rkkund
      *
     c     keyfir        klist
     c                   kfld                    aaffir
      *
     c     kwakey        klist
     c                   kfld                    rwkkey
     c                   eval      rkfirm = pfirm
     c                   eval      rnfirm = pfirm
      *
     c                   endsr
      *
