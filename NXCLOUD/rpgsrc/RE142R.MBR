     h/TITLE    Program : re142R   ASOKON     Visma integrasjon
     h decedit(',') datedit(*dmy.)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : re142R                                              *
      * Beskrivelse . . : Integrasjon Visma Payroll       ==> ASOKON          *
      *                   Danner databasefil m/klient.                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R8.00     231121  Nytt program                                   sst  *
8.01  * R8.00     130922  Endret frie felter til: Avd, Prosj, Bærer      sst  *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frevipf    if   e           k disk
     freutpf    o  a e           k disk
      *
     d*---------------------------------------------------------------------
     d* Stand Alone Fields - TOP
     d*---------------------------------------------------------------------
     d p_flgr          s              3
     d p_firmx         s              3
     d w_firm          s              3  0
     d*---------------------------------------------------------------------
     d* Stand Alone Fields - BOTTOM
     d*---------------------------------------------------------------------
      * Definering av recordbeskrivelse - eksportfil fra Visma
     d                 ds
     d d_inpu                       256
     d  d_kont                        6    overlay(d_inpu:7)
     d   d_kontn                      6  0 overlay(d_kont:1)
     d  d_mvak                        2    overlay(d_inpu:13)
     d  d_avde                        4    overlay(d_inpu:23)
     d   d_avden                      4  0 overlay(d_avde:1)
8.01 d  d_pros                        6    overlay(d_inpu:33)
8.01 d*  d_prosn                      6  0 overlay(d_pros:1)
8.01 d  d_spes                        4    overlay(d_inpu:47)
     d   d_spesn                      4  0 overlay(d_spes:1)
     d* d_dato                        8  0 overlay(d_inpu:45)
     d*  d_per                        2  0 overlay(d_dato:3)
     d*  d_aar                        4  0 overlay(d_dato:5)
     d  d_bdat                        8  0 overlay(d_inpu:122)
     d   d_bdag                       2  0 overlay(d_bdat:1)
     d   d_bmnd                       2  0 overlay(d_bdat:3)
     d   d_baar                       4  0 overlay(d_bdat:5)
     d  d_ants                       10  2 overlay(d_inpu:130)
     d  d_sats                       10  2 overlay(d_inpu:140)
     d  d_sign                        1    overlay(d_inpu:150)
     d  d_bel                        10  2 overlay(d_inpu:151)
      *
      * Bilagsdato på format ÅÅÅÅMMDD
      *
     d                 ds
     dw_bdat                   1      8  0
     dtbaar                    1      4  0
     dtbmnd                    5      6  0
     dtbdag                    7      8  0
      *
      * Leser eksportfil/H&L lønn
      *
     c                   read      revipf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = revirec
     c                   exsr      oppr_lpos
     c                   read      revipf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av lønnsposteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_lpos     begsr
      *
     c                   clear                   reutpfr
      *
     c                   eval      d_avde = %xlate(' ':'0':d_avde)
     c*                  eval      d_pros = %xlate(' ':'0':d_pros)
     c                   eval      d_spes = %xlate(' ':'0':d_spes)
      * Filen fra H&L må korrigeres når det ikke er aktivitet
     c*                  if        d_akt  = '  0' or
     c*                            d_akt  = '000'
     c*                  eval      d_akt  = *blanks
     c*                  endif
     c*                  eval      d_akt  = %xlate('+':'Å':d_akt)
      *
     c                   eval      reflgr = p_flgr
     c                   eval      refirm = w_firm
     c                   eval      rekont = d_kontn
     c                   eval      remvak = '0'
     c                   eval      reavde = d_avden
8.01 c                   eval      repros = d_pros
8.01 c                   if        d_pros = '     0'
8.01 c                   eval      repros = *blank
8.01 c                   endif
     c                   eval      respes = d_spesn
     c*                  eval      reakti = d_akt
      *
9.01 c                   eval      remngd = 0
      *
     c                   eval      rerper = d_bmnd
     c                   eval      reraar = d_baar
     c                   eval      tbaar  = d_baar
     c                   eval      tbmnd  = d_bmnd
     c                   eval      tbdag  = d_bdag
     c                   move      w_bdat        rebdat
     c                   if        d_sign = '-'
     c                   z-sub     d_bel         rebelØ
     c                   else
     c                   eval      rebelØ = d_bel
     c                   endif
      *
      * Skriv post
      *
     c                   if        rebelØ <> 0
     c                   write     reutpfr
     c                   endif
     c                   endsr
      * ----------------------------------------
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_flgr
     c                   parm                    p_firmx
      *
     c                   move      p_firmx       w_firm
      *
     c                   endsr
