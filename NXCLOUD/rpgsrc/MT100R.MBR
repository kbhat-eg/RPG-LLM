     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : MT100R
      * Beskrivelse . . : Behandling av pakkeregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * ASK   270804 ASK  Nytt program
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.nu  * 6.30  050614 bof  endring i forb. nummerutvidelse
6.31  * 6.30  151018 bof  utvidet med trelast-sortiment
7.xx  * 7.xx  180516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmtpalr    if   e           k disk    rename(mtpapfr:mtpalrr)
     fmtpalu    uf a e           k disk    rename(mtpapfr:mtpalur)
     fmtpal1    if   e           k disk    rename(mtpapfr:mtpal1r)
     fmtpal2    if   e           k disk    rename(mtpapfr:mtpal2r)
     fmupalu    uf a e           k disk    rename(mupapfr:mupalur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
      *
     fmt100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_user                911    920
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d mtpalr_numm     s                   like(mtnumm)
     d mtpalu_numm     s                   like(mtnumm)
     d mtpal1_numm     s                   like(mtnumm)
     d mtpal2_lvar     s                   like(mtlvar)
     d mtpal2_lage     s                   like(mtlage)
     d mtpal2_numm     s                   like(mtnumm)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_seqe          s              1
      *
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_endr          s              1    inz(*off)
      *
     d w_firm          s                   like(mtfirm)
     d w_totr          s                   like(mttotr)
     d w_leng          s                   like(mttotr)
     d w_text          s                   like(vvtek1)
     d w_enhl          s                   like(vvenhl)
     d w_lagn          s              2  0
     d w_numa          s              6
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_stat          s              1
     d w_htxt          s             20
     d w_slvn          s                   like(b2slvn)
     d w_slag          s                   like(b2slag)
     d w_modu          s             10    inz('NTR     ')
     d w_disp          s              1    inz('1')
6.31 d w_lv_vare       s                   like(vvvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * B4WIN  - B4 Vindu for slette (Delete)
      * C1BLD  - C1 Manuell pakkeregistrering
      * C2BLD  - C2 Se på / oppdatere pakkeinformasjon
      * D1BLD  - D1 Se på / oppdatere lengde spesifikasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_stat
      *
     c                   if        w_stat = '1'
     c                   goto      end_pgm
     c                   endif
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      ny_pakke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkg
     c                   exsr      vis_alle
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkh
     c                   exsr      vis_delte
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inki
     c                   exsr      vis_slettede
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkj
     c                   exsr      hent_pakke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkm
     c                   exsr      vis_status
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0 or
     c                             b2lvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
     c                   if        b2slvn <> w_slvn or
     c                             b2slag <> w_slag
     c                   eval      w_slvn = b2slvn
     c                   eval      w_slag = b2slag
     c                   eval      w_fcrn = 0
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   eval      mtpal1_numm = b1numm
     c                   else
     c                   eval      mtpal1_numm = 0
     c                   endif
      *
     c     mtpal1_key    setll     mtpal1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b_forn = *off
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på pakkenummer
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      mtpal1_numm = b2numm
     c     mtpal1_key    setll     mtpal1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      mtpal2_lvar = b2lvar
     c                   eval      mtpal2_lage = 0
     c                   eval      mtpal2_numm = 0
     c     mtpal2_key    setll     mtpal2
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c     end_pos       tag
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c*                  if        w_curn > 0
     c*                  eval      w_virn = w_curn
     c*                  endif
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Endre eksisterende pakke
      *
     c                   if        b1valg = 2
     c                   eval      b_endr = *on
     c                   exsr      pakkedet
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette pakke
      *
     c                   if        b1valg = 4
     c                   exsr      slett_pakke
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Se på detaljer i pakke
      *
     c                   if        b1valg = 5
     c                   exsr      pakkedet
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis lengdefordeling
      *
     c                   if        b1valg = 7
     c                   exsr      vis_lengde
     c                   eval      b1valg = 0
     c*                  eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis pakkehistorikk
      *
     c                   if        b1valg = 8
     c                   exsr      vis_historikk
     c                   eval      b1valg = 0
     c                   eval      b_forn = *off
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Overgang til lagerspørring
      *
     c                   if        b1valg = 9
     c                   exsr      vis_lager
     c                   eval      b1valg = 0
     c                   eval      b_forn = *off
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette ny pakke
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_pakke      begsr
      *
     c     c1taga        tag
     c                   eval      c1numm = 0
     c                   exfmt     c1bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_ny
     c                   endif
      *
     c                   eval      mtpalr_numm = c1numm
     c     mtpalr_key    chain     mtpalr
     c                   if        %found
     c                   eval      *in35 = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      c2numm = c1numm
     c                   eval      c2lvar = *blank
     c                   eval      c2lage = 0
     c                   eval      c2vare = 0
     c                   eval      c2text = *blank
     c                   eval      c2nobb = 0
     c                   eval      c2pdat = *loval
     c                   eval      c2ptid = *loval
     c                   eval      c2tant = 0
     c                   eval      c2ntyk = 0
     c                   eval      c2ptyp = *blank
     c                   eval      c2totl = 0
     c                   eval      c2nbre = 0
     c                   eval      c2seri = 0
     c                   eval      c2mlen = 0
     c                   eval      c2atyk = 0
     c                   eval      c2line = 0
     c                   eval      c2slen = 0
     c                   eval      c2abre = 0
     c                   eval      c2korr = 0
     c                   eval      c2snit = 0
     c                   eval      c2ljus = *blank
     c                   eval      c2kant = 0
     c                   eval      c2nomk = 0
     c                   eval      c2ordn = *blank
     c                   eval      c2totk = 0
     c                   eval      c2aktk = 0
     c                   eval      c2kund = *blank
     c                   eval      c2pros = 0
     c                   eval      c2knok = 0
     c                   eval      c2stat = *blank
     c                   eval      c2vekt = 0
     c                   eval      c2kakk = 0
     c                   eval      c2resl = 0
      *
     c                   eval      b_oppr = *on
     c                   exsr      pakkedet
      *
     c     end_ny        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle pakkedetaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pakkedet      begsr
      *
     c                   if        b_oppr = *on
     c                   eval      *in31 = *off
     c                   eval      c2funk = 'Ny   '
     c                   goto      c2taga
     c                   endif
      *
      * Hent pakkeinformasjon
      *
     c                   eval      mtpalr_numm = b1numm
     c     mtpalr_key    chain     mtpalr
     c                   if        not %found
     c                   goto      end_pakke
     c                   endif
      *
     c                   eval      c2numm = mtnumm
     c                   eval      c2lvar = mtlvar
     c                   eval      c2lage = mtlage
     c                   movel     mtvare        c2vare
     c                   eval      c2text = b1text
     c                   eval      c2nobb = mtnobb
     c     *dmy          move      mtdato        c2pdat
     c     *hms          move      mttime        c2ptid
     c                   eval      c2tant = mttant
     c                   eval      c2ntyk = mtntyk
     c                   eval      c2ptyp = mtptyp
     c                   eval      c2totl = mttotl
     c                   eval      c2nbre = mtnbre
     c                   eval      c2seri = mtseri
     c                   eval      c2mlen = mtmlen
     c                   eval      c2atyk = mtatyk
     c                   eval      c2line = mtline
     c                   eval      c2slen = mtslen
     c                   eval      c2abre = mtabre
     c                   eval      c2korr = mtkorr
     c                   eval      c2snit = mtsnit
     c                   eval      c2ljus = mtljus
     c                   eval      c2kant = mtkant
     c                   eval      c2nomk = mtnomk
     c                   eval      c2ordn = mtordn
     c                   eval      c2totk = mttotk
     c                   eval      c2aktk = mtaktk
     c                   eval      c2kund = mtkund
     c                   eval      c2pros = mtpros
     c                   eval      c2knok = mtknok
     c                   eval      c2stat = mtstat
     c                   eval      c2vekt = mtvekt
     c                   eval      c2kakk = mtkakk
     c                   eval      c2resl = mttotr
      *
      * Endring eller se på?
      *
     c                   if        b_endr = *on
     c                   eval      *in31 = *off
     c                   eval      c2funk = 'Endre'
     c                   else
     c                   eval      *in31 = *on
     c                   eval      c2funk = 'Vise '
     c                   endif
      *
      * Behandle pakkebilde
      *
     c     c2taga        tag
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_pakke
     c                   endif
      *
     c*                  if        *inkd
     c*                  call      'VV500R'
     c*                  parm                    w_firm
     c*                  parm                    a1vare
     c*                  parm                    a1tek1
     c*                  goto      c2taga
     c*                  endif
      *
     c                   if        *inkg
     c                   exsr      vis_lengde
     c                   goto      c2taga
     c                   endif
      *
      * Bare vise?
      *
     c                   if        b_oppr = *off and
     c                             b_endr = *off
     c                   goto      end_pakke
     c                   endif
      *
      * Gyldig varenummer?
      *
     c                   exsr      les_artikkel
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     end_pakke     tag
     c                   eval      b_oppr = *off
     c                   eval      b_endr = *off
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å slette pakke
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_pakke   begsr
      *
     c                   eval      b4numm = b1numm
     c                   eval      b4lvar = b1lvar
     c                   eval      b4text = b1text
      *
     c                   exfmt     b4win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_slett
     c                   endif
      *
      * Merker pakke som slettet
      *
     c                   eval      mtpalu_numm = b1numm
     c     mtpalu_key    chain     mtpalu
     c                   if        %found
     c                   eval      mtstat = 'S'
     c                   eval      w_totr = mttotr
     c                   eval      mttotr = 0
6.10 c                   time                    mtedat
6.10 c                   time                    mtetim
6.10 c                   eval      mteusr = l_user
     c                   update    mtpalur
     c                   endif
      *
      * Legger ut post i pakkehistorikk
      *
     c                   eval      w_leng = w_totr * -1
     c                   eval      w_htxt = 'Pakken ble slettet '
     c                   exsr      oppd_hist
      *
      * Oppdaterer lager etter sletting
      *
     c                   exsr      oppd_lager
      *
     c     end_slett     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise lengdefordeling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_lengde    begsr
      *
     c                   eval      mtpalr_numm = b1numm
     c     mtpalr_key    chain     mtpalr
     c                   if        not %found
     c                   goto      end_lengde
     c                   endif
      *
     c                   eval      D1numm = b1numm
     c                   eval      D1lvar = b1lvar
     c                   eval      D1text = b1text
      *
     c                   eval      D1dm18 = mtdm18
     c                   eval      D1dm19 = mtdm19
     c                   eval      D1dm20 = mtdm20
     c                   eval      D1dm21 = mtdm21
     c                   eval      D1dm22 = mtdm22
     c                   eval      D1dm23 = mtdm23
     c                   eval      D1dm24 = mtdm24
     c                   eval      D1dm25 = mtdm25
     c                   eval      D1dm26 = mtdm26
     c                   eval      D1dm27 = mtdm27
     c                   eval      D1dm28 = mtdm28
     c                   eval      D1dm29 = mtdm29
     c                   eval      D1dm30 = mtdm30
     c                   eval      D1dm31 = mtdm31
     c                   eval      D1dm32 = mtdm32
     c                   eval      D1dm33 = mtdm33
     c                   eval      D1dm34 = mtdm34
     c                   eval      D1dm35 = mtdm35
     c                   eval      D1dm36 = mtdm36
     c                   eval      D1dm37 = mtdm37
     c                   eval      D1dm38 = mtdm38
     c                   eval      D1dm39 = mtdm39
     c                   eval      D1dm40 = mtdm40
     c                   eval      D1dm41 = mtdm41
     c                   eval      D1dm42 = mtdm42
     c                   eval      D1dm43 = mtdm43
     c                   eval      D1dm44 = mtdm44
     c                   eval      D1dm45 = mtdm45
     c                   eval      D1dm46 = mtdm46
     c                   eval      D1dm47 = mtdm47
     c                   eval      D1dm48 = mtdm48
     c                   eval      D1dm49 = mtdm49
     c                   eval      D1dm50 = mtdm50
     c                   eval      D1dm51 = mtdm51
     c                   eval      D1dm52 = mtdm52
     c                   eval      D1dm53 = mtdm53
     c                   eval      D1dm54 = mtdm54
     c                   eval      D1dm55 = mtdm55
     c                   eval      D1dm56 = mtdm56
     c                   eval      D1dm57 = mtdm57
     c                   eval      D1dm58 = mtdm58
     c                   eval      D1dm59 = mtdm59
     c                   eval      D1dm60 = mtdm60
     c                   eval      D1dm61 = mtdm61
     c                   eval      D1dm62 = mtdm62
      *
     c                   eval      *in31 = *on
     c                   exfmt     d1bld
     c                   eval      *in31 = *off
      *
      *
     c     end_lengde    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på historikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_historikk begsr
      *
     c                   call      'MU500R'
     c                   parm                    w_firm
     c                   parm                    b1numm
      *
     c     end_historikk endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_lager     begsr
      *
     c                   call      'LL502R'
     c                   parm                    w_firm
     c                   parm                    b1vare
      *
     c     end_lager     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_status    begsr
      *
     c                   exfmt     h2win
      *
      *
     c     end_status    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = 'N'
     c                   read      mtpal1
     c                   else
     c                   read      mtpal2
     c                   endif
      *
     c                   if        %eof or                                      EOF
     c                             mtfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      *  Velg pakkestatus
      *
     c                   if        b2funk = 'ALLE' and
     c                             mtstat = 'S'
     c                   iter
     c                   endif
      *
     c                   if        b2funk = 'DELTE' and
     c                             mtstat <> 'B'
     c                   iter
     c                   endif
      *
     c                   if        b2funk = 'SLETTEDE' and
     c                             mtstat <> 'S'
     c                   iter
     c                   endif
      *
      *  Er begrensning gjort?
      *
     c                   if        b2slvn <> *blank and
     c                             mtlvar <> b2slvn
     c                   iter
     c                   endif
      *
     c                   if        b2slag <> *blank
     c                   move      b2slag        w_lagn
     c                   if        mtlage <> w_lagn
     c                   iter
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
      *
     c                   eval      b1vare = mtvare
     c                   eval      b1lvar = mtlvar
     c                   eval      b1lage = mtlage
     c                   eval      b1numm = mtnumm
     c                   eval      b1totr = mttotr
     c                   eval      b1totl = mttotl
     c                   eval      b1mlen = mtmlen
     c                   eval      b1slen = mtslen
     c                   eval      b1snit = mtsnit
      *
     c                   exsr      les_artikkel
     c                   eval      b1text = w_text
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   if        w_seqe = 'N'
     c     mtpal1_key    setgt     mtpal1
     c                   readp     mtpal1
     c                   else
     c     mtpal2_key    setgt     mtpal2
     c                   readp     mtpal2
     c                   endif
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   if        w_seqe = 'N'
     c                   readp     mtpal1
     c                   else
     c                   readp     mtpal2
     c                   endif
      *
     c                   if        %eof or
     c                             mtfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   if        w_seqe = 'N'
     c                   eval      mtpal1_numm = mtnumm
     c                   else
     c                   eval      mtpal2_lvar = mtlvar
     c                   eval      mtpal2_lage = mtlage
     c                   eval      mtpal2_numm = mtnumm
     c                   endif
      *
     c                   enddo
      *
     c                   if        %eof
     c                   if        w_seqe = 'N'
     c     mtpal1_key    setll     mtpal1
     c                   else
     c     mtpal2_key    setll     mtpal2
     c                   endif
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å styre visning av alle pakker med innhold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_alle      begsr
      *
     c                   eval      w_seqe = 'N'
     c                   eval      w_fcrn = 0
     c                   eval      b2funk = 'ALLE'
      *
     c     end_alle      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å styre visning av bare delte
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_delte     begsr
      *
     c                   eval      w_seqe = 'N'
     c                   eval      w_fcrn = 0
     c                   eval      b2funk = 'DELTE '
      *
     c     end_delte     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å styre visning av bare slettede
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_slettede  begsr
      *
     c                   eval      w_seqe = 'N'
     c                   eval      w_fcrn = 0
     c                   eval      b2funk = 'SLETTEDE'
      *
     c     end_slettede  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente nye pakker fra PLS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pakke    begsr
      *
     c                   call      'MT700C'
      *
     c     end_hent      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese artikkelregister på lev.vnr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_artikkel  begsr
      *
     c                   eval      b_feil = *off
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr,
6.31 c+        vvtek1,
6.31 c+        vvenhl
6.31 c+ into   :w_lv_vare,
6.31 c+        :vvtek1,
6.31 c+        :vvenhl
6.31 c+ from   vvlepf
6.31 c+ inner join vvarpf on vvfirm = vvlfir and
6.31 c+                      vvvare = vvlvnr
6.31 c+ where  vvllva = :b1vare
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      vvvare = w_lv_vare
6.31 c                   eval      w_text = vvtek1
6.31 c                   eval      w_enhl = vvenhl
6.31 c                   else
     c                   eval      vvarl4_lvar = mtlvar
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   eval      w_text = vvtek1
     c                   eval      w_enhl = vvenhl
     c                   else
     c                   eval      b_feil = *on
     c                   eval      w_text = 'Ukjent vare'
     c                   eval      w_enhl = 'LM '
     c                   endif
6.31 c                   endif
      *
     c     end_artikkel  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere pakkehistorikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_hist     begsr
      *
     c                   eval      mufirm = w_firm
     c                   eval      munumm = mtnumm
     c                   eval      mudato = w_udat
     c                   eval      mutime = w_utim
     c                   eval      mutext = w_htxt
     c                   eval      muleng = w_leng
     c                   eval      muuser = l_user
      *
6.10 c                   time                    muodat
6.10 c                   time                    muotim
6.10 c                   eval      muousr = l_user
6.10 c                   time                    muedat
6.10 c                   time                    muetim
6.10 c                   eval      mueusr = l_user
     c                   write     mupalur
      *
     c     end_hist      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente å oppdatere lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_lager    begsr
      *
     c                   exsr      les_artikkel
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      end_oppd
     c                   endif
     c                   move      mtnumm        w_numa
      *
     c                   move      mtvare        hlvare
     c                   move      mtlage        hllage
     c                   move      w_enhl        hlenhe
     c                   eval      hldato = w_udat
     c                   eval      hltime = w_utim
     c                   move      '  '          hlotyp
     c                   move      '  '          hlltyp
      *
     c                   eval      hlanta = w_totr
     c                   eval      hlkopr = 0
      *
     c                   eval      hlrefr = 'Pk. ' + w_numa
     c                   move      'L'           hlsyst
     c                   eval      hlaltk = 'U '
     c                   eval      hlrest = *blank
     c                   eval      hlonum = 0
     c                   eval      hlolin = 0
      *
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c*    *entry        plist
     c*                  parm                    w_type
      *
      * Key for oppslag på pakkeregister
      *
     c     mtpalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mtpalr_numm
      *
      * Key for oppdatering av pakkeregister
      *
     c     mtpalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mtpalu_numm
      *
      * Key for posisjonering på pakkeregister (pnumm)
      *
     c     mtpal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mtpal1_numm
      *
      * Key for posisjonering på pakkeregister (lvnr/lager/pnumm)
      *
     c     mtpal2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mtpal2_lvar
     c                   kfld                    mtpal2_lage
     c                   kfld                    mtpal2_numm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      b2funk = 'ALLE'
      *
      *  Legger inn dato og klokkeslett (timestamp)
      *
     c                   time                    w_udat
     c                   time                    w_utim
     c                   eval      w_slvn = *blank
     c                   eval      w_slag = *blank
      *
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'N'
     c                   eval      mtpal1_numm = 0
     c     mtpal1_key    setll     mtpal1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
