     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : RF813R
      * Beskrivelse . . : Eskport av korttransaksjoner til NBBL
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150610 Bsa  Nytt program
6.10  *       090311 Bsa  Endret fillayout
6.11  *       100812 Bsa  Sjekker mot valgt kortlager, hvis det er forskjellig
6.11  *                   fra 0.
6.20  *       141113 Bsa  Flytter sjekk på om poster er funnet
6.nu  *       230614 bof  sjekket mtp. nummerutvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbokkl3    uf   e           k disk    rename(bokkpfr:bokkl3r)
     fbcdtir    if   e           k disk    rename(bcdtst:bcdtirr)
     fsodtlr    if   e           k disk    rename(sodtpfr:sodtlrr)
      *
     fkortitum  o  a e             disk    rename(kortitum:kortitumr)
     f                                     prefix(xo:2)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fgr                 931    933
      *
      * Definering av datastrukturer
      *
     d                 ds
     d p_prec                       128
     d  p_ktyp                        2    overlay(p_prec:1)
     d  p_frda                         d   overlay(p_prec:3)
     d  p_tida                         d   overlay(p_prec:13)
     d  p_omkj                        1  0 overlay(p_prec:23)
     d  p_lage                        2  0 overlay(p_prec:24)
     d  p_ftxt                       30    overlay(p_prec:26)
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                        150
     d  d_unhknr                     10    overlay(d_unh: 1) inz('Kortnummer')
     d  d_unhfill1                    1    overlay(d_unh:11) inz(';')
     d  d_unhdat                      4    overlay(d_unh:12) inz('Dato')
     d  d_unhfill2                    1    overlay(d_unh:16) inz(';')
     d  d_unhprt                     16    overlay(d_unh:17) inz('Produkt/Tjene+
     d                                     ste')
     d  d_unhfill3                    1    overlay(d_unh:33) inz(';')
     d  d_unhtsm                     16    overlay(d_unh:34) inz('Total handels+
     d                                     sum')
     d  d_unhfill4                    1    overlay(d_unh:50) inz(';')
     d  d_unhnts                     22    overlay(d_unh:51) inz('Netto total h+
     d                                     andelssum')
     d  d_unhfill5                    1    overlay(d_unh:73) inz(';')
     d  d_unhrts                     26    overlay(d_unh:74) inz('Rabattert tot+
     d                                     al handelssum')
     d  d_unhfill6                    1    overlay(d_unh:100) inz(';')
     d  d_unhnrs                     27    overlay(d_unh:101) inz('Netto rabbat+
     d                                     ert handelssum')
     d**d_unhfill7                    1    overlay(d_unh:125) inz(';')
     d**d_unhnrs                     26    overlay(d_unh:126) inz('Netto rabbate+
     d**                                   rt handelssum')
      *
      * Recordbeskrivelse - bong
      *
     d                 ds
     d d_bong                       100
     d  d_bongkort                   25    overlay(d_bong: 1)
6.10 d    d_kortnr                   19    overlay(d_bong: 7)
6.10 d    d_strekk                   13    overlay(d_bong:13)
     d  d_bongfill1                   1    overlay(d_bong:26) inz(';')
     d  d_bongdato                   10    overlay(d_bong:27)
     d    d_bongar                    4    overlay(d_bongdato:1)
     d    d_bongmd                    2    overlay(d_bongdato:6)
     d    d_bongdg                    2    overlay(d_bongdato:9)
     d  d_bongfill2                   1    overlay(d_bong:37) inz(';')
     d  d_bongtxt                    10    overlay(d_bong:38) inz('Byggevarer')
     d  d_bongfill3                   1    overlay(d_bong:48) inz(';')
     d  d_bongtotsum                 11    overlay(d_bong:49)
     d    d_btotkr                    9    overlay(d_bongtotsum:1)
     d    d_btotore                   2    overlay(d_bongtotsum:10)
     d  d_bongfill4                   1    overlay(d_bong:60) inz(';')
     d  d_bongnetsum                 11    overlay(d_bong:61)
     d    d_bnetkr                    9    overlay(d_bongnetsum:1)
     d    d_bnetore                   2    overlay(d_bongnetsum:10)
     d  d_bongfill5                   1    overlay(d_bong:72) inz(';')
     d  d_bongrabsum                 11    overlay(d_bong:73)
     d    d_brabkr                    9    overlay(d_bongrabsum:1)
     d    d_brabore                   2    overlay(d_bongrabsum:10)
     d  d_bongfill6                   1    overlay(d_bong:84) inz(';')
     d  d_bongrntsum                 11    overlay(d_bong:85)
     d    d_brntkr                    9    overlay(d_bongrntsum:1)
     d    d_brntore                   2    overlay(d_bongrntsum:10)
      *
     d                 ds
     d d_dato                        26
     d  d_bdato                      10    overlay(d_dato: 1)
      *
      * Definering av variabler i nøkler
      *
     d bcdtll_bong                         like(bibong)
     d bcdtir_bong                         like(bibong)
     d bcdtll_boli                         like(biboli)
     d bokkl3_ktyp                         like(brktyp)
     d bokkll_ktyp                         like(brktyp)
     d bokkll_odat                         like(brodat)
     d sodtlr_aarr     s                   like(sdaarr)
     d sodtlr_binr     s                   like(sdbinr)
      *
      * Definering av variable
      *
     d b_first         s              1    inz(*off)
6.11 d b_rapp          s              1    inz(*off)
      *
     d w_tmst          s               z
     d w_dato          s               d   datfmt(*iso)
     d w_bred          s             12
     d w_tot_hsum      s             11  2
     d w_net_hsum      s             11  2
     d w_trab_hsum     s             11  2
     d w_nrab_hsum     s             11  2
     d w_netto         s             11  2
     d w_prec          s            128
     d w_frda          s               d   datfmt(*iso)
     d w_tida          s               d   datfmt(*iso)
6.10 d w_korid         s             19
      *
     d w_firm          s                   like(brfirm)
      *
      * Definering av parametre
     d p_post          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser kundekortregister
      *
     c     bokkll_key    setll     bokkl3
     c     bokkl3_key    reade     bokkl3
     c                   dow       not %eof(bokkl3)
      *
      * Nullstiller sumfelter og henter inn informasjon fra bongregister
      *
     c                   eval      w_tot_hsum = 0
     c                   eval      w_net_hsum = 0
     c                   eval      w_trab_hsum = 0
     c                   eval      w_nrab_hsum = 0
     c                   eval      d_bong = *blanks
      * Sjekk mot til-dato (større-avslutt)
     c                   if        brodat > w_tida
     c                   goto      avslutt
     c                   endif
      * Skal posten legges ut på nytt, må det være omkjøring
     c                   if        brofda <> *loval and
     c                             p_omkj = 0
     c                   goto      les_neste
     c                   endif
      * Skal posten legges ut, kan den ikke være behandlet tidligere
     c                   if        brofda = *loval and
     c                             p_omkj = 1
     c                   goto      les_neste
     c                   endif
      * Hvis fakturanummer utfylt, les fra historisk fakturaregister eller fra bongregister
6.20 c**                 eval      p_post = '1'
      *
     c                   if        brbinr <> 0
     c                   exsr      les_hfakt
     c                   elseif    brbong <> *blank
     c                   exsr      les_bong
     c                   endif
      * Hvis funnet skriv og oppdater post
6.11 c                   if        b_rapp = *on
      * Legg ut post på register
      *
      * Sjekk om negativt beløp
     c                   if        w_tot_hsum < 0
     c     w_tot_hsum    mult      -1            w_tot_hsum
     c                   movel     w_tot_hsum    d_bongtotsum
     c                   movel     '-'           d_bongtotsum
     c                   else
     c                   movel     w_tot_hsum    d_bongtotsum
     c                   endif
     c                   if        w_net_hsum < 0
     c     w_net_hsum    mult      -1            w_net_hsum
     c                   movel     w_net_hsum    d_bongnetsum
     c                   movel     '-'           d_bongnetsum
     c                   else
     c                   movel     w_net_hsum    d_bongnetsum
     c                   endif
     c                   if        w_trab_hsum < 0
     c     w_trab_hsum   mult      -1            w_trab_hsum
     c                   movel     w_trab_hsum   d_bongrabsum
     c                   movel     '-'           d_bongrabsum
     c                   else
     c                   movel     w_trab_hsum   d_bongrabsum
     c                   endif
     c                   if        w_nrab_hsum < 0
     c     w_nrab_hsum   mult      -1            w_nrab_hsum
     c                   movel     w_nrab_hsum   d_bongrntsum
     c                   movel     '-'           d_bongrntsum
     c                   else
     c                   movel     w_nrab_hsum   d_bongrntsum
     c                   endif
      *
     c                   if        b_first = *on
     c                   eval      xortitum = d_unh
     c                   write     kortitumr
     c                   eval      b_first = *off
     c                   endif
     c                   eval      xortitum = *blank
     c***                eval      xortitum = d_bong
6.10 c                   eval      xortitum = %trim(w_korid) + ';' +
6.10 c                                        d_bongdg + '.' + d_bongmd+'.' +
6.10 c                                        d_bongar + ';' +
     c                                        'Byggevarer' + ';' +
     c                                        %trim(d_btotkr) + ',' +
     c                                        %trim(d_btotore) + ';' +
     c                                        %trim(d_bnetkr) + ',' +
     c                                        %trim(d_bnetore) + ';' +
     c                                        %trim(d_brabkr) + ',' +
     c                                        %trim(d_brabore) + ';' +
     c                                        %trim(d_brntkr) + ',' +
     c                                        %trim(d_brntore)
     c                   write     kortitumr
6.20  * Post er skrevet
6.20 c                   eval      p_post = '1'
      *
      * Merk bokk postering med at den er lest
      *
     c                   if        p_omkj = 0
     c                   time                    brofda
     c                   time                    bredat
     c                   time                    brofti
     c                   time                    bretim
     c                   eval      breusr = l_user
     c                   update    bokkl3r
     c                   endif
6.11 c                   endif
      *
     c     les_neste     tag
      * Les neste post kortregister
     c     bokkl3_key    reade     bokkl3
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine poster fra historisk fakturaregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     les_hfakt     begsr
      *
6.11 c                   eval      b_rapp = *off
      * Les fra SODTPF for å finne poster som skal være med i rapporten
      *
     c**                 eval      sodtlr_aarr = uyear + 2000
     c                   eval      sodtlr_aarr = %subdt(brodat:*years)
     c                   eval      sodtlr_binr = brbinr
     c     sodtlr_key    setll     sodtlr
     c     sodtlr_key    reade     sodtlr
      *
     c                   dow       not %eof(sodtlr)
6.11  *
6.11 c                   if        p_lage <> 0 and
6.11 c                             p_lage <> sdlage
6.11 c                   goto      les_nyline
6.11 c                   endif
      *
6.11 c                   eval      b_rapp = *on
     c                   eval      w_netto = *zero
     c                   movel     brkort        d_bongkort
6.10 c                   if        brkort > 9999999999999
6.10 c                   move      d_kortnr      w_korid
6.10 c                   else
6.10 c                   move      d_strekk      w_korid
6.10 c                   endif
     c                   movel     brodat        d_dato
     c                   eval      d_bongdato = d_bdato
      *
     c**                 eval      w_tot_hsum = w_tot_hsum + bissim
     c                   eval      w_tot_hsum = w_tot_hsum + (sdsalg * 1.25)
      *
     c**                 eval      w_net_hsum = w_net_hsum + bisums
     c                   eval      w_net_hsum = w_net_hsum + sdsalg
      *
     c**                 eval      w_nrab_hsum = w_nrab_hsum + bisumn
     c                   eval      w_netto = sdsalg-sdrkr1-sdrkr2-sdbrk1-sdbrk2
     c                   eval      w_nrab_hsum = w_nrab_hsum + w_netto
      *
     c**                 eval      w_trab_hsum = w_trab_hsum + bisnim
     c                   eval      w_trab_hsum = w_trab_hsum + (w_netto * 1.25)
      *
6.11 c     les_nyline    tag
      *
     c     sodtlr_key    reade     sodtlr
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine poster fra bongregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     les_bong      begsr
      *
6.11 c                   eval      b_rapp = *off
     c                   eval      bcdtir_bong = brbong
     c                   eval      bcdtll_bong = brbong
     c                   eval      bcdtll_boli = 0
     c     bcdtll_key    setll     bcdtir
     c     bcdtir_key    reade     bcdtir
     c                   dow       not %eof(bcdtir)
      *
6.11 c                   if        p_lage <> 0 and
6.11 c                             p_lage <> bilage
6.11 c                   goto      les_nybline
6.11 c                   endif
      *
6.11 c                   eval      b_rapp = *on
     c                   movel     brkort        d_bongkort
     c                   movel     brodat        d_dato
6.10 c                   if        brkort > 9999999999999
6.10 c                   move      d_kortnr      w_korid
6.10 c                   else
6.10 c                   move      d_strekk      w_korid
6.10 c                   endif
     c                   eval      d_bongdato = d_bdato
     c                   eval      w_tot_hsum = w_tot_hsum + bissim
     c                   eval      w_net_hsum = w_net_hsum + bisums
     c                   eval      w_trab_hsum = w_trab_hsum + bisnim
     c                   eval      w_nrab_hsum = w_nrab_hsum + bisumn
      *
6.11 c     les_nybline   tag
      *
      * Les neste post bongdetaljer
      *
     c     bcdtir_key    reade     bcdtir
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_prec
     c                   parm                    p_post
     c                   eval      p_prec = w_prec
      *
      * Key for les av kortregister
      *
     c     bokkll_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokkll_ktyp
     c                   kfld                    bokkll_odat
      *
      * Key for les av kortregister
      *
     c     bokkl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokkl3_ktyp
      *
      * Key for setll på bongregister
      *
     c     bcdtll_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bcdtll_bong
     c                   kfld                    bcdtll_boli
      *
      * Key for les på bongregister
      *
     c     bcdtir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bcdtir_bong
      *
      * Key for les av historisk fakturaregister - linjer
      *
     c     sodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtlr_aarr
     c                   kfld                    sodtlr_binr
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      b_first = *on
     c                   eval      p_post = '0'
      * Initier felter fra parametre
     c                   move      p_ktyp        bokkll_ktyp
     c                   move      p_ktyp        bokkl3_ktyp
     c                   eval      w_frda = p_frda
     c                   eval      bokkll_odat = w_frda
     c                   eval      w_tida = p_tida
     c                   time                    w_dato
      *
     c                   endsr
