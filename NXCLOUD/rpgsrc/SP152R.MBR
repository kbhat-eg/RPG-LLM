      /TITLE  ASSTAT Utvalg Avdeling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SP152R
      * Beskrivelse . . : Utvalg Avdeling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.xx  * 7.00  031016 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsp152d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
     fskpml1    if   e           k disk    rename(skpmpfr:skpml1r)
     fskpmlr    if   e           k disk    rename(skpmpfr:skpmlrr)
     fskpmlu    uf a e           k disk    rename(skpmpfr:skpmlur)
     fra07pf    if   e           k disk
      *
      * Definering av variabler i nøkler
      *
     d skpml1_type     s                   like(sptype)
     d skpml1_kode     s                   like(spkode)
     d skpml1_valg     s                   like(spvalg)
     d skpmlr_type     s                   like(sptype)
     d skpmlr_kode     s                   like(spkode)
     d skpmlr_valg     s                   like(spvalg)
     d skpmlu_type     s                   like(sptype)
     d skpmlu_kode     s                   like(spkode)
     d skpmlu_valg     s                   like(spvalg)
     d ra07pf_gkod     s                   like(ragkod)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(spfirm)
     d wptype          s                   like(sptype)
     d wpkode          s                   like(spkode)
     d wptext          s             30
     d wprtxt          s                   like(wptext)
     d wpgkod          s                   like(b1gkod)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Definering av Local data area
      *
     d                uds
     d dsuser                911    920
     d dsfirm                944    946  0
     d l_fnav                951    980
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = *blank
     c                   if        b2gkod <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> *blank
     c                   exsr      control
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     forny         begsr
      *
     c     skpml1_key    setll     skpml1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi
      *
     c                   movel     b2gkod        skpml1_valg
     c     skpml1_key    setll     skpml1
     c                   goto      end_pos
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2gkod = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     control       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_feil = *off
      *
      * Opprette
      *
     c                   if        b2valg = '1'
     c                   eval      c1gkod = b2gkod
     c                   exsr      xc1bld
     c                   goto      end_ctl
     c                   endif
      *
      * Validering av nøkkel
      *
     c                   if        b2gkod = 0
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = '4'
     c                   eval      d1gkod = b2gkod
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = *blank
     c                   eval      b2gkod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Slette post
      *
     c                   if        b1valg = '4'
     c                   eval      d1gkod = b1gkod
     c                   eval      d1text = b1text
     c                   exsr      xd1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xc1bld        begsr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                             and wactre = 'C1BLD'
     c                             and wactfe = 'C1GKOD'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    wpgkod
     c                   parm                    wprtxt
      *
     c                   eval      c1gkod =  wpgkod
     c                   goto      c1taga
     c                   endsl
      *
      * Sender opp informasjonsbilde dersom post finnes fra før
      *
     c                   movel     c1gkod        skpmlr_valg
     c     skpmlr_key    chain     skpmlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Sender opp feilmelding dersom post ikke finnes
      *
     c                   move      c1gkod        ra07pf_gkod
     c     ra07pf_key    chain     ra07pf                             90
     c                   if        *in90  = *on
     c                   eval      *in32  = *on
     c                   goto      c1taga
     c                   else
     c                   movel     ragtxt        c2text
     c                   endif
      *
      * Kaller registreringsrutine
      *
     c                   move      c1gkod        c2gkod
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xc2bld        begsr
      *
     c                   movel     c1gkod        skpmlr_valg
     c     skpmlr_key    chain     skpmlr                             90
      *
     c                   if        *in90 = *off
     c                   movel     sprest        c2text
     c                   else
      *                  eval      c2text = *blank
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   skpmlur
      *
     c                   movel     c1gkod        skpmlu_valg
     c     skpmlu_key    chain     skpmlur                            90
      *
     c                   movel     c2text        sprest
     c                   move      wptype        sptype
     c                   movel     wpkode        spkode
     c                   movel     c1gkod        spvalg
     c                   eval      spfirm = w_firm
     c                   eval      spsyst = 'K'
      *
     c                   if        *in90 = *off
     c                   update    skpmlur
     c                   else
     c                   write     skpmlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '1' or b2valg = '1'
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xd1win        begsr
      *
     c                   movel     d1gkod        skpmlr_valg
     c     skpmlr_key    chain     skpmlr                             90
      *
     c                   movel     sprest        d1text
      *
     c                   write     s1win
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter
      *
     c                   movel     d1gkod        skpmlu_valg
     c     skpmlu_key    chain     skpmlu                             90
     c                   delete    skpmlur
      *
     c     end_d1win     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      skpml1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        spfirm <> w_firm
     c                             or spkode <> wpkode
     c                             or sptype <> wptype
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   movel     spvalg        b1gkod
     c                   movel     sprest        b1text
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      * KEY for file
      *
     c     skpml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    skpml1_type
     c                   kfld                    skpml1_kode
     c                   kfld                    skpml1_valg
      *
      * KEY for file
      *
     c     skpmlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    skpmlr_type
     c                   kfld                    skpmlr_kode
     c                   kfld                    skpmlr_valg
      *
      * KEY for file
      *
     c     skpmlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    skpmlu_type
     c                   kfld                    skpmlu_kode
     c                   kfld                    skpmlu_valg
      *
      * KEY for hente-register
      *
     c     ra07pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07pf_gkod
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    wptype
     c                   parm                    wptext
      *
     c                   eval      wpkode = 52
     c                   eval      *in22 = *on
      *
     c                   eval      b2type = wptype
     c                   eval      b2text = wptext
      *
     c                   eval      skpml1_type = wptype
     c                   eval      skpml1_kode = wpkode
     c                   eval      skpml1_valg = *blank
      *
     c                   eval      skpmlu_type = wptype
     c                   eval      skpmlu_kode = wpkode
     c                   eval      skpmlu_valg = *blank
      *
     c                   eval      skpmlr_type = wptype
     c                   eval      skpmlr_kode = wpkode
     c                   eval      skpmlr_valg = *blank
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = dsfirm
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     skpml1_key    setll     skpml1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
