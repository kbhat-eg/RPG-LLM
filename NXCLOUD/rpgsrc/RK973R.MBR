     H datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK973R
      * Beskrivelse . . : Oppdaterer rentedato kundeposter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191202 KOB  Nytt program
      * V5.00 051103 HFL  Rentekode skal ikke 0-stille Kreditrentemerke
6.10  * V6.10 220909 BSA  Lagt inn sporingsfelter
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
      *
      * Definering av Local data area
      *
     d                uds
     d rpfirm                300    310  0
     d rprdat                          d   datfmt(*iso)
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d w_firm          s                   like(rnfirm)
      *
      * Les kundeposteringer
      *
     c     rktrlu_key    setll     rktrlu                                 90
     c     rktrlu_key    reade     rktrlu                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rnfdat < rprdat
     c                   eval      rnrdat = rprdat
     c                   else
     c                   eval      rnrdat = *loval
     c                   endif
      *
T5.00 * Rentekode 3 viser at posten har vært med på kreditrentekjøring.
T5.00c                   if        rnrenk <> 3
     c                   eval      rnrenk = 0
T5.00c                   endif
      *
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
      *
     c     rktrlu_key    reade     rktrlu                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
