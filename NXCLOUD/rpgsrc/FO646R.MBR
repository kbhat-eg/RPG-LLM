     h option(*nodebugio) decedit('0,') datedit(*dmy.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO646R
      * Beskrivelse . . : Utskrift av kjøre-liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *                   Nytt program
      *       040805 HKO  Henter volum- og vekt-informasjon fra enhet
      * 5.50  090307 KOB  Benytter leveringsdato i stedet for ordredato
      * 5.50  090307 KOB  Skriver ut ordretype
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.nu  * R6.30 190614 bof  Sjekket mtp. nummerutvidelse
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffpsol1    ipe  e           k disk    rename(fpsopfr:fpsol1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     f*ohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
      *
     ffo646p    o    e             printer
      *
      *
     d                 ds
      *                                                      Parameter-inn
     d ldprec                        64
     d  ldotyp                        2    overlay(ldprec)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:03)
     d  ldtdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:13)
     d  ldfavd                        4  0 overlay(ldprec:23)
     d  ldtavd                        4  0 overlay(ldprec:27)
     d  ldfkjo                        2  0 overlay(ldprec:31)
     d  ldtkjo                        2  0 overlay(ldprec:33)
     d  ldvari                        1  0 overlay(ldprec:35)
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     d                uds
     d dsruti                800    809
     d dsalin                856    858  0
      *
     d dswsid                901    910
     d dsuser                        10
     d dsfirm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d d1npri          s                   like(fdsapr)
     d fcycle          s              1  0
     d sanumm          s                   like(fdnumm)
     d utime           s              6  0
     d wltell          s              3  0
     d wltest          s              3  0
     d wpkont          s              5  0
     d wpmvaf          s             10  9
     d wpmvas          s              6  2
     d wpmvat          s              5  4
     d wppdat          s              8  0
     d wppomr          s             10  6
     d wpptx1          s             30
     d wpptx2          s             30
     d wpretk          s              1
     d wpspes          s              2  0
     d wwdekn          s             13  4
     d w_firm          s                   like(fofirm)
     d wwkund          s                   like(fokund)
     d wwline          s                   like(fdline)
     d wwltyp          s                   like(fdltyp)
     d wwnumm          s                   like(fonumm)
     d wwotyp          s                   like(vaotyp)
     d wwparm          s             64
     d wwpkod          s                   like(vapkod)
     d wwruti          s             10
     d wwsuff          s                   like(fosuff)
     d wwwsid          s                   like(fowsid)
     d w0kund          s                   like(faakun)
     d w1anta          s                   like(fdanta)
     d w1npri          s                   like(fdsapr)
     d w1rab1          s                   like(fdsapr)
     d w_retk          s              1
     d w_gtxt          s             20
     d w_itxt          s             20
     d w_jtxt          s             20
     d w_ytxt          s             30
      *
      *
     ifpsol1r       01
     i                                          fkssrt        l4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * T1TOT  - Total   T1 på rapport
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      * D1NPRI - Netto  pris
      * D1BPRI - Brutto pris
      * D1NBEL - Netto  beløp
      * D1KBEL - Kost   beløp
      * D1RABP - Rabatt i prosent (Sum prosent 1 og 2)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     c   01fcycle        caseq     *zero         xstrsr
     c                   endcs
      *
      * Ordrehode
      *
     c     fkskod        cabeq     'H'           tag01
      *
      * Varelinje
      *
     c     fkskod        cabeq     'V'           tag02
      *
      * Kommentar/
      * tekstlinje
      *
     c     fkskod        cabeq     'K'           tag03
      *
      * Ferdig
      *
     c                   goto      xslutt
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag01         tag
      *
      * Null-stilling av
      * variabler
      *
     c                   move      *off          *in37
     c                   eval      a1atxt = *blank
     c                   eval      a1ltxt = *blank
     c                   eval      a1stxt = *blank
      *
     c                   eval      t1brut = 0
     c                   eval      t1volu = 0
      *
      * Hent ordre-heading
      *
     c                   move      fksonr        wwnumm
     c                   move      fkssuf        wwsuff
     c     fohel1_key    chain     fohel1r                            61
      *
      * Snu dato
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        a1bdat
     c                   endif
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        a1ldat
     c                   endif
      *
      * Legg inn tilbudsnummer
      *
     c                   move      fonumm        a1numm
     c                   movel     '/'           a1suff
     c                   move      fosuff        a1suff
      *
      * Hente inn kunderegister
      *
     c                   move      fokund        wwkund
     c     key04         chain     rkunl1r                            64
     c                   move      fokund        a1kund
     c                   move      rkkatg        a1katg
     c                   move      rknavn        a1navn
     c                   move      rkgate        a1gate
     c                   move      rkadr2        a1adr2
     c                   move      rksted        a1sted
      *
      *  Hent tekst for avdeling
      *
     c                   eval      a1atxt = *blank
      *
     c                   if        foavde <> 0
     c                   call      'RS707R'
     c                   parm                    w_retk
     c                   parm                    foavde
     c                   parm                    w_gtxt
     c                   move      w_gtxt        a1atxt
     c                   endif
      *
      *  Hent tekst for selger
      *
     c                   eval      a1stxt = *blank
      *
     c                   if        foselg <> 0
     c                   call      'RS709R'
     c                   parm                    w_retk
     c                   parm                    foselg
     c                   parm                    w_itxt
     c                   move      w_itxt        a1stxt
     c                   endif
      *
      *  Hent tekst for lager
      *
     c                   eval      a1ltxt = *blank
      *
     c                   if        folage <> 0
     c                   call      'RS710R'
     c                   parm                    w_retk
     c                   parm                    folage
     c                   parm                    w_jtxt
     c                   move      w_jtxt        a1ltxt
     c                   endif
      *
      *  Hent tekst for kjørerute
      *
     c                   eval      a1ktxt = *blank
      *
     c                   if        fokjor <> 0
     c                   call      'RS725R'
     c                   parm                    w_retk
     c                   parm                    fokjor
     c                   parm                    w_ytxt
     c                   move      fokjor        a1kjor
     c                   move      w_ytxt        a1ktxt
     c                   endif
      *
      * Flytte øvrige felter
      *
     c                   move      fonavn        a2navn
     c                   move      foadr1        a2adr1
     c                   move      foadr2        a2adr2
     c                   move      fosted        a2sted
     c                   move      foavde        a1avde
     c                   move      folage        a1lage
     c                   move      foselg        a1selg
     c                   move      fodref        a1dref
     c                   move      forekv        a1rekv
     c                   move      footyp        a1otyp
      *
      * Skrive heading
      *
     c                   if        fksalt <> 5
     c                   eval      wltell = 0
     c                   write     a1hdr
     c                   endif
      *
     c                   write     a1hdr1                               30
      *
     c                   if        fksalt <> 5
     c                   write     a1hdr2
     c                   endif
      *
      * Er det overflow ?
      *
     c                   if        fksalt = 5
     c     *in30         caseq     *on           xovrfl
     c                   endcs
     c                   endif
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag02         tag
      *
      * Nullstilling av variabler
      *
     c                   eval      d1line = *zero
     c                   eval      d1vare = *blank
     c                   eval      d1tek1 = *blank
     c                   eval      d1enh1 = *blank
     c                   eval      d1ant0 = *zero
     c                   eval      d1ant1 = *zero
     c                   eval      d1ant2 = *zero
     c                   eval      d1ant3 = *zero
     c                   move      *off          *in44
     c                   move      *off          *in46
      *
     c                   move      fksonr        wwnumm
     c                   move      fkssuf        wwsuff
     c                   move      fkslin        wwline
     c     key02         chain     fodtl1r                            62
      *
      * Hent linje-type
      *
     c                   move      fdltyp        wwltyp
     c     key08         chain     vltyl1r                            68
      *
      * Sjekker om tekstlinjer
      *
     c     valktx        cabeq     1             tag02a
      *
      * Oppslag mot vareregister
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
      *
      * Test på antall desimaler
      *
     c                   eval      *in40 = vvdesi = 0
     c                   eval      *in41 = vvdesi = 1
     c                   eval      *in42 = vvdesi = 2
     c                   eval      *in43 = vvdesi = 3
     c                   if        *in40 = *on
     c                   eval      d1ant0 = fdanta
     c                   endif
     c                   if        *in41 = *on
     c                   eval      d1ant1 = fdanta
     c                   endif
     c                   if        *in42 = *on
     c                   eval      d1ant2 = fdanta
     c                   endif
     c                   if        *in43 = *on
     c                   eval      d1ant3 = fdanta
     c                   endif
      *
     c                   if        fdvare = '10034185'
     c                   eval      d1ant3 = fdanta
     c                   endif
      *
      * Varelinje
      *
     c                   move      fdline        d1line
     c                   move      fdvare        d1vare
     c                   movel     fdtek1        d1tek1
     c                   move      fdenh1        d1enh1
      *
      * Henter volum og vektinformasjon fra enhet
      *
     c                   eval      vvenl1_vare = fdvare
     c                   eval      vvenl1_enhe = fdenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      d1leng = veleng
     c                   eval      d1bred = vebred
     c                   eval      d1hoyd = vehoyd
     c                   eval(h)   d1brut = vevekt * fdanta
     c                   eval(h)   d1volu = vevolu * fdanta
     c                   else
     c                   eval      d1hoyd = 0
     c                   eval      d1bred = 0
     c                   eval      d1leng = 0
     c                   eval(h)   d1brut = 0
     c                   eval(h)   d1volu = 0
     c                   endif
      *
     c                   eval      t1brut = t1brut + d1brut
     c                   eval      t1volu = t1volu + d1volu
      *
      * Skriv varelinjer
      *
     c     tag02a        tag
     c                   if        valktx <> 1
     c                   if        fksalt <> 5
     c                   eval      wltell = wltell + 1
     c                   write     d1det                                30
     c                   endif
     c                   else
     c                   movel     fdtek1        d1tek1
     c                   movel     fdtek2        d1tek2
     c                   move      *on           *in44
     c                   if        fksalt <> 5
     c                   eval      wltell = wltell + 1
     c                   write     d2det                                30
     c                   endif
     c                   endif
      *
      * Er det overflow ?
      *
     c                   if        fksalt <> 5
     c     *in30         caseq     *on           xovrfl
     c                   endcs
     c                   endif
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag03         tag
      *
     c                   move      fksonr        wwnumm
     c                   move      fkssuf        wwsuff
     c                   move      fkslin        wwline
     c     key03         chain     fotxl1r                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     c                   if        vaosam = 0
     c                   if        fkslin >= 5000
     c                   goto      xslutt
     c                   endif
     c                   endif
      *
     c                   movel     fttext        e1text
     c                   if        fksalt <> 5
     c                   eval      wltell = wltell + 1
     c                   write     e1det                                30
     c                   endif
      *
      * Er det overflow ?
      *
     c                   if        fksalt <> 5
     c     *in30         caseq     *on           xovrfl
     c                   endcs
     c                   endif
      *
     c     xslutt        tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Null-stille variabler
      *
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     cl4                 eval      wltest = dsalin - 24
      *
      * Trekker ut antall
      * linjer skrevet
      *
     cl4                 eval      wltest = wltest - wltell
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     cl4                 eval      wltest = wltest - 10
      *
      * Skrive ny heading ?
      *
     cl4                 if        wltest < 0
     cl4                 if        fksalt <> 5
     cl4                 write     a1hdr
     cl4                 write     a1hdr1
     cl4                 write     a1hdr2
     cl4                 eval      wltell = 0
     cl4                 endif
     cl4                 endif
      *
      * Skrive totalsum
      *
     cl4                 if        fksalt <> 5
     cl4                 write     t1tot                                30
     cl4                 endif
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     cl4                 if        vaosam = 0                                            .....
     cl4                 move      fksonr        wwnumm                                      .
     cl4                 move      fkssuf        wwsuff                                      .
     cl4                 eval      wwline = 5000                                             .
     cl4   key03         setll     fotxl1r                                                   .
     cl4                 read      fotxl1r                                63                 .
     cl4   *in63         cabeq     *on           tag99                                       .
      *                                                                 .
     cl4                 dow       wwnumm = ftnumm                                       ... .
     cl4                 if        wwsuff = ftsuff                                         . .
      *                                                               . .
     cl4                 movel     fttext        e1text                                    . .
     cl4                 eval      wltell = wltell + 1                                     . .
     cl4                 if        fksalt <> 5
     cl4                 write     e1det                                30                 . .
     cl4                 endif                                                             . .
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     cl4                 if        *in30 = *on                                             . .
     cl4                 if        fksalt <> 5
     cl4                 write     a1hdr                                                   . .
     cl4                 write     a1hdr1                                                  . .
     cl4                 write     a1hdr2                                                  . .
     cl4                 eval      wltell = 0                                              . .
     cl4                 endif                                                             . .
     cl4                 endif                                                             . .
      *                                                               . .
     cl4                 read      fotxl1r                                63               . .
     cl4   *in63         cabeq     *on           tag99                                     . .
     cl4                 endif                                                             . .
     cl4                 enddo                                                           ... .
     cl4                 endif                                                           .....
      *
     cl4   tag99         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr
      *
     c                   if        fksalt <> 5
     c                   write     a1hdr1
     c                   write     a1hdr2
     c                   endif
      *
     c                   eval      wltell = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xstrsr        begsr
      *
     c                   eval      fcycle = 1
      *
      * Legger inn firma i nøkkel
      *
     c                   move      fksfir        w_firm
     c                   move      fksfir        a1firm
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     c     key06         chain     afirl1r                            66
     c                   move      aafnav        a1fnav
      *
      * Hente Kunde-tilbuds-nummer
      *
     c     key09         chain     fstsl1r
     c                   if        %found
     c                   move      faakun        w0kund
     c                   endif
      *
      * Slår opp ordretype
      *
     c                   move      fksoty        wwotyp
     c     key07         chain     votyl1r                            67
      *
     c                   move      vaotxt        a1txt1
     c                   move      fksoty        a1otyp
      *
      * Er ordretype negativ ?
      *
     c                   if        vaokre = '-'
     c                   move      *on           *in36
     c                   endif
      *
      * Skrive heading
      *
     c                   if        fksalt = 5
     c                   eval      wltell = 0
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wpretk = *blank
      *
     c                   eval      wpmvas = *zero
     c                   eval      wpmvat = *zero
     c                   eval      wpmvaf = *zero
      *
     c                   eval      wppdat = *zero
     c                   eval      wppomr = *zero
      *
     c                   eval      wpkont = *zero
     c                   eval      wpspes = *zero
     c                   eval      wpptx1 = *blank
     c                   eval      wpptx2 = *blank
      *
     c                   eval      wwdekn = *zero
      *
     c                   eval      wwruti = *blank
     c                   eval      wltell = *zero
     c                   eval      wltest = *zero
      *
      * Key for Heading-file
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    wwnumm
     c                   kfld                    wwsuff
      *
      * Key for Detalj-file
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    wwnumm
     c                   kfld                    wwsuff
     c                   kfld                    wwline
      *
      * Key for Tekst-linjer
      *
     c     key03         klist
     c                   kfld                    w_firm
     c                   kfld                    wwnumm
     c                   kfld                    wwsuff
     c                   kfld                    wwline
      *
      * Key for kunderegister
      *
     c     key04         klist
     c                   kfld                    w_firm
     c                   kfld                    wwkund
      *
      * Key for firmaregister
      *
     c     key06         klist
     c                   kfld                    w_firm
      *
      * Key for ordretyper
      *
     c     key07         klist
     c                   kfld                    w_firm
     c                   kfld                    wwotyp
      *
      * Key for linjetyper
      *
     c     key08         klist
     c                   kfld                    w_firm
     c                   kfld                    wwltyp
      *
      * Key for statusregister
      *
     c     key09         klist
     c                   kfld                    w_firm
      *
      * Key for formular-register
      *
     c     key10         klist
     c                   kfld                    wwruti
      *
      * Key for priskode-register
      *
     c     key19         klist
     c                   kfld                    w_firm
     c                   kfld                    wwpkod
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    wwparm
      *
     c                   move      wwparm        ldprec
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   move      dsuser        a1user
     c                   move      dswsid        a1wsid
     c                   move      udate         a1date
     c                   time                    utime
     c                   move      utime         a1time
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     c                   move      dsruti        wwruti
     c     key10         chain     aforl1r
      *
     c                   endsr
