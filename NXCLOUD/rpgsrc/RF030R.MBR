     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RF030R
      * Beskrivelse . . : Fordelingsparametre, vedlikehold - hovedrecord
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071102 KOB  Nytt program
      * V5.50 060307 KOB  Valg i subfile ble ikk blanket ved valg = 7
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter for fil RFORFP
7.xx  * 7.xx  100516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frforl2    if   e           k disk    rename(rforpfr:rforl2r)
     frforl3    if   e           k disk    rename(rforpfr:rforl3r)
     frforlr    if   e           k disk    rename(rforpfr:rforlrr)
     frforlu    uf a e           k disk    rename(rforpfr:rforlur)
      *
     frf030d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rforl2_kont     s                   like(rfkont)
     d rforl2_spes     s                   like(rfspes)
     d rforl2_avde     s                   like(rfavde)
     d rforl2_linj     s                   like(rflinj)
      *
     d rforl3_kont     s                   like(rfkont)
     d rforl3_spes     s                   like(rfspes)
     d rforl3_avde     s                   like(rfavde)
      *
     d rforlr_kont     s                   like(rfkont)
     d rforlr_spes     s                   like(rfspes)
     d rforlr_avde     s                   like(rfavde)
     d rforlr_linj     s                   like(rflinj)
      *
     d rforlu_kont     s                   like(rfkont)
     d rforlu_spes     s                   like(rfspes)
     d rforlu_avde     s                   like(rfavde)
     d rforlu_linj     s                   like(rflinj)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rffirm)
      *
     d p_stat          s              1
     d p_firm          s                   like(rffirm)
     d p_kode          s                   like(rfkode)
     d p_kont          s                   like(rfkont)
     d p_spes          s                   like(rfspes)
     d p_avde          s                   like(rfavde)
     d p_text          s             30
      *
     d s_kont          s                   like(rfkont)
     d s_spes          s                   like(rfspes)
     d s_avde          s                   like(rfavde)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kont <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rforl2_kont = b1kont
     c                   eval      rforl2_spes = b1spes
     c                   eval      rforl2_avde = b1avde
     c     rforl2_key    setll     rforl2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rabattgruppe
      *
     c                   if        b2kont <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rforl2_kont = b2kont
     c                   eval      rforl2_spes = b2spes
     c                   eval      rforl2_avde = b2avde
     c     rforl2_key    setll     rforl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kont = 0
     c                   eval      b2spes = 0
     c                   eval      b2avde = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kont = b1kont
     c                   eval      k1spes = b1spes
     c                   eval      k1avde = b1avde
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kont = b1kont
     c                   eval      d1spes = b1spes
     c                   eval      d1avde = b1avde
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *                                                                 .
      * Valg 7 kan ikke brukes for kode P                               .
      *                                                                 .
     c                   if        b1valg = 7 and                                            .
     c                             b1kode = 'P'                                              .
     c                   move      *on           *in36                                       .
     c                   iter                                                                .
     c                   endif                                                               .
      *
      * Call program for til-konti
      *
     c                   if        b1valg = 7                                                .
     c                   eval      p_kode = b1kode                                           .
     c                   eval      p_kont = b1kont                                           .
     c                   eval      p_spes = b1spes                                           .
     c                   eval      p_avde = b1avde                                           .
     c                   eval      b1valg = 0
     c                   update    b1sfl
      *
     c                   call      'RF031R'                                                  .
     c                   parm                    p_kode                                      .
     c                   parm                    p_kont                                      .
     c                   parm                    p_spes                                      .
     c                   parm                    p_avde                                      .
      *
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1kont = b2kont
     c                   eval      c1spes = b2spes
     c                   eval      c1avde = b2avde
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1kont = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1kont = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rforlr_kont = c1kont
     c                   eval      rforlr_spes = c1spes
     c                   eval      rforlr_avde = c1avde
     c                   eval      rforlr_linj = 0
     c     rforlr_key    chain     rforlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1kont = 0
     c                   eval      c1spes = 0
     c                   eval      c1avde = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kont = 0
     c                   eval      b2spes = 0
     c                   eval      b2avde = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rforlr_kont = c1kont
     c                   eval      rforlr_spes = c1spes
     c                   eval      rforlr_avde = c1avde
     c                   eval      rforlr_linj = 0
     c     rforlr_key    chain     rforlr
      *
     c                   if        %found
     c                   eval      c2kode = rfkode
     c                   eval      c2pros = rfpros
     c                   eval      c2pris = rfpris
     c                   eval      c2eusr = rfeusr
     c     *dmy          move      rfedat        c2edat
      *
      * Hente inn tekst fra kontoplan
      *
     c                   eval      p_kont = rfkont
     c                   eval      p_spes = rfspes
     c                   eval      p_avde = rfavde
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
S5.00c*                  parm                    p_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_stat = *blank                                           .
     c                   eval      c2text = p_text                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   else
     c                   eval      c2kode = ' '
     c                   eval      c2pros = 0
     c                   eval      c2pris = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1kont = k1kont
     c                   eval      c1spes = k1spes
     c                   eval      c1avde = k1avde
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c***                if        c2besk = *blank
     c***                eval      *in31 = *on
     c***                goto      c2taga
     c***                endif
      *
      * Oppdatering
      *
     c                   clear                   rforlur
      *
     c                   eval      rforlu_kont = c1kont
     c                   eval      rforlu_spes = c1spes
     c                   eval      rforlu_avde = c1avde
     c                   eval      rforlu_linj = 0
     c     rforlu_key    chain     rforlur
      *
     c                   eval      rfkode = c2kode
     c                   eval      rfpros = c2pros
     c                   eval      rfpris = c2pris
      *
     c                   time                    rfedat
     c                   time                    rfetim
     c                   eval      rfeusr = l_user
      *
     c                   if        %found
     c                   update    rforlur
     c                   else
     c                   eval      rffirm = w_firm
     c                   eval      rfkont = c1kont
     c                   eval      rfspes = c1spes
     c                   eval      rfavde = c1avde
     c                   eval      rflinj = 0
6.10 c                   time                    rfodat
6.10 c                   time                    rfotim
6.10 c                   eval      rfousr = l_user
     c                   write     rforlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1kode = rfkode
     c                   eval      b1pros = rfpros
     c                   eval      b1pris = rfpris
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rforlr_kont = d1kont
     c                   eval      rforlr_spes = d1spes
     c                   eval      rforlr_avde = d1avde
     c                   eval      rforlr_linj = 0
     c     rforlr_key    chain     rforlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Slett underrecords
      *
     c                   eval      rforl3_kont = d1kont
     c                   eval      rforl3_spes = d1spes
     c                   eval      rforl3_avde = d1avde
     c     rforl3_key    setll     rforl3r                                60
     c     rforl3_key    reade     rforl3r                                60
      *
     c                   dow       *in60 = *off                                 EOF
      *
     c                   eval      rforlu_kont = rfkont
     c                   eval      rforlu_spes = rfspes
     c                   eval      rforlu_avde = rfavde
     c                   eval      rforlu_linj = rflinj
     c     rforlu_key    chain     rforlur                            61
      *
     c                   if        %found
     c                   delete    rforlur
     c                   endif
      *
     c     rforl3_key    reade     rforl3r                                60
      *
     c                   enddo
      *
      * Sletter rad
      *
     c                   eval      rforlu_kont = d1kont
     c                   eval      rforlu_spes = d1spes
     c                   eval      rforlu_avde = d1avde
     c                   eval      rforlu_linj = 0
     c     rforlu_key    chain     rforlur
      *
     c                   delete    rforlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kont = k1kont
     c                   eval      s_spes = k1spes
     c                   eval      s_avde = k1avde
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rforlr_kont = k1kont
     c                   eval      rforlr_spes = k1spes
     c                   eval      rforlr_avde = k1avde
     c                   eval      rforlr_linj = 0
     c     rforlr_key    chain     rforlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1kont = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rforlr_kont = k1kont
     c                   eval      rforlr_spes = k1spes
     c                   eval      rforlr_avde = k1avde
     c                   eval      rforlr_linj = 0
     c     rforlr_key    chain     rforlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kont = s_kont
     c                   eval      c1spes = s_spes
     c                   eval      c1avde = s_avde
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rforl2
      *
     c                   if        %eof or
     c                             rffirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rforl2_kont = rfkont
     c                   eval      rforl2_spes = rfspes
     c                   eval      rforl2_avde = rfavde
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = rfkont
     c                   eval      b1spes = rfspes
     c                   eval      b1avde = rfavde
     c                   eval      b1kode = rfkode
     c                   eval      b1pros = rfpros
     c                   eval      b1pris = rfpris
      *                                                                 .
      * Hente inn tekst fra kontoplan                                   .
      *                                                                 .
     c                   eval      p_kont = rfkont
     c                   eval      p_spes = rfspes
     c                   eval      p_avde = rfavde
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
S5.00c*                  parm                    p_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                 .
     c                   if        p_stat = *blank                                           .
     c                   eval      b1text = p_text                                           .
     c                   endif                                                               .
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rforl2_key    setgt     rforl2
     c                   readp     rforl2
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rforl2
      *
     c                   if        %eof or
     c                             rffirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rforl2_kont = rfkont
     c                   eval      rforl2_spes = rfspes
     c                   eval      rforl2_avde = rfavde
      *
     c                   enddo
      *
     c                   if        %eof
     c     rforl2_key    setll     rforl2
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på fordelingsparametre - hovedrecord
      *
     c     rforl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforl2_kont
     c                   kfld                    rforl2_spes
     c                   kfld                    rforl2_avde
      *
      * Key for posisjonering på fordelingsparametre - underrecord
      *
     c     rforl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforl3_kont
     c                   kfld                    rforl3_spes
     c                   kfld                    rforl3_avde
      *
      * Key for les på fordelingsparametre
      *
     c     rforlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforlr_kont
     c                   kfld                    rforlr_spes
     c                   kfld                    rforlr_avde
     c                   kfld                    rforlr_linj
      *
      * Key for oppdatering av fordelingsparametre
      *
     c     rforlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforlu_kont
     c                   kfld                    rforlu_spes
     c                   kfld                    rforlu_avde
     c                   kfld                    rforlu_linj
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rforl2_kont = 0
     c                   eval      rforl2_spes = 0
     c                   eval      rforl2_avde = 0
     c                   eval      rforl2_linj = 0
     c     rforl2_key    setll     rforl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
