     h datedit(*dmy)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : BO128R
      * Beskrivelse . . : Oppdatering av økonomi og statistikk
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040914 ASK  Programmet er sterkt omskrevet
      *                   og er å se på som et nytt program.
6.28  * 6.20  300715 bsa  Bruker ordredato fra bongen som ordredato
6.28  *                   i statistikken. (Kan være at bonger "overnatter"
6.28  *                   i kassa før de overføres.)
6.34  * R6.30 101014 bof  Utvidet BM statistikk
6.35  * 6.30  221015 BOF  Justert vp905r parm, pga. nummer.utv.
6.35a * R6.30 189815 bof  Bruk av inlr mot VP905R, pga. treghet
6.36  * 6.30  280116 ask  Lagt inn korreksjon på mva grunnlag ved negative linje
6.37  * 6.30  280116 ask  Oppdaterer reskontro med nytt felt "Reskontrototal"
6.38  * 6.30  040316 bof  Feil-retting vedr. negativ bong.
6.39  * 6.30  090316 bof  Feil-retting vedr. CK og mvagrunnlag
6.3a  * 6.30  180516 bof  Må teste på div. stat. hvordan o.rab + rab skal poster
6.3aa *       130616 nho  FLytter Lev,måte til statistikk
6.3b  * 6.30  010716 bof  Feilretting CK ordre posteres ikke, mva.kode = feil
6.3c  * 6.30  010716 bof  Justere reskontro beløp med øreavrunding
6.3d  * 6.30  010716 bof  Legger regnskapprosjekt i BOVFPF
6.3e  * 6.30  050716 bof  Feilretting CK ordre-rabatt, ekstra test
6.3f  * 6.30  240816 bof  Feilretting CK linjerabatt, setter mva.gr.lag
6.3g  * 6.30  310816 bof  Skal alltid sette BOKAVS = 1 når den er behandlet
6.3h  * 6.30  310816 bof  Rettelse av 6.3e, vedr. CD
6.3i  * 6.30  120916 bof  post.av rabatt, ikke snu forregn på mva.gr.
6.3j  * 6.30  280217 bof  Feilretting mva-grunnlag alm.rabatt.
6.3k  * 6.30  280217 bof  Rettelse postering alm.rabatt - retur.
6.3l  * 6.30  050317 bof  Hvis gavekort, ikke oppdat stat.
6.nu  *       180218 bsa  Endret ihht. nummerutvidelse
6.3m  * 6.30  300518 bsa  Hvis innbetaling, kan dette nå skje som varelinje
6.3m  *                   og ikke på egen bong.
6.3n  * 6.30  131118 bsa  Oppdaterer felter på statistikk hvis benyttet
6.3n  *                   BM klubb kuponger.
6.3o  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.3p  * 6.30  110219 bof  Sjekk mot kort.bet.reg BOBLST også for nega.
6.3q  * 6.30  180619 bsa  Hvis kobling til bestilling, hent leverandør og
6.3q  *                   info fra bestilling.
6.3r  * 6.30  270919 bkv  Rettet feil i sumn for negative bonger
6.3s  * 6.30  051219 bof  Ekstra sjekk før rutine vedr. innbetaling
7.01  * 7.00  130919 nho  I forb. med depositum fjernes chain til FOHEL
7.01  *                   erstattes med SDEPL1 (problemer med suffix)
7.02  * 7.00  211119 bkv  Nummerserie rettelse ldlavs
7.02a * 7.00  191119 sst  Justeringer vedr SAF_T og tildeling av bilagnr/kode
7.02a *                   Nytt bilagsnr for hver bong.
7.03  *       271119 sst  Egen teller på bilagsnummer fra Kasse
7.03  *                   Teller = 50000000 + 1
7.03  *                   w_1_nr_pr_bong = J dersom ett bilanr pr bong
7.04  * 7.00  111219 bkv  Kommentert ut deler av 7.02a  og 7.03
7.05  * 6.30  051217 bsa  Lager åpen post på Santander hvis det er betalt med
7.05  *                   kort. Oppretter post i KID register.
7.06  *       301019 bsa  Justert tildeling av bilagsnummer ved Santander poster
7.07  * 7.00  180320 bof  Utvidet med payX  kort-type = 97
7.08  * 7.00  260320 bsa  Justert beløp som går til Santander pga ørediff.
7.09  * k00   060520 bSA  Utvidet med Vipps betaling kort-type = 96
7.10  * 7.00  060420 ask  Lagt inn kostføring dersom dette er valgt.
7.11  * K00   150720 ask  Lagt inn timestamp for loggoppfølging og skriving til loggfil
7.12  * K00   040321 ask  Utvidet linjeteller på loggfil
7.12  * K00   220321 ask  Lagt inn logging på bilagsnummer nivå
8.01  * 800   290421 ask  Lagt inn ordrenummer fra bong for referanse på forskuddsbetaling
8.02  * 800   280521 ask  Lagt inn firmanummer i SQL der det manglet

7.fb  * 7.00  160217 bof  Ikke legg ut depositums varenr på statistikk.
7.fb  * 7.00  050417 bof  Må legge ut bk og bktekst vedr. depositum
8.03  * 800   300921 sst  Utelate postering +/- på kontantkunde/alle kunder
8.04  * 800   190522 sst  Kid Santanderkunde flyttet til RN002R (8.04 slettet her)
8.05  * 800   070722 ask  Legger timestamp fro logging ut i LDA for bruk i senere program (BO129R)
8.06  * 800   130922 sst  Fjerne referanse på de som ikke har tilhørende reskontro post
8.07  * 800   130922 sst  Dersom ikke Nexstep Øk sjekk referanse mot sohe
8.08  * 800   170123 ask  Henter gjsn. glidende kostpris på utleveringsdato, nytt felt på SSTAPF
8.09  * 800   070223 bof  Tar priskode fra bodtpf uavhengig om LVR (i forb.m/prisforslag)
8.10  * 800   150323 ask  Lagt inn nye felter i utvodet logregister for regnskapstranser
8.11  * 800   180923 sst  Skippe linjer m/antall og pris = 0 (TX linjer fra shop u/TX)
8.12  * 800   171023 ask  Lagt inn parameterfil for VISMA_KOSTPRIS føring
8.13  * 800   261023 ask  Flyttet evt. føring av glidende kostpris inn i test-loop
      *                   for føring av bonglinje
8.14  * 800   141113 ask  Lagt inn ny summeringsfunksjonalitet på bunt
8.15  * 800   191023 ask  Lagt inn parameter for logging av kostføringer i VG805R
8.16  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.17  * 800   041024 ask  Endret funksjonalitet for kostførings transaksjoner til regnskap
      *
     fbpsola    if   e           k disk    rename(bpsopfr:bpsolar)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
7.fb ffst2l1    if   e           k disk    rename(fst2pfr:fst2l1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fbohelu    uf   e           k disk    rename(bohepfr:bohelur)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvgkl1    if   e           k disk    rename(vvgkpfr:vvgkl1r)
     fsstapf    o  a e             disk
     fbovfpf    o  a e             disk
7.01 f**fohel1    if   e           k disk    rename(fohepfr:fohel1r)
7.01 fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
     frmbtlr    if   e           k disk    rename(rmbtpfr:rmbtlrr)
     fbobli1    if   e           k disk    rename(boblst:bobli1r)
     fbokoir    if   e           k disk    rename(bokost:bokoirr)
6.34 fbokkl1    if   e           k disk    rename(bokkpfr:bokkl1r)
6.34 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
6.3o fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
6.3n fbckuiu    uf   e           k disk    rename(bckust:bckuiur)
6.3q flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
6.3q ffdesl1    if   e           k disk    rename(fdespfr:fdesl1r)
6.3q fsihel3    if   e           k disk    rename(sihepfr:sihel3r)
6.3q fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
7.03 f*anuml1    uf a e           k disk    rename(anumpfr:anuml1r)
7.05 fbrefl1    if   e           k disk    rename(brefpfr:brefl1r)
7.05 fbreflu    uf   e           k disk    rename(brefpfr:breflur)
8.06 frktrl8    if   e           k disk    rename(rktrpfr:rktrl8r)
8.07 fsohel2    if   e           k disk    rename(sohepfr:sohel2r)
8.08 fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
      *
      * Definering av parametere
      *
     d p_parm          s            256
      *
      * Definering av array's
      *
     d a_t             s             10    dim(8) ctdata perrcd(1)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * Definering av local data area
      *
     d                uds
8.05 d l_tmst                751    776
     d l_user                911    920
8.03 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av datastruktur for parameter
      *
     d                 ds
     d d_parm                       256
     d  d_otal                        4    overlay(d_parm)
     d  d_ot01                        2    overlay(d_parm:5)
     d  d_ot02                        2    overlay(d_parm:7)
     d  d_ot03                        2    overlay(d_parm:9)
     d  d_ot04                        2    overlay(d_parm:11)
     d  d_ot05                        2    overlay(d_parm:13)
     d  d_ot06                        2    overlay(d_parm:15)
     d  d_ot07                        2    overlay(d_parm:17)
     d  d_ot08                        2    overlay(d_parm:19)
     d  d_ot09                        2    overlay(d_parm:21)
     d  d_ot10                        2    overlay(d_parm:23)
     d  d_wsgr                        6    overlay(d_parm:25)
     d  d_wsal                       10    overlay(d_parm:31)
     d  d_ws01                       10    overlay(d_parm:41)
     d  d_ws02                       10    overlay(d_parm:51)
     d  d_ws03                       10    overlay(d_parm:61)
     d  d_ws04                       10    overlay(d_parm:71)
     d  d_ws05                       10    overlay(d_parm:81)
     d  d_ws06                       10    overlay(d_parm:91)
     d  d_ws07                       10    overlay(d_parm:101)
     d  d_ws08                       10    overlay(d_parm:111)
     d  d_ws09                       10    overlay(d_parm:121)
     d  d_ws10                       10    overlay(d_parm:131)
     d  d_avst                        1  0 overlay(d_parm:142)
     d  d_fdat                       10d   datfmt(*iso)
     d                                     overlay(d_parm:143)
     d  d_peri                        6  0 overlay(d_parm:153)
     d  d_dekn                        1  0 overlay(d_parm:159)
     d  d_vdag                        2  0 overlay(d_parm:160)
     d  d_deta                        1  0 overlay(d_parm:162)
     d  d_tota                        1  0 overlay(d_parm:163)
     d  d_frad                       10d   datfmt(*iso)
     d                                     overlay(d_parm:164)
     d  d_tild                       10d   datfmt(*iso)
     d                                     overlay(d_parm:174)
     d  d_aarr                        4  0 overlay(d_parm:184)
7.02 d  d_lavs                        8  0 overlay(d_parm:188)
7.02 d  d_ldet                        6  0 overlay(d_parm:196)
      *
      * Definering av datastrukturer
      * for konto-begrepene
      *
     d                 ds
     d d_komp                        15  0
     d  d_avde                        4  0 overlay(d_komp)
     d  d_kont                        6  0 overlay(d_komp:5)
     d  d_spes                        4  0 overlay(d_komp:11)
     d  d_mkod                        1    overlay(d_komp:15)
      *
     d                 ds
     d d_aks1                        15  0
     d  d_avd1                        4  0 overlay(d_aks1)
     d  d_kto1                        6  0 overlay(d_aks1:5)
     d  d_sps1                        4  0 overlay(d_aks1:11)
     d  d_mko1                        1    overlay(d_aks1:15)
      *
     d                 ds
     d d_aks2                        15  0
     d  d_avd2                        4  0 overlay(d_aks2)
     d  d_kto2                        6  0 overlay(d_aks2:5)
     d  d_sps2                        4  0 overlay(d_aks2:11)
     d  d_mko2                        1    overlay(d_aks2:15)
      *
      * Definering av datastruktur
      * for kontoinformasjon
      *
     d                 ds
     d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
     d  d_ksp1                        4  0 overlay(d_hele:11)
     d  d_kav2                        4  0 overlay(d_hele:15)
     d  d_kko2                        6  0 overlay(d_hele:19)
     d  d_ksp2                        4  0 overlay(d_hele:25)
     d  d_kav3                        4  0 overlay(d_hele:29)
     d  d_kko3                        6  0 overlay(d_hele:33)
     d  d_ksp3                        4  0 overlay(d_hele:39)
     d  d_kav4                        4  0 overlay(d_hele:43)
     d  d_kko4                        6  0 overlay(d_hele:47)
     d  d_ksp4                        4  0 overlay(d_hele:53)
     d  d_kav5                        4  0 overlay(d_hele:57)
     d  d_kko5                        6  0 overlay(d_hele:61)
     d  d_ksp5                        4  0 overlay(d_hele:67)
     d  d_kav6                        4  0 overlay(d_hele:71)
     d  d_kko6                        6  0 overlay(d_hele:75)
     d  d_ksp6                        4  0 overlay(d_hele:81)
     d  d_kav7                        4  0 overlay(d_hele:85)
     d  d_kko7                        6  0 overlay(d_hele:89)
     d  d_ksp7                        4  0 overlay(d_hele:95)
     d  d_khev                        4    overlay(d_hele:99)
     d  d_nivo                        1    overlay(d_hele:103)
6.3m  *
6.3m d                 ds
6.3m d d_leng                         8  0
6.3m d  w_len8                        8    overlay(d_leng:1)
6.3m d  w_len7                        7    overlay(d_leng:2)
6.3m d  w_len6                        6    overlay(d_leng:3)
6.3m d  w_len5                        5    overlay(d_leng:4)
6.3m d  w_len4                        4    overlay(d_leng:5)
6.3m d  w_len3                        3    overlay(d_leng:6)
6.3m d  w_len2                        2    overlay(d_leng:7)
6.3m d  w_len1                        1    overlay(d_leng:8)
7.10 d w_disp          s              1
7.10 d w_tilg          s              1
7.10 d w_modu          s             10
7.05  *
7.05  * KID utregning
7.05  *
7.05 d                 ds
7.05 d d_kkid                        21
7.05 d  d_kkus                       20    overlay(d_kkid)
7.05 d   d_kkli                       3  0 overlay(d_kkus)
7.05 d   d_kkod                       3  0 overlay(d_kkus:4)
7.05 d   d_kund                       6  0 overlay(d_kkus:7)
7.05 d   d_kbil                       8  0 overlay(d_kkus:13)
7.05 d  d_kmod                        1  0 overlay(d_kkid:21)
7.05  *
7.05  * Datastruktur for egendefinert kidd
7.05  *
7.05 d*                ds
7.05 d*d_kkid_2                      23
7.05 d* d_kkus_2                     22    overlay(d_kkid_2)
7.05 d*  d_kdum_2                    14    overlay(d_kkus_2)
7.05 d*  d_kfak_2                     8  0 overlay(d_kkus_2:15)
7.05 d* d_kmod_2                      1  0 overlay(d_kkid_2:23)
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d bohelu_aarr     s                   like(boaarr)
     d bohelu_wsid     s                   like(bowsid)
     d bohelu_numm     s                   like(bobong)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_wsid     s                   like(bdwsid)
     d bodtl1_numm     s                   like(bdbong)
     d bodtl1_line     s                   like(bdline)
     d vvenl1_vare     s                   like(vevare)
     d vvgkl1_ogrp     s                   like(vkogrp)
     d vvgkl1_hgrp     s                   like(vkhgrp)
     d vvgkl1_ugrp     s                   like(vkugrp)
     d vvgkl1_ldor     s                   like(vkldor)
7.01 d*fohel1_numm     s                   like(fonumm)
7.01 d*fohel1_suff     s                   like(fosuff)
7.01 d sdepl1_numm     s                   like(sknumm)
7.01 d sdepl1_kund     s                   like(skkund)
     d rmbtlr_tbrt     s                   like(rmtbrt)
     d rmbtlr_talm     s                   like(rmtalm)
     d bobli1_kass     s                   like(bjkass)
     d bobli1_aarr     s                   like(bjaarr)
     d bobli1_bong     s                   like(bjbong)
     d bokoir_ktyp     s                   like(byktyp)
6.34 d bokkl1_bong     s                   like(brbong)
6.34 d amodl1_modu     s                   like(axmodu)
6.3n d bckuiu_bong     s                   like(sfbong)
6.3n d bckuiu_line     s                   like(sfline)
6.3q d lodtlr_numm     s                   like(ldnumm)
6.3q d lodtlr_suff     s                   like(ldsuff)
6.3q d lodtlr_line     s                   like(ldline)
6.3q d fdesl1_numm     s                   like(fsnumm)
6.3q d fdesl1_suff     s                   like(fssuff)
6.3q d fdesl1_line     s                   like(fsline)
6.3q d sihel3_aarr     s                   like(shaarr)
6.3q d sihel3_numm     s                   like(shnumm)
6.3q d jvarl1_vare     s                   like(jvvare)
7.03 d* anuml1_fell     s                   like(anfell)
7.03 d* anuml1_type     s                   like(antype)
7.03 d* anuml1_fast     s                   like(anfast)
7.05 d brefl1_aarr     s                   like(bxaarr)
7.05 d brefl1_bong     s                   like(bxbong)
7.05 d breflu_aarr     s                   like(bxaarr)
7.05 d breflu_bong     s                   like(bxbong)
8.06 d rktrl8_kund     s                   like(rnkund)
8.06 d rktrl8_biln     s                   like(rnbiln)
8.07 d sohel2_biln     s                   like(sobinr)
8.08 d vvenlr_vare     s                   like(vevare)
8.08 d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_udat          s               d   datfmt(*iso)
     d w_ldor          s              6  0
6.3o d w_lv_nobb       s                   like(vvlnob)
6.3o d w_lv_modn       s                   like(vvlmod)
6.3o d w_lv_lldo       s                   like(vvlnle)
6.3o d w_lv_llva       s                   like(vvllva)
     d w_dato          s               d   datfmt(*iso)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_lety          s                   like(bdlety)
     d w_line          s                   like(sfline)
     d w_pran          s                   like(sfpran)
     d w_saar          s              4  0
     d w_suke          s              2  0
     d w_vtyp          s                   like(vvvtyp)
7.02 d w_debug         s              1    inz('N')
7.02 d w_rut           s             50
7.03 d* w_saf_aar       s              2  0
7.05 d w_kund          s                   like(rkkund)
7.05 d s_kund          s                   like(rkkund)
7.05 d w_ksif          s              1  0
7.05 d w_kkus          s             24
7.05 d w_kkid          s             25
7.05 d w_kki2          s             25
7.05 d w_kki27         s             27
7.05 d w_kki21         s             21
7.05 d w_kidr          s              8  0
7.05 d w_refk          s             18
7.05 d s_bila          s              8  0
      *
6.nu d bilanr          s              8  0
     d subÅr           s              2  0
     d subdag          s              2  0
     d subdat          s              6  0
     d w_retk          s              1
     d w_hele          s            128
     d w_hgrp          s              2  0
     d w_ogrp          s              2  0
     d w_otyp          s              2
     d w_stat          s              1
     d w_ugrp          s              3  0
     d w_vare          s             15
     d w_tots          s             11  2
     d w_text          s             20
7.fb d w_texd          s             20
     d w_anta          s             11  3
     d w_sums          s             12  3
     d w_fast          s             10
     d w_snum          s             12
     d w_fell          s              6
7.03 d* w_fell          s              6    inz('BKASSE')
7.03 d* w_1_nr_pr_bong  s              1    inz(' ')
     d w_kode          s              1
6.nu d w_numm          s              8  0
     d w_type          s              2
     d w_belp          s             11  2
     d w_belb          s             11  2
     d w_mvag          s             11  2
     d w_depo          s             11  2
6.3m d w_innb          s             11  2
6.3m d w_leng          s              3  0
6.3m d w_fakn          s              8  0
     d w_resk          s             11  2
     d w_bila          s              8  0
     d w_bili          s              4  0
     d w_bunt          s              5  0
     d w_ffda          s              6  0
     d w_fkda          s              6  0
     d w_kkod          s              1
     d w_bkod          s              2  0
     d w_peri          s              4  0
     d w_avde          s              4  0
     d w_kont          s              6  0
     d w_spes          s              4  0
     d w_savd          s              4  0
     d w_skon          s              6  0
     d w_sspe          s              4  0
     d w_mkod          s              1
     d w_nega          s              1
     d w_valg          s              1  0
      *
6.34 d w_pkon          s              3
6.34 d w_sumn          s             11  2
6.34 d w_sumi          s             11  2
6.34 d wwsumo          s             11  2
6.34 d w_suno          s             11  2
6.34 d w_ntpr          s             11  2
6.34 d w_suoi          s             11  2
6.34 d wwsumk          s             11  2
6.34 d w_suok          s             11  2
6.34 d w_diff          s             11  2
6.34 d w_pkns          s              3
6.34 d w_pkko          s              3
6.34 d w_pkok          s              3
6.34 d w_pkin          s              3
6.34 d w_pkoi          s              3
6.34 d w_kofa          s              1
6.34 d w_card          s              2
6.34 d w_fcar          s              2
6.34 d w_kild          s              3
6.34 d w_soda          s                   like(sfsoda)
6.34 d w_soti          s                   like(sfsoti)
6.34 d w_rkro_stk      s             11  2
6.38 d w_sumb          s                   like(bdsums)
6.38 d w_sumo          s                   like(bdsums)
6.38 d w_sumk          s                   like(bdsumk)
6.3r d w_summ          s                   like(bdbmva)
7.12 d w_lintel        s              8  0
8.14 d w_dsum          s             12  2
8.14 d w_ksum          s             12  2
7.11 d w_sumbel        s             11  2
7.11 d w_dumtim        s               z
7.11 d w_dumsum        s             11  2
7.11 d w_dumusr        s             10
7.11 d w_dumerr        s              2
7.11 d w_dumkki        s              8  0
7.11 d w_dumkkk        s              1  0
8.10 d w_dumd20        s              2  0
7.11 d w_dumaki        s             25
7.11 d w_dumpru        s              6
7.13 d w_dumant        s              5  0
8.07 d w_Nx_Øk         s              1    inz(' ')
8.08 d w_gjdat         s              8  0
8.08 d w_gjko          s              9  2
8.08 d w_gjkp          s             11  2
6.34  *
6.34  * Datastruktur parameterliste -inn
6.34  *
6.34 d                 ds
6.34 d hirec                        120
6.34 d  hifirm                        3  0 overlay(hirec)
6.34 d  hirkat                        3  0 overlay(hirec:4)
6.34 d  hikund                        6  0 overlay(hirec:7)
6.34 d  hikpro                        6  0 overlay(hirec:13)
6.34 d  hivare                       15    overlay(hirec:19)
6.34 d  hilety                        1  0 overlay(hirec:34)
6.34 d  hienhe                        3    overlay(hirec:35)
6.34 d  hidato                         d   overlay(hirec:38) datfmt(*iso)
6.34 d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.35 d  hisapr                        9  2 overlay(hirec:80)
6.35 d  hikopr                        9  2 overlay(hirec:89)
8.16 d  hiprgr                        2    overlay(hirec:98)
8.16 d  hiinpr                        9  2 overlay(hirec:100)
6.34 d  hiinlr                        1    overlay(hirec:120)
6.34  *
6.34  * Datastruktur parameterliste -ut
6.34  *
6.34 d                 ds
6.34 d horec                        100
6.34 d  holety                        1  0 overlay(horec)
6.34 d  hokpri                        1    overlay(horec:2)
6.34 d  hooppr                        9  2 overlay(horec:3)
6.34 d  hosapr                        9  2 overlay(horec:12)
6.34 d  hokopr                        9  2 overlay(horec:21)
6.34 d  horab1                        5  2 overlay(horec:30)
6.34 d  horab2                        5  2 overlay(horec:35)
6.34 d  hodekn                        5  2 overlay(horec:40)
6.34 d  hopros                        5  2 overlay(horec:45)
6.34 d  hokamp                        5    overlay(horec:50)
6.34 d  hoalme                        4  0 overlay(horec:55)
6.34 d  hobrty                        1  0 overlay(horec:59)
6.34 d  hobrr1                        5  2 overlay(horec:60)
6.34 d  hobrr2                        5  2 overlay(horec:65)
6.34 d  hoomva                        1    overlay(horec:70)
8.16 d  hoprgr                        2    overlay(horec:71)
8.16 d  hoinpr                        9  2 overlay(horec:73)
8.16 d  hopkon                        3    overlay(horec:82)
8.16 d  hopkns                        3    overlay(horec:85)
8.16 d  hopkko                        3    overlay(horec:88)
8.16 d  hopkok                        3    overlay(horec:91)
8.16 d  hopkoi                        3    overlay(horec:94)
6.34  *
      *
     d w_mvak          s              1
     d w_mvtx          s             30
     d w_bpro          s              5  2
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
7.11 d w_logg          s               z
      *                                                                     -
     d wofkda          s              6  0
     d woperi          s              4  0
     d n               s              2  0
     d b_xeof          s              1    inz(*off)
     d b_tsth          s              1    inz(*off)
     d b_tstl          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
6.34 d b_bmmo          s              1    inz(*off)
7.05 d b_sant          s              1    inz(*off)
7.07 d b_payx          s              1    inz(*off)
7.09 d b_vips          s              1    inz(*off)
7.10 d b_kost          s              1    inz(*off)
8.03 d w_postkont      s              1    inz(' ')
8.03 d                 ds
8.03 d w_kontkunx                     6
8.03 d w_kontkund                     6  0 overlay(w_kontkunx)
8.12 d u_812           s              1    inz(*off)
8.03  *
8.03  *
8.03   dcl-s co402_verdi1  char(1);
8.03   dcl-s co402_verdi2  char(2048);
8.03   dcl-pr co402r extpgm('CO402R');
8.03     p_filgrp            char(3)     const;
8.03     p_firm              packed(3:0) const;
8.03     p_lager             packed(2:0) const;
8.03     p_nokkel            char(50)    const;
8.03     p_verdi1            char(1);
8.03     p_verdi2            char(2048);
8.03   end-pr;
      *
7.11  *
7.11 C/Exec SQL
7.11 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
7.11 C+             commit=*none
7.11 C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine                                                    les
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      bpsola
      *
     c                   dow       not %eof
      *
      * Sjekker om brudd på bongnummer
      *
     c                   if        bsorct = 'H'
     c                   if        w_snum <> *blank and
     c                             bsonum <> w_snum and
     c                             b_tsth = *off
     c                   exsr      slutt_bong
     c                   endif
     c                   eval      w_snum = bsonum
     c                   endif
      *
      * Sjekker om bonghode skal behandles
      *
     c                   if        bsorct = 'H'
     c                   exsr      sjekk_bong
     c                   if        b_tsth = *off
     c                   exsr      bong_hode
     c                   endif
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker om bonglinjer skal behandles
      *
     c                   if        b_tsth = *off
     c                   exsr      sjekk_linje
     c                   if        b_tstl = *off
     c                   exsr      bong_linje
8.13 c*                  endif
8.13 c*                  endif
7.10  *
7.10  *  Fører salg til glidende gjennomsnittlig kostpris skal føres
7.10  *
7.10 c                   if        b_kost = *on
7.10 c                   call      'VG805R'
7.10 c                   parm                    boaarr
7.10 c                   parm                    bowsid
7.10 c                   parm                    bdbong
7.10 c                   parm                    bdline
8.15 c                   parm                    w_logg
8.15 c                   parm                    w_lintel
8.15 c                   parm                    bfbdat
7.10 c                   endif
8.13 c                   endif
8.13 c                   endif
7.10  *
      *
     c     les_neste     tag
     c                   read      bpsola
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   if        b_tsth = *off
     c                   exsr      slutt_bong
     c                   endif
      *
     c                   eval      *inlr = *on
7.13 c                   exsr      logg_bilag
7.11 c                   exsr      logg_hode
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av bong hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bong_hode     begsr
      *
7.02 c                   if        w_debug = 'J'
7.02 c                   eval      w_rut = 'Bong_hode: ' +
7.02 c                                    %trim(bsonum)  + ' ' +
7.02 c                                    'vaoakk: ' +
7.02 c                                    %trim(%char(vaoakk)) + ' ' +
7.02 c                                    %trim(%char(bobinr)) + ' ' +
7.02 c                                    %trim(%char(bilanr))
7.02 c                   call      'DEBUGCL'
7.02 c                   parm                    w_rut
7.02 c                   endif
      *
     c                   eval      w_bkod = vaobil
      *
      * Dersom faktura/kreditnota bilagsnummer=fakturanummer
      *
     c                   if        vaoakk = 3
     c                   eval      w_bila = bobinr
     c                   else
7.03 c*                   if        w_1_nr_pr_bong = 'J'
7.02 c*                   exsr      hent_bilag
7.03 c*                   endif
7.07 c                   if        b_sant = *off and
7.09 c                             b_payx = *off and
7.09 c                             b_vips = *off
     c                   eval      w_bila = bilanr
7.06 c                   endif
     c                   endif
      *
      * Dersom fakturadato ikke finnes bruk bongdato
      *
     c                   if        bofkda = *loval
     c                   eval      bofkda = bodate
     c                   endif
      *
      * Snur fakturadato før utlegging til regnskap
      *
     c                   eval      w_fkda = 0
      *
     c                   if        bofkda <> *loval
     c     *dmy          move      bofkda        w_fkda
     c                   endif
      *
      * Legger inn forfallsdato før oppdatering
      *
     c                   eval      w_ffda = 0
      *
     c                   if        vaoakk = 4
     c                   eval      w_ffda = w_fkda
     c                   endif
      *
      * Legger over opplysninger i utfeltene
      *
     c                   eval      wofkda = 0
     c                   eval      w_bunt = 0
      *
     c                   if        bofkda <> *loval
     c     *ymd          move      bofkda        wofkda
     c                   move      wofkda        w_bunt
     c                   endif
      *
      * Sjekker om dette er en negativ bong
      *
     c                   if        vaokre = '-'
     c                   eval      w_nega = 'K'
     c                   endif
      *
      * Hent opplysninger fra KUNDE-REGISTER
      *
     c                   eval      rkunl1_kund = bokund
     c     rkunl1_key    chain     rkunl1r                            90
7.05 c                   eval      w_kund = rkkund
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av bong linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bong_linje    begsr
      *
      * Nullstiller totaler for utskrift
      *
     c                   eval      w_anta = 0
     c                   eval      w_sums = 0
     c                   eval      w_mvag = 0
7.fb c                   eval      w_texd = *blank
      *
     c                   eval      w_kkod = *blank
      *
      * Sjekker om depositum innbetaling
      *
     c                   if        bdvare = baadva
     c                   exsr      lag_depo
     c                   goto      end_linje
     c                   endif
6.3m  *
6.3m  * Sjekker om innbetaling av faktura har skjedd
6.3m  *
6.3m c                   if        bootyp <> 'IB' and
6.3m c                             bootyp <> 'UB'
6.3s c                   if        baaiva <> *blank and
6.3s c                             bdvare = baaiva
6.3m c                   exsr      lag_innbet
6.3m c                   goto      end_linje
6.3m c                   endif
6.3m c                   endif
      *
      * Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = bdvare
     c     vvarl1_key    chain     vvarl1r                            90
      *
      * Hent opplysninger fra undergruppe-register
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1r                            90
      *
      * Hente varetype
      *
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    bdvare
     c                   parm                    bolage
     c                   parm                    w_vtyp
     c                   parm                    w_udat
     c                   parm                    w_ldor
     c                   parm                    b_inlr
6.3o c                   parm                    w_lv_nobb
6.3o c                   parm                    w_lv_modn
6.3o c                   parm                    w_lv_lldo
6.3o c                   parm                    w_lv_llva
      *
      * Finner den kontoen som skal benyttes
      *
     c                   exsr      hntkon
      *
      * Bestemmer MVA-behandling  (MVA-fri transaksjon?)
      *
     c                   if        bomkod = 1
     c                   eval      w_valg = 4
     c                   eval      bdkmva = '4'
     c                   endif
      *
     c                   eval      w_mkod = bdkmva
      *
      * Sjekker om gavekort håndtering
      *
     c                   if        bdvare = baagva
     c                   eval      w_valg = 5
     c                   endif
      *
      * Omregning av enhet til statistikk
      *
     c                   exsr      omr_stat
      *
      * Beregner bruttobeløp dersom rabatter skal spesifiseres
      *
     c                   eval      w_sums = bdsumi
      *  Linjerabatt?
     c                   if        faasa1 = 1
6.3e c                   if        w_nega = 'K'
6.3e c                   eval(h)   w_sums = w_sums -
6.3e c                                      (bdrbi1 + bdrbi2)
6.3e c                   else
6.3e c                   eval(h)   w_sums = w_sums +
6.3e c                                      (bdrbi1 + bdrbi2)
6.3e c                   endif
     c                   endif
      *  Alm.rabatt?
     c                   if        faasa2 = 1
6.3e c                   if        w_nega = 'K'
6.3e c                   eval(h)   w_sums = w_sums -
6.3e c                                      (bdbib1 + bdbib2)
6.3e c                   else
6.3e c                   eval(h)   w_sums = w_sums +
6.3e c                                      (bdbib1 + bdbib2)
6.3e c                   endif
     c                   endif
      *  Ordrerabatt?
     c                   if        faasao = 1
6.3e c                   if        w_nega = 'K'
6.3e c                   eval(h)   w_sums = w_sums - bdorbi
6.3e c                   else
6.3e c                   eval(h)   w_sums = w_sums + bdorbi
6.3e c                   endif
     c                   endif
6.38  *
6.38  * danner beløpsfelt til statistikk  - snur evt. fortegn
6.38  *
6.38 c                   eval      w_sumb = bdsums
6.38 c                   eval      w_sumo = bdsumo
6.38 c                   eval      w_sumk = bdsumk
6.38 c                   eval      w_suok = bdsuok
6.3r c                   eval      w_summ = bdbmva
      *
      * Sjekker om dette er en negativ bong
      *
     c                   if        w_nega = 'K'
     c                   eval      w_anta = w_anta * -1
     c                   eval      w_sums = w_sums * -1
6.38 c*                  eval      bdrbe1 = bdrbe1 * -1
6.38 c*                  eval      bdrbe2 = bdrbe2 * -1
6.38 c*                  eval      bdorbe = bdorbe * -1
6.38 c*                  eval      bdbrb1 = bdbrb1 * -1
6.38 c*                  eval      bdbrb2 = bdbrb2 * -1
     c                   eval      bdsuki = bdsuki * -1
6.38 c                   eval      w_sumb = w_sumb * -1
6.38 c                   eval      w_sumo = w_sumo * -1
6.38 c                   eval      w_sumk = w_sumk * -1
6.38 c                   eval      w_suok = w_suok * -1
6.3r c                   eval      w_summ = w_summ * -1
     c                   endif
      *
      * Sjekker om dette er en negativ linje
      * Vær oppmerksom på at W_SUMS(BDSUMS) er lagret med rett fortegn
      *
     c                   if        valkre = '-'
     c                   eval      w_anta = w_anta * -1
6.38 c*                  eval      bdrbe1 = bdrbe1 * -1
6.38 c*                  eval      bdrbe2 = bdrbe2 * -1
6.38 c*                  eval      bdorbe = bdorbe * -1
6.38 c*                  eval      bdbrb1 = bdbrb1 * -1
6.38 c*                  eval      bdbrb2 = bdbrb2 * -1
6.38 c                   eval      w_sumb = w_sumb * -1
6.38 c                   eval      w_sumo = w_sumo * -1
6.38 c                   eval      w_sumk = w_sumk * -1
6.38 c                   eval      w_suok = w_suok * -1
6.3r c                   eval      w_summ = w_summ * -1
     c                   endif
      *
      * Sjekker om parametre tilsier oppdatering
      *
     c                   if        vaooko = 1
     c                   if        valoko = 1
     c                   if        vaoakk = 3 or
     c                             vaoakk = 4
      *
      * Oppdaterer salgskonto hovedbok
      *   Fjerner mva-grunlag på gavekortsalg og mva-frie salg
      *
     c                   if        bdvare = baagva
     c                   eval      w_mvag = 0
     C                   else
     c                   eval      w_mvag = bdgmva
     c                   endif
6.3a  * hvis ordre.rab/rabatter/alm.rab skal posteres, legg til i mva.gr.lag
6.3a c                   if        faasao = 1
6.3e c                   if        w_nega = 'K'
6.3e c                   eval(h)   w_mvag = w_mvag - bdorbe
6.3e c                   else
6.3e c                   eval(h)   w_mvag = w_mvag + bdorbe
6.3e c                   endif
6.3e c                   endif
6.3e c                   if        faasa1 = 1
6.3e c                   if        w_nega = 'K'
6.3e c                   eval(h)   w_mvag = w_mvag -
6.3e c                                      (bdrbe1 + bdrbe2)
6.3e c                   else
6.3e c                   eval(h)   w_mvag = w_mvag +
6.3e c                                      (bdrbe1 + bdrbe2)
6.3e c                   endif
6.3e c                   endif
6.3e c                   if        faasa2 = 1
6.3j c*                  if        w_nega = 'K'
6.3j c*                  eval(h)   w_mvag = w_mvag -
6.3j c*                                     (bdbrb1 + bdbrb2)
6.3j c*                  else
6.3j c*                  eval(h)   w_mvag = w_mvag +
6.3j c*                                     (bdbrb1 + bdbrb2)
6.3j c*                  endif
6.3a c                   endif
      *
     c                   if        w_mkod = '4'
     c                   eval      w_mvag = 0
     c                   endif
      *
     c                   eval      w_belp = w_sums
      *
     c                   eval      w_tots = w_tots + w_belp
      *
     c                   exsr      exekon
      *
     c                   eval      w_savd = w_avde
     c                   eval      w_skon = w_kont
     c                   eval      w_sspe = w_spes
      *
      * Oppdaterer konto for linjerabatt dersom spesifikasjon er valgt
      *
     c                   if        faasa1 = 1
6.3f c                   if        bdrbi1 <> 0 or
6.3f c                             bdrbi2 <> 0
6.3i c                   if        w_nega = 'K'
6.3i c                   eval      w_mvag = (bdrbe1 + bdrbe2) * -1
6.3i c                   else
6.3i c                   eval      w_mvag = (bdrbe1 + bdrbe2)
6.3i c                   endif
6.3h c                   eval      w_belp = (bdrbi1 + bdrbi2) * -1
     c                   eval      w_kont = d_kko6
     c                   eval      w_spes = d_ksp6
6.3f c                   if        bomkod = 1
6.3f c                   eval      w_mkod = '4'
6.3f c                   else
6.3f c                   eval      w_mkod = '3'
6.3f c                   endif
     c                   eval      w_valg = 8
     c                   exsr      exekon
     c                   endif
     c                   endif
      *
      * Oppdaterer konto for ordrerabatt dersom spesifikasjon er valgt
      *
     c                   if        faasao = 1 and
6.3b c                             bdorbi <> 0
6.3i c                   if        w_nega = 'K'
6.3i c                   eval      w_mvag = bdorbe * -1
6.3i c                   else
6.3i c                   eval      w_mvag = bdorbe
6.3i c                   endif
6.3h c                   eval      w_belp = bdorbi * -1
     c                   eval      w_kont = d_kko6
     c                   eval      w_spes = d_ksp6
3.3f c                   if        bomkod = 1
3.3f c                   eval      w_mkod = '4'
3.3f c                   else
3.3f c                   eval      w_mkod = '3'
3.3f c                   endif
     c                   eval      w_valg = 8
     c                   exsr      exekon
     c                   endif
      *
      * Oppdaterer konto for almenningsrabatt dersom spesifikasjon er valgt
      *
     c                   if        faasa2 = 1
6.3k c                   if        bdbib1 <> 0 or
6.3k c                             bdbib2 <> 0
     c                   eval      rmbtlr_talm = bdalme
     c                   eval      rmbtlr_tbrt = bdbrty
     c     rmbtlr_key    chain     rmbtlr
     c                   if        %found
     c                   eval      w_kont = rmtkto
     c                   eval      w_spes = rmtsps
     c                   else
     c                   eval      w_kont = d_kko6
     c                   eval      w_spes = d_ksp6
     c                   endif
      *
     c                   eval      w_mkod = '0'
     c                   eval      w_valg = 7
     c                   eval      w_mvag = 0
     c                   eval      w_belp = (bdbib1 + bdbib2) * -1
     c                   exsr      exekon
     c                   endif
     c                   endif
      *
      * Oppdatering av beholdningsendring i hovedbok dersom spesifikasjon er val
      *
     c                   if        faasak = 1
     c                   exsr      post_kost
     c                   endif
      *
     c                   endif
     c                   endif
     c                   endif
      *
      * Leveringsadresse oppdateres i feltet
      * for prisordre, dersom leveringsadresse
      * er oppgitt
      *
     c                   if        bokpro <> 0
     c                   eval      bopord = bokpro
     c                   eval      bopsuf = 0
     c                   endif
      *
      * Oppdatering av statistikk - dropper gavekort
      *
     c                   if        w_valg <> 5
6.3l c                   if        bdvare <> baagva
     c                   if        vaosta = 1
     c                   if        valsta = 1
     c                   if        vaoakk = 3
     c                             or vaoakk = 4
      *
     c                   if        bolkun = 0
     c                   eval      bolkun = bokund
     c                   endif
      *
     c                   exsr      oppd_stat
     c                   endif
     c                   endif
     c                   endif
6.3l c                   endif
     c                   endif
      *
      * Nullstiller totaler etter oppdatering
      *
     c                   eval      w_valg = 0
     c                   eval      w_avde = 0
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
     c                   eval      w_mkod = *blank
     c                   eval      w_belp = 0
     c                   eval      w_kkod = *blank
     c                   eval      w_anta = 0
      *
     c     end_linje     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sluttføring av bong
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt_bong    begsr
      *
      * Dersom ikke innbetaling, er salg oppgjort med gavekort?
6.3g  * Alle skal merkes 1
6.3g c*                  if        vaoakk <> 5
6.3g c*                  if        w_tots = 0
6.3g c*                  goto      end_slutt
6.3g c*                  endif
6.3g c*                  endif
      *
      * Hent opplysninger fra BONG-HODE-REGISTER
      *
     c     bohelu_key    chain     bohelur                            90
      *
      * Oppdaterer BONG-HODE-REGISTER, Kode=1
      *
     c                   if        *in90 = *off
     c                   eval      bokavs = 1
     c                   time                    boedat
     c                   time                    boetim
     c                   eval      boeusr = l_user
     c                   update    bohelur
     c                   endif
      *
      * Finner riktig fortegn på beløpene
      *
     c                   if        vaokre = '-'
     c                   eval      botots = botots * -1
     c                   eval      bototb = bototb * -1
     c                   endif
      *
      * Reduserer reskotrobeløp med evt. depositumsinnbetaling
      *
6.3m c**                 eval      w_resk = boresk - w_depo - boavrb
6.3m c                   eval      w_resk = boresk - w_depo - boavrb - w_innb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut transaksjon  (Kundereskontro)       (SALG)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        vaoakk = 3
     c                             or vaoakk = 4
     c                   eval      w_belp = w_resk * -1
     c                   if        w_belp <> 0
      *
     c                   eval      w_valg = 0
     c                   eval      w_bkod = vaobil
      *
     c                   eval      w_kkod = *blank
      *
     c                   eval      w_mkod = '0'
     c                   eval      w_kont = bokund
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
      *
     c                   if        vaoakk = 4
     c                   if        bobdpo > 0
     c                   eval      w_kkod = ' '
     c                   else
     c                   eval      w_kkod = 'K'
     c                   endif
     c                   endif
      *
      * Salg på kontantkunde legges ikke ut
      *
     c                   if        w_kont <> baakun
     c                   eval      w_mvag = 0
     c                   exsr      exekon
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut transaksjon  (Kundereskontro)       (BETALING)
      * Trekker fra tidligere innbetalt depositum på salgsbeløp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        vaoakk = 4
     c                             or vaoakk = 5
     c                   eval      w_belp = w_resk - bobdpo
     c                   if        w_belp <> 0
      *
     c                   eval      w_bkod = vaobil
      *
     c                   eval      w_valg = 0
     c                   eval      w_kkod = *blank
      *
      * Henter inn Bilagskode fra status, dersom kontantsalg
      *
     c                   if        vaoakk = 4
     c                   eval      w_bkod = baabin
     c                   if        bobdpo > 0
     c                   eval      w_kkod = ' '
     c                   else
     c                   eval      w_kkod = 'K'
     c                   endif
     c                   endif
      *
      *
     c                   eval      w_mkod = '0'
     c                   eval      w_kont = bokund
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
     c                   eval      w_mvag = 0
      *
      * Salg på kontantkunde legges ikke ut
      *
     c                   if        w_kont <> baakun
     c                   exsr      exekon
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut transaksjon  (Hovedbok)             (KASSE)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        vaoakk = 4
     c                             or vaoakk = 5
     c                   if        bototb <> 0 and
     c                             bobknt <> 0
      *
     c                   eval      w_kkod = *blank
      *
     c                   eval      w_belp = bobknt * -1
     c                   eval      w_valg = 0
     c                   eval      w_bkod = baabka
      *
     c                   eval      w_mkod = '0'
     c                   eval      w_kont = baaopk
     c                   eval      w_spes = baaops
     c                   eval      w_avde = boavde
     c                   eval      w_mvag = 0
      *
     c                   exsr      exekon
     c                   endif
     c                   endif
7.05  *
7.05  * Poster kortselskap reskontro
7.05  *
7.05 c                   if        vaoakk = 4
7.05 c                             or vaoakk = 5
7.05 c                   if        bototb <> 0 and
7.05 c                             bobkrt <> 0
7.05  *
7.05  * Santander
7.05  *
7.05 c                   if        b_sant = *on
7.05 c                   exsr      pos_kort
7.05 c                   endif
7.07  *
7.07  * Payex
7.07  *
7.07 c                   if        b_payx = *on
7.09 c                   exsr      pos_kort
7.09 c                   endif
7.07  *
7.09  * Vipps
7.09  *
7.09 c                   if        b_vips = *on
7.09 c                   exsr      pos_kort
7.09 c                   endif
7.09  *
7.05 c                   endif
7.05 c                   endif
7.07  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger ut transaksjon  (Hovedbok)             (BANK)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        vaoakk = 4
     c                             or vaoakk = 5
     c                   if        bototb <> 0 and
     c                             bobkrt <> 0
      *
7.05 c                   if        b_sant = *off and
7.09 c                             b_payx = *off and
7.09 c                             b_vips = *off
7.05  *
     c                   eval      w_kkod = *blank
      *
     c                   eval      w_belb = bobkrt
     c                   eval      w_valg = 0
     c                   eval      w_bkod = baabnk
     c                   eval      w_mkod = '0'
     c                   eval      w_mvag = 0
     c                   eval      w_avde = boavde
     c                   eval      w_bkod = baabnk
      *
      * Sjekker om kortbetaling skal posteres på egen konto
      *  Slår opp i kortbetalingsregister BOBLST.
      *
     c                   eval      bobli1_kass = bowsid
     c                   eval      bobli1_aarr = boaarr
     c                   eval      bobli1_bong = bobong
      *
     c     bobli1_key    setll     bobli1
     c     bobli1_key    reade     bobli1r
     c                   dow       not %eof(bobli1)
     c                   if        bjtype <> *blank and
6.3p c                             bjsumb <> 0
     c                   eval      bokoir_ktyp = bjtype
     c     bokoir_key    chain     bokoir
6.3p c                   if        %found(bokoir)
6.3p c***                          byhkon <> 0
     c                   eval      w_belp = bjsumb * -1
6.3p c                   if        byhkon <> 0
     c                   eval      w_kont = byhkon
     c                   eval      w_spes = byspes
6.3p c                   else
6.3p c                   eval      w_kont = baasak
6.3p c                   eval      w_spes = baaops
6.3p c                   endif
     c                   exsr      exekon
     c                   eval      w_belb = w_belb - bjsumb
6.3p c                   else
     c                   eval      w_belp = bjsumb * -1
6.3p c                   eval      w_kont = baasak
6.3p c                   eval      w_spes = baaops
6.3p c                   exsr      exekon
6.3p c                   eval      w_belb = w_belb - bjsumb
     c                   endif
     c                   endif
     c     bobli1_key    reade     bobli1r
     c                   enddo
      *
      * Evt restbeløp posteres på std konto for bank
      *
     c                   if        w_belb > 0
     c                   eval      w_bkod = baabnk
     c                   eval      w_belp = w_belb * -1
     c                   eval      w_kont = baasak
     c                   eval      w_spes = baaops
     c                   exsr      exekon
     c                   endif
      *
7.05 c                   endif
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Ø R E A V R U N D I N G
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Sjekker om øreavrunding er utført
      *
     c                   if        vaoakk = 5
     c                   goto      ikke_rund
     c                   endif
      *
     c                   if        boavrb = 0
     c                   goto      ikke_rund
     c                   endif
      *
      * Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = baavar
     c     vvarl1_key    chain     vvarl1r                            90
      *
      * Finner den kontoen som skal benyttes
      *
     c                   exsr      hntkon
      *
      * Legg ut mva-kode avhengig av ordre
      * avgiftfri eller avgiftspliktig
      *
     c                   if        bomkod = 1
     c                   eval      w_mkod = '4'
     c                   else
     c                   eval      w_mkod = '3'
     c                   endif
      *
     c                   eval      w_mvag = 0
     c                   eval      w_belp = boavrb
      *
     c                   exsr      exekon
      *
      * Nullstiller variable før neste bong
      *
     c     ikke_rund     tag
     c                   eval      w_fkda = 0
     c                   eval      w_ffda = 0
     c                   eval      wofkda = 0
     c                   eval      w_bunt = 0
     c                   eval      w_tots = 0
     c                   eval      w_bkod = 0
     c                   eval      w_depo = 0
6.3m c                   eval      w_innb = 0
6.3m c                   eval      w_fakn = 0
     c                   eval      w_resk = 0
      *
     c                   eval      w_nega = 'D'
      *
     c     end_slutt     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Utlegging av informasjon til konto
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     exekon        begsr
      *
      * Hent bilagstekst
      *
7.02 c                   if        w_bkod = *zero
7.02 c                   eval      votyl1_otyp = bootyp
7.02 c     votyl1_key    chain     votyl1r
7.02 c                   eval      w_bkod = vaobil
7.02 c                   endif
     c                   call      'RS701R'
     c                   parm                    w_retk
     c                   parm                    w_bkod
     c                   parm                    w_text
      *
      * Legger inn ekstra-bilags-tekst på slutten av bilagstekstfeltet
      *
     c                   if        w_valg = 2
     c                   eval      w_text = a_t(02)
     c                   endif
      *
     c                   if        w_valg = 3
     c                   eval      w_text = a_t(03)
     c                   endif
      *
     c                   if        w_valg = 4
     c                   eval      w_text = a_t(04)
     c                   endif
      *
     c                   if        w_valg = 5
     c                   eval      w_text = a_t(05)
     c                   endif
      *
     c                   if        w_valg = 7
     c                   eval      w_text = a_t(07)
     c                   endif
      *
7.fb c                   if        w_texd <> *blank
7.fb c                   eval      w_text = w_texd
7.fb c                   endif
      *
     c                   eval      d_avde = w_avde
     c                   eval      d_kont = w_kont
     c                   eval      d_spes = w_spes
     c                   eval      d_mkod = w_mkod
      *
      * Legger ut transaksjon til ØKONOMI-SYSTEMET
      *
     c                   eval      bffirm = w_firm
     c                   eval      bfperi = w_peri
     c                   eval      bfbunt = w_bunt
     c                   eval      bfbiln = w_bila
     c                   eval      bfbill = w_bili
     c                   eval      bfbdat = *loval
     c                   eval      bffdat = *loval
     c                   eval      bfbilk = w_bkod
7.03 c*                  eval      bftext = w_text
7.03 c                   eval      bftext = %trim(bobong) + '/' +
7.03 c                                      %trim(%char(d_lavs))
     c                   eval      bfdavd = 0
     c                   eval      bfdkto = 0
     c                   eval      bfdsps = 0
     c                   eval      bfdmom = ' '
     c                   eval      bfbelp = w_belp
     c                   eval      bfmvag = w_mvag
     c                   eval      bfcavd = 0
     c                   eval      bfckto = 0
     c                   eval      bfcsps = 0
     c                   eval      bfcmom = ' '
     c                   eval      bfkref = 0
6.3m c                   if        w_fakn <> 0
6.3m c                   eval      bfkref = w_fakn
6.3m c                   endif
8.06  *
8.06  *   Sjekk om motpost finnes med samme beløp, hvis ikke fjernes ref.
8.06 c                   if        bfkref <> *zero
8.07 c                   if        w_Nx_Øk = 'J'
8.06 c                   eval      rktrl8_kund = w_kund
8.06 c                   eval      rktrl8_biln = bfkref
8.06 c     rktrl8_key    setll     rktrl8
8.06 c     rktrl8_key    reade     rktrl8
8.06 c                   dow       not %eof(rktrl8)
8.06 c                   if        rnrest = bfbelp
8.06 c                   goto      ref_ok
8.06 c                   endif
8.06 c     rktrl8_key    reade     rktrl8
8.06 c                   enddo
8.06 c                   eval      bfkref = *zero
8.06 c     ref_ok        tag
8.07  *
8.07 c                   else
8.07 c                   eval      sohel2_biln = bfkref
8.07 c     sohel2_key    setll     sohel2
8.07 c     sohel2_key    reade     sohel2
8.07 c                   dow       not %eof(sohel2)
8.07 c                   if        sofsum = bfbelp
8.07 c                   goto      Sref_ok
8.07 c                   endif
8.07 c     sohel2_key    reade     sohel2
8.07 c                   enddo
8.07 c                   eval      bfkref = *zero
8.07 c     Sref_ok       tag
8.07  *
8.07 c                   endif
8.06 c                   endif
8.06  *
8.01 c                   if        bfkref = 0
8.01 c                   eval      bfkref = bdornr
8.01 c                   endif
7.05 c**                 eval      bfkund = rkkund
7.05 c                   eval      bfkund = w_kund
     c                   eval      bfvalk = *blank
     c                   eval      bfvalb = 0
     c                   eval      bfmngk = 0
     c                   eval      bfmngd = 0
     c                   eval      bfautx = w_kkod
6.3d c                   eval      bfproj = boproj
      *
      * Legger inn riktig
      * kontobegrep
      *
     c                   if        w_belp > 0
     c                   eval      bfcavd = w_avde
     c                   eval      bfckto = w_kont
     c                   eval      bfcsps = w_spes
     c                   eval      bfcmom = w_mkod
     c                   else
     c                   eval      bfdavd = w_avde
     c                   eval      bfdkto = w_kont
     c                   eval      bfdsps = w_spes
     c                   eval      bfdmom = w_mkod
     c                   eval      w_belp = w_belp * -1
     c                   eval      bfbelp = bfbelp * -1
     c                   endif
6.39 c                   if        bdanta < 0
6.39 c                   eval      bfmvag = w_mvag * -1
6.39 c                   endif
      *
      * Legger inn dato
      * og forfallsdato
      *
     c                   if        w_fkda <> 0
     c     *dmy          move      w_fkda        bfbdat
     c                   endif
      *
     c                   if        w_ffda <> 0
     c     *dmy          move      w_ffda        bffdat
     c                   endif
7.11  *
7.11  * Timestamp for logging
7.11  *
7.11 c                   eval      bflogg = w_logg
      *
8.03 c                   if        ((w_postkont = '2'
8.03 c                                 and w_kont > 9999)
8.03 c                               or (w_postkont = '1'
8.03 c                                 and w_kont = w_kontkund))
     c                             and bfautx = 'K'
     c                   else
     c                   write     bovfpfr
8.03 c                   endif
7.11  *
7.11  *  Skriv til loggfil
7.11  *
7.11 c                   exsr      logg_linjer
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fører varebevegleser dersom det er valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     post_kost     begsr
      *
      * Skal kost postering utelates?
      *
     c                   if        bdvare = baagva
     c                   goto      end_kost
     c                   endif
      *
     c                   eval      d_mko1 = '0'
     c                   eval      d_mko2 = '0'
      *
     c                   eval      d_avd1 = d_kav3
     c                   eval      d_kto1 = d_kko3
     c                   eval      d_sps1 = d_ksp3
      *
     c                   eval      d_avd2 = d_kav5
     c                   eval      d_kto2 = d_kko5
     c                   eval      d_sps2 = d_ksp5
      *
      * Legge ut transaksjon til hovedbok
      *
     c                   eval      bffirm = w_firm
     c                   eval      bfperi = w_peri
     c                   eval      bfbunt = w_bunt
     c                   eval      bfbiln = w_bila
     c                   eval      bfbill = w_bili
     c                   eval      bfbdat = *loval
     c                   eval      bffdat = *loval
     c                   eval      bfbilk = w_bkod
     c                   eval      bftext = w_text
     c                   eval      bfdavd = 0
     c                   eval      bfdkto = 0
     c                   eval      bfdsps = 0
     c                   eval      bfdmom = ' '
     c                   eval      bfcavd = 0
     c                   eval      bfckto = 0
     c                   eval      bfcsps = 0
     c                   eval      bfcmom = ' '
     c                   eval      bfkref = 0
7.05 c**                 eval      bfkund = rkkund
7.05 c                   eval      bfkund = w_kund
     c                   eval      bfvalk = *blank
     c                   eval      bfvalb = 0
     c                   eval      bfmngk = 0
     c                   eval      bfmngd = 0
     c                   eval      bfautx = w_kkod
     c                   eval      bfmvag = 0
      *
      * Legger inn dato
      * og forfallsdato
      *
     c                   if        w_fkda <> 0
     c     *dmy          move      w_fkda        bfbdat
     c                   endif
      *
     c                   if        w_ffda <> 0
     c     *dmy          move      w_ffda        bffdat
     c                   endif
      *
      * Legger inn kontobegrep og fører post
      *
     c                   if        bdsuki > 0
     c                   eval      bfbelp = bdsuki
     c                   eval      bfdavd = d_avd1
     c                   eval      bfdkto = d_kto1
     c                   eval      bfdsps = d_sps1
     c                   eval      bfdmom = d_mko1
     c                   else
     c                   eval      bfbelp = bdsuki * -1
     c                   eval      bfdavd = d_avd2
     c                   eval      bfdkto = d_kto2
     c                   eval      bfdsps = d_sps2
     c                   eval      bfdmom = d_mko2
     c                   endif
     c                   eval      bfcavd = 0
     c                   eval      bfckto = 0
     c                   eval      bfcsps = 0
     c                   eval      bfcmom = *blank
7.11  *
7.11  * Timestamp for logging
7.11  *
7.11 c                   eval      bflogg = w_logg
      *
     c                   write     bovfpfr
7.11  *
7.11  *  Skriv til loggfil
7.11  *
7.11 c                   exsr      logg_linjer
      *
      * Legger inn kontobegrep og fører motpost
      *
     c                   if        bdsuki > 0
     c                   eval      bfbelp = bdsuki
     c                   eval      bfcavd = d_avd2
     c                   eval      bfckto = d_kto2
     c                   eval      bfcsps = d_sps2
     c                   eval      bfcmom = d_mko2
     c                   else
     c                   eval      bfbelp = bdsuki * -1
     c                   eval      bfcavd = d_avd1
     c                   eval      bfckto = d_kto1
     c                   eval      bfcsps = d_sps1
     c                   eval      bfcmom = d_mko1
     c                   endif
     c                   eval      bfdavd = 0
     c                   eval      bfdkto = 0
     c                   eval      bfdsps = 0
     c                   eval      bfdmom = *blank
7.11  *
7.11  * Timestamp for logging
7.11  *
7.11 c                   eval      bflogg = w_logg
      *
     c                   write     bovfpfr
7.11  *
7.11  *  Skriv til loggfil
7.11  *
7.11 c                   exsr      logg_linjer
      *
     c                   eval      w_bili = 0
      *
      *
     c     end_kost      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter avdeling/konto/spesifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntkon        begsr
      *
      * Legger ut parametere for konto-henvisnings-program
      *
     c                   eval      w_stat = *blank
     c                   eval      w_otyp = bsooty
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_vare = vvvare
     c                   move      0             w_hele
      *
      * Dersom vare er hentet fra LVR,
      * skal varegruppe hentes fra linjenivå
      *
     c                   if        bdlvre = 1
     c                   eval      w_ogrp = bdogrp
     c                   eval      w_hgrp = bdhgrp
     c                   eval      w_ugrp = bdugrp
     c                   eval      w_vare = bdvare
     c                   move      0             w_hele
     c                   endif
      *
      * Legger inn leveringstype fra varelinje,
      *
     c                   eval      w_lety = bdlety
      *
      * Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_stat
     c                   parm                    w_lety
     c                   parm                    w_otyp
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_vare
     c                   parm                    w_hele
     c                   eval      d_hele = w_hele
      *
      * Overstyrer avdeling dersom denne
      * finnes på ordrehode
      *
     c                   if        boavde > 0
     c                   eval      d_kav1 = boavde
     c                   eval      d_kav2 = boavde
     c                   eval      d_kav3 = boavde
     c                   eval      d_kav4 = boavde
     c                   eval      d_kav5 = boavde
     c                   eval      d_kav6 = boavde
     c                   eval      d_kav7 = boavde
     c                   endif
      *
      * Legger inn avdeling/konto/spesifikasjon fra record
      *
     c                   eval      w_avde = boavde
     c                   eval      w_kont = bokont
     c                   eval      w_spes = bospes
      *
      * Gå videre dersom konto nå er funnet
      *
     c     w_kont        cabne     0             xtag01
      *
      * Hente konti fra kontohenvisningsregister
      *
     c                   if        bomkod = 0
     c                   eval      w_kont = d_kko1
     c                   eval      w_spes = d_ksp1
      *
     c                   eval      w_stat = *blank
     c                   eval      w_mvak = bdkmva
     c                   time                    w_dato

     c                   call      'RS205R'
     c                   parm                    w_stat
     c                   parm                    w_mvak
     c                   parm                    w_mvtx
     c                   parm                    w_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
     c                   parm                    w_bpro

     c                   if        w_stat = *blank and
     c                             w_mvap = 0
     c                   eval      w_kont = d_kko2
     c                   eval      w_spes = d_ksp2
     c                   endif
      *
     c                   else
     c                   eval      w_kont = d_kko2
     c                   eval      w_spes = d_ksp2
     c                   endif
      *
     c     xtag01        tag
      *
      * Gå videre dersom avdeling nå er funnet
      *
     c     w_avde        cabne     0             xtag02
      *
      * Hente avdeling fra kontohenvisningsregister
      *
     c                   if        bomkod = 0
     c                   eval      w_avde = d_kav1
      *
     c                   if        vvkmva = 1
     c                   eval      w_avde = d_kav2
     c                   endif
      *
     c                   else
     c                   eval      w_avde = d_kav2
     c                   endif
      *
     c     xtag02        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall i forhold til statistikkenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_stat      begsr
      *
      * Dersom stat.enhet ikke finnes på vare, hentes stat.enhet fra
      * undergruppe. Dersom stat.enhet ikke finnes på undergruppe, settes
      * stat.enhet lik solgt enhet
      *
     c                   if        vvenhs = *blank
     c                   eval      vvenhs = vguens
     c                   endif
      *
     c                   if        vvenhs = *blank
     c                   eval      vvenhs = bdenh1
     c                   endif
      *
     c                   eval      w_anta = bdanta
      *
      * Dersom solgt enhet er lik stat.enhet beholdes antall
      *
     c                   if        bdenh1 = vvenhs
     c                   goto      end_omr_stat
     c                   endif
      *
      * Finner omregningsfaktor volum for stat.enhet og for solgt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = bdvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 90
     c                   dow       *in90 = *off
     c                   if        bdenh1 = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c                   if        vvenhs = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 90
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
     c                   eval      w_anta = w_anta * w_omrs / w_omrl
      *
     c     end_omr_stat  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer statistikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_stat     begsr
      *
7.fb c                   if        bdvare = baadva
7.fb c                   leavesr
7.fb c                   endif
6.34  *
6.34  * Henter utvidet statistikk-data
6.34  *
6.34 c                   if        b_bmmo = *on
6.34 c                   exsr      utvid_stat
6.34 c                   endif
8.08  *
8.08  * Henter gj.sn. kostpris på utleveringstidspunkt
8.08  *
8.08 c                   exsr      hent_gjkost
      *
      * Flytt over felter
      *
     c                   clear                   sstapfr
      *
     c                   eval      sffirm = bofirm
     c                   movel     d_peri        sfpaar
     c                   move      d_peri        sfpmnd
      *
     c                   eval      sfvkun = bolkun
     c                   eval      sffkun = bokund
     c                   if        bolkun = 0
     c                   eval      sfvkun = bokund
     c                   endif
      *
     c                   eval      sfkpro = bokpro
     c                   eval      sfkkat = rkkatg
     c                   eval      sfrkat = borkat
     c                   eval      sfselg = bdselg
     c                   eval      sfland = 0
     c                   eval      sfdist = bodist
     c                   eval      sffrik = rkfrie
     c                   eval      sflety = bdlety
     c                   eval      sfvare = bdvare
     c                   eval      sftek1 = bdtek1
     c                   eval      sftek2 = bdtek2
     c                   eval      sfogrp = vvogrp
     c                   eval      sfhgrp = vvhgrp
     c                   eval      sfugrp = vvugrp
     c                   eval      sfrgrp = 0
8.09 c*                  eval      sfkpri = vvkpri
8.09 c                   eval      sfkpri = bdkpri
     c                   eval      sfvtyp = w_vtyp
     c                   eval      sfldor = vvldor
6.3o c                   if        w_lv_nobb > 0
6.3o c                   eval      sflvar = w_lv_llva
6.3o c                   eval      sfnobb = w_lv_nobb
6.3o c                   eval      sfmodn = w_lv_modn
6.3o c                   else
     c                   eval      sflvar = vvlvar
     c                   eval      sfnobb = vvnobb
     c                   eval      sfmodn = vvnmod
6.3o c                   endif
     c                   eval      sfahgr = vvahgr
     c                   eval      sfaugr = vvaugr
     c                   eval      sfkamp = bdkamp
     c                   eval      sflage = bolage
     c                   eval      sfbinr = w_bila
     c                   eval      w_line = w_line + 1
     c                   eval      sfline = w_line
     c                   eval      sfbida = bofkda
     c                   eval      sfotyp = bootyp
     c                   if        bdornr <> 0
     c                   eval      sfornr = bdornr
     c                   else
     c                   eval      sfornr = boornr
     c                   endif
     c                   eval      sforsu = bdorsu
     c                   eval      sforli = bdorli
6.28 c**                 eval      sforda = bodate
6.28 c                   eval      sforda = bobdat
     c                   eval      sfbong = bobong
     c                   eval      sfsavd = w_savd
     c                   eval      sfskon = w_skon
     c                   eval      sfssps = w_sspe
     c                   eval      sfomva = bdkmva
     c                   eval      sfanto = bdanta
     c                   eval      sfanta = w_anta
     c                   eval      sffakr = *blank
6.38 c                   eval(h)   sfsumo = w_sumo
6.38 c                   eval(h)   sfsumb = w_sumb
6.38 c                   eval      sfsumk = w_sumk
6.28 c                   eval      sfsuok = w_suok
     c                   eval      sfrab1 = bdrbe1
     c                   eval      sfrab2 = bdrbe2
     c                   eval      sfrabo = bdorbe
     c                   eval      sfbrk1 = bdbrb1
     c                   eval      sfbrk2 = bdbrb2
     c                   eval      sfkmva = bomkod
6.3r c*                   eval      sfsumm = bdbmva
6.3r c                   eval      sfsumm = w_summ
     c                   eval      sfkres = 0
     c                   eval      sfenhe = vvenhs
     c                   eval      sfenho = bdenh1
     c                   eval      sfkhev = d_khev
     c                   eval      sfproj = boproj
     c                   eval      sfakti = boakti
     c                   eval      sfwsid = bowsid
     c                   eval      sfuser = l_user
     c                   time                    sfdate
     c                   time                    sftime
6.3q  *
6.3q  * Hvis betalt ordre i kassa, hent info fra evnt bestilling
6.3q  *
6.3q c                   if        bdornr <> 0
6.3q  *
6.3q c                   eval      fdesl1_numm = bdornr
6.3q c                   eval      fdesl1_suff = bdorsu
6.3q c                   eval      fdesl1_line = bdorli
6.3q c     fdesl1_key    chain     fdesl1
6.3q c                   if        %found
6.3q c                   if        fsbenr <> 0
6.3q c                   eval      sfbenr = fsbenr
6.3q c                   eval      sfbsuf = fsbsuf
6.3q c                   eval      sfblin = fsblin
6.3q  *
6.3q c                   eval      lodtlr_numm = fsbenr
6.3q c                   eval      lodtlr_suff = fsbsuf
6.3q c                   eval      lodtlr_line = fsblin
6.3q c     lodtlr_key    chain     lodtlr
6.3q c                   if        %found
6.3q c                   eval      sfldor = ldldor
6.3q c                   else
6.3q c                   eval      sihel3_aarr = %subdt(bofkda:*years)
6.3q c                   eval      sihel3_numm = fsbenr
6.3q c     sihel3_key    chain     sihel3
6.3q c                   if        %found
6.3q c                   eval      sfldor = shldor
6.3q c                   endif
6.3q c                   endif
6.3q c                   endif
6.3q c                   endif
6.3q  *
6.3q c                   endif
      *
      * Overstyring av felter, dersom varelinjen
      * er en vare hentet fra LVR
      *
     c                   if        bdlvre =  1
     c                   eval      sfogrp = bdogrp
     c                   eval      sfhgrp = bdhgrp
     c                   eval      sfugrp = bdugrp
     c                   eval      sfkpri = bdkpri
     c                   eval      sfenhe = bdenh1
     c                   eval      sfenho = bdenh1
6.3q c                   eval      sfnobb = bdnobb
6.3q c                   eval      sflvar = bdlvar
6.3q c                   eval      sfmodn = 0
6.3q  * Hent inn modul
6.3q c                   eval      jvarl1_vare = bdvare
6.3q c     jvarl1_key    chain     jvarl1
6.3q c                   if        %found
6.3q c                   eval      sfmodn = jvmodn
6.3q c                   endif
     c                   endif
      *
      * Finner alternativ varegruppe
      *
     c                   if        b_xeof = *on and
     c                             sfahgr = 0
     c                   exsr      finn_avgr
     c                   endif
      *
      * Henter inn produktansvarlig
      *
     c                   call      'VG710R'
     c                   parm                    w_firm
     c                   parm                    sfogrp
     c                   parm                    sfhgrp
     c                   parm                    sfugrp
     c                   parm                    w_pran
      *
     c                   eval      sfpran = w_pran
      *
6.3aac                   eval      sflmat = bolmat
     c                   eval      sfldat = bodate
     c                   eval      sfpdat = bodate
     c                   eval      sfseri = bdseri
     c                   eval      sfgave = bdgave
     c                   eval      sfksel = boselg
     c                   eval      sfkoda = bodate
     c                   eval      sfkoti = botime
     c                   eval      sfkous = bouser
      *
      * Oppdaterer pakkseddeldato og uke
      *
     c                   if        sfldat <> *loval
     c                   eval      w_dato = sfldat
     c                   else
     c                   eval      w_dato = sfbida
     c                   endif
     c                   eval      w_stat = *blank
      *
     c                   call      'AX711R'
     c                   parm                    w_dato
     c                   parm                    w_saar
     c                   parm                    w_suke
     c                   parm                    w_stat
      *
     c                   if        w_suke > 52
     c                   eval      w_suke = 52
     c                   endif
     c                   eval      sfpuke = w_suke
8.08 c                   eval      sfgjkp = w_gjkp
6.34  *
6.34  * utvidet statistikk
6.34  *
6.34 c                   if        b_bmmo = *on
6.34 c                   eval      sfpkon = w_pkon
6.34 c                   eval      sfpkns = w_pkns
6.34 c                   eval      sfpkko = w_pkko
6.34 c                   eval      sfpkok = w_pkok
6.34 c                   eval      sfpkin = w_pkin
6.34 c                   eval      sfpkoi = w_pkoi
6.34 c                   eval      sfkofa = w_kofa
6.34 c                   eval      sfcard = w_card
6.34 c                   eval      sffcar = w_fcar
6.34 c                   eval      sfkild = w_kild
6.34  *
6.34  * Sjekker om dette er en negativ ordre
6.34  *
6.34 c                   if        w_nega = 'K'
6.34 c                   eval      w_suno = w_suno * -1
6.34 c                   eval      w_suok = w_suok * -1
6.34 c                   eval      w_sumn = w_sumn * -1
6.34 c                   eval      w_sumi = w_sumi * -1
6.34 c                   eval      w_suoi = w_suoi * -1
6.34 c                   endif
6.34  *
6.34  * Sjekker om dette er en negativ linje
6.34  *
6.34 c                   if        valkre = '-'
6.34 c                   eval      w_suno = w_suno * -1
6.34 c                   eval      w_suok = w_suok * -1
6.34 c                   eval      w_sumn = w_sumn * -1
6.34 c                   eval      w_sumi = w_sumi * -1
6.34 c                   eval      w_suoi = w_suoi * -1
6.34 c                   endif
6.34  *
6.34 c                   eval      sfsuno = w_suno
6.34 c                   eval      sfsuok = w_suok
6.34 c                   eval      sfsumn = w_sumn
6.34 c                   eval      sfsumi = w_sumi
6.34 c                   eval      sfsuoi = w_suoi
6.34 c                   eval      sfsoda = w_soda
6.34 c                   eval      sfsoti = w_soti
6.34 c                   eval      sffsys = 'KAS'
6.3n  *
6.3n  * Er dette ei linje kjøpt med kupong ??
6.3n  *
6.3n c                   eval      bckuiu_bong = bdbong
6.3n c                   eval      bckuiu_line = bdline
6.3n c     bckuiu_key    chain     bckuiur
6.3n c                   if        %found
6.3n c**                 if        klstat = 0
6.3n c                   eval      sfsuno = bdsapr
6.3n c                   eval      sfpkon = 'KUP'
6.3n c                   eval      sfpkns = 'KUP'
6.3n c                   eval      sfkamp = 'KUP'
6.3n c                   eval      klstat = 1
6.3n c                   update    bckuiur
6.3n c**                 endif
6.3n c                   endif
6.34 c                   endif
      *
     c                   write     sstapfr
8.12  *
8.12 c                   if        u_812 = *on
8.12        exec sql
8.12        insert into sovkst
8.12          (sovfir,sovtyp,sovaar,sovbil,
8.12           sovbli,sovdat,sovopd)
8.12        values(:sffirm,'SHOP',:sfpaar,:sfbinr,
8.12               :sfline,:sfbida,' ');
8.12 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som finner alternativ varegruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_avgr     begsr
      *
      * Leser på varegruppeknytning
      * Leter først etter knytninger mot leverandør
      *
     c                   eval      vvgkl1_ogrp = sfogrp
     c                   eval      vvgkl1_hgrp = sfhgrp
     c                   eval      vvgkl1_ugrp = sfugrp
     c                   eval      vvgkl1_ldor = sfldor
      *
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = sfldor
     c                   eval      vvgkl1_ugrp = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = sfldor
     c                   eval      vvgkl1_hgrp = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        *in91 = *off
     c                   eval      sfahgr = vkahgr
     c                   eval      sfaugr = vkaugr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om bong skal behandles.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_bong    begsr
      *
     c                   eval       b_tsth = *on
7.06 c                   if        s_bila  <> 0
7.06 c                   eval      w_bila = s_bila
7.06 c                   eval      s_bila = 0
7.06 c                   endif
      *
      * Er dette en pakkseddel/plukkliste?
      *
     c                   if        bsoakk < 3
     c                   goto      end_sjkb
     c                   end
      *
      * Hent opplysninger fra BONG-HODE-REGISTER
      *
     c                   eval      bohelu_aarr = bsoaar
     c                   eval      bohelu_wsid = bsowsd
     c                   eval      bohelu_numm = bsonum
     c     bohelu_key    chain     bohelur                            90
     c                   unlock    bohelu
      *
      * Er denne Bong kjørt ut før ?
      *
     c                   if        bokavs <> 2
     c                   goto      end_sjkb
     c                   endif
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r
      *
      * Skal denne ordretypen oppdatere Økonomi og/eller statistikk
      *
     c                   if        vaooko = 0 and
     c                             vaosta = 0
     c                   goto      end_sjkb
     c                   endif
7.06  *
7.06  * Hvis Santander/Payex/Vipps kortbetaling, skal det dannes reskontropost
7.06  * mot kortselskap. Reskontronummer finnes fra betalinskort styring.
7.06  * Hent bilagsnummer, poster, oppdater med KID, slik at det kan
7.06  * krysses av ved OCR innbetaling.
7.06  *
7.06  * Er dette en bong som er betalt med Santander kort og KID ?
7.06  *
7.06 c                   eval      b_sant = *off
7.07 c                   eval      b_payx = *off
7.09 c                   eval      b_vips = *off
7.06  *
7.06 c                   eval      brefl1_aarr = boaarr
7.06 c                   eval      brefl1_bong = bobong
7.06 c     brefl1_key    chain     brefl1
7.05 c                   if        %found
7.05  *
7.05 c                   if        bxktyp = '98'
7.05 c                   eval      bokoir_ktyp = bxktyp
7.05 c     bokoir_key    chain     bokoir
7.05 c                   if        %found      and
7.05 c                             byresk <> 0 and
7.05 c                             bybinu <> *blanks
7.05 c                   eval      b_sant = *on
7.05  *
7.05  * Finn bilagsnummer (Hentes fra en egen serie)
7.05  *
7.05 c                   eval      w_fell = bybinu
7.05 c                   eval      w_type = *blanks
7.05 c                   eval      w_fast = *blanks
7.05 c                   exsr      hntnum
7.05 c                   eval      s_bila = w_bila
7.05 c                   eval      w_bila = w_numm
7.05  *
7.05 c                   endif
7.05 c                   endif
      *
7.07  * payx
7.07 c                   if        bxktyp = '97'
7.07 c                   eval      bokoir_ktyp = bxktyp
7.07 c     bokoir_key    chain     bokoir
7.07 c                   if        %found      and
7.07 c                             byresk <> 0 and
7.07 c                             bybinu <> *blanks
7.07 c                   eval      b_payx = *on
7.07  *
7.07  * Finn bilagsnummer (Hentes fra en egen serie)
7.07  *
7.07 c                   eval      w_fell = bybinu
7.07 c                   eval      w_type = *blanks
7.07 c                   eval      w_fast = *blanks
7.07 c                   exsr      hntnum
7.07 c                   eval      s_bila = w_bila
7.07 c                   eval      w_bila = w_numm
7.07  *
7.07 c                   endif
7.07 c                   endif
7.09  * Vipps
7.09 c                   if        bxktyp = '96'
7.09 c                   eval      bokoir_ktyp = bxktyp
7.09 c     bokoir_key    chain     bokoir
7.09 c                   if        %found      and
7.09 c                             byresk <> 0 and
7.09 c                             bybinu <> *blanks
7.09 c                   eval      b_vips = *on
7.09  *
7.09  * Finn bilagsnummer (Hentes fra en egen serie)
7.09  *
7.09 c                   eval      w_fell = bybinu
7.09 c                   eval      w_type = *blanks
7.09 c                   eval      w_fast = *blanks
7.09 c                   exsr      hntnum
7.09 c                   eval      s_bila = w_bila
7.09 c                   eval      w_bila = w_numm
7.09  *
7.09 c                   endif
7.09 c                   endif
      *
7.07 c                   endif
      *
     c                   eval      b_tsth = *off
      *
     c     end_sjkb      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om bong skal behandles.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_linje   begsr
      *
     c                   eval       b_tstl = *on
      *
      * Hent opplysninger fra BUTIKK-BONG-LINJE-REGISTER
      *
     c                   eval      bodtl1_aarr = bsoaar
     c                   eval      bodtl1_wsid = bsowsd
     c                   eval      bodtl1_numm = bsonum
     c                   eval      bodtl1_line = bsolin
     c     bodtl1_key    chain     bodtl1r                            90
      *
      * Tar vare på linjenummer
      *
     c                   eval      w_bili = bdline
      *
      * Hent opplysninger fra LINJETYPE-REGISTER
      *
     c                   eval      vltyl1_ltyp = bdltyp
     c     vltyl1_key    chain     vltyl1r                            90
      *
      *  Er dette en tekstlinje?
      *
     c                   if        valktx <> 0
     c                   goto      end_sjkl
     c                   endif
8.11  *
8.11  *  Er dette en tekstlinje m/feil linjetype??
8.11  *
8.11 c                   if            bdanta = 0
8.11 c                             and bdoppr = 0
8.11 c                   goto      end_sjkl
8.11 c                   endif
      *
      * Skal denne linjetypen oppdatere Økonomi og/eller statistikk
      *
     c                   if        valoko = 0 and
     c                             valsta = 0
     c                   goto      end_sjkl
     c                   endif
      *
     c                   eval      b_tstl = *off
      *
     c     end_sjkl      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn bilagslistenummer fra NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_bilag    begsr
      *
     c                   if        baabil = 1
     c                   eval      w_fell = baabnr
7.03 c*                   if        w_1_nr_pr_bong <> 'J'
7.03 c*                   eval      w_fell = baabnr
7.03 c*                   endif
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   exsr      hntnum
     c                   eval      bilanr = w_numm
     c                   endif
      *
     c                   if        bilanr = 0
     c                   eval      bilanr = d_lavs
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_kode = '0'
      *
      * Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
7.03 c**                  call      'AS100R'
7.03 c**                  parm                    w_kode
7.03 c**                  parm                    w_firm
7.03 c**                  parm                    w_fell
7.03 c**                  parm                    w_type
7.03 c**                  parm                    w_fast
7.03 c**                  parm                    w_numm
7.03 c*     nyttnr        tag
7.03 c*                   eval      anuml1_type = w_type
7.03 c*                   eval      anuml1_fast = w_fast
7.03 c*                   eval      anuml1_fell = w_fell
7.03 c*     anuml1_key    chain     anuml1
7.03  **    Dersom teller ikke finnes
7.03 c*                   if        not %found(anuml1)
7.03 c*                   eval      anfirm = w_firm
7.03 c*                   eval      anfell = w_fell
7.03 c*                   eval      antype = w_type
7.03 c*                   eval      anfast = w_fast
7.03 c*                   eval      antxt1 = 'Teller for Kassebilag +
7.03 c*                                       pr bong'
7.03 c*                   eval      antxt2 = 'Aut. fra 50000000 '
7.03 c*                   eval      anwrap = '0'
7.03 c*                   eval      anÅrnu = '0'
7.03 c*                   eval      anleng = 0
7.03 c*                   eval      anauto = 'A'
7.03 c*                   eval      ansist = 50000000
7.03 c*                   eval      annfom = ansist
7.03 c*                   eval      anntom = 80000000
7.03 c*                   write     anuml1r
7.03 c*                   goto      nyttnr
7.03 c*                   endif
7.03  **   Dersom nytt år starter teller på nytt fra åå000000
7.03 c**  Endret slik     eval      w_saf_aar = ansist / 1000000
7.03 c**  teller bare     if        w_saf_aar <> uyear
7.03 c**  fortsetter på   eval      ansist = uyear * 1000000
7.03 c**  50000000 serien eval      annfom = ansist
7.03 c**                  eval      anntom = uyear * 1000000 + 999999
7.03 c**                  update    anuml1r
7.03 c**                  goto      nyttnr
7.03 c**                  endif
7.03  **   Tell opp teller med 1
7.03 c*                   eval      ansist = ansist + 1
7.03 c*                   update    anuml1r
7.03 c*                   eval      w_numm = ansist
7.03 c*
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Snu dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     snudat        begsr
      *
     c                   movel     subdat        subdag
     c                   move      subdat        subÅr
     c                   movel     subÅr         subdat
     c                   move      subdag        subdat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Depositumbehandling - legger ut reskontropost for depositum
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_depo      begsr
      *
7.01 c*                  eval      fohel1_numm = bdornr
7.01 c*                  eval      fohel1_suff = bdorsu
7.01 c*    fohel1_key    chain     fohel1r
7.01 c                   eval      sdepl1_numm = bdornr
7.01 c                   eval      sdepl1_kund = bokund
7.01 c     sdepl1_key    chain     sdepl1r
     c                   if        %found
     c                   eval      w_belp = bdsums
     c                   eval      w_depo = w_depo + w_belp
     c                   eval      w_tots = w_tots + bdsums
     c                   eval      w_bkod = vaobil
      *
7.fb c                   eval      w_texd = 'Delbet.O/ ' +%char(bdornr)
      *
     c                   eval      w_valg = 0
     c                   eval      w_kkod = *blank
      *
7.fb c                   if        faafbk <> 0
7.fb c                   eval      w_bkod = faafbk
7.fb c                   else
     c                   eval      w_bkod = baabin
7.fb c                   endif
     c                   eval      w_kkod = ' '
      *
      *
     c                   eval      w_mkod = '0'
7.01 c**                 eval      w_kont = fokund
7.01 c                   eval      w_kont = skkund
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
     c                   eval      w_mvag = 0
      *
     c                   exsr      exekon
     c                   endif
      *
7.fb c                   eval      w_texd = *blank
      *
     c                   endsr
      *
6.3m  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3m  * Innbetaling - legger ut reskontropost for innbetalt faktura
6.3m  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3m  *
6.3m c     lag_innbet    begsr
6.3m  *
6.3m c                   eval      w_belp = bdsums
6.3m c                   eval      w_innb = w_innb + w_belp
6.3m c                   eval      w_tots = w_tots + bdsums
6.3m  *
6.3m c                   eval      w_valg = 0
6.3m c                   eval      w_kkod = *blank
6.3m c                   eval      w_bkod = baabin
6.3m  *
6.3m c                   eval      w_mkod = '0'
6.3m c                   eval      w_fakn = 0
6.3m c                   eval      w_kont = bokund
6.3m c                   eval      w_avde = 0
6.3m c                   eval      w_spes = 0
6.3m c                   eval      w_mvag = 0
6.3m c                   eval      w_leng = 0
6.3m c                   eval      w_leng = %len(%trim(bdtek1))
6.3m  *
6.3m c                   if        w_leng = 8
6.3m c                   eval      w_len8 = %subst(bdtek1:1:8)
6.3m c                   move      w_len8        w_fakn
6.3m c                   elseif    w_leng = 7
6.3m c                   eval      w_len7 = %subst(bdtek1:1:7)
6.3m c                   move      w_len7        w_fakn
6.3m c                   elseif    w_leng = 6
6.3m c                   eval      w_len6 = %subst(bdtek1:1:6)
6.3m c                   move      w_len6        w_fakn
6.3m c                   elseif    w_leng = 5
6.3m c                   eval      w_len5 = %subst(bdtek1:1:5)
6.3m c                   move      w_len5        w_fakn
6.3m c                   elseif    w_leng = 4
6.3m c                   eval      w_len4 = %subst(bdtek1:1:4)
6.3m c                   move      w_len4        w_fakn
6.3m c                   elseif    w_leng = 3
6.3m c                   eval      w_len3 = %subst(bdtek1:1:3)
6.3m c                   move      w_len3        w_fakn
6.3m c                   elseif    w_leng = 2
6.3m c                   eval      w_len2 = %subst(bdtek1:1:2)
6.3m c                   move      w_len2        w_fakn
6.3m c                   elseif    w_leng = 1
6.3m c                   eval      w_len1 = %subst(bdtek1:1:1)
6.3m c                   move      w_len1        w_fakn
6.3m c                   endif
6.3m  *
6.3m c                   exsr      exekon
6.3m  *
6.3m c                   endsr
6.34  *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Henter utvidet statstikk-data
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 csr   utvid_stat    begsr
6.34  *
6.34 c                   eval      w_pkon = *blank
6.34 c                   eval      w_pkns = *blank
6.34 c                   eval      w_pkko = *blank
6.34 c                   eval      w_pkok = *blank
6.34 c                   eval      w_pkin = *blank
6.34 c                   eval      w_pkoi = *blank
6.34 c                   eval      w_kofa = *blank
6.34 c                   eval      w_card = *blank
6.34 c                   eval      w_fcar = *blank
6.34 c                   eval      w_kild = *blank
6.34 c                   eval      w_sumn = 0
6.34 c                   eval      w_sumi = 0
6.34 c                   eval      w_suoi = 0
6.34 c                   eval      w_suno = 0
6.34 c                   eval      w_suok = 0
6.34 c                   eval      w_suoi = 0
6.34 c                   eval      w_ntpr = 0
6.34 c                   eval      w_rkro_stk = 0
6.34  *
6.34 c                   eval      hifirm = w_firm
6.34 c                   eval      hirkat = 0
6.34  *
6.34 c                   eval      hikund = bokund
6.34 c                   if        bolkun <> 0
6.34 c                   eval      hikund = bolkun
6.34 c                   endif
6.34  *
6.34 c                   eval      hikpro = bokpro
6.34 c                   eval      hivare = vvvare
6.34 c                   eval      hienhe = bdenh1
6.34 c                   eval      hilety = bdlety
6.34 c                   eval      hidekn = 0
6.34 c                   eval      hiavgf = bomkod
6.34 c                   eval      hipriv = 0
6.34 c                   eval      hidato = bobdat
6.34 c                   if        bobdat = bodate
6.34 c                   eval      hitime = botime
6.34 c                   else
6.34 c                   eval      hitime = *loval
6.34 c                   endif
6.34 c                   eval      w_soda = bodate
6.34 c                   eval      w_soti = botime
6.34 c                   eval      hinumm = bopord
6.34 c                   eval      hikode = *blank
6.34 c                   eval      hiskaf = *off
6.34 c                   eval      hildor = bdldor
6.34 c                   eval      hisapr = 0
6.34 c                   eval      hikopr = 0
6.34 c                   eval      hiinpr = 0
6.34 c                   eval      hidekn = 0
6.35ac                   eval      hiinlr = *off
6.34
6.34 c                   call      'VP905R'
6.34 c                   parm                    hirec
6.34 c                   parm                    horec
6.34  *
6.34  * Koder fra VP905
6.34  *
6.34 c                   eval      w_pkon = hopkon
6.34 c                   eval      w_pkns = hopkns
6.34 c                   eval      w_pkko = hopkko
6.34 c                   eval      w_pkok = hopkok
6.34 c                   eval      w_pkoi = hopkoi
6.34  *
6.34  * Sum opprinnelig netto salgspris (SFSUNO) -----------------------
6.34  *
6.34 c                   eval      w_ntpr = hosapr
6.34  *
6.34 c                   if        horab1 > 0
6.34 c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab1 / 100)
6.34 c                   endif
6.34 c                   if        horab2 > 0
6.34 c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab2 / 100)
6.34 c                   endif
6.34  * trekker fra ordrerabatt
6.34 c                   if        bdorab <> 0
6.34 c                   eval      w_rkro_stk = w_ntpr * bdorab/100
6.34 c                   endif
6.34 c                   eval      w_ntpr = w_ntpr - w_rkro_stk
6.34  *
6.34 c                   eval(h)   w_suno = w_ntpr * bdanta
6.34  *
6.34  * Sum netto salg salgspris (SFSUMN)
6.34  *
6.3r c                   if        w_nega = 'K'
6.3r c                   eval(h)   w_sumn = bdsums + bdrbe1
6.3r c                                             + bdrbe2
6.3r c                   else
6.34 c                   eval(h)   w_sumn = bdsums - bdrbe1
6.34 c                                             - bdrbe2
6.3r c                   endif
6.34  *
6.34  * Priskode netto salgspris  (SFPKNS)
6.34  *
6.34 c                   if        w_sumn <> 0
6.34 c                   eval      w_diff  = %abs(w_suno - w_sumn)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkns  = 'MAN'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Sum  kostpris (SFSUMK)
6.34  *
6.34 c                   eval      wwsumk = bdkopr * bdanta
6.34  *
6.34  * Sum opprinnelig kostpris (SFSUOK)
6.34  *
6.34 c                   eval      w_suok = hokopr * bdanta
6.34  *
6.34  * Priskode netto kostpris  (SFPKKO)
6.34  *
6.34 c                   if        w_suok <> 0
6.34 c                   eval      w_diff  = %abs(wwsumk - w_suok)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkko  = 'MAN'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Sum  innkjøps (SFSUMI) - bruker her opprinnelig, har ikke annet
6.34  *
6.34 c                   eval      w_sumi = hoinpr * bdanta
6.34  *
6.34  * Sum  opprinnelig innkjøps (SFSUOI)
6.34  *
6.34 c                   eval      w_suoi = hoinpr * bdanta
6.34  *
6.34  * Priskode netto innkjøpspris  (SFPKIN)
6.34  *
6.34 c                   if        hoinpr <> 0
6.34 c                   eval      w_diff  = %abs(w_sumi - w_suoi)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkin  = 'XXX'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Kilde Salg      (SFKILD)
6.34  *
6.34 c                   eval      w_kild = 'KAS'
6.34  *
6.34  * BM Card (SFCARD)
6.34  *
6.34 c                   eval      bokkl1_bong = bobong
6.34 c     bokkl1_key    setll     bokkl1
6.34 c     bokkl1_key    reade     bokkl1
6.34 c                   dow       not %eof(bokkl1)
6.34 c                   if        brktyp <> 0 and
6.34 c                             brktyp =  5
6.34 c                   eval      w_card = '05'
6.34 c                   endif
6.34 c                   if        brktyp <> 0 and
6.34 c                             brktyp =  6
6.34 c                   eval      w_fcar = '06'
6.34 c                   endif
6.23 c     bokkl1_key    reade     bokkl1
6.34 c                   enddo
6.34  *
6.34  *
6.34  * Kontakt/fakturert (SFKOFA)
6.34  *
6.34 c                   if        bootyp = 'CD' or
6.34 c                             bootyp = 'CK'
6.34 c                   eval      w_kofa = 'K'
6.34 c                   else
6.34 c                   eval      w_kofa = 'F'
6.34 c                   endif
6.34 c
6.34  *
6.34 csr                 endsr
      *
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  * Kortpostering - lik postering for Santander/Payex/Vipps (for øyeblikket
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  *
7.09 c     pos_kort      begsr
7.09  *
7.09 c                   eval      w_kkod = *blank
7.09  *
7.09 c                   if        boresk <> 0
7.09 c                   eval      w_belp = boresk * -1
7.09 c                   else
7.09 c                   eval      w_belp = bobkrt * -1
7.09 c                   endif
7.09 c                   eval      w_valg = 0
7.09 c                   eval      w_bkod = bybilk
7.09  *
7.09 c                   eval      w_mkod = '0'
7.09 c                   eval      w_spes = 0
7.09 c                   eval      w_avde = 0
7.09 c                   eval      w_mvag = 0
7.09  *
7.09  * Selve tildelingen av bilagsnumnmer er flyttet. Skal gjelde hele bongen.
7.09  *
7.09  * Bytt ut kundenummer som skal til reskontro
7.09  *
7.09 c                   eval      s_kund = w_kund
7.09 c                   eval      w_kund = byresk
7.09 c                   eval      w_kont = byresk
7.09  *
7.09  * Legg ut AKIDPF med nok info til å identifisere OCR innlesing
7.09  * (Bygg "Lang KID")
7.09  *
7.09 c                   eval      d_kkli = w_firm
7.09 c                   eval      d_kkod = 0
7.09 c                   eval      d_kbil = w_numm
7.09 c                   eval      d_kund = byresk
7.09 c                   move      d_kkus        w_kkus
7.09 c                   eval      w_ksif = 0
7.09  *
7.09  * Beregning av sjekksiffer på kiddfelt (lang versjon)
7.09  *
7.09 c                   call      'AK720R'
7.09 c                   parm                    w_kkus
7.09 c                   parm                    w_ksif
7.09  *
7.09 c                   eval      d_kmod = w_ksif
7.09  *
7.09  * Oppdatering av kidd-referanse-fil
7.09  *
7.09 c                   eval      w_kkid = d_kkid
7.09 c                   eval      w_refk = %trim(bxkidr)
7.09 c                   move      w_refk        w_kki2
7.09  *
7.09 c                   call      'AK700R'
7.09 c                   parm                    w_firm
7.09 c                   parm                    w_kkid
7.09 c                   parm                    w_kidr
7.09 c                   parm                    w_ksif
7.09 c                   parm                    w_kki2
7.09  *
7.09  * Legge ut post med Santander som skylder
7.09  *
7.09 c                   exsr      exekon
7.09  *
7.09  * Oppdater BREF med bilagsnummer
7.09  *
7.09 c                   eval      breflu_aarr = boaarr
7.09 c                   eval      breflu_bong = bobong
7.09 c     breflu_key    chain     breflu
7.09 c                   if        %found
7.09 c                   eval      bxbiln = w_bila
7.09 c                   time                    bxedat
7.09 c                   time                    bxetim
7.09 c                   eval      bxeusr = l_user
7.09 c                   update    breflur
7.09 c                   endif
7.09  *
7.09 c                   eval      w_kund = s_kund
7.05 c**7.06             eval      w_bila = s_bila
7.09  *
7.09 c                   endsr
7.09  *
7.11  *
7.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.11  * Legger ut posteringstrans til loggfil linjer
7.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.11  *
7.11  /Free
7.11   begsr logg_linjer;
7.11
7.11     w_lintel = w_lintel + 1;
7.11     w_dumkki = 0;
7.11     w_dumkkk = 0;
8.10     w_dumd20 = 0;
7.11     w_dumaki = '  ';
7.11     w_dumpru = '  ';
7.11
7.11        exec sql
7.11        insert into rlodst
7.11          (rldfir,rldtyp,rldts1,rldlin,rldper,
7.11           rldbun,rldbin,rldbli,rldbda,rldfda,
7.11           rldbko,rldbtx,rlddav,rlddko,rlddsp,
7.11           rlddmv,rldbel,rldmvg,rldkav,rldkko,
7.11           rldksp,rldkmv,rldpro,rldpra,rldpru,
7.11           rldpao,rldkrf,rldres,rldlki,rldkki,
7.11           rldkkk,rldaki,rldvko,rldvbe,rldmko,
8.10           rldmen,rldsal,rldsfa,rldtty,rldbet,rldrem)
7.11        values(:bffirm,'SHOP',:w_logg,:w_lintel,
7.11               :bfperi,:bfbunt,:bfbiln,:bfbill,
7.11               :bfbdat,:bffdat,:bfbilk,:bftext,
7.11               :bfdavd,:bfdkto,:bfdsps,:bfdmom,
7.11               :bfbelp,:bfmvag,:bfcavd,:bfckto,
7.11               :bfcsps,:bfcmom,:bfproj,:bfakti,
7.11               :w_dumpru,:w_dumpru,:bfkref,:bfkund,
7.11               :w_dumaki,:w_dumkki,:w_dumkkk,:w_dumaki,
7.11               :bfvalk,:bfvalb,:bfmngk,:bfmngd,
8.10               :bfautx,' ',' ',:w_dumd20,' ');
7.11
7.11   endsr;
7.11  /END-FREE
7.13  *
7.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.13  * Legger ut post til loggfil hode
7.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.13  *
7.13  /FREE
7.13   begsr logg_bilag;
7.13
7.13     w_sumbel = 0;
7.13     w_dumtim = *loval;
7.13     w_dumsum = 0;
7.13     w_dumant = 0;
7.13     w_dumusr = '  ';
7.13     w_dumerr = '  ';
7.13
7.13        exec sql
7.13         insert into rlobst
7.13          (RLBFIR, RLBBIL, RLBTS1,RLBAN1,RLBSD1,RLBSK1,RLBBR1,
7.13           RLBTS2,RLBAN2,RLBSD2,RLBSK2,RLBBR2,RLBFF2,RLBFT2,
7.13           RLBTS3,RLBAN3,RLBSD3,RLBSK3,RLBBR3,RLBFF3,RLBFT3,
7.13           RLBTS4,RLBAN4,RLBSD4,RLBSK4,RLBBR4,RLBFF4,RLBFT4,
7.13           RLBTS5, RLBBR5, RLBTS6, RLBBR6)
7.13           select
7.13            bffirm, bfbiln, :w_logg, count(*) ,
7.13            sum(bfbelp)/2 ,sum(bfbelp)/2, :l_user,
7.13            :w_dumtim, :w_dumant, :w_dumsum, :w_dumsum,
7.13            :w_dumusr, ' ', ' ',
7.13            :w_dumtim, :w_dumant, :w_dumsum, :w_dumsum,
7.13            :w_dumusr, ' ', ' ',
7.13            :w_dumtim, :w_dumant, :w_dumsum, :w_dumsum,
7.13            :w_dumusr, ' ', ' ',
7.13            :w_dumtim, :w_dumusr, :w_dumtim, :w_dumusr
7.13          from bovfpf
8.02          where bffirm = :w_firm and bflogg = :w_logg
7.13          group by bffirm,bfbiln
7.13          order by bfbiln ;
           if sqlcod < 0;
           endif;
7.13   endsr;
7.13  /END-FREE
7.11  *
7.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.11  * Legger ut post til loggfil hode
7.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.11  *
7.11  /FREE
7.11   begsr logg_hode;
7.11
7.11     w_sumbel = 0;
7.11     w_dumtim = *loval;
7.11     w_dumsum = 0;
7.11     w_dumusr = '  ';
7.11     w_dumerr = '  ';
7.11
8.14        exec sql
8.14          select sum(rlbsd1), sum(rlbsk1)
8.14            into :w_dsum, :w_ksum
8.14             from rlobst
8.14             where rlbts1 = :w_logg;
8.14
8.14        w_sumbel = (w_dsum + w_ksum) / 2;
7.11
7.11        if w_sumbel > 0;
7.11        exec sql
7.11        insert into rlohst
7.11          (rlhfir,rlhtyp,rlhts1,rlhbs1,rlhbr1,
7.11           rlhts2,rlhbs2,rlhbr2,rlhfl2,
7.11           rlhts3,rlhbs3,rlhbr3,rlhfl3,
7.11           rlhts4,rlhbs4,rlhbr4,rlhfl4,
7.11           rlhts5,rlhbr5,
7.11           rlhts6,rlhbr6)
7.11        values(:w_firm,'SHOP',:w_logg,:w_sumbel,:l_user,
7.11               :w_dumtim,:w_dumsum,:w_dumusr,:w_dumerr,
7.11               :w_dumtim,:w_dumsum,:w_dumusr,:w_dumerr,
7.11               :w_dumtim,:w_dumsum,:w_dumusr,:w_dumerr,
7.11               :w_dumtim,:w_dumusr,
7.11               :w_dumtim,:w_dumusr);
7.11
7.11        endif;
7.11   endsr;
7.11  /END-FREE
8.08  *
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  * Henter gj.sn. kostpris på utleverings tidspunkt
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  *
8.08 c     hent_gjkost   begsr
8.08  *
8.08  * Bruk kalk kost på noen leveringstyper og vare typer
8.08  *
8.08 c                   if        bdlety = 1 or
8.08 c                             bdlety = 5 or
8.08 c                             bdlety = 8
8.08 c                   eval      w_gjkp = w_sumk
8.08 c                   goto      end_gjkost
8.08 c                   endif
8.08  *
8.08 c                   if        sfvtyp = 'S' or
8.08 c                             sfvtyp = 'T' or
8.08 c                             sfvtyp = 'D'
8.08 c                   eval      w_gjkp = w_sumk
8.08 c                   goto      end_gjkost
8.08 c                   endif
8.08
8.08 c                   if        bobdat <> *loval
8.08 c                   eval      w_gjdat = %dec(bobdat:*iso)
8.08 c                   endif
8.08 c                   if        boldat <> *loval
8.08 c                   eval      w_gjdat = %dec(boldat:*iso)
8.08 c                   endif
8.08
8.08 c                   eval      w_gjko = 0
8.08 c                   if        w_gjdat > 0
8.08 c                   call      'VG701R'
8.08 c                   parm                    bdvare
8.08 c                   parm                    bdlage
8.08 c                   parm                    w_gjdat
8.08 c                   parm                    w_gjko
8.08 c                   else
8.08 c                   eval      w_gjkp = w_sumk
8.08 c                   goto      end_gjkost
8.08 c                   endif
8.08  *
8.08  * Sjekker om statistikk enhet avviker lagerenhet, regn om til statistikk enhet
8.08  *
8.08 c                   if        vvenhs <> vvenhl
8.08 c                   exsr      beregn_pris
8.08 c                   endif
8.08
8.08 c                   eval      w_gjkp = w_gjko * bdanta
8.08
8.08 c     end_gjkost    endsr
8.08  *
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  * Subrutine for beregning av gjennomsnitlig kostpris i statistikk enhet
8.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.08  *
8.08 c     beregn_pris   begsr
8.08  *
8.08  * Finn omregningsfaktor på statistikk enhet og beregn priser i forhold til basisenhet
8.08  *
8.08 c                   eval      vvenlr_vare = bdvare
8.08 c                   eval      vvenlr_enhe = vvenhs
8.08 c     vvenlr_key    chain     vvenlr
8.08 c                   if        %found(vvenlr)
8.08 c                   eval(h)   w_gjko = w_gjko * veomre
8.08 c                   endif
8.08  *
8.08  * Finn omregningsfaktor på lagerenhet og beregn priser dersom lagerenhet er ulik basisenhet
8.08  *
8.08 c                   if        vvenhe <> vvenhl
8.08 c                   eval      vvenlr_enhe = vvenhl
8.08 c     vvenlr_key    chain     vvenlr
8.08 c                   if        %found(vvenlr)
8.08 c                   eval(h)   w_gjko = w_gjko / veomre
8.08 c                   endif
8.08 c                   endif
8.08  *
8.08 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      bilanr = 0
      *
     c                   eval      w_text = *blank
      *
     c                   eval      w_valg = 0
     c                   eval      w_avde = 0
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
     c                   eval      w_mkod = *blank
     c                   eval      w_belp = 0
      *
     c                   eval      w_kode = *blank
     c                   eval      w_fell = *blank
7.03 c*                   if        w_1_nr_pr_bong <> 'J'
7.03 c*                   eval      w_fell = *blank
7.03 c*                   endif
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = 0
      *
     c                   move      0             w_hele
     c                   eval      w_ogrp = 0
     c                   eval      w_hgrp = 0
     c                   eval      w_ugrp = 0
     c                   eval      w_vare = *blank
     c                   eval      w_stat = *blank
     c                   eval      w_otyp = *blank
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : LINJETYPE-REGISTER
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for file : UNDER-GRUPPE-REGISTER
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les på varegruppeknytning
      *
     c     vvgkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl1_ogrp
     c                   kfld                    vvgkl1_hgrp
     c                   kfld                    vvgkl1_ugrp
     c                   kfld                    vvgkl1_ldor
      *
      * Key for file : BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : STATUS-KODER OFAK
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *                                                    SW
      * Key for file : BONG-HODE-REGISTER
      *
     c     bohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelu_aarr
     c                   kfld                    bohelu_wsid
     c                   kfld                    bohelu_numm
      *
      * Key for file : BONG-LINJE-REGISTER
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_numm
     c                   kfld                    bodtl1_line
      *
7.01  * Key for file : ORDRE-HODE-REGISTER
7.01  *
7.01 c*    fohel1_key    klist
7.01 c*                  kfld                    w_firm
7.01 c*                  kfld                    fohel1_numm
7.01 c*                  kfld                    fohel1_suff
      *
7.01  * Key for oppslag mot depositum-fil
7.01  *
7.01 c     sdepl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    sdepl1_numm
7.01 c                   kfld                    sdepl1_kund
      *
      * Key for lesing av bruksrett-type
      *
     c     rmbtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmbtlr_talm
     c                   kfld                    rmbtlr_tbrt
      *
      * Key for lesing av kortbetalings transaksjoner
      *
     c     bobli1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bobli1_kass
     c                   kfld                    bobli1_aarr
     c                   kfld                    bobli1_bong
      *
      * Key for les på korttype
      *
     c     bokoir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bokoir_ktyp
6.34  *
6.34  * Key for lesing av kort-trans
6.34  *
6.34 c     bokkl1_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    bokkl1_bong
6.34  *
6.34  * Key for les av moduler Nexstep
6.34  *
6.34 c     amodl1_key    klist
6.34 c                   kfld                    amodl1_modu
6.3n  *
6.3n  * Key for file : kundeklubb kuponger
6.3n  *
6.3n c     bckuiu_key    klist
6.3n c                   kfld                    w_firm
6.3n c                   kfld                    bckuiu_bong
6.3n c                   kfld                    bckuiu_line
6.3q  *
6.3q  * Key for file : bestilling
6.3q  *
6.3q c     lodtlr_key    klist
6.3q c                   kfld                    w_firm
6.3q c                   kfld                    lodtlr_numm
6.3q c                   kfld                    lodtlr_suff
6.3q c                   kfld                    lodtlr_line
6.3q  *
6.3q  * Key for file : slettet register
6.3q  *
6.3q c     fdesl1_key    klist
6.3q c                   kfld                    w_firm
6.3q c                   kfld                    fdesl1_numm
6.3q c                   kfld                    fdesl1_suff
6.3q c                   kfld                    fdesl1_line
6.3q  *
6.3q  * Key for file : historikk
6.3q  *
6.3q c     sihel3_key    klist
6.3q c                   kfld                    w_firm
6.3q c                   kfld                    sihel3_aarr
6.3q c                   kfld                    sihel3_numm
6.3q  *
6.3q  * Key for file : skaffevare
6.3q  *
6.3q c     jvarl1_key    klist
6.3q c                   kfld                    jvarl1_vare
7.03  **
7.03  ** Key for file : Nummerteller
7.03  **
7.03 c*     anuml1_key    klist
7.03 c*                   kfld                    w_firm
7.03 c*                   kfld                    anuml1_fell
7.03 c*                   kfld                    anuml1_type
7.03 c*                   kfld                    anuml1_fast
7.05  *
7.05  * Key for lesing av kortbetalings transaksjoner
7.05  *
7.05 c     brefl1_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    brefl1_aarr
7.05 c                   kfld                    brefl1_bong
7.05  *
7.05  * Key for lesing av kortbetalings transaksjoner
7.05  *
7.05 c     breflu_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    breflu_aarr
7.05 c                   kfld                    breflu_bong
7.fb  *
7.fb  * Key for file : STATUS-KODER OFAK
7.fb  *
7.fb c     fst2l1_key    klist
7.fb c                   kfld                    w_firm
8.06  *
8.06  * Key for oppslag på kunde-transer
8.06  *
8.06 c     rktrl8_key    klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    rktrl8_kund
8.06 c                   kfld                    rktrl8_biln
8.06  *
8.07  *
8.07  * Key for oppslag på Ordrehistorikk
8.07  *
8.07 c     sohel2_key    klist
8.07 c                   kfld                    w_firm
8.07 c                   kfld                    sohel2_biln
8.07  *
8.08  *  Key for oppdslag på vare/enhet
8.08  *
8.08 c     vvenlr_key    klist
8.08 c                   kfld                    w_firm
8.08 c                   kfld                    vvenlr_vare
8.08 c                   kfld                    vvenlr_enhe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Legger inn parametre i variablene
      *
     c                   eval      d_parm = p_parm
      *
      * Legger inn firmanummer i nøkkel
      *
     c                   eval      w_firm = l_firm
      *
      * Henter felles bilagsnummer for listen
      *
     c                   exsr      hent_bilag
7.03 c*                   if        w_1_nr_pr_bong <> 'J'
7.02 c*                   exsr      hent_bilag
7.03 c*                   endif
      *
      * Hent opplysninger fra BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    chain     bstsl1r                            90
     c                   if        *in90 = *on
     c                   eval      baaavs = *blank
     c                   eval      baadet = *blank
     c                   endif
      *
      * Hent opplysninger fra OFAK-STATUS-REGISTER
      *
     c     fstsl1_key    chain     fstsl1r                            90
7.fb c     fst2l1_key    chain     fst2l1r                            90
8.07  *
8.07  * Sjekk om Nexstep Økonomi skal oppdateres
8.07  *
8.07 c                   if        faak04 = 0
8.07 c                   eval      w_Nx_Øk = 'J'
8.07 c                   endif
      *
      * Snur periode før oppdatering av regnskap
      *
     c                   move      d_peri        woperi
     c                   eval      subdat = 0
     c                   move      woperi        subdat
     c                   exsr      snudat
     c                   movel     subdat        w_peri
6.34  *
6.34  * Sjekker om BM Modul
6.34  *
6.34 c                   eval      b_bmmo = *off
6.34 c                   eval      amodl1_modu = 'BM_MODUL'
6.24 c     amodl1_key    chain     amodl1
6.34 c                   if        %found(amodl1)
6.34 c                   eval      b_bmmo = *on
6.34 c                   endif
      *
      * Sjekker om det finnes poster i
      * knytningsregister for alternativ varegruppe
      *
     c                   eval      vvgkl1_ogrp = 0
     c                   eval      vvgkl1_hgrp = 0
     c                   eval      vvgkl1_ugrp = 0
     c                   eval      vvgkl1_ldor = 0
      *
     c     vvgkl1_key    setll     vvgkl1
     c                   read      vvgkl1
      *
     c                   if        %eof
     c                   eval      b_xeof = *off
     c                   else
     c                   eval      b_xeof = *on
     c                   endif
7.10  *
7.10  *  Sjekker om salg til glidende gjennomsnittlig kostpris skal føres
7.10  *
7.10 c                   eval      w_modu = 'GJKOST'
7.10 c                   call      'AX700R'
7.10 c                   parm                    w_modu
7.10 c                   parm                    w_disp
7.10 c                   parm                    w_tilg
7.10  *
7.10 c                   if        w_tilg = '0'
7.10 c                   eval      b_kost = *on
7.10 c                   endif
7.11  *
7.11  * Legger inn timestamp for logging
7.11  *
7.11 c                   time                    w_logg
8.05 c                   eval      l_tmst = %char(w_logg)
      *
8.03  *
8.03  * Skal butikksalg på kontantkunde posteres +/- ?
8.03  *
8.03   CallP CO402R(l_filg:w_firm:0:'IKKE_POSTERE_PÅ_KONTANTKUNDE':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     eval w_kontkunx = %subst(co402_verdi2:15:6);
8.03     eval w_postkont = %subst(co402_verdi2:22:1);
8.03   else;
8.03     w_postkont = *blank;
8.03   endif;
8.12  *
8.17  * Sjekk om det skal kjøres kostføring mot regnskap
8.12  *
8.17   CallP CO402R(l_filg:w_firm:0:'POST_KOST':
8.12                    co402_verdi1:co402_verdi2);
8.12   if co402_verdi1 = '1';
8.12     u_812 = *on;
8.12   endif;
      *
     c                   endsr
      *
** Ekstra bilags-tekster
 ********
 Avrunding
 Pliktig
 Fritt
 Gavekort
 Depositum
 Alm.rab.
 Rabatt
