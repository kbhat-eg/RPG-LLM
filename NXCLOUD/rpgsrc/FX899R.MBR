     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FX899R
      * Beskrivelse . . : FIX: Oppdaterer EAN-nummer på alle Jernia-varer
      *
      * Programmet leser total Jernia-fil og erstatter alle EAN-nummer
      * knyttet mot disse varene i med EAN-nummer fra Jernia-filen.
      * Dersom EAN-nummeret fra Jernia allerede finnes på en annen Jernia-
      * vare slettes dette fra den andre varen før det leses inn på den
      * varen som Jernia har oppgitt.
      *
      * NB! Hardkodet leverandørnummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280604 HKO  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk    rename(flvwpfr:flvwpfr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     f                                     prefix(xx:2)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
      *
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
     fveanl2    uf   e           k disk    rename(veanpfr:veanl2r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Jernia
      *
     d                 ds
     d d_inpu                       259
     d  d_ekod                        1    overlay(d_inpu)
     d  d_vare                       15    overlay(d_inpu:2)
     d  d_tek1                       30    overlay(d_inpu:17)
     d  d_enhe                        3    overlay(d_inpu:47)
     d  d_lvar                       20    overlay(d_inpu:50)
     d  d_ldor                        8    overlay(d_inpu:70)
     d  d_vgr1                        2    overlay(d_inpu:78)
     d  d_vgr2                        4    overlay(d_inpu:80)
     d  d_vgr3                        7    overlay(d_inpu:84)
     d**d_eann                       15  0 overlay(d_inpu:91)
     d  d_eann                       13  0 overlay(d_inpu:93)
     d  d_ean2                       15  0 overlay(d_inpu:106)
     d  d_ean3                       15  0 overlay(d_inpu:121)
     d  d_ean4                       15  0 overlay(d_inpu:136)
     d  d_nobb                        8    overlay(d_inpu:151)
     d  d_pend                        1    overlay(d_inpu:159)
     d  d_inpr                       10  2 overlay(d_inpu:160)
     d  d_saim                       10  2 overlay(d_inpu:170)
     d  d_antp                       10  0 overlay(d_inpu:180)
     d  d_akod                        1    overlay(d_inpu:190)
     d  d_sort                        4    overlay(d_inpu:191)
     d  d_evar                       15    overlay(d_inpu:195)
     d  d_eean                       15  0 overlay(d_inpu:210)
     d  d_enob                        8    overlay(d_inpu:225)
     d  d_raba                        5  2 overlay(d_inpu:233)
     d  d_fdat                        6  0 overlay(d_inpu:238)
     d  d_tdat                        6  0 overlay(d_inpu:244)
     d  d_crdo                       10    overlay(d_inpu:250)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvarl5_ldor     s                   like(vvldor)
     d vvarl5_lvar     s                   like(vvlvar)
     d veanlu_eann     s                   like(vxeann)
     d veanl1_eann     s                   like(vxeann)
     d veanl2_vare     s                   like(vxvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_ldor          s                   like(vvldor)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_ldor = 600037
      *
      * Leser Jernia totalfil
      *
     c                   read      flvwpf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = fwirad
      *
     c                   if        d_vare = *blank or
     c                             d_eann = 0
     c                   goto      neste_vare
     c                   endif
      *
     c                   eval      vvarl5_ldor = w_ldor
     c                   eval      vvarl5_lvar = d_vare
     c     vvarl5_key    chain     vvarl5
     c                   if        not %found
     c                   goto      neste_vare
     c                   endif
      *
      * Sletter gamle EAN-nr
      *
     c                   eval      veanl2_vare = vvvare
     c     veanl2_key    setll     veanl2
     c     veanl2_key    reade     veanl2r
     c                   dow       not %eof
     c                   delete    veanl2r
     c     veanl2_key    reade     veanl2r
     c                   enddo
      *
      * Sletter samme EAN-nummer på eventuelt annen Jernia-vare
      *
     c                   eval      veanl1_eann = d_eann
     c     veanl1_key    setll     veanl1
     c     veanl1_key    reade     veanl1
     c                   dow       not %eof
      *
     c                   eval      vvarl1_vare = vxvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found and
     c                             xxldor = w_ldor
     c                   eval      veanlu_eann = d_eann
     c     veanlu_key    chain     veanlur
     c                   delete    veanlur
     c                   endif
      *
     c     veanl1_key    reade     veanl1
     c                   enddo
      *
      * Oppretter EAN-nummer på vare
      *
     c     veanl1_key    chain     veanl1
     c                   if        not %found
     c                   clear                   veanlur
     c                   eval      vxfirm = w_firm
     c                   eval      vxeann = d_eann
     c                   eval      vxvare = vvvare
6.10 c                   time                    vxodat
6.10 c                   time                    vxotim
6.10 c                   eval      vxousr = l_user
     c                   time                    vxtime
     c                   time                    vxetim
     c                   time                    vxedat
     c                   eval      vxeusr = l_user
     c                   write     veanlur
     c                   endif
      *
     c     neste_vare    tag
      *
     c                   read      flvwpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl5_ldor
     c                   kfld                    vvarl5_lvar
      *
      * Key for oppslag på EAN
      *
     c     veanl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl1_eann
      *
      * Key for oppdatering av EAN
      *
     c     veanlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlu_eann
      *
     c     veanl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl2_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
