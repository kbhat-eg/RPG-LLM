     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EK100R
      * Beskrivelse . . : Kunde bonus, vedlikehold - parameter (liste)
      *
      *                   Velger kunde fra liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201005 HKO  Nytt program
5.10  *       120208 ASK  Viser bare bonusberettigete kundekat 310/314
5.10  *       291208 ASK  Lagt inn masseoppdatering av bonusparametre
5.10  *       291208 ASK  Lagt inn lagring av bonusparametre
5.10  *       291208 ASK  Lagt inn bonusberegnings rutiner
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
9.01  *       040310 bof  Takle flere bonusgrupper (inntil 99)
6.11  * 6.10  100811 BSA  Tar hensyn til bonustype ved oppdatering
6.12  * 6.10  210911 BSA  Lager ny type kundebrev. Reversert Degernes
6.12  *                   tilpasninger.
6.13  * 6.10  280911 BSA  Finner kalkulert bonus fra bonustype register
6.14  * 6.10  020312 BSA  Utskrift av kundebrev med printerstyring
6.20  * 6.20  080513 BSA  Legger til periode for bonusberegning
6.21  * 6.20  261113 BSA  Legger til fraperiode for bonusberegning
6.22  * 6.20  150916 BSA  Hardkoder popup, da den legger seg i subfile.
6.22  *                   Skaper problemer for jacada. (NB Kun DDS)
6.30  * 6.30  080617 BSA  Kopierer ikke med seg alle nivåer.
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  301020 bkv  Feil antall i subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunl2    if   e           k disk    rename(rkunpfr:rkunl2r)
     fekboi1    if   e           k disk    rename(ekbost:ekboi1r)
6.13 fekbtir    if   e           k disk    rename(ekbtst:ekbtirr)
6.30 fekbtiu    uf a e           k disk    rename(ekbtst:ekbtiur)
     fekbei1    if   e           k disk    rename(ekbest:ekbei1r)
     fekboiu    uf a e           k disk    rename(ekbost:ekboiur)
     fekbeiu    uf a e           k disk    rename(ekbest:ekbeiur)
      *
     fek100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d  l_dato               101    108
     d  l_tek1               201    270
     d  l_tek2               301    370
     d  l_tek3               401    470
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rkunlr_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
     d rkunl2_alfa     s                   like(rkalfa)
     d ekboi1_bkun     s                   like(ekbkun)
     d ekboiu_bkun     s                   like(ekbkun)
     d ekbei1_ebku     s                   like(ekebku)
     d ekbeiu_ebku     s                   like(ekebku)
     d ekbeiu_erka     s                   like(ekerka)
     d ekbeiu_ekun     s                   like(ekekun)
     d ekbeiu_ekpr     s                   like(ekekpr)
     d ekbeiu_eogr     s                   like(ekeogr)
     d ekbeiu_ehgr     s                   like(ekehgr)
     d ekbeiu_eugr     s                   like(ekeugr)
     d ekbeiu_emod     s                   like(ekemod)
     d ekbeiu_eldo     s                   like(ekeldo)
     d ekbeiu_evar     s                   like(ekevar)
     d ekbeiu_eenh     s                   like(ekeenh)
     d ekbeiu_elet     s                   like(ekelet)
     d ekbeiu_ebot     s                   like(ekebot)
6.13 d ekbtir_tbku     s                   like(ektbku)
6.30 d ekbtiu_tbku     s                   like(ektbku)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_kund_alf      s              6
     d w_paar_alf      s              4
6.11 d w_tbot          s              2
6.12 d p_boty          s              2
6.12 d p_alt1          s              1  0
6.12 d p_kund          s              6  0
6.12 d p_paar          s              4  0
6.20 d w_peri          s              4  0
6.21 d w_fper          s              4  0
     d w_m1ar          s              4  0
     d w_naar          s              4  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
     d w_firm          s                   like(ekfirm)
     d w_navn          s                   like(rknavn)
     d w_bkun          s                   like(ekbkun)
     d b_kuti          s              1    inz(*off)
6.13 d b_avtal         s              1    inz(*off)
      *
      * Definering av konstanter
      *
7.01 d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * K1WIN  - K1 Vindu for kopiere
      * O1WIN  - O1 Vindu for starte beregnign av årsbonus for kunde
      * M1WIN  - M1 Vindu for innlegging av prosenter for masseoppdatering
      * L1WIN  - L1 Vindu for innlegging av tidsrom for masseoppdatering
      * P1WIN  - P1 Vindu for starte beregnign av årsbonus for alle kunder
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
9.01 c                   call      'EK120R'
     c                   goto      b2tagb
     c                   when      *inkh
     c                   eval      b_kuti = *off
     c                   exsr      endre_tid
     c                   goto      b2tagb
     c                   when      *inki
     c                   exsr      kalk_alle
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F23=Vis alle / kun med bonus
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2alfa <> *blank or
     c                             b2kund <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    setll     rkunl1
     c                   other
     c                   eval      rkunl2_alfa = b1alfa
     c     rkunl2_key    setll     rkunl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på navn/alfa
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      rkunl2_alfa = b2alfa
     c     rkunl2_key    setll     rkunl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på nummer
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    setll     rkunl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2alfa = *blank
     c                   eval      b2kund = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Kopier bonus
      *
     c                   if        b1valg = 3
     c                   if        b1bonu <> 0
     c                   exsr      kopier_bonus
     c                   eval      b_forn = *on
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.12  *
6.12  * Skrive kundebrev
6.12  *
6.12 c                   if        b1valg = 6
6.12 c                   exsr      sr_kbrev
6.12 c                   eval      b_forn = *on
6.12 c                   eval      b1valg = 0
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
      *
      * Bonus
      *
     c                   if        b1valg = 7
     c                   call      'EK104R'
     c                   parm                    b1kund
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endre bonustid på enkeltkunde
      *
     c                   if        b1valg = 8
     c                   eval      b_kuti = *on
     c                   exsr      endre_tid
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kalkuler årsbonus
      *
     c                   if        b1valg = 9
     c                   if        b1bonu <> 0
     c                   exsr      kalk_bonus
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   read      rkunl1
     c                   other
     c                   read      rkunl2
     c                   endsl
      *
     c                   if        %eof or
     c                             rkfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
6.13 c**                 eval      ekboi1_bkun = rkkund
6.13 c**   ekboi1_key    setll     ekboi1
6.13 c**   ekboi1_key    reade     ekboi1
6.13 c                   eval      ekbtir_tbku = rkkund
6.13 c     ekbtir_key    setll     ekbtir
6.13 c     ekbtir_key    reade     ekbtir
     c                   if        %eof and
     c                             *in80 = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rkunl1_kund = rkkund
     c                   other
     c                   eval      rkunl2_alfa = rkalfa
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1alfa = rkalfa
     c                   eval      b1navn = rknavn
     c                   eval      b1kund = rkkund
     c                   eval      b1bonu = 0
     c                   eval      b1sbon = 0
     c                   eval      b1bdat = 0
      *
6.13 c**                 if        not %eof(ekboi1)
6.13 c                   if        not %eof(ekbtir)
     c                   eval      b1bonu = 1
     c                   endif
      *
6.13 c**                 dow       not %eof(ekboi1)
6.13 c                   dow       not %eof(ekbtir)
6.13 c**                 eval      b1sbon = b1sbon + eksbon
6.13 c                   eval      b1sbon = b1sbon + ektsbo
6.13 c**                 if        ekbdat <> *loval
6.13 c                   if        ektbda <> *loval
6.13 c**   *dmy          move      ekbdat        b1bdat
6.13 c     *dmy          move      ektbda        b1bdat
     c                   endif
6.13 c**   ekboi1_key    reade     ekboi1
6.13 c     ekbtir_key    reade     ekbtir
     c                   enddo
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   if        w_seqe = 'K'
     c     rkunl1_key    setgt     rkunl1
     c                   readp     rkunl1
     c                   else
     c     rkunl2_key    setgt     rkunl2
     c                   readp     rkunl2
     c                   endif
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   if        w_seqe = 'K'
     c                   readp     rkunl1
     c                   else
     c                   readp     rkunl2
     c                   endif
      *
     c                   if        %eof or
     c                             rkfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      ekboi1_bkun = rkkund
     c     ekboi1_key    chain     ekboi1
     c                   if        not %found and
     c                             *in80 = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   if        w_seqe = 'K'
     c                   eval      rkunl1_kund = rkkund
     c                   else
     c                   eval      rkunl2_alfa = rkalfa
     c                   endif
      *
     c                   enddo
      *
     c                   if        %eof
     c                   if        w_seqe = 'K'
     c     rkunl1_key    setll     rkunl1
     c                   else
     c     rkunl2_key    setll     rkunl2
     c                   endif
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kopiering av bonus til annen kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopier_bonus  begsr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      k1kund = 0
     c                   eval      ekbkun = b1kund
     c                   eval      w_bkun = b1kund
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   if        w_afld = 'K1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    k1kund
     c                   parm                    w_navn
     c                   endif
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker input
      *
     c                   if        k1kund = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      rkunlr_kund = k1kund
     c     rkunlr_key    chain     rkunlr
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *  fjerner denne testen, sletter gamle bonus/bonuselement
     c*                  eval      ekboi1_bkun = k1kund
     c*    ekboi1_key    chain     ekboi1
     c*                  if        %found
     c*                  eval      *in33 = *on
     c*                  goto      k1tagb
     c*                  endif
      *
      * rydd først begge nivåer - for sikkerhet skyld !
      *
     c                   eval      ekboiu_bkun = k1kund
     c     ekboiu_key    setll     ekboiu
     c     ekboiu_key    reade     ekboiu
     c                   dow       not %eof(ekboiu)
     c                   delete    ekboiur
     c     ekboiu_key    reade     ekboiu
     c                   enddo
      *
     c                   eval      ekbeiu_ebku = k1kund
     c     ekbeiu_key2   setll     ekbeiu
     c     ekbeiu_key2   reade     ekbeiu
     c                   dow       not %eof(ekbeiu)
     c                   delete    ekbeiur
     c     ekbeiu_key2   reade     ekbeiu
     c                   enddo
6.30  *
6.30 c                   eval      ekbtiu_tbku = k1kund
6.30 c     ekbtiu_key    setll     ekbtiu
6.30 c     ekbtiu_key    reade     ekbtiu
6.30 c                   dow       not %eof(ekbtiu)
6.30 c                   delete    ekbtiur
6.30 c     ekbtiu_key    reade     ekbtiu
6.30 c                   enddo
      *
      * Kopierer bonus og bonuselementer
      *
     c                   eval      ekboi1_bkun = w_bkun
     c     ekboi1_key    setll     ekboi1
     c     ekboi1_key    reade     ekboi1
     c                   dow       not %eof
      *
     c                   eval      ekbkun = k1kund
     c                   eval      ekeusr = l_user
     c                   time                    ekodat
6.10 c                   time                    ekotim
6.10 c                   eval      ekousr = l_user
     c                   time                    ekedat
     c                   time                    eketim
     c                   write     ekboiur
      *
     c     ekboi1_key    reade     ekboi1
     c                   enddo
      *
     c                   eval      ekbei1_ebku = w_bkun
     c     ekbei1_key    setll     ekbei1
     c     ekbei1_key    reade     ekbei1
     c                   dow       not %eof
      *
     c                   eval      ekebku = k1kund
     c                   eval      ekeeus = l_user
6.10 c                   eval      ekeous = l_user
     c                   time                    ekeoda
     c                   time                    ekeeda
     c                   time                    ekeeti
6.10 c                   time                    ekeoti
     c                   write     ekbeiur
      *
     c     ekbei1_key    reade     ekbei1
     c                   enddo
6.30  * Legg også ut EKBTST transaksjon
6.30 c                   eval      ekbtiu_tbku = w_bkun
6.30 c     ekbtiu_key    setll     ekbtiu
6.30 c     ekbtiu_key    reade     ekbtiu
6.30 c                   dow       not %eof
6.30  *
6.30 c                   eval      ektbku = k1kund
6.30 c                   eval      ektbgr = 0
6.30 c                   eval      ektsbo = 0
6.30 c                   eval      ektbda = *loval
6.30 c                   eval      ekteus = l_user
6.30 c                   eval      ektous = l_user
6.30 c                   time                    ektoda
6.30 c                   time                    ekteda
6.30 c                   time                    ekteti
6.30 c                   time                    ektoti
6.30 c                   write     ekbtiur
6.30  *
6.30 c     ekbtiu_key    reade     ekbtiu
6.30 c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulering av årsbonus for kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_bonus    begsr
      *
      * Fyller bildet
      *
6.11 c                   eval      o1tbot = *blank
     c                   eval      o1kund = b1kund
     c                   eval      o1knav = b1navn
      *
     c                   time                    w_udat
     c     *iso0         move      w_udat        w_udat_8
     c                   movel     w_udat_8      o1paar
      *
6.20 c                   if        umonth-1 > 9
6.20 c                   eval      o1peri = %char(umonth-1)
6.20 c                   else
6.20 c                   eval      o1peri = '0' + %char(umonth-1)
6.20 c                   endif
6.21 c                   eval      o0peri = '01'
      *
     c     o1tagb        tag
      *
     c                   exfmt     o1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekker input
      *
     c                   if        o1paar = 0
     c                   movel     w_udat_8      o1paar
     c                   endif
      *
     c                   if        o1paar < 2000
     c                   eval      *in31 = *on
     c                   goto      o1tagb
     c                   endif
6.21  * Fra periode
6.21 c                   move      o0peri        w_peri
6.21 c                   if        w_peri < 1   or
6.21 c                             w_peri > 12
6.21 c                   eval      *in32 = *on
6.21 c                   goto      o1tagb
6.21 c                   endif
6.20  * Periode
6.20 c                   move      o1peri        w_peri
6.20 c                   if        w_peri < 1   or
6.20 c                             w_peri > 12
6.20 c                   eval      *in32 = *on
6.20 c                   goto      o1tagb
6.20 c                   endif
6.21  * Periode
6.21 c                   if        %int(o0peri) > %int(o1peri)
6.21 c                   eval      *in33 = *on
6.21 c                   goto      o1tagb
6.21 c                   endif
      *
      * Kaller kalkulasjonsprogram
      *
     c                   movel     w_firm        w_firm_alf
     c                   movel     b1kund        w_kund_alf
     c                   movel     o1paar        w_paar_alf
6.11 c                   eval      w_tbot = o1tbot
      *
     c                   call      'EK710C'
     c                   parm                    w_firm_alf
     c                   parm                    w_kund_alf
     c                   parm                    w_paar_alf
6.11 c                   parm                    w_tbot
6.21 c                   parm                    o0peri
6.20 c                   parm                    o1peri
      *
      * Oppdaterer subfile
      *
     c                   eval      b1sbon = 0
     c                   eval      b1bdat = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for masse endring av bonus tidsrom
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endre_tid     begsr
      *
      * Fyller bildet
      *
6.11 c                   eval      l1tbot = *blank
     c                   eval      l1stda = 0
     c                   eval      l1slda = 0
     c                   if        b_kuti = *on
     c                   eval      l1kund = b1navn
     c                   else
     c                   eval      l1kund = 'Alle kunder'
     c                   endif
      *
     c     l1taga        tag
     c                   exfmt     l1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Leser alle poster i bonusfile og oppdaterer
      *
     c                   if        b_kuti = *on
     c                   eval      ekboiu_bkun = b1kund
     c                   else
     c                   eval      ekboiu_bkun = 0
     c                   endif
      *
     c     ekboiu_key    setll     ekboiu
     c                   read      ekboiu
      *
     c                   dow       not %eof(ekboiu)
     c                   if        b_kuti = *on and
     c                             ekbkun <> b1kund
     c                   goto      end_tid
     c                   endif
      * Hvis lest bonustype ikke er lik lest bonustype, les ny.
6.11 c                   if        l1tbot <> *blank and
6.11 c                             l1tbot <> ekboty
6.11 c                   goto      ny_les
6.11 c                   endif
      *
      * Leser tilhørende bonuselementer
      *
     c                   eval      ekbeiu_ebku = ekbkun
     c                   eval      ekbeiu_erka = ekrkat
     c                   eval      ekbeiu_ekun = ekkund
     c                   eval      ekbeiu_ekpr = ekkpro
     c                   eval      ekbeiu_eogr = ekogrp
     c                   eval      ekbeiu_ehgr = ekhgrp
     c                   eval      ekbeiu_eugr = ekugrp
     c                   eval      ekbeiu_eldo = ekldor
     c                   eval      ekbeiu_emod = ekmodn
     c                   eval      ekbeiu_evar = ekvare
     c                   eval      ekbeiu_eenh = ekenhe
     c                   eval      ekbeiu_elet = eklety
     c                   eval      ekbeiu_ebot = ekboty
     c     ekbeiu_key    setll     ekbeiu
     c     ekbeiu_key    reade     ekbeiur
      *
     c                   dow       not %eof(ekbeiu)
      *
      * Oppdaterer tidsrom på bonuselement
      *
      *
     c                   eval      ekefda = %date(l1stda:*dmy)
     c                   eval      eketda = %date(l1slda:*dmy)
     c                   eval      ekeeda = w_date
     c                   eval      ekeeti = w_time
     c                   eval      ekeeus = l_user
     c                   update    ekbeiur
      *
     c     ekbeiu_key    reade     ekbeiur
     c                   enddo
      *
     c                   eval      ekfdat = %date(l1stda:*dmy)
     c                   eval      ektdat = %date(l1slda:*dmy)
     c                   eval      ekedat = w_date
     c                   eval      eketim = w_time
     c                   eval      ekeusr = l_user
     c                   update    ekboiur
      *
6.11 c     ny_les        tag
      *
     c                   read      ekboiu
     c                   enddo
      *
     c     end_tid       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bonusberegning av alle kunder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_alle     begsr
      *
     c                   call      'EK715R'
     c                   endsr
      *
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  * Subrutine for bonusberegning av alle kunder
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.12 c     sr_kbrev      begsr
6.12  *
6.12  * Fyller bildet
6.12  *
6.12 c                   eval      p2kund = b1kund
6.12 c                   movel     b1navn        p2knav
6.12  *
6.12 c     p2tagb        tag
6.12  *
6.12 c                   exfmt     p2win
6.12  *
6.12 c                   if        *inkc or *inkl
6.12 c                   leavesr
6.12 c                   endif
6.12  *
6.12  * Sjekk at valgt kunde har bonusavtale
6.12  *
6.12 c                   eval      b_avtal = *off
6.12 c                   eval      ekbtir_tbku = b1kund
6.12 c     ekbtir_key    setll     ekbtir
6.12 c     ekbtir_key    reade     ekbtir
6.12 c                   dow       not %eof(ekbtir)
6.12 c                   eval      b_avtal = *on
6.12 c     ekbtir_key    reade     ekbtir
6.12 c                   enddo
6.12 c                   if        b_avtal = *off
6.12 c                   eval      *in33 = *on
6.12 c                   goto      p2tagb
6.12 c                   endif
6.12  *
6.12  * Sjekk om valgt år er gyldig
6.12  *
6.12 c                   if        p2paar = 0
6.12 c                   eval      *in31 = *on
6.12 c                   goto      p2tagb
6.12 c                   endif
6.12  *
6.12  * Info til kundebrev?
6.12  *
6.12 c*                  in        lda_ds
6.12 c*                  eval      l_dato = p2paar
6.12 c*                  eval      l_tek1 = p2tek1
6.12 c*                  eval      l_tek2 = p2tek2
6.12 c*                  eval      l_tek3 = p2tek3
6.12 c*                  out       lda_ds
6.12 c                   eval      p_kund = p2kund
6.12 c                   eval      p_paar = p2paar
6.12 c                   eval      p_alt1 = p2alt1
6.12 c                   eval      p_boty = *blanks
6.12  *
6.14 c**                 call      'EK605R'
6.14 c                   call      'EK605C'
6.12 c                   parm                    p_kund
6.12 c                   parm                    p_paar
6.12 c                   parm                    p_alt1
6.12 c                   parm                    p_boty
6.12  *
6.12 c                   endsr
6.12  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kunde
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      * Key for posisjonering på kundenr
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på navn/alfa
      *
     c     rkunl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl2_alfa
      *
      * Key for oppslag/les på bonus
      *
     c     ekboi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboi1_bkun
      *
     c     ekboi1_key2   klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av bonus
      *
     c     ekboiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboiu_bkun
      *
      * Key for oppdatering av bonus
      *
     c     ekbeiu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    ekbeiu_ebku
      *
      * Key for les på kunde bonuselement
      *
     c     ekbei1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbei1_ebku
6.13  *
6.13  * Key for les på kunde bonus
6.13  *
6.13 c     ekbtir_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    ekbtir_tbku
6.30  *
6.30  * Key for les på kunde bonus
6.30  *
6.30 c     ekbtiu_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ekbtiu_tbku
      *
      * Key for oppdatering av kunde bonuselement
      *
     c     ekbeiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbeiu_ebku
     c                   kfld                    ekbeiu_erka
     c                   kfld                    ekbeiu_ekun
     c                   kfld                    ekbeiu_ekpr
     c                   kfld                    ekbeiu_eogr
     c                   kfld                    ekbeiu_ehgr
     c                   kfld                    ekbeiu_eugr
     c                   kfld                    ekbeiu_eldo
     c                   kfld                    ekbeiu_emod
     c                   kfld                    ekbeiu_evar
     c                   kfld                    ekbeiu_eenh
     c                   kfld                    ekbeiu_elet
     c                   kfld                    ekbeiu_ebot
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *off
      *
     c     ekboi1_key2   chain     ekboi1
     c                   if        not %found
     c                   eval      *in80 = *on
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rkunl2_alfa = *blank
     c     rkunl2_key    setll     rkunl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
