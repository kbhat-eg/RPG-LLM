      /TITLE  Utskrift av kunderegister - parameter
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK620R                                              *
      * Beskrivelse . . : Utskrift av kunderegister - parameter               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ----------------------------------------------------*
      * R3.30     021200  Rettet feil pga endring i parameter            KOB  *
      * R3.30     021200  Lagt inn standard datosjekk                    KOB  *
      * V5.30     090304  Lagt inn spørrefunksjoner i skjermbildet       KOB  *
      * V5.50     021007  Distrikt er 4 siffer.                          HFL  *
6.20  * V6.20     080911  Også endret felt p_dist til 4
7.xx  * 7.xx      180516  Endringer reltart til ny GUI. NB Kan kun gjelde BSA
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     frk620d    cf   e             workstn
      *
     d s               s              1  0
     d wpakod          s              2  0
     d wpdat1          s              8  0
      *
     d p_in03          s              1
     d p_stat          s              1
     d p_kode          s              4  0
     d p_kund          s              6  0
6.20 d*p_dist          s              2  0
6.20 d p_dist          s              4  0
     d p_tx30          s             30
     d w_firm          s              3  0
     d w_user          s                   like(l_user)
      *
      *  Parameter
     d/COPY QCPYLESRC,RPARRKDS
      *
      /eject
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c                   exfmt     a2bld
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
     c                   eval      *in38 = *off
     c                   eval      *in39 = *off
     c                   eval      *in40 = *off
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
     c                   eval      *in46 = *off
     c                   eval      *in47 = *off
     c                   eval      *in48 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           p_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spr_rut
     c                   goto      a2taga
     c                   endif
      *
      * Sjekke om "Eldre enn dato" er gyldig
      *
     c                   if        a2peda = *zero                                        .....
     c                   move      *date         a2peda                                      .
     c                   endif                                                           .....
      *
     c     *dmy          test(d)                 a2peda                 31
     c                   if        *in31 = *on                                           .....
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll kundenummer
      *
     c                   if        a2pknf > a2pknt                                       .....
     c                   move      *on           *in32                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll avdeling
      *
     c                   if        a2pavf > a2pavt                                       .....
     c                   move      *on           *in33                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll kundekategori
      *
     c                   if        a2pkaf > a2pkat                                       .....
     c                   move      *on           *in34                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll selger
      *
     c                   if        a2psef > a2pset                                       .....
     c                   move      *on           *in35                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll distrikt
      *
     c                   if        a2pdif > a2pdit                                       .....
     c                   move      *on           *in36                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll type utskrift
      *
     c                   if        a2ptyp <> 'E'                                         .....
     c                             and a2ptyp <> 'F'                                         .
     c                   move      *on           *in37                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Kontroll av tilleggsfelt
      * kun et kan være utfylt
      *
     c                   eval      s = 0
      *
     c                   if        a2psal = 'J'
     c                   eval      s = s + 1
     c                   endif
      *
     c                   if        a2prab = 'J'
     c                   eval      s = s + 1
     c                   endif
      *
     c                   if        a2ppsl = 'J'
     c                   eval      s = s + 1
     c                   endif
      *
     c                   if        s > 1                                                 .....
     c                   move      *on           *in38                          Pos.cur.     .
     c                   move      *on           *in45                          MELDING.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
     c                   if        a2psal <> *blank
     c                             and a2psal <> 'J'
     c                   move      *on           *in38                          Pos.cur.     .
     c                   move      *on           *in46                          MELDING.     .
     c                   goto      a2taga                                                    .
     c                   endif
     c                   if        a2prab <> *blank
     c                             and a2prab <> 'J'
     c                   move      *on           *in39                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif
     c                   if        a2ppsl <> *blank
     c                             and a2ppsl <> 'J'
     c                   move      *on           *in40                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif
      *
      *  Kontroll type sortering
      *
     c                   if        a2psrt <> 'J' and                                     .....
     c                             a2psrt <> ' '                                             .
     c                   move      *on           *in41                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Skriv parameter
      *
     c                   move      a2pknf        pknrf
     c                   move      a2pknt        pknrt
     c                   move      a2pavf        pavdf
     c                   move      a2pavt        pavdt
     c                   move      a2pkaf        pkatf
     c                   move      a2pkat        pkatt
     c                   move      a2psef        pself
     c                   move      a2pset        pselt
     c                   move      a2pdif        pdisf
     c                   move      a2pdit        pdist
     c     *dmy          move      a2peda        pedat
     c                   move      a2psal        psald
     c                   move      a2prab        prabk
     c                   move      a2ppsl        ppsla
     c                   move      a2psrt        psort
     c                   move      a2ptyp        ptype
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      *
      * Subrutine for forespørsel
      *
     c     spr_rut       begsr
      *
      * Call kunder fra
      *
     c                   if        w_afld = 'A2PKNF'
     c                   eval      p_kund = a2pknf
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_tx30
      *
     c                   eval      a2pknf = p_kund
     c                   eval      *in57 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call kunder til
      *
     c                   if        w_afld = 'A2PKNT'
     c                   eval      p_kund = a2pknt
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_tx30
      *
     c                   eval      a2pknt = p_kund
     c                   eval      *in58 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for avdeling fra
      *
     c                   if        w_afld = 'A2PAVF'
     c                   eval      p_kode = a2pavf
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pavf = p_kode
     c                   eval      *in51 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for avdeling til
      *
     c                   if        w_afld = 'A2PAVT'
     c                   eval      p_kode = a2pavt
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pavt = p_kode
     c                   eval      *in52 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kundekategori fra
      *
     c                   if        w_afld = 'A2PKAF'
     c                   eval      p_kode = a2pkaf
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pkaf = p_kode
     c                   eval      *in53 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kundekategori til
      *
     c                   if        w_afld = 'A2PKAT'
     c                   eval      p_kode = a2pkat
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pkat = p_kode
     c                   eval      *in54 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for selger fra
      *
     c                   if        w_afld = 'A2PSEF'
     c                   eval      p_kode = a2psef
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2psef = p_kode
     c                   eval      *in55 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for selger til
      *
     c                   if        w_afld = 'A2PSET'
     c                   eval      p_kode = a2pset
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pset = p_kode
     c                   eval      *in56 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for distrikt fra
      *
     c                   if        w_afld = 'A2PDIF'
     c                   eval      p_dist = a2pdif
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    p_dist
     c                   parm                    p_tx30
      *
     c                   eval      a2pdif = p_dist
     c                   eval      *in59 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for distrikt til
      *
     c                   if        w_afld = 'A2PDIT'
     c                   eval      p_dist = a2pdit
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    p_dist
     c                   parm                    p_tx30
      *
     c                   eval      a2pdit = p_dist
     c                   eval      *in60 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
     c     end_spr_rut   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
     c                   eval      wpdat1 = 0
      *
      *
      * Legge inn firmanummer og navn fra parameter
      *
     c                   move      l_user        w_user
     c                   move      l_firm        w_firm
     c                   eval      a2pknf = *zeros
     c                   move      999999        a2pknt
     c                   eval      a2pavf = *zeros
     c                   move      9999          a2pavt
     c                   eval      a2pkaf = *zeros
     c                   move      9999          a2pkat
     c                   eval      a2psef = *zeros
     c                   move      9999          a2pset
     c                   eval      a2pdif = *zeros
     c                   move      9999          a2pdit
      *
     c                   move      udate         a2peda
      *
     c                   move      'E'           a2ptyp
      *
     c                   move      'J'           a2psrt
      *
      * Hent bruker-record
      *
     c     key03         chain     ausrl1                             63
      *
      *  Sjekk om begrenset til avdeling
      *
     c                   if        abbavd = 'J'                                          .....
     c                   move      *on           *in30                          Protect.     .
     c                   move      absavd        a2pavf                                      .
     c                   move      absavd        a2pavt                                      .
     c                   endif                                                           .....
      *
     c                   eval      p_in03 = ' '
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for bruker-file
      *
     c     key03         klist
     c                   kfld                    w_user
      *
     c                   endsr
      /eject
