      /TITLE  ASOKON Spørring på mottakskvittering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL113R                                        *
      * Beskrivelse . . : Spørring på mottakskvittering.                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           250195  Nytt program januar-1995                 RH   *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *   SUBFILE-1:                                                    *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *    30-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgl113d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
     fgurml1    if   e           k disk
     f                                     rename(gurmpfr:gurmpf1)
     fgurmlu    uf   e           k disk
     f                                     rename(gurmpfr:gurmpfu)
      *
     d txt             s             20    dim(3) ctdata perrcd(1)
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * w_arow - Actual line, angir startlinje for vindu.
      * w_acol - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record - Mottatte transaksjoner
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1SFL  - C1 Subfile - record
      * C2CTL  - C2 Subfile - control record - Viser RECORD ("rådump").
      * C2CMD  - C2 Eget skjermbilde for CMD-taster
      * D1WSF  - C1 Subfile window - record
      * D2WCT  - C2 Subfile window - control record - Til oppdatering.
      * D2CMD  - C2 Eget skjermbilde for CMD-taster - Window definisjon
      *
      /EJECT
      *
      * Henter firmanavn.
      *
     d                uds
     d dsuser                911    920
     d dsfgrp                931    933
     d dsfirm                944    946  0
     d dsfnav                951    980
      /EJECT
      *
      * Datastruktur for dato:
      *
     d                 ds
     d wpdato                         8  0
     d  wpdatÅ                        4  0 overlay(wpdato)
     d  wpdatm                        2  0 overlay(wpdato:5)
     d  wpdatd                        2  0 overlay(wpdato:7)
      *
      * Datastruktur for dato:
      *
     d                 ds
     d dsdato                         8  0
     d  dsdatd                        2  0 overlay(dsdato)
     d  dsdatm                        2  0 overlay(dsdato:3)
     d  dsdatÅ                        4  0 overlay(dsdato:5)
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d sfrn01          s                   like(srrn01)
     d spge01          s              3  0
     d srrn01          s              4  0
     d ssrn01          s                   like(srrn01)
     d w_arow          s              2  0
     d w_acol          s              3  0
     d wpfelt          s              1
     d wpfgrp          s              3
     d wpfirm          s              3  0
     d wpslet          s              2
      *---------------------------------------------------------------------
      * Stand Alone Fields - BOTTOM
      *---------------------------------------------------------------------
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      xdsp01
      *
      * F3=Avslutt program i fra subfile program.
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * F6=Vis alle (*IN30=*ON) eller vis egne (*IN30=OFF):
      *
     c                   if        *inkf = *on                                  .....
      * Vis alle:                                              .
     c                   if        *in30 = *off                                 ... .
     c                   move      *on           *in30                            . .
     c                   eval      cmdf06 = *blanks                               . .
     c                   movel     txt(2)        cmdf06                           . .
     c                   eval      wpfgrp = *blanks                               . .
     c                   eval      wpfirm = 0                                     . .
      * Vis filgruppe/firma:                                 . .
     c                   else                                                     . .
     c                   move      *off          *in30                            . .
     c                   eval      cmdf06 = *blanks                               . .
     c                   movel     txt(1)        cmdf06                           . .
     c                   movel     dsfgrp        wpfgrp                           . .
     c                   eval      wpfirm = dsfirm                                . .
     c                   endif                                                  ... .
      *                                                        .
     c     key01         setll     gurml1                                           .
     c                   exsr      xclr01                                           .        .
     c                   exsr      xcrt01                                           .
     c                   goto      b2taga                                           .
     c                   endif                                                  .....
      *
      * Forny
      *
     c                   if        *inke = *on                                  .....
     c                   exsr      xforny                                           .
     c                   goto      b2taga                                           .
     c                   endif                                                  .....
      *
      * Roll
      *
     c                   if        *in10 = *on                                           .....
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     c                   do        ssrn01                                                .....
     c                   readc     b1sfl                                  16                 .
      *                                                                 .
     c                   if        *in16 = *on                                  .....        .
     c                   leave                                                      .        .
     c                   endif                                                  .....        .
      *                                                                 .
      * Slette post:                                                    .
      *                                                                 .
     c                   if        b1valg = '4'                                 .....        .
      *                                                        .        .
     c                   movel     'JA'          wpslet                             .        .
     c                   movel     b1mfgr        c1mfgr                             .        .
     c                   eval      c1mfir = b1mfir                                  .        .
     c                   eval      c1mubf = b1mubf                                  .        .
     c                   movel     b1mrko        c1mrko                             .        .
     c                   movel     b1inut        c1inut                             .        .
     c                   eval      c1dato = b1dato                                  .        .
     c                   eval      c1klok = b1klok                                  .        .
      *                                                        .        .
     c                   exfmt     c1win                                            .        .
      * Slett post:                                            .        .
     c                   if        *inkc <> *on                                 ... .        .
     c     key02         chain     gurmlu                             60          . .        .
     c                   if        *in60 = *off                                 . . .        .
     c                   delete    gurmpfu                                      . . .        .
     c                   endif                                                  . . .        .
     c                   endif                                                  ... .        .
      *                                                        .        .
     c                   move      srrn01        w_virn                             .
     c                   eval      b1valg = *blank                                  .
     c                   update    b1sfl                                            .
      *                                                        .        .
     c                   iter                                                       .        .
     c                   endif                                                  .....        .
      *                                                                 .
      *                                                                 .
     c                   enddo                                                           .....
      *
     c                   if        wpslet <> *blanks                            ...
     c                   eval      wpslet = *blanks                               .
     c                   exsr      xforny                                         .
     c                   endif                                                  ...
      *
     c                   goto      b2taga
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Les subfile på nytt:                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xforny        begsr
      *
      * Vis alle:                                              .
     c                   if        *in30 = *on                                  ... .
     c                   eval      wpfgrp = *blanks                               . .
     c                   eval      wpfirm = 0                                     . .
      * Vis filgruppe/firma:                                 . .
     c                   else                                                     . .
     c                   movel     dsfgrp        wpfgrp                           . .
     c                   eval      wpfirm = dsfirm                                . .
     c                   endif                                                  ... .
      *                                                        .
     c     key01         setll     gurml1                                           .
     c                   exsr      xclr01                                           .        .
     c                   exsr      xcrt01                                           .
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile - 1                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile - 1                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   eval      spge01 = spge01 + 12                         SFL END
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01                                       .....
      *                                                                 .
      * *IN30=*OFF dersom det skal leses på nøkkel.  Dersom det er      .
      * valgt F6 i bildet settes *IN30 "*ON", for å indikerer at alle   .
      * poster skal vises:                                              .
      *                                                                 .
     c                   if        *in30 = *off                                 ...          .
     c     key01         reade     gurml1                                 15      .          .
     c                   else                                                     .          .
     c                   read      gurml1                                 15      .          .
     c                   endif                                                  ...          .
      *                                                                 .
     c     *in15         cabeq     *on           xend01                         EOF          .
      * Snur dato:                                                      .
     c                   eval      wpdato = gdmdat                                           .
     c                   eval      dsdatÅ = wpdatÅ                                           .
     c                   eval      dsdatm = wpdatm                                           .
     c                   eval      dsdatd = wpdatd                                           .
     c                   eval      b1dato = dsdato                                           .
      * Display felter:                                                 .
     c                   eval      srrn01 = srrn01 + 1                                       .
     c                   movel     gdmfgr        b1mfgr                                      .
     c                   eval      b1mfir = gdmfir                                           .
     c                   eval      b1klok = gdmtim                                           .
     c                   movel     gdmrko        b1mrko                                      .
     c                   movel     gdmrid        b1mrid                                      .
     c                   eval      b1mubf = gdmubf                                           .
     c                   movel     gdmtxt        b1komm                                      .
      *                                                                 .
     c                   eval      b1inut = *blanks                                          .
     c                   move      gdmrid        wpfelt                                      .
      *                                                                 .
     c                   if        wpfelt = 'I'                                              .
     c                   movel     'Innland'     b1inut                                      .
     c                   endif                                                               .
      *                                                                 .
     c                   if        wpfelt = 'U'                                              .
     c                   movel     'Utland '     b1inut                                      .
     c                   endif                                                               .
      *                                                                 .
     c                   write     b1sfl                                                     .
     c                   enddo                                                           .....
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01                                       .....
     c                   eval      sfrn01 = ssrn01 + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      w_virn = sfrn01
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile - 1                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0                                           .....
     c                   move      *on           *in12                                       .
     c                   endif                                                           .....
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Initierer hjelpefelt:
      *
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      wpfelt = *blank
     c                   eval      wpslet = *blanks
     c                   eval      w_arow = 4
     c                   eval      w_acol = 39
      *
      *
     c                   movel     dsfgrp        a1fgrp
     c                   eval      a1firm = dsfirm
     c                   movel     dsfnav        a1fnav
      *
      *
      *
      * Key til bruk på returfiler - GURML1, lese på filgruppe/firma:
      *
     c     key01         klist
     c                   kfld                    wpfgrp
     c                   kfld                    wpfirm
      *
      * Key til bruk for sletting av post på GURMLU:
      *
     c     key02         klist
     c                   kfld                    b1mfgr
     c                   kfld                    b1mfir
     c                   kfld                    b1mubf
     c                   kfld                    b1mrko
     c                   kfld                    b1mrid
      *
     c                   movel     dsfgrp        wpfgrp
     c                   eval      wpfirm = dsfirm
      *
      * Starter les ev poster:
      *
     c                   movel     txt(1)        cmdf06
     c     key01         setll     gurml1
      *
      * Fyll opp subfile:
      *
     c                   exsr      xcrt01
      *
     c                   endsr
      *
**
F6=Vis alle returer 1
F6=Vis egne returer 2
********************3
