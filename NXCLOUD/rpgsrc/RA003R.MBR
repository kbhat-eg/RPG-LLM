      /TITLE  Vedlikehold av statuskoder - kode 3
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RA003R                                              *
      * Beskrivelse . . : Vedlikehold av statuskoder  - 3                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R3.30     271100  Rettet feil i parameterliste RA103R             KOB *
      * V5.40     140705  Lagt inn kode for krav til utfylte felt kunder  KOB *
      * V5.50     191105  Lagt inn kode for utskrift av kundebrev         KOB *
      * V5.50     140705  Lagt inn kode for krav til utfylte felt lever.  KOB *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fraa1lu    uf a e           k disk    rename(raa1pfr:raa1lur)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
     fraa3lu    uf a e           k disk    rename(raa3pfr:raa3lur)
     fra003d    cf   e             workstn
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *                                                                -----
     d p_stat          s              1
     d p_ckod          s                   like(a21bek)
     d p_ctxt          s             20
     d p_ctx2          s             20
     d w_firm          s                   like(ra1fir)
      *
     c     dummy         tag
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for vedlikehold av statuskode              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
     c                   exfmt     a2bld
      *
      * Call program for
      * betalingsbetingelse
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'A21BEK'
     c                   call      'RA103R'
     c                   parm                    w_firm
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
     c                   parm                    p_ctx2
      *
     c                   if        p_ckod <> *zero
     c                   move      p_ckod        a21bek
     c                   endif
     c                   goto      a2taga
     c                   endif
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'A21BEL'
     c                   call      'RA103R'
     c                   parm                    w_firm
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
     c                   parm                    p_ctx2
      *
     c                   if        p_ckod <> *zero
     c                   move      p_ckod        a21bel
     c                   endif
     c                   goto      a2taga
     c                   endif
      /eject
      *
      * Gå ut
      *
     c     *inkc         cabeq     *on           xslutt
     c     *inkl         cabeq     *on           xslutt
      *
      *  Standard betalingsbetingelse kunder
      *
     c                   if        a21bek <> *zero
     c                   move      a21bek        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in31
     c                   goto      a2taga
     c                   endif
     c                   endif
      *
      *  Standard betalingsbetingelse leverandører
      *
     c                   if        a21bel <> *zero
     c                   move      a21bel        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in32
     c                   goto      a2taga
     c                   endif
     c                   endif
      *
      *  Aldersfordeling
      *
     c                   if        a23ka1 = *zero
     c                   move      *on           *in33
     c                   goto      a2taga
     c                   endif
      *
     c                   if        a23ka2 <= a23ka1
     c                   move      *on           *in34
     c                   goto      a2taga
     c                   endif
      *
     c                   if        a23ka3 <= a23ka2
     c                   move      *on           *in35
     c                   goto      a2taga
     c                   endif
      *
     c                   if        a23la1 = *zero
     c                   move      *on           *in37
     c                   goto      a2taga
     c                   endif
      *
     c                   if        a23la2 <= a23la1
     c                   move      *on           *in38
     c                   goto      a2taga
     c                   endif
      *
     c                   if        a23la3 <= a23la2
     c                   move      *on           *in39
     c                   goto      a2taga
     c                   endif
      /eject
      *
      * Oppdater statuskode 1
      *
     c     keya1         chain     raa1lu                             60
      *
     c                   move      a21bek        ra1bek
     c                   move      a21bel        ra1bel
     c                   move      a21bkk        ra1bkk
     c                   move      a21bkl        ra1bkl
      *
     c                   time                    ra1dat
     c                   time                    ra1tim
     c                   eval      ra1usr = l_user
      *
     c                   if        *in60 = *off
     c                   update    raa1lur
     c                   else
     c                   write     raa1lur
     c                   endif
      *
      * Oppdater statuskode 3
      *
     c     keya3         chain     raa3lu                             61
      *
     c                   move      a23ka1        ra3ka1
     c                   move      a23ka2        ra3ka2
     c                   move      a23ka3        ra3ka3
      *
     c                   move      a23la1        ra3la1
     c                   move      a23la2        ra3la2
     c                   move      a23la3        ra3la3
      *
     c                   move      a23sak        ra3sak
      *
     c                   move      a23mfe        ra3mfe
     c                   move      a23mfl        ra3mfl
     c                   move      a23kbr        ra3kbr
      *
     c                   time                    ra3dat
     c                   time                    ra3tim
     c                   eval      ra3usr = l_user
      *
     c                   if        *in61 = *off
     c                   update    raa3lur
     c                   else
     c                   write     raa3lur
     c                   endif
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      * Legge inn firmanummer og navn fra LDA
      *
     c                   move      l_firm        w_firm
      *
      * Hent STATUSKODE 1
      *
     c     keya1         chain     raa1l1                             60
      *
     c                   if        *in60 = *on
      *
     c                   move      15            a21bek
     c                   move      15            a21bel
      *
     c                   endif
      *
     c                   if        *in60 = *off
      *
     c                   move      ra1bek        a21bek
     c                   move      ra1bel        a21bel
     c                   move      ra1bkk        a21bkk
     c                   move      ra1bkl        a21bkl
      *
     c                   endif
      *
      * Hent STATUSKODE 3
      *
     c     keya3         chain     raa3l1                             61
      *
     c                   if        *in61 = *on
      *
     c                   move      15            a23ka1
     c                   move      30            a23ka2
     c                   move      45            a23ka3
      *
     c                   move      15            a23la1
     c                   move      30            a23la2
     c                   move      45            a23la3
      *
     c                   endif
      *
     c                   if        *in61 = *off
     c                   move      ra3ka1        a23ka1
     c                   move      ra3ka2        a23ka2
     c                   move      ra3ka3        a23ka3
      *
     c                   move      ra3la1        a23la1
     c                   move      ra3la2        a23la2
     c                   move      ra3la3        a23la3
      *
     c                   move      ra3sak        a23sak
     c                   move      ra3mfe        a23mfe
     c                   move      ra3mfl        a23mfl
     c                   move      ra3kbr        a23kbr
     c                   eval      a23usr = ra3usr
     c     *dmy          move      ra3dat        a23dat
      *
     c                   endif
      *
      * Key for statuskode 1
      *
     c     keya1         klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 3
      *
     c     keya3         klist
     c                   kfld                    w_firm
      *
     c                   endsr
      /eject
