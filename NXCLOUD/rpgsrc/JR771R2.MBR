     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JR771R
      * Beskrivelse . . : Danning av leverandør vareregister, Mellbye
      *
      *
      *
      * Spesialversjon. :
      * Tilpasset for . : Stensberg
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120104 GRO  Nytt program
      *       140905 HKO  Byttet program for oppdatering av søktekster
5.70  * PRGR  171108 BOF  Utvidet med prisgruppe
6.11  *       271005 bof  Utvidet med JVPPPF for bestillbar enhet
6.12  *       081010 bof  Lagt til JVPENH til jvarlu
6.30  * 6.30  280113 bof  Omskriving i forb. endring pakning VA (fjernet
6.30  *                   jvpppf)
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     fjvdtlu    o  a e           k disk    rename(jvdtpfr:jvdtlur)
     fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fjrvgl1    if   e           k disk    rename(jrvgpfr:jrvgl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
     fjpfal6    uf a e           k disk    rename(jpfapfr:jpfal6r)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
     d p_list          s             32
      *
      * Definering av parametre
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Mellbye
      *
     d                 ds
     d d_inpu                       251
     d  d_vare                        7    overlay(d_inpu:1)
     d  d_kkod                        3    overlay(d_inpu:8)
     d  d_tek1                       32    overlay(d_inpu:11)
     d  d_anta                        4    overlay(d_inpu:43)
     d  d_enhe                        4    overlay(d_inpu:47)
     d  d_sapr_alf                    9    overlay(d_inpu:51)
     d  d_sapr                        9  2 overlay(d_inpu:51)
     d  d_fopr                        9  2 overlay(d_inpu:60)
     d  d_eann_alf                   13    overlay(d_inpu:79)
     d  d_eann                       13  0 overlay(d_inpu:79)
     d  d_evgr                        5    overlay(d_inpu:143)
     d  d_evgt                       30    overlay(d_inpu:148)
     d  d_ernm                        8    overlay(d_inpu:178)
     d  d_pnum                        9    overlay(d_inpu:186)
     d  d_pvar                       16    overlay(d_inpu:195)
     d  d_inpr_alf                    9    overlay(d_inpu:211)
     d  d_inpr                        9  2 overlay(d_inpu:211)
     d  d_nobn_alf                    8    overlay(d_inpu:226)
     d  d_nobn                        8  0 overlay(d_inpu:226)
     d  d_nobg_alf                    7    overlay(d_inpu:234)
     d    d_nogr                      2  0 overlay(d_nobg_alf:1)
     d    d_nhgr                      2  0 overlay(d_nobg_alf:3)
     d    d_nugr                      3  0 overlay(d_nobg_alf:5)
     d  d_nbpr                        9  2 overlay(d_inpu:241)
     d  d_prkd                        1    overlay(d_inpu:250)
      *
      * Definering av variabler i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jlevl1_ldor     s                   like(jlldor)
     d jvarlu_vare     s                   like(jvvare)
     d jvpklu_vare     s                   like(jwvare)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
     d fenkl1_enhf     s                   like(feenhf)
     d jrvgl1_grap     s                   like(jrgrap)
     d jrvgl1_gvgr     s                   like(jrgvgr)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvarld_ean1     s                   like(jvean1)
     d jpfal6_vare     s                   like(jcvare)
      *
      * Definering av variable
      *
     d b_vare          s              1    inz(*off)
     d b_inlr          s              1
      *
     d w_firm          s              3  0
     d w_eanx          s             13
     d w_eann          s             13  0
     d w_nobx          s              8
     d w_nobn          s              8  0
     d w_anta          s              4  0
     d w_stat          s                   like(jvstat)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1
      *
     c                   eval      jlnavn = %trim(jlnavn)
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      jrvapf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   exsr      varelin
      *
     c                   read      jrvapf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     varelin       begsr
      *
     c                   if        d_vare = *blank
     c                   leavesr
     c                   endif
      *
      * Trimmer og oversetter tekster til ascii
      *
     c                   eval      d_vare = %trim(d_vare)
     c                   eval      d_tek1 = %trim(d_tek1)
     c                   eval      d_enhe = %trim(d_enhe)
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
     c     X'46':'Æ'     xlate     d_enhe        d_enhe
     c     X'A0':'æ'     xlate     d_enhe        d_enhe
     c     X'77':'Ø'     xlate     d_enhe        d_enhe
     c     X'90':'ø'     xlate     d_enhe        d_enhe
     c     X'2A':'Å'     xlate     d_enhe        d_enhe
     c     X'EF':'å'     xlate     d_enhe        d_enhe
      *
     c     Lo:Up         xlate     d_tek1        d_tek1
     c     Lo:Up         xlate     d_enhe        d_enhe
      *
     c     ' ':'0'       xlate     d_anta        d_anta
     c     ' ':'0'       xlate     d_inpr_alf    d_inpr_alf
     c     ' ':'0'       xlate     d_sapr_alf    d_sapr_alf
      *
     c                   if        %len(%trim(d_eann_alf)) < 13
     c                   evalr     w_eanx = %trim(d_eann_alf)
     c                   eval      w_eanx = %xlate(' ':'0':w_eanx)
     c                   else
     c                   move      d_eann_alf    w_eanx
     c                   eval      w_eanx = %xlate(' ':'0':w_eanx)
     c                   endif
      *
     c                   if        %len(%trim(d_nobn_alf)) < 8
     c                   evalr     w_nobx = %trim(d_nobn_alf)
     c                   eval      w_nobx = %xlate(' ':'0':w_nobx)
     c                   else
     c                   move      d_nobn_alf    w_nobx
     c                   eval      w_nobx = %xlate(' ':'0':w_nobx)
     c                   endif
      *
     c                   move      w_eanx        w_eann
     c                   move      w_nobx        w_nobn
      *
      * Konverterer enhet
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1
     c                   if        %found
     c                   eval      d_enhe = feenht
     c                   endif
      *
      * Starter oppdatering
      *
     c                   clear                   jvarlur
     c                   eval      b_vare = *off
     c                   exsr      finn_vare
      *
     c                   if        b_vare = *on
     c                   exsr      oppr_pris
     c                   leavesr
     c                   else
     c                   exsr      ny_vare
     c                   endif
      *
     c                   eval      jvarlu_vare = jvvare
     c     jvarlu_key    chain     jvarlur
      *
      * Oppretter eller oppdaterer vare
      *
     c                   eval      jvtek1 = d_tek1
     c                   eval      jvldor = jrldor
     c                   eval      jvlvar = d_vare
6.12 c                   eval      jvpenh = d_enhe
     c                   eval      jvprod = 0
     c                   eval      jvpvar = *blank
     c                   eval      jveusr = l_user
     c                   time                    jvetim
     c                   time                    jvedat
      *
     c                   if        %check(digits : w_eanx) > 0
     c                   eval      jvean1 = *zero
     c                   else
     c                   move      w_eanx        jvean1
     c                   endif
      *
     c                   if        %check(digits : w_nobx) > 0
     c                   eval      jvnobb = *zero
     c                   else
     c                   move      w_nobx        jvnobb
     c                   endif
      *
     c                   eval      d_nobg_alf = %xlate(' ':'0':d_nobg_alf)
     c                   if        %check(digits : d_nobg_alf) > 0
     c                   eval      d_nogr = *zero
     c                   eval      d_nhgr = *zero
     c                   eval      d_nugr = *zero
     c                   endif
      *
     c                   eval      jvogrp = d_nogr
     c                   eval      jvhgrp = d_nhgr
     c                   eval      jvugrp = d_nugr
      *
      * Finner varegr. fra rapp. varegruppe - hvis ikke satt
      *
     c                   if        jvugrp = *zero
     c                   eval      jrvgl1_grap = d_rapp
     c                   eval      jrvgl1_gvgr = %trim(d_evgr)
     c     jrvgl1_key    chain     jrvgl1
     c                   if        %found
     c                   eval      jvogrp = jrgogr
     c                   eval      jvhgrp = jrghgr
     c                   eval      jvugrp = jrgugr
     c                   endif
     c                   endif
      *
      * Oppretter vare
      *
     c                   write     jvarlur
      *
      * Oppretter vare i vare-detalj-register
      *
     c                   clear                   jvdtlur
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
      *
      * Oppretter vare-forpakning
      *
     c                   exsr      oppd_forpakn
      *
      * Oppretter vare-pris
      *
     c                   exsr      oppd_pris
      *
      * Oppretter påslag
      *
     c                   exsr      oppd_paslag
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av funnet vare (opprette pris/påslag)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
      * Sletter eventuelt eksisterende priser for leverandør på varen
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
      * Oppretter pris på vare for leverandør
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
     c                   eval      jxlvar = d_vare
     c                   eval      jxpris = d_inpr
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
      *
      * Sletter påslag
      *
     c                   eval      jpfal6_vare = jvvare
     c     jpfal6_key    chain     jpfal6r
     c                   if        %found
     c                   delete    jpfal6r
     c                   endif
      *
      * Oppretter påslag
      *
     c                   exsr      oppd_paslag
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
     c                   eval      jvstat = 2
      *
      * Henter neste ledige varenummer, og oppdaterer statusregisteret
      *
     c     jstslu_key    chain     jstslu
     c                   eval      jaavsi = jaavsi + 1
     c                   update    jstslur
      *
     c                   movel     jaavsi        jvvare
      *
      * Initierer varegruppe
      *
     c                   eval      jvogrp = 0
     c                   eval      jvhgrp = 0
     c                   eval      jvugrp = 0
      *
      * Opprettelsesdato
      *
     c                   time                    jvodat
     c                   time                    jvadat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-forpakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_forpakn  begsr
      *
     c                   clear                   jvpklur
      *
     c                   eval      jwvare = jvvare
6.30 c                   eval      jwldor = jrldor
     c                   eval      jwpakn = '000'
     c                   eval      jwenhe = d_enhe
     c                   eval      jwofak = 1
6.30 c                   eval      jwgtin = 0
6.30 c                   eval      jwbest = 1
     c                   eval      jwrsts = 0
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   write     jvpklur
      *
      *
     c                   move      d_anta        w_anta
      *
     c                   if        w_anta > 1
     c                   if        d_enhe <> 'STK'
     c                   eval      jwvare = jvvare
6.30 c                   eval      jwldor = jrldor
     c                   eval      jwpakn = '001'
     c                   eval      jwenhe = 'STK'
     c                   eval(h)   jwofak = 1 / w_anta
     c                   eval      jwrsts = 0
     c                   write     jvpklur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jvldor
     c                   eval      jxlvar = jvlvar
     c                   eval      jxpris = d_inpr
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av påslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_paslag   begsr
      *
     c                   clear                   jpfalur
      *
     c                   eval      jcfirm = w_firm
5.70 c                   eval      jcprgr = *blank
     c                   eval      jcogrp = jvogrp
     c                   eval      jchgrp = jvhgrp
     c                   eval      jcugrp = jvugrp
     c                   eval      jcldor = jrldor
     c                   eval      jcmodn = jvmodn
     c                   eval      jcvare = jvvare
     c                   eval      jckofa = 1
     c                   eval      jckobe = 0
      *
     c                   if        d_sapr <> 0 and
     c                             d_inpr <> 0
      *
     c                   eval(h)   jcsafa = d_sapr / d_inpr
     c                   eval      jceusr = l_user
      *
     c                   time                    jcodat
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   write     jpfalur
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av vare på NOBB/EAN/Lev.varenr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vare     begsr
      *
     c*                  move      w_nobx        jvarl3_nobb
     c                   if        w_nobn <> *zero
     c                   eval      jvarl3_nobb = w_nobn
     c     jvarl3_key    chain     jvarl3
     c                   if        %found
     c                   eval      b_vare = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c*                  move      w_eanx        jvarld_ean1
     c                   if        w_eann <> *zero
     c                   eval      jvarld_ean1 = w_eann
     c     jvarld_key    chain     jvarld
     c                   if        %found
     c                   eval      b_vare = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      jvarl8_ldor = jrldor
     c                   eval      jvarl8_lvar = d_vare
     c     jvarl8_key    chain     jvarl8
     c                   if        %found
     c                   eval      b_vare = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør-register
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppdatering av vare
      *
     c     jvarlu_key    klist
     c                   kfld                    jvarlu_vare
      *
      * Key for oppdatering av vare-forpakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      * Key for oppslag på rapportør varegruppereferanse-register
      *
     c     jrvgl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrvgl1_grap
     c                   kfld                    jrvgl1_gvgr
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på vare
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppdatering av påslag
      *
     c     jpfal6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jpfal6_vare
      *
      * Henter firma og leverandør fra parameter
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
