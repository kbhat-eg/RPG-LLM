     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FM100R
      * Beskrivelse . . : Kort/fakt.avtaler, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030704 GRO  Nytt program
5.60  *       090108 bof  Utvidet kundeprosjekt med kjørerute
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffmkalr    if   e           k disk    rename(fmkapfr:fmkalrr)
     ffmkal1    if   e           k disk    rename(fmkapfr:fmkal1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffmkalu    uf a e           k disk    rename(fmkapfr:fmkalur)
      *
     ffm100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fmfirm)
     d p_kund          s                   like(fmkund)
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fmkalr_kund     s                   like(fmkund)
     d fmkalr_kpro     s                   like(fmkpro)
     d fmkalr_kftp     s                   like(fmkftp)
     d fmkalr_ktsq     s                   like(fmktsq)
     d fmkal1_kund     s                   like(fmkund)
     d fmkal1_kpro     s                   like(fmkpro)
     d fmkal1_kftp     s                   like(fmkftp)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d fmkalu_kund     s                   like(fmkund)
     d fmkalu_kpro     s                   like(fmkpro)
     d fmkalu_kftp     s                   like(fmkftp)
     d fmkalu_ktsq     s                   like(fmktsq)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
     d w_kftp          s              2  0
     d w_ktpn          s             25
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_tlgx          s              1    inz(*off)
      *
     d w_firm          s                   like(fmfirm)
     d w_kund          s                   like(fmkund)
     d w_kort          s             16
     d w_ddat          s               d   datfmt(*iso)
     d w_adr1          s             30
     d w_adr2          s             30
     d w_sted          s             30
     d w_kprs          s             30
     d w_tlfa          s             15
     d w_tlfp          s             15
     d w_tlfm          s             15
     d w_mail          s             50
     d w_orka          s              3  0
     d w_okun          s              6  0
     d w_okpr          s              6  0
     d w_opda          s               d
     d w_proj          s              6  0
     d w_kjor          s              2  0
     d w_kjnr          s              5  0
     d w_posi          s              2  0
     d w_modu          s             10
     d w_disp          s              1
     d w_stat          s              1
      *
      * Definering av konstanter
      *
     d c_modu          c                   'OF_KORT01 '
     d c_sfil          s              2  0 inz(13)
     d c_digi          c                   '0123456789 '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * F1WIN  - F1 Vindu for spørring på valgalternativer (Hardkodet)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        b_tlgx = *off
     c                   goto      avslutt
     c                   endif
      *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      bc2bld
     c                   eval      c2kund = w_kund
     c                   exsr      xc2bld
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      fmkal1_kund = b1kund
     c     fmkal1_key    setll     fmkal1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      b_forn = *off
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
     c                   eval      c2kund = b1kund
     c                   eval      c2kpro = b1kpro
     c                   eval      c2kftp = b1kftp
     c                   eval      c2ktsq = b1ktsq
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kund = b1kund
     c                   eval      d1kpro = b1kpro
     c                   eval      d1kftp = b1kftp
     c                   eval      d1ktsq = b1ktsq
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      fmkal1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        fmfirm <> w_firm or
     c                             fmkund <> w_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      fmkal1_kund = fmkund
      *
     c                   eval      b1valg = 0
     c                   eval      b1kund = fmkund
     c                   eval      b1kpro = fmkpro
     c                   eval      b1kftp = fmkftp
     c                   eval      b1ktsq = fmktsq
      *
     c                   exsr      avt_tp_sh
      *
     c                   if        fmkrnr <> 0
     c                   move      fmkrnr        w_kort
     c                   eval      b1kort = '*' +
     c                             %subst(w_kort:11:6)
     c                   else
     c                   eval      b1kort = *blank
     c                   endif
      *
     c                   movel     fmhldr        b1navn
     c                   move      fmoblg        b1obli
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rkunl1_kund = c2kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      c2knav = rknavn
     c                   else
     c                   eval      c2knav = *blank
     c                   endif
      *
     c                   if        c2kpro <> *zero
     c                   eval      fkprl1_kund = c2kund
     c                   eval      fkprl1_kpro = c2kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found(fkprl1)
     c                   eval      c2kpnv = flnavn
     c                   else
     c                   eval      c2kpnv = *blank
     c                   endif
     c                   endif
      *
     c                   eval      fmkalr_kund = c2kund
     c                   eval      fmkalr_kpro = c2kpro
     c                   eval      fmkalr_kftp = c2kftp
     c                   eval      fmkalr_ktsq = c2ktsq
     c     fmkalr_key    chain     fmkalr
      *
     c                   if        %found(fmkalr)
     c                   eval      c2krnr = fmkrnr
     c                   eval      c2expd = fmexpd
     c                   eval      c2oblg = fmoblg
      *
     c                   exsr      avt_tp_fu
      *
     c                   eval      c2hldr = fmhldr
     c                   eval      c2envn = fmenvn
     c                   eval      c2fnvn = fmfnvn
     c                   eval      c2adr1 = fmadr1
     c                   eval      c2adr2 = fmadr2
     c                   eval      c2ponr = fmponr
     c                   eval      c2sted = fmsted
     c                   eval      c2tlfa = fmtlfa
     c                   eval      c2tlfp = fmtlfp
     c                   eval      c2tlfm = fmtlfm
     c                   eval      c2mail = fmmail
     c                   eval      c2eusr = fmeusr
      *
     c                   if        fmedat <> *loval
     c     *dmy          move      fmedat        c2edat
     c                   endif
      *
     c                   else
      *
     c                   eval      c2krnr = *zero
     c                   eval      c2expd = *zero
     c                   eval      c2oblg = *zero
     c                   eval      c2ktpn = *blank
     c                   eval      c2hldr = *blank
     c                   eval      c2envn = *blank
     c                   eval      c2fnvn = *blank
     c                   eval      c2adr1 = *blank
     c                   eval      c2adr2 = *blank
     c                   eval      c2ponr = *zero
     c                   eval      c2sted = *blank
     c                   eval      c2tlfa = *blank
     c                   eval      c2tlfp = *blank
     c                   eval      c2tlfm = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
      *
     c                   endif
      *
      * Dersom 'vise' settes protect-indikatorer
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c*                  write     c2cmd
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   if        w_afld = 'C2KPRO'
     c                   eval      c2kpnv = *blank
     c                   call      'FL501R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_ddat
     c                   parm                    c2kpro
     c                   parm                    w_pnav
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_sted
     c                   parm                    w_kprs
     c                   parm                    w_tlfa
     c                   parm                    w_tlfp
     c                   parm                    w_tlfm
     c                   parm                    w_mail
     c                   parm                    w_orka
     c                   parm                    w_okun
     c                   parm                    w_okpr
     c                   parm                    w_opda
     c                   parm                    w_proj
     c                   parm                    w_kjor
     c                   parm                    w_kjnr
     c                   eval      c2kpnv = w_pnav
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2KFTP'
     c                   eval      c2ktpn = *blank
     c                   eval      f1head = 'kort/avt.type'
     c                   eval      f1alth = 'Kort/avt.type'
     c                   exsr      xf1win1
     c                   eval      c2kftp = w_kftp
     c                   eval      c2ktpn = w_ktpn
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *
      * Validering
      *
     c                   if        c2kftp = 0
     c                   eval      *in31  = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Purchasing/ClearPark krever en faktura pr. ordre
      *
     c*                  if        c2kftp = 1 or
     c*                            c2kftp = 2
     c*                  if        rksamf <> 1
     c*                  eval      *in32  = *on
     c*                  goto      c2tagb
     c*                  endif
     c*                  endif
      *
     c                   if        c2krnr = *zero
     c                   eval      *in33  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2expd = *zero
     c                   eval      *in34  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2hldr = *blank
     c                   eval      *in35  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2ktsq = *zero
     c                   exsr      finn_ktsq
     c                   endif
      *
      * Oppdaterer tabell
      *
     c                   clear                   fmkalur
      *
     c                   eval      fmkalu_kund = c2kund
     c                   eval      fmkalu_kpro = c2kpro
     c                   eval      fmkalu_kftp = c2kftp
     c                   eval      fmkalu_ktsq = c2ktsq
     c     fmkalu_key    chain     fmkalur
      *
     c                   eval      fmkrnr = c2krnr
     c                   eval      fmexpd = c2expd
     c                   eval      fmoblg = c2oblg
     c                   eval      fmhldr = c2hldr
     c                   eval      fmenvn = c2envn
     c                   eval      fmfnvn = c2fnvn
     c                   eval      fmadr1 = c2adr1
     c                   eval      fmadr2 = c2adr2
     c                   eval      fmponr = c2ponr
     c                   eval      fmsted = c2sted
     c                   eval      fmtlfa = c2tlfa
     c                   eval      fmtlfm = c2tlfm
     c                   eval      fmtlfp = c2tlfp
     c                   eval      fmmail = c2mail
     c                   eval      fmeusr = l_user
     c                   time                    fmetim
     c                   time                    fmedat
      *
     c                   if        %found(fmkalu)
     c                   update    fmkalur
     c                   else
     c                   eval      fmfirm = w_firm
     c                   eval      fmkund = c2kund
     c                   eval      fmkpro = c2kpro
     c                   eval      fmkftp = c2kftp
     c                   eval      fmktsq = c2ktsq
     c                   write     fmkalur
     c                   eval      b1kund = c2kund
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å init. av skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bc2bld        begsr
      *
     c                   eval      c2kpro = *zero
     c                   eval      c2kpnv = *blank
     c                   eval      c2kftp = *zero
     c                   eval      c2ktsq = *zero
     c                   eval      c2ktpn = *blank
     c                   eval      c2krnr = *zero
     c                   eval      c2expd = *zero
     c                   eval      c2oblg = *zero
     c                   eval      c2ktpn = *blank
     c                   eval      c2hldr = *blank
     c                   eval      c2envn = *blank
     c                   eval      c2fnvn = *blank
     c                   eval      c2adr1 = *blank
     c                   eval      c2adr2 = *blank
     c                   eval      c2ponr = *zero
     c                   eval      c2sted = *blank
     c                   eval      c2tlfa = *blank
     c                   eval      c2tlfp = *blank
     c                   eval      c2tlfm = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
      *
     c                   endsr
      *
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     fmkal1_key    setgt     fmkal1
     c                   readp     fmkal1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     fmkal1
      *
     c                   if        %eof or
     c                             fmfirm <> w_firm or
     c                             fmkund <> w_kund
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      fmkal1_kund = fmkund
      *
     c                   enddo
      *
     c                   if        %eof
     c     fmkal1_key    setll     fmkal1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hardkodet søk  (F1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1win1       begsr
      *
     c                   exsr      xf1nul
     c                   eval      f1alt1 = 'VISA Purchasing          '
     c                   eval      f1alt2 = 'Europay Purchasing       '
     c                   eval      f1alt3 = '                         '
     c                   eval      f1alt4 = '                         '
     c                   eval      f1alt5 = '                         '
     c                   eval      f1alt6 = '                         '
     c                   eval      f1alt7 = '                         '
     c                   eval      f1alt8 = '                         '
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_f1win1
     c                   endif
      *
     c                   select
     c                   when      f1val1 = '1'
     c                   eval      w_kftp = 01
     c                   eval      w_ktpn = f1alt1
     c                   when      f1val2 = '1'
     c                   eval      w_kftp = 02
     c                   eval      w_ktpn = f1alt2
     c                   when      f1val3 = '1'
     c                   eval      w_kftp = 03
     c                   eval      w_ktpn = f1alt3
     c                   when      f1val4 = '1'
     c                   eval      w_kftp = 04
     c                   eval      w_ktpn = f1alt4
     c                   when      f1val5 = '1'
     c                   eval      w_kftp = 05
     c                   eval      w_ktpn = f1alt5
     c                   when      f1val6 = '1'
     c                   eval      w_kftp = 06
     c                   eval      w_ktpn = f1alt6
     c                   when      f1val7 = '1'
     c                   eval      w_kftp = 07
     c                   eval      w_ktpn = f1alt7
     c                   when      f1val8 = '1'
     c                   eval      w_kftp = 08
     c                   eval      w_ktpn = f1alt8
     c                   endsl
      *
     c     end_f1win1    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å blanke ut hardkodet spørrebilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1nul        begsr
      *
     c                   eval      f1alt1 = *blank
     c                   eval      f1alt2 = *blank
     c                   eval      f1alt3 = *blank
     c                   eval      f1alt4 = *blank
     c                   eval      f1alt5 = *blank
     c                   eval      f1alt6 = *blank
     c                   eval      f1alt7 = *blank
     c                   eval      f1alt8 = *blank
     c                   eval      f1val1 = *blank
     c                   eval      f1val2 = *blank
     c                   eval      f1val3 = *blank
     c                   eval      f1val4 = *blank
     c                   eval      f1val5 = *blank
     c                   eval      f1val6 = *blank
     c                   eval      f1val7 = *blank
     c                   eval      f1val8 = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst (kort)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_tp_sh     begsr
      *
     c                   if        fmkftp = 1
     c                   eval      b1type = 'VISA Purchasing'
     c                   else
     c                   if        fmkftp = 2
     c                   eval      b1type = 'Europay Purchasing'
     c                   else
     c                   eval      b1type = 'Udefinert....'
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst (full)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_tp_fu     begsr
      *
     c                   if        fmkftp = 1
     c                   eval      c2ktpn = 'VISA Purchasing'
     c                   else
     c                   if        fmkftp = 2
     c                   eval      c2ktpn = 'Europay Purchasing'
     c                   else
     c                   eval      c2ktpn = 'Ukjent'
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne høyeste sekv.nr. på kunde/prosjekt/korttype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ktsq     begsr
      *
     c                   eval      fmkal1_kund = c2kund
     c                   eval      fmkal1_kpro = c2kpro
     c                   eval      fmkal1_kftp = c2kftp
      *
     c     fmkal1_key2   setll     fmkal1
     c     fmkal1_key2   reade     fmkal1r
      *
     c                   dow       not %eof
     c     fmkal1_key2   reade     fmkal1r
     c                   enddo
      *
     c                   eval      c2ktsq = fmktsq + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      fmkalr_kund = d1kund
     c                   eval      fmkalr_kpro = d1kpro
     c                   eval      fmkalr_kftp = d1kftp
     c                   eval      fmkalr_ktsq = d1ktsq
     c     fmkalr_key    chain     fmkalr
      *
     c                   eval      rkunl1_kund = d1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      d1knav = rknavn
     c                   else
     c                   eval      d1knav = *blank
     c                   endif
      *
     c                   if        d1kpro <> *zero
     c                   eval      fkprl1_kund = d1kund
     c                   eval      fkprl1_kpro = d1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found(fkprl1)
     c                   eval      d1kpnv = flnavn
     c                   else
     c                   eval      d1kpnv = *blank
     c                   endif
     c                   endif
      *
     c                   eval      d1type = b1type
     c                   eval      d1hldr = fmhldr
     c                   eval      d1krnr = fmkrnr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      fmkalu_kund = d1kund
     c                   eval      fmkalu_kpro = d1kpro
     c                   eval      fmkalu_kftp = d1kftp
     c                   eval      fmkalu_ktsq = d1ktsq
     c     fmkalu_key    chain     fmkalur
      *
     c                   delete    fmkalur
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
      *
      * Key for oppslag på kort/fakt.avtale-register
      *
     c     fmkalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkalr_kund
     c                   kfld                    fmkalr_kpro
     c                   kfld                    fmkalr_kftp
     c                   kfld                    fmkalr_ktsq
      *
      * Key for posisjonering på kunde
      *
     c     fmkal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkal1_kund
      *
      * Key for les siste nr på kunde/prosjekt/korttype
      *
     c     fmkal1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fmkal1_kund
     c                   kfld                    fmkal1_kpro
     c                   kfld                    fmkal1_kftp
      *
      * Key for oppdat. av kort/fakt.avtale-register
      *
     c     fmkalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkalu_kund
     c                   kfld                    fmkalu_kpro
     c                   kfld                    fmkalu_kftp
     c                   kfld                    fmkalu_ktsq
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   time                    w_ddat
     c                   eval      w_firm = p_firm
     c                   eval      w_kund = p_kund
      *
     c                   eval      *in22 = *on
      *
      * Sjekker om tilleggsmodul er installert
      *
     c                   eval      w_modu = c_modu
     c                   eval      w_disp = '1'
     c                   eval      w_stat = '0'
     c                   eval      b_tlgx = *on
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_stat
      *
     c                   if        w_stat = '1'
     c                   eval      b_tlgx = *off
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fmkal1_kund = w_kund
     c     fmkal1_key    setll     fmkal1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
