     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO745R
      * Beskrivelse . . : Automatisk generering av bestillinger fra ordre
      *                   Rutinen gjennomføres uten bruker dialog.
      *                   Styres av at kode i automatisk bestilling = 2
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040408 ASK  Nytt program
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl2    if   e           k disk    rename(fodtpfr:fodtl2r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
      *
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl2_numm     s                   like(fdnumm)
     d fodtl2_suff     s                   like(fdsuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_ldor          s                   like(fdldor)
     d w_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk om automatisk bestilling er satt på
      *
     c*                  if        laaabe <> 2
     c*                  goto      avslutt
     c*                  endif
      *
      * Hent opplysninger fra ordre
      *
     c     fohel1_key    chain     fohel1r
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   exsr      ordre_linjer
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser ordrelinjer pr. leverandør for bestillingssjekk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_linjer  begsr
      *
     c     fodtl2_key    setll     fodtl2
     c                   eval      w_ldor = 0
      *
     c     fodtl2_key    reade     fodtl2
      *
     c                   dow       not %eof                                     EOF
      *
     c                   if        fdldor = w_ldor
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      w_ldor = fdldor
     c                   call      'FO746R'
     c                   parm                    w_firm
     c                   parm                    fdnumm
     c                   parm                    fdsuff
     c                   parm                    w_ldor
     c                   parm                    w_stat
      *
     c     les_neste     tag
     c     fodtl2_key    reade     fodtl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : Ordre-linje-register leverandørsortert
      *
     c     fodtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl2_numm
     c                   kfld                    fodtl2_suff
      *
      * Key for file : Ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : Status-koder Ofak
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : Status-koder Lager
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_stat
      *
      * Legger inn firmanummer
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn ordrenummer og suffix i nøklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
      *
     c                   eval      fodtl2_numm = p_numm
     c                   eval      fodtl2_suff = p_suff
      *
      * Henter opplysninger fra Lager-status-register
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   endsr
