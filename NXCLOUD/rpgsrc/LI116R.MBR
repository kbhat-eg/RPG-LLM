     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI116R
      * Beskrivelse . . : Program som sjekker ordre mot inngående EDI-fakt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110507 ASK  Nytt program
6.nu  *       280514 bof  gjennomgått mtp. nummerutvidelse
6.3c  * 6.30  081018 bof  Utvidet med trelast sortiment
8.01  * 8.xx  220621 bsa  Utvidet meldingsnummer fra 6-8 siffer
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    uf   e           k disk    rename(fodtpfr:fodtl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
      *
     fli116d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av arrays
      *
     d htx             s             33    dim(2) ctdata perrcd(1)
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av parametere
      *
     d p_mnum          s                   like(limnum)
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * Fritekst/hodelinje
      *
     d d_fakh3         ds
     d  d_fh_ftxq                     3
     d  d_fh_ftxt                    70
      *
      * Fakturareferanser/hode
      *
     d d_fakh4         ds
     d  d_fh_bstn                    35
     d  d_fh_ordr                    35
     d  d_fh_fakk                    35
     d  d_fh_paks                    35
     d  d_fh_kunr                    35
     d  d_fh_proj                    35
     d  d_fh_lvfa                    35
     d  d_fh_ofav                    35
     d  d_fh_plnr                    35
     d  d_fh_konr                    35
     d  d_fh_kidn                    35
      *
      * Faktura partsinfo./hode
      *
     d d_fakh5         ds
     d  d_fh_part                     3
     d  d_fh_pean                    13  0
     d  d_fh_pnav                    30
     d  d_fh_pad1                    30
     d  d_fh_pad2                    30
     d  d_fh_pste                    30
     d  d_fh_porg                    20
      *
      * Faktura transport/lev. - hode
      *
     d d_fakh6         ds
     d  d_fh_trmo                     3
     d  d_fh_trna                    35
     d  d_fh_lebe                     3
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
     d  d_fl_vean                    13  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
     d  d_ft_oavx                    15
     d  d_ft_oavr                    15  2 overlay(d_ft_oavx:1)
      *
      * Definering av variabler i nøkler
      *
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
8.01 d ledil1_mlin     s                   like(limlin)
3.17 d lohel1_numm     s                   like(lonumm)
3.17 d lohel1_suff     s                   like(losuff)
3.17 d lodtl1_numm     s                   like(ldnumm)
3.17 d lodtl1_suff     s                   like(ldsuff)
3.17 d lodtl1_line     s                   like(ldline)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d rlevl1_levr     s                   like(rllevr)
5.32 d rkunl1_kund     s                   like(rkkund)
     d vvarl3_nobb     s                   like(vvnobb)
     d jvarl3_nobb     s                   like(jvnobb)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_stel          s              1  0
      *
     d w_firm          s                   like(fofirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_nfko          s              9  2
     d w_noko          s              9  2
     d w_nopr          s              9  2
     d w_nfan          s              9  3
     d w_noan          s              9  3
     d w_afko          s             10
     d w_aoko          s             10
     d w_aopr          s             10
     d w_afan          s             10
     d w_aoan          s             10
     d w_fenh          s              3
     d w_oenh          s              3
     d w_enob          s                   like(vvnobb)
6.3c d w_lv_vare       s                   like(vvvare)
      *
     d b_linj          s              1    inz(*off)
     d b_till          s              1    inz(*off)
     d b_ordr          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      * Hent opplysninger fra melding/bestilling/ordre
      *
     c                   exsr      hent_ordre
     c                   if        b_feil = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      ledil1_mlin = 500001
     c     ledil1_key    setll     ledil1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     d1taga        tag
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      *in22 = *on
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
     c                   eval      *in22 = *off
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * F3=avslutt
      *
     c                   if        *inkc = *on
     c                   goto      avslutt
     c                   endif
      *
      * F7=Vis priser
      *
     c                   if        *inkg = *on
     c                   eval      *in31 = *on
     c                   eval      *in32 = *off
     c                   eval      b2head = htx(1)
     c                   exsr      forny
     c                   goto      d1taga
     c                   endif
      *
      * F8=Vis antall
      *
     c                   if        *inkh = *on
     c                   eval      *in31 = *off
     c                   eval      *in32 = *on
     c                   eval      b2head = htx(2)
     c                   exsr      forny
     c                   goto      d1taga
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      d1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      ledil1_mlin = 500001
     c     ledil1_key    setll     ledil1
     c                   eval      b_linj = *on
     c                   eval      b_till = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ledil1
      *
      * Ikke flere fakturalinjer - sjekk om tillegg.
      *
     c                   if        limlin > 549999
     c                   eval      b_linj = *off
     c                   eval      b_till = *on
     c                   if        limlin < 600001
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        %eof or                                      EOF
     c                             lifirm <> w_firm or
     c                             limnum <> p_mnum or
     c                             limlin > 649999
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   if        b_linj = *on
     c                   exsr      hent_linjer
     c                   endif
      *
     c                   if        b_till = *on
     c                   exsr      hent_tillegg
     c                   endif
      *
     c                   if        *in31 = *on
     c                   eval      b1felt = w_afko + ' ' + w_aoko +
     c                                      ' ' + w_aopr
     c                   endif
      *
     c                   if        *in32 = *on
     c                   eval      b1felt = w_afan + ' ' + w_fenh +
     c                                      ' ' + w_aoan + ' ' + w_oenh
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger om bestilling/ordre linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_linjer   begsr
      *
      *
     c                   eval      d_fakl = limeld
     c                   eval      b1tek1 = d_fl_tek1
6.3c  * sjekker om logistikk-vare
6.3c c                   eval      w_lv_vare = *blank
6.3c c/exec sql
6.3c c+ select vvlvnr
6.3c c+ into   :w_lv_vare
6.3c c+ from   vvlepf
6.3c c+ where  vvlnob = :d_fl_vnob and
6.3c c+        vvlldo = :loldor
6.3c c+ fetch first 1 rows only
6.3c c/end-exec
6.3c c                   if        w_lv_vare <> *blank
6.3c c                   eval      b1vare = w_lv_vare
6.3c c                   else
      *
      * Hent vareinfo via nobbnr
      *
     c                   eval      vvarl3_nobb = d_fl_vnob
     c     vvarl3_key    chain     vvarl3
     c                   if        %found
     c                   eval      b1vare = vvvare
     c                   else
     c                   eval      jvarl3_nobb = d_fl_vnob
     c     jvarl3_key    chain     jvarl3r
     c                   if        %found
     c                   eval      b1vare = jvvare
     c                   endif
     c                   endif
6.3c c                   endif
      *
     c                   eval      w_nfko = d_fl_bnet
     c                   eval      w_afko = %editc(w_nfko : 'M')
     c                   eval      w_nfan = d_fl_fkva
     c                   eval      w_fenh = d_fl_fenh
     c                   eval      w_afan = %editc(w_nfan : 'M')
      *
      * Hent bestillingsinfo
      *
     c                   eval      w_noko = 0
     c                   eval      w_nopr = 0
     c                   eval      w_noan = 0
     c                   eval      w_oenh = *blank
      *
     c                   eval      lodtl1_numm = linumm
     c                   eval      lodtl1_suff = lisuff
     c                   eval      lodtl1_line = liline
     c     lodtl1_key    chain     lodtl1r
     c                   if        %found
      *
      * Hent ordreinfo
      *
     c                   eval      fodtl1_numm = ldornu
     c                   eval      fodtl1_suff = ldorsu
     c                   eval      fodtl1_line = ldorli
     c     fodtl1_key    chain     fodtl1r
      *
     c                   if        %found
     c                   eval      w_noko = fdkopr
     c                   eval      w_aoko = %editc(w_noko : 'M')
     c                   eval      w_nopr = fdsapr
     c                   eval      w_aopr = %editc(w_nopr : 'M')
     c                   eval      w_noan = fdanta
     c                   eval      w_aoan = %editc(w_noan : 'M')
     c                   eval      w_oenh = fdenh1
     c                   endif
     c                   endif
      *
     c     end_linjer    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger om tilleggslinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_tillegg  begsr
      *
      * Finn nobbnr på tillegg
      *
     c                   eval      d_fakh2 = limeld
      *
     c                   select
     c                   when      d_fh_tkod = 'FRA'
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'FC '
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'IN '
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'HD '
     c                   eval      w_enob = 26924761
     c                   when      d_fh_tkod = 'PI '
     c                   eval      w_enob = 26924803
     c                   when      d_fh_tkod = 'PBE'
     c                   eval      w_enob = 26924829
     c                   when      d_fh_tkod = 'PC '
     c                   eval      w_enob = 26924837
     c                   when      d_fh_tkod = 'ZEE'
     c                   eval      w_enob = 26924985
     c                   when      d_fh_tkod = 'RCH'
     c                   eval      w_enob = 26925024
     c                   when      d_fh_tkod = 'IS '
     c                   eval      w_enob = 26925032
     c                   endsl
      *
     c                   eval      vvarl3_nobb = w_enob
     c     vvarl3_key    chain     vvarl3r
     c                   if        %found
     c                   eval      b1vare = vvvare
     c                   else
     c                   move      w_enob        b1vare
     c                   endif
      *
     c                   eval      b1tek1 = d_fh_ttek
     c                   eval      w_nfko = d_fh_tbel
     c                   eval      w_afko = %editc(w_nfko : 'M')
     c                   eval      w_noko = 0
     c                   eval      w_aoko = %editc(w_noko : 'M')
     c                   eval      w_nopr = 0
     c                   eval      w_aopr = %editc(w_nopr : 'M')
      *
     c                   eval      w_nfan = 1
     c                   eval      w_afan = %editc(w_nfan : 'M')
     c                   eval      w_fenh = 'STK'
     c                   eval      w_noan = 0
     c                   eval      w_aoan = %editc(w_noan : 'M')
     c                   eval      w_oenh = *blank
      *
     c     end_tillegg   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger om bestilling/ordre hoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_ordre    begsr
      *
      * Hent opplysninger fra melding
      *
     c                   eval      ledil1_mlin = 100001
     c     ledil1_key    chain     ledil1r
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   goto      end_ordre
     c                   endif
      *
      * Hent opplysninger fra bestillingshode
      *
     c                   eval      lohel1_numm = linumm
     c                   eval      lohel1_suff = lisuff
     c     lohel1_key    chain     lohel1r
     c                   eval      b2bnum = lonumm
     c                   eval      b2btyp = lootyp
     c                   eval      b2levr = loldor
      *
      * Hent opplysninger fra ordre-hode
      *
     c                   eval      fohel1_numm = loornr
     c                   eval      fohel1_suff = loorsu
     c     fohel1_key    chain     fohel1r
      *
     c                   eval      b2onum = fonumm
     c                   eval      b2otyp = footyp
     c                   eval      b2kund = fokund
      *
      * Hent opplysninger fra kunde
      *
     c                   eval      b2knav = *blank
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b2knav = rknavn
     c                   endif
      *
      * Hent opplysninger fra leverandør
      *
     c                   eval      b2lnav = *blank
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2lnav = rlnavn
     c                   endif
      *
     c     end_ordre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
     c                   kfld                    ledil1_mlin
3.17  *
3.17  * Key for les av Ordre-hode-register, for bestilling
3.17  *
3.17 c     lohel1_key    klist
3.17 c                   kfld                    w_firm
3.17 c                   kfld                    lohel1_numm
3.17 c                   kfld                    lohel1_suff
3.17  *
3.17  * Key for les av Ordre-linje-register, for bestilling
3.17  *
3.17 c     lodtl1_key    klist
3.17 c                   kfld                    w_firm
3.17 c                   kfld                    lodtl1_numm
3.17 c                   kfld                    lodtl1_suff
3.17 c                   kfld                    lodtl1_line
      *
      * Key for les av Ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for les av Ordre-linje-register
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for file : Leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
5.32  * Key for file : KUNDE-REGISTER
5.32  *
5.32 c     rkunl1_key    klist
5.32 c                   kfld                    w_firm
5.32 c                   kfld                    rkunl1_kund
      *
      * Key for file : Vare-register
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for les av vare på nobb-nummer (Vareadm.)
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for file : Ordre-type-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : Status-koder Ofak
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : Status-koder Lager
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_mnum
      *
      * Legger inn firmanummer og parameterdata
      *
     c                   eval      w_firm = l_firm
     c                   eval      ledil1_type = 'FAKT'
     c                   eval      ledil1_mnum = p_mnum
     c                   eval      b2head = htx(1)
     c                   eval      *in31 = *on
     c                   eval      b_linj = *on
      *
      * Henter opplysninger fra Ofak-status-register
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Henter opplysninger fra Lager-status-register
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
      *
**     Variabel heading
  Fak.kost   Ord.kost   Ord.pris
 Fakt. ant Enh  Ordre ant Enh
