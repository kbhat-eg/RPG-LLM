     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JO101R
      * Beskrivelse . . : Matching, Matching av varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121096 HKO  Nytt program
      *       180104 HKO  Lagt inn matching mot Nobbnr
6.30  *       180515 BKV  Endret LVAR pga økt fra 15 til 20 tegn
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl3    if   e           k disk    rename(jmatpfr:jmatl3r)
     fjmatl5    if   e           k disk    rename(jmatpfr:jmatl5r)
     fjmatlu    uf a e           k disk    rename(jmatpfr:jmatlur)
      *
     fjo101d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av array
      *
     d a_meld          s             25    dim(7) ctdata perrcd(1)               MELDINGER
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
     d p_ldor          s              6
     d p_ogrp          s                   like(joogrp)
     d p_hgrp          s                   like(johgrp)
     d p_ugrp          s                   like(jougrp)
     d p_va1f          s                   like(jovar1)
     d p_va1t          s                   like(jovar1)
     d p_kode          s                   like(jokode)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d jmatl3_vkod     s                   like(jovkod)
     d jmatl3_ldor     s                   like(joldor)
     d jmatl3_kode     s                   like(jokode)
     d jmatl5_vkod     s                   like(jovkod)
     d jmatl5_kode     s                   like(jokode)
     d jmatlu_var1     s                   like(jovar1)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_ogrp_pos      s              2  0
     d w_hgrp_pos      s              2  0
     d w_ugrp_pos      s              3  0
     d w_stek_pos      s             35
     d w_tek1_pos      s             35
     d w_vare_pos      s             15
     d w_nobb_pos      s              8  0
6.30 d w_lvar_pos      s                   like(jolvar)
      *
     d w_firm          s                   like(jofirm)
     d w_ldo2          s                   like(joldo2)
     d w_var2          s                   like(jovar2)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(15)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   if        p_ldor <> *blank
     c     jmatl3_key    setll     jmatl3                                                    .
     c                   else
     c     jmatl5_key    setll     jmatl5                                                    .
     c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Matche vare
      *
     c                   if        b1valg = 1                                                .
     c                   exsr      match_vare                                                .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter                                                                .
     c                   endif                                                               .
      *
      * Fjern match
      *
     c                   if        b1valg = 4                                                .
     c                   exsr      fjern_matc                                                .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter                                                                .
     c                   endif                                                               .
      *
      * Vise vare
      *
     c                   if        b1valg = 5                                                .
     c                   if        b1var2 <> *blank
     c                   call      'JV510R'
     c                   parm                    p_firm
     c                   parm                    b1var2
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter                                                                .
     c                   endif                                                               .
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for matching av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     match_vare    begsr
      *
     c                   eval      w_var2 = *blank
      *
     c                   call      'JV501R'
     c                   parm                    p_firm
     c                   parm                    b1tek1
     c                   parm                    b1tek2
     c                   parm                    b1ldor
     c                   parm                    b1lvar
     c                   parm                    b1eann
     c                   parm                    b1nobb
     c                   parm                    w_var2
     c                   parm                    w_ldo2
      *
     c                   parm                    w_ogrp_pos
     c                   parm                    w_hgrp_pos
     c                   parm                    w_ugrp_pos
     c                   parm                    w_stek_pos
     c                   parm                    w_tek1_pos
     c                   parm                    w_vare_pos
     c                   parm                    w_nobb_pos
     c                   parm                    w_lvar_pos
      *
     c                   if        w_var2 <> *blank
      *
      * Matcher med valgt vare
      *
     c                   eval      jmatlu_var1 = b1var1
     c     jmatlu_key    chain     jmatlur                            90
     c                   if        *in90 = *off                                          .....
     c                   eval      jovar2 = w_var2
     c                   eval      joldo2 = w_ldo2
     c                   eval      jokode = 1
     c                   update    jmatlur                                                   .
     c                   endif                                                           .....
      *
      * Oppdater subfile
      *
     c                   eval      b1var2 = w_var2
     c                   eval      b1tek1 = *blank                                           .
     c                   eval      b1tek1 = a_meld(1)                                        .
      *
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for nullstilling av matche-status på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fjern_matc    begsr
      *
     c                   eval      jmatlu_var1 = b1var1
     c     jmatlu_key    chain     jmatlur                            90
     c                   if        *in90 = *off                                          .....
     c                   eval      jovar2 = *blank
     c                   eval      joldo2 = 0
     c                   eval      jokode = 0
     c                   update    jmatlur                                                   .
     c                   endif                                                           .....
      *
      * Oppdater subfile
      *
     c                   eval      b1tek1 = *blank                                           .
     c                   eval      b1tek1 = a_meld(2)                                        .
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        p_ldor <> *blank
     c     jmatl3_key    reade     jmatl3                                 15                 .
     c                   else
     c     jmatl5_key    reade     jmatl5                                 15                 .
     c                   endif
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        p_ogrp <> 0 and
     c                             joogrp <> p_ogrp
     c                   iter
     c                   endif
      *
     c                   if        p_hgrp <> 0 and
     c                             johgrp <> p_hgrp
     c                   iter
     c                   endif
      *
     c                   if        p_ugrp <> 0 and
     c                             jougrp <> p_ugrp
     c                   iter
     c                   endif
      *
     c                   if        p_va1f <> *blank and
     c                             jovar1 < p_va1f
     c                   iter
     c                   endif
      *
     c                   if        p_va1t <> *blank and
     c                             jovar1 > p_va1t
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1tek1 = jotek1                                           .
     c                   eval      b1var1 = jovar1                                           .
     c                   eval      b1ldor = joldor                                           .
     c                   eval      b1lvar = jolvar                                           .
6.30 c*                  eval      b1lvar_10 = jolvar                                        .
6.30 c                   movel     jolvar        b1lvar_10                                   .
     c                   eval      b1eann = joeann                                           .
     c                   eval      b1nobb = jonobb                                           .
     c                   eval      b1tek2 = jotek2                                           .
     c                   eval      b1var2 = jovar2                                           .
     c                   write     b1sfl                                                     .
      *
     c                   enddo                                                           .....
      *
     c                   if        w_srrn > w_ssrn                                       .....
     c                   eval      w_sfrn = w_ssrn + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0                                           .....
     c                   eval      *in12 = *on                                               .
     c                   else
     c                   write     b2cmd
     c                   endif                                                           .....
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_va1f
     c                   parm                    p_va1t
     c                   parm                    p_kode
      *
      * Key for les på matcheregister - leverandør valgt
      *
     c     jmatl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl3_vkod
     c                   kfld                    jmatl3_ldor
     c                   kfld                    jmatl3_kode
      *
      * Key for les på matcheregister
      *
     c     jmatl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatl5_vkod
     c                   kfld                    jmatl5_kode
      *
      * Key for oppdatering av matcheregister
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer skjermbildet, og posisjonerer i databasen
      *
     c                   eval      *in22 = *on
      *
     c                   select
     c                   when      p_kode = 0
     c                   eval      b2kode = a_meld(3)                                        .
     c                   when      p_kode = 1
     c                   eval      b2kode = a_meld(4)                                        .
     c                   when      p_kode = 2
     c                   eval      b2kode = a_meld(5)                                        .
     c                   when      p_kode = 3
     c                   eval      b2kode = a_meld(6)                                        .
     c                   when      p_kode = 4
     c                   eval      b2kode = a_meld(7)                                        .
     c                   endsl
      *
     c                   if        p_ldor = *blank or
     c                             p_ldor = '*BL'
     c                   eval      jmatl3_ldor = 0
     c                   else
     c                   move      p_ldor        jmatl3_ldor
     c                   endif
      *
     c                   if        p_ldor <> *blank
     c                   eval      jmatl3_kode = p_kode
     c                   eval      jmatl3_vkod = 0
     c     jmatl3_key    setll     jmatl3                                                    .
     c                   else
     c                   eval      jmatl5_kode = p_kode
     c                   eval      jmatl5_vkod = 0
     c     jmatl5_key    setll     jmatl5                                                    .
     c                   endif
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
* Matchet *                001
* Ikke matchet *           001
 Ikke matchet              001
 Manuelt matchet           001
 Matchet mot lev. varenr   001
 Matchet mot EAN-nr        001
 Matchet mot Nobbnr        001
