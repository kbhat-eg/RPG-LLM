     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VG100R
      * Beskrivelse . . : Overgruppe, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120199 HKO  Nytt program
      *       101000 KOB  Lagt inn produktansvarlig
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
7.xx  * 7.00  040216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvogrl2    if   e           k disk    rename(vogrpfr:vogrl2r)
     fvogrlr    if   e           k disk    rename(vogrpfr:vogrlrr)
     fvogrlu    uf a e           k disk    rename(vogrpfr:vogrlur)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvhgrlu    uf a e           k disk    rename(vhgrpfr:vhgrlur)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvugrlu    uf a e           k disk    rename(vugrpfr:vugrlur)
     fra26l1    if   e           k disk    rename(ra26pfr:ra26l1r)
      *
     fvg100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vogrl1_oogr     s                   like(vgoogr)
     d vogrl2_otxt     s                   like(vgotxt)
     d vogrlr_oogr     s                   like(vgoogr)
     d vogrlu_oogr     s                   like(vgoogr)
     d vkhvl1_kkod     s                   like(vakkod)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrlu_hogr     s                   like(vghogr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrlu_uogr     s                   like(vguogr)
     d ra26l1_zsak     s                   like(razsak)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vgofir)
     d w_ktxt          s                   like(vaktxt)
     d w_ztxt          s                   like(raztxt)
      *
     d s_oogr          s                   like(vgoogr)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
7.xx c*                  when      *in21
7.xx c*                  eval      *in22 = *on
7.xx c*                  goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2oogr <> 0 or
     c                             b2otxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vogrl2_otxt = b1otxt
     c     vogrl2_key    setll     vogrl2
     c                   other
     c                   eval      vogrl1_oogr = b1oogr
     c     vogrl1_key    setll     vogrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på overgruppe
      *
     c                   if        b2oogr <> 0
     c                   eval      w_seqe = ' '
     c                   eval      vogrl1_oogr = b2oogr
     c     vogrl1_key    setll     vogrl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på beskrivelse
      *
     c                   if        b2otxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vogrl2_otxt = b2otxt
     c     vogrl2_key    setll     vogrl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2oogr = 0
     c                   eval      b2otxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1oogr = b1oogr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1oogr = b1oogr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1oogr = b1oogr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1oogr = b1oogr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av hovedgruppe
      *
     c                   if        b1valg = 7
     c                   call      'VG101R'
     c                   parm                    w_firm
     c                   parm                    b1oogr
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1oogr = b2oogr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1oogr = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vogrlr_oogr = c1oogr
     c     vogrlr_key    chain     vogrlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1oogr = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2oogr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      c2ktxt = *blank
     c                   eval      c2ztxt = *blank
      *
     c                   eval      vogrlr_oogr = c1oogr
     c     vogrlr_key    chain     vogrlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2otxt = vgotxt
     c                   eval      c2okhv = vgokhv
     c                   eval      c2opra = vgopra
     c                   eval      c2oeus = vgoeus
     c     *dmy          move      vgoeda        c2oeda
      *
     c                   if        vgokhv <> *blank
     c                   eval      vkhvl1_kkod = vgokhv
     c     vkhvl1_key    chain     vkhvl1                             91
     c                   if        *in91 = *off
     c                   eval      c2ktxt = vaktxt
     c                   endif
     c                   endif
      *
     c                   if        vgopra <> *blank
     c                   eval      ra26l1_zsak = vgopra
     c     ra26l1_key    chain     ra26l1                             91
     c                   if        *in91 = *off
     c                   eval      c2ztxt = raztxt
     c                   endif
     c                   endif
      *
     c                   else
     c                   eval      c2otxt = *blank
     c                   eval      c2okhv = *blank
     c                   eval      c2ktxt = *blank
     c                   eval      c2opra = *blank
     c                   eval      c2ztxt = *blank
     c                   eval      c2oeus = *blank
     c                   eval      c2oeda = 0
     c                   eval      c2oeus = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1oogr = k1oogr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2OKHV'
     c                   call      'VA520R'
     c                   parm                    w_firm
     c                   parm                    c2okhv
     c                   parm                    w_ktxt
     c                   eval      c2ktxt = w_ktxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2OPRA'
     c                   call      'RA526R'
     c                   parm                    w_firm
     c                   parm                    c2opra
     c                   parm                    w_ztxt
     c                   eval      c2ztxt = w_ztxt
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2otxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2okhv <> *blank
     c                   eval      vkhvl1_kkod = c2okhv
     c     vkhvl1_key    chain     vkhvl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2opra <> *blank
     c                   eval      ra26l1_zsak = c2opra
     c     ra26l1_key    chain     ra26l1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering av overgruppe
      *
     c                   clear                   vogrlur
      *
     c                   eval      vogrlu_oogr = c1oogr
     c     vogrlu_key    chain     vogrlur                            90
      *
     c                   eval      vgotxt = c2otxt
     c                   eval      vgokhv = c2okhv
     c                   eval      vgopra = c2opra
      *
     c                   time                    vgoeda
     c                   time                    vgoeti
     c                   eval      vgoeus = l_user
      *
     c                   if        *in90 = *off
     c                   update    vogrlur
     c                   else
     c                   eval      vgofir = w_firm
     c                   eval      vgoogr = c1oogr
6.10 c                   time                    vgooda
6.10 c                   time                    vgooti
6.10 c                   eval      vgoous = l_user
     c                   write     vogrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1otxt = vgotxt
     c                   eval      b1okhv = vgokhv
     c                   eval      b1opra = vgopra
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vogrlr_oogr = d1oogr
     c     vogrlr_key    chain     vogrlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter overgruppe
      *
     c                   eval      vogrlu_oogr = d1oogr
     c     vogrlu_key    chain     vogrlur                            90
     c                   delete    vogrlur
      *
      * Sletter tilhørende hovedgrupper og undergrupper
      *
     c                   eval      vhgrlu_hogr = vogrlu_oogr
     c     vhgrlu_key    setll     vhgrlu
     c     vhgrlu_key    reade     vhgrlur                                91
     c                   dow       *in91 = *off
     c                   delete    vhgrlur
     c     vhgrlu_key    reade     vhgrlur                                91
     c                   enddo
      *
     c                   eval      vugrlu_uogr = vogrlu_oogr
     c     vugrlu_key    setll     vugrlu
     c     vugrlu_key    reade     vugrlur                                91
     c                   dow       *in91 = *off
     c                   delete    vugrlur
     c     vugrlu_key    reade     vugrlur                                91
     c                   enddo
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_oogr = k1oogr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vogrlr_oogr = k1oogr
     c     vogrlr_key    chain     vogrlr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1oogr = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vogrlr_oogr = k1oogr
     c     vogrlr_key    chain     vogrlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1oogr = s_oogr
     c                   exsr      xc2bld
      *
      * Dersom overgruppe er opprettet, kopieres hovedgrupper og
      * undergrupper
      *
     c                   eval      vogrlr_oogr = k1oogr
     c     vogrlr_key    chain     vogrlr                             90
     c                   if        *in90 = *off
      *
     c                   eval      vhgrl1_hogr = s_oogr
     c     vhgrl1_key    setll     vhgrl1
     c     vhgrl1_key    reade     vhgrl1                                 91
     c                   dow       *in91 = *off
     c                   eval      vghogr = k1oogr
6.10 c                   time                    vghoda
6.10 c                   time                    vghoti
6.10 c                   eval      vghous = l_user
6.10 c                   time                    vgheda
6.10 c                   time                    vgheti
6.10 c                   eval      vgheus = l_user
     c                   write     vhgrlur
     c     vhgrl1_key    reade     vhgrl1                                 91
     c                   enddo
      *
     c                   eval      vugrl1_uogr = s_oogr
     c     vugrl1_key    setll     vugrl1
     c     vugrl1_key    reade     vugrl1                                 91
     c                   dow       *in91 = *off
     c                   eval      vguogr = k1oogr
6.10 c                   time                    vguoda
6.10 c                   time                    vguoti
6.10 c                   eval      vguous = l_user
6.10 c                   time                    vgueda
6.10 c                   time                    vgueti
6.10 c                   eval      vgueus = l_user
     c                   write     vugrlur
     c     vugrl1_key    reade     vugrl1                                 91
     c                   enddo
      *
     c                   endif
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vogrl2
     c                   other
     c                   read      vogrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vgofir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vogrl2_otxt = vgotxt
     c                   other
     c                   eval      vogrl1_oogr = vgoogr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1oogr = vgoogr
     c                   eval      b1otxt = vgotxt
     c                   eval      b1okhv = vgokhv
     c                   eval      b1opra = vgopra
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vogrl2_key    setgt     vogrl2
     c                   readp     vogrl2
     c                   other
     c     vogrl1_key    setgt     vogrl1
     c                   readp     vogrl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vogrl2
     c                   other
     c                   readp     vogrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vgofir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vogrl2_otxt = vgotxt
     c                   other
     c                   eval      vogrl1_oogr = vgoogr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vogrl2_key    setll     vogrl2
     c                   other
     c     vogrl1_key    setll     vogrl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for posisjonering på beskrivelse
      *
     c     vogrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl2_otxt
      *
      * Key for oppslag på overgruppe
      *
     c     vogrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrlr_oogr
      *
      * Key for oppdatering av overgruppe
      *
     c     vogrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrlu_oogr
      *
      * Key for oppslag på kontohenvisning
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
      *
      * Key for oppslag på produktansvarlig
      *
     c     ra26l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra26l1_zsak
      *
      * Key for les på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
      *
      * Key for oppdatering av hovedgruppe
      *
     c     vhgrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrlu_hogr
      *
      * Key for les på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
      *
      * Key for oppdatering av undergruppe
      *
     c     vugrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrlu_uogr
      *
      *
      * Henter firma fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vogrl1_oogr = 0
     c     vogrl1_key    setll     vogrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
