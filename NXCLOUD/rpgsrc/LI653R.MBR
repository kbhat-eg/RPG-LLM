     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI653R
      * Beskrivelse . . : EDI, Kvalitetskontroll - sjekk varer mot NEB standard
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  240112 bsa  Nytt program
6.10  * 6.10  060612 bsa  Fjenrnet kontrollen mot overstyrt varetekst
6.20  *       140612 bsa  Utvidet parameterliste til VL710R
6.21  * 6.10  070213 bsa  Utvidet med sjekk av GLN nr og logistikkpkt.
6.nu  * 6.30  050614 bof  Utvidelser mtp. nummerutvidelse
6.30  * 6.30  060117 bsa  Fjerner kontroll mot varetype. Ref SKA bør
6.30  *                   varenummer for frakt osv gå gjennom. Dette
6.30  *                   er ikke lagervare.
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodetrr)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.21 flldil1    if   e           k disk    rename(lldipfr:lldil1r)
6.21 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_lage               1001   1002  0
      *
      * Definering av variabler i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vvarl1_vare     s                   like(vvvare)
     d vvepl1_pvar     s                   like(vepvar)
     d vvepl1_penh     s                   like(vepenh)
     d vvepl1_pldo     s                   like(vepldo)
     d jvarl1_vare     s                   like(jvvare)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
6.21 d lldil1_ldor     s                   like(lpldor)
6.21 d lldil1_lage     s                   like(lplage)
6.21 d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_stat          s              1
     d w_lage          s              2  0
     d w_vtyp          s              1
     d w_eann          s             14  0
     d w_east          s              1
6.20 d w_udat          s               d   datfmt(*iso)
6.20 d w_lenr          s                   like(vvldor)
6.20 d b_inlr          s              1    inz(*off)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
      *
     d b_feil          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram: Styrerutine for valgte sjekkpunkter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Les fra bestillingen
      *
     c     lohel1_key    chain     lohel1
     c                   if        not %found
     c                   eval      w_stat = '1'
     c                   goto      end_pgm
     c                   endif
      *
6.21  *
6.21  *  Les fra leverandørregister
6.21  *
6.21 c                   eval      rlevl1_levr = loldor
6.21 c     rlevl1_key    chain     rlevl1
6.21 c                   if        not %found
6.21 c                   eval      w_stat = '2'
6.21 c                   goto      end_pgm
6.21 c                   endif
6.21  * Sjekk at GLN nummer mangler
6.21 c                   if        rleanl = 0
6.21 c                   eval      w_stat = '2'
6.21 c                   goto      end_pgm
6.21 c                   endif
6.21  *
6.21  *  Les fra logistikkpunkt
6.21  *
6.21 c                   eval      lldil1_ldor = loldor
6.21 c                   move      lolage        lldil1_lage
6.21 c     lldil1_key    chain     lldil1
6.21 c                   if        not %found
6.21 c                   eval      lldil1_lage = *blank
6.21 c     lldil1_key    chain     lldil1
6.21 c                   if        not %found
6.21 c                   eval      w_stat = '2'
6.21 c                   goto      end_pgm
6.21 c                   endif
6.21 c                   endif
      *
      *  Les varene som ligger på bestillingen, og sjekk varene som ligger der
      *
     c     lodtlr_key    setll     lodtlr
     c     lodtlr_key    reade     lodtlr
     c                   dou       %eof(lodtlr)
      * Sjekk varen (kun varenummer)
     c                   if        ldvare <> *blank
     c                   exsr      sjk_varer
      *
     c                   if        w_stat = '1'
     c                   goto      end_pgm
     c                   endif
     c                   endif
      *
     c     lodtlr_key    reade     lodtlr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_varer     begsr
      *
      * Sjekk EAN-nummer
      *
     c                   exsr      sjekk_ean
     c                   if        b_feil = *on
     c                   eval      w_stat = '1'
     c                   goto      end_varer
     c                   endif
      *
      * Sjekk Nobb-nummer
      *
     c                   if        ldnobb = 0
     c                   eval      w_stat = '1'
     c                   goto      end_varer
     c                   endif
      *
      * Sjekk om diversevare
      *
6.30 c                   if        1=2
     c                   call      'VL710R'
     c                   parm                    w_firm
     c                   parm                    ldvare
     c                   parm                    ldlage
     c                   parm                    w_vtyp
6.20 c                   parm                    w_udat
6.20 c                   parm                    w_lenr
6.20 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
      *
     c                   if        w_vtyp = 'D'
     c                   eval      w_stat = '1'
     c                   goto      end_varer
     c                   endif
6.30 c                   endif
      *
      * Sjekk matching mot LVR
      *
     c                   exsr      sjekk_match
     c                   if        b_feil = *on
     c                   eval      w_stat = '1'
     c                   goto      end_varer
     c                   endif
      *
      * Sjekk innkjøpsenheter - ikke nødvendig. Test er god nok i VE713R.
      *
     c                   if        1=2
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      w_stat = '1'
     c                   goto      end_varer
     c                   endif
     c                   endif
      *
      * Sjekk om overstyrt varetekst - for streng, da enkelte nobbnr skal
      * kunne overstyres på varetekst. (feks diversenr på dører/vinduer osv)
      *
6.10 c                   if        1=2
     c                   exsr      sjekk_tekst
     c                   if        b_feil = *on
     c                   eval      w_stat = '1'
     c                   goto      end_varer
     c                   endif
6.10 c                   endif
      *
     c     end_varer     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke EAN-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ean     begsr
      *
     c                   eval      b_feil = *off
     c                   eval      w_eann = 0
     c                   eval      w_east = *blank
      *
     c                   call      'VE713R'
     c                   parm                    w_firm
     c                   parm                    loldor
     c                   parm                    ldvare
     c                   parm                    ldenh1
     c                   parm                    w_eann
     c                   parm                    w_east
      *
     c                   if        w_east <> '1' or
     c                             w_eann = 0
     c                   eval      b_feil = *on
     c                   goto      end_ean
     c                   endif
      *
     c     end_ean       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke match mot LVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_match   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      jvarl1_vare = ldvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   endif
      *
     c     end_match     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke innkjøpsenheter mot LVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_enh     begsr
      *
     c                   eval      b_feil = *on
     c                   eval      vvenl1_vare = ldvare
     c                   eval      vvenl1_enhe = ldenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found and
     c                             veinen > 0
     c                   eval      b_feil = *off
     c                   endif
      *
     c     end_enh       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke overstyrt varetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_tekst   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   goto      end_tekst
     c                   endif
      *
     c                   if        vvtek1 <> jvtek1
     c                   eval      b_feil = *on
     c                   endif
      *
     c     end_tekst     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_stat
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare enhet pr. leverandør
      *
     c     vvepl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvepl1_pvar
     c                   kfld                    vvepl1_penh
     c                   kfld                    vvepl1_pldo
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les av bestillinger - hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les av bestillinger - linjer
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
      *
6.21  *
6.21  * Key for oppslag på leverandør distribusjon
6.21  *
6.21 c     lldil1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    lldil1_ldor
6.21 c                   kfld                    lldil1_lage
6.21  *
6.21  * Key for oppslag på leverandør
6.21  *
6.21 c     rlevl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    rlevl1_levr
      *
      *  Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      lohel1_numm = w_numm
     c                   eval      lohel1_suff = w_suff
     c                   eval      lodtlr_numm = w_numm
     c                   eval      lodtlr_suff = w_suff
      *
     c                   endsr
