     h option(*nodebugio) datedit(*dmy)
      /title  ASOFAK Gen. av faktura i Autofakt-format - styreprog.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO183R
      * Beskrivelse . . : Gen. av faktura i Autofakt-format - styreprog.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070205 GRO  Nytt program
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametre
      *
     d p_aarr          s              4  0
     d p_faty          s              2  0
6.nu d p_fnum          s              8  0
6.nu d p_tnum          s              8  0
     d p_omkj          s              1
     d p_rapp          s              1
     d p_anta          s              4  0
     d p_feil          s              1
      *
6.nu d p_parm          s             32
     d p_firm          s              3  0
     d p_vkun          s              6
     d p_kkun          s              6
     d p_filk          s              1
      *
      * Definering av datastruktur for parameter
      *
     d                 ds
6.nu d d_parm                        32
     d  d_aarr                        4  0 overlay(d_parm)
     d  d_otyp                        2    overlay(d_parm:5)
6.nu d  d_ffnr                        8  0 overlay(d_parm:7)
6.nu d  d_tfnr                        8  0 overlay(d_parm:15)
6.nu d  d_prog                       10    overlay(d_parm:23)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_dato          s               d   datfmt(*iso)
     d w_paar          s              4  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle kunder
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
      *
     c                   dow       not %eof(rkunl1)
      *
     c                   if        rkfaty = 30
     c                   move      d_parm        p_parm
     c                   move      rkkund        p_vkun
     c                   if        rkkoku <> *zero
     c                   move      rkkoku        p_kkun
     c                   else
     c                   eval      p_kkun = *blank
     c                   endif
      *
     c                   call      'FO183C'
     c                   parm                    p_aarr
     c                   parm                    p_faty
6.nu c                   parm                    p_fnum
6.nu c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_feil
     c                   parm                    p_vkun
     c                   parm                    p_kkun
     c                   parm                    p_filk
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_aarr
     c                   parm                    p_faty
6.nu c                   parm                    p_fnum
6.nu c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_feil
      *
      * Key for les på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Setter initielle data/dato/år
      *
     c                   eval      w_firm = l_firm
     c                   eval      p_filk = *blank
     c                   eval      p_feil = *blank
     c                   eval      p_anta = *zero
      *
     c                   time                    w_dato
      *
     c                   endsr
