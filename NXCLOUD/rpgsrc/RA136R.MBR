     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : NXCLOUD
      * Program . . . . : RL136R
      * Beskrivelse . . : Vedlikehold av kobling mot eksternt lager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071022 ASK  Nytt program
8.01  * 800   211122 ask  Enderet ekstern lagerkode fra N(2,0) til A(20) etter ønske fra NGN
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra36iu    uf a e           k disk    rename(ra36st:ra36iur)
     fra36ir    if   e           k disk    rename(ra36st:ra36irr)
     fra36i1    if   e           k disk    rename(ra36st:ra36i1r)
     fra36i2    if   e           k disk    rename(ra36st:ra36i2r)
      *
     fra136d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parameter
      *
     d p_fors          s                   like(rkrfsk)
      *
      * Definering av variabler i nøkler
      *
     d ra36iu_ruid     s                   like(rkruid)
     d ra36ir_ruid     s                   like(rkruid)
     d ra36i1_rfsk     s                   like(rkrfsk)
     d ra36i1_rsla     s                   like(rkrsla)
     d ra36i2_rfsk     s                   like(rkrfsk)
     d ra36i2_rsla     s                   like(rkrsla)
     d ra36i2_rela     s                   like(rkrela)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_firm          s                   like(rkrfir)
     d w_lage          s                   like(rkrsla)
     d w_elag          s                   like(rkrela)
     d w_ldor          s              6  0
     d w_fmtx          s             20
     d w_glnn          s             14  0
     d w_latx          s             20
     d w_letx          s             30
     d w_eltx          s             30
     d w_egln          s             14  0
     d w_elev          s              6  0
      *
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
     c/exec sql
     c+ SET OPTION DatFmt=*ISO,
     c+            SRTSEQ=*LANGIDUNQ,
     c+            LANGID=NOR,
     c+            commit=*none
     c/end-exec

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oppr = *on
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   eval      b_oppr = *off
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      ra36i1_rfsk = p_fors
     c     ra36i1_key    setll     ra36i1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   eval      c2fmat = b2fmat
     c                   eval      c2fmtx = b2fmtx
      *
     c                   eval      ra36ir_ruid = b1uid
     c     ra36ir_key    chain     ra36ir
      *
     c                   if        %found and
     c                             b_oppr = *off
     c                   eval      c2slag = rkrsla
     c                   eval      w_lage = rkrsla
     c                   exsr      finn_lager
     c                   eval      c2sltx = w_latx
     c                   eval      c2elag = rkrela
     c                   eval      w_elag = rkrela
     c                   exsr      finn_ekst
     c                   eval      c2eltx = w_eltx
     c                   eval      c2egln = w_egln
     c                   eval      c2edor = w_elev
     c                   eval      w_ldor = w_elev
     c                   exsr      finn_lev
     c                   eval      c2ltek = w_letx
     c     *dmy          move      rkreda        c2edat
     c                   eval      c2eusr = rkreus
     c                   eval      *in30 = *on
     c                   else
     c                   eval      c2slag = 0
     c                   eval      c2sltx = '  '
8.01 c                   eval      c2elag = ' '
     c                   eval      c2eltx = '  '
     c                   eval      c2egln = 0
     c                   eval      c2edor = 0
     c                   eval      c2ltek = '  '
     c                   eval      b1uid  = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   eval      *in31 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in31
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        *inkd
     c                   if        w_afld = 'C2SLAG'
     c                   exsr      lag_sok
     c                   endif
     c                   if        w_afld = 'C2ELAG'
     c                   exsr      ekst_sok
     c                   endif
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk at salgslager er gyldig
      *
     c                   eval      w_lage = c2slag
     c                   exsr      finn_lager
     c                   if        b_feil = *on
     c                   exsr      xc2msg1
     c                   goto      c2taga
     c                   else
     c                   eval      c2sltx = w_latx
     c                   endif
      *
      * Sjekk at eksternt lager er gyldig
      *
     c                   eval      w_elag = c2elag
     c                   exsr      finn_ekst
     c                   if        b_feil = *on
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   else
     c                   eval      c2eltx = w_eltx
     c                   endif
      *
      * Sjekk at kobling ikke er brukt før
      *
     c                   if        b_oppr = *on
     c                   eval      ra36i2_rsla = c2slag
     c                   eval      ra36i2_rela = c2elag
     c     ra36i2_key    chain     ra36i2r
     c                   if        %found(ra36i2)
     c                   exsr      xc2msg3
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Sjekk at salgslager ikke er koblet mot annet eksternt lager
      *
     c                   if        b_oppr = *on
     c                   eval      w_lage = c2slag
     c                   exsr      ekst_sjekk
     c                   if        b_feil = *on
     c                   exsr      xc2msg4
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      ra36iu_ruid = b1uid
     c     ra36iu_key    chain     ra36iur
      *
     c                   eval      rkrela = c2elag
     c                   time                    rkreti
     c                   time                    rkreda
     c                   eval      rkreus = l_user
      *
     c                   if        %found
     c                   update    ra36iur
     c                   else
     c                   eval      rkrfir = w_firm
     c                   eval      rkrfsk = p_fors
     c                   eval      rkrsla = c2slag
     c                   eval      rkrela = c2elag
     c                   write     ra36iur
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Kobling til selgende lager er lagt opp før
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg1       begsr
      *
     c                   exfmt     c2msg1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Ugyldig selgende lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg2       begsr
      *
     c                   exfmt     c2msg2
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * duplikat GLN på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg3       begsr
      *
     c                   exfmt     c2msg3
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * duplikat GLN på distribusjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg4       begsr
      *
     c                   exfmt     c2msg4
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra36ir_ruid = b1uid
     c     ra36ir_key    chain     ra36ir
     c                   if        %found
     c                   eval      d1fmat = rkrfsk
     c                   exsr      finn_fmtx
     c                   eval      d1fmtx = w_fmtx
     c                   eval      d1slag = rkrsla
     c                   eval      w_lage = rkrsla
     c                   exsr      finn_lager
     c                   eval      d1sltx = w_latx
     c                   eval      d1elag = rkrela
     c                   eval      w_elag = rkrela
     c                   exsr      finn_ekst
     c                   eval      d1sltx = w_eltx
     c                   else
     c                   goto      end_d1win
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      ra36iu_ruid = b1uid
     c     ra36iu_key    chain     ra36iu
      *
     c                   if        %found
     c                   delete    ra36iu
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ra36i1
      *
     c                   if        %eof or
     c                             rkrfir <> w_firm or
     c                             rkrfsk <> p_fors
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1uid  = rkruid
     c                   eval      b1slag = rkrsla
     c                   eval      w_lage = rkrsla
     c                   exsr      finn_lager
     c                   eval      b1sltx = w_latx
     c                   eval      b1elag = rkrela
     c                   eval      w_elag = rkrela
     c                   exsr      finn_ekst
     c                   eval      b1eltx = w_eltx
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ra36i1_key    setgt     ra36i1
     c                   readp     ra36i1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ra36i1
      *
     c                   if        %eof or
     c                             rkrfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     ra36i1_key    setll     ra36i1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   eval      b2fmat = p_fors
     c                   exsr      finn_fmtx
     c                   eval      b2fmtx = w_fmtx
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for lagersøk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_sok       begsr
      *
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   eval      c2slag = w_lage
     c                   eval      c2sltx = w_latx
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for eksternt lagersøk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ekst_sok      begsr
      *
     c                   call      'RA537R'
     c                   parm                    w_firm
     c                   parm                    w_elag
     c                   parm                    w_eltx
     c                   eval      c2elag = w_elag
     c                   eval      c2eltx = w_eltx
      *
     c                   endsr

      //
      // Subrutine for finne ekstern lager kobling
      //
       begsr ekst_sjekk;

         b_feil = *off;
         exec SQL
           select rkrela into :w_elag
             from ra36st
             where rkrfir = :w_firm
               and rkrsla = :w_lage;

         if sqlcod = 0 and
            w_elag <> c2elag;
           b_feil = *on;
         endif;
       endsr;

      //
      // Subrutine for finne forsendelsemåte tekst
      //
       begsr finn_fmtx;

         exec SQL
           select raqtxt into :w_fmtx
             from ra17pf
             where raqfir = :w_firm
               and raqkod = :p_fors;

       endsr;

      //
      // Subrutine for finne lagertekst
      //
       begsr finn_lager;

         b_feil = *off;
         exec SQL
           select rajtxt into :w_latx
             from ra10pf
             where rajfir = :w_firm
               and rajkod = :w_lage;

         if sqlcod < 0 or
            sqlcod = 100;
           w_latx = 'Ukjent lager';
           b_feil = *on;
         endif;
       endsr;

      //
      // Subrutine for finne eksternt lager info
      //
       begsr finn_ekst;

         b_feil = *off;
         exec SQL
           select rkenav, rkegln, rkelev
             into :w_eltx, :w_egln, :w_elev
             from ra37st
             where rkefir = :w_firm
               and rkeela = :w_elag;

         if sqlcod < 0 or
            sqlcod = 100;
           w_eltx = 'Ukjent lager';
           b_feil = *on;
         endif;
       endsr;

      //
      // Subrutine for finne leverandørnavn
      //
       begsr finn_lev;

         exec SQL
           select rlnavn into :w_letx
             from rlevpf
             where rlfirm = :w_firm
               and rllevr = :w_ldor;

         if sqlcod < 0 or
            sqlcod = 100;
           w_letx = '  ';
         endif;

       endsr;

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Parameter
      *
      *
     c     *entry        plist
     c                   parm                    p_fors
      *
      * Key for oppdatering av koblingspost
      *
     c     ra36iu_key    klist
     c                   kfld                    ra36iu_ruid
      *
      * Key for les på koblingspost
      *
     c     ra36ir_key    klist
     c                   kfld                    ra36ir_ruid
      *
      * Key for posisjonering på forsendelsesmåte
      *
     c     ra36i1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra36i1_rfsk
     c                   kfld                    ra36i1_rsla
      *
      * Key for oppslag på salgslager
      *
     c     ra36i2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra36i2_rfsk
     c                   kfld                    ra36i2_rsla
     c                   kfld                    ra36i2_rela
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      *in22 = *on
     c                   eval      ra36i1_rfsk = p_fors
     c                   eval      ra36i2_rfsk = p_fors
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     ra36i1_key    setll     ra36i1
      *
     c                   eval      ra36i1_rsla = 0
     c                   exsr      crt_subfile
      *
     c                   endsr
