      /TITLE  ASOFAK Vise informasjon om hvem som holder ordre
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO104R
      * Beskrivelse . . : Samkjøre ordre tilbake til basisordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170599 AMD  Nytt program
      * R3.11 220300 AMD  Leveringstype er tatt med i datastruktur hlrec
      *                   I tillegg legges det ut referanse på 10 posisjoner
      * R3.31 160301 AMD  Oppdatering av lagerregister ble ikke riktig ved
      *                   tilbakeføring til basisordre, dette er korrigert.
5.31  * R5.31 100104 ASK  Lagt inn endringer for lager på linjenivå.
5.60  * R5.60 091107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      *
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  110610 bof  Justert i utvidelse av parm VL001R
6.12  * 6.10  300710 bsa  Skal ikke hente inn priser ved samkjøring
6.12  *                   av ordre.
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
7.00  * R7.00 060320 bkv  132 bilde
      *
8 00  *       150323 sst  Beregne memosaldo m/FO763R
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *K0000  040924 bsa  Oppdaterer tilhørende bestillingslinje med
8.02  *K0000              nytt suffiks/linjenummer.
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
8.02 flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
      *
     ffo104d    cf   e             workstn
      *
      * Datastruktur ved oppdatering av lager
      *
     d                 ds
6.nu d d_lrec                       128
     d  d_vare                       15    overlay(d_lrec)
     d  d_lage                        2  0 overlay(d_lrec:16)
     d  d_dato                         d   overlay(d_lrec:18) datfmt(*iso)
     d  d_time                         t   overlay(d_lrec:28) timfmt(*iso)
     d  d_otyp                        2    overlay(d_lrec:36)
     d  d_ltyp                        2    overlay(d_lrec:38)
     d  d_anta                       11  3 overlay(d_lrec:40)
     d  d_kopr                        9  2 overlay(d_lrec:51)
6.nu d  d_refr                       20    overlay(d_lrec:60)
     d  d_syst                        1    overlay(d_lrec:80)
     d  d_altk                        2    overlay(d_lrec:81)
     d  d_rest                        1    overlay(d_lrec:83)
     d  d_type                        1    overlay(d_lrec:84)
     d  d_enhe                        3    overlay(d_lrec:85)
3.11 d  d_inlr                        1    overlay(d_lrec:88)
3.11 d  d_lety                        1    overlay(d_lrec:89)
6.nu d  d_onum                        8  0 overlay(d_lrec:90)
6.nu d  d_olin                        4  0 overlay(d_lrec:98)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
6.NU d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
8.00 d p_firm          s              3
8.00 d p_kund          s              6
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
8.02 d lodtlu_numm     s                   like(ldnumm)
8.02 d lodtlu_suff     s                   like(ldsuff)
8.02 d lodtlu_line     s                   like(ldline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_line          s                   like(fdline)
     d w_enor          s              1
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Klargjøring av data for A1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      a1numm = p_numm
     c                   eval      a1suff = p_suff
     c                   eval      a2suff = *zero
      *
      * Merk av at basisordre nå er til behandling for samkjøring
      *
     c                   eval      fohelu_numm = a1numm
     c                   eval      fohelu_suff = a1suff
     c                   exsr      xmerk_ord
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1 Behandler skjembilde for samkjøring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      * Gå tilbake til forrige bilde
      *
     c                   if        *inkc
     c                   eval      fohelu_numm = a1numm
     c                   eval      fohelu_suff = a1suff
     c                   exsr      xopph_ord
     c                   goto      xslutt
     c                   endif
      *
      * Sjekk om ordre med oppgitt suffix ble funnet
      *
     c                   eval      fohel1_numm = a1numm
     c                   eval      fohel1_suff = a2suff
     c     fohel1_key    chain     fohel1r                            90
      *
     c                   if        *in90  = *on
     c                   eval      *in31  = *on
     c                   goto      a1tagb
     c                   endif
      *
      * Er suffix ordre til behandling ?
      *
     c                   if        fokode <> 0
     c                   eval      *in32  =  *on
     c                   goto      a1tagb
     c                   endif
      *
      * Merk av at suffix-ordre nå er til behandling for samkjøring
      *
     c                   eval      fohelu_numm = a1numm
     c                   eval      fohelu_suff = a2suff
     c                   exsr      xmerk_ord
      *
      * Behandle samkjøringen
      *
     c                   exsr      xoppd_ord
      *
      * Merk av at ordre nå er ferdigbehandlet
      *
     c                   eval      fohelu_numm = a1numm
     c                   eval      fohelu_suff = a1suff
     c                   exsr      xopph_ord
      *
      * Avslutt program
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for merking av at basisordre er i bruk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xmerk_ord     begsr
      *
     C                   call      'FO709R'
     C                   parm                    fohelu_numm
     C                   parm                    fohelu_suff
      *
     C     fohelu_key    chain     fohelur                            91
     C                   eval      fokode = 1
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     C                   update    fohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppheving av merking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopph_ord     begsr
      *
     C                   call      'FO709R'
     C                   parm                    fohelu_numm
     C                   parm                    fohelu_suff
      *
     C     fohelu_key    chain     fohelur                            91
     C                   eval      fokode = 0
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     C                   update    fohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av ordre (sam-kjøring)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xoppd_ord     begsr
      *
      * Tilbakefør basisordre
      *
     c                   eval      fohel1_numm = a1numm
     c                   eval      fohel1_suff = a1suff
     c     fohel1_key    chain     fohel1r                            90
      *
     c                   eval      fodtl1_numm = a1numm
     c                   eval      fodtl1_suff = a1suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1r                                90
      *
     c                   dow       *in90  = *off
3.31 c                   eval      fdanta =  (fdanta * -1)
     c                   exsr      xtilbake
     c     fodtl1_key    reade     fodtl1r                                90
     c                   enddo
      *
      * Tilbakefør suffix-ordre
      *
     c                   eval      fohel1_numm = a1numm
     c                   eval      fohel1_suff = a2suff
     c     fohel1_key    chain     fohel1r                            90
      *
     c                   eval      fodtl1_numm = a1numm
     c                   eval      fodtl1_suff = a2suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1r                                90
      *
     c                   dow       *in90  = *off
3.31 c                   eval      fdanta =  (fdanta * -1)
     c                   exsr      xtilbake
     c     fodtl1_key    reade     fodtl1r                                90
     c                   enddo
      *
8.00  * Oppdatere memosaldo basisordre. gjøres lenger nede(FO730/763)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hente suffix-linjer inn til basis-ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finn siste linjenr på basisordre
      *
     c                   eval      fodtlu_numm = a1numm
     c                   eval      fodtlu_suff = a1suff
     c                   eval      fodtlu_line = 9999
     c     fodtlu_key    setll     fodtl1r
     c                   eval      *in90 = *off
     c                   readp     fodtl1r                                90
      *
     c                   eval      w_line = 0010
     c                   if        *in90  = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = a1numm
     c                   if        fdsuff = a1suff
     c                   eval      w_line = fdline
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * Les suffixlinjer, og flytt de over til basisordre
      *
     c                   eval      fodtl1_numm = a1numm
     c                   eval      fodtl1_suff = a2suff
     c     fodtl1_key    setll     fodtlu
     c     fodtl1_key    reade     fodtlur                                90
      *
     c                   dow       *in90  = *off
     c                   eval      w_line = w_line + 10
     c                   eval      fdsuff = a1suff
     c                   eval      fdline = w_line
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
8.02  * Oppdater tilhørende ordrelinjer med ny bestillingsreferanse
8.02 c                   if        fdbenr <> 0
8.02 c                   eval      lodtlu_numm = fdbenr
8.02 c                   eval      lodtlu_suff = fdbsuf
8.02 c                   eval      lodtlu_line = fdblin
8.02 c     lodtlu_key    chain     lodtlu
8.02 c                   if        %found
8.02 c                   eval      ldorsu = fdsuff
8.02 c                   eval      ldorli = fdline
8.02 c                   time                    ldedat
8.02 c                   time                    ldetim
8.02 c                   eval      ldeusr = l_user
8.02 c                   update    lodtlur
8.02 c                   endif
8.02 c                   endif
      *
     c     fodtl1_key    reade     fodtlur                                90
     c                   enddo
      *
      * Slett deretter ordrehode på suffix-ordre
      *
     c                   eval      fohelu_numm = a1numm
     c                   eval      fohelu_suff = a2suff
     c     fohelu_key    chain     fohelur                            90
     c                   if        *in90 = *off
     c                   delete    fohelur
     c                   endif
3.31  *
3.31  * Oppdatere ny sammenkjørt basisordre
3.31  *
3.31 c                   eval      fohel1_numm = a1numm
3.31 c                   eval      fohel1_suff = a1suff
3.31 c     fohel1_key    chain     fohel1r                            90
3.31  *
3.31 c                   eval      fodtl1_numm = a1numm
3.31 c                   eval      fodtl1_suff = a1suff
3.31 c     fodtl1_key    setll     fodtl1
3.31 c     fodtl1_key    reade     fodtl1r                                90
3.31  *
3.31 c                   dow       *in90  = *off
3.31 c                   exsr      xtilbake
3.31 c     fodtl1_key    reade     fodtl1r                                90
3.31 c                   enddo
3.31  *
3.31  * Oppdater ny sammenkjørt basis-ordre
3.31  *
6.12  ** Vi skal ikke hente inn priser på nytt. De som ligger på ordren gjelder.
3.31 c**6.12             eval      w_enor = '0'
6.12 c                   eval      w_enor = ' '
3.31 c                   call      'FO730R'
3.31 c                   parm                    w_enor
3.31 c                   parm                    w_firm
6.NU c                   parm                    a1numm
3.31 c                   parm                    a1suff
3.31  *
8.00  * Oppdatere memosaldo basisordre  Dette gjøres i FO730R
3.31  *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakeføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xtilbake      begsr
      *
      * Tilbakeføring
      *
     c                   if        faalag <> 0
      *
     c                   eval      d_vare =  fdvare
5.31 c                   eval      d_lage =  fdlage
     c                   eval      d_enhe =  fdenh1
     c                   eval      d_dato =  *loval
     c                   eval      d_time =  *loval
     c                   eval      d_otyp =  footyp
     c                   eval      d_ltyp =  fdltyp
      *
     c                   eval      d_anta =  fdanta
     c                   eval      d_kopr =  fdkopr
      *
     c                   eval      d_refr =  *blank
     c                   eval      d_syst =  'F'
     c                   eval      d_altk =  *blank
     c                   eval      d_rest =  *blank
     c                   eval      d_refr = 'O:' + %char(fdnumm) +
     c                                      'L:' + %char(fdline)
     c                   eval      d_onum = fdnumm
     c                   eval      d_olin = fdline
3.11 c                   move      fdlety        d_lety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec =  d_lrec
      *
     c                   call      'VL001R'
     c                   parm                    w_v001            1
6.nu c                   parm                    w_lrec          128
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for Ordre-hode-tabell les
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for Ordre-hode-tabell update
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for Ordre-linje-tabell les
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for Ordre-linje-tabell update
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
8.02  *
8.02  * Key for Bestilling-linje-tabell update
8.02  *
8.02 c     lodtlu_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    lodtlu_numm
8.02 c                   kfld                    lodtlu_suff
8.02 c                   kfld                    lodtlu_line
      *
      * Key for Status-tabell les
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm      = l_firm
      *
      * Henter informasjon fra status-tabell
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
     c                   endsr
      *
