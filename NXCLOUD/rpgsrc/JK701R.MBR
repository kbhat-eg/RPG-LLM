     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : asvadm
      * Program . . . . : jk701r
      * Beskrivelse . . : Leser CSV fil
      *
      * Spesialversjon. :
      * Tilpasset for . : D27
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       061118 bof  Nytt program
      *       091118 bof  Endret slik at ikke kaller jk702R
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
     fjvkhlu    uf a e           k disk    rename(jvkhpfr:jvkhlur)
     fjvkll1    if   e           k disk    rename(jvklpfr:jvkll1r)
     fjvkllu    uf a e           k disk    rename(jvklpfr:jvkllur)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
      *
      *  Datastruktur - excel-fil
      *
     d                 ds
     d d_ex                         512
     d  d_fill                        2    overlay(d_ex:1)
     d  d_vare                        8    overlay(d_ex:3)
     d  d_uvar                        8    overlay(d_ex:11)
     d    d_uvarn                     8  0 overlay(d_uvar:1)
     d  d_ldor                        6    overlay(d_ex:19)
     d    d_ldorn                     6  0 overlay(d_ldor:1)
     d  d_hlev                        3    overlay(d_ex:25)

      * Definering av variable i nøkler
      *
     d jvkhl1_kvnr     s                   like(jvkvnr)
     d jvkhlu_kvnr     s                   like(jvkvnr)
     d jvkll1_qvnr     s                   like(jvqvnr)
     d jvkll1_quno     s                   like(jvquno)
     d jvkll1_qldo     s                   like(jvqldo)
     d jvkllu_qvnr     s                   like(jvqvnr)
     d jvkllu_quno     s                   like(jvquno)
     d jvkllu_qldo     s                   like(jvqldo)
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
      *
     d jvarl1_vare     s                   like(jvvare)
     d jvarl3_nobb     s                   like(jvnobb)
     d w_ldor          s                   like(jvldor)
     d w_ldox          s              6
     d w_vare          s                   like(jvvare)
     d s_vare          s                   like(jvvare)
     d w_dumm          s              1    inz('*')

     d w_pos           s              5u 0
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_posb          s              2  0
     d len             s              3  0
     d st              s              3  0
     d w_3x            s              3
     d w_dato          s              6
     d w_6x            s              6
     d w_2x            s              2
     d w_stat          s              1
     d w_firm          s                   like(jvqfir)
     d b_ok            s              1    inz(*off)
     d c_null          s             15    inz('000000000000000')
     d digits          c                   ' 0123456789'
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO , commit=*none
     C/End-Exec
      *
      *  Lese file
      *
     c                   read      lwexpf                                 90
     c                   dow       *in90 = *off
     c                   exsr      utled_ds
      *
      *  Sjekk at ikke heading-rad er lest inn
      *
     c                   if        b_ok = *on
     c                   exsr      behand
     c                   endif
      *
     c
      *
     c                   read      lwexpf                                 90
     c                   enddo
      *
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behand        begsr
      *
     c     ' ':'0'       xlate     d_vare        d_vare
     c     ' ':'0'       xlate     d_uvar        d_uvar
      *
     c                   if        s_vare <> d_Vare
     c*                   if        s_vare <> *blank
     c*                   movel     s_vare        w_vare
     c*                   call      'JK702R'
     c*                   parm                    w_vare
     c*                   parm                    w_ldor
     c*                   parm                    w_dumm
     c*                   endif
     c                   eval      s_vare = d_vare
     c                   endif
     c                   eval      jvarl1_vare = d_vare
     c     jvarl1_key    chain     jvarl1r
     c                   if        %found(jvarl1)
      * sjekk nobbnr
     c                   eval      jvarl3_nobb = d_uvarn
     c     jvarl3_key    chain     jvarl3
     c                   if        %found(jvarl3)
      *
     c                   if        d_hlev = 'JA'
     c                   clear                   jvkhlur
     c                   movel     d_vare        jvkhlu_kvnr
     c     jvkhlu_key    chain     jvkhlu
     c                   eval      jvkldo = d_ldorn
     c                   eval      jvknup = 1
     c                   time                    jvkeda
     c                   time                    jvketi
     c                   eval      jvkeus = l_user
     c                   if        %found(jvkhlu)
     c                   update    jvkhlur
     c                   else
     c                   eval      jvkfir = w_firm
     c                   movel     d_Vare        jvkvnr
     c                   time                    jvkoda
     c                   time                    jvkoti
     c                   write     jvkhlur
     c                   endif
      * dann undervare på hoved
     c                   clear                   jvkllur
     c                   movel     d_vare        jvkllu_qvnr
     c                   eval      jvkllu_quno = d_uvarn
     c                   eval      jvkllu_qldo = d_ldorn
     c     jvkllu_key    chain     jvkllu
      *
     c                   eval      jvqofa = 1
     c                   time                    jvqeda
     c                   time                    jvqeti
     c                   eval      jvqeus = l_user
     c                   if        %found(jvkllu)
     c                   update    jvkllur
     c                   else
     c                   eval      jvqfir = w_firm
     c                   movel     d_Vare        jvqvnr
     c                   eval      jvquno = d_uvarn
     c                   eval      jvqldo = d_ldorn
     c                   time                    jvqoda
     c                   time                    jvqoti
     c                   eval      jvqous = l_user
     c                   write     jvkllur
     c                   endif
     c                   else
      * ikke hovedlev.
     c                   clear                   jvkllur
     c                   movel     d_vare        jvkllu_qvnr
     c                   eval      jvkllu_quno = d_uvarn
     c                   eval      jvkllu_qldo = d_ldorn
     c     jvkllu_key    chain     jvkllu
      *
     c                   eval      jvqofa = 1
     c                   time                    jvqeda
     c                   time                    jvqeti
     c                   eval      jvqeus = l_user
     c                   if        %found(jvkllu)
     c                   update    jvkllur
     c                   else
     c                   eval      jvqfir = w_firm
     c                   movel     d_Vare        jvqvnr
     c                   eval      jvquno = d_uvarn
     c                   eval      jvqldo = d_ldorn
     c                   time                    jvqoda
     c                   time                    jvqoti
     c                   eval      jvqous = l_user
     c                   write     jvkllur
     c                   endif

     c                   endif
     c                   endif
     c                   endif
     c     behand_ut     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     exclin        exclin
      *
      * vare    A
      *
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_vare = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_vare = *blank
     c                   leavesr
     c                   endif
     c                   if        %check(digits : d_vare) <>0
     c                   leavesr
     c                   endif
      *
      * under vare   B
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_uvar = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_uvar = *blank
     c                   leavesr
     c                   endif
     c                   if        %check(digits : d_uvar) <>0
     c                   leavesr
     c                   endif
      *
      * leverandør   C
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_ldor = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_ldor = *blank
     c                   leavesr
     c                   endif
     c                   if        %check(digits : d_ldor) <>0
     c                   leavesr
     c                   endif

     c                   eval      len    = %len(%trim(d_ldor))
     c                   eval      st = 7 - len
     c                   eval      w_6x  = *blank
     c                   eval      %subst(w_6x:st:len) = %subst(d_ldor:1:len)
     c                   eval      d_ldor = w_6x
     c                   eval      d_ldor = %xlate(' ':'0':d_ldor)
      *
      * Ledige D, E F, G,H,
      *  D
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *  E
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *  F
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *  G
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *  H
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * best. mengde
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   if        w_posi > 0
     c                   eval      d_hlev = %trim(%subst(exclin:1:w_posi-1))
     c                   else
     c                   eval      d_hlev = %trim(%subst(exclin:1:3))
     c                   endif
     c                   eval      b_ok = *on
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for les vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_Vare
      *
      * Key for les vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb

      * Key for posisjonering på nobbnummer
      *
     c     jvkll1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvkll1_qvnr
     c                   kfld                    jvkll1_quno
     c                   kfld                    jvkll1_qldo
      *
      * Key for oppdatering av logistikk undervare
      *
     c     jvkllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvkllu_qvnr
     c                   kfld                    jvkllu_quno
     c                   kfld                    jvkllu_qldo
      *
      *
      * Key for posisjonering på varenummer
      *
     c     jvkhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvkhl1_kvnr
      *
      * Key for oppdatering av logistikkvare
      *
     c     jvkhlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvkhlu_kvnr
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_ldox = *blank
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'LOGILEV   '
      *
      * Sjekk om byggdok eller cobuilder aktivering er på/av
      *
     c     afpslr_key    chain     afpslrr
     c                   if        %found(afpslr)
     c                   eval      w_ldox = afudss
     c                   endif
     c                   if        w_ldox = *blank
     c                   eval      w_ldox = '900099'
     c                   endif
     c                   move      w_ldox        w_ldor
      *
     c                   endsr
