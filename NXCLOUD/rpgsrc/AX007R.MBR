     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . :
      *  Program . . . . : AX007R
      *  Beskrivelse . . : MVAKODE   Legger ut XML til SAF-T
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
Versj *Uts   Dato Usr Referanse Beskrivelse
      *____________________________________________________________________________________________
      *K00-200420               START FOR VERSJON K00
7.00  *K00 200420 SST NXS-4426  Utrulling av SAFT-Rapporter til alle kundene
7.01  *K00 090222 SST NXS-11659 Justering av std mvakode som ikke er definert.
      *
      *
      *
      *____________________________________________________________________________________________
      *
      *  Dette programmet henter data fra ADBxxx/ra05PF m/raefir = Firma
      *
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
     frat5pf    if   e           k disk    rename(rat5pfr:rat5pfr)
     fral5pf    if   e           k disk    rename(ral5pfr:ral5pfr)
     frxmll1    if   e           k disk    rename(rxmlpfr:rxmll1r)
      *
     faxmlut    o    e             disk
      *
      *
     d w_firm          s                       like(raefir)
     d ra05l1_ekod     s                       like(raekod)
      *
     d rat5pf_ekod     s                       like(rtekod)
     d ral5pf_ekod     s                       like(rtlkod)
     d ral5pf_ltda     s                       like(rtltda)
      *
     d rxmll1_head     s                       like(axhead)
     d rxmll1_linj     s                       like(axlinj)
      *
     d w_TaxCode       s              2
     d text            s             50
     d i               s              9S 0
     d f               s              3
     d t               s              3
     d p_perfm         s              2
     d p_perfa         s              4
     d p_pertm         s              2
     d p_perta         s              4
     d p_pfm           s              2S 0
     d p_pfa           s              4S 0
     d p_ptm           s              2S 0
     d p_pta           s              4S 0
     d p_hcom          s             50
      *
     d w_fdat          s              8S 0
     d w_tdat          s              8S 0
     d w_td_iso        s                     like(rtltda)
     d w_st_iso        s                     like(rtltda)
     d w_start         s             10
     d w_slutt         s             10
     d w_mpro          s                     like(raepro)
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Start
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     start         tag
      *
      *    TaxTable
      *
     c                   eval      rxmll1_head  = 'MVAKODE'
     c                   eval      rxmll1_linj  = 10
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *    TaxTableEntry
      *
     c                   eval      rxmll1_linj  = 20
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *   TaxType
      *
     c                   eval      rxmll1_linj  = 30
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     'MVA'         text
     c                   exsr      redut
     c                   endif
      *
      *   TaxDescription
      *
     c                   eval      rxmll1_linj  = 40
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   exsr      redut
     c                   endif
      *
     c                   read      ra05l1
      *
     c     TaxCodeDetailstag
      *
     c                   if        not %eof(ra05l1) and
     c                             raefir = l_firm
      *
      *  Sjekk om momskode har blitt endret i perioden
      *
     c                   eval      w_start = *blank
     c                   eval      ral5pf_ekod = raekod
     c                   eval      w_fdat = p_pfa * 10000 + p_pfm * 100 + 01
     c     *ymd          move      w_fdat        ral5pf_ltda
     c                   eval      w_tdat = p_pta * 10000 + p_ptm * 100 + 01
     c     *ymd          move      w_tdat        w_td_iso
     c                   eval      w_td_iso = w_td_iso + %months(1) - %days(1)
     c     ral5pf_key    setll     ral5pf
     c                   read      ral5pf
     c                   dow       not %eof(ral5pf)
     c                             and rtlkod = raekod
     c                             and rtltda <= w_td_iso
     c                   eval      w_slutt = %char(rtltda)
     c                   if        w_td_iso = rtltda
     c                   eval      w_slutt = *blank
     c                   endif
      *
     c                   eval      w_mpro = rtlmpr
     c                   exsr      legg_ut_mva
      *
     c                   read      ral5pf
      *
     c                   enddo
     c                   eval      w_slutt = *blank
      *
      * debug start
     c                   eval      w_start = w_start
     c                   eval      rtltda = rtltda
      * debug end
     c                   if        w_st_iso < w_td_iso
     c                   eval      w_mpro = raepro
     c                   exsr      legg_ut_mva
     c                   endif
     c                   eval      w_st_iso = *loval
      *
      *   Les neste mvakode
      *
     c                   read      ra05l1
     c                   if        not %eof(ra05l1) and
     c                             raefir = l_firm
     c                   goto      TaxCodeDetails
     c                   endif
      *
      *   /TaxTableEntry
      *
     c                   eval      rxmll1_linj  = 130
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *   /TaxTable
      *
     c                   eval      rxmll1_linj  = 140
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Rediger og skriv xml-melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     redut         begsr
      *
     c                   if        axmaop = 'M' or
     c                             text  <> *blank
     c                   eval      i = %scan('?V?':axxmlt)
     c                   if        i > 0
     c                   if        axtype = 'F'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      TWrite
     c                   endif
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      TWrite
     c                   endif
     c                   eval      i = %scan('?F?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      TWrite
     c                   endif
     c     TWrite        tag
     c                   write     axmlutr
     c                   endif
     c                   eval      text = *blank
      *
     c                   endsr
      * ______________________________________________________________________
      *
     c     Legg_Ut_Mva   begsr
      *
      *    TaxCodeDetails
      *
     c                   eval      rxmll1_linj  = 50
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
      *    TaxCode
      *
     c                   eval      rxmll1_linj  = 60
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     raekod        text
     c                   exsr      redut
     c                   endif
      *
      *  Sjekk om momskode har blitt endret i perioden
      *
     c                   if        w_start <> *blank
     c                             or w_slutt <> *blank
     c                   eval      rxmll1_linj = 63
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      text = w_start
     c                   exsr      redut
     c                   endif
     c                   eval      rxmll1_linj = 66
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      text = w_slutt
     c                   exsr      redut
     c                   endif
     c                   eval      w_start = %char(rtltda + %days(1))
     c                   eval      w_st_iso = rtltda + %days(1)
      *
     c                   endif
     c
      *
      *   Description
      *
     c                   eval      rxmll1_linj  = 70
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     raetxt        text
     c                   exsr      redut
     c                   endif
      *
      *   TaxPercentage
      *
     c                   eval      rxmll1_linj  = 80
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     w_mpro        text
     c                   eval      text = %subst(text:1:2) + '.' +
     c                                    %subst(text:3:2)
     c                   exsr      redut
     c                   endif
      *
      *   Country
      *
     c                   eval      rxmll1_linj  = 90
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   exsr      redut
     c                   endif
      *
      *   StandardTaxCode
      *
     c                   eval      rxmll1_linj  = 100
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
      *
     c                   move      raekod        rat5pf_ekod
     c     rat5pf_key    chain     rat5pf
     c                   move      *blank        w_taxCode
     c                   if        %found(rat5pf)
7.01 c                             and rtesmk <> *zero
     c                   movel     rtesmk        w_taxCode
7.01 c                   else
7.01 c                   eval      w_taxCode = 'NA'
     c                   endif
      *
     c                   movel     w_taxCode     text
     c                   exsr      redut
     c                   endif
      *
      *   BaseRate
      *
     c                   eval      rxmll1_linj  = 110
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   movel     raebpr        text
     c                   eval      text = %subst(text:1:3) + '.' +
     c                                    %subst(text:4:2)
     c                   exsr      redut
     c                   endif
      *
      *   /TaxCodeDetails
      *
     c                   eval      rxmll1_linj  = 120
     c     rxmll1_key    chain     rxmll1
     c                   if        %found(rxmll1 )
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
     c                   endsr
      * ______________________________________________________________________
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
      *
     c     rat5pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rat5pf_ekod
      *
     c     ral5pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ral5pf_ekod
     c                   kfld                    ral5pf_ltda
      *
     c     rxmll1_key    klist
     c                   kfld                    rxmll1_head
     c                   kfld                    rxmll1_linj
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter verdier fra LDA / Legger inn verdier i felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *dtaara       define                  ax000par        100
     c     *lock         in        *dtaara
     c                   eval      p_perfm = %subst(ax000par:1:2)
     c                   eval      p_perfa = %subst(ax000par:3:4)
     c                   eval      p_pertm = %subst(ax000par:7:2)
     c                   eval      p_perta = %subst(ax000par:9:4)
     c                   eval      p_hcom  = %subst(ax000par:13:50)
      *
     c                   move      p_perfm       p_pfm
     c                   move      p_perfa       p_pfa
     c                   move      p_pertm       p_ptm
     c                   move      p_perta       p_pta
      *
     c                   movel     'MVAKODE'     rxmll1_head
     c                   move      *zero         rxmll1_linj
      *
     c                   eval      w_firm      = l_firm
     c                   eval      ra05l1_ekod  = *blank
     c     ra05l1_key    setll     ra05l1
      *
     c                   endsr
