     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . :
      *  Program . . . . : AX390R
      *  Beskrivelse . . : HOVEDBOK: Beregner summer for utvalget
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
Versj *Uts   Dato Usr Referanse Beskrivelse
      *____________________________________________________________________________________________
      *K00-200420               START FOR VERSJON K00
7.00  *K00 200420 SST NXS-4426  Utrulling av SAFT-Rapporter til alle kundene
7.01  *K00 081121 SST NXS-10314 Justering av behandling av firma
8.00  *K00 100222 sst NXS-11659 Tilpasset ny RHTRL5
8.01  *K00 140222 sst NXS_11659 Telle alle transer med samme bilagsnummer som et bilag
      *
      *____________________________________________________________________________________________
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Dette programmet henter data fra ADBxxx/RHTRPF m/RBFIRM = Firma
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhtrl5    if   e           k disk    rename(rhtrpfr:rhtrl5r)
      *
      *
     d rhtrl5_firm     s                       like(rbfirm)
     d rhtrl5_raar     s                       like(rbraar)
     d rhtrl5_rper     s                       like(rbrper)
     d rhtrl5_biln     s                       like(rbbiln)
8.01 d*rhtrl5_jour     s                       like(rbjour)
      *
      *
     d w_firm          s              3A   inz('000')
     d w_firmn         s              3S 0
     d w_biln          s                   like(rbbiln)
8.00 d w_jour          s                   like(rbjour)
      *
     d w_sum_ant       s             15S 0
     d w_sum_deb       s             15S 3
     d w_sum_kre       s             15S 3
      *
      *
     d p_perfm         s              2
     d p_perfa         s              4
     d p_pertm         s              2
     d p_perta         s              4
     d p_pfm           s              2S 0
     d p_pfa           s              4S 0
     d p_ptm           s              2S 0
     d p_pta           s              4S 0
     d p_hcom          s             50
      *
     d p_bilfra        s                       like(rbbiln)
     d p_biltil        s                       like(rbbiln)
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Starter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Nullstiller akk.felter
      *
     c                   eval      w_sum_ant  = *zero
     c                   eval      w_sum_deb  = *zero
     c                   eval      w_sum_kre  = *zero
      *
      *    Posisjoner Hovedbok på fra periode
      *
     c                   eval      rhtrl5_firm = w_firmn
     c                   eval      rhtrl5_raar = p_pfa
     c                   eval      rhtrl5_rper = p_pfm
     c                   eval      rhtrl5_biln = *zero
8.01 c*                  eval      rhtrl5_jour = *zero
     c     rhtrl5_key    setll     rhtrl5
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Behandle alle bilagstransaksjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     XSTART        tag
      *
     c                   read      rhtrl5
      *
      *  Hopper ut dersom utenfor parameter-grenser
      *
     c                   if        %eof(rhtrl5)
     c                   goto      xslutt
     c                   endif
      *
     c                   if        rbfirm <> w_firmn
     c                   goto      xslutt
     c                   endif
      *
     c                   if        rbraar <  p_pfa
     c                   goto      xstart
     c                   endif
     c                   if        rbraar >  p_pta
     c                   goto      xstart
     c                   endif
      *
     c                   if        rbraar =  p_pfa
     c                   if        rbrper <  p_pfm
     c                   goto      xstart
     c                   endif
     c                   endif
      *
     c                   if        rbraar =  p_pta
     c                   if        rbrper >  p_ptm
     c                   goto      xstart
     c                   endif
     c                   endif
      *
     c                   if        rbbiln <  p_bilfra
7.00 c                             and p_bilfra <> 0
     c                   goto      xstart
     c                   endif
     c                   if        rbbiln >  p_biltil
7.00 c                             and p_biltil <> 0
     c                   goto      xstart
     c                   endif
      *
      *    Her skal man komme tilbake for hvert nytt bilag
      *
     c                   if        rbbiln <> w_biln
8.01 c*                            or rbjour <> w_jour
     c                   eval      w_sum_ant = w_sum_ant +1
     c                   eval      w_biln = rbbiln
8.00 c                   eval      w_jour = rbjour
     c                   endif
      *
     c                   exsr      Bilag
      *
      *   Les neste konto(postering)
      *
     c                   goto      XSTART
      *
      *    Siste bilag er nå behandlet
      *
     c     XSLUTT        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine = Bilag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     Bilag         begsr
      *
      *
     c                   if        rbneto < 0
     c                   eval      w_sum_deb = w_sum_deb + (rbneto * -1)
     c                   else
     c                   eval      w_sum_kre = w_sum_kre + (rbneto *  1)
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
     c     rhtrl5_key    klist
     c                   kfld                    rhtrl5_firm
     c                   kfld                    rhtrl5_raar
     c                   kfld                    rhtrl5_rper
     c                   kfld                    rhtrl5_biln
8.01 c*                  kfld                    rhtrl5_jour
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    ax000par        100
     c                   parm                    p_bilfra
     c                   parm                    p_biltil
     c                   parm                    w_sum_ant
     c                   parm                    w_sum_deb
     c                   parm                    w_sum_kre
      *
      *  Behandler local Data Area
      *
     c                   eval      p_perfm = %subst(ax000par:1:2)
     c                   eval      p_perfa = %subst(ax000par:3:4)
     c                   eval      p_pertm = %subst(ax000par:7:2)
     c                   eval      p_perta = %subst(ax000par:9:4)
      *
     c                   move      p_perfm       p_pfm
     c                   move      p_perfa       p_pfa
     c                   move      p_pertm       p_ptm
     c                   move      p_perta       p_pta
      *
7.01 c                   movel     l_firm        w_firmn
      *
     c                   endsr
