      /title  Spørring mot bruker-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASPGPL                                       *
      *  Program . . . . : AR750R                                       *
      *  Beskrivelse . . : Spørring mot bruker-register                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     far750d    cf   e             workstn sfile(b1wsf:srrn01)
      *
     fausrl2    if   e           k disk    rename(ausrpfr:ausrl2r)
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B2 Behandle åpningsbildet med subfile                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Send Alt
      *
     c     B2TAGA        TAG
     c                   WRITE     B2CMD
      *
      *  Bare data
      *
     c     B2TAGB        TAG
     c                   EXSR      XDSP01                                       DSP SFL
      *
      *  F3=Avslutt program
      *
     c     *INKC         IFEQ      *ON
     c                   MOVE      '9'           w_retu
     c                   GOTO      XSLUTT
     c                   END                                                                  .
      *
      *  F5=Forny
      *
     c     *INKE         IFEQ      *ON
     c                   EXSR      XLSP01
     c                   GOTO      B2TAGB
     c                   ENDIF
      *
      *  Roll
      *
     c     *IN10         IFEQ      *ON
     c                   EXSR      XCRT01                                       CRT SFL
     c                   GOTO      B2TAGB
     c                   ENDIF
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Posisjoner
      *  startverdi
      *  på konto
      *
     C     b2user        IFNE      *blank
     C                   MOVE      ' '           WSEQEN
     C                   MOVE      b2user        ausrl1_user
     C     ausrl1_key    SETLL     ausrl1
     C                   EXSR      XCLR01                                       TØM SFL
     C                   EXSR      XCRT01                                       CRT SFL
     C                   MOVE      *blank        b2user
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      *  Posisjoner
      *  startverdi
      *  på alfafelt
      *
     C     b2navn        IFNE      *BLANK
     C                   MOVE      'A'           WSEQEN
     C                   MOVE      *blank        ausrl1_user
     C                   MOVEL     b2navn        ausrl2_navn
     C     ausrl2_key    SETLL     ausrl2
     C                   EXSR      XCLR01                                       TØM SFL
     C                   EXSR      XCRT01                                       CRT SFL
     C                   MOVE      *blank        b2user
     C                   MOVE      *BLANK        b2navn
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
     C/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt i
      *  subfile
      *
     C                   DO        SSRN01
     C                   READC     B1WSF                                  16
     C     *IN16         IFEQ      *ON
     C                   LEAVE
     C                   ENDIF
      *
      *  Valgt LEVERANDØR taes med tilbake til forrige program
      *
     C     B1VALG        IFEQ      '1'
     C                   MOVE      b1user        wpuser
     C                   MOVE      b1navn        wpnavn
     C                   GOTO      XSLUTT
     C                   ENDIF
      *
     C                   ENDDO
      *
      *  Nullstiller/blanker feltet for start f.o.m.
      *
     C                   MOVE      *blank        b2user
      *
     C                   GOTO      B2TAGB
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XLSP01        BEGSR
      *
     C     SFRN01        IFNE      *ZERO
     C     SFRN01        CHAIN     B1WSF                              60
     C                   END
      *
     C                   MOVE      b1user        ausrl1_user
     C                   MOVE      b1navn        ausrl2_navn
      *
     C     WSEQEN        IFEQ      'A'
     C     ausrl2_key    SETLL     ausrl2
     C                   END
     C     WSEQEN        IFEQ      *BLANK
     C     ausrl1_key    SETLL     ausrl1
     C                   END
      *
     C                   EXSR      XCLR01                                       TØM SFL
     C                   EXSR      XCRT01                                       CRT SFL
      *
     C                   ENDSR
      *
     C/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XDSP01        BEGSR
      *
     C     SRRN01        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     B2WCT
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XCLR01        BEGSR
      *
     C                   MOVE      *ON           *IN14
     C                   WRITE     B2WCT
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN01
     C                   Z-ADD     0             SFRN01
     C                   Z-ADD     0             SSRN01
     C                   Z-ADD     0             SPGE01
      *
     C                   ENDSR
      *
     C/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XCRT01        BEGSR
      *
     C                   ADD       7             SPGE01                         SFL END
     C                   Z-ADD     SSRN01        SRRN01
      *
     C     SRRN01        DOUEQ     SPGE01
      *
     C     WSEQEN        IFEQ      'A'
     C                   READ      ausrl2                                 15
     C                   END
     C     WSEQEN        IFEQ      *BLANK
     C                   READ      ausrl1                                 15
     C                   END
      *
     C     *IN15         CABEQ     '1'           XEND01                         EOF
      *
      * SJEKK OM DETTE ER EN POTETLEVERANDØR
      *
     C                   MOVE      abuser        ausrl1_user
     C     ausrl1_key    CHAIN     ausrl1R                            90
      *
     C                   ADD       1             SRRN01
     C                   MOVE      abuser        b1user
     C                   MOVE      abnavn        b1navn
     C                   WRITE     B1WSF
     C                   ENDDO
      *
     C     XEND01        TAG
      *
     C     SRRN01        IFGT      SSRN01
     C     SSRN01        ADD       1             SFRN01
     C                   ENDIF
      *
     C                   Z-ADD     SRRN01        SSRN01
     C                   Z-ADD     SFRN01        SVRN01
      *
     C                   ENDSR
      *
     C/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   MOVE      *BLANK        WSEQEN            1
     C                   MOVE      *BLANK        w_retu            1
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    abuser        wpuser
     C     *LIKE         DEFINE    abnavn        wpnavn
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    abuser        ausrl1_user
      *
     C     *LIKE         DEFINE    abnavn        ausrl2_navn
      *
      *  Key for file : BRUKER-REGISTER
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      *  Key for file : BRUKER-NAVN-REGISTER
      *
     c     ausrl2_key    klist
     c                   kfld                    ausrl2_navn
     c                   kfld                    ausrl1_user
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Parameterliste
      *
     c     *ENTRY        PLIST
     c                   PARM                    wpuser
     c                   PARM                    wpnavn
      *
      *  Fyller opp skjermbildet for første visning
      *
     C     ausrl1_key    SETLL     ausrl1                                                     .
     C                   EXSR      XCRT01
      *
     C                   ENDSR
