     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK721R
      * Beskrivelse . . : Kunde bonus, beregner og oppdaterer bonus - alle
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261005 HKO  Nytt program
6.10  * 6.10  110811 BSA  Endringer relatert til sjekk mmot valgt bonus
6.20  * 6.20  020513 BSA  Lagt med periode som det skal sjekkes mot
6.21  * 6.20  261113 BSA  Lagt med fra periode også
6.22  * 6.20  251016 BSA  Bruk firmakoden fra parameterstreng
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fekboi1    if   e           k disk    rename(ekbost:ekboi1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ekboi1_bkun     s                   like(ekbkun)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_paar          s              4
     d w_pfir          s              3
     d w_palt          s              1
6.10 d w_tbot          s              2
6.20 d w_peri          s              2
6.21 d w_fper          s              2
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
      *
     d w_firm_alf      s              3
     d w_bkun_alf      s              6
     d w_paar_alf      s              4
     d w_inlr          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn parametre
      *
     c                   eval      w_paar_alf = w_paar
     c                   eval      w_firm_alf = w_pfir
     c                   eval      w_inlr = ' '
      *
      * Kaller kalkulasjonsprogram for alle kunder det er knyttet
      * bonusavtale i mot
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       not %eof
      *
     c                   eval      ekboi1_bkun = rkkund
     c     ekboi1_key    chain     ekboi1
     c                   if        %found
     c                   movel     ekbkun        w_bkun_alf
     c                   call      'EK711R'
     c                   parm                    w_firm_alf
     c                   parm                    w_bkun_alf
     c                   parm                    w_paar_alf
6.11 c                   parm                    w_tbot
6.21 c                   parm                    w_fper
6.20 c                   parm                    w_peri
     c                   parm                    w_inlr
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Et siste kall for å stenge åpne filer i kalkulasjonsprogram
      *
     c                   eval      w_inlr = '1'
     c                   eval      w_bkun_alf = *blank
     c                   call      'EK711R'
     c                   parm                    w_firm_alf
     c                   parm                    w_bkun_alf
     c                   parm                    w_paar_alf
6.11 c                   parm                    w_tbot
6.21 c                   parm                    w_fper
6.20 c                   parm                    w_peri
     c                   parm                    w_inlr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_pfir
     c                   parm                    w_paar
     c                   parm                    w_palt
6.10 c                   parm                    w_tbot
6.21 c                   parm                    w_fper
6.20 c                   parm                    w_peri
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kundebonus
      *
     c     ekboi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboi1_bkun
      *
6.22 c                   eval      w_firm = %int(w_pfir)
      *
     c                   endsr
