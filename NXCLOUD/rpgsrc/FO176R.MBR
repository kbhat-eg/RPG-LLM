     h option(*nodebugio) datedit(*ymd)
      /title  ASOFAK Generering utgående fakturafiler - param.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO176R
      * Beskrivelse . . : Generering utgående fakturafiler
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150224 bsa  Nytt program
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffnufl1    if   e           k disk    rename(fnufpfr:fnufl1r)
     ffntrl2    if   e           k disk    rename(fntrpfr:fntrl2r)
     fsohel3    if   e           k disk    rename(sohepfr:sohel3r)
     faftplr    if   e           k disk    rename(aftppfr:aftplrr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fkey                        1    overlay(dspfbk:369)
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d fnufl1_aarr     s                   like(fnuaar)
     d fnufl1_binr     s                   like(fnubin)
     d fntrl2_kftp     s                   like(fnkftp)
     d sohel3_aarr     s                   like(soaarr)
     d sohel3_numm     s                   like(sonumm)
     d aftplr_navn     s                   like(afnavn)
      *
      * Definering av variable
      *
     d w_tell          s              1  0
     d w_firm          s              3  0
     d w_dato          s               d   datfmt(*iso)
     d w_year          s              4  0
     d w_udat          s               d   datfmt(*iso)
     d w_modu          s             10
     d w_disp          s              1
     d w_stat          s              1
     d w_anta          s              4  0
     d w_ante          s              6  0
     d w_ants          s              6  0
     d w_vepa          s              6  0
     d w_cmd           s            256
     d w_len           s             15  5
     d w_x             s              2  0
      *
     d b_feil          s              1    inz(*off)
     d b_tlgx          s              1    inz(*off)
     d b_fa10          s              1    inz(*off)
     d b_fa12          s              1    inz(*off)
     d b_fa20          s              1    inz(*off)
     d b_fa21          s              1    inz(*off)
     d b_fa22          s              1    inz(*off)
     d b_fa23          s              1    inz(*off)
     d b_fa24          s              1    inz(*off)
     d b_fa25          s              1    inz(*off)
     d b_fa26          s              1    inz(*off)
     d b_fa28          s              1    inz(*off)
     d b_fa30          s              1    inz(*off)
      *
      *
      * Definering av parametre
      *
     d p_aarr          s              4  0
     d p_faty          s              2  0
     d p_fnum          s              8  0
     d p_tnum          s              8  0
     d p_omkj          s              1
     d p_rapp          s              1
     d p_anta          s              4  0
     d p_stat          s              1
      *
      * Definering av konstanter
      *
     d c_modu          c                   'OF_KORT01 '
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Modul er ikke installert
      *
     c                   if        b_tlgx = *off
     c                   goto      avslutt
     c                   endif
      *
      * sjekker om overføring allerede pågår
      *
     c                   eval      w_cmd = 'DLYJOB DLY(2)'
     c                   eval      w_len = 200
      *
     c     wait          tag
      *
     c                   eval      w_x = 0
     c                   eval      aftplr_navn = 'LOGIQFAKT'
     c     aftplr_key    chain     aftplr
     c                   if        %found(aftplr) and
     c                             afstat <> *blank
     c                   if        w_x < 10
     c                   call      'QCMDEXC'
     c                   parm                    w_cmd
     c                   parm                    w_len
     c                   eval      w_x = w_x + 1
     c                   goto      wait
     c                   else
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c                   eval      p_aarr = *year
     c                   eval      p_anta = *zero
     c                   eval      p_stat = *blank
     c                   eval      w_anta = *zero
      *
     c                   exsr      sjk_anta
      *
      * eFaktura - Norgros/Logiq-fellesformat
      *
     c                   if        b_fa20 = *on
     c                   eval      p_faty = 20
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_felles1
     c                   endif
      *
      * ePrint - Norgros/Logiq-fellesformat
      *
     c                   if        b_fa21 = *on
     c                   eval      p_faty = 21
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_felles1
     c                   endif
      *
      * eMail - Norgros/Logiq-fellesformat
      *
     c                   if        b_fa22 = *on
     c                   eval      p_faty = 22
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_felles1
     c                   endif
      *
     c                   if        b_fa25 = *on
     c                   eval      p_faty = 25
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_felles1
     c                   endif
      *
     c                   if        b_fa26 = *on
     c                   eval      p_faty = 26
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_felles1
     c                   endif
      *
      * eCards - Norgros/Logiq-fellesformat
      *
     c                   if        b_fa23 = *on
     c                   eval      p_faty = 23
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_felles1
     c                   endif
      *
      * EDI - Norgros/Logiq-fellesformat
      *
     c                   if        b_fa24 = *on
     c                   eval      p_faty = 24
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_EDI24
     c                   endif
      *
     c                   if        b_fa28 = *on
     c                   eval      p_faty = 28
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_EDI28
     c                   endif
      *
      * Autofakt
      *
     c                   if        b_fa30 = *on
     c                   eval      p_faty = 30
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_autofak
     c                   endif
      *
      * eFaktura XML
      *
     c                   if        b_fa10 = *on
     c                   eval      p_faty = 10
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_xml
     c                   endif
      *
      * eFaktura mail XML
      *
     c                   if        b_fa12 = *on
     c                   eval      p_faty = 12
     c                   eval      p_fnum = 1
     c                   eval      p_tnum = 99999999
     c                   eval      p_omkj = *blanks
     c                   eval      p_rapp = '1'
     c                   exsr      strt_xml
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall til rutine for oppbygging av Norgros format
      * - eFaktura, ePrint, eMail, eCards
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     strt_felles1  begsr
      *
     c                   call      'FO181C'
     c                   parm                    p_aarr
     c                   parm                    p_faty
     c                   parm                    p_fnum
     c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_stat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall til rutine for oppbygging av Norgros format
      * - EDI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     strt_EDI24    begsr
      *
     c                   eval      w_ants = 0
     c     ny_send24     tag
     c
     c                   call      'FO182C'
     c                   parm                    p_aarr
     c                   parm                    p_faty
     c                   parm                    p_fnum
     c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_stat
      *
      * sjekker om feilsituasjon
      *
     c                   if        p_stat <> *blank
     c                   leavesr
     c                   endif
      *
      * sjekker om det er igjen flere EDI og overføre
      *
     c                   eval      w_ante = 0
     c                   eval      fnufl1_aarr = *year
     c     fnufl1_key    setll     fnufl1
     c     fnufl1_key    reade     fnufl1
     c                   dow       not %eof
     c                   if        fnukda = *loval
     c                   if        fnufat = 24
     c                   eval      w_ante = w_ante + 1
     c                   endif
     c                   endif
     c     fnufl1_key    reade     fnufl1
     c                   enddo
      *
     c                   if        w_ante <> 0
     c                   goto      ny_send24
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall til rutine for oppbygging av Norgros format
      * - EDI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     strt_EDI28    begsr
      *
     c     ny_send28     tag
     c
     c                   call      'FO182C'
     c                   parm                    p_aarr
     c                   parm                    p_faty
     c                   parm                    p_fnum
     c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_stat
      *
      * sjekker om feilsituasjon
      *
     c                   if        p_stat <> *blank
     c                   leavesr
     c                   endif
      *
      * sjekker om det er igjen flere EDI og overføre
      *
     c                   eval      w_ante = 0
     c                   eval      fnufl1_aarr = *year
     c     fnufl1_key    setll     fnufl1
     c     fnufl1_key    reade     fnufl1
     c                   dow       not %eof
     c                   if        fnukda = *loval
     c                   if        fnufat = 28
     c                   eval      w_ante = w_ante + 1
     c                   endif
     c                   endif
     c     fnufl1_key    reade     fnufl1
     c                   enddo
      *
     c                   if        w_ante <> 0
     c                   goto      ny_send28
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall til rutine for oppbygging av Autofakt-format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     strt_autofak  begsr
      *
     c                   call      'FO183R'
     c                   parm                    p_aarr
     c                   parm                    p_faty
     c                   parm                    p_fnum
     c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_stat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall til prog. for uttr. til VISA/Europay Purchasing
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     strt_purchas  begsr
      *
     c                   call      'FO192C'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall til rutine for oppbygging av XML format
      * - eFaktura, eMail
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     strt_xml      begsr
      *
     c                   call      'FO195C'
     c                   parm                    p_aarr
     c                   parm                    p_faty
     c                   parm                    p_fnum
     c                   parm                    p_tnum
     c                   parm                    p_omkj
     c                   parm                    p_rapp
     c                   parm                    p_anta
     c                   parm                    p_stat
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av antall fakt. på kø
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_anta      begsr
      *
     c                   eval      w_udat = *loval
     c                   eval      b_fa10 = *off
     c                   eval      b_fa12 = *off
     c                   eval      b_fa20 = *off
     c                   eval      b_fa21 = *off
     c                   eval      b_fa22 = *off
     c                   eval      b_fa23 = *off
     c                   eval      b_fa24 = *off
     c                   eval      b_fa25 = *off
     c                   eval      b_fa26 = *off
     c                   eval      b_fa28 = *off
     c                   eval      b_fa30 = *off
      *
     c                   eval      fnufl1_aarr = *year
     c     fnufl1_key    setll     fnufl1
     c     fnufl1_key    reade     fnufl1
      *
     c                   dow       not %eof
      *
     c                   if        fnukda = *loval
     c                   select
     c                   when      fnufat = 10
     c                   eval      b_fa10 = *on
     c                   when      fnufat = 12
     c                   eval      b_fa12 = *on
     c                   when      fnufat = 20
     c                   eval      b_fa20 = *on
     c                   when      fnufat = 21
     c                   eval      b_fa21 = *on
     c                   when      fnufat = 22
     c                   eval      b_fa22 = *on
     c                   when      fnufat = 23
     c                   eval      b_fa23 = *on
     c                   when      fnufat = 24
     c                   eval      b_fa24 = *on
     c                   when      fnufat = 25
     c                   eval      b_fa25 = *on
     c                   when      fnufat = 26
     c                   eval      b_fa26 = *on
     c                   when      fnufat = 28
     c                   eval      b_fa28 = *on
     c                   when      fnufat = 30
     c                   eval      b_fa30 = *on
     c                   endsl
      *
     c                   endif
      *
     c     fnufl1_key    reade     fnufl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av antall VISA/Europay Purch. fakt.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_vepa      begsr
      *
     c                   eval      fntrl2_kftp = *zero
     c     fntrl2_key    setll     fntrl2
     c                   read      fntrl2
      *
     c                   dow       not %eof(fntrl2)
      *
      * VISA Purchasing - trans.type 01
      * Europay Purchasing - trans.type 02
      *
     c                   if        fnkftp <> 1 and
     c                             fnkftp <> 2
     c                   goto      neste_t2
     c                   endif
      *
      * Sjekk om oppdat. tidligere og riktig år
      *
     c                   if        fnbinr <> *zero or
     c                             fnaarr <> *year
     c                   goto      neste_t2
     c                   endif
      *
     c                   eval      sohel3_aarr = fnaarr
     c                   eval      sohel3_numm = fnnumm
     c     sohel3_key    setll     sohel3
     c     sohel3_key    reade     sohel3r
      *
     c                   dow       not %eof(sohel3)
     c                   if        sosuff = fnsuff
     c                   eval      w_vepa = w_vepa + 1
     c                   endif
     c     sohel3_key    reade     sohel3r
     c                   enddo
      *
     c     neste_t2      tag
     c                   read      fntrl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for kort/fakt trans - type
      *
     c     fntrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fntrl2_kftp
      *
      * Key for utg.fakturalogg
      *
     c     fnufl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fnufl1_aarr
      *
      * Key for utg.fakturalogg
      *
     c     fnufl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fnufl1_aarr
     c                   kfld                    fnufl1_binr
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel3_aarr
     c                   kfld                    sohel3_numm
      *
      * Key for les av ftp overførings parameter
      *
     c     aftplr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftplr_navn
      *
     c                   eval      w_firm = l_firm
      *
      * Sjekker om tilleggsmodul er installert
      *
     c                   eval      w_modu = c_modu
     c                   eval      w_disp = '1'
     c                   eval      w_stat = '0'
     c                   eval      b_tlgx = *on
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_stat
      *
     c                   if        w_stat = '1'
     c                   eval      b_tlgx = *off
     c                   endif
      *
     c                   endsr
      *
