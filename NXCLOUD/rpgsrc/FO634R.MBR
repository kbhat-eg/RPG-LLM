     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO634R
      * Beskrivelse . . : Utskrift av kontroll-liste, plukker ordres
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V3.20 170700 KOB  Lagt inn test på avdeling
      * V3.20 170700 KOB  Lagt inn test på kjørerute
3.11  * V3.11 130900 AMD  Korrigering på dec data error ved kjøring
3.31  * V3.31 060401 AMD  Lagt inn utvalg også på pakkseddeldato
5.01  * V5.01 030502 AMD  Lagt inn utvalg også på info-kode.
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
5.31  * V5.31 250804 GRO  Endring på sorteringsstringen
      *       041104 HKO  Endret parameter ved kall til VP900R
5.53  * R5.53 230307 AMD  Utvidet sorteringsbegrep slik at både Navn og
      *                   Adresse 1 kan skape brudd
5.70  * PRGR  301008 bof  Utvidet med prisgruppe
6.nu  * R6.30 160614 bof  Endringer ihht. nummerutvidelse
6.30  * 6.30  061014 BKV  Henter overstyrt info fra kundeprosjekt
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     Frkunl1    IF   E           K DISK    RENAME(RKUNPFR:rkunl1r)
     FFOHEL1    IF   E           K DISK    RENAME(FOHEPFR:FOHEL1R)
     FFODTL1    IF   E           K DISK    RENAME(FODTPFR:FODTL1R)
     FFOTXL1    IF   E           K DISK    RENAME(FOTXPFR:FOTXL1R)
     FFPLOL1    IF   E           K DISK    RENAME(FPLOPFR:FPLOL1R)
     FFPSOLU    UF A E           K DISK    RENAME(FPSOPFR:FPSOLUR)
5.31 ffntrl1    if   e           k disk    rename(fntrpfr:fntrl1r)
6.30 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     D                 DS
      *                                                      SORTERING
5.53 d sorter                       114
5.53 d  sor001                        8  0 overlay(sorter)
5.53 d  sor002                       60    overlay(sorter:9)
5.53 d  sor003                       16    overlay(sorter:69)
5.53 d  sor004                        3    overlay(sorter:85)
5.53 d  sor005                       17    overlay(sorter:88)
6.nu d  sor006                        8  0 overlay(sorter:105)
6.nu d  sor007                        2  0 overlay(sorter:113)
      *
     D                 DS
      *                                                      Parameter-inn
6.nu D  LDPREC                 1     84
     D  LDOTYP                 1      2
     D  LDSEKV                 3      4  0
6.nu D  LDFONR                 5     12  0
6.nu D  LDTONR                13     20  0
6.nu D  LDFKUN                21     26  0
6.nu D  LDTKUN                27     32  0
6.nu D  LDFDAT                33     42d   datfmt(*iso)
6.nu D  LDTDAT                43     52d   datfmt(*iso)
6.nu D  LDVAL1                53     53  0
6.nu D  LDVAL2                54     54  0
6.nu D  LDVAL3                55     55  0
6.nu D  LDVAL4                56     56  0
6.nu D  LDVAL5                57     57  0
6.nu D  LDVAL6                58     58  0
6.nu D  LDVAL7                59     59  0
6.nu D  LDPROS                60     61  0
6.nu D  ldavde                62     65  0
6.nu D  ldkjor                66     67  0
6.nu D  ldldat                68     77d   datfmt(*iso)
6.nu D  ldityp                78     79
6.nu D  ldaltf                80     81
      *
      * - - - - - - - - - - - - - - - - - - *
      *   Priser      :  Overføringsfelter  *
      * - - - - - - - - - - - - - - - - - - *
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
8.01 d  hoprgr                        2    overlay(horec:71)
5.31  *
5.31  * Definering av variable i nøkler
5.31  *
5.31 d fntrl1_numm     s                   like(fnnumm)
5.31 d fntrl1_suff     s                   like(fnsuff)
6.30 d fkprl1_kund     s                   like(flkund)
6.30 d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d w_dato_alf      s              8
5.31 d w_firm          s                   like(fofirm)
5.70 d w_prgr          s                   like(fdprgr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  Local Data Area                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                UDS
      *
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utplukk, Setter inn riktige parameterverdier          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      WWPARM        LDPREC
      *
     C                   MOVE      LDFONR        HEDNUM
     C                   MOVE      *ZERO         HEDSUF
     C                   MOVE      LDTONR        SLTONR
     C                   MOVE      99            SLTSUF
      *
     C     HEDKEY        SETLL     FOHEL1R
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Leser poster fra register for utplukks-test                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     START         TAG
      *
     C                   MOVE      *OFF          *IN90
     C                   READ      FOHEL1R                                90
5.70 c                   exsr      hent_prisgr
     C     *IN90         CABEQ     *ON           SLUTT
      *
      *  Sjekk   Er ordrenummer innenfor grensene, Ellers hopp til Slutt
      *
     C     FONUMM        CABGT     SLTONR        SLUTT
      *
     C     FONUMM        IFEQ      SLTONR
     C     FOSUFF        IFGT      SLTSUF
     C                   GOTO      SLUTT
     C                   END
     C                   END
      *
      *  Sjekk   Er ordretype lik valgt,           Hvis nei les neste post
      *
     C     FOOTYP        CABNE     LDOTYP        START
      *
      *  Sjekk   Er kundenummer innenfor grensene  Hvis nei les neste post
      *
     C     FOKUND        IFLT      LDFKUN
     C     FOKUND        ORGT      LDTKUN
     C                   GOTO      START
     C                   END
      *
      *  Sjekk   Er Datoene innenfor grensene,     Hvis nei les neste post
      *
     C     FOBDAT        IFLT      LDFDAT
     C     FOBDAT        ORGT      LDTDAT
     C                   GOTO      START
     C                   END
3.31  *
3.31  *  Sjekk   Er Pakkseddeldato utenfor grense, Hvis ja  les neste post
3.31  *
3.31 c                   if        foldat > ldldat
3.31 c                   goto      start
3.31 c                   end
      *
      *  Sjekk   Er avdeling riktig, Ellers hopp til Slutt
      *
     C     ldavde        ifne      *zeros
     C     foavde        ifne      ldavde
     C                   goto      start
     C                   endif
     C                   endif
      *
      *  Sjekk   Er kjørerute riktig, Ellers hopp til Slutt
      *
     C     ldkjor        ifne      *zeros
     C     fokjor        ifne      ldkjor
     C                   goto      start
     C                   endif
     C                   endif
5.01  *
5.01  *  Sjekk   Er info-kode riktig, Ellers hopp til Slutt
5.01  *
5.01 C     ldityp        ifne      *blank
5.01 C     foityp        ifne      ldityp
5.01 C                   goto      start
5.01 C                   endif
5.01 C                   endif
5.31  *
5.31  *  Sjekk   Er alt.fakturering valgt
5.31  *
5.31 c                   if        ldaltf <> *blank
5.31  *
5.31  *  Alt. 1 - Kort/purchasing
5.31  *
5.31 c                   if        ldaltf = '01'
5.31 c                   eval      fntrl1_numm = fonumm
5.31 c                   eval      fntrl1_suff = fosuff
5.31 c     fntrl1_key    chain     fntrl1r
5.31 c                   if        not %found
5.31 c                   goto      start
5.31 c                   endif
5.31 c                   endif
5.31  *
5.31 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk opplysninger i KUNDE-REGISTER                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Sjekk   Er valgt sekvens lik oppgitt sekvens for denne kunde
      *
     C     LDSEKV        IFNE      *ZERO
     C                   MOVE      FOKUND        KUNNUM
     C     KUNKEY        CHAIN     rkunl1                             90
     C     *IN90         CABEQ     *ON           START
      *
6.30  * Slå opp på kundeprosjekt for å finne om prosjekt er
6.30  * overstyrer verdi på kunde
6.30 c                   if        fokpro > 0
6.30 c                   eval      fkprl1_kund = fokund
6.30 c                   eval      fkprl1_kpro = fokpro
6.30 c     fkprl1_key    chain     fkprl1
6.30 c                   if        %found(fkprl1)
6.30 C     flsekv        IFGT      LDSEKV
6.30 C                   GOTO      START
6.30 C                   END
6.30 c                   endif
     c                   else
     C     RKSEKV        IFGT      LDSEKV
     C                   GOTO      START
     C                   END
     C                   END
6.30 c                   endif
6.30  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk opplysninger i Plukk-register                            *
      *  Det skal bare sjekkes dersom valg 3=1 (Plukket record)         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     LDVAL3        IFEQ      1
      *
      *  Hent opplysninger fra ORDRE-PLUKK-REGISTER
      *
     C                   MOVE      FONUMM        PLONUM
     C                   MOVE      FOSUFF        PLOSUF
     C     PLOKEY        CHAIN     FPLOL1R                            90
     C     *IN90         CABEQ     *ON           START
      *
      *  Sjekk   Er Arbeidsstasjon lik valgt,      Hvis nei les neste post
      *
     C     LDVAL4        IFEQ      1
     C     FKPWSD        IFNE      DSWSID
     C                   GOTO      START
     C                   END
     C                   END
      *
      *  Sjekk   Er Bruker lik valgt,              Hvis nei les neste post
      *
     C     LDVAL5        IFEQ      1
     C     FKPUSR        IFNE      DSUSER
     C                   GOTO      START
     C                   END
     C                   END
      *
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk opplysninger i Ordre-register                            *
      *  Det skal bare sjekkes dersom valg 3=2   (Kontroll-kode)        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     LDVAL3        IFEQ      2
      *
      *  Sjekk   Er valgt kontrollkode=J,          Hvis nei les neste post
      *
     C     FOBIND        IFNE      'J'
     C                   GOTO      START
     C                   END
      *
      *  Sjekk   Er Arbeidsstasjon lik valgt,      Hvis nei les neste post
      *
     C     LDVAL4        IFEQ      1
     C     FOWSID        IFNE      DSWSID
     C                   GOTO      START
     C                   END
     C                   END
      *
      *  Sjekk   Er Bruker lik valgt,              Hvis nei les neste post
      *
     C     LDVAL5        IFEQ      1
     C     FOUSER        IFNE      DSUSER
     C                   GOTO      START
     C                   END
     C                   END
      *
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utlegging av records til Plukk/Sort-register          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legger inn ordrenummer og suffix i nøkklene
      *
     C                   MOVE      FONUMM        HEDNUM
     C                   MOVE      FOSUFF        HEDSUF
      *
     C                   MOVE      FONUMM        LINNUM
     C                   MOVE      FOSUFF        LINSUF
      *
     C                   MOVE      FONUMM        TXTNUM
     C                   MOVE      FOSUFF        TXTSUF
      *
     C                   MOVE      FONUMM        PSONUM
     C                   MOVE      FOSUFF        PSOSUF
      *
      *  Nullstiller sorteringsbegrep pr. ordre
      *
     C                   MOVE      *BLANK        SORTER
     C                   MOVE      *ZERO         SOR001
      *
      *  Legger inn faste verdier i sorteringsbegrep
      *
5.31 C                   MOVEL     FONUMM        SOR006
5.31 C                   MOVEL     FOSUFF        SOR007
      *
      *  Dersom utskrift i registreringsrekkefølge legges
      *  Dato og klokkeslett inn i sorteringsbegrepet
      *
     C     LDVAL2        IFEQ      1
     c     *iso0         move      fodate        w_dato_alf
     c                   move      w_dato_alf    sor001
     C                   MOVEL     FOTIME        SOR002
     C                   END
      *
      *  Dersom utskrift i vareadresse-rekkefølge legges
      *  Kundenummer og vareadresse inn i sorteringsbegrepet
      *
     C     LDVAL2        IFEQ      2
     C                   MOVE      FOKUND        SOR001
5.53 c                   eval      SOR002 = FONAVN + FOADR1
     C                   END
      *
      *  Henter linjer/records fra de enkelte registre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *  Nøkkel for utplukks/sortert register er      *
      *                                               *
      *  FKSFIR  Firma                                *
      *  FKSSRT  Sorteringsbegrep                     *
      *  FKSONR  Ordrenummer                          *
      *  FKSSUF  Suffix                               *
      *  FKSTLL  Rekkefølgekode  : Hode         = 0   *
      *  FKSLIN  Linjenummer     : Detaljlinjer = 3   *
      *                          : Tekst, Foran = 2   *
      *                          : Tekst, Etter = 4   *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Blanker hjelpevariabel for test for detaljlinjer
      *
     C                   MOVE      *BLANK        TSTBYT
      *
      *  Henter detalj-linjer
      *
     C                   EXSR      LINJE
      *
      *  Henter bare Heading + tekstlinjer dersom varelinjer finnes
      *
     C     TSTBYT        IFNE      *BLANK
     C                   EXSR      OHEAD
     C                   EXSR      FORAN
     C                   EXSR      ETTER
     C                   END
      *
      *  Dersom Ordren ikke lenger skal være plukket etter
      *  utskrift av kontroll-liste slettes merket
      *
     C     LDVAL6        IFEQ      1
      *
      *  Kaller opp subprogrammet for sletting av utplukk
      *  og blanking av behandlinskode i ORDRE-HODE-REGISTER
      *
     C                   CALL      'FO412R'
     C                   PARM                    WSTATG
     C                   PARM                    HEDFIR
     C                   PARM                    HEDNUM
     C                   PARM                    HEDSUF
      *
     C                   END
      *
      *  Velg neste Ordre
      *
     C                   GOTO      START
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     SLUTT         TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-heading og legger ut til sorterings-register      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   OHEAD         BEGSR
      *
     C                   MOVE      *ZERO         PSOTLL
     C                   MOVE      *ZERO         PSOLIN
     C     PSOKEY        CHAIN     FPSOLUR                            92
      *
     C                   MOVE      'H'           FKSKOD
     C                   MOVEL     LDOTYP        FKSOTY
     C                   MOVE      LDVAL1        FKSALT
     C                   MOVEL     SORTER        FKSSRT
     C                   MOVEL     LDVAL7        FKSDIV
     C                   MOVE      LDPROS        FKSDIV
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    FPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        FKSFIR
     C                   MOVE      PSONUM        FKSONR
     C                   MOVE      PSOSUF        FKSSUF
     C                   MOVE      PSOTLL        FKSTLL
     C                   MOVE      PSOLIN        FKSLIN
     C                   WRITE     FPSOLUR
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-linjer og legger ut til sorterings-register       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   LINJE         BEGSR
      *
     C                   MOVE      *ZERO         LINLIN
      *
     C     LINKEY        SETLL     FODTL1R
      *
     C     LINJE1        TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      FODTL1R                                90
     C     *IN90         IFEQ      *OFF
     C     FDFIRM        IFEQ      LINFIR
     C     FDNUMM        IFEQ      LINNUM
     C     FDSUFF        IFEQ      LINSUF
      *
      *  Er dette en tekstlinje ?
      *
     C     LDVAL7        IFEQ      1
     C     LDPROS        ORNE      *ZERO
     C     FDVARE        CABEQ     *BLANK        LINJE1
     C                   END
      *
      *
      *  Dersom sjekk for varelinjer kall opp subrutine
      *
     C     LDVAL7        IFEQ      1
     C     LDPROS        ORNE      *ZERO
     C                   EXSR      SJEKK
     C     WWWTST        IFEQ      *BLANK
     C                   GOTO      LINJE1
     C                   END
     C                   END
      *
     C                   Z-ADD     3             PSOTLL
     C                   Z-ADD     FDLINE        PSOLIN
     C     PSOKEY        CHAIN     FPSOLUR                            92
      *
     C                   MOVE      'X'           TSTBYT
      *
     C                   MOVE      'V'           FKSKOD
     C                   MOVEL     LDOTYP        FKSOTY
     C                   MOVE      LDVAL1        FKSALT
     C                   MOVEL     SORTER        FKSSRT
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    FPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        FKSFIR
     C                   MOVE      PSONUM        FKSONR
     C                   MOVE      PSOSUF        FKSSUF
     C                   MOVE      PSOTLL        FKSTLL
     C                   MOVE      PSOLIN        FKSLIN
     C                   WRITE     FPSOLUR
     C                   END
      *
     C                   GOTO      LINJE1
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk av linjeopplysninger for om skal med til utskrift        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   SJEKK         BEGSR
      *
      *  Blanker testfelter for om linjen skal være med
      *
     C                   MOVE      *BLANK        WWWTST
      *
      *  Sjekk   Er Salgspris og rabatt forsjellig fra standard
      *
     C     LDVAL7        IFEQ      1
     C                   EXSR      HNTVAR
      *
     C     fdsapr        IFNE      HOSAPR
     C     FDRAB1        ORNE      HORAB1
     C     FDRAB2        ORNE      HORAB2
     C                   MOVE      'X'           WWWTST
     C                   GOTO      SJEKK9
     C                   END
     C                   END
      *
      *  Beregner dekningsgrad for linjen
      *
     C     FDSUMS        SUB       FDSUMR        WWNETT
     C     WWNETT        SUB       FDSUMK        WWDEKN
      *
     C                   MOVE      *ZERO         WWGRAD
     C     WWNETT        IFNE      *ZERO
     C     WWDEKN        DIV       WWNETT        WWGRAD
     C                   MULT      100           WWGRAD
     C                   END
      *
      *  Sjekk   Er Dekningsgrad 100% eller mindre enn oppgitt %, skriv linjen
      *
     C     WWGRAD        IFEQ      100
     C     WWGRAD        ORLT      LDPROS
     C                   MOVE      'X'           WWWTST
     C                   END
      *
     CSR   SJEKK9        ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-tekst (FORAN) og legger ut til sorterings-register*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   FORAN         BEGSR
      *
     C                   MOVE      *ZERO         TXTLIN
      *
     C     TXTKEY        SETLL     FOTXL1R
      *
     C     FORAN1        TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      FOTXL1R                                90
     C     *IN90         IFEQ      *OFF
     C     FTFIRM        IFEQ      TXTFIR
     C     FTNUMM        IFEQ      TXTNUM
     C     FTSUFF        IFEQ      TXTSUF
     C     FTLINE        IFLE      4999
      *
     C                   Z-ADD     2             PSOTLL
     C                   Z-ADD     FTLINE        PSOLIN
     C     PSOKEY        CHAIN     FPSOLUR                            92
      *
     C                   MOVE      'K'           FKSKOD
     C                   MOVEL     LDOTYP        FKSOTY
     C                   MOVE      LDVAL1        FKSALT
     C                   MOVEL     SORTER        FKSSRT
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    FPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        FKSFIR
     C                   MOVE      PSONUM        FKSONR
     C                   MOVE      PSOSUF        FKSSUF
     C                   MOVE      PSOTLL        FKSTLL
     C                   MOVE      PSOLIN        FKSLIN
     C                   WRITE     FPSOLUR
     C                   END
      *
     C                   GOTO      FORAN1
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-tekst (ETTER) og legger ut til sorterings-register*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   ETTER         BEGSR
      *
     C                   MOVE      5000          TXTLIN
      *
     C     TXTKEY        SETLL     FOTXL1R
      *
     C     ETTER1        TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      FOTXL1R                                90
     C     *IN90         IFEQ      *OFF
     C     FTFIRM        IFEQ      TXTFIR
     C     FTNUMM        IFEQ      TXTNUM
     C     FTSUFF        IFEQ      TXTSUF
     C     FTLINE        IFLE      9999
      *
     C                   Z-ADD     4             PSOTLL
     C                   Z-ADD     FTLINE        PSOLIN
     C     PSOKEY        CHAIN     FPSOLUR                            92
      *
     C                   MOVE      'K'           FKSKOD
     C                   MOVEL     LDOTYP        FKSOTY
     C                   MOVE      LDVAL1        FKSALT
     C                   MOVEL     SORTER        FKSSRT
      *
     C     *IN92         IFEQ      *OFF
     C                   UPDATE    FPSOLUR
     C                   ELSE
     C                   MOVE      PSOFIR        FKSFIR
     C                   MOVE      PSONUM        FKSONR
     C                   MOVE      PSOSUF        FKSSUF
     C                   MOVE      PSOTLL        FKSTLL
     C                   MOVE      PSOLIN        FKSLIN
     C                   WRITE     FPSOLUR
     C                   END
      *
     C                   GOTO      ETTER1
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     CSR                 ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter opplsyninger om varen fra pris/vare subrutinen          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     HNTVAR        BEGSR
      *
     C                   MOVE      HEDFIR        HIFIRM
     c                   eval      hirkat = 0
      *
     C                   MOVE      FOKUND        HIKUND
     C     FOLKUN        IFNE      *ZERO
     C                   MOVE      FOLKUN        HIKUND
     C                   END
      *
     C                   MOVE      fokpro        hikpro
     C                   MOVE      FDVARE        HIVARE
3.11 c                   move      fdlety        hilety
     C                   MOVE      FOBDAT        HIDATO
     C                   MOVE      *loval        HItime
     C                   MOVE      *zeros        HInumm
     C                   MOVE      FOPASK        hikode
     C                   MOVE      FDPASL        hidekn
     C                   MOVE      FDENH1        hienhe
3.11 c                   move      *blank        hitilb
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hiavgf = fomkod
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
3.11 c                   move      *blank        hiinlr
      *
     C                   CALL      'VP900R'
     C                   PARM                    HIREC
     C                   PARM                    HOREC
      *
     C                   ENDSR
      *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  * Henter prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL712R   '
5.70 c                   parm                    w_firm
5.70 c                   parm                    folage
5.70 c                   parm                    foavde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WSTATG            1
     C                   MOVE      *BLANK        TSTBYT            1
     C                   MOVE      *BLANK        WWWTST            1
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    FDSUMS        WWNETT
     C     *LIKE         DEFINE    FDSUMS        WWDEKN
     C     *LIKE         DEFINE    FDSUMS        WWGRAD
      *
     C     *LIKE         DEFINE    FKPONR        SLTONR
     C     *LIKE         DEFINE    FKPSUF        SLTSUF
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    RKFIRM        KUNFIR
     C     *LIKE         DEFINE    RKKUND        KUNNUM
      *
     C     *LIKE         DEFINE    FOFIRM        HEDFIR
     C     *LIKE         DEFINE    FONUMM        HEDNUM
     C     *LIKE         DEFINE    FOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    FDFIRM        LINFIR
     C     *LIKE         DEFINE    FDNUMM        LINNUM
     C     *LIKE         DEFINE    FDSUFF        LINSUF
     C     *LIKE         DEFINE    FDLINE        LINLIN
      *
     C     *LIKE         DEFINE    FTFIRM        TXTFIR
     C     *LIKE         DEFINE    FTNUMM        TXTNUM
     C     *LIKE         DEFINE    FTSUFF        TXTSUF
     C     *LIKE         DEFINE    FTLINE        TXTLIN
      *
     C     *LIKE         DEFINE    FKPFIR        PLOFIR
     C     *LIKE         DEFINE    FKPONR        PLONUM
     C     *LIKE         DEFINE    FKPSUF        PLOSUF
      *
     C     *LIKE         DEFINE    FKSFIR        PSOFIR
     C     *LIKE         DEFINE    FKSONR        PSONUM
     C     *LIKE         DEFINE    FKSSUF        PSOSUF
     C     *LIKE         DEFINE    FKSTLL        PSOTLL
     C     *LIKE         DEFINE    FKSLIN        PSOLIN
      *
      *  Key for file : KUNDE-REGISTER
      *
     C     KUNKEY        KLIST
     C                   KFLD                    KUNFIR
     C                   KFLD                    KUNNUM
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     C     LINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINNUM
     C                   KFLD                    LINSUF
     C                   KFLD                    LINLIN
      *
      *  Key for file : ORDRE-TEKST-REGISTER
      *
     C     TXTKEY        KLIST
     C                   KFLD                    TXTFIR
     C                   KFLD                    TXTNUM
     C                   KFLD                    TXTSUF
     C                   KFLD                    TXTLIN
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        KLIST
     C                   KFLD                    PLOFIR
     C                   KFLD                    PLONUM
     C                   KFLD                    PLOSUF
      *
      *  Key for file : PLUKK-S-LINJE-REGISTER
      *
     C     PSOKEY        KLIST
     C                   KFLD                    PSOFIR
     C                   KFLD                    PSONUM
     C                   KFLD                    PSOSUF
     C                   KFLD                    PSOTLL
     C                   KFLD                    PSOLIN
5.31  *
5.31  * Key for kort/fakt trans/numm
5.31  *
5.31 c     fntrl1_key    klist
5.31 c                   kfld                    w_firm
5.31 c                   kfld                    fntrl1_numm
5.31 c                   kfld                    fntrl1_suff
6.30  *
6.30  * Key for oppslag på kundeprosjekt
6.30  *
6.30 c     fkprl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    fkprl1_kund
6.30 c                   kfld                    fkprl1_kpro
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
6.nu C                   PARM                    WWPARM           84
      *
      *  Legge inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        KUNFIR
     C                   MOVE      DSSFIR        HEDFIR
     C                   MOVE      DSSFIR        LINFIR
     C                   MOVE      DSSFIR        TXTFIR
     C                   MOVE      DSSFIR        PLOFIR
     C                   MOVE      DSSFIR        PSOFIR
      *
     CSR                 ENDSR
