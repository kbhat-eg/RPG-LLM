     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT111R
      * Beskrivelse . . : Oppdaterer kundeinformasjon fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       181114 ask  Nytt program
7.01  * 7.01  080321 ask  Dersom kode for "Spes.fakt" er satt oppdateres ikke avd
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlu    uf a e           k disk    rename(rkunpfr:rkunlur)
      *
      * Parameter-inn
      *
     d p_firm          s                   like(rkfirm)
     d p_comd          s             20
     d p_parm          s           2048
     d p_user          s             10
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d rkunlu_kund     s                   like(rkkund)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds          2048
     d  d_kund                        6
     d  d_navn                       30
     d  d_adr1                       30
     d  d_adr2                       30
     d  d_ponr                        4
     d  d_pste                       30
     d  d_bad1                       30
     d  d_bad2                       30
     d  d_bpnr                        4
     d  d_land                        2
     d  d_upnr                        9
     d  d_fort                        9
     d  d_alfa                       18
     d  d_prsn                       11
     d  d_emai                       50
     d  d_weba                       40
     d  d_tlfn                       11
     d  d_tlfx                       11
     d  d_tlfm                       11
     d  d_bgir                       11
     d  d_eann                       13
     d  d_avde                        4
     d  d_lage                        2
     d  d_selg                        4
     d  d_dist                        4
     d  d_bbet                        2
     d  d_valk                        3
     d  d_lbet                        2
     d  d_sped                        3
     d  d_sper                        1
     d  d_pass                        1
     d  d_rent                        1
     d  d_kreg                        9
     d  d_kres                        1
     d  d_krty                        1
     d  d_avgf                        1
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_kund          s                   like(rkkund)
     d w_kgra          s              8
     d w_kgrt          s              8  0
     d w_kgrn          s              5  0
     d w_pos1          s              2  0
     d w_pos2          s              2  0
      *
     d b_sign          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - oppdaterer informasjon på kunde, setter statusflagg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Bytter ut blanke med nuller i tommer felter som skal konverteres
      *
     c                   eval      d_parm = p_parm
      *
     c                   if        d_ponr = *blanks
     c     ' ':'0'       xlate     d_ponr        d_ponr
     c                   endif
     c                   if        d_bpnr = *blanks
     c     ' ':'0'       xlate     d_bpnr        d_bpnr
     c                   endif
     c                   if        d_fort = *blanks
     c     ' ':'0'       xlate     d_fort        d_fort
     c                   endif
     c                   if        d_prsn = *blanks
     c     ' ':'0'       xlate     d_prsn        d_prsn
     c                   endif
     c                   if        d_bgir = *blanks
     c     ' ':'0'       xlate     d_bgir        d_bgir
     c                   endif
     c                   if        d_eann = *blanks
     c     ' ':'0'       xlate     d_eann        d_eann
     c                   endif
     c                   if        d_avde = *blanks
     c     ' ':'0'       xlate     d_avde        d_avde
     c                   endif
     c                   if        d_lage = *blanks
     c     ' ':'0'       xlate     d_lage        d_lage
     c                   endif
     c                   if        d_selg = *blanks
     c     ' ':'0'       xlate     d_selg        d_selg
     c                   endif
     c                   if        d_dist = *blanks
     c     ' ':'0'       xlate     d_dist        d_dist
     c                   endif
     c                   if        d_bbet = *blanks
     c     ' ':'0'       xlate     d_bbet        d_bbet
     c                   endif
     c                   if        d_lbet = *blanks
     c     ' ':'0'       xlate     d_lbet        d_lbet
     c                   endif
     c                   if        d_sped = *blanks
     c     ' ':'0'       xlate     d_sped        d_sped
     c                   endif
     c                   if        d_rent = *blanks
     c     ' ':'0'       xlate     d_rent        d_rent
     c                   endif
     c                   if        d_krty = *blanks
     c     ' ':'0'       xlate     d_krty        d_krty
     c                   endif
     c                   if        d_avgf = *blanks
     c     ' ':'0'       xlate     d_avgf        d_avgf
     c                   endif
     c                   if        d_kreg = *blanks
     c     ' ':'0'       xlate     d_kreg        d_kreg
     c                   endif
      *
      * Omformer numeriske felter fra parameterfelt til variable
      *
      * Kundenummer
     c                   eval      w_kund = %dec(d_kund:6:0)
      * kreditgrense
     c                   eval      w_pos1 = %scan('-':d_kreg)
     c                   if        w_pos1 > 0
     c                   eval      b_sign = *on
     c                   eval      w_pos1 = w_pos1 + 1
     c                   else
     c                   eval      w_pos1 = 1
     c                   endif
      *
     c                   eval      w_pos2 = %scan(' ':d_kreg)
     c                   eval      w_pos2 = w_pos2 - 1
     c                   eval      w_kgra = %subst(d_kreg:w_pos1:w_pos2)
     c                   eval      w_kgrt = %dec(w_kgra:8:0)
     c                   eval      w_kgrn = w_kgrt / 1000
      *
      * Velger oppdatering avhengig av kommando
      *
     c                   select
     c                   when      p_comd = 'INSERT'
     c                   exsr      NyEndrK
     c                   when      p_comd = 'UPDATE'
     c                   exsr      NyEndrK
     c                   when      p_comd = 'DELETE'
     c                   exsr      sletkund
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Ny kundepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     NyEndrK       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      rkunlu_kund = w_kund
     c     rkunlu_key    chain     rkunlu
     c                   eval      rkfirm = w_firm
     c                   eval      rkkund = w_kund
     c                   eval      rknavn = d_navn
     c                   eval      rkgate = d_adr1
     c                   eval      rkadr2 = d_adr2
     c                   eval      rkponr = %dec(d_ponr:4:0)
     c                   eval      rksted = d_ponr + ' ' + d_pste
     c                   if        d_land = 'NO'
     c                   eval      rkland = *blank
     c                   else
     c                   eval      rkland = d_land
     c                   endif
     c                   eval      rkupnr = d_upnr
     c                   eval      rkftnr = %dec(d_fort:9:0)
     c                   eval      rkalfa = d_alfa
     c                   eval      rkpenr = %dec(d_prsn:11:0)
     c                   eval      rkemal = d_emai
     c                   eval      rkweba = d_weba
     c                   eval      rktlfn = d_tlfn
     c                   eval      rktlfx = d_tlfx
     c                   eval      rkbgir = %dec(d_bgir:11:0)
     c                   eval      rkeank = %dec(d_eann:13:0)
7.01 c                   if        rkspfa = 0
     c                   eval      rkavde = %dec(d_avde:4:0)
7.01 c                   endif
     c                   eval      rklagr = %dec(d_lage:2:0)
     c                   eval      rkslgr = %dec(d_selg:4:0)
     c                   eval      rkdist = %dec(d_dist:4:0)
     c                   eval      rkbetb = %dec(d_bbet:2:0)
     c                   eval      rkvalk = d_valk
     c                   eval      rklbet = %dec(d_lbet:2:0)
     c                   eval      rksped = %dec(d_sped:3:0)
     c                   if        d_sper = 'N'
     c                   eval      rksprk = *blank
     c                   eval      rkpass = *blank
     c                   else
     c                   eval      rksprk = d_sper
     c                   eval      rkpass = d_sper
     c                   endif
     c                   eval      rkgrns = w_kgrn
     c                   eval      rkndat = w_date
     c                   eval      rkntim = w_time
     c                   eval      rknsig = p_user
     c                   if        not %found(rkunlu)
     c                   eval      rkmobn = d_tlfm
     c                   eval      rkbgat = d_bad1
     c                   eval      rkbadr = d_bad2
     c                   eval      rkbpnr = %dec(d_bpnr:4:0)
     c                   eval      rkkgrk = d_kres
     c*                  eval      rkkres = %dec(d_krty:1:0)
     c*                  eval      rkmkod = %dec(d_avgf:1:0)
     c*                  eval      rkrenk = %dec(d_rent:1:0)
     c                   write     rkunlur
     c                   eval      p_stat = '1'
     c                   else
     c                   update    rkunlur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Slett kundepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sletkund      begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      rkunlu_kund = w_kund
     c     rkunlu_key    chain     rkunlu
     c                   if        %found
     c                   delete    rkunlur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_comd
     c                   parm                    p_parm
     c                   parm                    p_user
     c                   parm                    p_stat
      *
      * Key for posisjonering på valgt kode
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Setter "timestamp" for postoppdateringer
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
