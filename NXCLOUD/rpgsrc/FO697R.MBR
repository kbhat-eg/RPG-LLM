      /TITLE  ASOFAK: Utskrift av lev.plan-liste, plukker ordres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO697R
      * Beskrivelse . . : Utskrift av lev.plan-liste, plukker ordres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * 3.11  130900 AMD  Lagt på valg av leveringsmåte
5.31  * 5.31  250804 GRO  Endring i sorteringsstringen
5.53  * R5.53 230307 AMD  Utvidet sorteringsbegrep slik at både Navn og
      *                   Adresse 1 kan skape brudd
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     ffpsolu    uf a e           k disk    rename(fpsopfr:fpsolur)
      *
      * Definering av datastruktur for sortering
      *
     d                 ds
6.nu d d_sort                       114
     d  d_sor1                        8    overlay(d_sort)
     d  d_sor2                       30    overlay(d_sort:9)
     d  d_sor3                       16    overlay(d_sort:39)
     d  d_sor4                        3    overlay(d_sort:55)
     d  d_sor5                       17    overlay(d_sort:58)
6.nu d  d_sor6                        8    overlay(d_sort:75)
6.nu d  d_sor7                        2    overlay(d_sort:83)
      *
      * Definering av datastruktur for parameter inn
      *
     d                 ds
     d d_prec                        64
     d  d_otyp                        2    overlay(d_prec)
6.nu d  d_fonr                        8  0 overlay(d_prec:5)
6.nu d  d_tonr                        8  0 overlay(d_prec:13)
6.nu d  d_fkun                        6  0 overlay(d_prec:21)
6.nu d  d_tkun                        6  0 overlay(d_prec:27)
     d  d_fdat                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:33)
     d  d_tdat                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:43)
6.nu d  d_val1                        1  0 overlay(d_prec:53)
6.nu d  d_val2                        1  0 overlay(d_prec:54)
6.nu d  d_lmat                        2  0 overlay(d_prec:55)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_sfir                944    946  0
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_line     s                   like(fdline)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fpsolu_slin     s                   like(fkslin)
     d fpsolu_numm     s                   like(fksonr)
     d fpsolu_suff     s                   like(fkssuf)
     d fpsolu_stll     s                   like(fkstll)
     d fotxl1_line     s                   like(ftline)
     d fotxl1_numm     s                   like(ftnumm)
     d fotxl1_suff     s                   like(ftsuff)
      *
      * Definering av variable
      *
6.nu d w_tnum          s                   like(fonumm)
     d w_tsuf          s                   like(fosuff)
     d w_dato_alf      s              8
      *
      * Definering av parametere
      *
     d p_parm          s             64
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av utplukking av linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      d_prec = p_parm
      *
     c                   eval      w_firm = l_sfir
     c                   eval      fohel1_numm = d_fonr
     c                   eval      fohel1_suff = *zero
     c                   eval      w_tnum = d_tonr
     c                   eval      w_tsuf = 99
      *
     c                   if        w_tnum = *zero
6.nu c                   eval      w_tnum = 99999999
     c                   eval      w_tsuf = 99
     c                   endif
      *
     c     fohel1_key    setll     fohel1r
      *
     c     neste         tag
     c                   read      fohel1r                                90
     c     *in90         cabeq     *on           slutt
     c     fonumm        cabgt     w_tnum        slutt
      *
     c                   if        fonumm = w_tnum
     c                   if        fosuff > w_tsuf
     c                   goto      slutt
     c                   endif
     c                   endif
      *
      * Er ordretype lik den som er valgt ?
      *
     c                   if        footyp <> d_otyp
     c                   goto      neste
     c                   endif
3.11  *
3.11  * Er leveringsmåte lik den som er valgt ?
3.11  *
3.11 c                   if        d_lmat <> 0
3.11 c                   if        folmat <> d_lmat
3.11 c                   goto      neste
3.11 c                   endif
3.11 c                   endif
      *
      * Er kundenummer innenfor grensene ?
      *
     c                   if        fokund < d_fkun or
     c                             fokund > d_tkun
     c                   goto      neste
     c                   endif
      *
      * Er leveringsdato innenfor grensene ?
      *
     c                   if        foldat < d_fdat or
     c                             foldat > d_tdat
     c                   goto      neste
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Rutine for innhenting av riktig utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   eval      w_firm = l_sfir
      *
     c                   eval      fohel1_numm = fonumm
     c                   eval      fohel1_suff = fosuff
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
      *
     c                   eval      fotxl1_numm = fonumm
     c                   eval      fotxl1_suff = fosuff
      *
     c                   eval      fpsolu_numm = fonumm
     c                   eval      fpsolu_suff = fosuff
      *
      * Legger inn sorteringsbegrep  (Fast =Ordrenummer + suffix)
      *
     c                   eval      d_sort = *blank
      *
      * Henter
      *
     c                   exsr      ohead
     c                   exsr      foran
     c                   exsr      linje
     c                   exsr      etter
      *
     c                   goto      neste
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-heading og legger ut til sorterings-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     ohead         begsr
      *
     c                   eval      fpsolu_stll = *zero
     c                   eval      fpsolu_slin = *zero
     c     fpsolu_key    chain     fpsolur                            92
      *
      * Legger inn sorteringsbegrep  (Variabel del)
      *
     c                   if        d_val1 = 1
     c                   move      foavde        d_sor2
     c                   endif
      *
     c                   if        d_val1 = 2
     c     *iso0         move      foldat        w_dato_alf
     c                   move      w_dato_alf    d_sor1
     c                   endif
      *
     c                   if        d_val1 = 3
     c                   move      fokund        d_sor1
     c                   endif
      *
     c                   if        d_val1 = 4
     c                   move      fonumm        d_sor1
     c                   endif
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   move      fofirm        fksfir
     c                   move      fonumm        fksonr
     c                   move      fosuff        fkssuf
     c                   eval      fkslin = *zero
     c                   eval      fkstll = *zero
     c                   move      'H'           fkskod
     c                   movel     d_otyp        fksoty
     c                   move      d_val2        fksalt
     c                   movel     d_sort        fkssrt
     c                   write     fpsolur
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-linjer og legger ut til sorterings-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     linje         begsr
      *
     c                   eval      fodtl1_line = *zero
      *
     c     fodtl1_key    setll     fodtl1r
      *
     c     linje1        tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = w_firm
     c                   if        fdnumm = fodtl1_numm
     c                   if        fdsuff = fodtl1_suff
      *
     c                   eval      fpsolu_stll = 1
     c                   eval      fpsolu_slin = fdline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 3
     c                   move      fdline        fkslin
     c                   move      'V'           fkskod
     c                   movel     d_otyp        fksoty
     c                   move      d_val2        fksalt
     c                   movel     d_sort        fkssrt
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      linje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (FORAN) og legger ut til sorterings-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     foran         begsr
      *
     c                   eval      fotxl1_line = *zero
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     foran1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 4999
      *
     c                   eval      fpsolu_stll = 2
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 2
     c                   move      ftline        fkslin
     c                   move      'K'           fkskod
     c                   movel     d_otyp        fksoty
     c                   move      d_val2        fksalt
     c                   movel     d_sort        fkssrt
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      foran1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ordre-tekst (ETTER) og legger ut til sorterings-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     etter         begsr
      *
     c                   move      5000          fotxl1_line
      *
     c     fotxl1_key    setll     fotxl1r
      *
     c     etter1        tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = w_firm
     c                   if        ftnumm = fotxl1_numm
     c                   if        ftsuff = fotxl1_suff
     c                   if        ftline <= 9999
      *
     c                   eval      fpsolu_stll = 4
     c                   eval      fpsolu_slin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   eval      fkstll = 4
     c                   move      ftline        fkslin
     c                   move      'K'           fkskod
     c                   movel     d_otyp        fksoty
     c                   move      d_val2        fksalt
     c                   movel     d_sort        fkssrt
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      etter1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for file : ORDRE-TEKST-REGISTER
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxl1_numm
     c                   kfld                    fotxl1_suff
     c                   kfld                    fotxl1_line
      *
      * Key for file : PLUKK-S-LINJE-REGISTER
      *
     c     fpsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpsolu_numm
     c                   kfld                    fpsolu_suff
     c                   kfld                    fpsolu_stll
     c                   kfld                    fpsolu_slin
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
     c                   endsr
