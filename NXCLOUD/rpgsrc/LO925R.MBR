     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LO925R                                        *
      * Beskrivelse . . : Utskrift av Bestilling, telefax               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
5.01  * R5.01 221002 AMD  Løsning for å kunne sende e-mail
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.11  * R6.10 220211 BSA  Feil hvis tekstlinjer er laget på andre suffikser.
6.nu  * R6.30 120614 bof  Endringer i forb. med nummerutvidelse         *
6.30  * 6.30  051214 Bsa  Utvidet parameterliste til FÅ707R
6.31  * 6.30  101018 bof  Utvidet med trelast sortiment
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FLPSOL1    IPE  E             DISK    RENAME(LPSOPFR:LPSOL1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FVLTYL1    IF   E           K DISK    RENAME(VLTYPFR:VLTYL1R)
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTL1    IF   E           K DISK    RENAME(LODTPFR:LODTL1R)
     FLOTXL1    IF   E           K DISK    RENAME(LOTXPFR:LOTXL1R)
     FVVARL1    IF   E           K DISK    RENAME(VVARPFR:VVARL1R)
     Frlevl1    IF   E           K DISK    RENAME(RLEVPFR:rlevl1R)
     FAFIRL1    IF   E           K DISK    RENAME(AFIRPFR:AFIRL1R)
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
5.34 ffvlsl1    if   e           k disk    rename(fvlspfr:fvlsl1r)
      *
     FLO925P    O    E             PRINTER
      *
      * - - - - - - - - - - - - - - - -
      * Datastruktur for foretaksnummer
      * - - - - - - - - - - - - - - - -
      *
     D                 DS
     D  DSFFTX                 1     30
     D  DSFF01                 1      8
     D  DSFF02                 9     11
     D  DSFF03                12     21
     D  DSFF04                22     25
     D  DSFF05                22     25
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
     D  DSRUTI               800    809
5.01 D  dsoutq               810    819
     D  DSALIN               856    858  0
     D  DSFIRM               944    946  0
5.01  *
5.01  * Definering av parametre
5.01  *
5.01 d p_mail          s              1
5.01 d p_kund          s              6  0
6.nu d p_numm          s              8  0
5.02 d p_selg          s              4  0
5.34  *
5.34  * Definering av variabler i nøkler
5.34  *
5.34 d fvlsl1_numm     s                   like(fvnumm)
5.34 d fvlsl1_suff     s                   like(fvsuff)
5.34 d fvlsl1_line     s                   like(fvline)
      *
      * Definering av variable
      *
3.12 d w_prfi          s              1  0
3.12 d w_ffir          s              3  0
3.12 d w_avde          s              4  0
3.12 d w_fnav          s             30
3.12 d w_fad1          s             30
3.12 d w_fad2          s             30
3.12 d w_fste          s             30
3.12 d w_fftx          s             30
3.12 d w_tfax          s              8
3.12 d w_ftlf          s             11
3.12 d w_ffax          s             11
3.33 d w_tlfn          s             16
3.33 d w_tlfx          s             16
3.33 d w_mobn          s             16
6.30 d w_mail          s             50
6.30 d w_weba          s             50
6.31 d w_lv_nobb       s                   like(vvnobb)
6.31 d w_lv_lvar       s                   like(vvvare)
      *
     I/EJECT
     ILPSOL1R       01
     I                                          LKSSRT        L4
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * T1TOT  - Total   T1 på rapport
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
      *
      * Hent bestillings-heading
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C     KEY01         CHAIN     LOHEL1R                            61
     C   01FCYCLE        CASEQ     *ZERO         XSTRSR
     C                   ENDCS
      *
      * Bestillingshode
      *
     C     LKSKOD        CABEQ     'H'           TAG01
      *
      * Varelinje
      *
     C     LKSKOD        CABEQ     'V'           TAG02
      *
      * Kommentar/
      * tekstlinje
      *
     C     LKSKOD        CABEQ     'K'           TAG03
      *
      * Ferdig
      *
     C                   GOTO      XSLUTT
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01         TAG
      *
     c                   eval      a1bdat = 0
     c                   eval      a1ldat = 0
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        a1bdat
     c                   endif
      *
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        a1ldat
     c                   endif
      *
      * Legg inn bestillingsnummer
      *
     C                   MOVE      LONUMM        A1NUMM
     C                   MOVEL     '/'           A1SUFF
     C                   MOVE      LOSUFF        A1SUFF
      *
      * Flere bestillinger på samme
      * bestilling ?
      *
     C  NL4              GOTO      TAG01A
      *
      * Hente inn leverandørregister
      *
     C                   MOVE      LOLDOR        WWLEVR
     C     KEY04         CHAIN     rlevl1R                            64
     C                   MOVE      RLNAVN        A1NAVN
     C                   MOVE      RLGATE        A1GATE
     C                   MOVE      RLADR2        A1ADR2
     C                   MOVE      RLSTED        A1STED
     C                   MOVE      RLTLFN        A1LTLF
     C                   MOVE      RLTLFX        A1LFAX
      *
      * Er det registrert
      * leveringsadresse?
      *
     C                   MOVE      *OFF          *IN31
     C     LONAVN        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   MOVE      LONAVN        A2NAVN
     C                   MOVE      LOADR1        A2ADR1
     C                   MOVE      LOADR2        A2ADR2
     C                   MOVE      LOSTED        A2STED
     C                   ENDIF
      *
      * Er det registrert
      * leverandørens referanse?
      *
     C                   MOVE      *OFF          *IN33
     C     LODREF        IFNE      *BLANK
     C                   MOVE      *ON           *IN33
     C                   MOVE      LODREF        A1DREF
     C                   ENDIF
      *
      * Er det registrert
      * leveringsdato?
      *
     C                   MOVE      *OFF          *IN32
     C     LOLDAT        IFNE      *loval
     C                   MOVE      *ON           *IN32
     C                   ENDIF
      *
      * Er det registrert
      * ordrenummer?
      *
     C                   MOVE      *OFF          *IN36
     C     LOORNR        IFNE      *ZERO
     C                   MOVE      *ON           *IN36
     C                   MOVE      LOORNR        A1ORNR
     C                   ENDIF
      *
      * Er det registrert
      * leveringsmåte?
      *
     C                   MOVE      *OFF          *IN34
     C     LOLMTX        IFNE      *BLANK
     C                   MOVE      *ON           *IN34
     C                   MOVE      LOLMTX        A1LMTX
     C                   ENDIF
      *
      * Er bestilling sendt EDI ?
      *
     C                   MOVE      *OFF          *IN38
     C     LOBSEN        IFNE      *ZERO
     C                   MOVE      *ON           *IN38
     C                   ENDIF
      *
      * Flytte øvrige felter
      *
     C                   MOVE      LOVREF        A1VREF
5.01  *
5.01  * Legg ut leverandørnummer i parameter
5.01  *
5.01 c                   if        dsoutq = 'E_MAIL_ASP'
5.01 c                   eval      p_kund = loldor
5.02 c                   eval      p_numm = lonumm
5.02 c                   eval      p_selg = loinnk
5.01 c                   endif
      *
      * Skrive heading
      *
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Heading U/L4-brudd                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01A        TAG
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG02         TAG
      *
      * Nullstilling av variabler
      *
     C                   MOVE      *BLANK        D1VARN
     C                   MOVE      *BLANK        D1VARL
     C                   MOVE      *BLANK        D1TEK1
     C                   MOVE      *BLANK        D1TEK2
     C                   MOVE      *BLANK        D1ENH1
     C                   MOVE      *ZERO         D1ANT0
     C                   MOVE      *ZERO         D1ANT1
     C                   MOVE      *ZERO         D1ANT2
     C                   MOVE      *ZERO         D1ANT3
     C                   MOVE      *OFF          *IN44
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C                   MOVE      LKSLIN        WWLINE
     C     KEY02         CHAIN     LODTL1R                            62
      *
      * Hent linje-type
      *
     C                   MOVE      LDLTYP        WWLTYP
     C     KEY08         CHAIN     VLTYL1R                            68
      *
      * Sjekker om linjen ikke skrives på ordretypen
      *
     C     LOOTYP        CABEQ     VALO01        XSLUTT
      *
     C     LOOTYP        CABEQ     VALO02        XSLUTT
      *
     C     LOOTYP        CABEQ     VALO03        XSLUTT
      *
     C     LOOTYP        CABEQ     VALO04        XSLUTT
      *
      * Sjekker om tekstlinjer
      *
     C     VALKTX        CABEQ     1             TAG02A
      *
      * Oppslag mot vareregister
      *
     C                   MOVE      LDVARE        WWVARE
     C     KEY11         CHAIN     VVARL1R                            69
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_nobb = 0
6.31 c                   eval      w_lv_lvar = *blank
6.31 c/exec sql
6.31 c+ select vvlnob, vvllva
6.31 c+ into   :w_lv_nobb,
6.31 c+        :w_lv_lvar
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :ldvare and
6.31 c+        vvlldo = :ldldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
      *
      * Test på antall desimaler
      *
     C                   if        ldlvre = 0
     C     VVDESI        COMP      0                                      40
     C     VVDESI        COMP      1                                      41
     C     VVDESI        COMP      2                                      42
     C     VVDESI        COMP      3                                      43
     C   40              Z-ADD     LDANTA        D1ANT0
     C   41              Z-ADD     LDANTA        D1ANT1
     C   42              Z-ADD     LDANTA        D1ANT2
     C   43              Z-ADD     LDANTA        D1ANT3
     c                   else
     c                   z-add     ldanta        d1ant2
     c                   eval      *in42 = *on
     c                   endif
      *
      * Varelinje - bruk varenr fra NOBB0 og LEV
      *
     c                   if        ldlvre = 0
6.31 c                   if        w_lv_nobb <> 0
6.31 c                   movel     w_lv_lvar     d1varl
6.31 c                   movel     w_lv_nobb     d1varn
6.31 c                   else
     c                   movel     vvlvar        d1varl
     c                   movel     vvnobb        d1varn
6.31 c                   endif
     c                   else
     c                   movel     ldlvar        d1varl
     c                   movel     ldnobb        d1varn
     c                   endif
      *
     C                   MOVE      LDTEK1        D1TEK1
     C                   MOVE      LDTEK2        D1TEK2
     C                   MOVE      LDENH1        D1ENH1
      *
      * Skriv varelinjer
      *
     C     TAG02A        TAG
     C                   ADD       1             WLTELL
     C     VALKTX        IFNE      1
     C                   WRITE     D1DET                                30
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   Z-ADD     0             WLTELL
     C                   ENDIF
     C                   IF        LDTEK2 <> *blank
     C                   WRITE     D1DET2                               30
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   Z-ADD     0             WLTELL
     C                   ENDIF
     C                   ENDIF
5.34  *
5.34  * Sjekk om lengde spesifikasjon
5.34  *
5.34 c                   exsr      len_spec
5.34  *
     C                   ELSE
     C                   MOVE      LDTEK1        D1TEK1
     C                   MOVE      LDTEK2        D1TEK2
     C                   MOVE      *ON           *IN44
     C                   WRITE     D2DET                                30
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   Z-ADD     0             WLTELL
     C                   ENDIF
     C                   END
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG03         TAG
      *
     C                   MOVE      LKSONR        WWNUMM
     C                   MOVE      LKSSUF        WWSUFF
     C                   MOVE      LKSLIN        WWLINE
     C     KEY03         CHAIN     LOTXL1R                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     C     VAOSAM        IFEQ      0
     C     LKSLIN        IFGE      5000
     C                   GOTO      XSLUTT
     C                   END
     C                   ENDIF
      *
     C                   MOVEL     LTTEXT        E1TEXT
     C                   ADD       1             WLTELL
     C                   WRITE     E1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   Z-ADD     0             WLTELL
     C                   ENDIF
      *
     C     XSLUTT        TAG
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     CL4   DSALIN        SUB       24            WLTEST
      *
      * Trekker ut antall
      * linjer skrevet
      *
     CL4                 SUB       WLTELL        WLTEST
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     CL4                 SUB       10            WLTEST
      *
      * Skrive ny heading ?
      *
     CL4   WLTEST        IFLT      0
     CL4                 WRITE     A1HDR
     CL4                 Z-ADD     0             WLTELL
     CL4                 ENDIF
      *
      * Skrive totalsum
      *
     CL4                 WRITE     T1TOT                                30
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     CL4   VAOSAM        IFEQ      0
     CL4                 MOVE      LKSONR        WWNUMM
     CL4                 MOVE      LKSSUF        WWSUFF
     CL4                 Z-ADD     5000          WWLINE
     CL4   KEY03         SETLL     LOTXL1R
     CL4                 READ      LOTXL1R                                63
     CL4   *IN63         CABEQ     *ON           TAG99
      *                                                                 .
     CL4   WWNUMM        DOWEQ     LTNUMM
     CL4   WWSUFF        IFEQ      LTSUFF
      *                                                               . .
     CL4                 MOVEL     LTTEXT        E1TEXT
     CL4                 ADD       1             WLTELL
     CL4                 WRITE     E1DET                                30
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     CL4   *IN30         IFEQ      *ON
     CL4                 WRITE     A1HDR
     CL4                 Z-ADD     0             WLTELL
     CL4                 ENDIF
      *                                                               . .
6.11 CL4                 ENDIF
     CL4                 READ      LOTXL1R                                63
     CL4   *IN63         CABEQ     *ON           TAG99
6.11 C*4                 ENDIF
     CL4                 ENDDO
     CL4                 ENDIF
      *
     CL4   TAG99         TAG
5.34  *
5.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
5.34  * Subrutine for lengde spesifikasjon
5.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
5.34  *
5.34 c     len_spec      begsr
5.34  *
5.34 c                   eval      fvlsl1_numm = ldornu
5.34 c                   eval      fvlsl1_suff = ldorsu
5.34 c                   eval      fvlsl1_line = ldorli
5.34 c     fvlsl1_key    setll     fvlsl1
5.34 c     fvlsl1_key    reade     fvlsl1
5.34  *
5.34 c                   dow       not %eof
5.34 c                   eval      d1tek2 = %char(fvleng) + ' mm x ' +
5.34 c                                      %char(fvanta) + ' stk'
5.34  *
5.34 c                   write     d1det2                               30
5.34 C     *IN30         IFEQ      *ON
5.34 C                   WRITE     A1HDR
5.34 C                   Z-ADD     0             WLTELL
5.34 C                   ENDIF
5.34 c     fvlsl1_key    reade     fvlsl1
5.34 c                   enddo
5.34  *
5.34 c     end_spec      endsr
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSTRSR        BEGSR
      *
     C                   Z-ADD     1             FCYCLE            1 0
      *
      * Klargjøring dersom firma/avdelings -
      * opplysninger skal printes ut
      *
3.12 c                   if        afprfi = 1 or
3.12 c                             afprfi = 2
3.12 c                   eval      w_prfi = afprfi
3.12 c                   eval      w_ffir = wwfirm
3.12 c                   eval      w_avde = loavde
3.12  *
3.12 c                   call      'FÅ707R'
3.12 c                   parm                    w_prfi
3.12 c                   parm                    w_ffir
3.12 c                   parm                    w_avde
3.12 c                   parm                    w_fnav
3.12 c                   parm                    w_fad1
3.12 c                   parm                    w_fad2
3.12 c                   parm                    w_fste
3.12 c                   parm                    w_fftx
3.12 c                   parm                    w_tfax
3.12 c                   parm                    w_ftlf
3.12 c                   parm                    w_ffax
6.30 c                   parm                    w_mail
6.30 c                   parm                    w_weba
3.12  *
3.12 c                   eval      a1fnav = w_fnav
3.12 c                   eval      a1fad1 = w_fad1
3.12 c                   eval      a1fad2 = w_fad2
3.12 c                   eval      a1fste = w_fste
3.12 c                   eval      a1fftx = w_fftx
3.12 c*                  eval      a1tfax = w_tfax
3.12 c                   eval      a1ftlf = w_ftlf
3.12 c                   eval      a1ffax = w_ffax
3.12 c                   endif
      *
      * Slår opp ordretype
      *
     C                   MOVE      LKSOTY        WWOTYP
     C     KEY07         CHAIN     VOTYL1R                            67
      *
      * Er ordretype negativ ?
      *
     C     VAOKRE        IFEQ      '-'
     C                   MOVE      *ON           *IN36
     C                   ENDIF
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
5.01  *
5.01  * Parametre
5.01  *
5.01 c     *entry        plist
5.01 c                   parm                    p_mail
5.01 c                   parm                    p_kund
5.02 c                   parm                    p_numm
5.02 c                   parm                    p_selg
      *
     C     *LIKE         DEFINE    VAOTYP        WWOTYP
     C     *LIKE         DEFINE    LOLDOR        WWLEVR
     C     *LIKE         DEFINE    LOFIRM        WWFIRM
     C     *LIKE         DEFINE    LONUMM        WWNUMM
     C     *LIKE         DEFINE    LOSUFF        WWSUFF
     C     *LIKE         DEFINE    LDLINE        WWLINE
     C     *LIKE         DEFINE    LDLTYP        WWLTYP
     C     *LIKE         DEFINE    LDNUMM        SANUMM
     C     *LIKE         DEFINE    VVVARE        WWVARE
      *
     C                   MOVE      *BLANK        WWRUTI           10
     C                   MOVE      *ZERO         WLTELL            3 0
     C                   MOVE      *ZERO         WLTEST            3 0
      *
      * Key for Heading-file
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
      *
      * Key for Detalj-file
      *
     C     KEY02         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for Tekst-linjer
      *
     C     KEY03         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for leverandørregister
      *
     C     KEY04         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLEVR
      *
      * Key for firmaregister
      *
     C     KEY06         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for ordretyper
      *
     C     KEY07         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWOTYP
      *
      * Key for linjetyper
      *
     C     KEY08         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLTYP
      *
      * Key for statusregister
      *
     C     KEY09         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for formular-register
      *
     C     KEY10         KLIST
     C                   KFLD                    WWRUTI
      *
      * Key for vare-register
      *
     C     KEY11         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWVARE
5.34  *
5.34  * Key for oppslag på lengdespesif.linje
5.34  *
5.34 c     fvlsl1_key    klist
5.34 c                   kfld                    wwfirm
5.34 c                   kfld                    fvlsl1_numm
5.34 c                   kfld                    fvlsl1_suff
5.34 c                   kfld                    fvlsl1_line
      *
      * Legger inn firma i nøkkel
      *
     C                   MOVE      DSFIRM        WWFIRM
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     C                   MOVE      DSRUTI        WWRUTI
      *
     C     KEY10         CHAIN     AFORL1R                            69
     C  N69              MOVE      AFTXT1        A1TXT1
     C  N69              MOVE      AFMLD1        T1MLD1
5.01  *
5.01  * Sjekker om dette skal sendes
5.01  * som e-mail
5.01  *
5.01 c                   if        dsoutq = 'E_MAIL_ASP'
5.01 c                   eval      p_mail = 'J'
5.01 c                   endif
      *
     C                   ENDSR
