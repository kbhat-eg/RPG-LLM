     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LA681R
      *  Beskrivelse . . : Kontroll av lagerbeholdning mot milepeler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            150402  Nytt program                            ASK
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhisl1    if   e           k disk    rename(vhispfr:vhisl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fla681p    o    e             printer
      *
      * Parameter-inn
      *
     d                 ds
     d d_parm                        20
     d  d_firm                        3  0 overlay(d_parm)
     d  d_lage                        2  0 overlay(d_parm:4)
     d  d_val1                        1  0 overlay(d_parm:6)
     d  d_val2                        1  0 overlay(d_parm:7)
     d  d_val3                        1  0 overlay(d_parm:8)
     d  d_dato                         d   datfmt(*iso)
     d                                     overlay(d_parm:9)
     d  d_dumm                        1    overlay(d_parm:20) inz('*')
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definere variable i nøkler
      *
     d vhisl1_lage     s                   like(vhlage)
     d vhisl1_vare     s                   like(vhvare)
     d vhisl1_dato     s                   like(vhdato)
     d vlagl1_vare     s                   like(vllage)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definere øvrige variable
      *
     d w_firm          s                   like(vhfirm)
     d w_parm          s             20
     d w_anta          s                   like(vhanta)
     d w_dato          s              6  0
     d w_dat01         s               d   datfmt(*iso)
     d w_dato_6        s              6  0
     d w_time          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra lager for kontroll - bare valgt lager behandles
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      skriv_head
      *
     c     vlagl1_key    setll     vlagl1r
     c     vlagl1_key    reade     vlagl1r
      *
     c                   dow       %eof = *off
     c                   if        vllage = d_lage
     c                   eval      vhisl1_vare = vlvare
     c                   exsr      sjekk_hist
     c                   endif
     c     vlagl1_key    reade     vlagl1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av historisk saldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_hist    begsr
      *
      * Leser informasjon fra vare
      *
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      end_hist
     c                   endif
      *
      * Setter startverdier avhengig av valg
      *
     c                   select
     c                   when      d_val1 = 1
     c                   eval      w_anta = vlb101
     c                   eval      vhisl1_dato = w_dat01
     c                   when      d_val2 = 1
     c                   eval      w_anta = 0
     c                   eval      vhisl1_dato = vlopda
     c                   when      d_val3 = 1
     c                   exsr      finn_dato
     c                   eval      vhisl1_dato = d_dato
     c                   endsl
      *
      * Leser historikkposter og beregner
      *
     c     vhisl1_key    setll     vhisl1r
     c                   read      vhisl1r
      *
     c                   dow       %eof = *off and
     c                             vhfirm = w_firm and
     c                             vhvare = vlvare and
     c                             vhlage = d_lage
     c                   select
     c                   when      vhkode = 'T '
     c                   eval      w_anta = vhbehl
     c                   when      vhtype = 'I'
     c                   eval      w_anta = w_anta + vhanta
     c                   when      vhtype = 'U'
     c                   eval      w_anta = w_anta - vhanta
     c                   endsl
      *
     c                   read      vhisl1r
     c                   enddo
      *
     c                   if        vlbehl <> w_anta
     c                   eval      d1vare = vvvare
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1behl = vlbehl
     c                   eval      d1akkb = w_anta
     c                   eval      d1opda = vlopda
     c                   eval      d1b101 = vlb101
     c                   write     d1det                                40
     c                   if        *in40 = *on
     c                   exsr      skriv_head
     c                   eval      *in40 = *off
     c                   endif
     c                   endif
      *
     c     end_hist      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne beholdning på angitt dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_dato     begsr
      *
     c                   eval      w_anta = 0
     c                   eval      vhisl1_dato = d_dato
     c     vhisl1_key    setll     vhisl1r
     c                   readp     vhisl1r
      *
     c                   if        %eof = *off and
     c                             vhfirm = w_firm and
     c                             vhvare = vlvare and
     c                             vhlage = d_lage
     c                   eval      w_anta = vhbehl
     c                   endif
      *
     c     end_dato      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_head    begsr
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1lage = d_lage
      *
     c                   select
     c                   when      d_val1 = 1
     c     *dmy          move      w_dat01       a1dato
     c                   eval      *in30 = *off
     c                   when      d_val2 = 1
     c                   eval      a1dato = 0
     c                   eval      *in30 = *on
     c                   when      d_val3 = 1
     c                   eval      *in30 = *off
     c     *dmy          move      d_dato        a1dato
     c                   endsl
      *
     c                   write     a1hdr
      *
     c     end_head      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av HISTORISK-LAGERBOK
      *
     c     vhisl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl1_lage
     c                   kfld                    vhisl1_vare
     c                   kfld                    vhisl1_dato
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_parm = w_parm
      *
      *  Legge inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      vhisl1_lage = d_lage
      *
      *  Legger inn 01.01 dette år
      *
     c                   time                    w_dat01
     c     *ymd          move      w_dat01       w_dato_6
     c                   move      0101          w_dato_6
     c     *ymd          move      w_dato_6      w_dat01
      *
     c                   endsr
