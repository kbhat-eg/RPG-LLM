      /title  ASLAGR Håndterminal ordre-hode, spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HO500R
      * Beskrivelse . . : Håndterminal ordre-hode, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050600 ASK  Nytt program
      *       280900 HKO  Tilpasset nye filer
      *       131200 HKO  Lagt inn kundeprosjekt
      *       230301 HKO  Lagt inn lev.adr og kundens ref.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhohelr    if   e           k disk    rename(hohepfr:hohelrr)
     fhohel1    if   e           k disk    rename(hohepfr:hohel1r)
     fhohel2    if   e           k disk    rename(hohepfr:hohel2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     fho500d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(hofirm)
     d p_refn          s                   like(horefn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d hohelr_refn     s                   like(horefn)
     d hohel1_refn     s                   like(horefn)
     d hohel2_kund     s                   like(hokund)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_hent          s              1    inz(*off)
      *
     d w_dato          s              6  0
     d w_time          s              6  0
      *
     d w_firm          s                   like(hofirm)
     d w_refn          s                   like(horefn)
     d w_navn          s                   like(rknavn)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = 0 and
     c                             b2refn <> 0 or
     c                             b2kund <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> 0
     c                   exsr      control
     c                   if        b_hent = *on
     c                   goto      avslutt
     c                   endif
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_hent = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   call      'HO700C'
      *
     c                   select
     c                   when      w_seqe = 'K'
     c     hohel2_key    setll     hohel2
     c                   other
     c     hohel1_key    setll     hohel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på referansenummer
      *
     c                   if        b2refn <> 0
     c                   eval      w_seqe = ' '
     c                   eval      hohel1_refn = b2refn
     c     hohel1_key    setll     hohel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundenummer
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      hohel2_kund = b2kund
     c     hohel2_key    setll     hohel2
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c     end_pos       tag
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2refn = 0
     c                   eval      b2kund = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_feil = *off
      *
      * Validering av nøkkel
      *
     c                   if        b2refn = 0
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      hohelr_refn = b2refn
     c     hohelr_key    chain     hohelr                             90
     c                   if        *in90
     c                   eval      b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Hent inn
      *
     c                   if        b2valg = 1
     c                   eval      w_refn = b2refn
     c                   exsr      hent_inn
     c                   goto      end_ctl
     c                   endif
      *
      * Vis detaljer
      *
     c                   if        b2valg = 5
     c                   eval      w_refn = b2refn
     c                   exsr      vis_detalj
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = 0
     c                   eval      b2refn = 0
     c                   eval      b2kund = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Hente inn
      *
     c                   if        b1valg = 1
     c                   eval      w_refn = b1refn
     c                   exsr      hent_inn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Se på detaljer
      *
     c                   if        b1valg = 5
     c                   eval      w_refn = b1refn
     c                   exsr      vis_detalj
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for innhenting til ordre/butikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_inn      begsr
      *
     c                   eval      b_hent = *off
      *
     c                   eval      hohelr_refn = w_refn
     c     hohelr_key    chain     hohelr                             92
     c                   if        *in92 = *off
      *
     c                   if        hostat = 1
     c                   exfmt     b8win
     c                   if        *inkj = *off
     c                   eval      b_feil = *on
     c                   goto      end_hent
     c                   endif
     c                   endif
      *
     c                   eval      p_refn = w_refn
     c                   eval      b_hent = *on
      *
     c                   endif
      *
     c     end_hent      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på detaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_detalj    begsr
      *
     c                   call      'HD510R'
     c                   parm                    w_firm
     c                   parm                    w_refn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
     c                   if        w_actf = 'B2KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_navn
      *
     c                   if        b2kund <> 0
     c                   exsr      posisjoner
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   read      hohel1                                 15
     c                   when      w_seqe = 'K'
     c                   read      hohel2                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        hofirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1refn = horefn
     c                   eval      b1kund = hokund
     c                   eval      b1selg = hoselg
     c                   eval      b1navn = honavn
     c                   eval      b1dref = hodref
     c                   eval      b1knav = *blank
     c                   eval      b1pnav = *blank
     c                   eval      b1dato = 0
     c                   eval      b1time = 0
      *
     c                   eval      rkunl1_kund = hokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      b1knav = rknavn
     c                   else
     c                   eval      b1knav = 'Ukjent kunde'
     c                   endif
      *
     c                   eval      fkprl1_kund = hokund
     c                   eval      fkprl1_kpro = hokpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
     c                   eval      b1pnav = flnavn
     c                   endif
      *
     c                   if        hodato <> *loval
     c     *dmy          move      hodato        w_dato
     c                   movel     w_dato        b1dato
     c                   endif
      *
     c                   if        hotime <> *loval
     c     *hms          move      hotime        w_time
     c                   movel     w_time        b1time
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_refn
      *
      * Key for oppslag på ordre-hode
      *
     c     hohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohelr_refn
      *
      * Key for les på ordre-hode
      *
     c     hohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohel1_refn
      *
      * Key for posisjonering på kunde
      *
     c     hohel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohel2_kund
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      *
      * Initierer verdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      p_refn = 0
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      hohel1_refn = 0
     c     hohel1_key    setll     hohel1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
