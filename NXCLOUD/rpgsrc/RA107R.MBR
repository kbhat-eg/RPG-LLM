     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA107R
      * Beskrivelse . . : Avdelinger, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250302 KOB  Nytt program
T5.00 * R5.00 110702 HFL  Lagt inn sperrekode.
T5.00 * R5.30 161203 KOB  Lagt inn leveringstype
6.20  * R6.20 230211 bsa  Endret lengde på avdelingstekst fra 20 til 30 pos.
6.20  *                   (endring gjelder kun displayfile)
6.21  * R6.20 250511 bsa  Lagt ut web og email adresse for vedlikehold
7.xx  * 7.00  010316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
     fra07l2    if   e           k disk    rename(ra07pfr:ra07l2r)
     fra07lr    if   e           k disk    rename(ra07pfr:ra07lrr)
     fra07lu    uf a e           k disk    rename(ra07pfr:ra07lur)
      *
     fra107d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra07l1_gkod     s                   like(ragkod)
     d ra07l2_gtxt     s                   like(ragtxt)
     d ra07lr_gkod     s                   like(ragkod)
     d ra07lu_gkod     s                   like(ragkod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(ragfir)
      *
     d s_gkod          s                   like(ragkod)
      *
     d p_retk          s              1
     d p_ponr          s                   like(ragpnr)
     d p_sted          s                   like(ragste)
     d p_ltxt          s             35
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2gkod <> 0 or
     c                             b2gtxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra07l2_gtxt = b1gtxt
     c     ra07l2_key    setll     ra07l2
     c                   other
     c                   eval      ra07l1_gkod = b1gkod
     c     ra07l1_key    setll     ra07l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Avdeling
      *
     c                   if        b2gkod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ra07l1_gkod = b2gkod
     c     ra07l1_key    setll     ra07l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2gtxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra07l2_gtxt = b2gtxt
     c     ra07l2_key    setll     ra07l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2gkod = 0
     c                   eval      b2gtxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1gkod = b1gkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1gkod = b1gkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1gkod = b1gkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1gkod = b1gkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1gkod = b2gkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1gkod = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1gkod = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra07lr_gkod = c1gkod
     c     ra07lr_key    chain     ra07lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1gkod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2gkod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra07lr_gkod = c1gkod
     c     ra07lr_key    chain     ra07lr
      *
     c                   if        %found
     c                   eval      c2gtxt = ragtxt
     c                   eval      c2gadr = ragadr
     c                   eval      c2gad2 = ragad2
     c                   eval      c2gpnr = ragpnr
     c                   eval      c2gste = ragste
     c                   eval      c2gtlf = ragtlf
     c                   eval      c2gfax = ragfax
     c                   eval      c2gbgi = ragbgi
T5.00c                   eval      c2gspr = ragspr
T5.00c                   eval      c2glty = raglty
6.21 c                   eval      c2gweb = ragweb
6.21 c                   eval      c2gmai = ragmai
     c                   eval      c2eusr = ragusr
     c     *dmy          move      ragdat        c2edat
     c                   else
     c                   eval      c2gtxt = *blank
     c                   eval      c2gadr = *blank
     c                   eval      c2gad2 = *blank
     c                   eval      c2gpnr = *zeros
     c                   eval      c2gste = *blank
     c                   eval      c2gtlf = *blank
     c                   eval      c2gfax = *blank
     c                   eval      c2gbgi = *zeros
T5.00c                   eval      c2gspr = *blank
T5.00c                   eval      c2glty = 0
6.21 c                   eval      c2gweb = *blank
6.21 c                   eval      c2gmai = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1gkod = k1gkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      * Call program for post-sted
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2GPNR'
      *
     c                   call      'AF010R'
     c                   parm                    p_ponr
      *
      * Hent poststed
      *
     c                   call      'RS730R'
     c                   parm                    p_retk
     c                   parm                    p_ponr
     c                   parm                    p_sted
      *
     c                   eval      c2gpnr = p_ponr
     c                   eval      c2gste = p_sted
      *
     c                   goto      c2taga
     c                   endif
      *
      * Spørring på leveringstype
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2GLTY'
     c                   call      'RY500C'
     c                   parm                    w_firm
     c                   parm                    c2glty
     c                   parm                    p_ltxt
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2gtxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk poststed
      *
     c                   if        c2gpnr <> *zeros
     c                   eval      p_ponr = c2gpnr
     c                   call      'RS730R'
     c                   parm                    p_retk
     c                   parm                    p_ponr
     c                   parm                    p_sted
      *
     c                   eval      c2gste = p_sted
      *
     c                   if        p_retk <> *blank
     c                   move      *on           *in32
     c                   goto      c2taga
     c                   endif
      *
     c                   else
      *
     c                   eval      c2gste = *blank
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra07lur
      *
     c                   eval      ra07lu_gkod = c1gkod
     c     ra07lu_key    chain     ra07lur
      *
     c                   eval      ragtxt = c2gtxt
     c                   eval      ragadr = c2gadr
     c                   eval      ragad2 = c2gad2
     c                   eval      ragpnr = c2gpnr
     c                   eval      ragste = c2gste
     c                   eval      ragtlf = c2gtlf
     c                   eval      ragfax = c2gfax
     c                   eval      ragbgi = c2gbgi
T5.00c                   eval      ragspr = c2gspr
T5.00c                   eval      raglty = c2glty
6.21 c                   eval      ragweb = c2gweb
6.21 c                   eval      ragmai = c2gmai
      *
     c                   time                    ragdat
     c                   time                    ragtim
     c                   eval      ragusr = l_user
      *
     c                   if        %found
     c                   update    ra07lur
     c                   else
     c                   eval      ragfir = w_firm
     c                   eval      ragkod = c1gkod
     c                   write     ra07lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1gtxt = ragtxt
     c                   eval      b1gadr = ragadr
     c                   eval      b1gpnr = ragpnr
     c                   eval      b1gste = ragste
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra07lr_gkod = d1gkod
     c     ra07lr_key    chain     ra07lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra07lu_gkod = d1gkod
     c     ra07lu_key    chain     ra07lur
      *
     c                   delete    ra07lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_gkod = k1gkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra07lr_gkod = k1gkod
     c     ra07lr_key    chain     ra07lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1gkod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra07lr_gkod = k1gkod
     c     ra07lr_key    chain     ra07lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1gkod = s_gkod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra07l2
     c                   other
     c                   read      ra07l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ragfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra07l2_gtxt = ragtxt
     c                   other
     c                   eval      ra07l1_gkod = ragkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1gkod = ragkod
     c                   eval      b1gtxt = ragtxt
     c                   eval      b1gadr = ragadr
     c                   eval      b1gpnr = ragpnr
     c                   eval      b1gste = ragste
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra07l2_key    setgt     ra07l2
     c                   readp     ra07l2
     c                   other
     c     ra07l1_key    setgt     ra07l1
     c                   readp     ra07l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra07l2
     c                   other
     c                   readp     ra07l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ragfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra07l2_gtxt = ragtxt
     c                   other
     c                   eval      ra07l1_gkod = ragkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra07l2_key    setll     ra07l2
     c                   other
     c     ra07l1_key    setll     ra07l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_gkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra07l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l2_gtxt
      *
      * Key for les på avdeling
      *
     c     ra07lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07lr_gkod
      *
      * Key for oppdatering av avdeling
      *
     c     ra07lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07lu_gkod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra07l1_gkod = 0
     c     ra07l1_key    setll     ra07l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
