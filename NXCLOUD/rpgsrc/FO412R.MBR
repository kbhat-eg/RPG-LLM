      /TITLE  ASOFAK: Sletting av utplukk og merking av ordren
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO412R
      * Beskrivelse . . : Sletting av utplukk og merking av ordren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R4.00 100102 AMD  Skrevet om til ny standard
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffplolu    uf   e           k disk    rename(fplopfr:fplolur)
     ffplll1    if   e           k disk    rename(fpllpfr:fplll1r)
     ffplllu    uf   e           k disk    rename(fpllpfr:fplllur)
      *
      * Definering av parametere
      *
     d p_retu          s                   like(w_alf1)
     d p_firm          s                   like(fofirm)
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fplolu_numm     s                   like(fkponr)
     d fplolu_suff     s                   like(fkpsuf)
     d fplll1_lonr     s                   like(fklonr)
     d fplll1_lsuf     s                   like(fklsuf)
     d fplllu_lonr     s                   like(fklonr)
     d fplllu_lsuf     s                   like(fklsuf)
     d fplllu_llin     s                   like(fkllin)
      *
      * Definering av variable
      *
     d w_alf1          s              1
      *
      * Definering av local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av utplukk    UTPLUKK-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fplll1_key    setll     fplll1r
     c     fplll1_key    reade     fplll1r                                90
     c                   dow       *in90 = *off
      *
     c                   eval      fplllu_llin = fkllin
     c     fplllu_key    chain     fplllur                            91
     c                   if        *in91 = *off
     c                   delete    fplllur
     c                   endif
      *
     c     fplll1_key    reade     fplll1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av utplukk    UTPLUKK-PARAM-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fplolu_key    chain     fplolur                            92
     c                   if        *in92 = *off
     c                   delete    fplolur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Blanker ut merkingen i recorden i ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fohelu_key    chain     fohelur                            90
     c                   if        *in90 = *off
     c                   eval      fokode = *zero
     c                   eval      fokoda = *loval
     c                   eval      fokoti = *loval
     c                   eval      fokous = *blank
     c                   eval      fokows = *blank
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     fplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplolu_numm
     c                   kfld                    fplolu_suff
      *
      * Key for file : UTPLUKK-LINJE-REGISTER LES
      *
     c     fplll1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplll1_lonr
     c                   kfld                    fplll1_lsuf
      *
      * Key for file : UTPLUKK-LINJE-REGISTER UPDATE
      *
     c     fplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplllu_lonr
     c                   kfld                    fplllu_lsuf
     c                   kfld                    fplllu_llin
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_retu
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Legger inn firmanummer i nøkkler
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn ordrenummer og suffix i nøkklene
      *
     c                   eval      fohelu_numm = p_numm
     c                   eval      fohelu_suff = p_suff
     c                   eval      fplolu_numm = p_numm
     c                   eval      fplolu_suff = p_suff
     c                   eval      fplll1_lonr = p_numm
     c                   eval      fplll1_lsuf = p_suff
     c                   eval      fplllu_lonr = p_numm
     c                   eval      fplllu_lsuf = p_suff
     c                   eval      fplllu_llin = *zero
      *
     c                   endsr
