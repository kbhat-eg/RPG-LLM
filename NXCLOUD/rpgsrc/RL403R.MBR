     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RL403R
      * Beskrivelse . . : Leverandører, sletting av posteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V3.30 160202 KOB  Endret logikk for lesing av poster
      * V5.20 170205 HFL  Rettet feil, W_DATO endret.
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl1    if   e           k disk    rename(rltrpfr:rltrl1r)
     frltrlu    uf a e           k disk    rename(rltrpfr:rltrlur)
      *
     d rlevl1_firm     s                   like(rlfirm)
     d rltrl1_firm     s                   like(rlfirm)
     d rltrl1_levr     s                   like(rllevr)
      *
     d rltrlu_firm     s                   like(rlfirm)
     d rltrlu_levr     s                   like(rllevr)
     d rltrlu_bdat     s                   like(robdat)
     d rltrlu_tist     s                   like(rotist)
      *
     d w_firm          s                   like(rlfirm)
     d w_belØ          s                   like(robelØ)
     d w_rest          s                   like(rorest)
     d w_raar          s                   like(roraar)
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste - inn
      *
     d p_list          s             64
      *
     d                 ds
     d d_list                        64
     d  d_raar                        4  0 overlay(d_list)
      *
     d                 ds
E5.20d w_dato                         8  0
E5.20d  w_aar                         4  0 overlay(w_dato)
E5.20d  w_mnd                         2  0 overlay(w_dato:5)
E5.20d  w_dag                         2  0 overlay(w_dato:7)
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
      * Leser alle leverandører
      *
     c                   eval      rlevl1_firm = w_firm
      *
     c     rlevl1_key    setll     rlevl1r                                90
     c     rlevl1_key    reade     rlevl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      poster
      *
      * Lag en ny saldopost
      *
     c                   if        w_belØ <> 0 or
     c                             w_rest <> 0
      *
     c                   eval      w_aar = d_raar - 1
     c                   eval      w_mnd = 12
     c                   eval      w_dag = 31
E5.20c                   move      w_dato        robdat
      *
     c                   eval      w_tell = 1
      *
     c     rltrlu_tst    tag
      *
      * Teller/Timestamp
      *
     c                   move      w_tell        w_tell_alf
     c                   time                    rotist
     c     *iso0         move      rotist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rotist
      *
     c                   eval      rltrlu_firm = rofirm
     c                   eval      rltrlu_levr = rolevr
     c                   eval      rltrlu_bdat = robdat
     c                   eval      rltrlu_tist = rotist
      *
     c     rltrlu_key    chain     rltrlur                            91
      *
     c                   if        *in91 = *off
     c                   eval      w_tell = w_tell + 1
     c                   goto      rltrlu_tst
     c                   endif
      *
     c                   eval      robelØ = w_belØ
     c                   eval      rorest = w_rest
6.NU c                   eval      robiln = 99999999
E5.20c                   eval      roraar = w_aar
     c                   eval      rorper = 12
     c                   eval      rofdat = robdat
     c                   eval      robilk = 98
     c                   eval      rotext = 'Saldo pr. 31.12.'
     c                   move      w_aar         rotext
     c                   eval      rojour = *zeros
     c                   eval      rovalk = *blank
     c                   eval      rovalb = *zeros
     c                   eval      rovalr = *zeros
     c                   eval      rorefn = *zeros
     c                   eval      rovirk = *blank
     c                   eval      roavde = *zeros
     c                   eval      rorekl = *blank
     c                   eval      rosamb = *zeros
     c                   eval      robetk = *blank
     c                   eval      roatts = *blank
     c                   eval      rorrf1 = *zeros
     c                   eval      rorrf2 = *zeros
     c                   eval      rodeck = *blank
     c                   eval      robfnr = *zeros
     c                   eval      robfli = *zeros
     c                   eval      rokidi = *blank
     c                   eval      rodelb = *zeros
     c                   eval      roresn = *zeros
     c                   eval      roress = *zeros
     c                   eval      roresa = *zeros
     c                   eval      roresm = *blank
      *
6.10 c                   time                    roodat
6.10 c                   time                    rootim
6.10 c                   eval      roousr = l_user
6.10 c                   time                    roedat
6.10 c                   time                    roetim
6.10 c                   eval      roeusr = l_user
     c                   write     rltrlur
     c                   endif
      *
     c                   eval      w_belØ = 0
     c                   eval      w_rest = 0
      *
     c     rlevl1_key    reade     rlevl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     poster        begsr
      *
      * Leser leverandørposter
      *
     c                   eval      rltrl1_firm = w_firm
     c                   eval      rltrl1_levr = rllevr
     c     rltrl1_key    setll     rltrl1r                                90
     c     rltrl1_key    reade     rltrl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   if        roraar < d_raar and
     c                             rorest = 0
      *
     c                   eval      w_belØ = w_belØ + robelØ
     c                   eval      w_rest = w_rest + rorest
      *
     c                   eval      rltrlu_firm = rofirm
     c                   eval      rltrlu_levr = rolevr
     c                   eval      rltrlu_bdat = robdat
     c                   eval      rltrlu_tist = rotist
     c     rltrlu_key    chain     rltrlur                            91
      *
     c                   delete    rltrlur
     c                   endif
      *
     c     rltrl1_key    reade     rltrl1r                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_list
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = l_firm
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    rlevl1_firm
      *
      * Key for leverandørposter
      *
     c     rltrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl1_levr
      *
      * Key for leverandørposter
      *
     c     rltrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlu_levr
     c                   kfld                    rltrlu_bdat
     c                   kfld                    rltrlu_tist
      *
     c                   endsr
