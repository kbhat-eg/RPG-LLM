     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV544R
      * Beskrivelse . . : Vare, Bytte Enhet i EVR hvis :
      *     Basisenhet = Gml enhet med en omr.faktor
      *     som er lik omr.faktor i ny enhet.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020909 bof  Nytt program
6.11  *       120510 bof  Utvidet omr.faktor til 8,6
6.12  *       160810 bof  Justert utlegg av vare i subfile
6.30  *       280114 bof  Utvidet nøkkel med lev.nr i JVPKPF
6.31  *       010414 bof  Utvidet med lev.nr ved kall JW510R
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjvpglr    if   e           k disk    rename(jvpgpfr:jvpglrr)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
     fjv544d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s                   like(jvldor)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d vvarla_ldor     s                   like(vvldor)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d jvpkl1_vare     s                   like(jwvare)
6.30 d jvpkl1_ldor     s                   like(jwldor)
     d jvpglr_vare     s                   like(jwgvar)
     d vvenl1_vare     s                   like(vevare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
      *
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_firm          s                   like(vvfirm)
      *
     d b_bytt          s              1    inz(*off)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(15)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkh
     c                   eval      e1ldor = b2ldor
     c                   eval      e1lnav = b2lnav
     c                   exsr      xe1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     vvarla_key    setll     vvarla
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Detaljer
      *
     c                   if        b1valg = 5
     c                   call      'JV510R'
     c                   parm                    p_firm
     c                   parm                    b1vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise vare-pris
      *
     c                   if        b1valg = 6
     c                   call      'JX510R'
     c                   parm                    b1vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise vare-enhet
      *
     c                   if        b1valg = 7
     c                   call      'JW510R'
     c                   parm                    p_firm
     c                   parm                    b1vare
6.30 c                   parm                    p_ldor
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.11  *
6.11  * Endre enhet i EVR
6.11  *
6.11 c                   if        b1valg = 8
6.11 c                   call      'JV144R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    b1vare
6.11 c                   eval      b1valg = 0
6.11 c                   update    b1sfl
6.11 c                   iter
6.11 c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bytte alle enheter pr. denne lev. som har status E
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xe1win        begsr
      *
     c
     c     e1taga        tag
     c                   exfmt     e1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   call      'JV744R  '
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     vvarla_key    reade     vvarla
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   exsr      sjekk_sts3
      *
     c                   if        b_bytt = *off
     c                   iter
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   movel     vvvare        b1vare
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1benh = vvenhe
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1genh = jwgenh
6.11 c                   if        jwgofa < 1000000
     c                   eval      b1gofa = jwgofa
6.11 c                   else
6.11 c                   eval      b1gofa = 999999,999999
6.11 c                   endif
     c                   eval      b1nenh = jwenhe
6.11 c                   if        jwofak < 1000000
     c                   eval      b1nofa = jwofak
6.11 c                   else
6.11 c                   eval      b1nofa = 999999,999999
6.11 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   exsr      forny
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
6.10  *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * Sjekker om varen har status E
6.10  * E = endring av enhet kan skje i overføringsrutinen
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  *
6.10 c     sjekk_sts3    begsr
6.10  *
6.10  *
6.10 c                   eval      jvpglr_vare = vvvare
6.10 c     jvpglr_key    setll     jvpglr
6.10 c     jvpglr_key    reade     jvpglr
6.10 c                   dow       not %eof
6.10  *
6.10 c                   if        jwgenh = vvenhe
6.10  * finner om denne er salgsenhet 1
6.10 c                   eval      vvenl1_vare = vvvare
6.10 c     vvenl1_key    chain     vvenl1
6.10 c                   if        not %found(vvenl1)
6.10 c                   leavesr
6.10 c                   endif
6.10 c                   if        vesaen > 1
6.10 c                   leavesr
6.10 c                   endif
6.10  * sjekker om omregningsfaktor på gml.pakn. er lik ny pakning
6.10 c
6.10 c                   eval      jvpkl1_vare = vvvare
6.30 c                   eval      jvpkl1_ldor = vvnlev
6.10 c     jvpkl1_key    setll     jvpkl1
6.10 c     jvpkl1_key    reade     jvpkl1
6.10 c                   dow       not %eof(jvpkl1)
6.10 c                   if        jwenhe = jwgenh and
6.10 c                             jwofak=  jwgofa
6.10  * status settes til E
6.10 c                   eval      b_bytt = *on
6.10 c                   leavesr
6.10 c                   endif
6.10 c     jvpkl1_key    reade     jvpkl1
6.10 c                   enddo
6.10  *
6.10 c                   endif
6.10  *
6.10 c     jvpglr_key    reade     jvpglr
6.10 c                   enddo
6.10  *
6.10 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for les på vare
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
      *
      * Key for les på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Key for les på leverandør
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på EVR-vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
      *
      * Key for oppslag på vare-pakning - gammel
      *
     c     jvpglr_key    klist
     c                   kfld                    jvpglr_vare
      *
      *
      * Intitierer skjermbildet
      *
     c                   eval      *in22 = *on
     c                   eval      w_firm = p_firm
      *
     c                   eval      jlevl1_ldor = p_ldor
     c     jlevl1_key    chain     jlevl1
     c                   eval      b2ldor = p_ldor
     c                   eval      b2lnav = jlnavn
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vvarla_ldor = jlenum
     c     vvarla_key    setll     vvarla
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
