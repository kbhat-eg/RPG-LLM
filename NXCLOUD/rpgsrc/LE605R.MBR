      /TITLE  ASLAGR: Lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LE605R
      *  Beskrivelse . . : Utskrift av telle-liste, plukker varenummer
      *                                             for syklisk telling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           261007  Nytt program - utgangspkt i  LE605R       BOF
5.70  *  PRGR     121208  Utvidet med prisgruppe/ varetype          BOF
6.11  *  6.10     110610  Justert i kall VL711R                     BOF
6.20  *  6.20     130511  Lagt inn nye sorteringsbegreper på liste  ASK
6.21  * 6.20  190511 bof  Henter utg. fra lager - utvidet paramtre
6.22  * 6.20  021215 bsa  Må alltid forhøye, hvis ikke kan en registrere
6.22  *                   at varer aldri blir plukket.
6.3q  *       260515 bof  Datoformat sql
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fmstpl1    if   e           k disk    rename(mstppfr:mstpl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     flpstlu    uf a e           k disk    rename(lpstpfr:lpstlur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av array
      *
     d a_lage          s              2    dim(100)
     d a_lagl          s              2    dim(100)
     d a_tabc          s              2    dim(9)
      *
      * Datastruktur for parameterliste
      *
     d                 ds
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fvar                       15    overlay(d_prec:11)
     d  d_tvar                       15    overlay(d_prec:26)
     d  d_fogr                        2  0 overlay(d_prec:41)
     d  d_togr                        2  0 overlay(d_prec:43)
     d  d_fhgr                        2  0 overlay(d_prec:45)
     d  d_thgr                        2  0 overlay(d_prec:47)
     d  d_fugr                        3  0 overlay(d_prec:49)
     d  d_tugr                        3  0 overlay(d_prec:52)
     d  d_fpla                        8    overlay(d_prec:55)
     d  d_tpla                        8    overlay(d_prec:63)
     d  d_flag                        2  0 overlay(d_prec:71)
     d  d_tlag                        2  0 overlay(d_prec:73)
     d  d_list                        1  0 overlay(d_prec:75)
     d  d_hovd                        1  0 overlay(d_prec:76)
     d  d_val1                        1  0 overlay(d_prec:77)
     d  d_val2                        1  0 overlay(d_prec:78)
     d  d_val3                        1  0 overlay(d_prec:79)
     d  d_opda                        6  0 overlay(d_prec:80)
     d  d_vtyp                        1    overlay(d_prec:86)
     d  d_pans                        4    overlay(d_prec:87)
     d  d_levr                        6  0 overlay(d_prec:92)
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lpstlu_lage     s                   like(lzlage)
     d lpstlu_vare     s                   like(lzvare)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d mstpl1_aar      s                   like(msaar)
     d mstpl1_lage     s                   like(mslage)
     d ra10l1_kode     s                   like(rajkod)
      *
      * Definering av variable
      *
     d w_dato          s               d   datfmt(*iso)
     d w_opda          s               d   datfmt(*iso)
     d w_firm          s                   like(lzfirm)
     d w_lage          s                   like(vllage)
     d w_plas          s                   like(vlplas)
     d w_vare          s                   like(vvvare)
     d w_tabc          s                   like(vltabc)
     d w_axdd          s                   like(msaxag)
     d w_aydd          s                   like(msaxag)
     d w_azdd          s                   like(msaxag)
     d w_bxdd          s                   like(msaxag)
     d w_bydd          s                   like(msaxag)
     d w_bzdd          s                   like(msaxag)
     d w_cxdd          s                   like(msaxag)
     d w_cydd          s                   like(msaxag)
     d w_czdd          s                   like(msaxag)
      *
     d w_parm          s            128
     d w_anta          s                   like(lzantl)
     d w_ltel          s              3  0
     d w_sort          s             60
     d w_ogr_alf       s              2
     d w_hgr_alf       s              2
     d w_ugr_alf       s              3
     d w_lag_alf       s              2
     d w_var_alf       s             15
6.20 d w_lvr_alf       s              6
     d i               s              2  0
     d j               s              3  0
     d w_antv          s              6  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
6.21 d b_inlr          s              1    inz(*off)
      *
     d b_tell          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
     c                   exsr      les_s_parm
      *
     c                   eval      d_prec = w_parm
      *
     c                   if        d_opda <> 0
     c     *dmy          move      d_opda        w_opda
     c                   endif
      *
      *  Sjekk mot eksisterende poster i lager?
      *
     c                   if        d_list = 0
     c                   exsr      lag_tab
     c                   endif
     c                   exsr      lag_tab_les
      *
     c                   eval      j = 1
     c                   dow       j <  w_ltel
     c                   move      a_lagl(j)     w_lage
      *
      *  Leser lager for lager
      *
     c                   eval      i = 0
      *
      *  Leser pr. abc-kode
     c                   dow       i < 9
     c                   eval      i = i + 1
     c                   eval      w_tabc = a_tabc(i)
      *
      *  Leser fra vare-register
     c                   eval      w_antv = 1
     c                   exsr      sel_pr_kode
      *
      *  Leser fra vare/lager
     c     fetch_vare    tag
     c/exec sql
     c+                  fetch next
     c+                  from  sok_l
     c+                  into  :vlvare,
     c+                        :vvtek1,
     c+                        :vvogrp,
     c+                        :vvhgrp,
     c+                        :vvugrp,
     c+                        :vvvtyp,
     c+                        :vvldor,
     c+                        :vltabc,
     c+                        :vlatel
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   iter
     c                   endif
      *
      *
      *  Sjekk   Sjekker om varen er tellet max ant. ganger
      *
     c                   exsr      sjekk_antt
     c                   if        b_tell = *off
     c                   goto      neste_vare
     c                   endif
      *
      *
      *  Sjekk   Er varenummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        vlvare > d_tvar or
     c                             vlvare < d_fvar
     c                   leave
     c                   endif
      *
      *  Sjekk   Er overgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        vvogrp < d_fogr or
     c                             vvogrp > d_togr
     c                   goto      neste_vare
     c                   endif
      *
      *  Sjekk   Er hovedgruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        vvhgrp < d_fhgr or
     c                             vvhgrp > d_thgr
     c                   goto      neste_vare
     c                   endif
      *
      *  Sjekk   Er Undergruppe innenfor grensene  Hvis nei les neste post
      *
     c                   if        vvugrp < d_fugr or
     c                             vvugrp > d_tugr
     c                   goto      neste_vare
     c                   endif
      *
      * Seleksjon på produktansvarlig?
      *
     c                   if        d_pans <> *blank
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             95
      *
     c                   if        *in95 = *on
     c                   goto      neste_vare
     c                   endif
     c                   if        vgupra <> d_pans
     c                   goto      neste_vare
     c                   endif
     c                   endif
5.70  *
5.70  * Kaller program for henting av varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
6.11 c                   parm                    vlvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
      *  Sjekk   Er Varetype valgt og er den evt gyldig. "T" utelukkes.
      *
5.70 c**                 if        vvvtyp = 'T'
5.70 c                   if        w_vtyp = 'T'
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        d_vtyp <> *blank and
5.70 c                             w_vtyp <> d_vtyp
5.70 c**                           vvvtyp <> d_vtyp
     c                   goto      neste_vare
     c                   endif
      *
      *  Sjekk   Er Leverandør valgt og er det den riktige?
      *
     c                   if        d_levr <> 0 and
     c                             vvldor <> d_levr
     c                   goto      neste_vare
     c                   endif
      *
     c                   if        vlvare = '10019526'
     c                   eval      vvarl1_vare = vlvare
     c                   endif
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   eval      w_antv = w_antv + 1
      *
      *  Legg ut rader for alle lagre eller bare de som finnes
      *
     c                   if        d_list = 0
     c                   exsr      lag_alle
     c                   else
     c                   exsr      lag_finnes
     c                   endif
     c                   exsr      sorsub
      *
      *  Velg neste Vare-register post
      *
     c     neste_vare    tag
     c                   goto      fetch_vare
      *
     c                   enddo
      *
     c                   eval      j = j + 1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som legger ut rader for alle aktuelle lagre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_alle      begsr
      *
     c                   eval      vlagl1_vare = vvvare
     c                   eval      w_ltel = 1
     c                   dow       a_lage(w_ltel) <> *blank
     c                   move      a_lage(w_ltel)w_lage
     c                   eval      vlagl1_lage = w_lage
      *
     c     vlagl1_key    chain     vlagl1                             93
     c                   if        *in93 = *off
     c                   eval      w_anta = vlbehl
     c                   else
     c                   eval      w_anta = 0
     c                   endif
      *
      *  Er Lagerplass innenfor grensene?
      *
     c                   if        d_fpla <> *blank and
     c                             d_tpla <> *blank
     c                   if        vlplas < d_fpla or
     c                             vlplas > d_tpla
     c                   goto      end_alle
     c                   endif
     c                   endif
      *
      * Er bare null eller negativt valgt?
      *
     c                   if        d_val2 = 1 and
     c                             w_anta > 0
     c                   goto      end_alle
     c                   endif
      *
      * Finn sorteringsbegrep og skriv rad
      *
     c                   exsr      sorsub
     c                   exsr      skriv_rad
     c                   eval      w_anta = 0
     c                   eval      w_ltel = w_ltel + 1
     c                   enddo
      *
     c     end_alle      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som legger ut rader for lagre som finnes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_finnes    begsr
      *
     c                   eval      vlagl1_vare = vvvare
     c                   eval      vlagl1_lage = d_flag
     c     vlagl1_key    setll     vlagl1
      *
     c                   read      vlagl1                                 94
      *
     c                   dow       *in94 = *off and
     c                             vlfirm = w_firm and
     c                             vlvare = vvvare and
     c                             vllage <= d_tlag
      *
      *  Er Lagerplass innenfor grensene?
      *
     c                   if        d_fpla <> *blank and
     c                             d_tpla <> *blank
     c                   if        vlplas < d_fpla or
     c                             vlplas > d_tpla
     c                   goto      end_finnes
     c                   endif
     c                   endif
      *
      * Er bare null eller negativt valgt?
      *
     c                   if        d_val2 = 1 and
     c                             w_anta > 0
     c                   goto      end_finnes
     c                   endif
      *
      * Finn sorteringsbegrep og skriv rad
      *
     c                   eval      w_anta = vlbehl
     c                   eval      w_lage = vllage
      *
     c                   exsr      sorsub
     c                   exsr      skriv_rad
     c                   eval      w_anta = 0
      *
     c                   read      vlagl1                                 94
     c                   enddo
      *
     c     end_finnes    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som oppdaterer uplukkspost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_rad     begsr
      *
     c                   eval      lpstlu_vare = vvvare
     c                   eval      lpstlu_lage = w_lage
     c     lpstlu_key    chain     lpstlur                            92
      *
     c                   eval      lzvare = vvvare
     c                   eval      lztek1 = vvtek1
     c                   eval      lzenh1 = vvenhl
     c                   eval      lzlage = w_lage
     c                   eval      lzogrp = vvogrp
     c                   eval      lzhgrp = vvhgrp
     c                   eval      lzugrp = vvugrp
     c                   eval      lzplas = vlplas
5.70 c**                 if        vvvtyp = 'L'
5.70 c                   if        w_vtyp = 'L'
     c                   eval      lzantl = w_anta
     c                   endif
     c                   eval      lzsort = w_sort
      *
     c                   if        *in92 = *off
     c                   update    lpstlur
     c                   else
     c                   eval      lzfirm = w_firm
     c                   eval      lzvare = lpstlu_vare
     c                   eval      lzlage = lpstlu_lage
     c                   write     lpstlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som legger inn sorteringsbegrep
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *  Hva betyr de enkelte valg                    *
      *                                               *
      *  Valg1   Lager/vgr/varenummer                 *
6.20  *  Valg2   Lager/vgr/varetekst                  *
6.20  *  Valg3   Lager/varetekst                      *
6.20  *  Valg4   Lagerplass/varetekst                 *
6.20  *  Valg5   Lager/vgr/varetekst/lagerplass       *
6.20  *  Valg6   Lager/lev/vgr/varetekst/lagerplass   *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sorsub        begsr
      *
      *  Flytter sorteringsbegreper til alfa-felter
      *
     c                   move      vvogrp        w_ogr_alf
     c                   move      vvhgrp        w_hgr_alf
     c                   move      vvugrp        w_ugr_alf
     c                   move      w_lage        w_lag_alf
     c                   move      vvvare        w_var_alf
6.20 c                   move      vvldor        w_lvr_alf
      *
      *  Sorteringsrekkefølge
      *
     c                   if        d_val3 = 1
     c                   eval      w_sort = w_lag_alf + w_ogr_alf +
     c                                      w_hgr_alf + w_ugr_alf +
     c                                      w_var_alf
     c                   endif
      *
6.20 c                   if        d_val3 = 2
6.20 c                   eval      w_sort = w_lag_alf + w_ogr_alf +
6.20 c                                      w_hgr_alf + w_ugr_alf +
6.20 c                                      vvtek1
     c                   endif
      *
6.20 c                   if        d_val3 = 3
6.20 c                   eval      w_sort = w_lag_alf + vvtek1
6.20 c                   endif
      *
6.20 c                   if        d_val3 = 4
6.20 c                   eval      w_sort = vlplas + vvtek1
6.20 c                   endif
      *
     c                   if        d_val3 = 5
6.20 c                   eval      w_sort = w_lag_alf + w_ogr_alf +
6.20 c                                      w_hgr_alf + w_ugr_alf +
6.20 c                                      vvtek1 + vlplas
     c                   endif
      *
     c                   if        d_val3 = 6
6.20 c                   eval      w_sort = w_lag_alf +
6.20 c                                      w_lvr_alf + w_ogr_alf +
6.20 c                                      w_hgr_alf + w_ugr_alf +
6.20 c                                      vvtek1 + vlplas
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som finner aktuelle lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_tab       begsr
      *
     c                   eval      w_ltel = 1
     c                   eval      a_lage(*) = *blank
     c                   eval      ra10l1_kode = d_flag
     c     ra10l1_key    setll     ra10l1
      *
     c                   read      ra10l1                                 91
     c                   dow       *in91 = *off and
     c                             rajfir = w_firm and
     c                             rajkod <= d_tlag
     c                   move      rajkod        a_lage(w_ltel)
     c                   eval      w_ltel = w_ltel + 1
     c                   read      ra10l1                                 91
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som finner aktuelle lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_tab_les   begsr
      *
     c                   eval      w_ltel = 1
     c                   eval      a_lagl(*) = *blank
     c                   eval      ra10l1_kode = d_flag
     c     ra10l1_key    setll     ra10l1
      *
     c                   read      ra10l1                                 91
     c                   dow       *in91 = *off and
     c                             rajfir = w_firm and
     c                             rajkod <= d_tlag
     c                   move      rajkod        a_lagl(w_ltel)
     c                   eval      w_ltel = w_ltel + 1
     c                   read      ra10l1                                 91
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som gjør en select pr. abckode og lavest ant.telt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sel_pr_kode   begsr
      *
     c/exec sql
     c+                  declare sok_l cursor for
     c+                  select vlvare,
     c+                         vvtek1,
     c+                         vvogrp,
     c+                         vvhgrp,
     c+                         vvugrp,
     c+                         vvvtyp,
     c+                         vvldor,
     c+                         vltabc,
     c+                         vlatel
     c+                  from   vlagpf,vvarpf
     c+                  where  vlfirm = :w_firm and
     c+                         vllage = :w_lage and
     c+                         vltabc = :w_tabc and
     c+                         vlfirm = vvfirm  and
     c+                         vlvare = vvvare
     c+                  order by vlatel
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_l
     c/end-exec
     c/exec sql
     c+                  open sok_l
     c/end-exec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine som sjekker at varen ikke er tellet max ant. ganger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_antt    begsr
      *
     c                   eval      b_tell = *off
     c                   if        vlatel >= msantt
     c                   goto      end_sjekk
     c                   endif
     c                   select
     c                   when      vltabc = 'AX'
     c                   if        w_antv <= w_axdd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'AY'
     c                   if        w_antv <= w_aydd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'AZ'
     c                   if        w_antv <= w_azdd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'BX'
     c                   if        w_antv <= w_bxdd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'BY'
     c                   if        w_antv <= w_bydd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'BZ'
     c                   if        w_antv <= w_bzdd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'CX'
     c                   if        w_antv <= w_cxdd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'CY'
     c                   if        w_antv <= w_cydd
     c                   eval      b_tell = *on
     c                   endif
     c                   when      vltabc = 'CZ'
     c                   if        w_antv <= w_czdd
     c                   eval      b_tell = *on
     c                   endif
     c                   endsl
      *
     c     end_sjekk     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for les av syklisk telle parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_s_parm    begsr
     c                   time                    w_dato
     c                   eval      mstpl1_aar = %subdt(w_dato:*years)
     c                   eval      mstpl1_lage = d_flag
     c     mstpl1_key    chain     mstpl1
     c                   if        msantt > 0
     c                   eval(h)   w_axdd = (msaxaf * msaxag)/msantt
6.22 c                   if        w_axdd < 1 and
6.22 c                             msaxag > 0
6.22 c                   eval      w_axdd = 1
6.22 c                   endif
     c                   eval(h)   w_aydd = (msayaf * msayag)/msantt
6.22 c                   if        w_aydd < 1 and
6.22 c                             msayag > 0
6.22 c                   eval      w_aydd = 1
6.22 c                   endif
     c                   eval(h)   w_azdd = (msazaf * msazag)/msantt
6.22 c                   if        w_azdd < 1 and
6.22 c                             msazag > 0
6.22 c                   eval      w_azdd = 1
6.22 c                   endif
     c                   eval(h)   w_bxdd = (msbxaf * msbxag)/msantt
6.22 c                   if        w_bxdd < 1 and
6.22 c                             msbxag > 0
6.22 c                   eval      w_bxdd = 1
6.22 c                   endif
     c                   eval(h)   w_bydd = (msbyaf * msbyag)/msantt
6.22 c                   if        w_bydd < 1 and
6.22 c                             msbyag > 0
6.22 c                   eval      w_bydd = 1
6.22 c                   endif
     c                   eval(h)   w_bzdd = (msbzaf * msbzag)/msantt
6.22 c                   if        w_bzdd < 1 and
6.22 c                             msbzag > 0
6.22 c                   eval      w_bzdd = 1
6.22 c                   endif
     c                   eval(h)   w_cxdd = (mscxaf * mscxag)/msantt
6.22 c                   if        w_cxdd < 1 and
6.22 c                             mscxag > 0
6.22 c                   eval      w_cxdd = 1
6.22 c                   endif
     c                   eval(h)   w_cydd = (mscyaf * mscyag)/msantt
6.22 c                   if        w_cydd < 1 and
6.22 c                             mscyag > 0
6.22 c                   eval      w_cydd = 1
6.22 c                   endif
     c                   eval(h)   w_czdd = (msczaf * msczag)/msantt
6.22 c                   if        w_czdd < 1 and
6.22 c                             msczag > 0
6.22 c                   eval      w_czdd = 1
6.22 c                   endif
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for les av undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les av syklisk telle parameter
      *
     c     mstpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mstpl1_aar
     c                   kfld                    mstpl1_lage
      *
      * Key for lager-kode
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_kode
      *
      *  Key for oppdatering av LAGERUTSKRIFT-HJELPE-REGISTER
      *
     c     lpstlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpstlu_vare
     c                   kfld                    lpstlu_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      d_prec = w_parm
      *
     c                   eval      a_tabc(1) = 'AX'
     c                   eval      a_tabc(2) = 'AY'
     c                   eval      a_tabc(3) = 'AZ'
     c                   eval      a_tabc(4) = 'BX'
     c                   eval      a_tabc(5) = 'BY'
     c                   eval      a_tabc(6) = 'BZ'
     c                   eval      a_tabc(7) = 'CX'
     c                   eval      a_tabc(8) = 'CY'
     c                   eval      a_tabc(9) = 'CZ'
      *
     c                   endsr
