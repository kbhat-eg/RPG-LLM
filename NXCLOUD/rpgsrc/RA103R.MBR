     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA103R
      * Beskrivelse . . : Betalingsbetingelser, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240202 KOB  Nytt program
      *       090409 KOB  Lagt inn test på endringsdato
7.xx  * 7.00  010316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 6.30  271217 bsa  Legger inn adgangskontroll i programmet
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra03l1    if   e           k disk    rename(ra03pfr:ra03l1r)
     fra03l2    if   e           k disk    rename(ra03pfr:ra03l2r)
     fra03lr    if   e           k disk    rename(ra03pfr:ra03lrr)
     fra03lu    uf a e           k disk    rename(ra03pfr:ra03lur)
      *
     fra103d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra03l1_ckod     s                   like(rackod)
     d ra03l2_ctxt     s                   like(ractxt)
     d ra03lr_ckod     s                   like(rackod)
     d ra03lu_ckod     s                   like(rackod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_cnul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(racfir)
7.01 d w_tilgko        s             10
7.01 d w_rutine        s             10
7.01 d w_adv           s              3
7.01 d w_status        s             10
      *
     d s_ckod          s                   like(rackod)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
7.01 d u_s701          s              1    inz(*off)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_cfld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
7.01  *
7.01  * Sjekk om denne bruker har tilgang til denne F-tast
7.01  *
7.01 c                   if        u_s701 = *on
7.01 c                   if        *in16 = *on
7.01 c                   select
7.01 c                   when      *inkf
7.01 c                   eval      w_adv    = 'F06'
7.01 c                   other
7.01 c                   goto      f_tast_ok
7.01 c                   endsl
7.01 c                   eval      w_tilgko = l_user
7.01 c                   eval      w_rutine = 'RA103R'
7.01 c                   eval      w_status = *blank
7.01 c                   call      'AD005R'
7.01 c                   parm                    w_tilgko
7.01 c                   parm                    w_rutine
7.01 c                   parm                    w_adv
7.01 c                   parm                    w_status
7.01 c                   if        w_status = 'Sperret'
7.01 c                   goto      b2tagb
7.01 c                   endif
7.01 c                   endif
7.01 c     F_tast_ok     tag
7.01 c                   endif
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ckod <> 0 or
     c                             b2ctxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra03l2_ctxt = b1ctxt
     c     ra03l2_key    setll     ra03l2
     c                   other
     c                   eval      ra03l1_ckod = b1ckod
     c     ra03l1_key    setll     ra03l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Betalingsbetingelse
      *
     c                   if        b2ckod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ra03l1_ckod = b2ckod
     c     ra03l1_key    setll     ra03l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2ctxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra03l2_ctxt = b2ctxt
     c     ra03l2_key    setll     ra03l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ckod = 0
     c                   eval      b2ctxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
7.01  *
7.01  * Sjekk om denne bruker har tilgang til dette valg
7.01  *
7.01 c                   if        u_s701 = *on
7.01 c                   if        b1valg <> 0
7.01 c                   move      b1valg        w_adv
7.01 c                   eval      w_tilgko = l_user
7.01 c                   eval      w_rutine = 'RA103R'
7.01 c                   eval      w_status = *blank
7.01 c                   call      'AD005R'
7.01 c                   parm                    w_tilgko
7.01 c                   parm                    w_rutine
7.01 c                   parm                    w_adv
7.01 c                   parm                    w_status
7.01 c                   if        w_status = 'Sperret'
7.01 c                   eval      b1valg   = *zero
7.01 c                   update    b1sfl
7.01 c                   iter
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ckod = b1ckod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ckod = b1ckod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ckod = b1ckod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ckod = b1ckod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ckod = b2ckod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1ckod = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1ckod = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra03lr_ckod = c1ckod
     c     ra03lr_key    chain     ra03lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_cnul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ckod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ckod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_cnul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_cnul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra03lr_ckod = c1ckod
     c     ra03lr_key    chain     ra03lr
      *
     c                   if        %found
     c                   eval      c2ctxt = ractxt
     c                   eval      c2ctx2 = ractx2
     c                   eval      c2cdag = racdag
     c                   eval      c2crab = racrab
     c                   eval      c2cjan = racjan
     c                   eval      c2cda1 = racda1
     c                   eval      c2cda2 = racda2
     c                   eval      c2cda3 = racda3
     c                   eval      c2cra2 = racra2
     c                   eval      c2cra3 = racra3
     c                   eval      c2eusr = racusr
      *
     c                   if        racdat = *loval
     c                   eval      c2edat = 0
     c                   else
     c     *dmy          move      racdat        c2edat
     c                   endif
      *
     c                   else
     c                   eval      c2ctxt = *blank
     c                   eval      c2ctx2 = *blank
     c                   eval      c2cdag = *zeros
     c                   eval      c2crab = *zeros
     c                   eval      c2cjan = *blank
     c                   eval      c2cda1 = *zeros
     c                   eval      c2cda2 = *zeros
     c                   eval      c2cda3 = *zeros
     c                   eval      c2cra2 = *zeros
     c                   eval      c2cra3 = *zeros
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ckod = k1ckod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2ctxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra03lur
      *
     c                   eval      ra03lu_ckod = c1ckod
     c     ra03lu_key    chain     ra03lur
      *
     c                   eval      ractxt = c2ctxt
     c                   eval      ractx2 = c2ctx2
     c                   eval      racdag = c2cdag
     c                   eval      racrab = c2crab
     c                   eval      racjan = c2cjan
     c                   eval      racda1 = c2cda1
     c                   eval      racda2 = c2cda2
     c                   eval      racda3 = c2cda3
     c                   eval      racra2 = c2cra2
     c                   eval      racra3 = c2cra3
      *
     c                   time                    racdat
     c                   time                    ractim
     c                   eval      racusr = l_user
      *
     c                   if        %found
     c                   update    ra03lur
     c                   else
     c                   eval      racfir = w_firm
     c                   eval      rackod = c1ckod
     c                   write     ra03lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ctxt = ractxt
     c                   eval      b1cdag = racdag
     c                   eval      b1cjan = racjan
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra03lr_ckod = d1ckod
     c     ra03lr_key    chain     ra03lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra03lu_ckod = d1ckod
     c     ra03lu_key    chain     ra03lur
      *
     c                   delete    ra03lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ckod = k1ckod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra03lr_ckod = k1ckod
     c     ra03lr_key    chain     ra03lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1ckod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra03lr_ckod = k1ckod
     c     ra03lr_key    chain     ra03lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ckod = s_ckod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra03l2
     c                   other
     c                   read      ra03l1
     c                   endsl
      *
     c                   if        %eof or
     c                             racfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra03l2_ctxt = ractxt
     c                   other
     c                   eval      ra03l1_ckod = rackod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ckod = rackod
     c                   eval      b1ctxt = ractxt
     c                   eval      b1cdag = racdag
     c                   eval      b1cjan = racjan
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra03l2_key    setgt     ra03l2
     c                   readp     ra03l2
     c                   other
     c     ra03l1_key    setgt     ra03l1
     c                   readp     ra03l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra03l2
     c                   other
     c                   readp     ra03l1
     c                   endsl
      *
     c                   if        %eof or
     c                             racfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra03l2_ctxt = ractxt
     c                   other
     c                   eval      ra03l1_ckod = rackod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra03l2_key    setll     ra03l2
     c                   other
     c     ra03l1_key    setll     ra03l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på betalingsbetingelse
      *
     c     ra03l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03l1_ckod
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'OKO_TILGANGSKONTROLL':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra03l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03l2_ctxt
      *
      * Key for les på betalingsbetingelse
      *
     c     ra03lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03lr_ckod
      *
      * Key for oppdatering av betalingsbetingelse
      *
     c     ra03lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03lu_ckod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
7.01  *
7.01  * Sjekk om denne bruker har tilgang til denne rutina
7.01  *
7.01 c                   if        u_s701 = *on
7.01 c                   eval      w_adv    = *blank
7.01 c                   eval      w_tilgko = l_user
7.01 c                   eval      w_rutine = 'RA103R'
7.01 c                   eval      w_status = *blank
7.01 c                   call      'AD005R'
7.01 c                   parm                    w_tilgko
7.01 c                   parm                    w_rutine
7.01 c                   parm                    w_adv
7.01 c                   parm                    w_status
7.01 c                   if        w_status = 'Sperret'
7.01 c                   goto      avslutt
7.01 c                   endif
7.01 c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra03l1_ckod = 0
     c     ra03l1_key    setll     ra03l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
