     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : EHANDEL
      * Program . . . . : CK002R
      * beskrivelse . . : Kobling kjedkategori mot kunde
      *
      * Spesialversjon. :
      * Tilpasset for . : Byggeriet Norge
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020916 ask  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fckgrl1    if   e           k disk    rename(ckgrpfr:ckgrl1r)
     fckgrlr    if   e           k disk    rename(ckgrpfr:ckgrlrr)
     fckgrlu    uf a e           k disk    rename(ckgrpfr:ckgrlur)
     fckkllr    if   e           k disk    rename(ckklpfr:ckkllrr)
     fra11lr    if   e           k disk    rename(ra11pfr:ra11lrr)
      *
     fck002d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ckgrlr_gkla     s                   like(ckgkla)
     d ckgrlr_gkat     s                   like(ckgkat)
     d ckgrlu_gkla     s                   like(ckgkla)
     d ckgrlu_gkat     s                   like(ckgkat)
     d ckgrl1_gkla     s                   like(ckgkla)
     d ckgrl1_gkat     s                   like(ckgkat)
     d ckkllr_kkla     s                   like(ckkkla)
     d ra11lr_kkod     s                   like(rakkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_kabc          s              1    inz(*off)
      *
     d w_firm          s                   like(rakfir)
     d w_kkod          s                   like(rakkod)
     d w_ktxt          s                   like(raktxt)
     d w_gkla          s                   like(ckgkla)
     d w_gkat          s                   like(ckgkat)
6.30 d w_udate         s               d   datfmt(*iso)
6.34 d w_stat          s              1
6.34 d w_disp          s              1
6.34 d w_modu          s             10
6.34 d w_frad          s               d   datfmt(*iso)
6.34 d w_tild          s               d   datfmt(*iso)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
6.34 d c_modu          c                   'BM_MODUL  '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkh
     c                   exsr      kategori
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2gkla <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      ckgrl1_gkla = b1gkla
     c                   eval      ckgrl1_gkat = b1gkat
     c     ckgrl1_key    setll     ckgrl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kjedeklasifisering
      *
     c                   if        b2gkla <> 0
     c                   eval      ckgrl1_gkla = b2gkla
     c                   eval      ckgrl1_gkat = 0
     c     ckgrl1_key    setll     ckgrl1
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2gkla = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c2gkla = b1gkla
     c                   movel     b1gtxt        c2gtxt
     c                   eval      c2gkat = b1gkat
     c                   movel     b1ktxt        c2ktxt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1gkla = b1gkla
     c                   eval      d1gkat = b1gkat
     c                   movel     b1gtxt        d1gtxt
     c                   movel     b1ktxt        d1ktxt
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c2gkla = b1gkla
     c                   eval      c2gkat = b1gkat
     c                   movel     b1gtxt        c2gtxt
     c                   movel     b1ktxt        c2ktxt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1gkla = 0
     c                   eval      c1gkat = 0
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      sok_kat
     c                   eval      c1gkat = w_gkat
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1gkla = 0 or
     c                             c1gkat = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
      * Sender opp informasjonsbilde dersom kjedekategori ikke finnes
      *
     c                   eval      ckkllr_kkla = c1gkla
     c     ckkllr_key    chain     ckkllr
     c                   if        not %found(ckkllr)
     c                   exsr      xc1msg
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom kundekategori ikke finnes
      *
     c                   eval      ra11lr_kkod = c1gkat
     c     ra11lr_key    chain     ra11lr
     c                   if        not %found(ra11lr)
     c                   exsr      xc2msg
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   movel     c1gkla        ckgrlr_gkla
     c                   movel     c1gkat        ckgrlr_gkat
     c     ckgrlr_key    chain     ckgrlr
     c                   if        %found
     c                   exsr      xc3msg
     c                   goto      c1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c2gkla = c1gkla
     c                   eval      c2gkat = c1gkat
     c                   exsr      xc2bld
      *
     c                   eval      c1gkat = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2gkla = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av manglende kjedekategorimelding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av manglende kundekategorimelding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg        begsr
      *
     c                   exfmt     c2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3msg        begsr
      *
     c                   exfmt     c3msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ckkllr_kkla = c2gkla
     c     ckkllr_key    chain     ckkllr
     c                   if        %found(ckkllr)
     c                   eval      c2gtxt = ckktxt
     c                   else
     c                   eval      c2gtxt = *blank
     c                   endif
     c                   eval      ra11lr_kkod = c2gkat
     c     ra11lr_key    chain     ra11lr
     c                   if        %found(ra11lr)
     c                   eval      c2ktxt = raktxt
     c                   else
     c                   eval      c2ktxt = *blank
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ckgrlur
      *
     c                   eval      ckgrlu_gkat = c2gkat
     c                   eval      ckgrlu_gkla = c2gkla
     c     ckgrlu_key    chain     ckgrlur
      *
     c                   time                    ckgeda
     c                   time                    ckgeti
     c                   eval      ckgeus = l_user
      *
     c                   if        %found
     c                   update    ckgrlur
     c                   else
     c                   eval      ckgfir = w_firm
     c                   eval      ckgkat = c1gkat
     c                   eval      ckgkla = c1gkla
     c                   time                    ckgoda
6.10 c                   time                    ckgoti
6.10 c                   eval      ckgous = l_user
     c                   write     ckgrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1gkla = ckgkla
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ckgrlr_gkat = d1gkat
     c                   eval      ckgrlr_gkla = d1gkla
     c     ckgrlr_key    chain     ckgrlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ckgrlu_gkat = d1gkat
     c                   eval      ckgrlu_gkla = d1gkla
     c     ckgrlu_key    chain     ckgrlur
      *
     c                   delete    ckgrlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ckgrl1
      *
     c                   if        %eof or
     c                             ckgfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      ckgrl1_gkla = ckgkla
     c                   eval      ckgrl1_gkat = ckgkat
      *
     c                   eval      b1valg = 0
     c                   eval      b1gkla = ckgkla
      *
     c                   eval      ckkllr_kkla = ckgkla
     c     ckkllr_key    chain     ckkllr
     c                   if        %found
     c                   eval      b1gtxt = ckktxt
     c                   else
     c                   eval      b1gtxt = *blank
     c                   endif
     c                   eval      b1gkat = ckgkat
     c                   eval      ra11lr_kkod = ckgkat
     c     ra11lr_key    chain     ra11lr
     c                   if        %found(ra11lr)
     c                   eval      b1ktxt = raktxt
     c                   else
     c                   eval      b1ktxt = *blank
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ckgrl1_key    setgt     ckgrl1
     c                   readp     ckgrl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ckgrl1
      *
     c                   if        %eof or
     c                             ckgfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      ckgrl1_gkla = ckgkla
      *
     c                   enddo
      *
     c                   if        %eof
     c     ckgrl1_key    setll     ckgrl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for vedlikehold av kjedekategorier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kategori      begsr
      *
     c                   call      'CK001R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søk på kundekategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_kat       begsr
      *
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    w_gkat
     c                   parm                    w_ktxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les på koblingspunkt
      *
     c     ckgrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ckgrlr_gkla
     c                   kfld                    ckgrlr_gkat
      *
      * Key for oppdatering av koblingspunkt
      *
     c     ckgrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ckgrlu_gkla
     c                   kfld                    ckgrlu_gkat
      *
      * Key for posisjonering på koblingspunkt
      *
     c     ckgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ckgrl1_gkla
     c                   kfld                    ckgrl1_gkat
      *
      * Key for les på kjedekategori
      *
     c     ckkllr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ckkllr_kkla
      *
      * Key for les på koderegister
      *
     c     ra11lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11lr_kkod
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.30 c                   time                    w_udate
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ckgrl1_gkla = 0
     c                   eval      ckgrl1_gkat = 0
     c     ckgrl1_key    setll     ckgrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
