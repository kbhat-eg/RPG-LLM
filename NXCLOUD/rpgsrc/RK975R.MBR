     H datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK974R
      * Beskrivelse . . : Oppdaterer passive
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       311202 KOB  Nytt program
      *  5.30 050904 KOB  Korrigert test på dato for opprettelse
5.60  * R5.60 271107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter for utvalgte OKO filer.
6.11  * V6.10 070510 BSA  Endret logging på kunde
6.20  * V6.20 140611 BSA  Memosaldo lagt ut på egen file
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
6.20 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
      *
      * Definering av Local data area
      *
     d                uds
     d rpfirm                300    310  0
     d rprdat                          d   datfmt(*iso)
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_dato                          d   datfmt(*iso)
     d w_ndat                          d   datfmt(*iso)
      *
      * Definering av variabler i nøkler
      *
6.20 d rkmel1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
      *
      * Les kunderegister
      *
     c     rkunlu_key    setll     rkunlu                                 90
     c     rkunlu_key    reade     rkunlu                                 90
     c                   dow       *in90 = *off
6.20  * Hent inn informasjon om memosaldo
6.20 c                   eval      rkmel1_kund = rkkund
6.20 c     rkmel1_key    chain     rkmel1                             61
      *
      * Kontroll om saldo/memosaldo <> 0
      *
     c                   if        rksald <> 0 or
5.60 c                             rkmems <> 0 or
5.60 c                             rkmema <> 0
     c                   goto      neste
     c                   endif
      *
      * Kontroll mot: Siste trans.dato og ny dato
      *
     c                   if        rprdat > rktdat and
     c                             w_ndat > rkndat
     c                   eval      rkpass = 'J'
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK975R    '
     c                   update    rkunlur
     c                   endif
      *
     c     neste         tag
      *
     c     rkunlu_key    reade     rkunlu                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
      * Beregn dato for kontroll mot nydato
      *
     c                   time                    w_dato
     C     w_dato        subdur    1 : *y        w_ndat
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
6.20  *
6.20 c     rkmel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rkmel1_kund
      *
     c                   endsr
