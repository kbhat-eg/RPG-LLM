      /TITLE  Generering av poster utfra fordelingsparametre
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH198R                                              *
      * Beskrivelse . . : Foretar B = Beregning                               *
      *                           P = Prisberegning (ikke i bruk foreløpig)   *
      *                           O = Omfordeling                             *
      *                   dersom konto finnes i fordelingsparametre           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frtrapf    ip   e           k disk    rename(rtrapfr:rtrapfr)
     frbunl1    if   e           k disk    rename(rbunpfr:rbunl1r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fraa4l1    uf   e           k disk    rename(raa4pfr:raa4l1r)
     frfobl1    if   e           k disk    rename(rforpfr:rfobl1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtral1    o    e           k disk    rename(rtrapfr:rtral1r)
      *
      *  Fordelings-parametre
      *
     d rtbrud          ds                                                       Bruddb
     d  wtfirm                        3  0                                      Firma
     d  wtkont                        5  0                                      Konto
     d  wtspes                        2  0                                      Spes
     d  wtavde                        4  0                                      Avd
      *
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_mem1          s                   like(rsmemb)
     d rfbrud          ds                                                       Bruddb
     d  wffirm                        3  0                                      Firma
     d  wfkont                        5  0                                      Konto
     d  wfspes                        2  0                                      Spes
     d  wfavde                        4  0                                      Avd
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d dekr            s              1
     d wkraar          s              4  0
     d wsraar          s              2  0
     d wtbelØ          s                   like(rtbelØ)
     d wtmngd          s                   like(rtmngd)
     d wtvalb          s                   like(rtvalb)
     d w_belØ          s                   like(rtbelØ)
     d w_biln          s                   like(rtbiln)
     d w_bunt          s                   like(rsbunt)
     d w_cavd          s                   like(rtcavd)
     d w_ckto          s                   like(rtckto)
     d w_cmva          s                   like(rtcmva)
     d w_csps          s                   like(rtcsps)
     d w_davd          s                   like(rtdavd)
     d w_dkto          s                   like(rtdkto)
     d w_dmva          s                   like(rtdmva)
     d w_dsps          s                   like(rtdsps)
     d w_fØrs          s                   like(rsfØrs)
     d w_fakt          s              7  4
     d w_linj          s              5  0
     d w_mngd          s                   like(rtmngd)
     d w_pdeb          s                   like(rspdeb)
     d w_pkre          s                   like(rspkre)
     d w_pros          s              5  4
     d w_prot          s              5  4
     d w_psum          s                   like(rspsum)
     d w_sist          s                   like(rssist)
     d w_text          s                   like(rttext)
     d w_tx17          s             17
     d w_valb          s                   like(rtvalb)
     d xbelØp          s                   like(rtbelØ)
     d xmengd          s                   like(rtmngd)
     d xvalbe          s                   like(rtvalb)
      *
     d p_stat          s              1
     d p_akod          s              2
     d p_atxt          s             20
      *
     irtrapfr
      *  Posteringer inn
     i                                          rtfirm        l3
     i                                          rtbunt        l1
      *
      /eject
      *
     c     dummy         tag
610   *
610  c                   if        rtmemb > w_mem1
610  c                   move      *on           *inlr
610  c                   goto      ut
610  c                   endif
610   *
610  c                   if        rtmemb <> w_mem1
610  c                   move      *off          *inl1
610  c                   move      *off          *inl3
610  c                   goto      ut
610  c                   endif
      *
      *  Hent statusopplysninger
      *
     c                   if        *inl3 = *on
     c                   move      rtfirm        ra1fir
     c     keyaa1        chain     raa1l1                             52
      *
      * Hent sist brukte bilagsnummer
      *
     c                   move      rtfirm        ra4fir
     c                   move      wkraar        ra4aar
     c     keyaa4        chain     raa4l1                             60
      *
     c                   if        *in60 = *off                                          .....
     c                   eval      w_biln = ra4sgb + 1                                       .
     c                   move      w_biln        ra4sgb                                      .
     c                   update    raa4l1r                                                   .
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      *  Sjekk om bunt finnes/utgått
      *
     c                   if        *inl1 = *on
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_mem1
     c                   movel     rtfirm        rsfirm
     c                   movel     rtbunt        rsbunt
     c     keybun        chain     rbunl1                             53
      *
     c                   if        *in53 = *off
     c                   eval      *in53 = rsstat = 'U'
     c                   endif
      *
      *  Finn ledig buntnr.
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_mem1
     c                   move      rtfirm        rsfirm                         Firma
     c                   move      99999         rsbunt                         Type
     c     keybun        setgt     rbunlu
     c                   readp     rbunlu                                 10
      *
     c                   if        *in10 = *on
     c                   eval      w_bunt = 1
     c                   else
     c                   eval      w_bunt = rsbunt + 1
     c                   eval      w_psum = 0                                                .
     c                   eval      w_fØrs = 2                                                .
     c                   eval      w_sist = 1                                                .
     c                   eval      w_linj = 1
     c                   endif
      *
      * Hent sist brukte bilagsnummer
      *
     c                   move      rsrper        wsraar
     c                   if        wsraar > 80
     c                   move      1900          wkraar
     c                   else
     c                   move      2000          wkraar
     c                   endif
      *
     c                   move      wsraar        wkraar
     c                   endif
      *
     c                   if        *in53 = *on
     c                   goto      ut
     c                   endif
      *
      *  Hent bilagstekst hvis blank
      *
     c                   if        rttext = *blanks                                      .....
     c                   move      rtbilk        p_akod                                      .
     c                   call      'RS701R'                                      bilags-
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *
     c                   if        p_stat <> 'N'                                Kode
     c                   move      p_atxt        rttext                          FINNES      .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      *  LEGGER UT PERIODE PR. TRANS
      *
     c                   move      rsrper        rtrper                                      .
     c                   move      w_biln        rtbiln                                      .
      *
      * Ta vare på kontobegrep/mva/tekst
      *
     c                   move      rtdkto        w_dkto
     c                   move      rtdsps        w_dsps
     c                   move      rtdavd        w_davd
     c                   move      rtdmva        w_dmva
     c                   move      rtbelØ        w_belØ
     c                   move      rtvalb        w_valb
     c                   move      rtmngd        w_mngd
     c                   move      rtckto        w_ckto
     c                   move      rtcsps        w_csps
     c                   move      rtcavd        w_cavd
     c                   move      rtcmva        w_cmva
     c                   move      rttext        w_text
     c                   movel     rttext        w_tx17
      *
     c     debet         tag
      ******************************************************
      *   Kontroller om debetkonto finnes                  *
      *   i fordelingsregisteret                           *
      ******************************************************
     c                   eval      wtbelØ = 0
     c                   eval      wtvalb = 0
     c                   eval      wtmngd = 0
     c                   if        w_dkto <> *zero                                       .....
     c                   move      w_dkto        wtkont                                      .
     c                   move      w_dsps        wtspes                                      .
     c                   move      w_davd        wtavde                                      .
     c                   move      rtfirm        rffirm                                      .
     c                   move      w_dkto        rfkont                                      .
     c                   move      w_dsps        rfspes                                      .
     c                   move      w_davd        rfavde                                      .
     c                   eval      rflinj = *zero                                            .
     c                   move      'D'           dekr                                        .
     c     keyfor        setll     rfobl1                             54                     .
      *                                                                 .
      * Les fordelingsregister                                          .
      *                                                                 .
     c                   read      rfobl1                                 54                 .
      *                                                                 .
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      *   Konto finnes i fordelingsregister                           . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Beregn faktor basert på %-andel                             . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                  ....    . . .
     c                   eval      w_pros = rfpros / 100                            .    . . .
     c                   endif                                                   ....    . . .
     c                   if        rfkode = 'O'                                  .....   . . .
     c                   eval      w_pros = rfpros / 100                             .   . . .
     c                   endif                                                   .....   . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
      *                                                                 .
      * Les neste fordeling-rec for B, O eller P                        .
      *                                                                 .
     c     nydeb         tag                                                                 .
      *                                                                 .
     c                   read      rfobl1                                 54                 .
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      * Sjekk om fortsatt innenfor samme frabegrep                    . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Test funksjon                                               . . .
      *                                                             . . .
      * Beregning                                                   . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                          . . .
     c                   exsr      beregn                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Prissetting                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'P'                                          . . .
     c                   exsr      pris                                                  . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Omfordeling                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'O'                                          . . .
     c                   eval      w_prot = rfprot / 100                                 . . .
     c                   eval      w_fakt = w_pros * w_prot                              . . .
     c                   exsr      omford                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
     c                   goto      nydeb                                                 . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *                                                             . . .
      *DEBET************************************************
      *   Opprinnelig post blir skrevet ved B og P mens    *
      *   rest blir skrevet ved O (Omfordeling)            *
      ******************************************************
      *                                                             . . .
     c                   eval      rtbelØ = w_belØ - wtbelØ
     c                   eval      rtvalb = w_valb - wtvalb
     c                   eval      rtmngd = w_mngd - wtmngd
     c                   move      w_dkto        rtdkto
     c                   move      w_dsps        rtdsps
     c                   move      w_davd        rtdavd
     c                   move      w_dmva        rtdmva
     c                   eval      rtckto = *zero                                            .
     c                   eval      rtcsps = *zero                                            .
     c                   eval      rtcavd = *zero                                            .
     c                   move      w_text        rttext
      *
     c     kredit        tag
      *                                                             . . .
      ******************************************************
      *   Kontroller om kreditkonto finnes                 *
      *   i fordelingsregisteret                           *
      ******************************************************
      *                                                             . . .
     c                   eval      wtbelØ = 0
     c                   eval      wtvalb = 0
     c                   eval      wtmngd = 0
     c                   if        w_ckto <> *zero                                       .....
     c                   move      rtfirm        rffirm                                      .
     c                   move      w_ckto        wtkont                                      .
     c                   move      w_csps        wtspes                                      .
     c                   move      w_cavd        wtavde                                      .
     c                   move      w_ckto        rfkont                                      .
     c                   move      w_csps        rfspes                                      .
     c                   move      w_cavd        rfavde                                      .
     c                   eval      rflinj = *zero                                            .
     c                   move      'K'           dekr                                        .
     c     keyfor        setll     rfobl1                             54                     .
      *                                                                 .
      * Les fordelingsregister                                          .
      *                                                                 .
     c                   read      rfobl1                                 54                 .
      *                                                                 .
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      *   Konto finnes i fordelingsregister                           . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Beregn faktor basert på %-andel                             . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                  ....    . . .
     c                   eval      w_pros = rfpros / 100                            .    . . .
     c                   endif                                                   ....    . . .
     c                   if        rfkode = 'O'                                  .....   . . .
     c                   eval      w_pros = rfpros / 100                             .   . . .
     c                   endif                                                   .....   . . .
      *                                                             . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
      *                                                                 .
      * Les neste fordeling-rec for B, O eller P                        .
      *                                                                 .
     c     nykre         tag                                                                 .
      *                                                                 .
     c                   read      rfobl1                                 54                 .
     c                   if        *in54 = *off                                        ..... .
     c                   move      rfkont        wfkont                                    . .
     c                   move      rfspes        wfspes                                    . .
     c                   move      rfavde        wfavde                                    . .
      *                                                               . .
      * Sjekk om fortsatt innenfor samme frabegrep                    . .
      *                                                               . .
     c                   if        rfbrud = rtbrud                                     ... . .
      *                                                             . . .
      * Test funksjon                                               . . .
      *                                                             . . .
      * Beregning                                                   . . .
      *                                                             . . .
     c                   if        rfkode = 'B'                                          . . .
     c                   exsr      beregn                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Prissetting                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'P'                                          . . .
     c                   exsr      pris                                                  . . .
     c                   endif                                                           . . .
      *                                                             . . .
      * Omfordeling                                                 . . .
      *                                                             . . .
     c                   if        rfkode = 'O'                                          . . .
     c                   eval      w_prot = rfprot / 100                                 . . .
     c                   eval      w_fakt = w_pros * w_prot                              . . .
     c                   exsr      omford                                                . . .
     c                   endif                                                           . . .
      *                                                             . . .
     c                   goto      nykre                                                 . . .
     c                   endif                                                         ... . .
     c                   endif                                                         ..... .
     c                   endif                                                           .....
      *                                                             . . .
      *KREDIT***********************************************
      *   Opprinnelig post blir skrevet ved B og P mens    *
      *   rest blir skrevet ved O (Omfordeling)            *
      ******************************************************
      *                                                             . . .
     c                   eval      rtbelØ = w_belØ - wtbelØ
     c                   eval      rtvalb = w_valb - wtvalb
     c                   eval      rtmngd = w_mngd - wtmngd
     c                   move      w_cavd        rtcavd
     c                   move      w_ckto        rtckto
     c                   move      w_csps        rtcsps
     c                   move      w_cmva        rtcmva
     c                   eval      rtdkto = *zero                                            .
     c                   eval      rtdsps = *zero                                            .
     c                   eval      rtdavd = *zero                                            .
     c                   move      w_text        rttext
      *
      ******************************************************
      *  AVSLUTT DENNE TRANS                               *
      ******************************************************
      *                                                             . . .
     c     ut            tag                                                    ****
      *
      *  Oppdater buntrecord i posteringsfile
      *
     cl1                 move      rtfirm        rsfirm
     cl1                 move      w_bunt        rsbunt
     cl1                 eval      rsstat = *blank
     cl1                 move      rhprÅr        rsrper
     cl1                 movel     rhpper        rsrper
     cl1                 move      w_fØrs        rsfØrs
     cl1                 move      w_sist        rssist
     cl1   *dmy          move      rhpkda        rspdat
     cl1                 move      w_psum        rsbsum
     cl1                 move      w_psum        rspsum
     cl1                 move      w_pdeb        rspdeb
     cl1                 move      w_pkre        rspkre
      *
610  cl1                 eval      rsmemb = w_mem1
610  cl1                 time                    rsedat
610  cl1                 time                    rsetim
610  cl1                 eval      rseusr = l_user
     cl1                 write     rbunlur                                       SKRIV BUNT
      /eject
     c     beregn        begsr
      *                                                             . . .
      ******************************************************
      * Foretar beregning og legger ut                     *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *                                                             . . .
     c                   eval(h)   rtbelØ = w_belØ * w_pros
     c                   eval      rtvalb = *zero
     c                   eval      rtmngd = *zero
      *
     c                   move      w_tx17        rttext
     c                   movel     'B+ '         rttext
     c                   move      w_dmva        rtdmva
     c                   move      w_cmva        rtcmva
      *
     c                   if        dekr = 'D'                                            .....
     c                   eval      rtdkto = *zero
     c                   move      rfkto1        rtdkto
     c                   move      rfavd1        rtdavd
     c                   move      rfsps1        rtdsps
     c                   eval      rtckto = *zero
     c                   move      rfkto2        rtckto
     c                   move      rfavd2        rtcavd
     c                   move      rfsps2        rtcsps
      *
     c                   else
      *
     c                   eval      rtckto = *zero
     c                   move      rfkto1        rtckto
     c                   move      rfavd1        rtcavd
     c                   move      rfsps1        rtcsps
     c                   eval      rtdkto = *zero
     c                   move      rfkto2        rtdkto
     c                   move      rfavd2        rtdavd
     c                   move      rfsps2        rtdsps
     c                   endif                                                           ...
      *
     c                   if        rtdkto = *zero and                                    .....
     c                             rtdkto = *zero                                            .
     c                   goto      endber                                                    .
     c                   endif                                                           .....
      *
     c                   if        rtbelØ > *zero or                                     .....
     c                             rtvalb > *zero or                                         .
     c                             rtmngd > *zero                                            .
     c                   eval      w_sist = w_sist + 1                                       .
     c                   eval      w_linj = w_linj + 1                                       .
     c                   eval      rtbunt = w_bunt                                           .
     c                   eval      rtlinj = w_linj                                           .
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtral1r                                                   .
     c                   if        rtdkto <> *zero                                       ... .
     c                   eval      w_psum = w_psum + rtbelØ                                . .
     c                   eval      w_pdeb = w_pdeb + rtbelØ                                . .
     c                   endif                                                           ... .
     c                   if        rtckto <> *zero                                       ... .
     c                   eval      w_pkre = w_pkre + rtbelØ                                . .
     c                   endif                                                           ... .
     c                   endif                                                           .....
      *
     c     endber        endsr
      /eject
     c     pris          begsr
      *                                                             . . .
      ******************************************************
      * Foretar prisberegning og legger ut                 *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *                                                             . . .
     c                   eval(h)   rtbelØ = w_mngd * rfpris
     c                   eval      rtvalb = *zero
     c                   move      w_mngd        rtmngd
      *
     c                   move      w_tx17        rttext
     c                   movel     'P+ '         rttext
      *
     c                   if        rtbelØ > *zero                                        .....
     c                             or rtvalb > *zero                                         .
     c                             or rtmngd > *zero                                         .
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtral1r                                                   .
     c                   endif                                                           .....
      *
     c                   endsr
      *
     c     omford        begsr
      *                                                             . . .
      ******************************************************
      * Foretar omfordeling og legger ut                   *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *                                                             . . .
     c                   eval(h)   rtbelØ = w_belØ * w_fakt
     c                   eval(h)   rtvalb = w_valb * w_fakt
     c                   eval(h)   rtmngd = w_mngd * w_fakt
      *
      * Akkumuler fordelte summer
      *
     c                   eval      wtbelØ = wtbelØ + rtbelØ
     c                   eval      wtvalb = wtvalb + rtvalb
     c                   eval      wtmngd = wtmngd + rtmngd
      *
      * Sjekk om avrunding
      *
     c                   exsr      avrund
      *
     c                   move      w_tx17        rttext
     c                   movel     'O+ '         rttext
      *
     c                   if        dekr = 'D'
     c                   eval      rtdkto = *zero
     c                   move      rfkto1        rtdkto
     c                   move      rfavd1        rtdavd
     c                   move      rfsps1        rtdsps
     c                   move      rfkont        rtckto
     c                   move      rfavde        rtcavd
     c                   move      rfspes        rtcsps
     c                   eval      rtcmva = *blank
      *
     c                   else
      *
     c                   eval      rtckto = *zero
     c                   move      rfkto1        rtckto
     c                   move      rfavd1        rtcavd
     c                   move      rfsps1        rtcsps
     c                   move      rfkont        rtdkto
     c                   move      rfavde        rtdavd
     c                   move      rfspes        rtdsps
     c                   eval      rtdmva = *blank
     c                   endif
      *
     c                   if        rtbelØ > *zero or                                     .....
     c                             rtvalb > *zero or                                         .
     c                             rtmngd > *zero                                            .
     c                   eval      w_sist = w_sist + 1                                       .
     c                   eval      w_linj = w_linj + 1                                       .
     c                   eval      rtbunt = w_bunt                                           .
     c                   eval      rtlinj = w_linj                                           .
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtral1r                                                   .
      *                                                             . . .
     c                   if        rtdkto <> *zero                                       ... .
     c                   eval      w_psum = w_psum + rtbelØ                                . .
     c                   eval      w_pdeb = w_pdeb + rtbelØ                                . .
     c                   endif                                                           ... .
      *                                                             . . .
     c                   if        rtckto <> *zero                                       ... .
     c                   eval      w_pkre = w_pkre + rtbelØ                                . .
     c                   endif                                                           ... .
     c                   endif                                                           .....
      *
     c                   endsr
      *
     c     avrund        begsr
      *                                                             . . .
      *-------------------------------------------------------------------
      *  A V R U N D :  Hvis beløp/mengde som gjenstår til fordeling er
      *                 mindre enn 1, betraktes dette som avrunding
      *-------------------------------------------------------------------
      *                                                             . . .
     c                   eval      xbelØp = w_belØ - wtbelØ
     c                   eval      xvalbe = w_valb - wtvalb
     c                   eval      xmengd = w_mngd - wtmngd
      *
     c                   if        xbelØp <= 1                                           .....
     c                   eval      rtbelØ = rtbelØ + xbelØp                                  .
     c                   eval      wtbelØ = wtbelØ + xbelØp                                  .
     c                   eval      xbelØp = 0                                                .
     c                   endif                                                           .....
      *
     c                   if        xvalbe <= 1                                           .....
     c                   eval      rtvalb = rtvalb + xvalbe                                  .
     c                   eval      wtvalb = wtvalb + xvalbe                                  .
     c                   eval      xvalbe = 0                                                .
     c                   endif                                                           .....
      *
     c                   if        xmengd <= 1                                           .....
     c                   eval      rtmngd = rtmngd + xmengd                                  .
     c                   eval      wtmngd = wtmngd + xmengd                                  .
     c                   eval      xmengd = 0                                                .
     c                   endif                                                           .....
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_mem1                         user
      *
     c                   eval      w_linj = 1
     c                   eval      rtbrud = *zero
     c                   eval      rfbrud = *zero
      *
      * Key for fordelings-parametre
      *
     c     keyfor        klist
     c                   kfld                    rffirm
     c                   kfld                    rfkont
     c                   kfld                    rfspes
     c                   kfld                    rfavde
     c                   kfld                    rflinj
      *
      * Key for buntregister
      *
     c     keybun        klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    rsfirm
     c                   kfld                    rsbunt
      *
      * Key for statusopplysninger
      *
     c     keyaa1        klist
     c                   kfld                    ra1fir
      *
     c     keyaa4        klist
     c                   kfld                    ra4fir
     c                   kfld                    ra4aar
      *
     c                   endsr
