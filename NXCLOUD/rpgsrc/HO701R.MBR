      /title  ASLAGR Håndterminal ordre-hode, innlasting - METO
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HO701R
      * Beskrivelse . . : Håndterminal ordre-hode, innlasting - METO
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050600 HKO  Nytt program
      *       280900 HKO  Tilpasset nye filer
      *       230301 HKO  Fjernet merknad
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhbtrpf    if   e           k disk
     fhohelu    uf a e           k disk    rename(hohepfr:hohelur)
     fhodtlu    uf a e           k disk    rename(hodtpfr:hodtlur)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur - håndterminalfil
      *
     d                 ds
     d d_ht                          34
     d  d_ht_kund                     6  0 overlay(d_ht)
     d  d_ht_refn                     6  0 overlay(d_ht:7)
     d  d_ht_vare                     8  0 overlay(d_ht:13)
     d  d_ht_enhe                     3    overlay(d_ht:21)
     d  d_ht_anta                    11    overlay(d_ht:24)
      *
      * Definering av variable i nøkler
      *
     d hodtlu_refn     s                   like(hdrefn)
     d hodtlu_vare     s                   like(hdvare)
     d hodtlu_enhe     s                   like(hdenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(hofirm)
     d w_anta          s              8  3
     d w_anta_alf      s             11
     d w_refn          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle rader i håndterminalfilen
      *
     c                   read      hbtrpf                                 90
     c                   dow       *in90 = *off
      *
     c                   exsr      behandling
      *
     c                   read      hbtrpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av innlest rad
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_ht = hoinfo
      *
     c                   if        d_ht_vare = 0
     c                   goto      end_behandl
     c                   endif
      *
      * Oppretter linje i håndterminal ordre-hode
      *
     c                   if        w_refn <> d_ht_refn
      *
     c                   eval      hofirm = w_firm
     c                   eval      horefn = d_ht_refn
     c                   eval      hokund = d_ht_kund
     c                   eval      hoselg = 0
     c                   eval      hostat = 0
      *
     c                   time                    hodato
     c                   time                    hotime
      *
     c                   write     hohelur
      *
     c                   eval      w_refn = d_ht_refn
      *
     c                   endif
      *
      * Oppretter linje i håndterminal ordre-linje
      *
     c                   eval      w_anta_alf = %subst(d_ht_anta:1:8) +
     c                                          %subst(d_ht_anta:10:2) + '0'
     c                   move      w_anta_alf    w_anta
      *
     c                   time                    hddato
     c                   time                    hdtime
      *
     c                   eval      hodtlu_refn = d_ht_refn
     c                   eval      hodtlu_vare = d_ht_vare
     c                   eval      hodtlu_enhe = d_ht_enhe
     c     hodtlu_key    chain     hodtlur                            92
     c                   if        *in92 = *on
     c                   eval      hdfirm = w_firm
     c                   eval      hdrefn = d_ht_refn
     c                   eval      hdvare = d_ht_vare
     c                   eval      hdenhe = d_ht_enhe
     c                   eval      hdanta = w_anta
     c                   write     hodtlur
     c                   else
     c                   eval      hdanta = hdanta + w_anta
     c                   update    hodtlur
     c                   endif
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av håndterminal ordre-linje
      *
     c     hodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hodtlu_refn
     c                   kfld                    hodtlu_vare
     c                   kfld                    hodtlu_enhe
      *
      * Henter inn firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
