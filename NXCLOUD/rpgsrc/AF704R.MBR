      /TITLE  ASOKON Spørring v.h.a. rapportter (Utvalg)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASFTP
      * Program . . . . : FTPSJEKK
      * Beskrivelse . . : Sjekker FTP logg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       161101 MHA  Nytt program
6.10  *       030712 bof  Lagt inn en variant til på logg - 226
6.11  *       291113 bof  hvis "No response from remote host", så feil
6.12  *       090115 bof  Scan for å se om en tekst er inne i teksten.
6.30  *       220218 bsa  Scan for å flagge etter ingen filer funnet.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faflopf    if a e             disk      rename(aflopf:aflopfr)
     faftpl1    if a e           k disk      rename(aftppfr:aftpl1r)
      *
      *
     dLinje            ds           200
     d line                    1    200
      *
      * Definering av parametre
     d p_status        s              1
     d p_navn          s                     like(afnavn)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler
     d w_feil          s              1
     d w_oktx          s                     like(afoktx)
      *
     d w_kommando      s            200a
     d w_kommlen       s             15p 5   inz(200)
      *
     d aftpl1_navn     s                     like(afnavn)
      *
     d w_firm          s                     like(affirm)
      *
      /eject
      *
     c     aftpl1_key    chain     aftpl1r
     c                   if        %found
     c                   eval      w_oktx = afoktx
     c                   else
     c                   eval      p_status = 'N'
     c                   goto      slutt
     c                   endif
      *
     c                   eval      w_feil = 'E'
      *
     c     start         tag
     c                   read      aflopf
     c                   dou       %EOF
     c                   eval      linje = SrcDta
     c                   if        %scan('File transfer completed ' +
     c                             'successfully.': line) > 0 or
6.10 c                             %scan('226 Transfer complete.': line) > 0 or
6.10 c                             %scan('226 File received ok.': line) > 0
     c                             or (%scan(%trim(w_oktx) : %trim(line)) > 0
     c                             and w_oktx <> *blank)
     c                   eval      w_feil = *blank
6.11 c*                  leave
     c                   endif
6.11 c                   if        %scan('No response from' : line) > 0
6.11 c                   eval      w_feil = 'E'
6.11 c                   leave
6.11 c                   endif
6.30 c                   if        %scan('No files found' : line) > 0
6.30 c                   eval      w_feil = 'F'
6.30 c                   leave
6.30 c                   endif
     c                   read      aflopf
     c                   enddo
      *
      * Gjennomfører diverse opprydninger
     c                   if        aflibt = 'J'
     c                   select
     c                   when      %trim(aftype) = 'GET'
     c                   eval      w_kommando = 'RSTLIB SAVLIB(' +
     c                             %trim(affiln) + ') DEV(*SAVF) ' +
     c                             'SAVF(ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   if        affilt <> *blank
     c                   eval      w_kommando = %trim(w_kommando) +
     c                             ' RSTLIB(' +
     c                             %trim(affilt) + ')'
     c                   endif
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
     c                   when      %trim(aftype) = 'PUT'
     c                   eval      w_kommando = 'DLTF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
     c                   endsl
     c                   endif
      *
     c                   eval      p_status = w_feil
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
     c     *entry        plist
     c                   parm                    p_navn
     c                   parm                    p_status
      *
     c     aftpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftpl1_navn
      *
     c                   eval      w_firm = l_firm
     c                   eval      aftpl1_navn = p_navn
      *
     c                   endsr
      *
