      /title  ASLAGR Håndterminal ordre-hode, vedlikehold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HO100R
      * Beskrivelse . . : Håndterminal ordre-hode, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121000 HKO  Nytt program
      *       131200 HKO  Lagt inn kundeprosjekt
      *       230301 HKO  Lagt inn lev.adr, kundens ref. og rekvisisjonsnr.
6.20  * 6.20  060511 ASK  Fjernet oppslag mot batch håndterminal løsning.
6.nu  * 6.30  111215 bsa  Utvidet referansefelt. NB kun i DDS
7.xx  * 7.00  200516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhohelr    if   e           k disk    rename(hohepfr:hohelrr)
     fhohel1    if   e           k disk    rename(hohepfr:hohel1r)
     fhohel2    if   e           k disk    rename(hohepfr:hohel2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fhohelu    uf   e           k disk    rename(hohepfr:hohelur)
     fhodtlu    uf   e           k disk    rename(hodtpfr:hodtlur)
      *
     fho100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av LDA (Local Data Area)
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d hohelr_refn     s                   like(horefn)
     d hohel1_refn     s                   like(horefn)
     d hohel2_kund     s                   like(hokund)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d hohelu_refn     s                   like(horefn)
     d hodtlu_refn     s                   like(hdrefn)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s              6  0
     d w_time          s              6  0
      *
     d w_firm          s                   like(hofirm)
     d w_refn          s                   like(horefn)
     d w_navn          s                   like(rknavn)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = 0 and
     c                             b2refn <> 0 or
     c                             b2kund <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> 0
     c                   exsr      control
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
6.20 c*                  call      'HO700C'
      *
     c                   select
     c                   when      w_seqe = 'K'
     c     hohel2_key    setll     hohel2
     c                   other
     c     hohel1_key    setll     hohel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på referansenummer
      *
     c                   if        b2refn <> 0
     c                   eval      w_seqe = ' '
     c                   eval      hohel1_refn = b2refn
     c     hohel1_key    setll     hohel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundenummer
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      hohel2_kund = b2kund
     c     hohel2_key    setll     hohel2
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c     end_pos       tag
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2refn = 0
     c                   eval      b2kund = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_feil = *off
      *
      * Validering av nøkkel
      *
     c                   if        b2refn = 0
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      hohelr_refn = b2refn
     c     hohelr_key    chain     hohelr                             90
     c                   if        *in90
     c                   eval      b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = 4
     c                   eval      d1refn = b2refn
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
      * Vis detaljer
      *
     c                   if        b2valg = 5
     c                   eval      w_refn = b2refn
     c                   exsr      vis_detalj
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = 0
     c                   eval      b2refn = 0
     c                   eval      b2kund = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Slette
      *
     c                   if        b1valg = 4
     c                   eval      d1refn = b1refn
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Se på detaljer
      *
     c                   if        b1valg = 5
     c                   eval      w_refn = b1refn
     c                   exsr      vis_detalj
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      hohelr_refn = d1refn
     c     hohelr_key    chain     hohelr                             90
      *
     c                   eval      rkunl1_kund = hokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      d1knav = rknavn
     c                   else
     c                   eval      d1knav = *blank
     c                   endif
      *
     c                   eval      fkprl1_kund = hokund
     c                   eval      fkprl1_kpro = hokpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
     c                   eval      d1pnav = flnavn
     c                   else
     c                   eval      d1pnav = *blank
     c                   endif
      *
     c                   if        hodato <> *loval
     c     *dmy          move      hodato        d1dato
     c                   endif
      *
     c                   if        hotime <> *loval
     c     *hms          move      hotime        d1time
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter referanse med tilhørende varelinjer
      *
     c                   eval      hohelu_refn = d1refn
     c     hohelu_key    chain     hohelur                            90
     c                   if        *in90 = *off
      *
     c                   delete    hohelur
      *
     c                   eval      hodtlu_refn = d1refn
     c     hodtlu_key    setll     hodtlu
     c     hodtlu_key    reade     hodtlur                                91
     c                   dow       *in91 = *off
      *
     c                   delete    hodtlur
      *
     c     hodtlu_key    reade     hodtlur                                91
     c                   enddo
      *
     c                   endif
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å se på detaljer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_detalj    begsr
      *
     c                   call      'HD520R'
     c                   parm                    w_firm
     c                   parm                    w_refn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_actf = 'B2KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_navn
      *
     c                   if        b2kund <> 0
     c                   exsr      posisjoner
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   read      hohel1                                 15
     c                   when      w_seqe = 'K'
     c                   read      hohel2                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        hofirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1refn = horefn
     c                   eval      b1kund = hokund
     c                   eval      b1selg = hoselg
     c                   eval      b1navn = honavn
     c                   eval      b1dref = hodref
     c                   eval      b1knav = *blank
     c                   eval      b1pnav = *blank
     c                   eval      b1dato = 0
     c                   eval      b1time = 0
      *
     c                   eval      rkunl1_kund = hokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      b1knav = rknavn
     c                   else
     c                   eval      b1knav = 'Ukjent kunde'
     c                   endif
      *
     c                   eval      fkprl1_kund = hokund
     c                   eval      fkprl1_kpro = hokpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
     c                   eval      b1pnav = flnavn
     c                   endif
      *
     c                   if        hodato <> *loval
     c     *dmy          move      hodato        w_dato
     c                   movel     w_dato        b1dato
     c                   endif
      *
     c                   if        hotime <> *loval
     c     *hms          move      hotime        w_time
     c                   movel     w_time        b1time
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på ordre-hode
      *
     c     hohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohelr_refn
      *
      * Key for les på ordre-hode
      *
     c     hohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohel1_refn
      *
      * Key for posisjonering på kunde
      *
     c     hohel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohel2_kund
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppdatering av håndterminal ordre-hode
      *
     c     hohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hohelu_refn
      *
      * Key for oppdatering av håndterminal ordre-linje
      *
     c     hodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hodtlu_refn
      *
      *
      * Initierer verdier
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      hohel1_refn = 0
     c     hohel1_key    setll     hohel1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
