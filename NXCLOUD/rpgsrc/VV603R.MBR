     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV603R
      * Beskrivelse . . : Vare, utskrift av vare-/prisinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280999 HKO  Nytt program
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *       180204 HKO  Lagt inn valg av minimun dekningsgrad
5.70  * PRGR  241008 bof  Innført prisgruppe /varetupe fra lager
6.20  *       010411 bof  Uvidet vare-pris med prisgiver
6.22  * 6.20  190511 bø   Utvidet vl710r med div. info
6.23  * 6.20  100114 nho  b-pris ble aldri satt til '0'
6.30  * 6.30  280514 BKV  LVAR økt fra 15 til 20
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.01  *PRG2               Endringer består kun i kompilering, da alle
8.01  *PRG2               referanser er eksternt definert.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvawpf    if   e           k disk
      *
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvv603p    o    e             printer
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
5.70 d l_lagr               1001   1002  0
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        80
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_fogr                        2  0 overlay(d_list:4)
     d  d_fhgr                        2  0 overlay(d_list:6)
     d  d_fugr                        3  0 overlay(d_list:8)
     d  d_togr                        2  0 overlay(d_list:11)
     d  d_thgr                        2  0 overlay(d_list:13)
     d  d_tugr                        3  0 overlay(d_list:15)
     d  d_flev                        6  0 overlay(d_list:18)
     d  d_tlev                        6  0 overlay(d_list:24)
     d  d_fvar                       15    overlay(d_list:30)
     d  d_tvar                       15    overlay(d_list:45)
     d  d_dekn                        4  2 overlay(d_list:60)
     d  d_utsk                        1  0 overlay(d_list:64)
     d  d_srt1                        1  0 overlay(d_list:65)
     d  d_srt2                        1  0 overlay(d_list:66)
     d  d_srt3                        1  0 overlay(d_list:67)
     d  d_srt4                        1  0 overlay(d_list:68)
      *
      * Definering av parametre
      *
     d p_list          s             80
      *
      * Definering av variabler i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d rlevl1_levr     s                   like(rllevr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_enh1          s              1    inz(*off)
     d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_timestamp     s             12  0
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_udat          s               d   datfmt(*iso)
     d w_kode          s              1  0
      *
     d w_firm          s                   like(vvfirm)
     d w_kopr          s                   like(vpkopr)
     d w_dekn          s                   like(vvdekn)
     d w_enhe_gml      s                   like(vpenhe)
     d w_lety_gml      s                   like(vplety)
5.70 d w_prgr          s                   like(vpprgr)
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.20 d w_ldor          s                   like(vpldor)
6.22 d w_utda          s                   like(vvudat)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Utskrift av vareregister
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre
      *
     c                   eval      a2fogr = d_fogr
     c                   eval      a2fhgr = d_fhgr
     c                   eval      a2fugr = d_fugr
     c                   eval      a2togr = d_togr
     c                   eval      a2thgr = d_thgr
     c                   eval      a2tugr = d_tugr
     c                   eval      a2flev = d_flev
     c                   eval      a2tlev = d_tlev
     c                   eval      a2fvar = d_fvar
     c                   eval      a2tvar = d_tvar
      *
     c                   select
     c                   when      d_srt1 = 1
     c                   eval      a2sort = 'Varegruppe'
     c                   when      d_srt2 = 1
     c                   eval      a2sort = 'Leverandør'
     c                   when      d_srt3 = 1
     c                   eval      a2sort = 'Varetekst'
     c                   when      d_srt4 = 1
     c                   eval      a2sort = 'Varenummer'
     c                   endsl
      *
     c                   select
     c                   when      d_srt1 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varegruppe'
     c                   when      d_srt2 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Leverandør'
     c                   when      d_srt3 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varetekst'
     c                   when      d_srt4 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varenummer'
     c                   endsl
      *
     c                   select
     c                   when      d_srt1 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varegruppe'
     c                   when      d_srt2 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Leverandør'
     c                   when      d_srt3 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varetekst'
     c                   when      d_srt4 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varenummer'
     c                   endsl
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser arbeidsregsiter, og behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Leser neste rad
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.22 c                   parm                    w_utda
6.22 c                   parm                    w_ldor
6.22 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
      *
      * Henter vareinformasjon
      *
     c                   eval      d1ogrp = vvogrp
     c                   eval      d1hgrp = vvhgrp
     c                   eval      d1ugrp = vvugrp
     c                   eval      d1ldor = vvldor
     c                   eval      d1lnav = *blank
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1vare = vvvare
6.31 c                   if        w_lv_nobb > 0
6.31 c                   eval      d1nobb = w_lv_nobb
6.31 c                   movel     w_lv_llva     d1lvar
6.31 c                   else
     c                   eval      d1nobb = vvnobb
6.30 c*                  eval      d1lvar = vvlvar
6.30 c                   movel     vvlvar        d1lvar
6.31 c                   endif
     c                   eval      d1kabc = vvkabc
     c                   eval      d1kpri = vvkpri
5.70 c                   eval      d1vtyp = w_vtyp
     c                   eval      d1enhe = *blank
     c                   eval      d1lety = 0
     c                   eval      d1pdat = 0
     c                   eval      d1inpr = 0
     c                   eval      d1kopr = 0
     c                   eval      d1bupr = 0
     c                   eval      d1sapr = 0
     c                   eval      d1saim = 0
     c                   eval      d1dekn = 0
      *
      * Henter leverandørnavn
      *
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      d1lnav = rlnavn
     c                   endif
      *
      *
      * Henter og skriver priser for alle enheter/leveringstyper
      *
     c                   eval      b_enh1 = *on
     c                   eval      w_enhe_gml = *blank
     c                   eval      w_lety_gml = 0
      *
     c                   eval      vvenl1_vare = vvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 91
     c                   dow       *in91 = *off
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      hent_prlev
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_prlev
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      hent_prlev
6.20 c                   endif
6.20 c                   endif
      *
     c     vvenl1_key    reade     vvenl1                                 91
     c                   enddo
      *
     c                   endsr
      *
6.20  *------------------------------------------------------------------------------
6.20  *  Beregner pris i forhold til test på prisgivere
6.20  *------------------------------------------------------------------------------
6.20 c     hent_prlev    begsr
6.20  *
6.20 c                   eval      vvprl1_vare = vevare
6.20 c                   eval      vvprl1_prgr = w_prgr
6.20 c                   eval      vvprl1_enhe = veenhe
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 92
6.23 c                   eval      b_pris = *off
6.23  *
6.20 c                   dow       *in92 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   exsr      hent_priser
6.20  *
6.20 c                   if        b_enh1 = *on or
6.20 c                             vpenhe <> w_enhe_gml or
6.20 c                             vplety <> w_lety_gml
6.20  *
6.20 c                   if        d_dekn = 0 or
6.20 c                             d_dekn <> 0 and
6.20 c                             d1dekn <= d_dekn
6.20 c                   write     d1dtl                                30
6.20 c                   exsr      overflow
6.20 c                   endif
6.20  *
6.20 c                   eval      b_enh1 = *off
6.20 c                   eval      w_enhe_gml = vpenhe
6.20 c                   eval      w_lety_gml = vplety
6.20  *
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 92
6.20 c                   enddo
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
6.20  * prisgruppe = *blank
6.20 c                   if        b_pris = *off and
6.20 c                             w_prgr <> *blank
6.20  *
6.20 c                   eval      vvprl1_vare = vevare
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = veenhe
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 92
6.20 c                   dow       *in92 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   exsr      hent_priser
6.20  *
6.20 c                   if        b_enh1 = *on or
6.20 c                             vpenhe <> w_enhe_gml or
6.20 c                             vplety <> w_lety_gml
6.20  *
6.20 c                   if        d_dekn = 0 or
6.20 c                             d_dekn <> 0 and
6.20 c                             d1dekn <= d_dekn
6.20 c                   write     d1dtl                                30
6.20 c                   exsr      overflow
6.20 c                   endif
6.20  *
6.20 c                   eval      b_enh1 = *off
6.20 c                   eval      w_enhe_gml = vpenhe
6.20 c                   eval      w_lety_gml = vplety
6.20  *
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 92
6.20 c                   enddo
6.20  *
6.20 c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av priser for enhet/leveringstype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_priser   begsr
      *
5.70 c                   eval      d1prgr = vpprgr
     c                   eval      d1enhe = vpenhe
     c                   eval      d1lety = vplety
     c                   eval      d1inpr = vpinpr
     c                   eval      d1kopr = vpkopr
     c                   eval      d1bupr = vpbupr
     c                   eval      d1sapr = vpsapr
     c                   eval      d1saim = 0
     c                   eval      d1dekn = 0
      *
     c     *dmy          move      vppdat        d1pdat
      *
     c                   if        b_enh1 = *off
     c                   eval      d1ogrp = 0
     c                   eval      d1hgrp = 0
     c                   eval      d1ugrp = 0
     c                   eval      d1ldor = 0
     c                   eval      d1tek1 = *blank
     c                   eval      d1vare = *blank
     c                   eval      d1nobb = 0
     c                   eval      d1lvar = *blank
     c                   eval      d1lnav = *blank
     c                   eval      d1kpri = *blank
     c                   eval      d1vtyp = *blank
     c                   endif
      *
      * Beregner salgspris inkl. mva
      *
     c                   eval      d1saim = d1sapr * w_mvat
      *
     c                   if        d1saim <> 0
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_kode
     c                   parm                    d1saim
     c                   endif
      *
      * Beregner dekningsgrad (DG)
      * Dersom kostpris = 0, beregnes kostpris ut fra standard deknings-
      * bidrag på vare eller undergruppe
      *
     c                   eval      w_kopr = d1kopr
      *
     c                   if        w_kopr = 0
      *
     c                   eval      w_dekn = vvdekn
     c                   if        w_dekn = 0
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_dekn = vgudek
     c                   endif
     c                   endif
      *
     c                   eval      w_kopr = d1sapr * w_dekn / 100
     c                   if        w_kopr <> 0
     c                   eval      w_kopr = d1sapr - w_kopr
     c                   endif
      *
     c                   endif
      *
     c                   if        d1sapr <> 0
     c                   select
     c                   when      (100 - (w_kopr * 100 / d1sapr)) < -999,99
     c                   eval      d1dekn = -999,99
     c                   when      (100 - (w_kopr * 100 / d1sapr)) > 999,99
     c                   eval      d1dekn = 999,99
     c                   other
     c                   eval(h)   d1dekn = 100 - (w_kopr * 100 / d1sapr)
     c                   endsl
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter dagens dato og firma fra parameter
      *
     c                   time                    w_udat
      *
     c                   eval      w_firm = d_firm
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
5.70 c                   eval      w_lage = l_lagr
      *
     c                   endsr
