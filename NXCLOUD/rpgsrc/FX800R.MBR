      /TITLE  ASOFAK Fjerner fra slettereg. varer som ligger i varereg.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX800R
      * Beskrivelse . . : Fjerner fra VVASPF (slettede varer) varer som
      *                   ligger både i VVASPF og VVARPF
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010699 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvaslu    uf   e           k disk    rename(vvaspfr:vvaslur)
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvaslu_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1r                                90
     c                   dow       *in90 = *off
      *
     c                   eval      vvaslu_vare = vvvare
     c     vvaslu_key    chain     vvaslur                            91
     c                   if        *in91 = *off
     c                   delete    vvaslur
     c                   endif
      *
     c     vvarl1_key    reade     vvarl1r                                90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av vare-slettet-register
      *
     c     vvaslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvaslu_vare
      *
      *
      * Legger inn firmanummer i nøkkelen
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
