     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO11AR
      * Beskrivelse . . : Vise bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.20  * R6.20 180511 BSA  Legger ut leverandørens ordrenr.
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  300914 bkv  F14 vis leverandør info
7.xx  * 7.00  020216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  021019 BKV  Utvidet skjerm
7.02  * 7.00  130220 BKV  Vasker for Newlook problem
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
      *
     flo505d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
6.nu d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av datastrukturer
      *
      *  Datastruktur for detaljlinje
      *
7.01 d*                 ds
7.01 d* d_text                        77
7.01 d*  d_vare                        8    overlay(d_text)
7.01 d*  c_sep0                        1    overlay(d_text:9)  inz(x'20')
7.01 d*  d_tek1                       30    overlay(d_text:10)
7.01 d*  c_sep1                        1    overlay(d_text:40) inz(x'20')
7.01 d*  d_anta                       12    overlay(d_text:41)
7.01 d*  c_sep2                        1    overlay(d_text:53) inz(x'20')
7.01 d*  d_enh1                        3    overlay(d_text:54)
7.01 d*  c_sep3                        1    overlay(d_text:57) inz(x'20')
7.01 d*  d_raba                        6    overlay(d_text:58)
7.01 d*  c_sep4                        1    overlay(d_text:64) inz(x'20')
7.01 d*  d_sumi                       13    overlay(d_text:65)
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_raba          s              5  2
     d w_csts          s              1
     d w_ctx2          s             20
      *
     d w_firm          s                   like(lofirm)
     d w_ldor          s                   like(loldor)
6.nu d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
7.02 d w_len           s              3  0
7.02 d w_teller        s              3  0
      *
      * Definering av konstanter
      *
7.01 d c_sfil          s              2  0 inz(14)
7.xx d c_sepg          c                   const(X'20')
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Fyller control-bildet
      *
     c                   exsr      control_init
      *
      * Fyller subfile
      *
     c     lodtl1_key    setll     lodtl1
     c                   exsr      crt_subfile
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
6.30  *
6.30  * F14=Vis leverandør-informasjon
6.30  *
6.30 c                   if        *inkn = *on
6.30 c                   call      'LO575R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    loldor
6.30 c                   parm                    lolage
6.30 c                   goto      b2taga
6.30 c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å initierer/fylle opp control-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
      * Finner ordre-hode
      *
     c     lohel1_key    setll     lohel1
     c     lohel1_key    reade     lohel1                                 60
     c                   if        *in60 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in60 = *off
     c     lohel1_key    reade     lohel1                                 60
     c                   enddo
      *
     c                   eval      b2kund = loldor
     c                   eval      b2navn = lonavn
     c                   eval      b2adr1 = loadr1
     c                   eval      b2sted = losted
     c                   eval      b2numm = lonumm
     c                   eval      b2ornr = loornr
     c                   eval      b2vref = lovref
     c                   eval      b2dref = lodref
6.20 c                   eval      b2sorn = losorn
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        b2bdat
     c                   endif
      *
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        b2ldat
     c                   endif
      *
     c                   if        lomoda <> *loval
     c     *dmy          move      lomoda        b2lbda
     c                   endif
      *
      * Henter leverandørnavn
      *
     c                   if        loldor <> 0
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1                             60
     c                   if        *in60 = *off
     c                   eval      b2knav = rlnavn
     c                   eval      b2kgat = rlgate
     c                   eval      b2kste = rlsted
     c                   endif
     c                   endif
      *
      * Finner bestillingstotal
      *
     c                   eval      b2salg = (lototi - lototr)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     lodtl1_key    setll     lodtl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Vise
      *
     c                   if        b1valg = '5'
     c                   call      'LD505R'
     c                   parm                    w_firm
     c                   parm                    b1numm
     c                   parm                    b1suff
     c                   parm                    b1line
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     lodtl1_key    reade     lodtl1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1numm = ldnumm
     c                   eval      b1suff = ldsuff
     c                   eval      b1line = ldline
     c                   eval      b1text = *blank
      *
      * Finner linjetype
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1                             62
7.01 c                   eval      b1vare = 'Tekst'
7.01 c                   eval      b1enh1 = *blank
7.01 c                   eval      b1anta = *zero
7.01 c                   eval      b1sumi = *zero
      *
      * Tekstlinje
      *
     c                   if        valktx = 1
     c                   eval      b1text = ldtek1 + ldtek2
      *
      * Varelinje
      *
     c                   else
      *
     c                   eval(h)   w_raba = ldrab1 +
     c                                      ((100 - ldrab1) * ldrab2 / 100)
     c                   eval(h)   ldsumi = ldsumi - (ldsumi * w_raba / 100)
      *
7.01 c*                   eval      d_anta = %editc(ldanta : 'M')
7.01 c*                   eval      d_sumi = %editc(ldsumi : 'M')
7.01  **
7.01 c*                   eval      d_vare = ldvare
7.01 c*                   eval      d_tek1 = ldtek1
7.01 c*                   eval      d_enh1 = ldenh1
7.01 c*                   eval      d_raba = *blank
      *
7.01 c*                   if        w_raba <> 0
7.01 c*                   eval      d_raba = %editc(w_raba : '4')
7.01 c*                   endif
7.01 c*                   endif
      *
7.01 c                   eval      b1vare = ldvare
7.01 c                   eval      b1enh1 = ldenh1
7.01 c                   eval      b1anta = ldanta
7.01 c                   eval      b1raba = w_raba
7.01 c                   eval      b1sumi = ldsumi
      *
7.01 c                   eval      b1text = %TRIM(ldtek1) + ' ' + %TRIM(ldtek2)
      *
     c                   endif
      *
7.02 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
7.02  *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Newlook vask av data
7.02  *  Fjerner tegn som gjør at Newlook feiltolker
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02  /FREE
7.02
7.02   begsr newlo_vask;
7.02     for w_teller = 1 to 25;
7.02       w_len = %len(%trim(b1text));
7.02       if (w_len > 0);
7.02       b1text = %ScanRpl('.':'':b1text:w_len); // newlook tåler ikke . på slutten
7.02       endif;
7.02       w_len = %len(%trim(b1text));
7.02       if (w_len > 0);
7.02       b1text = %ScanRpl(':':'':b1text:w_len); // newlook tåler ikke : på slutten
7.02       endif;
7.02     endfor ;
7.02   endsr;
7.02  /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på ordre-hode-register
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les på ordre-linje-register
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på linje-type-register
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *
      * Henter parameter-verdier
      *
     c                   eval      w_firm = p_firm
6.nu c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
      *
     c                   eval      *in22 = *on
      *
     c                   endsr
