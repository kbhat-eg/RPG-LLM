     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RK514R
      * Beskrivelse . . : Kundeposteringer, spørring generalnota
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251003 KOB  Nytt program
T5.30 * V5.30 230704 HFL  Summerte feil
T5.30 * V5.50 270507 KOB  Viste ikke alle samlefakturaer
T5.50 * V5.50 290108 HFL  Viste ikke generalnotaer i fjor
7.xx  * 7.xx  020516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frktrl5    if   e           k disk    rename(rktrpfr:rktrl5r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl2    if   e           k disk    rename(rktrpfr:rktrl2r)
      *
     frk514d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rktrl5_samf     s                   like(rnsamf)
     d rkunl1_kund     s                   like(rnkund)
     d rktrl2_kund     s                   like(rnkund)
     d rktrl2_bdat     s                   like(rnbdat)
     d rktrl2_tist     s                   like(rntist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d p_stat          s              1
     d p_gkod          s                   like(rkavde)
     d p_ikod          s                   like(rkslgr)
     d p_gtxt          s             20
     d p_itxt          s             20
     d p_kund          s                   like(rkkund)
      *
     d w_firm          s                   like(rnfirm)
     d w_samf          s                   like(rnsamf)                                      .
     d w_kund          s                   like(rnkund)                                      .
     d w_raar          s                   like(rnraar)                                      .
     d gml_samf        s                   like(rnsamf)
     d gml_kund        s                   like(rnkund)
     d w_fors          s              1  0
     d w_post          s              1  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2samf <> 0 or
     c                             b2raar <> w_raar
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rktrl5_samf = b1samf
     c     rktrl5_key    setll     rktrl5
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi, regnskapsår utfyllt
      *
     c                   if        b2raar <> *zero or                                    .....
     c                             b2raar <> w_raar                                          .
T5.50 * For å få tak i poster for forrige år
T5.50c                   if        b2samf = 0
T5.50c                   eval      b2samf = 1
T5.50c                   endif
     c                   eval      w_raar = b2raar                                           .
      *
     c                   endif                                                           .....
      *
      * Generalnota
      *
     c                   if        b2samf <> 0
     c                   eval      w_seqe = *blank
     c                   eval      w_samf = b2samf
     c                   eval      w_kund = 0
     c                   eval      rktrl5_samf = b2samf
     c     rktrl5_key    setll     rktrl5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2samf = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 8
     c                   exsr      xc2win                                                  . .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   eval      w_fors = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
T5.30c                   eval      b1belØ = 0
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rktrl5
      *
     c                   if        %eof or
     c                             rnfirm <> w_firm
     c                   if        w_post > 0
     c                   exsr      skr_subfile                                         ..... .
     c                   endif
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> rnraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   if        p_kund <> 0 and                                     ..... .
     c                             p_kund <> rnkund
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   if        w_fors = 0
     c                   eval      gml_kund = rnkund                                   ..... .
     c                   eval      gml_samf = rnsamf
     c                   eval      w_fors = 1
     c                   endif                                                         ..... .
      *
     c                   if        rnkund <> gml_kund or                               ..... .
     c                             rnsamf <> gml_samf
     c                   exsr      skr_subfile                                         ..... .
     c                   endif                                                         ..... .
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = rnbdat
     c                   eval      b1tist = rntist
     c                   eval      b1samf = rnsamf
     c     *dmy          move      rnbdat        b1bida
     c                   eval      b1belØ = b1belØ + rnbelØ
     c                   eval      b1kund = rnkund
     c                   eval      w_post = 1
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive til subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_subfile   begsr
      *
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rktrl5_samf = gml_samf
      *
      * Hent kunde
      *
     c                   eval      rkunl1_kund = gml_kund                                    .
     c     rkunl1_key    chain     rkunl1                             61                     .
     c                   if        *in61 = *off                                              .
     c                   eval      b1navn = rknavn                                           .
     c                   endif                                                               .
      *
     c                   write     b1sfl
     c                   eval      b1belØ = 0
     c                   eval      w_post = 0
      *
     c                   eval      gml_kund = rnkund                                   ..... .
     c                   eval      gml_samf = rnsamf
      *
     c     end_skrfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rktrl5_key    setgt     rktrl5
     c                   readp     rktrl5
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rktrl5
      *
     c                   if        %eof or
     c                             rnfirm <> w_firm
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> rnraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rktrl5_samf = rnsamf
      *
     c                   enddo
      *
     c                   if        %eof
     c     rktrl5_key    setll     rktrl5
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for vis                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   call      'RK515R'
     c                   parm                    b1kund
     c                   parm                    b1samf
     c                   parm                    w_raar
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_kund
      *
      * Key for posisjonering på generalnotanummer
      *
     c     rktrl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl5_samf
      *
      * Key for kunderegister via kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for lesing av postering
      *
     c     rktrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl2_kund
     c                   kfld                    rktrl2_bdat
     c                   kfld                    rktrl2_tist
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = *year
     c                   eval      b2raar = *year
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rktrl5_samf = 0
     c     rktrl5_key    setll     rktrl5
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
