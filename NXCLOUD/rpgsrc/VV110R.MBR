     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV110R
      * Beskrivelse . . : Vare, bytter varenummer på vare - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210699 HKO  Nytt program
6.20  *       040116 bsa  Sjekker om til varen allerede er opprettet på
6.20  *                   noen av registrene.
7.xx  * 7.00  030216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *       070323 bof  Utvidet VV112R slik at man kan se hva som var feil.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvv110d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_gvar          s                   like(vvvare)
     d w_nvar          s                   like(vvvare)
6.20 d w_stat          s              1
8.01 d w_stxt          s             20
8.01 d w_meld          s             50
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Kaller oppdateringsprogram
      *
     c                   movel     b1gvar        w_gvar
     c                   movel     b1nvar        w_nvar
      *
     c                   call      'VV111R'
     c                   parm                    w_firm
     c                   parm                    w_gvar
     c                   parm                    w_nvar
      *
      * Viser bildet på nytt
      *
     c                   eval      b1gvar = 0
     c                   eval      b1tek1 = *blank
     c                   eval      b1nvar = 0
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Varenummer
      *
     c                   if        w_afld = 'B1GVAR'
     c                   movel     b1gvar        w_gvar
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_gvar
     c                   parm                    b1tek1
     c                   movel     w_gvar        b1gvar
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1tek1 = *blank
      *
      * Gammelt varenummer
      *
     c                   if        b1gvar = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   movel     b1gvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1tek1 = vvtek1
      *
      * Nytt varenummer
      *
     c                   if        b1nvar = 0
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   movel     b1nvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *off
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
6.20  *
6.20  * Sjekk om tilvarenummer allerede finnes på noen av regsitrene
6.20  *
6.20 c                   movel     b1gvar        w_gvar
6.20 c                   movel     b1nvar        w_nvar
6.20 c                   eval      w_stat = *blanks
8.01 c                   eval      w_stxt = *blanks
6.20  *
6.20 c                   call      'VV112R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    w_gvar
6.20 c                   parm                    w_nvar
6.20 c                   parm                    w_stat
8.01 c                   parm                    w_stxt
6.20  *
6.20 c                   if        w_stat = '1'
8.01 c                   eval      w_meld = 'Vare finnes fra før i : ' +
8.01 c                                      %trim(w_stxt)
8.01 c                   call      'AA005R'
8.01 c                   parm                    w_meld
6.20 c                   eval      *in35  = *on
6.20 c                   eval      b_feil = *on
6.20 c                   goto      end_sjekk
6.20 c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
