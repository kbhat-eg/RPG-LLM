     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VK701R
      * Beskrivelse . . : Varegruppeknytning, finner alt. varegruppe for
      *                   varegruppe, leverandør og vare som parameter
      *
      *                   Leter først etter alt.varegruppe på vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051205 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvgkl1    if   e           k disk    rename(vvgkpfr:vvgkl1r)
      *
      * Definering parametre
      *
     d p_firm          s                   like(vkfirm)
     d p_ogrp          s                   like(vkogrp)
     d p_hgrp          s                   like(vkhgrp)
     d p_ugrp          s                   like(vkugrp)
     d p_ldor          s                   like(vkldor)
     d p_vare          s                   like(vvvare)
     d p_ahgr          s                   like(vkahgr)
     d p_augr          s                   like(vkaugr)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvasl1_vare     s                   like(vvvare)
     d vvgkl1_ogrp     s                   like(vkogrp)
     d vvgkl1_hgrp     s                   like(vkhgrp)
     d vvgkl1_ugrp     s                   like(vkugrp)
     d vvgkl1_ldor     s                   like(vkldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vkfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter, og initierer parametre
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      p_ahgr = 0
     c                   eval      p_augr = 0
      *
      * Henter alternativ varegruppe fra vare
      *
     c                   if        p_vare <> *blank
      *
     c                   eval      vvahgr = 0
     c                   eval      vvaugr = 0
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      vvasl1_vare = p_vare
     c     vvasl1_key    chain     vvasl1
     c                   endif
      *
     c                   if        vvahgr <> 0 and
     c                             vvaugr <> 0
     c                   eval      p_ahgr = vvahgr
     c                   eval      p_augr = vvaugr
     c                   goto      avslutt
     c                   endif
      *
     c                   endif
      *
      * Leser på varegruppeknytning
      * Leter først etter knytninger mot leverandør
      *
     c                   eval      vvgkl1_ogrp = p_ogrp
     c                   eval      vvgkl1_hgrp = p_hgrp
     c                   eval      vvgkl1_ugrp = p_ugrp
     c                   eval      vvgkl1_ldor = p_ldor
      *
     c     vvgkl1_key    chain     vvgkl1
     c                   if        not %found
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1
     c                   if        not %found
     c                   eval      vvgkl1_ldor = p_ldor
     c                   eval      vvgkl1_ugrp = 0
     c     vvgkl1_key    chain     vvgkl1
     c                   if        not %found
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1
     c                   if        not %found
     c                   eval      vvgkl1_ldor = p_ldor
     c                   eval      vvgkl1_hgrp = 0
     c     vvgkl1_key    chain     vvgkl1
     c                   if        not %found
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        %found(vvgkl1)
     c                   eval      p_ahgr = vkahgr
     c                   eval      p_augr = vkaugr
     c                   goto      avslutt
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldor
     c                   parm                    p_vare
     c                   parm                    p_ahgr
     c                   parm                    p_augr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-slettet
      *
     c     vvasl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvasl1_vare
      *
      * Key for les på varegruppeknytning
      *
     c     vvgkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl1_ogrp
     c                   kfld                    vvgkl1_hgrp
     c                   kfld                    vvgkl1_ugrp
     c                   kfld                    vvgkl1_ldor
      *
     c                   endsr
