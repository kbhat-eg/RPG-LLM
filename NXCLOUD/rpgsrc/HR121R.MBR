     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR121R
      * Beskrivelse . . : Radioterminal trans, lagertelling buntlinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *              ASK  Nytt program
      *       201204 HKO  Lagt inn ny logikk for scanning
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
      *       060307 bø   Hvis enhet på ean - skal denne foreslås.
      *       011107 bø   Hvis vare ikke finnes ved scanning - legg i JVSCAPF
      *       030108 bø   Teste om antall skal være større enn 1000
5.61  *       080109 bø   Kaller mot cl ved scanne-sjekk
      * 6.10  150609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.21  * 6.20  061210 bof  Gir melding hvis skaffevare
6.22  * 6.20  080410 bø   Utvidet med timestamp for bruk videre
6.23  * 6.20  190511 bø   Utvidet vl710r med div. info
6.nu  * 5.30  170614 bof  endret ihht. nummerutvidelse
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhrtrl2    if   e           k disk    rename(hrtrpfr:hrtrl2r)
     fhrtrl3    uf   e           k disk    rename(hrtrpfr:hrtrl3r)
     fhrtrlr    if   e           k disk    rename(hrtrpfr:hrtrlrr)
     fhrtrlu    uf a e           k disk    rename(hrtrpfr:hrtrlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flbhelu    uf a e           k disk    rename(lbhepfr:lbhelur)
     flbdtlu    uf a e           k disk    rename(lbdtpfr:lbdtlur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fhr121d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lufirm)
     d p_kode          s                   like(hrkode)
     d p_tims          s                   like(hrtims)
     d p_batc          s                   like(lubatc)
     d p_lage          s                   like(lulage)
     d p_numm          s                   like(lunumm)
     d p_text          s                   like(lutext)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Definering av variabler i nøkler
      *
     d hrtrlr_vare     s                   like(hrvare)
     d hrtrlr_enhe     s                   like(hrenhe)
     d hrtrlu_vare     s                   like(hrvare)
     d hrtrlu_enhe     s                   like(hrenhe)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
     d lbdtlu_batc     s                   like(lvbatc)
     d lbdtlu_numm     s                   like(lvnumm)
     d lbdtlu_line     s                   like(lvline)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_scan          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_bunt          s              1    inz(*off)
6.21 d b_skaf          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_scan          s             26
5.61 d w_sctx          s             30
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_vare          s             15
6.11 d w_eanx          s             14
     d w_kod2          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_stek          s             35
     d x               s              1  0
      *
     d w_firm          s                   like(hrfirm)
     d w_kode          s                   like(hrkode)
     d w_tims          s                   like(hrtims)
     d w_enhe          s                   like(hrenhe)
     d w_anta          s                   like(hranta)
     d w_dato          s                   like(hrdato)
     d w_time          s                   like(hrtime)
     d w_anta_kalk     s                   like(hranta)
     d w_bnum          s                   like(lvnumm)
6.11 d w_xenhe         s                   like(hrenhe)
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.21 d w_vtyp          s                   like(vvvtyp)
6.23 d w_utda          s                   like(vvudat)
6.23 d w_ldor          s                   like(vvldor)
6.23 d b_inlr          s              1    inz(*off)
6.nu d w_numm          s              8  0
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
6.30 d w_lv_vare       s                   like(vvlvnr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(6)
     d c_digi          c                   '0123456789 '
     d c_null          c                   '0000000000000'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1BLD  - D1 Skjermbilde for slette (Delete)
      * E1BLD  - E1 Skjermbilde for avslutning av tellebunt
      * F1BLD  - F1 Skjermbilde for bekreftelse av 0 i antall
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.30 C/Exec SQL
6.30 C+  Set Option DatFmt=*ISO
6.30 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   endsl
      *
      * F4=Spørring på vare
      *
     c                   if        *inkd
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    b2sca1
     c                   parm                    w_stek
     c                   eval      *in22 = *on
     c                   if        b2sca1 = *blank
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av scanning
      *
     c                   if        b2sca1 <> *blank or
     c                             b2sca2 <> *blank
     c                   exsr      scanning
     c                   eval      *in22 = *on
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      * Oppretter tellebunt
      *
     c                   exsr      opprett
     c                   if        b_oppr = *off
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
      *
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
      * 4) num + lengde <= 14:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
     c                   eval      b_feil = *off
     c                   eval      b_scan = *on
      *
     c                   eval      c1enhe = *blank
     c                   eval      w_anta = 0
      *
     c                   eval      w_scan = %trim(b2sca1) + %trim(b2sca2)
      *
     c                   eval      w_leng = %len(%trim(w_scan))
      *
      * Sjekker om scannefelt inneholder alfa-verdi
      *
     c     c_digi        check     w_scan        w_posi
      *
      * Behandling dersom alfa
      *
     c                   if        w_posi > 0
      *
6.30  * sjekker om logistikk-vare
6.30 c                   eval      w_lv_vare = *blank
6.30 c/exec sql
6.30 c+ select vvlvnr
6.30 c+ into   :w_lv_vare
6.30 c+ from   vvlepf
6.30 c+ where  vvllva = :w_scan
6.30 c+ fetch first 1 rows only
6.30 c/end-exec
6.30 c                   if        w_lv_vare <> *blank
6.30 c                   movel     w_lv_vare     c1vare
6.30 c                   exsr      xc2bld
6.30 c                   goto      end_scan
6.30 c                   endif
      *
     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
5.61 c**                 call      'JV871R '
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      w_xenhe = *blank
     c                   eval      d_e128 = w_scan
6.11 c                   eval      w_eann = d_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.21 c                   eval      b_skaf  = *off
6.21 c                   exsr      sjk_skaff
6.21 c                   if        b_skaf  = *on
6.21 c                   goto      end_scan
6.21 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   eval      w_anta = d_anta
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
5.61 c**                 call      'JV871R '
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
5.61 c**                 call      'JV871R '
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vare = %subst(c_null:1:8-w_leng) +
     c                                      w_scan
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.21 c                   eval      b_skaf  = *off
6.21 c                   exsr      sjk_skaff
6.21 c                   if        b_skaf  = *on
6.21 c                   goto      end_scan
6.21 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde <= 13
      *
6.11 c                   if        w_leng <= 14
      *
6.11 c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
     c                                      w_scan
      *
     c                   eval      w_xenhe = *blank
6.11 c                   movel     w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
6.21 c                   eval      b_skaf  = *off
6.21 c                   exsr      sjk_skaff
6.21 c                   if        b_skaf  = *on
6.21 c                   goto      end_scan
6.21 c                   endif
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
6.30  * sjekker om logistikk-vare
6.30 c                   eval      w_lv_vare = *blank
6.30 c/exec sql
6.30 c+ select vvlvnr
6.30 c+ into   :w_lv_vare
6.30 c+ from   vvlepf
6.30 c+ where  vvllva = :w_scan
6.30 c+ fetch first 1 rows only
6.30 c/end-exec
6.30 c                   if        w_lv_vare <> *blank
6.30 c                   movel     w_lv_vare     c1vare
6.30 c                   exsr      xc2bld
6.30 c                   goto      end_scan
6.30 c                   endif
      *
     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
      * Setter feil-indikator dersom vare ikke ble funnet
      *
5.61 c**                 call      'JV871R '
5.61 c                   call      'LL871C '
     c                   parm                    w_scan
5.61 c                   parm                    w_sctx
      *
     c                   call      'JV871R '
     c                   parm                    w_scan
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c     end_scan      tag
      *
     c                   eval      b_scan = *off
      *
     c                   if        b_feil = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
     c                   eval      *in22 = *off
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
     c                   eval      d1enhe = b1enhe
     c                   exsr      xd1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_enhe = c1enhe
      *
      * Henter vareinformasjon
      *
     c***                eval      vvarl1_vare = c1vare
     c                   eval      vvarl1_vare = *blank
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c                   goto      end_c2bld
     c                   endif
      *
     c                   eval      vvtek1 = %trim(vvtek1)
     c                   eval      c2tek1 = %subst(vvtek1:1:19)
     c                   eval      c2tek2 = %subst(vvtek1:20:16)
      *
      * Henter tre salgsenheter.
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      c2enh1 = *blank
     c                   eval      c2enh2 = *blank
     c                   eval      c2enh3 = *blank
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = veenhe
     c                   when      x = 2
     c                   eval      c2enh2 = veenhe
     c                   when      x = 3
     c                   eval      c2enh3 = veenhe
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
      *
     c                   if        w_xenhe <> *blank
     c                   select
     c                   when      w_xenhe = c2enh1
     c                   eval      *in80  = *on
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh2
     c                   eval      *in80  = *off
     c                   eval      *in81  = *on
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh3
     c                   eval      *in80  = *off
     c                   eval      *in81  = *off
     c                   eval      *in82  = *on
     c                   endsl
     c                   endif
      *
      * Antall
      *
     c                   eval      c2anta = w_anta
      *
      * Bestilling
      *
     c                   if        b_scan = *off
      *
     c                   eval      hrtrlr_vare = c1vare
     c                   eval      hrtrlr_enhe = c1enhe
     c     hrtrlr_key    chain     hrtrlr                             90
     c                   if        *in90 = *off
      *
     c                   eval      *in80 = *off
      *
     c                   select
     c                   when      hrenhe = c2enh1
     c                   eval      *in80 = *on
     c                   when      hrenhe = c2enh2
     c                   eval      *in81 = *on
     c                   when      hrenhe = c2enh3
     c                   eval      *in82 = *on
     c                   endsl
      *
     c                   eval      c2anta = hranta
      *
     c                   endif
     c                   endif
      *
      * Viser bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc
     c                   goto      end_c2bld
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   select
     c                   when      *in80 = *on and
     c                             c2enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        c2enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
     c                   goto      c2tagb
     c                   endif
      *
      * F2=Kalkulator
      *
     c                   if        *inkb
     c                   call      'HR200R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_anta_kalk
     c                   goto      c2tagb
     c                   endif
      *
      * F5=Forny
      *
     c                   if        *inke
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      c2anta = 0
     c                   goto      c2tagb
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2anta <= 0
      *
     c                   exfmt     f1bld
      *
     c                   if        *inkc
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Tester om antall skal være større en 1000
      *
     c                   if        c2anta > 1000
      *
     c                   exfmt     g1bld
      *
     c                   if        *inkc
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   hrtrlur
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      c1enhe = c2enh1
     c                   when      *in81 = *on
     c                   eval      c1enhe = c2enh2
     c                   when      *in82 = *on
     c                   eval      c1enhe = c2enh3
     c                   endsl
      *
     c                   if        b1valg = 2 and
     c                             w_enhe <> c1enhe
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = w_enhe
     c     hrtrlu_key    chain     hrtrlur                            90
     c                   if        *in90 = *off
     c                   delete    hrtrlur
     c                   endif
     c                   endif
      *
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = c1enhe
     c     hrtrlu_key    chain     hrtrlur                            90
      *
     c                   eval      hrvare = c1vare
     c                   eval      hrenhe = c1enhe
      *
     c                   if        *in90 = *off and
     c                             b_scan = *on
     c                   eval      hranta = hranta + c2anta
     c                   else
     c                   eval      hranta = c2anta
     c                   endif
      *
     c                   time                    hrdato
     c                   time                    hrtime
      *
     c                   if        *in90 = *off
     c                   update    hrtrlur
     c                   else
     c                   eval      hrfirm = w_firm
     c                   eval      hrkode = w_kode
     c                   eval      hrtims = w_tims
     c                   write     hrtrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1enhe = hrenhe
     c     hranta        mult      1             b1anta
     c                   endif
      *
     c     end_c2bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (d1bld)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1bld        begsr
      *
     c                   eval      hrtrlr_vare = d1vare
     c                   eval      hrtrlr_enhe = d1enhe
     c     hrtrlr_key    chain     hrtrlr                             90
      *
     c                   eval      d1anta = hranta
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *off
     c                   eval      d1tek1 = %subst(vvtek1:1:19)
     c                   eval      d1tek2 = %subst(vvtek1:20:16)
     c                   endif
      *
     c                   exfmt     d1bld
      *
     c                   if        *inkc
     c                   goto      end_d1bld
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      hrtrlu_vare = d1vare
     c                   eval      hrtrlu_enhe = d1enhe
     c     hrtrlu_key    chain     hrtrlur                            90
      *
     c                   delete    hrtrlur
      *
     c     end_d1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     hrtrl2_key    reade     hrtrl2                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1tek1 = *blank
     c                   eval      b1enhe = hrenhe
     c                   eval      b1anta = 0
     c                   eval      b1vare = hrvare
      *
     c     hranta        mult      1             b1anta
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av tellebunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c                   eval      b_oppr = *off
      *
     c     hrtrl2_key    chain     hrtrl2                             90
     c                   if        *in90 = *on
     c                   eval      b_oppr = *on
     c                   goto      end_opprett
     c                   endif
      *
      * Tilleggsbilde
      *
     c     e1taga        tag
      *
     c                   exfmt     e1bld
      *
     c                   if        *inkc
     c                   goto      end_opprett
     c                   endif
      *
     c                   if        *inke
     c                   goto      e1taga
     c                   endif
      *
      * Inntastet buntnummer eller fra teller?
      *
     c                   if        b_bunt = *on
     c                   exsr      ny_bunt
     c                   else
     c                   eval      w_bnum = p_numm
     c                   endif
      *
      * Oppretter bunthode
      *
     c                   eval      b_oppr = *on
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbhelu_numm = w_bnum
     c     lbhelu_key    chain     lbhelur
      *
     c                   if        not %found
     c                   eval      lutext = p_text
     c                   eval      luoper = *blank
     c                   eval      lulage = p_lage
     c                   eval      lutype = 'HTRT      '
     c                   eval      luwsid = l_wsid
     c                   eval      luuser = l_user
     c                   eval      lutnum = 0
     c                   time                    ludato
     c                   time                    lutime
      *
     c                   eval      lusdat = *loval
     c                   eval      luodat = *loval
     c                   eval      luskod = 0
     c                   eval      lukode = 0
      *
     c                   eval      lufirm = w_firm
     c                   eval      lubatc = lbhelu_batc
     c                   eval      lunumm = lbhelu_numm
6.10 c                   time                    ludato
6.10 c                   time                    lutime
6.10 c                   eval      luuser = l_user
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
     c                   write     lbhelur
     c                   endif
      *
      * Flytter telling fra radioterminal til tellebunt-linje
      *
     c     hrtrl3_key    setll     hrtrl3
     c     hrtrl3_key    reade     hrtrl3r                                90
     c                   dow       *in90 = *off
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
      *
     c                   eval      lvfirm = hrfirm
     c                   eval      lvbatc = p_batc
     c                   eval      lvnumm = w_bnum
     c                   eval      lvline = lvline + 1
     c                   eval      lvlage = p_lage
     c                   eval      lvogrp = vvogrp
     c                   eval      lvhgrp = vvhgrp
     c                   eval      lvugrp = vvugrp
     c                   eval      lvvare = vvvare
     c                   eval      lvenh1 = hrenhe
     c                   eval      lvtek1 = vvtek1
     c                   eval      lvantt = hranta
6.22 c                   eval      lvtims = hrtims
      *
6.10 c                   time                    lvodat
6.10 c                   time                    lvotim
6.10 c                   eval      lvousr = l_user
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
      *
     c                   write     lbdtlur
      *
     c                   delete    hrtrl3r
      *
     c     hrtrl3_key    reade     hrtrl3r                                90
     c                   enddo
      *
     c     end_opprett   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne neste ledige buntnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_bunt       begsr
      *
     c     ny_les        tag
     c                   eval      w_kod2 = '0'
     c                   eval      w_fell = laatbu
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
     c                   call      'AS100R'
     c                   parm                    w_kod2
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_numm
      *
6.nu c                   eval      w_bnum = w_numm
      *
      * Sjekk om bunt allerede brukt
      *
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbhelu_numm = w_bnum
     c     lbhelu_key    chain     lbhelur
      *
     c                   if        %found
     c                   goto      ny_les
     c                   endif
      *
     c     end_ny_bunt   endsr
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *   Subrutine som sjekker om varen er skaffevare for lageret
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21 c     sjk_skaff     begsr
6.21  *
6.21 c                   call      'VL710R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    vvvare
6.21 c                   parm                    p_lage
6.21 c                   parm                    w_vtyp
6.23 c                   parm                    w_utda
6.23 c                   parm                    w_ldor
6.23 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
6.21  *
6.21 c                   if        w_vtyp = 'S'
6.21 c                   exfmt     m1msg
6.21  *
6.21 c                   if        *inkc
6.21 c                   eval      b_skaf =*on
6.21 c                   leavesr
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på radioterminal-trans desc
      *
     c     hrtrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      * Key for les på radioterminal-trans asc
      *
     c     hrtrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      * Key for oppslag på radioterminal-trans
      *
     c     hrtrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlr_vare
     c                   kfld                    hrtrlr_enhe
      *
      * Key for oppdatering av radioterminal-trans
      *
     c     hrtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlu_vare
     c                   kfld                    hrtrlu_enhe
      *
     c     hrtrlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på lager status-koder
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for oppdatering av TELLEBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for oppdatering TELLEBUNT-LINJE
      *
     c     lbdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtlu_batc
     c                   kfld                    lbdtlu_numm
     c                   kfld                    lbdtlu_line
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kode
     c                   parm                    p_tims
     c                   parm                    p_batc
     c                   parm                    p_lage
     c                   parm                    p_numm
     c                   parm                    p_text
      *
      * Henter firma og timestamp fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kode = p_kode
     c                   eval      w_tims = p_tims
      *
     c                   if        p_numm = 0
     c                   eval      b_bunt = *on
     c                   endif
      *
      * Hent opplysninger fra lager status-koder
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
5.61 c                   eval      w_sctx = 'HR121R Lagertelling'
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
