     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX895R
      * Beskrivelse . . : Sletter vare enhet fra vveppf hvis ikke
      *                   samme enhet finnes på vvenpf
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       041110 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvveplu    uf   e           k disk    rename(vveppfr:vveplur)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s              3  0
     d w_inlr          s              1
     d vvenlr_firm     s                   like(vefirm)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d vveplu_pfir     s                   like(vepfir)
     d vveplu_pvar     s                   like(vepvar)
     d vveplu_penh     s                   like(vepenh)
     d vveplu_pldo     s                   like(vepldo)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vveplr
     c                   dow       not %eof
      *
     c                   eval      vvenlr_firm = vepfir
     c                   eval      vvenlr_vare = vepvar
     c                   eval      vvenlr_enhe = vepenh
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found(vvenlr)
      * sletter denne fra vveppf
     c                   eval      vveplu_pfir = vepfir
     c                   eval      vveplu_pvar = vepvar
     c                   eval      vveplu_penh = vepenh
     c                   eval      vveplu_pldo = vepldo
     c     vveplu_key    chain     vveplu
     c                   if        %found(vveplu)
     c                   delete    vveplur
     c                   endif
     c                   endif
      *
     c                   read      vveplr
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
      *  Key for les av vare-enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    vvenlr_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      *  Key for upd av vare-enhet-ldor
      *
     c     vveplu_key    klist
     c                   kfld                    vveplu_pfir
     c                   kfld                    vveplu_pvar
     c                   kfld                    vveplu_penh
     c                   kfld                    vveplu_pldo
      *
      *
     c                   endsr
