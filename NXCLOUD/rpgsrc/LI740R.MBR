     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : EDI -LAGR
      * Program . . . . : LI740R
      * beskrivelse . . : Spesialbehandling i EDI mottak
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150518 bsa  Nytt program
7.01  * K000  201020 bsa  Utvidet funksjonalitet rundt innlesing av EDI
7.01  * K000              meldinger.
7.02  * K000  270421 bsa  Endret ledetekst for å unngå misforståelser ved
7.02  * K000              opprettelse av nye regler. Feltene er alfa, og
7.02  * K000              må derfor ha foranstilte 0'er. Sjekker på om riktig
7.02  * K000              lengde.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flekol1    if   e           k disk    rename(lekopfr:lekol1r)
     flekolr    if   e           k disk    rename(lekopfr:lekolrr)
     flekolu    uf a e           k disk    rename(lekopfr:lekolur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
7.01 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.01 fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
7.01 fra14l1    if   e           k disk    rename(ra14pfr:ra14l1r)
      *
     fli740d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
7.01 d lekolr_kmty     s                   like(lekmty)
     d lekolr_ktyp     s                   like(lektyp)
7.01 d lekol1_kmty     s                   like(lekmty)
     d lekol1_ktyp     s                   like(lektyp)
     d lekol1_kid1     s                   like(lekid1)
     d lekol1_kid2     s                   like(lekid2)
     d lekol1_kkod     s                   like(lekkod)
7.01 d lekolu_kmty     s                   like(lekmty)
     d lekolu_ktyp     s                   like(lektyp)
     d lekolu_kid1     s                   like(lekid1)
     d lekolu_kid2     s                   like(lekid2)
     d lekolu_kkod     s                   like(lekkod)
     d rlevl1_levr     s                   like(rllevr)
7.01 d rkunl1_kund     s                   like(rkkund)
7.01 d ra11l1_kkod     s                   like(rakkod)
7.01 d ra14l1_nkod     s                   like(rankod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_kabc          s              1    inz(*off)
      *
     d w_firm          s                   like(lekfir)
     d w_levr          s              6  0
     d w_lnav          s             30
     d w_kund          s              6  0
     d w_knav          s             30
     d w_udate         s               d   datfmt(*iso)
     d w_stat          s              1
     d w_disp          s              1
     d w_modu          s             10
     d w_frad          s               d   datfmt(*iso)
     d w_tild          s               d   datfmt(*iso)
7.01 d w_kkod          s                   like(rlkatg)
7.01 d w_ktxt          s             20
      *
      *
      * Definering av konstanter
      *
7.01 d c_sfil          s              2  0 inz(17)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c*                  when      *inkd
     c*                  exsr      sok_kunde
     c*                  goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ktyp <> *blanks
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
7.01 c                   eval      lekolr_kmty = b1kmty
     c                   eval      lekolr_ktyp = b1ktyp
     c     lekolr_key    setll     lekolr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Type
      *
     c                   if        b2ktyp <> *blanks
7.01 c                   eval      lekolr_kmty = 'F'
     c                   eval      lekolr_ktyp = b2ktyp
     c     lekolr_key    setll     lekolr
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ktyp = *blanks
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
7.01 c                   eval      c1kmty = b1kmty
     c                   eval      c1ktyp = b1ktyp
     c                   eval      c1kid1 = b1kid1
7.01 c**                 eval      c1kid2 = b1kid2
     c                   eval      c1kkod = b1kkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
7.01 c                   eval      k2kmty = b1kmty
     c                   eval      k2ktyp = b1ktyp
     c                   eval      k2kid1 = b1kid1
7.01 c**                 eval      k2kid2 = b1kid2
     c                   eval      k2kkod = b1kkod
7.01 c                   eval      k1kmty = 'F'
     c                   eval      k1ktyp = *blanks
     c                   eval      k1kid1 = *blanks
7.01 c**                 eval      k1kid2 = *blanks
     c                   eval      k1kkod = *blanks
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
7.01 c                   eval      d1kmty = b1kmty
     c                   eval      d1ktyp = b1ktyp
     c                   eval      d1kid1 = b1kid1
7.01 c**                 eval      d1kid2 = b1kid2
     c                   eval      d1kkod = b1kkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
7.01 c                   eval      c1kmty = b1kmty
     c                   eval      c1ktyp = b1ktyp
     c                   eval      c1kid1 = b1kid1
7.01 c**                 eval      c1kid2 = b1kid2
     c                   eval      c1kkod = b1kkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
7.01 c                   eval      c1kmty = 'F'
     c                   eval      c1ktyp = *blanks
     c                   eval      c1kid1 = *blanks
7.01 c**                 eval      c1kid2 = *blanks
     c                   eval      c1kkod = *blanks
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      sok
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1ktyp = *blanks
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
7.01 c                   if        c1kmty <> 'F'
7.01 c                   eval      *in31 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
      *
7.01 c                   if        c1ktyp < '1' or
7.01 c                             c1ktyp > '4'
7.01 c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
7.01  *
7.01 c                   if        c1kkod < '1' or
7.01 c                             c1kkod > '3'
7.01 c                   eval      *in34 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.02  *
7.02  * Sjekk at lengden på input er ihht lengden på feltet
7.02  *
7.02  * Kunder og leverandører må være 6 posisjoner
7.02  *
7.02 c                   if        c1ktyp = '1' or
7.02 c                             c1ktyp = '2'
7.02 c                   if        %len(%trim(c1kid1)) <> 6
7.02 c                   eval      *in36 = *on
7.02 c                   goto      c1taga
7.02 c                   endif
7.02 c                   endif
7.02  *
7.02  * Kategorier må være 4 posisjoner
7.02  *
7.02 c                   if        c1ktyp = '3' or
7.02 c                             c1ktyp = '4'
7.02 c                   if        %len(%trim(c1kid1)) <> 4
7.02 c                   eval      *in36 = *on
7.02 c                   goto      c1taga
7.02 c                   endif
7.02 c                   endif
7.01  * Sjekk identitet
7.01 c                   if        c1ktyp <> *blanks and
7.01 c                             c1kid1 = *blanks
7.01 c                   eval      *in33 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.01  * Sjekk leverandør
7.01 c                   if        c1ktyp = '1'
7.01 c                   eval      rlevl1_levr = %int(%trim(c1kid1))
7.01 c     rlevl1_key    chain     rlevl1
7.01 c                   if        %found
7.01 c                   eval      c1d1tx = rlnavn
7.01 c                   else
7.01 c                   eval      *in33 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Sjekk kunde
7.01 c                   if        c1ktyp = '2'
7.01 c                   eval      rkunl1_kund = %int(%trim(c1kid1))
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        %found
7.01 c                   eval      c1d1tx = rknavn
7.01 c                   else
7.01 c                   eval      *in33 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Sjekk leverandørlkategori
7.01 c                   if        c1ktyp = '3'
7.01 c                   eval      ra14l1_nkod = %int(%trim(c1kid1))
7.01 c     ra14l1_key    chain     ra14l1
7.01 c                   if        %found
7.01 c                   eval      c1d1tx = rantxt
7.01 c                   else
7.01 c                   eval      *in33 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Sjekk kundekategori
7.01 c                   if        c1ktyp = '4'
7.01 c                   eval      ra11l1_kkod = %int(%trim(c1kid1))
7.01 c     ra11l1_key    chain     ra11l1
7.01 c                   if        %found
7.01 c                   eval      c1d1tx = raktxt
7.01 c                   else
7.01 c                   eval      *in33 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Ugyldige kombinasjoner
7.01 c                   if        c1ktyp = '1' and
7.01 c                             c1kkod > '2'
7.01 c                   eval      *in35 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.01  * Ugyldige kombinasjoner
7.01 c                   if        c1ktyp = '1' and
7.01 c                             c1kkod > '2'
7.01 c                   eval      *in35 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
7.01 c                   eval      lekol1_kmty = c1kmty
     c                   eval      lekol1_ktyp = c1ktyp
     c                   eval      lekol1_kid1 = c1kid1
7.01 c**                 eval      lekol1_kid2 = c1kid2
7.01 c                   eval      lekol1_kid2 = *blanks
     c                   eval      lekol1_kkod = c1kkod
     c     lekol1_key    chain     lekol1
     c                   if        %found
     c                   exsr      xc2msg
     c                   goto      c1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
7.01 c                   eval      c1kmty = *blanks
     c                   eval      c1ktyp = *blanks
     c                   eval      c1kid1 = *blanks
7.01 c**                 eval      c1kid2 = *blanks
     c                   eval      c1kkod = *blanks
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ktyp = *blanks
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av manglende melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    xc1msg        begsr
      *
     c*                  exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c*                  if        *inkc or *inkl
     c*                  leavesr
     c*                  endif
      *
     c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg        begsr
      *
     c                   exfmt     c2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      c1d1tx = *blanks
7.01 c**                 eval      c1d2tx = *blanks
      *
7.01 c                   eval      lekol1_kmty = c1kmty
     c                   eval      lekol1_ktyp = c1ktyp
     c                   eval      lekol1_kid1 = c1kid1
7.01 c**                 eval      lekol1_kid2 = c1kid2
7.01 c                   eval      lekol1_kid2 = *blanks
     c                   eval      lekol1_kkod = c1kkod
     c     lekol1_key    chain     lekol1
      *
     c                   if        %found
     c                   eval      c2kbes = lekbes
7.01 c     *dmy          move      lekoda        c2koda
7.01 c     *dmy          move      lekeda        c2keda
7.01 c                   eval      c2keus = lekeus
     c                   else
     c                   eval      c2kbes = *blanks
     c                   endif
      *
     c                   if        c1ktyp = '1'
     c                   eval      c1kttx = 'Spesialbehandling leverandør'
     c                   elseif    c1ktyp = '2'
     c                   eval      c1kttx = 'Spesialbehandling kunde'
     c                   elseif    c1ktyp = '3'
7.01 c                   eval      c1kttx = 'Spesialbeh. leverandørkategori'
7.01 c                   elseif    c1ktyp = '4'
7.01 c                   eval      c1kttx = 'Spesialbeh. kundekategori'
     c                   else
     c                   eval      c1kttx = *blanks
     c                   endif
      * Leverandør
     c                   if        c1ktyp = '1'
     c                   eval      rlevl1_levr = %int(%trim(c1kid1))
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      c1d1tx = rlnavn
     c                   endif
     c                   endif
7.01  * Kunder
7.01 c                   if        c1ktyp = '2'
7.01 c                   eval      rkunl1_kund = %int(%trim(c1kid1))
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        %found
7.01 c                   eval      c1d1tx = rknavn
7.01 c                   endif
7.01 c                   endif
7.01  * Leverandørkategori
7.01 c                   if        c1ktyp = '3'
7.01 c                   eval      ra14l1_nkod = %int(%trim(c1kid1))
7.01 c     ra14l1_key    chain     ra14l1
7.01 c                   if        %found
7.01 c                   eval      c1d1tx = rantxt
7.01 c                   endif
7.01 c                   endif
7.01  * Kundekategori
7.01 c                   if        c1ktyp = '4'
7.01 c                   eval      ra11l1_kkod = %int(%trim(c1kid1))
7.01 c     ra11l1_key    chain     ra11l1
7.01 c                   if        %found
7.01 c                   eval      c1d1tx = raktxt
7.01 c                   endif
7.01 c                   endif
      *
     c                   if        c1kkod = '1'
     c                   eval      c1kktx = 'Utelate omregning av enheter'
     c                   elseif    c1kkod = '2'
7.01 c                   eval      c1kktx = 'Stoppes fra automatisk innlesing'
7.01 c                   elseif    c1kkod = '3'
7.01 c                   eval      c1kktx = 'Utelates fra beløpskontroll'
     c                   else
     c                   eval      c1kttx = *blanks
     c                   endif
      *
     c                   if        b1valg = 3
7.01 c                   eval      c1kmty = k1kmty
     c                   eval      c1ktyp = k1ktyp
     c                   eval      c1kid1 = k1kid1
7.01 c**                 eval      c1kid2 = k1kid2
     c                   eval      c1kkod = k1kkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   lekolur
      *
7.01 c                   eval      lekolu_kmty = c1kmty
     c                   eval      lekolu_ktyp = c1ktyp
     c                   eval      lekolu_kid1 = c1kid1
7.01 c**                 eval      lekolu_kid2 = c1kid2
7.01 c                   eval      lekolu_kid2 = *blanks
     c                   eval      lekolu_kkod = c1kkod
     c     lekolu_key    chain     lekolur
      *
     c                   eval      lekbes = c2kbes
      *
     c                   time                    lekeda
     c                   time                    leketi
     c                   eval      lekeus = l_user
      *
     c                   if        %found
     c                   update    lekolur
     c                   else
     c                   eval      lekfir = w_firm
7.01 c                   eval      lekmty = c1kmty
     c                   eval      lektyp = c1ktyp
     c                   eval      lekid1 = c1kid1
7.01 c**                 eval      lekid2 = c1kid2
7.01 c                   eval      lekid2 = *blanks
     c                   eval      lekkod = c1kkod
     c                   time                    lekoda
     c                   time                    lekoti
     c                   eval      lekous = l_user
     c                   write     lekolur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
7.01 c                   eval      b1kmty = lekmty
     c                   eval      b1ktyp = lektyp
     c                   eval      b1kid1 = lekid1
7.01 c**                 eval      b1kid2 = lekid2
     c                   eval      b1kkod = lekkod
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
7.01 c                   eval      lekol1_kmty = d1kmty
     c                   eval      lekol1_ktyp = d1ktyp
     c                   eval      lekol1_kid1 = d1kid1
7.01 c**                 eval      lekol1_kid2 = d1kid2
7.01 c                   eval      lekol1_kid2 = *blanks
     c                   eval      lekol1_kkod = d1kkod
     c     lekol1_key    chain     lekol1
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
7.01 c                   eval      lekolu_kmty = d1kmty
     c                   eval      lekolu_ktyp = d1ktyp
     c                   eval      lekolu_kid1 = d1kid1
7.01 c**                 eval      lekolu_kid2 = d1kid2
7.01 c                   eval      lekolu_kid2 = *blanks
     c                   eval      lekolu_kkod = d1kkod
     c     lekolu_key    chain     lekolur
7.01 c                   if        %found
     c                   delete    lekolur
7.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Fyller kopierings-bildet
      *
7.01 c                   eval      lekol1_kmty = k1kmty
     c                   eval      lekol1_ktyp = k1ktyp
     c                   eval      lekol1_kid1 = k1kid1
7.01 c**                 eval      lekol1_kid2 = k1kid2
7.01 c                   eval      lekol1_kid2 = *blanks
     c                   eval      lekol1_kkod = k1kkod
     c     lekol1_key    chain     lekol1
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   leavesr
     c                   endsl
      *
     c                   if        k1ktyp = *blanks
     c                   goto      k1taga
     c                   endif
      *
7.01 c**                 if        k1kkod = *blanks
7.01 c**                 goto      k1taga
7.01 c**                 endif
      *
7.01  * Kjør validering
      *
7.01 c                   if        k1kmty <> 'F'
7.01 c                   eval      *in31 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
      *
7.01 c                   if        k1ktyp < '1' or
7.01 c                             k1ktyp > '4'
7.01 c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
7.01  *
7.01 c                   if        k1kkod < '1' or
7.01 c                             k1kkod > '3'
7.01 c                   eval      *in34 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
7.01  * Sjekk identitet
7.01 c                   if        k1ktyp <> *blanks and
7.01 c                             k1kid1 = *blanks
7.01 c                   eval      *in33 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
7.01  * Sjekk leverandør
7.01 c                   if        k1ktyp = '1'
7.01 c                   eval      rlevl1_levr = %int(%trim(k1kid1))
7.01 c     rlevl1_key    chain     rlevl1
7.01 c                   if        not %found
7.01 c                   eval      *in33 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Sjekk kunde
7.01 c                   if        k1ktyp = '2'
7.01 c                   eval      rkunl1_kund = %int(%trim(k1kid1))
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        not %found
7.01 c                   eval      *in33 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Sjekk leverandørlkategori
7.01 c                   if        k1ktyp = '3'
7.01 c                   eval      ra14l1_nkod = %int(%trim(k1kid1))
7.01 c     ra14l1_key    chain     ra14l1
7.01 c                   if        not %found
7.01 c                   eval      *in33 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Sjekk kundekategori
7.01 c                   if        k1ktyp = '4'
7.01 c                   eval      ra11l1_kkod = %int(%trim(k1kid1))
7.01 c     ra11l1_key    chain     ra11l1
7.01 c                   if        not %found
7.01 c                   eval      *in33 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
7.01 c                   endif
7.01  * Ugyldige kombinasjoner
7.01 c                   if        k1ktyp = '1' and
7.01 c                             k1kkod > '2'
7.01 c                   eval      *in35 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
7.01  * Ugyldige kombinasjoner
7.01 c                   if        k1ktyp = '1' and
7.01 c                             k1kkod > '2'
7.01 c                   eval      *in35 = *on
7.01 c                   goto      k1taga
7.01 c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
7.01 c                   eval      c1kmty = k1kmty
     c                   eval      c1ktyp = k1ktyp
     c                   eval      c1kid1 = k1kid1
7.01 c**                 eval      c1kid2 = k1kid2
     c                   eval      c1kkod = k1kkod
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      lekolr
      *
     c                   if        %eof or
     c                             lekfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
7.01 c                   eval      lekol1_kmty = lekmty
     c                   eval      lekol1_ktyp = lektyp
     c                   eval      lekol1_kid1 = lekid1
     c                   eval      lekol1_kid2 = lekid2
     c                   eval      lekol1_kkod = lekkod
      *
     c                   eval      b1valg = 0
7.01 c                   eval      b1kmty = lekmty
     c                   eval      b1ktyp = lektyp
     c                   eval      b1kid1 = lekid1
7.01 c**                 eval      b1kid2 = lekid2
     c                   eval      b1kkod = lekkod
     c                   eval      b1kbes = lekbes
7.01 c                   eval      c1kttx = *blanks
7.01 c                   if        lektyp = '1'
7.01 c                   eval      b1ktxt = 'Leverandør'
7.01 c                   elseif    lektyp = '2'
7.01 c                   eval      b1ktxt = 'Kunde'
7.01 c                   elseif    lektyp = '3'
7.01 c                   eval      b1ktxt = 'Leverandørkategori'
7.01 c                   elseif    lektyp = '4'
7.01 c                   eval      b1ktxt = 'Kundekategori'
7.01 c                   endif
7.01  *
7.01 c                   eval      b1kktx = *blanks
7.01 c                   if        lekkod = '1'
7.01 c                   eval      b1kktx = 'Utelate omregning av enheter'
7.01 c                   elseif    lekkod = '2'
7.01 c                   eval      b1kktx = 'Stoppes fra automatisk innlesing'
7.01 c                   elseif    lekkod = '3'
7.01 c                   eval      b1kktx = 'Utelates fra beløpskontroll'
7.01 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     lekolr_key    setgt     lekolr
     c                   readp     lekolr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     lekolr
      *
     c                   if        %eof or
     c                             lekfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
7.01 c                   eval      lekolr_ktyp = lekmty
     c                   eval      lekolr_ktyp = lektyp
      *
     c                   enddo
      *
     c                   if        %eof
     c     lekolr_key    setll     lekolr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok           begsr
      * Leverandører
     c                   if        c1ktyp = '1'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    w_levr
     c                   parm                    w_lnav
     c                   movel     w_levr        c1kid1
      * Kunder
     c                   elseif    c1ktyp = '2'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_knav
     c                   movel     w_kund        c1kid1
7.01  * Leverandørkategori
7.01 c                   elseif    c1ktyp = '3'
7.01 c                   call      'RA514R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_kkod
7.01 c                   parm                    w_ktxt
7.01 c                   movel     w_kkod        c1kid1
7.01  * Kundekategori
7.01 c                   elseif    c1ktyp = '4'
7.01 c                   call      'RA511R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_kkod
7.01 c                   parm                    w_ktxt
7.01 c                   movel     w_kkod        c1kid1
7.01  *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les på spesialbehandling
      *
     c     lekol1_key    klist
     c                   kfld                    w_firm
7.01 c                   kfld                    lekol1_kmty
     c                   kfld                    lekol1_ktyp
     c                   kfld                    lekol1_kid1
     c                   kfld                    lekol1_kid2
     c                   kfld                    lekol1_kkod
      *
      * Key for les på spesialbehandling
      *
     c     lekolu_key    klist
     c                   kfld                    w_firm
7.01 c                   kfld                    lekolu_kmty
     c                   kfld                    lekolu_ktyp
     c                   kfld                    lekolu_kid1
     c                   kfld                    lekolu_kid2
     c                   kfld                    lekolu_kkod
      *
      * Key for oppdatering av spesialbehandling
      *
     c     lekolr_key    klist
     c                   kfld                    w_firm
7.01 c                   kfld                    lekolr_kmty
     c                   kfld                    lekolr_ktyp
      *
      * Key for oppdatering av spesialbehandling
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
7.01  *
7.01  * Key for oppdatering av spesialbehandling
7.01  *
7.01 c     rkunl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rkunl1_kund
7.01  *
7.01  * Key for oppdatering av kundekategori
7.01  *
7.01 c     ra11l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    ra11l1_kkod
7.01  *
7.01  * Key for oppdatering av levkategori
7.01  *
7.01 c     ra14l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    ra14l1_nkod
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_udate
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
7.01  * Foreløpig kun en gyldig type :-) (F=EDI faktura)
7.01 c                   eval      lekolr_kmty = 'F'
     c                   eval      lekolr_ktyp = *blanks
     c     lekolr_key    setll     lekolr
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
