     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASSTAT
      *  Program . . . . : SX851R
      *  Beskrivelse . . : Oppdaterer skyggetabell (statistikk)  butikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           241014  Nytt program                              BOF
      *           091214  Hvis bong finnes på SSTXPF, så rydd       BOF
      *           050215  Feilretting vaokre                        BOF
6.38  *           250315  Feilretting vedr. kostpris og omregn.     BOF
6.3q  *       260515 bof  Datoformat sql
6.36  *           040915  Styrer 1.gang og resten med parameter     BOF
6.36  *                   Kjører 1.gang på kvelden, så nytt fra morgen
6.37  *           170316  BDSUMR er fjernet, må bruke andre elter   bof
6.38  *           291217  hent lagr/prisgr. send til pris.prog      bof
6.nu  *           180218  Endret ihht. nummerutvidelse              bsa
6.39  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.3a  *           191018  Oppdaterer felter med info om bruk av     bsa
6.3a  *                   BM klubb kuponger.                        bsa
6.3b  * 6.30      230819  sfkpri tas alltid fra bdkpri (ordrelinja) bof
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
     fsstxlb    if   e           k disk    rename(sstxpfr:sstxlbr)
     fsstxlp    if   e           k disk    rename(sstxpfr:sstxlpr)
     fsstxlo    uf   e           k disk    rename(sstxpfr:sstxlor)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
5.24 fvvgkl1    if   e           k disk    rename(vvgkpfr:vvgkl1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     frmbtlr    if   e           k disk    rename(rmbtpfr:rmbtlrr)
6.34 fbokkl1    if   e           k disk    rename(bokkpfr:bokkl1r)
6.39 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
6.3a fbckuiu    uf   e           k disk    rename(bckust:bckuiur)
     fsstxpf    o  a e             disk
      *
      * Definering av array
      *
     d a_aarr          s              4  0 dim(5000)
     d a_wsid          s             10    dim(5000)
     d a_bong          s             12    dim(5000)
      *
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d sstxlb_tmsb     s                   like(sftmsb)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
     d bohel1_aarr     s                   like(boaarr)
     d bohel1_wsid     s                   like(bowsid)
     d bohel1_bong     s                   like(bobong)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_wsid     s                   like(bdwsid)
     d bodtl1_bong     s                   like(bdbong)
     d rkunl1_kund     s                   like(rkkund)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
5.24 d vvgkl1_ogrp     s                   like(vkogrp)
5.24 d vvgkl1_hgrp     s                   like(vkhgrp)
5.24 d vvgkl1_ugrp     s                   like(vkugrp)
5.24 d vvgkl1_ldor     s                   like(vkldor)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rmbtlr_tbrt     s                   like(rmtbrt)
     d rmbtlr_talm     s                   like(rmtalm)
6.34 d bokkl1_bong     s                   like(brbong)
     d sstxlp_paar     s                   like(sfpaar)
     d sstxlp_wsid     s                   like(sfwsid)
     d sstxlp_bong     s                   like(sfbong)
     d sstxlp_line     s                   like(sfline)
     d sstxlo_paar     s                   like(sfpaar)
     d sstxlo_ornr     s                   like(sfornr)
     d sstxlo_orsu     s                   like(sforsu)
6.3a d bckuiu_bong     s                   like(sfbong)
6.3a d bckuiu_line     s                   like(sfline)
      *
      * Definering av variable
      *
5.32 d w_saar          s              4  0
5.32 d w_suke          s              2  0
6.14 d w_vtyp          s                   like(vvvtyp)
     d w_firm          s                   like(sffirm)
     d w_edat          s                   like(boedat)
     d w_etim          s                   like(boetim)
     d w_fdat          s                   like(bodate)
     d w_tmsb          s                   like(sftmsb)
6.38 d w_prgr          s                   like(sfprgr)
     d w_tmsbx         s             26
     d w_timex         s              8
3.10 d w_vlin          s              1
6.34 d i               s              4  0
      *
     d w_omrs          s                   like(veomre)
     d w_omrl          s                   like(veomre)
5.63 d w_omrm          s                   like(veomre)
5.20 d w_lety          s                   like(bdlety)
5.38 d w_onum          s                   like(vaonum)
5.39 d w_pran          s              4
5.62 d w_trek          s             11  2
5.60  *
5.60 d s_rabo          s             11  2
5.60 d s_rab1          s             11  2
5.60 d s_rab2          s             11  2
      *
6.34 d w_pkon          s              3
6.34 d w_sumn          s             11  2
6.34 d w_suno          s             11  2
6.34 d w_ntpr          s             11  2
6.34 d w_sumi          s             11  2
6.34 d w_sumo          s             11  2
6.34 d w_suoi          s             11  2
6.34 d w_sumk          s             11  2
6.34 d w_suok          s             11  2
6.34 d w_diff          s             11  2
     d w_rabo          s             11  2
6.34 d w_pkns          s              3
6.34 d w_pkko          s              3
6.34 d w_pkok          s              3
6.34 d w_pkin          s              3
6.34 d w_pkoi          s              3
6.34 d w_kofa          s              1
6.34 d w_card          s              2
6.34 d w_fcar          s              2
6.34 d w_kild          s              3
6.34 d w_soda          s                   like(sfsoda)
6.34 d w_soti          s                   like(sfsoti)
6.28 d w_udat          s               d   datfmt(*iso)
6.28 d w_ddat          s               d   datfmt(*iso)
6.28 d w_ldor          s                   like(vvldor)
6.39 d w_lv_nobb       s                   like(vvlnob)
6.39 d w_lv_modn       s                   like(vvlmod)
6.39 d w_lv_lldo       s                   like(vvlnle)
6.39 d w_lv_llva       s                   like(vvllva)
     d w_belb          s             11  2
     d w_beln          s             12  3
     d w_belo          s             11  2
     d w_rkro_stk      s             11  2
     d w_fkda          s              6  0
     d w_peri          s              4  0
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_numm          s              6  0
     d w_type          s              2
5.40 d w_mvag          s             11  2
     d w_dbkr          s              1
     d w_valg          s              1  0
     d w_gebm          s             13  4
     d w_geby          s             11  2
     d w_geb0          s             11  2
     d w_geb1          s             11  2
     d w_geb2          s             11  2
     d w_geb3          s             11  2
     d w_anta          s                   like(bdanta)
     d w_tots          s             11  2
     d w_sums          s             12  3
     d w_sumb          s             11  2
     d w_sumg          s             11  2
     d w_suko          s             11  2
     d w_avde          s              4  0
     d w_kont          s              6  0
     d w_spes          s              4  0
     d w_mkod          s              1
     d w_nega          s              1
     d w_rab1          s             11  2
     d w_rab2          s             11  2
     d w_algr          s             11  2
     d w_alm1          s             11  2
     d w_alm2          s             11  2
     d w_snum          s             12
     d w_belp          s             11  2
     d w_mvab          s             11  2
     d w_gkbe          s             11  2
     d w_gkne          s             11  2
     d w_bila          s              6  0
     d w_bili          s              4  0
     d w_bunt          s              5  0
     d w_ffda          s              6  0
     d w_kkod          s              1
     d w_bkod          s              2  0
      *
     d w_eaar          s              2  0
     d w_emnd          s              2  0
5.20 d w_hele          s            128
     d w_hgrp          s              2  0
     d w_ogrp          s              2  0
     d w_otyp          s              2
     d w_qmem          s              1
     d w_sald          s             11  2
     d w_retk          s              1
     d w_ugrp          s              3  0
     d w_vare          s             15
     d w_atxt          s             20
5.04 d w_kidr          s              6  0
5.04 d w_ksif          s              1  0
5.04 d w_kkus          s             24
5.04 d w_kkid          s             25
5.05 d w_line          s              4  0
5.49 d w_kki2          s             25
      *
5.31 d w_stat          s              1
5.31 d w_mvak          s              1
5.31 d w_mvtx          s             30
5.31 d w_bpro          s              5  2
5.31 d w_dato          s               d   datfmt(*iso)
5.31 d w_mvap          s              6  2
5.31 d w_mvat          s              5  4
5.31 d w_mvaf          s             10  9
5.70 d p_vtyp          s                   like(vvvtyp)
6.28 d b_inlr          s              1    inz(*off)
     d b_xeof          s              1    inz(*off)
     d b_stat          s              1    inz(*off)
6.36  *
6.36 d p_first         s              1
6.34  *
6.34  * Datastruktur parameterliste -inn
6.34  *
6.34 d                 ds
6.34 d hirec                        120
6.34 d  hifirm                        3  0 overlay(hirec)
6.34 d  hirkat                        3  0 overlay(hirec:4)
6.34 d  hikund                        6  0 overlay(hirec:7)
6.34 d  hikpro                        6  0 overlay(hirec:13)
6.34 d  hivare                       15    overlay(hirec:19)
6.34 d  hilety                        1  0 overlay(hirec:34)
6.34 d  hienhe                        3    overlay(hirec:35)
6.34 d  hidato                         d   overlay(hirec:38) datfmt(*iso)
6.34 d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
8.01 d  hiinpr                        9  2 overlay(hirec:100)
6.34 d  hiinlr                        1    overlay(hirec:120)
6.34  *
6.34  * Datastruktur parameterliste -ut
6.34  *
6.34 d                 ds
6.34 d horec                        100
6.34 d  holety                        1  0 overlay(horec)
6.34 d  hokpri                        1    overlay(horec:2)
6.34 d  hooppr                        9  2 overlay(horec:3)
6.34 d  hosapr                        9  2 overlay(horec:12)
6.34 d  hokopr                        9  2 overlay(horec:21)
6.34 d  horab1                        5  2 overlay(horec:30)
6.34 d  horab2                        5  2 overlay(horec:35)
6.34 d  hodekn                        5  2 overlay(horec:40)
6.34 d  hopros                        5  2 overlay(horec:45)
6.34 d  hokamp                        5    overlay(horec:50)
6.34 d  hoalme                        4  0 overlay(horec:55)
6.34 d  hobrty                        1  0 overlay(horec:59)
6.34 d  hobrr1                        5  2 overlay(horec:60)
6.34 d  hobrr2                        5  2 overlay(horec:65)
6.34 d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
8.01 d  hoinpr                        9  2 overlay(horec:73)
8.01 d  hopkon                        3    overlay(horec:82)
8.01 d  hopkns                        3    overlay(horec:85)
8.01 d  hopkko                        3    overlay(horec:88)
8.01 d  hopkok                        3    overlay(horec:91)
8.01 d  hopkoi                        3    overlay(horec:94)
6.34  *
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
5.20 d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
4.02 d  d_ksp1                        4  0 overlay(d_hele:11)
4.02 d  d_kav2                        4  0 overlay(d_hele:15)
4.02 d  d_kko2                        6  0 overlay(d_hele:19)
4.02 d  d_ksp2                        4  0 overlay(d_hele:25)
4.02 d  d_kav3                        4  0 overlay(d_hele:29)
4.02 d  d_kko3                        6  0 overlay(d_hele:33)
4.02 d  d_ksp3                        4  0 overlay(d_hele:39)
4.02 d  d_kav4                        4  0 overlay(d_hele:43)
4.02 d  d_kko4                        6  0 overlay(d_hele:47)
4.02 d  d_ksp4                        4  0 overlay(d_hele:53)
4.02 d  d_kav5                        4  0 overlay(d_hele:57)
4.02 d  d_kko5                        6  0 overlay(d_hele:61)
4.02 d  d_ksp5                        4  0 overlay(d_hele:67)
4.02 d  d_kav6                        4  0 overlay(d_hele:71)
4.02 d  d_kko6                        6  0 overlay(d_hele:75)
4.02 d  d_ksp6                        4  0 overlay(d_hele:81)
4.02 d  d_kav7                        4  0 overlay(d_hele:85)
4.02 d  d_kko7                        6  0 overlay(d_hele:89)
4.02 d  d_ksp7                        4  0 overlay(d_hele:95)
4.02 d  d_khev                        4    overlay(d_hele:99)
5.20 d  d_nivo                        1    overlay(d_hele:103)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
     c                   time                    w_ddat
     c                   time                    w_fdat
     c     w_fdat        subdur    14:*days      w_fdat
5.24  *
     c                   eval      w_tmsb = *loval
      * finn siste timestamp
     c                   eval      sstxlb_tmsb = *hival
     c                   read      sstxlb
     c                   dow       not %eof(sstxlb)
     c                   eval      w_tmsb = sftmsb
     c                   leave
     c                   enddo
      *
     c                   if        p_first = '1'
     c                   exsr      dann_total
     c                   goto      end_pgm
     c                   endif
      *
     c                   exsr      finn_ordre
     c                   exsr      dann_nye
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
     c                   eval      hiinlr = *off
6.34 c                   eval      hifirm = w_firm
6.34 c                   eval      hirkat = 0
6.34 c                   eval      hikund = 0
6.34 c                   eval      hikpro = 0
6.34 c                   eval      hivare = *blank
     c                   eval      hienhe = *blank
     c                   eval      hilety = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
6.34 c                   eval      hidato = *loval
6.34 c                   eval      hitime = *loval
6.34  *
6.34  *
6.34 c                   call      'VP905R'
6.34 c                   parm                    hirec
6.34 c                   parm                    horec
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne total skyggetabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_total    begsr
      *
      *
     c/exec sql
     c+                  declare sok_b cursor for
     c+                  select boaarr,
     c+                         bowsid,
     c+                         bobong
     c+                  from   bohepf
     c+                  where  bofirm = :w_firm and
     c+                         bodate >= :w_fdat and
     c+                         bokavs = 0
     c+                  order by boaarr, bowsid,bobong
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_b
     c/end-exec
     c/exec sql
     c+                  open sok_b
     c/end-exec
      *
      *
      *
     c                   dou       *in15 = *on
      *
      *  Leser alle ordre etter opprettet et bestemt tidspunkt
     c     fetch_b_tot   tag
     c/exec sql
     c+                  fetch next
     c+                  from  sok_b
     c+                  into  :boaarr,
     c+                        :bowsid,
     c+                        :bobong
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   iter
     c                   endif
      *
      * les av bong-heading
     c                   eval      bohel1_aarr = boaarr
     c                   eval      bohel1_wsid = bowsid
     c                   eval      bohel1_bong = bobong
     c     bohel1_key2   chain     bohel1
     c                   if        %found(bohel1)
      *
      * tar bonger intil 30 dager gamle og at de ikke er overført statistikk
      *
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r
     c                   eval      rkunl1_kund = bokund
     c     rkunl1_key    chain     rkunl1r
      *
     c                   eval      sstxlo_paar = %subdt(w_ddat:*years)
     c                   eval      sstxlo_ornr = boornr
     c                   eval      sstxlo_orsu = boorsu
     c                   exsr      sjekk_ordre
      *
     c                   if        b_stat = *off
     c                   exsr      dann_stat
     c                   endif
      *
     c                   endif
      *
      *  Velg neste Vare-register post
      *
     c     neste_b_tot   tag
     c                   goto      fetch_b_tot
      *
     c                   enddo
      *
      *
      * Lukker cursor
      *
     c/exec sql
     c+ close sok_b
     c/end-exec
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne total skyggetabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ordre    begsr
      *
      *  Rydder i ordretabell
     c                   eval      a_aarr(*) = 0
     c                   eval      a_wsid(*) = *blank
     c                   eval      a_bong(*) = *blank
     c                   eval      i = 0
      *
      *
      * Hvis forrige trans er dagen før så sett tidspunkt til dagens dato
      * og lavest mulig klokkeslett
      *
     c                   eval      w_edat = %date(w_tmsb)
     c                   if        w_edat <> w_ddat
     c                   time                    w_edat
     c                   eval      w_etim = *loval
     c                   else
     c                   move      w_tmsb        w_tmsbx
     c                   eval      w_timex = %subst(w_tmsbx:12:8)
     c                   move      w_timex       w_etim
     c                   endif
     c                   eval      *in15 = *off
      *
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select boaarr,
     c+                         bowsid,
     c+                         bobong
     c+                  from   bohepf
     c+                  where  bofirm = :w_firm and
     c+                         boedat >= :w_edat and
     c+                         boetim >= :w_etim  and
     c+                         bokavs = 0
     c+                  order by boaarr, bowsid,bobong
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *
      *
      *
     c                   dou       *in15 = *on or
     c                             i = 4999
      *
      *  Leser alle ordre etter opprettet et bestemt tidspunkt
     c     fetch_bong    tag
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :boaarr,
     c+                        :bowsid,
     c+                        :bobong
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   iter
     c                   endif
      *
      * legger ordre i tabell
     c                   eval      i = i + 1
     c                   eval      a_aarr(i)  =  boaarr
     c                   eval      a_wsid(i)  =  bowsid
     c                   eval      a_bong(i)  =  bobong
      *
      *  Velg neste Vare-register post
      *
     c     neste_bong    tag
     c                   goto      fetch_bong
      *
     c                   enddo
      *
      * Lukker cursor
      *
     c/exec sql
     c+ close sok_o
     c/end-exec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne total skyggetabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_nye      begsr
      *
     c                   eval      i = 1
     c                   dow       i <  5000
     c                   eval      bohel1_aarr = a_aarr(i)
     c                   eval      bohel1_wsid = a_wsid(i)
     c                   eval      bohel1_bong = a_bong(i)
     c     bohel1_key2   chain     bohel1
     c                   if        %found(bohel1)
     c                   eval      votyl1_otyp = bootyp
     c     votyl1_key    chain     votyl1r
     c                   eval      rkunl1_kund = bokund
     c     rkunl1_key    chain     rkunl1r
      *
     c                   eval      sstxlo_paar = %subdt(w_ddat:*years)
     c                   eval      sstxlo_ornr = boornr
     c                   eval      sstxlo_orsu = boorsu
     c                   exsr      sjekk_ordre
      *
     c                   if        b_stat = *off
     c                   exsr      dann_stat
     c                   endif
     c                   endif
     c                   eval      i = i + 1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne en og en ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_stat     begsr
      *
      * sjekker om statistikk finnes
      *
      *
     c                   eval      sstxlp_paar = %subdt(w_ddat:*years)
     c                   eval      sstxlp_wsid = bowsid
     c                   eval      sstxlp_bong = bobong
     c                   eval      sstxlp_line = bdline
     c     sstxlp_key    chain     sstxlp
     c                   if        %found(sstxlp)
     c                   leavesr
     c                   endif
      *
     c                   eval      w_beln = 0
     c                   eval      w_belo = 0
     c                   eval      w_belb = 0
      *
     c                   eval      bodtl1_aarr = boaarr
     c                   eval      bodtl1_wsid = bowsid
     c                   eval      bodtl1_bong = bobong
     c     bodtl1_key    setll     bodtl1
     c     bodtl1_key    reade     bodtl1
     c                   dow       not %eof(bodtl1)
      *
     c                   eval      vltyl1_ltyp = bdltyp
     c     vltyl1_key    chain     vltyl1r                            90
      *
     c                   if        valktx = 0
6.35  *
      * bdsums er lagret negativt, hvis linje er negativ.
      * skifter fortegn her
6.35 c                   if        valkre = '-'
6.35 c                   eval      bdsums = bdsums * -1
6.35 c                   endif
      *
      *
     c                   eval      vvarl1_vare = bdvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found(vvarl1)
6.34  *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1
      *
     c                   else
     c                   eval      vugrl1_uogr = bdogrp
     c                   eval      vugrl1_uhgr = bdhgrp
     c                   eval      vugrl1_uugr = bdugrp
     c     vugrl1_key    chain     vugrl1
     c                   endif
      *
      * Bruttobeløp og kostpris
      *
6.34 c                   exsr      bong_linje
6.34 c                   exsr      utvid_stat
      *
     c                   clear                   sstxpfr
     c                   eval      sffirm = w_firm
     c                   eval      sfpaar = %subdt(w_ddat:*years)
     c                   eval      sfpmnd = %subdt(w_ddat:*months)
      *
     c                   eval      sfvkun = bolkun
     c                   eval      sffkun = bokund
     c                   if        bolkun = 0
     c                   eval      sfvkun = bokund
     c                   endif
      *
     c                   eval      sfkpro = bokpro
     c                   eval      sfkkat = rkkatg
     c                   eval      sfrkat = borkat
     c                   eval      sfselg = boselg
     c                   eval      sfland = 0
     c                   eval      sfdist = bodist
     c                   eval      sffrik = rkfrie
3.11 c                   eval      sflety = bdlety
6.2d c                   eval      sflvre = bdlvre
     c                   eval      sfvare = bdvare
     c                   movel     bdtek1        sftek1
     c                   eval      sfogrp = vvogrp
     c                   eval      sfhgrp = vvhgrp
     c                   eval      sfugrp = vvugrp
     c                   eval      sfrgrp = 0
6.3b c*                  eval      sfkpri = vvkpri
6.3b c                   eval      sfkpri = bdkpri
     c                   eval      sfoakk = vaoakk
5.70  *
5.70  * Kaller program for varetype, utg.dato og lev.nr.
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    bolage
5.70 c                   parm                    p_vtyp
6.28 c                   parm                    w_udat
6.28 c                   parm                    w_ldor
6.28 c                   parm                    b_inlr
6.39 c                   parm                    w_lv_nobb
6.39 c                   parm                    w_lv_modn
6.39 c                   parm                    w_lv_lldo
6.39 c                   parm                    w_lv_llva
5.70  *
5.70 c                   eval      sfvtyp = p_vtyp
     c                   eval      sfldor = vvldor
6.39 c                   if        w_lv_nobb > 0
6.39 c                   eval      sflvar = w_lv_llva
6.39 c                   eval      sfnobb = w_lv_nobb
6.39 c                   eval      sfmodn = w_lv_modn
6.39 c                   else
     c                   eval      sflvar = vvlvar
     c                   eval      sfnobb = vvnobb
     c                   eval      sfmodn = vvnmod
6.39 c                   endif
     c                   eval      sfahgr = vvahgr
     c                   eval      sfaugr = vvaugr
     c                   eval      sfkamp = bdkamp
5.32 c                   eval      sflage = bdlage
     c                   eval      sfbinr = 0
5.05 c                   eval      sfline = 0
     c                   eval      sfbida = bofkda
     c                   eval      sfotyp = bootyp
     c                   eval      sfornr = boornr
     c                   eval      sforsu = boorsu
6.3a c**                 eval      sforli = bdline
6.3a c                   eval      sforli = bdorli
     c                   eval      sforda = bobdat
5.39 c                   eval      sfbong = bobong
     c                   eval      sfsavd = w_avde
     c                   eval      sfskon = w_kont
     c                   eval      sfssps = w_spes
     c                   eval      sfanto = bdanta
     c                   eval      sfanta = w_anta
     c                   eval      sffakr = *blank
     c                   eval      sfsumo = w_sumo
     c                   eval      sfsumb = w_sumb
     c                   eval      sfsumk = w_sumk
     c                   eval      sfrab1 = w_rab1
     c                   eval      sfrab2 = w_rab2
     c                   eval      sfrabo = w_rabo
     c                   eval      sfkmva = bomkod
     c                   eval      sfkres = 0
     c                   eval      sfenhe = vvenhs
     c                   eval      sfenho = bdenh1
5.01 c                   eval      sfnref = *blank
5.21 c                   eval      sffsys = 'KAS'
5.02 c                   eval      sfbenr = bdbenr
5.02 c                   eval      sfbsuf = bdbesu
6.2f c                   eval      sfblin = bdbeli
5.03 c                   eval      sflmat = bolmat
5.31 c                   eval      sfalme = bdalme
5.31 c                   eval      sfbrty = bdbrty
5.31 c                   eval      sfpriv = bdpriv
5.31 c                   eval      sfomva = bdkmva
5.31 c                   eval      sfbrk1 = w_alm1
5.31 c                   eval      sfbrk2 = w_alm2
5.31 c                   eval      sfuser = bouser
     c                   eval      sfwsid = bowsid
5.64 c                   eval      sfkhev = d_khev
5.65 c                   eval      sfeann = 0
6.38 c                   eval      sfprgr = hoprgr
5.70 c                   eval      sfoprg = 0
6.31 c                   eval      sfproj = boproj
6.31 c                   eval      sfakti = boakti
6.34  *
6.34  * utvidet statistikk
6.34  *
6.34 c                   eval      sfpkon = w_pkon
6.34 c                   eval      sfpkns = w_pkns
6.34 c                   eval      sfpkko = w_pkko
6.34 c                   eval      sfpkok = w_pkok
6.34 c                   eval      sfpkoi = w_pkoi
6.34 c                   eval      sfpkin = w_pkin
6.34 c                   eval      sfkofa = w_kofa
6.34 c                   eval      sfcard = w_card
6.34 c                   eval      sffcar = w_fcar
6.34 c                   eval      sfkild = w_kild
      *
6.34 c                   eval      sfsuno = w_suno
6.34 c                   eval      sfsuok = w_suok
6.34 c                   eval      sfsumn = w_sumn
6.34 c                   eval      sfsumi = w_sumi
6.34 c                   eval      sfsuoi = w_suoi
      *
6.34 c                   eval      sfsoda = w_soda
6.34 c                   eval      sfsoti = w_soti
3.10  *
3.10  * Overstyring av felter, dersom varelinjen
3.10  * er en vare hentet fra LVR
3.10  *
3.10 c                   if        bdlvre =  1
3.10 c                   eval      sfogrp = bdogrp
3.10 c                   eval      sfhgrp = bdhgrp
3.10 c                   eval      sfugrp = bdugrp
6.39 c                   if        w_lv_nobb > 0
6.39 c                   eval      sflvar = w_lv_llva
6.39 c                   eval      sfnobb = w_lv_nobb
6.39 c                   else
3.10 c                   eval      sflvar = bdlvar
5.37 c                   eval      sfnobb = bdnobb
6.39 c                   endif
3.10 c                   eval      sfkpri = bdkpri
3.10 c                   eval      sfvtyp = 'S'
3.10 c                   eval      sfldor = bdldor
3.10 c***                eval      sfmodn = 0
3.10 c                   eval      sfenhe = bdenh1
3.10 c                   eval      sfenho = bdenh1
     c                   endif
      *
      * Finner alternativ varegruppe
      *
     c                   if        b_xeof = *on and
     c                             sfahgr = 0
     c                   exsr      finn_avgr
     c                   endif
      *
5.39  *
5.39  * Henter inn produktansvarlig
5.39  *
5.39 c                   call      'VG710R'
5.39 c                   parm                    w_firm
5.39 c                   parm                    sfogrp
5.39 c                   parm                    sfhgrp
5.39 c                   parm                    sfugrp
5.39 c                   parm                    w_pran
5.39  *
5.39 c                   eval      sfpran = w_pran
5.43  *
6.29  * Oppdaterer leveringsdato, pakkseddelsdato  og uke
5.43  *
6.29 c                   eval      sfldat =bodate
6.29 c                   eval      sfpdat =bodate
6.29  *
6.29 c                   eval      sfpada =bodate
5.43  *
5.43 c                   if        sfldat <> *loval
5.43 c                   eval      w_dato = sfldat
5.43 c                   else
5.43 c                   eval      w_dato = sfbida
5.43 c                   endif
5.43  *
5.43 c                   call      'AX711R'
5.43 c                   parm                    w_dato
5.43 c                   parm                    w_saar
5.43 c                   parm                    w_suke
5.43 c                   parm                    w_stat
5.43  *
5.43 c                   if        w_suke > 52
5.43 c                   eval      w_suke = 52
5.43 c                   endif
5.47 c                   eval      sfpuke = w_suke
      *
     c                   time                    sfdate
     c                   time                    sftime
6.10 c                   eval      sfuser = l_user
     c                   time                    sftmsb
6.3a  * Er dette ei linje kjøpt med kupong ??
6.3a c                   eval      bckuiu_bong = bdbong
6.3a c                   eval      bckuiu_line = bdline
6.3a c     bckuiu_key    chain     bckuiur
6.3a c                   if        %found
6.3a c**                 if        klstat = 0
6.3a c                   eval      sfsuno = bdsapr
6.3a c                   eval      sfpkon = 'KUP'
6.3a c                   eval      sfpkns = 'KUP'
6.3a c                   eval      sfkamp = 'KUP'
6.3a c                   eval      klstat = 2
6.3a c                   update    bckuiur
6.3a c**                 endif
6.3a c                   endif
      *
     c                   write     sstxpfr
      *
     c                   endif
5.24  *
     c     bodtl1_key    reade     bodtl1
     c                   enddo
      *
     c                   endsr
6.34  *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Henter utvidet statstikk-data
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 csr   utvid_stat    begsr
6.34  *
6.34 c                   eval      w_pkon = *blank
6.34 c                   eval      w_pkns = *blank
6.34 c                   eval      w_pkko = *blank
6.34 c                   eval      w_pkok = *blank
6.34 c                   eval      w_pkin = *blank
6.34 c                   eval      w_pkoi = *blank
6.34 c                   eval      w_kofa = *blank
6.34 c                   eval      w_card = *blank
6.34 c                   eval      w_fcar = *blank
6.34 c                   eval      w_kild = *blank
6.34 c                   eval      w_sumn = 0
6.34 c                   eval      w_sumi = 0
6.34 c                   eval      w_suoi = 0
6.34 c                   eval      w_sumo = 0
6.34 c                   eval      w_suno = 0
6.34 c                   eval      w_suok = 0
6.34 c                   eval      w_suoi = 0
6.34 c                   eval      w_ntpr = 0
6.34 c                   eval      w_rkro_stk = 0
6.34  *
6.34 c                   eval      hifirm = w_firm
6.34 c                   eval      hirkat = 0
6.34  *
6.34 c                   eval      hikund = bokund
6.34 c                   if        bolkun <> 0
6.34 c                   eval      hikund =bolkun
6.34 c                   endif
6.34  *
6.34 c                   eval      hikpro = bokpro
6.34 c                   eval      hivare = bdvare
     c                   eval      hienhe = bdenh1
     c                   eval      hilety = bdlety
     c                   eval      hidekn = 0
     c                   eval      hiavgf = bomkod
     c                   eval      hipriv = 0
6.34 c                   eval      hidato = bobdat
6.34 c                   if        bobdat = bodate
6.34 c                   eval      hitime = botime
6.34 c                   else
6.34 c                   eval      hitime = *loval
6.34 c                   endif
6.34 c                   eval      w_soda = bodate
6.34 c                   eval      w_soti = botime
6.34 c                   eval      hinumm = bopord
6.34 c                   eval      hikode = *blank
6.34 c                   eval      hiskaf = *off
6.34 c                   eval      hildor = bdldor
6.38 c                   eval      hiprgr = w_prgr
6.34 c                   eval      hisapr = 0
6.34 c                   eval      hikopr = 0
6.34 c                   eval      hiinpr = 0
     c                   eval      hiinlr = *on
6.34  *
6.34 c                   call      'VP905R'
6.34 c                   parm                    hirec
6.34 c                   parm                    horec
6.34  *
6.34  * Koder fra VP905
6.34  *
6.34 c                   eval      w_pkon = hopkon
6.34 c                   eval      w_pkns = hopkns
6.34 c                   eval      w_pkko = hopkko
6.34 c                   eval      w_pkok = hopkok
6.34 c                   eval      w_pkoi = hopkoi
6.34  *
6.34  * Sum opprinnelig netto salgspris (SFSUMO)
6.34  *
6.34 c                   eval      w_ntpr = hosapr
6.34  *
6.34 c                   if        horab1 > 0
6.34 c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab1 / 100)
6.34 c                   endif
6.34 c                   if        horab2 > 0
6.34 c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab2 / 100)
6.34 c                   endif
      *
      * Varelinjens andel av ordrerabatt (pr. stk)
      *
     c                   if        bdorab <> 0
     c                   eval(h)   w_rkro_stk = (w_ntpr * bdorab) / 100
     c                   endif
      * trekkker fra ordrerabatt
     c                   eval      w_ntpr = w_ntpr - w_rkro_stk
      *
6.35 c                   eval(h)   w_suno = w_ntpr * w_anta
6.34  *
6.34  * Sum netto salg salgspris (SFSUMN)
6.34  *
6.37 c                   eval(h)   w_sumn = bdsums - bdrbe1
6.37 c                                             - bdrbe2
6.37 c                                             - bdorbe
6.34  *
6.34  * Priskode netto salgspris  (SFPKNS)
6.34  *
     c                   if        w_sumn <> 0
6.34 c                   eval      w_diff  = %abs(w_suno - w_sumn)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkns  = 'MAN'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Sum  kostpris (SFSUMK)
6.34  *
6.38 c*                  eval      w_sumk = bdkopr * w_anta
6.38 c                   eval      w_sumk = bdkopr * bdanta
6.34  *
6.34  * Sum opprinnelig kostpris (SFSUOK)
6.34  *
6.35 c                   eval      w_suok = hokopr * w_anta
6.34  *
6.34  * Priskode netto kostpris  (SFPKKO)
6.34  *
     c                   if        w_suok <> 0
6.34 c                   eval      w_diff  = %abs(w_sumk - w_suok)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkko  = 'MAN'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Sum  innkjøps (SFSUMI)
6.34  *
6.35 c                   eval      w_sumi = hoinpr * w_anta
6.34  *
6.34  * Sum  opprinnelig innkjøps (SFSUOI)
6.34  *
6.35 c                   eval      w_suoi = hoinpr * w_anta
6.34  *
6.34  * Priskode netto kostpris  (SFPKIN)
6.34  *
6.34 c                   if        hoinpr <> 0
6.34 c                   eval      w_diff  = %abs(w_sumi - w_suoi)
6.34 c                   if        w_diff > 1
6.34 c                   eval      w_pkin  = 'XXX'
6.34 c                   endif
6.34 c                   endif
6.34  *
6.34  * Kilde Salg      (SFKILD)
6.34  *
6.34 c                   eval      w_kild = 'KAS'
6.34  *
6.34  * BM Card (SFCARD)
6.34  *
6.34 c                   eval      bokkl1_bong = bobong
6.34 c     bokkl1_key    setll     bokkl1
6.34 c     bokkl1_key    reade     bokkl1
6.34 c                   dow       not %eof(bokkl1)
6.34 c                   if        brktyp <> 0 and
6.34 c                             brktyp =  5
6.34 c                   eval      w_card = '05'
6.34 c                   endif
6.34 c                   if        brktyp <> 0 and
6.34 c                             brktyp =  6
6.34 c                   eval      w_fcar = '06'
6.34 c                   endif
6.23 c     bokkl1_key    reade     bokkl1
6.34 c                   enddo
6.34  *
6.34  *
6.34  * Kontakt/fakturert (SFKOFA)
6.34  *
6.34 c                   if        bootyp = 'CD' or
6.34 c                             bootyp = 'CK'
6.34 c                   eval      w_kofa = 'K'
6.34 c                   else
6.34 c                   eval      w_kofa = 'F'
6.34 c                   endif
6.34  *
6.34  * Behandle diverse vare
6.34  *
     c                   if        %found(vvarl1) and
     c                             vvvtyp = 'D'
     c                   eval      w_suno = 0
     c                   eval      w_pkon = *blank
     c                   eval      w_pkns = 'DIV'
     c                   eval      w_suok = 0
     c                   eval      w_pkok = *blank
     c                   eval      w_pkko = 'DIV'
     c                   endif
6.34  *
6.34 csr                 endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandling av bong linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bong_linje    begsr
      *
      * Nullstiller totaler for utskrift
      *
     c                   eval      w_anta = 0
     c                   eval      w_sums = 0
     c                   eval      w_sumo = 0
     c                   eval      w_sumb = 0
     c                   eval      w_rab1 = 0
     c                   eval      w_rab2 = 0
     c                   eval      w_rabo = 0
     c                   eval      w_sumk = 0
     c                   eval      w_suko = 0
     c                   eval      w_mvag = 0
5.60 c                   eval      w_alm1 = 0
5.60 c                   eval      w_alm2 = 0
5.60 c                   eval      s_rab1 = 0
5.60 c                   eval      s_rab2 = 0
5.60 c                   eval      s_rabo = 0
      *
     c                   eval      w_kkod = *blank
      *
      * Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = bdvare
     c     vvarl1_key    chain     vvarl1r                            90
      *
      * Hent opplysninger fra undergruppe-register
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1r                            90
6.14  *
6.14  * Hente varetype
6.14  *
6.14 c                   call      'VL710R'
6.14 c                   parm                    w_firm
6.14 c                   parm                    bdvare
6.14 c                   parm                    bolage
6.14 c                   parm                    w_vtyp
6.20 c                   parm                    w_udat
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.39 c                   parm                    w_lv_nobb
6.39 c                   parm                    w_lv_modn
6.39 c                   parm                    w_lv_lldo
6.39 c                   parm                    w_lv_llva
6.38  *
6.38  * henter prisgruppe
6.38  *
6.38 c                   eval                    w_prgr = *blank
6.38 c                   call      'VL712R   '
6.38 c                   parm                    w_firm
6.38 c                   parm                    bdlage
6.38 c                   parm                    bdavde
6.38 c                   parm                    w_prgr
      *
      * Finner den kontoen som skal benyttes
      *
     c                   exsr      hntkon
      *
 6.11 * Bestemmer MVA-behandling  (MVA-fri transaksjon?)
 6.11 *
 6.11c                   if        bomkod = 1
 6.11c                   eval      w_valg = 4
 6.11c                   eval      bdkmva = '4'
 6.11c                   endif
      *
     c                   eval      w_mkod = bdkmva
      *
      * Sjekker om gavekort håndtering
      *
     c                   if        bdvare = baagva
     c                   eval      w_valg = 5
     c                   endif
      *
      * Legger inn inntastet selvkost dersom overstyrt
      *
6.14 c                   if        bdselk <> 0 and
6.14 c                             w_vtyp <> 'D'
     c                   eval      bdkopr = bdselk
     c                   endif
      *
      * Omregning av enhet til statistikk.....
      * ----------------------------------------
      * Dersom stat.enhet ikke finnes på vare, hentes stat.enhet fra
      * undergruppe. Dersom stat.enhet ikke finnes på undergruppe, settes
      * stat.enhet lik solgt enhet
      *
     c                   if        vvenhs = *blank
     c                   eval      vvenhs = vguens
     c                   endif
      *
     c                   if        vvenhs = *blank
     c                   eval      vvenhs = bdenh1
     c                   endif
      *
      * Kaller opp subrutine for omregning av antall til statistikkenhet
      *
     c                   eval      w_anta = bdanta
     c                   exsr      omr_antall
      *
     c                   eval      w_sumo = bdoppr * bdanta
     c                   eval      w_sumb = bdsapr * bdanta
     c                   eval      w_sumk = bdkopr * bdanta
     c                   eval      w_suko = bdselk * bdanta
     c                   eval      w_sumg = bdsapr * bdanta
      *
      * Beregner rabattbeløp
      *
     c                   if        bdrab1 <> 0
     c                   eval      w_rab1 = (w_sumb * bdrab1) / 100
     c                   endif
     c                   if        bdrab2 <> 0
     c                   eval      w_rab2 = ((w_sumb - w_rab1)
     c                                      * bdrab2) / 100
     c                   endif
     c                   if        bdorab <> 0
     c                   eval      w_rabo = ((w_sumb - w_rab1 - w_rab2)
     c                                      * bdorab) / 100
     c                   endif
5.60  *
5.60  * Ta vare på linjerabatter
5.60  *
5.60 c                   eval      s_rab1 = w_rab1
5.60 c                   eval      s_rab2 = w_rab2
5.60 c                   eval      s_rabo = w_rabo
      *
      * Dersom almenningsrabatt skal spesifiseres, er sum salg brutto
      *
     c                   if        faasa2 = 0 and
     c                             bdbrr1 = 0
     c                   eval      w_sums = bdsums
     c                   else
     c                   if        valkre = '-'
     c                   eval      w_sums = ((bdsapr * bdanta) - w_rab1
     c                                      - w_rab2 - w_rabo) * -1
     c                   else
     c                   eval      w_sums = ((bdsapr * bdanta) - w_rab1
     c                                      - w_rab2 - w_rabo)
     c                   endif
     c                   endif
      *
      * Sjekker om dette er en negativ bong
      *
6.35 c                   if        vaokre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   eval      w_sums = w_sums * -1
     c                   eval      w_sumb = w_sumb * -1
     c                   eval      w_sumg = w_sumg * -1
     c                   eval      w_sumo = w_sumo * -1
     c                   eval      w_sumk = w_sumk * -1
     c                   eval      w_suko = w_suko * -1
     c                   eval      w_rab1 = w_rab1 * -1
     c                   eval      w_rab2 = w_rab2 * -1
6.13 c                   eval      w_rabo = w_rabo * -1
     c                   endif
      *
      * Sjekker om dette er en negativ linje
      * Vær oppmerksom på at W_SUMS(BDSUMS) er lagret med rett fortegn
      *
     c                   if        valkre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   eval      w_sumb = w_sumb * -1
     c                   eval      w_sumg = w_sumg * -1
     c                   eval      w_sumo = w_sumo * -1
     c                   eval      w_sumk = w_sumk * -1
     c                   eval      w_suko = w_suko * -1
     c                   eval      w_rab1 = w_rab1 * -1
     c                   eval      w_rab2 = w_rab2 * -1
     c                   endif
      *
      * Beregner gavekort beløp for justering av posteringer
      *
     c                   if        bdvare = baagva
     c                   if        w_sums > 0
     c                   eval      w_gkbe = w_gkbe + w_sums
     c                   else
     c                   eval      w_gkne = w_gkne + w_sums
     c                   eval      w_tots = w_tots - w_sums
     c                   endif
     c                   endif
      *
6.11  * Finner mva-grunlag korrigert for alm.rab
      *
     c                   eval      w_sumg = w_sumg -
     c                                      w_rab1 - w_rab2 - w_rabo
      *
     c                   eval      w_stat = *blank
     c                   eval      w_mvak = bdkmva
     c                   time                    w_dato
      *
     c                   call      'RS205R'
     c                   parm                    w_stat
     c                   parm                    w_mvak
     c                   parm                    w_mvtx
     c                   parm                    w_dato
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
     c                   parm                    w_bpro
      *
      * Henter kontoinformasjon for føring av almenningsrabatt
      *
     c                   if        faasa2 = 1 and
     c                             bdbrr1 > 0
     c                   eval      rmbtlr_talm = bdalme
     c                   eval      rmbtlr_tbrt = bdbrty
     c     rmbtlr_key    chain     rmbtlr
     c                   if        %found
     c                   eval      w_kont = rmtkto
     c                   eval      w_spes = rmtsps
     c                   else
     c                   eval      w_kont = d_kko6
     c                   eval      w_spes = d_ksp6
     c                   endif
     c                   eval      w_mkod = '0'
     c                   eval      w_valg = 7
     c                   eval      w_mvag = 0
     c                   endif
      *
      *
      * Beregner rett beløp eks./inkl. mva   - avgiftskode 0 = 0 i mva grunnlag
      *
6.12 c**                 if        bdkmva = '0'
6.12 c                   if        w_mvap = 0
     c                   eval      w_mvag = 0
     c                   else
     c                   eval      w_mvag = w_sumg
     c                   endif
     c                   eval(h)   w_mvab = (w_sumg * w_mvap) / 100
     c                   eval(h)   w_belp = w_sums + w_mvab
      *
     c                   eval      w_tots = w_tots + w_belp
      *
      * Leveringsadresse oppdateres i feltet
      * for prisordre, dersom leveringsadresse
      * er oppgitt
      *
     c                   if        bokpro <> 0
     c                   eval      bopord = bokpro
     c                   eval      bopsuf = 0
     c                   endif
5.60  *
5.60 c                   eval      w_alm1 = 0
5.60 c                   eval      w_alm2 = 0
5.60 c                   eval      w_algr = (bdsapr * bdanta) -
5.60 c                                      s_rab1 -s_rab2 - s_rabo
5.60 c                   if        bdbrr1 > 0
5.60 c                   eval(h)   w_alm1 = (w_algr * bdbrr1) / 100
5.60 c                   endif
5.60 c                   if        bdbrr2 > 0
5.60 c                   eval      w_algr = w_algr - w_alm1
5.60 c                   eval(h)   w_alm2 = (w_algr * bdbrr2) / 100
5.60 c                   endif
5.60  *
5.60  *
5.60  * Sjekker om dette er en negativ bong
5.60  *
6.35 c                   if        vaokre = '-'
5.60 c                   eval      w_alm1 = w_alm1 * -1
5.60 c                   eval      w_alm2 = w_alm2 * -1
5.60 c                   endif
5.60  *
5.60  * Sjekker om dette er en negativ linje
5.60  *
5.60 c                   if        valkre = '-'
5.60 c                   eval      w_alm1 = w_alm1 * -1
5.60 c                   eval      w_alm2 = w_alm2 * -1
5.60 c                   endif
      *
     c     end_linje     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter avdeling/konto/spesifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntkon        begsr
      *
      * Legger ut parametere for konto-henvisnings-program
      *
     c                   eval      w_stat = *blank
     c                   eval      w_otyp = bootyp
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_vare = vvvare
     c                   move      0             w_hele
      *
      * Dersom vare er hentet fra LVR,
      * skal varegruppe hentes fra linjenivå
      *
     c                   if        bdlvre = 1
     c                   eval      w_ogrp = bdogrp
     c                   eval      w_hgrp = bdhgrp
     c                   eval      w_ugrp = bdugrp
     c                   eval      w_vare = bdvare
     c                   move      0             w_hele
     c                   endif
      *
      * Legger inn leveringstype fra varelinje,
      *
     c                   eval      w_lety = bdlety
      *
      * Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_stat
     c                   parm                    w_lety
     c                   parm                    w_otyp
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_vare
     c                   parm                    w_hele
     c                   eval      d_hele = w_hele
      *
      * Overstyrer avdeling dersom denne
      * finnes på ordrehode
      *
     c                   if        boavde > 0
     c                   eval      d_kav1 = boavde
     c                   eval      d_kav2 = boavde
     c                   eval      d_kav3 = boavde
     c                   eval      d_kav4 = boavde
     c                   eval      d_kav5 = boavde
     c                   eval      d_kav6 = boavde
     c                   eval      d_kav7 = boavde
     c                   endif
      *
      * Legger inn avdeling/konto/spesifikasjon fra record
      *
     c                   eval      w_avde = boavde
     c                   eval      w_kont = bokont
     c                   eval      w_spes = bospes
      *
      * Gå videre dersom konto nå er funnet
      *
     c     w_kont        cabne     0             xtag01
      *
      * Hente konti fra kontohenvisningsregister
      *
     c                   if        bomkod = 0
     c                   eval      w_kont = d_kko1
     c                   eval      w_spes = d_ksp1
      *
6.21 c                   eval      w_stat = *blank
6.21 c                   eval      w_mvak = bdkmva
6.21 c                   time                    w_dato

6.21 c                   call      'RS205R'
6.21 c                   parm                    w_stat
6.21 c                   parm                    w_mvak
6.21 c                   parm                    w_mvtx
6.21 c                   parm                    w_dato
6.21 c                   parm                    w_mvap
6.21 c                   parm                    w_mvat
6.21 c                   parm                    w_mvaf
6.21 c                   parm                    w_bpro

6.21 c                   if        w_stat = *blank and
6.21 c                             w_mvap = 0
     c                   eval      w_kont = d_kko2
     c                   eval      w_spes = d_ksp2
     c                   endif
      *
     c                   else
     c                   eval      w_kont = d_kko2
     c                   eval      w_spes = d_ksp2
     c                   endif
      *
     c     xtag01        tag
      *
      * Gå videre dersom avdeling nå er funnet
      *
     c     w_avde        cabne     0             xtag02
      *
      * Hente avdeling fra kontohenvisningsregister
      *
     c                   if        bomkod = 0
     c                   eval      w_avde = d_kav1
      *
     c                   if        vvkmva = 1
     c                   eval      w_avde = d_kav2
     c                   endif
      *
     c                   else
     c                   eval      w_avde = d_kav2
     c                   endif
      *
     c     xtag02        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall i forhold til statistikkenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Dersom solgt enhet er lik stat.enhet beholdes antall
      *
     c                   if        bdenh1 = vvenhs
     c                   goto      end_omr
     c                   endif
      *
      * Finner omregningsfaktor volum for stat.enhet og for solgt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = bdvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 90
     c                   dow       *in90 = *off
     c                   if        bdenh1 = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c                   if        vvenhs = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 90
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
     c                   eval      w_anta = w_anta * w_omrs / w_omrl
      *
     c     end_omr       endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som finner alternativ varegruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_avgr     begsr
      *
      * Leser på varegruppeknytning
      * Leter først etter knytninger mot leverandør
      *
     c                   eval      vvgkl1_ogrp = sfogrp
     c                   eval      vvgkl1_hgrp = sfhgrp
     c                   eval      vvgkl1_ugrp = sfugrp
     c                   eval      vvgkl1_ldor = sfldor
      *
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = sfldor
     c                   eval      vvgkl1_ugrp = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = sfldor
     c                   eval      vvgkl1_hgrp = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   if        *in91 = *on
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    chain     vvgkl1                             91
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        *in91 = *off
     c                   eval      sfahgr = vkahgr
     c                   eval      sfaugr = vkaugr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne en og en ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ordre   begsr
      *
     c                   eval      b_stat = *off
      *
     c                   if        sstxlo_ornr <> 0
     c     sstxlo_key    setll     sstxlo
     c     sstxlo_key    reade     sstxlo
     c                   dow       not %eof(sstxlo)
     c                   eval      b_stat = *on
     c                   leavesr
     c     sstxlo_key    reade     sstxlo
     c                   enddo
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.36  *
6.36 c     *entry        plist
6.36 c                   parm                    p_first
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for file : UNDER-GRUPPE-REGISTER
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *  Key for les av VARE enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
5.24  *
5.24  * Key for les på varegruppeknytning
5.24  *
5.24 c     vvgkl1_key    klist
5.24 c                   kfld                    w_firm
5.24 c                   kfld                    vvgkl1_ogrp
5.24 c                   kfld                    vvgkl1_hgrp
5.24 c                   kfld                    vvgkl1_ugrp
5.24 c                   kfld                    vvgkl1_ldor
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : LINJE-TYPE-REGISTER
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for file : ORDRE-LINJE-heading
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-LINJE-heading
      *
     c     bohel1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    bohel1_aarr
     c                   kfld                    bohel1_wsid
     c                   kfld                    bohel1_bong
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_bong
      *
      * Key for file : BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : STATUS-KODER OFAK
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for lesing av bruksrett-type
      *
     c     rmbtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmbtlr_talm
     c                   kfld                    rmbtlr_tbrt
6.34  *
6.34  * Key for lesing av kort-trans
6.34  *
6.34 c     bokkl1_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    bokkl1_bong
      *
      * Key for file : skyggetabell
      *
     c     sstxlp_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstxlp_paar
     c                   kfld                    sstxlp_wsid
     c                   kfld                    sstxlp_bong
     c                   kfld                    sstxlp_line
      *
      * Key for file : skyggetabell
      *
     c     sstxlo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstxlo_paar
     c                   kfld                    sstxlo_ornr
     c                   kfld                    sstxlo_orsu
6.3a  *
6.3a  * Key for file : kundeklubb kuponger
6.3a  *
6.3a c     bckuiu_key    klist
6.3a c                   kfld                    w_firm
6.3a c                   kfld                    bckuiu_bong
6.3a c                   kfld                    bckuiu_line
      *
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
      * Hent opplysninger fra BUTIKK-KODE-REGISTER
      *
     c     bstsl1_key    chain     bstsl1r                            90
     c                   if        *in90 = *on
     c                   eval      baaavs = *blank
     c                   eval      baadet = *blank
     c                   endif
      *
      * Hent opplysninger fra OFAK-STATUS-REGISTER
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
      * Sjekker om det finnes poster i
      * knytningsregister for alternativ varegruppe
      *
     c                   eval      vvgkl1_ogrp = 0
     c                   eval      vvgkl1_hgrp = 0
     c                   eval      vvgkl1_ugrp = 0
     c                   eval      vvgkl1_ldor = 0
      *
     c     vvgkl1_key    setll     vvgkl1
     c                   read      vvgkl1
      *
     c                   if        %eof
     c                   eval      b_xeof = *off
     c                   else
     c                   eval      b_xeof = *on
     c                   endif
      *
     c                   endsr
