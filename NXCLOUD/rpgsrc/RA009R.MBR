      /TITLE  Vedlikehold av statuskoder
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RA009R                                              *
      * Beskrivelse . . : Vedlikehold av statuskoder  - 9                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.00 *            061003 Nytt program                                   KOB  *
6.10  * V6.10      210909 Lagt inn sporingsfelter                        BSA  *
7.xx  * 7.00  100516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  * 800   220222 ask  Parameter for factoring via iizy
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     fraa9lu    uf a e           k disk    rename(raa9pfr:raa9lur)
     fra009d    cf   e             workstn
      *
     d                uds
     d l_user                911    920
8.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d p_firm          s                   like(ra9fir)
     d p_kund          s                   like(ra9knr)
     d p_navn          s             30
     d p_retk          s              1
      *
     d w_firm          s                   like(ra9fir)
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   DCL-PR CO402R EXTPGM('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
8.01     p_verdi2            char(2048);
8.01   END-PR;
8.01 d u_iizy          s              1    inz(*off)
      *
      /eject
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - Bilde for vedlikehold av statuskode 9
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for vedlikehold av statuskode              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Hent STATUSKODE 9
      *
     c     raa9l1_key    chain     raa9l1
      *
     c                   if        %found
      *
     c                   move      ra9knr        a29knr
     c                   move      ra9fmt        a29fmt
     c                   move      ra9kli        a29kli
     c                   move      ra9kun        a29kun
     c                   move      ra9reg        a29reg
     c                   move      ra9bil        a29bil
     c                   eval      a29usr = ra9usr
     c     *dmy          move      ra9dat        a29dat
      *
      * Hent kunde
      *
     c                   eval      p_kund = ra9knr
     c                   call      'RS750R'
     c                   parm                    p_retk
     c                   parm                    p_kund
     c                   parm                    p_navn
      *
     c                   eval      a2navn = p_navn
      *
     c                   endif
8.01  *
8.01  * Sjekk om factoring via iizy
8.01  *
8.01 c                   if         u_iizy = *on
8.01 c                   eval      *in31 = *on                                               .
8.01 c                   endif
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      xslutt
     c                   when      *inkd
     c                   exsr      spØrr
     c                   goto      a2tagb
8.01 c                   when      *inkf
8.01 c                   if         u_iizy = *on
8.01 c                   call      'RA134R'                                                  .
8.01 c                   endif
8.01 c                   goto      xslutt
     c                   endsl
      /space 3
      *
      *  KONTROLL
      *
      /eject
      *
      * Oppdater statuskode 9
      *
     c     raa9lu_key    chain     raa9lu
      *
     c                   move      a29knr        ra9knr
     c                   move      a29fmt        ra9fmt
     c                   move      a29kli        ra9kli
     c                   move      a29kun        ra9kun
     c                   move      a29reg        ra9reg
     c                   move      a29bil        ra9bil
      *
     c                   time                    ra9dat
     c                   time                    ra9tim
     c                   eval      ra9usr = l_user
      *
     c                   if        %found
     c                   update    raa9lur
     c                   else
6.10 c                   time                    ra9oda
6.10 c                   time                    ra9oti
6.10 c                   eval      ra9ous = l_user
     c                   write     raa9lur
     c                   endif
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for spørring                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     spØrr         begsr
      *
      * Call program for kunderegister
      *
     c                   if        w_afld = 'A29KNR'                                         .
      *
     c                   call      'RK500R'                                                  .
     c                   parm                    p_firm                                      .
     c                   parm                    p_kund                                      .
     c                   parm                    p_navn                                      .
      *
     c                   eval      a29knr = p_kund
     c                   eval      a2navn = p_navn
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Legge inn firmanummer og navn fra LDA
      *
     c                   move      l_firm        w_firm
     c                   move      l_firm        ra9fir
      *
      * Key for statuskode 9
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 9
      *
     c     raa9lu_key    klist
     c                   kfld                    w_firm
8.01  *
8.01  * Factoring via izzy
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'FACTORING_VIA_IIZY':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_iizy = *on;
8.01   endif;
      *
     c                   endsr
