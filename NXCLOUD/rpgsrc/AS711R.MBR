      /title  ASPGPL Datorutine, retur av ukenummer for dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AS711R
      * Beskrivelse . . : Datorutine, ukenummer for dato
      *
      *                   Parametre :
      *                   Inn: Dato
      *                   Ut : År, ukenummer, status
      *
6.21  *                   LR-flagg må være lik '1' for at *inLR ikke skal
6.21  *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       241104 GRO  Nytt program
6.21  *       300415 bof  tatt i bruk p_INLR
6.22  *       131016 bsa  Endret program for at den ikke skal kunne lukkes
6.23  *       161116 bof  For sikkerhet skyld, nullstiller øverst i prokes
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av parametre
      *
     d p_dato          s               d
     d p_aarr          s              4  0
     d p_week          s              2  0
     d p_stat          s              1
6.21 d p_inlr          s              1
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_dtst          s             10
     d w_aarx          s              4
     d w_aarr          s              4  0
     d w_wdat          s               d   datfmt(*iso)
     d w_wdax          s             10
     d w_unbr          s              2  0
      *
     d w_paar          s              4  0
     d w_puke          s              2  0
     d w_pfda          s               d
     d w_ptda          s               d
     d w_psta          s              1
      *
     d w_ostr          s             20
     d w_odat          s             10
     d w_oaar          s              4
     d w_owek          s              2
     d w_osta          s              1
      *
     d b_Ok            s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogam
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23 c                   eval      p_stat = *blank
6.23 c                   eval      p_week = *zero
6.23 c                   eval      p_aarr = *zero
      *
      * Sjekker at dato er utfyllt - hvis ikke returneres status '1'
      *
     c                   move      p_dato        w_dtst
     c     *iso          test (de)               w_dtst
     c                   if        %error
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk siste ukenr (52/53) forrige år
      *
     c                   eval      w_wdat = p_dato
     c                   move      w_wdat        w_wdax
     c                   eval      w_aarx = %subst(w_wdax:1:4)
     c                   move      w_aarx        w_aarr
     c                   eval      w_paar = w_aarr - 1
     c                   eval      w_puke = 52
      *
     c                   exsr      finn_uke
     c                   if        b_Ok = *on or
     c                             b_feil = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_paar = w_aarr - 1
     c                   eval      w_puke = 53
      *
     c                   exsr      finn_uke
     c                   if        b_Ok = *on or
     c                             b_feil = *on
     c                   goto      avslutt
     c                   endif
      *
      * Løper gjennom årets uker for treff
      *
     c                   eval      w_unbr = *zero
     c                   eval      w_paar = w_aarr
      *
     c                   dow       b_Ok = *off and
     c                             b_feil = *off and
     c                             w_unbr < 53
     c                   eval      w_unbr = w_unbr + 1
     c                   eval      w_puke = w_unbr
     c                   exsr      finn_uke
     c                   enddo
      *
      * Sjekk første ukenr neste år
      *
     c                   if        b_Ok = *off and
     c                             b_feil = *off
     c                   eval      w_paar = w_aarr + 1
     c                   eval      w_puke = 1
      *
     c                   exsr      finn_uke
     c                   endif
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        b_Ok = *off
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   if        b_feil = *on
     c                   eval      p_stat = '1'
     c                   endif
      *
     c*                  move      p_dato        w_odat
     c*                  move      p_aarr        w_oaar
     c*                  move      p_week        w_owek
     c*                  move      p_stat        w_osta
     c*                  eval      w_ostr = w_odat + ',' + w_oaar + ',' +
     c*                                     w_owek + ',' + w_osta
     c*    w_ostr        dsply
      *
6.21 c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
6.21 c                   endif
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kall rutine for fra- og tildato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_uke      begsr
      *
     c                   eval      b_Ok = *off
     c                   eval      b_feil = *off
     c                   eval      w_pfda = *loval
     c                   eval      w_ptda = *loval
     c                   eval      w_psta = *blank
      *
6.22 c**                 call      'AX710R'
6.22 c                   call      'AS710R'
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_pfda
     c                   parm                    w_ptda
     c                   parm                    w_psta
6.21 c                   parm                    p_inlr
      *
     c                   if        w_psta = '1'
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        p_dato >= w_pfda and
     c                             p_dato <= w_ptda
     c                   eval      b_Ok = *on
     c                   eval      p_week = w_puke
     c                   eval      p_aarr = w_paar
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_dato
     c                   parm                    p_aarr
     c                   parm                    p_week
     c                   parm                    p_stat
6.21 c                   parm                    p_inlr
      *
      * Initierer variable
      *
     c                   eval      p_stat = *blank
     c                   eval      p_week = *zero
     c                   eval      p_aarr = *zero
      *
     c                   endsr
