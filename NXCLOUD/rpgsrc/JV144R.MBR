     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV144R
      * Beskrivelse . . : Overføring til EVR, bytte enhet i EVR
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270809 bof  Nytt program
6.10  *       120510 bof  Utvidet omregn.faktor til 8,6
6.11  *       090811 bof  Feilretting vedr. status lik E
6.30  *       280114 bof  Utvidet nøkkel med lev.nr i JVPKPF
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjvpglr    if   e           k disk    rename(jvpgpfr:jvpglrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fjv144d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
      *
      * Definering av variable
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
6.11 d vvenl1_enhe     s                   like(veenhe)
     d jvpkl1_vare     s                   like(jwvare)
6.30 d jvpkl1_ldor     s                   like(jwldor)
     d jvpglr_vare     s                   like(jwgvar)
     d b_bytt          s              1    inz(*off)
6.32 d b_pakn          s              1    inz(*off)
     d w_Firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_genh          s                   like(vvenhe)
     d w_nenh          s                   like(vvenhe)
6.32 d w_dato          s                   like(jwodat)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1tag         tag
      *
6.10 c                   exsr      sjekk_sts3
      *
     c                   if        b_bytt = *off
     c                   goto      avslutt
     c                   endif
      *
     c                   exfmt     b1bld
      *
      * Funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      *
      * Kaller program for bytting av enhet gjennom hele systemet
      *
     c                   call      'VV755R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_genh
     c                   parm                    w_nenh
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
6.10  *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * Sjekker om varen har status E
6.10  * E = endring av enhet kan skje i overføringsrutinen
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  *
6.10 c     sjekk_sts3    begsr
6.10  *
6.10 c                   eval      vvarl1_vare = p_vare
6.10 c     vvarl1_key    chain     vvarl1
6.10 c                   if        %found(vvarl1)
6.10  *
6.10 c                   eval      jvpglr_vare = p_vare
6.10 c     jvpglr_key    setll     jvpglr
6.10 c     jvpglr_key    reade     jvpglr
6.10 c                   dow       not %eof
6.10  *
6.10 c                   if        jwgenh = vvenhe
6.10  * finner om denne er salgsenhet 1
6.10 c                   eval      vvenl1_vare = p_vare
6.11 c                   eval      vvenl1_enhe = vvenhe
6.10 c     vvenl1_key    chain     vvenl1
6.10 c                   if        not %found(vvenl1)
6.10 c                   leavesr
6.10 c                   endif
6.10 c                   if        vesaen > 1
6.10 c                   leavesr
6.10 c                   endif
6.10  * sjekker om omregningsfaktor på gml.pakn. er lik ny pakning
6.10 c
6.10 c                   eval      jvpkl1_vare = p_vare
6.30 c                   eval      jvpkl1_ldor = vvnlev
6.10 c     jvpkl1_key    setll     jvpkl1
6.10 c     jvpkl1_key    reade     jvpkl1
6.10 c                   dow       not %eof(jvpkl1)
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
      *
6.10 c                   if        jwpakn = jwgpak and
6.10 c                             jwofak=  jwgofa
6.10  * status settes til E
     c                   eval      b1benh = vvenhe
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1genh = jwgenh
     c                   eval      w_genh = jwgenh
     c                   eval      b1gofa = jwgofa
     c                   eval      b1nenh = jwenhe
     c                   eval      w_nenh = jwenhe
     c                   eval      b1nofa = jwofak
6.10 c                   eval      b_bytt = *on
6.10 c                   leavesr
6.10 c                   endif
6.32 c                   endif
6.10 c     jvpkl1_key    reade     jvpkl1
6.10 c                   enddo
6.10  *
6.10 c                   endif
6.10  *
6.10 c     jvpglr_key    reade     jvpglr
6.10 c                   enddo
      *
     c                   endif
6.10  *
6.10 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på EVR-vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på EVR-vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
6.11 c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
      *
      * Key for oppslag på vare-pakning - gammel
      *
     c     jvpglr_key    klist
     c                   kfld                    jvpglr_vare
      *
      * Initierer skjermbildet
      *
     c                   eval      b1vare = p_vare
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
     c                   endsr
