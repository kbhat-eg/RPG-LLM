     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPNIB
      * Program . . . . : FR700R
      * Beskrivelse . . : Rabattmatrise, kalkuler DG for rad
      *
      *                   DG kalkuleres for rad i rabattmatrisen og
      *                   oppgitt dato. Alle feltene i rabattmatrisen er
      *                   input-parametre.
      *
      *                   I tillegg er LR-flagg input-parameter til rutinen.
      *                   Dette benyttet for å styre *inLR ved kall til
      *                   VP700R
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040599 HKO  Nytt program
      *       170300 HKO  Lagt inn leverandør som nivå i rabattmatrisen
      *       151103 HKO  Erstattet rabattgruppe med varegruppe og modulnr
      *                   Lagt inn rabatt på bunnpris
5.70  * prgr  080210 bof  Utvidet med prisgruppe                       lnr
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffrabpf    if   e           k disk
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvvarlb    if   e           k disk    rename(vvarpfr:vvarlbr)
      *
      * Definering parametre
      *
     d p_firm          s                   like(frfirm)
5.70 d p_prgr          s                   like(frprgr)
     d p_ogrp          s                   like(frogrp)
     d p_hgrp          s                   like(frhgrp)
     d p_ugrp          s                   like(frugrp)
     d p_ldor          s                   like(frldor)
     d p_modn          s                   like(frmodn)
     d p_vare          s                   like(frvare)
     d p_enhe          s                   like(frenhe)
     d p_lety          s                   like(frlety)
      *
     d p_rab1          s                   like(frrab1)
     d p_ra1d          s                   like(frra1d)
     d p_ra1f          s                   like(frra1f)
     d p_ra1t          s                   like(frra1t)
     d p_rab2          s                   like(frrab2)
     d p_ra2d          s                   like(frra2d)
     d p_ra2f          s                   like(frra2f)
     d p_ra2t          s                   like(frra2t)
     d p_rabn          s                   like(frrabn)
     d p_rand          s                   like(frrand)
     d p_ranf          s                   like(frranf)
     d p_rant          s                   like(frrant)
     d p_rabb          s                   like(frrabb)
     d p_rabd          s                   like(frrabd)
     d p_rbuf          s                   like(frrbuf)
     d p_rbut          s                   like(frrbut)
     d p_rabt          s                   like(frrabt)
     d p_ratd          s                   like(frratd)
     d p_ratf          s                   like(frratf)
     d p_ratt          s                   like(frratt)
     d p_dekn          s                   like(frdekn)
     d p_dekd          s                   like(frdekd)
     d p_dekf          s                   like(frdekf)
     d p_dekt          s                   like(frdekt)
      *
     d p_dato          s               d   datfmt(*iso)
     d p_time          s               t   timfmt(*iso)
     d p_inlr          s              1
     d p_dggj          s              5  2
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d vvarla_ldor     s                   like(vvldor)
     d vvarlb_nmod     s                   like(vvnmod)
      *
      * Definering av variable
      *
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
     d w_anta          s              7  0
     d w_sapr_sum      s             15  2
     d w_kopr_sum      s             15  2
      *
     d w_firm          s                   like(vvfirm)
     d w_enhe          s                   like(frenhe)
     d w_dekn          s                   like(frdekn)
     d w_rab1          s                   like(frrab1)
     d w_rab2          s                   like(frrab2)
     d w_rabn          s                   like(frrabn)
     d w_rabb          s                   like(frrabb)
     d w_rabt          s                   like(frrabt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Dersom DG er utfyllt, returneres denne
      *
     c                   if        p_dekn <> 0 or
     c                             p_dekd <> 0
      *
     c                   if        p_dekd <> 0 and
     c                             p_dato >= p_dekf and
     c                             p_dato <= p_dekt
     c                   eval      w_dekn = p_dekd
     c                   else
     c                   eval      w_dekn = p_dekn
     c                   endif
      *
     c                   endif
      *
     c                   if        w_dekn <> 0
     c                   eval      p_dggj = w_dekn
     c                   goto      avslutt
     c                   endif
      *
      * Finner rabatter i rabattmatrisen i.h.t. parameter-dato
      *
     c                   exsr      hent_rab
      *
      * Behandling avhengig av nivå i rabattmatrisen
      *
     c                   select
     c                   when      p_vare <> *blank
     c                   exsr      behandl_vare
     c                   when      p_modn <> 0
     c                   exsr      behandl_modn
     c                   when      p_ldor <> 0
     c                   exsr      behandl_ldor
     c                   when      p_ogrp <> 0
     c                   exsr      behandl_vgrp
     c                   other
     c                   goto      avslutt
     c                   endsl
      *
      * Returnerer gjennomsnittlig DG for alle varer
      *
     c                   if        w_sapr_sum <> 0
     c                   select
     c                   when      (100 - (w_kopr_sum * 100 / w_sapr_sum))
     c                             < -999,99
     c                   eval      p_dggj = -999,99
     c                   when      (100 - (w_kopr_sum * 100 / w_sapr_sum))
     c                             > 999,99
     c                   eval      p_dggj = 999,99
     c                   other
     c                   eval(h)   p_dggj = 100 -
     c                             (w_kopr_sum * 100 / w_sapr_sum)
     c                   endsl
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente rabatter fra rabattmatrisen i.h.t.
      * parameter-dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_rab      begsr
      *
      * Linjerabatt 1
      *
     c                   if        p_rab1 <> 0 or
     c                             p_ra1d <> 0
      *
     c                   if        p_ra1d <> 0 and
     c                             p_dato >= p_ra1f and
     c                             p_dato <= p_ra1t
     c                   eval      w_rab1 = p_ra1d
     c                   else
     c                   eval      w_rab1 = p_rab1
     c                   endif
      *
     c                   endif
      *
      * Linjerabatt 2
      *
     c                   if        p_rab2 <> 0 or
     c                             p_ra2d <> 0
      *
     c                   if        p_ra2d <> 0 and
     c                             p_dato >= p_ra2f and
     c                             p_dato <= p_ra2t
     c                   eval      w_rab2 = p_ra2d
     c                   else
     c                   eval      w_rab2 = p_rab2
     c                   endif
      *
     c                   endif
      *
      * Rabatt på Nettopris
      *
     c                   if        p_rabn <> 0 or
     c                             p_rand <> 0
      *
     c                   if        p_rand <> 0 and
     c                             p_dato >= p_ranf and
     c                             p_dato <= p_rant
     c                   eval      w_rabn = p_rand
     c                   else
     c                   eval      w_rabn = p_rabn
     c                   endif
      *
     c                   endif
      *
      * Rabatt på Bunnpris
      *
     c                   if        p_rabb <> 0 or
     c                             p_rabd <> 0
      *
     c                   if        p_rabd <> 0 and
     c                             p_dato >= p_rbuf and
     c                             p_dato <= p_rbut
     c                   eval      w_rabb = p_rabd
     c                   else
     c                   eval      w_rabb = p_rabb
     c                   endif
      *
     c                   endif
      *
      * Rabatt på tilbudspris
      *
     c                   if        p_rabt <> 0 or
     c                             p_ratd <> 0
      *
     c                   if        p_ratd <> 0 and
     c                             p_dato >= p_ratf and
     c                             p_dato <= p_ratt
     c                   eval      w_rabt = p_ratd
     c                   else
     c                   eval      w_rabt = p_rabt
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling dersom vare er utfylt i rabattmatr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vare  begsr
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   exsr      kalkuler
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling dersom modul er utfylt i rabattmatr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_modn  begsr
      *
     c                   eval      vvarlb_nmod = p_modn
     c     vvarlb_key    setll     vvarlb
     c     vvarlb_key    reade     vvarlb
     c                   dow       not %eof
     c                   exsr      kalkuler
     c     vvarlb_key    reade     vvarlb
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling dersom leveranør er utfylt i rabattmatr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_ldor  begsr
      *
     c                   eval      vvarla_ldor = p_ldor
     c     vvarla_key    setll     vvarla
     c     vvarla_key    reade     vvarla
     c                   dow       not %eof
      *
     c                   select
     c                   when      p_ogrp = 0
     c                   exsr      kalkuler
     c                   when      p_ugrp <> 0 and
     c                             p_ogrp = vvogrp and
     c                             p_hgrp = vvhgrp and
     c                             p_ugrp = vvugrp
     c                   exsr      kalkuler
     c                   when      p_hgrp <> 0 and
     c                             p_ogrp = vvogrp and
     c                             p_hgrp = vvhgrp
     c                   exsr      kalkuler
     c                   when      p_ogrp <> 0 and
     c                             p_ogrp = vvogrp
     c                   exsr      kalkuler
     c                   endsl
      *
     c     vvarla_key    reade     vvarla
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling dersom varegruppe er utfylt i rabattmatr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vgrp  begsr
      *
     c                   eval      vvarl8_ogrp = p_ogrp
     c                   eval      vvarl8_hgrp = p_hgrp
     c                   eval      vvarl8_ugrp = p_ugrp
      *
     c                   select
     c                   when      p_ugrp <> 0
     c     vvarl8_key    setll     vvarl8
     c     vvarl8_key    reade     vvarl8
     c                   when      p_hgrp <> 0
     c     vvarl8_key2   setll     vvarl8
     c     vvarl8_key2   reade     vvarl8
     c                   when      p_ogrp <> 0
     c     vvarl8_key3   setll     vvarl8
     c     vvarl8_key3   reade     vvarl8
     c                   endsl
      *
     c                   dow       not %eof
      *
     c                   if        p_ldor = 0 or
     c                             p_ldor <> 0 and
     c                             p_ldor = vvldor
     c                   exsr      kalkuler
     c                   endif
      *
     c                   select
     c                   when      p_ugrp <> 0
     c     vvarl8_key    reade     vvarl8
     c                   when      p_hgrp <> 0
     c     vvarl8_key2   reade     vvarl8
     c                   when      p_ogrp <> 0
     c     vvarl8_key3   reade     vvarl8
     c                   endsl
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulerer DG for vare for rabatt-rad, og summerer antall varer og
      * total DG
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkuler      begsr
      *
      * Initiering av variable
      *
     c                   eval      w_sapr = 0
     c                   eval      w_bupr = 0
     c                   eval      w_tipr = 0
     c                   eval      w_kopr = 0
      *
     c                   if        p_enhe <> *blank
     c                   eval      w_enhe = p_enhe
     c                   else
     c                   eval      w_enhe = vvenhe
     c                   endif
      *
      * Henter og beregner prisgrunnlag fra vareregisteret
      *
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    p_prgr
     c                   parm                    w_enhe
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
      *
      * Finner salgspris og tilbudspris etter rabatter
      *
     c                   exsr      bestem_pris
      *
      * Summerer antall varer, kostpris og salgspris
      *
     c                   eval      w_anta = w_anta + 1
      *
     c                   eval      w_kopr_sum = w_kopr_sum + w_kopr
     c                   eval      w_sapr_sum = w_sapr_sum + w_sapr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bestemme hvilken rabatt og pris som er gyldig
      * og som skal benyttes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bestem_pris   begsr
      *
      * Beregner tilbudspris fratrukket eventuelle rabatter
      *
     c                   if        w_tipr <> 0 and
     c                             w_rabt <> 0
     c                   eval      w_tipr = w_tipr -
     c                                     (w_tipr * w_rabt / 100 )
     c                   endif
      *
      * Beregner salgspris fratrukket eventuelle rabatter
      *
      * Ordinært priset vare
      *
     c                   if        vvkpri = *blank
     c                   if        w_rab1 <> 0
     c                   eval      w_sapr = w_sapr -
     c                                     (w_sapr * w_rab1 / 100)
     c                   endif
     c                   if        w_rab2 <> 0
     c                   eval      w_sapr = w_sapr -
     c                                     (w_sapr * w_rab2 / 100)
     c                   endif
     c                   goto      end_bestem
     c                   endif
      *
      * Nettopriset-vare
      *
     c                   if        vvkpri = 'N'
     c                   if        w_rabn <> 0
     c                   eval      w_sapr = w_sapr -
     c                                     (w_sapr * w_rabn / 100)
     c                   endif
     c                   goto      end_bestem
     c                   endif
      *
      * Bunnpriset-vare
      *
     c                   if        vvkpri = 'B'
     c                   if        w_rabb <> 0
     c                   eval      w_sapr = w_sapr -
     c                                     (w_sapr * w_rabb / 100)
     c                   endif
     c                   goto      end_bestem
     c                   endif
      *
      * Setter salgspris til laveste pris
      *
     c     end_bestem    tag
      *
     c                   if        w_bupr <> 0 and
     c                             w_bupr < w_sapr
     c                   eval      w_sapr = w_bupr
     c                   endif
      *
     c                   if        w_tipr <> 0 and
     c                             w_tipr < w_sapr
     c                   eval      w_sapr = w_tipr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametere
      *
     c     *entry        plist
     c                   parm                    p_firm
5.70 c                   parm                    p_prgr
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldor
     c                   parm                    p_modn
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_rab1
     c                   parm                    p_ra1d
     c                   parm                    p_ra1f
     c                   parm                    p_ra1t
     c                   parm                    p_rab2
     c                   parm                    p_ra2d
     c                   parm                    p_ra2f
     c                   parm                    p_ra2t
     c                   parm                    p_rabn
     c                   parm                    p_rand
     c                   parm                    p_ranf
     c                   parm                    p_rant
     c                   parm                    p_rabb
     c                   parm                    p_rabd
     c                   parm                    p_rbuf
     c                   parm                    p_rbut
     c                   parm                    p_rabt
     c                   parm                    p_ratd
     c                   parm                    p_ratf
     c                   parm                    p_ratt
     c                   parm                    p_dekn
     c                   parm                    p_dekd
     c                   parm                    p_dekf
     c                   parm                    p_dekt
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_inlr
      *
     c                   parm                    p_dggj
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare m/varegruppe
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
     c     vvarl8_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
      *
     c     vvarl8_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
      *
      * Key for les på vare m/leverandør
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
      *
      * Key for les på vare m/modulnr
      *
     c     vvarlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlb_nmod
      *
      *
      * Initierer
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      p_dggj = 0
      *
     c                   endsr
