     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : BM600R
      * Beskrivelse . . : Danner varer på ABC-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190804 bø   Nytt program
5.40  *       120107 bø   Justert på les av sortiment - rekkefølge
5.60  *       020908 bø   Justert på les av sortiment - flere les
5.70  *  PRGR 021208 bof  Utvidet med prisgruppe
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  071110 bof  Utvidet med lager på sortiment
6.22  * 6.20  151210 bof  Skal ikke sette LR i VL720R
6.23  * 6.20  200511 bof  Utvidet vl710r med utg.dato og lev.nr
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstala    if   e           k disk    rename(sstapfr:sstalar)
     fsstali    if   e           k disk    rename(sstapfr:sstalir)
pref f                                     prefix(sx:2)
     flwb1lu    uf a e           k disk    rename(lwb1pfr:lwb1lur)
     flwb1l5    if   e           k disk    rename(lwb1pfr:lwb1l5r)
     flwb1l6    if   e           k disk    rename(lwb1pfr:lwb1l6r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvverlr    if   e           k disk    rename(vverpfr:vverlrr)
     fvverl2    if   e           k disk    rename(vverpfr:vverl2r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
6.20 fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
     fmabdlu    uf a e           k disk    rename(mabdpfr:mabdlur)
     fmabclu    uf   e           k disk    rename(mabcpfr:mabclur)
     fmabdl1    if   e           k disk    rename(mabdpfr:mabdl1r)
     fmabdl2    if   e           k disk    rename(mabdpfr:mabdl2r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Parameter
      *
     d p_list          s            128
      *
      * Local Data Area
      *
     d                uds
      *
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastruktur - parameter inn
      *
     d                 ds
     d d_list                       128
     d  d_fvar                        8  0 overlay(d_list)
     d  d_tvar                        8  0 overlay(d_list:9)
     d  d_flag                        2  0 overlay(d_list:17)
     d  d_tlag                        2  0 overlay(d_list:19)
     d  d_fogr                        2  0 overlay(d_list:21)
     d  d_togr                        2  0 overlay(d_list:23)
     d  d_fhgr                        2  0 overlay(d_list:25)
     d  d_thgr                        2  0 overlay(d_list:27)
     d  d_fugr                        3  0 overlay(d_list:29)
     d  d_tugr                        3  0 overlay(d_list:32)
     d  d_levn                        6  0 overlay(d_list:35)
     d  d_selg                        4  0 overlay(d_list:41)
     d  d_faar                        4  0 overlay(d_list:45)
     d  d_fmnd                        2  0 overlay(d_list:49)
     d  d_taar                        4  0 overlay(d_list:51)
     d  d_tmnd                        2  0 overlay(d_list:55)
     d  d_aint                        2  0 overlay(d_list:57)
     d  d_bint                        2  0 overlay(d_list:59)
     d  d_cint                        2  0 overlay(d_list:61)
     d  d_xint                        2  0 overlay(d_list:63)
     d  d_yint                        2  0 overlay(d_list:65)
     d  d_zint                        2  0 overlay(d_list:67)
     d  d_sorb                        1  0 overlay(d_list:69)
     d  d_sorn                        1  0 overlay(d_list:70)
     d  d_sora                        1  0 overlay(d_list:71)
     d  d_oppd                        1  0 overlay(d_list:72)
     d  d_lt01                        1  0 overlay(d_list:73)
     d  d_lt02                        1  0 overlay(d_list:74)
     d  d_lt03                        1  0 overlay(d_list:75)
     d  d_lt04                        1  0 overlay(d_list:76)
     d  d_lt05                        1  0 overlay(d_list:77)
     d  d_lt06                        1  0 overlay(d_list:78)
     d  d_lt07                        1  0 overlay(d_list:79)
     d  d_lt08                        1  0 overlay(d_list:80)
     d  d_lt09                        1  0 overlay(d_list:81)
     d  d_lt10                        1  0 overlay(d_list:82)
     d  d_vtyp                        1    overlay(d_list:83)
     d  d_fvso                       10    overlay(d_list:84)
     d  d_tvso                       10    overlay(d_list:94)
     d  d_lager                      20  0 overlay(d_list:104)
     d  d_lagk                        2    overlay(d_list:124)
     d  d_dumm                        1    overlay(d_list:126)
      *
     d d_lagr          ds
     d  d_la01                        2  0
     d  d_la02                        2  0
     d  d_la03                        2  0
     d  d_la04                        2  0
     d  d_la05                        2  0
     d  d_la06                        2  0
     d  d_la07                        2  0
     d  d_la08                        2  0
     d  d_la09                        2  0
     d  d_la10                        2  0
      *
      *
      * Datastrukturer for testing på varegrupper
      *
     d                 ds
     d f_vgrp                         7
     d  f_ogrp                        2  0 overlay(f_vgrp)
     d  f_hgrp                        2  0 overlay(f_vgrp:3)
     d  f_ugrp                        3  0 overlay(f_vgrp:5)
      *
     d                 ds
     d t_vgrp                         7
     d  t_ogrp                        2  0 overlay(t_vgrp)
     d  t_hgrp                        2  0 overlay(t_vgrp:3)
     d  t_ugrp                        3  0 overlay(t_vgrp:5)
      *
     d                 ds
     d v_vgrp                         7
     d  v_ogrp                        2  0 overlay(v_vgrp)
     d  v_hgrp                        2  0 overlay(v_vgrp:3)
     d  v_ugrp                        3  0 overlay(v_vgrp:5)
      *
     d w_aarmnd        ds
     d sfpaar                         4  0
     d sfpmnd                         2  0
      *
     d w_aarmnd_er     ds
     d sxpaar                         4  0
     d sxpmnd                         2  0
      *
      * Definering av variable i nøkler
      *
     d sstala_paar     s                   like(sfpaar)
     d sstala_vare     s                   like(sfvare)
     d sstali_vare     s                   like(sfvare)
     d lwb1l5_abpr     s                   like(mwabpr)
     d lwb1l6_xypr     s                   like(mwxypr)
     d lwb1lv_vare     s                   like(mbvare)
     d mabdlu_lagk     s                   like(mblagk)
     d mabdlu_vsor     s                   like(mbvsor)
     d mabdlu_ogrp     s                   like(mbogrp)
     d mabdlu_hgrp     s                   like(mbhgrp)
     d mabdlu_ugrp     s                   like(mbugrp)
     d mabdlu_vare     s                   like(mbvare)
     d mabdl2_lagk     s                   like(mblagk)
     d mabdl2_vsor     s                   like(mbvsor)
     d mabdl2_ogrp     s                   like(mbogrp)
     d mabdl2_hgrp     s                   like(mbhgrp)
     d mabdl2_ugrp     s                   like(mbugrp)
     d mabdl2_kabc     s                   like(mbkabc)
     d mabclu_vsor     s                   like(mavsor)
     d mabclu_ogrp     s                   like(maogrp)
     d mabclu_hgrp     s                   like(mahgrp)
     d mabclu_ugrp     s                   like(maugrp)
     d mabclu_lagk     s                   like(malagk)
     d vvarl1_vare     s                   like(vvvare)
     d vverlr_envr     s                   like(vvenvr)
     d vverl2_egvr     s                   like(vvegvr)
     d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
6.20 d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
     d w_vare          s                   like(vvvare)
     d w_ddat          s                   like(vvudat)
     d w_fraam         s              6
     d w_tilam         s              6
5.70 d w_vtyp          s                   like(vvvtyp)
      *
6.20 d b_inlr          s              1    inz(*off)
     d b_ok            s              1    inz(*off)
     d b_ok_er         s              1    inz(*off)
     d s_vare          s                   like(mbvare)
     d s_kabc          s                   like(mbkabc)
     d s_bfir          s                   like(mwbfir)
     d s_bvar          s                   like(mwbvar)
     d s_bogr          s                   like(mwbogr)
     d s_bhgr          s                   like(mwbhgr)
     d s_bugr          s                   like(mwbugr)
     d s_btk1          s                   like(mwbtk1)
     d s_benh          s                   like(mwbenh)
     d w_lagk          s                   like(mblagk)
     d w_vsor          s                   like(mbvsor)
     d w_ogrp          s                   like(mbogrp)
     d w_hgrp          s                   like(mbhgrp)
     d w_ugrp          s                   like(mbugrp)
     d w_sumb          s             11  2
     d w_sumr          s             11  2
     d w_sumn          s             11  2
     d w_anta          s             11  3
     d w_anto          s             11  0
     d w_tsumb         s             15  2
     d w_tsumn         s             15  2
     d w_tanta         s             15  3
     d w_tanto         s             15  3
     d w_wsumn         s             15  2
     d w_wanta         s             15  3
     d w_wanto         s             15  3
     d w_gran          s             15  5
     d w_grbn          s             15  5
     d w_grxa          s             15  3
     d w_grya          s             15  3
     d w_apro          s             10  7
     d w_bpro          s             10  7
     d w_xpro          s             10  7
     d w_ypro          s             10  7
     d w_pros          s             10  7
     d w_tpro          s             10  7
     d w_akkpr         s             10  7
     d w_ktot          s             11  2
     d w_ktota         s             11  0
     d w_akpr          s              5  2
     d w_totax         s             11  0
     d w_totay         s             11  0
     d w_totaz         s             11  0
     d w_totbx         s             11  0
     d w_totby         s             11  0
     d w_totbz         s             11  0
     d w_totcx         s             11  0
     d w_totcy         s             11  0
     d w_totcz         s             11  0
      *
     d w_xvar          s             15
     d w_xsmb          s             11  2
6.20 d w_udat          s                   like(vvedat)
6.23 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
     d b_a_ok          s              1    inz(*off)
     d b_b_ok          s              1    inz(*off)
     d b_c_ok          s              1    inz(*off)
     d b_x_ok          s              1    inz(*off)
     d b_y_ok          s              1    inz(*off)
     d b_z_ok          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      slett_tidl
      *
     c                   eval      w_tsumb = 0
     c                   eval      w_tsumn = 0
     c                   eval      w_tanta = 0
     c                   eval      w_tanto = 0
      *
     c                   read      sstala
      *
     c                   dow       not %eof  and
     c                             sfpaar <= d_taar
     c                   eval      w_vare = sfvare
      *
     c                   exsr      rec_ktrl
      *
     c                   if        b_ok = *on
     c                   exsr      rec_ok
     c                   endif
      *
     c                   read      sstala
     c                   enddo
      *
      * Oppdaterer evt. siste varenummer som er akkumulert
      *
     c                   if        s_vare <> sfvare and
     c                             s_vare <> *blank
     c                   clear                   lwb1lur
     c                   eval      lwb1lv_vare = s_vare
     c     lwb1lv_key    chain     lwb1lu
     c                   eval      mwbant = mwbant + w_anta
     c                   eval      mwbaol = mwbaol + w_anto
     c                   eval      mwbsmb = mwbsmb + w_sumb
     c                   eval      mwbsmr = mwbsmr + w_sumr
     c                   eval      mwbsmn = mwbsmn + w_sumn
     c                   eval      mwbogr = s_bogr
     c                   eval      mwbhgr = s_bhgr
     c                   eval      mwbugr = s_bugr
     c                   eval      mwbtk1 = s_btk1
     c                   eval      mwbtk1 = s_btk1
     c                   eval      mwbenh = s_benh
     c                   if        %found(lwb1lu)
6.10 c                   time                    mwbeda
6.10 c                   time                    mwbeti
6.10 c                   eval      mwbeus = l_user
     c                   update    lwb1lur
     c                   else
     c                   eval      mwbfir = w_firm
     c                   eval      mwbvar = s_vare
6.10 c                   time                    mwboda
6.10 c                   time                    mwboti
6.10 c                   eval      mwbous = l_user
6.10 c                   time                    mwbeda
6.10 c                   time                    mwbeti
6.10 c                   eval      mwbeus = l_user
     c                   write     lwb1lur
     c                   endif
     c                   endif
      *
      * Finner %vis fordeling av omsetning og antall - oppd. arbeidsreg.
      *
     c                   exsr      finn_pros
     c                   eval      w_akkpr  = 0
      *
      * Gj.gang mot omsetning
      *
     c                   eval      w_akkpr  = 0
      *
     c                   eval      lwb1l5_abpr = *hival
     c     lwb1l5_key    setll     lwb1l5
     c                   read      lwb1l5
     c                   dow       not %eof
      *
     c                   exsr      oppd_abc
      *
     c                   read      lwb1l5
     c                   enddo
      *
      * Gj.gang mot antall
      *
     c                   eval      w_akkpr  = 0
      *
     c                   eval      lwb1l6_xypr = *hival
     c     lwb1l6_key    setll     lwb1l6
     c                   read      lwb1l6
     c                   dow       not %eof
      *
     c                   exsr      oppd_xyz
      *
     c                   read      lwb1l6
     c                   enddo
      *
     c                   exsr      upd_sum
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
6.22 c                   eval      b_inlr = *off
6.23 c                   call      'VL710R'
6.23 c                   parm                    w_firm
6.23 c                   parm                    sfvare
6.23 c                   parm                    sflage
5.73 c                   parm                    w_vtyp
6.23 c                   parm                    w_udat
6.23 c                   parm                    w_ldor
6.23 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ktrl      begsr
      *
     c                   eval      b_ok = *off
      *
      * Kalkuler totalsum brutto/netto + antall alle varer
      *
      *
      * Sjekk riktig firma
      *
     c                   if        sffirm <> l_firm
     c                   leavesr
     c                   endif
      *
      * Sjekk om det er en erstattet vare - skal i så fall ikke være med
      *
     c                   eval      vverl2_egvr = sfvare
     c     vverl2_key    chain     vverl2
     c                   if        %found(vverl2)
     c                   leavesr
     c                   endif
      *
      * Sjekk datointervall
      *
     c                   if        w_aarmnd < w_fraam or
     c                             w_aarmnd > w_tilam
     c                   leavesr
     c                   endif
      *
     c                   if        sflety <> d_lt01 and
     c                             sflety <> d_lt02 and
     c                             sflety <> d_lt03 and
     c                             sflety <> d_lt04 and
     c                             sflety <> d_lt05 and
     c                             sflety <> d_lt06 and
     c                             sflety <> d_lt07 and
     c                             sflety <> d_lt08 and
     c                             sflety <> d_lt09 and
     c                             sflety <> d_lt10
     c                   leavesr
     c                   endif
      *
     c                   eval      vvarl1_vare = sfvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
6.23  * henter div. lagerinfo
      *
6.22 c                   eval      b_inlr = *on
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    sfvare
5.70 c                   parm                    sflage
5.70 c                   parm                    w_vtyp
6.23 c                   parm                    w_udat
6.23 c                   parm                    w_ldor
6.23 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp <> 'L' and
5.70 c                             w_vtyp <> 'S'
     c                   leavesr
     c                   endif
      *
     c                   if        d_vtyp <> *blank and
5.70 c                             w_vtyp <> d_vtyp
     c                   leavesr
     c                   endif
      *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat >= w_ddat
     c                   leavesr
     c                   endif
      *
      * Sjekk lagernr.
      *
     c                   if        d_lager <> 0
     c                   if        sflage <> d_la01 and
     c                             sflage <> d_la02 and
     c                             sflage <> d_la03 and
     c                             sflage <> d_la04 and
     c                             sflage <> d_la05 and
     c                             sflage <> d_la06 and
     c                             sflage <> d_la07 and
     c                             sflage <> d_la08 and
     c                             sflage <> d_la09 and
     c                             sflage <> d_la10
     c                   leavesr
     c                   endif
     c                   else
     c                   if        sflage <  d_flag or
     c                             sflage >  d_tlag
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk varesortiment
      *
     c                   if        d_fvso <> *blank
      * med leverandør
     c                   eval      vsdtl1_vsor = d_fvso
     c                   eval      vsdtl1_ldor = sfldor
     c                   eval      vsdtl1_ogrp = sfogrp
     c                   eval      vsdtl1_hgrp = sfhgrp
     c                   eval      vsdtl1_ugrp = sfugrp
     c                   eval      vsdtl1_modn = sfmodn
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_modn = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ogrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      * uten leverandør
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ldor = 0
     c                   eval      vsdtl1_ogrp = sfogrp
     c                   eval      vsdtl1_hgrp = sfhgrp
     c                   eval      vsdtl1_ugrp = sfugrp
     c                   eval      vsdtl1_modn = sfmodn
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_modn = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      * bare vare
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ldor = 0
     c                   eval      vsdtl1_ogrp = 0
     c                   eval      vsdtl1_hgrp = 0
     c                   eval      vsdtl1_ugrp = 0
     c                   eval      vsdtl1_modn = 0
     c                   eval      vsdtl1_vare = sfvare
     c     vsdtl1_key    chain     vsdtl1
     c                   endif
     c                   if        not %found(vsdtl1)
     c                   leavesr
     c                   endif
     c
     c                   else
      *
      * Sjekk varegruppe
      *
     c                   if        d_fogr <> *zero and
     c                             sfogrp <> d_fogr
     c                   leavesr
     c                   endif
      *
     c                   if        d_fhgr <> *zero and
     c                             sfhgrp <> d_fhgr
     c                   leavesr
     c                   endif
      *
     c                   if        d_fugr <> *zero and
     c                             sfugrp <> d_fugr
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
      * Sett OK-indikator - record skal være med
      *
     c                   eval      b_ok = *on
      *
     c     end_ktrl      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for akkumulering/oppd. av arbeidsfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ok        begsr
      *
     c                   if        s_vare = *blank
     c                   eval      w_sumb = 0
     c                   eval      w_sumr = 0
     c                   eval      w_sumn = 0
     c                   eval      w_anta = 0
     c                   eval      w_anto = 0
     c                   endif
      *
     c                   if        s_vare <> sfvare and
     c                             s_vare <> *blank
     c                   exsr      sjekk_erstv
     c                   clear                   lwb1lur
     c                   eval      lwb1lv_vare = s_vare
     c     lwb1lv_key    chain     lwb1lu
     c                   eval      mwbant = mwbant + w_anta
     c                   eval      mwbaol = mwbaol + w_anto
     c                   eval      mwbsmb = mwbsmb + w_sumb
     c                   eval      mwbsmr = mwbsmr + w_sumr
     c                   eval      mwbsmn = mwbsmn + w_sumn
     c                   eval      mwbogr = s_bogr
     c                   eval      mwbhgr = s_bhgr
     c                   eval      mwbugr = s_bugr
     c                   eval      mwbtk1 = s_btk1
     c                   eval      mwbtk1 = s_btk1
     c                   eval      mwbenh = s_benh
     c                   if        %found(lwb1lu)
6.10 c                   time                    mwbeda
6.10 c                   time                    mwbeti
6.10 c                   eval      mwbeus = l_user
     c                   update    lwb1lur
     c                   else
     c                   eval      mwbfir = w_firm
     c                   eval      mwbvar = s_vare
6.10 c                   time                    mwboda
6.10 c                   time                    mwboti
6.10 c                   eval      mwbous = l_user
6.10 c                   time                    mwbeda
6.10 c                   time                    mwbeti
6.10 c                   eval      mwbeus = l_user
     c                   write     lwb1lur
     c                   endif
      *
     c                   eval      w_sumb = 0
     c                   eval      w_sumr = 0
     c                   eval      w_sumn = 0
     c                   eval      w_anta = 0
     c                   eval      w_anto = 0
     c                   endif
      *
     c                   eval      w_tanta = w_tanta + sfanta
     c                   eval      w_tsumb = w_tsumb + sfsumb
     c                   eval      w_tsumn = w_tsumn + (sfsumb -
     c                                       (sfrab1 + sfrab2 + sfrabo))
      *
     c                   eval      w_anta = w_anta + sfanta
     c                   if        sfanta >= 0
     c                   eval      w_anto = w_anto + 1
     c                   eval      w_tanto = w_tanto + 1
     c                   else
     c                   eval      w_anto = w_anto - 1
     c                   eval      w_tanto = w_tanto - 1
     c                   endif
     c                   eval      w_sumb = w_sumb + sfsumb
     c                   eval      w_sumr = w_sumr + (sfrab1 + sfrab2 +
     c                                               sfrabo)
     c                   eval      w_sumn = w_sumn + (sfsumb -
     c                                      (sfrab1 + sfrab2 + sfrabo))
      *
     c                   eval      s_bfir = w_firm
     c                   eval      s_bvar = w_vare
     c                   eval      s_bogr = sfogrp
     c                   eval      s_bhgr = sfhgrp
     c                   eval      s_bugr = sfugrp
     c                   eval      s_btk1 = sftek1
     c                   eval      s_btk1 = sftek1
     c                   eval      s_benh = sfenhe
      *
     c                   eval      s_vare = sfvare
      *
     c     end_ok        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om denne varen har erstatningsvare
      *            akkumulerer dette inn i samme akkumulatorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_erstv   begsr
      *
     c                   eval      vverlr_envr = s_vare
     c     vverlr_key    setll     vverlr
     c     vverlr_key    reade     vverlr
     c                   dow       not %eof(vverlr)
     c                   exsr      les_erstv
     c     vverlr_key    reade     vverlr
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om denne varen har erstatningsvare
      *            akkumulerer dette inn i samme akkumulatorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_erstv     begsr
      *
     c                   eval      sstali_vare = vvegvr
     c     sstali_key    setll     sstali
     c     sstali_key    reade     sstali
     c                   dow       not %eof(sstali)
     c                   exsr      rec_ktrl_er
      *
     c                   if        b_ok_er = *on
     c                   exsr      rec_ok_er
     c                   endif
      *
     c     sstali_key    reade     sstali
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for akkumulering/oppd. av arbeidsfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ok_er     begsr
      *
     c                   eval      w_tanta = w_tanta + sxanta
     c                   eval      w_tsumb = w_tsumb + sxsumb
     c                   eval      w_tsumn = w_tsumn + (sxsumb -
     c                                       (sxrab1 + sxrab2 + sxrabo))
      *
     c                   eval      w_anta = w_anta + sxanta
     c                   if        sxanta > 0
     c                   eval      w_anto = w_anto + 1
     c                   eval      w_tanto = w_tanto + 1
     c                   endif
     c                   eval      w_sumb = w_sumb + sxsumb
     c                   eval      w_sumr = w_sumr + (sxrab1 + sxrab2 +
     c                                               sxrabo)
     c                   eval      w_sumn = w_sumn + (sxsumb -
     c                                      (sxrab1 + sxrab2 + sxrabo))
      *
     c     end_ok_er     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ktrl_er   begsr
      *
     c                   eval      b_ok_er = *off
      *
      * Kalkuler totalsum brutto/netto + antall alle varer
      *
      *
      * Sjekk riktig firma
      *
     c                   if        sxfirm <> l_firm
     c                   leavesr
     c                   endif
     c                   eval      vverl2_egvr = sxvare
     c     vverl2_key    chain     vverl2
     c                   if        %found(vverl2)
     c                   leavesr
     c                   endif
      *
      * Sjekk datointervall
      *
     c                   if        w_aarmnd_er < w_fraam or
     c                             w_aarmnd_er > w_tilam
     c                   leavesr
     c                   endif
      *
     c                   if        sxlety <> d_lt01 and
     c                             sxlety <> d_lt02 and
     c                             sxlety <> d_lt03 and
     c                             sxlety <> d_lt04 and
     c                             sxlety <> d_lt05 and
     c                             sxlety <> d_lt06 and
     c                             sxlety <> d_lt07 and
     c                             sxlety <> d_lt08 and
     c                             sxlety <> d_lt09 and
     c                             sxlety <> d_lt10
     c                   leavesr
     c                   endif
      *
     c                   eval      vvarl1_vare = sxvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    sxvare
5.70 c                   parm                    sxlage
5.70 c                   parm                    w_vtyp
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp <> 'L' and
5.70 c                             w_vtyp <> 'S'
     c                   leavesr
     c                   endif
      *
     c                   if        d_vtyp <> *blank and
5.70 c                             w_vtyp <> d_vtyp
     c                   leavesr
     c                   endif
      *
      * Sjekk lagernr.
      *
     c                   if        d_lager <> 0
     c                   if        sxlage <> d_la01 and
     c                             sxlage <> d_la02 and
     c                             sxlage <> d_la03 and
     c                             sxlage <> d_la04 and
     c                             sxlage <> d_la05 and
     c                             sxlage <> d_la06 and
     c                             sxlage <> d_la07 and
     c                             sxlage <> d_la08 and
     c                             sxlage <> d_la09 and
     c                             sxlage <> d_la10
     c                   leavesr
     c                   endif
     c                   else
     c                   if        sxlage <  d_flag or
     c                             sxlage >  d_tlag
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekk varesortiment
      *
     c                   if        d_fvso <> *blank
     c                   eval      vsdtl1_vsor = d_fvso
     c                   eval      vsdtl1_ldor = sxldor
     c                   eval      vsdtl1_ogrp = sxogrp
     c                   eval      vsdtl1_hgrp = sxhgrp
     c                   eval      vsdtl1_ugrp = sxugrp
     c                   eval      vsdtl1_modn = sxmodn
     c                   eval      vsdtl1_vare = sxvare
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_modn = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ldor = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found(vsdtl1)
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c
     c                   else
      *
      * Sjekk varegruppe
      *
     c                   if        d_fogr <> *zero and
     c                             sxogrp <> d_fogr
     c                   leavesr
     c                   endif
      *
     c                   if        d_fhgr <> *zero and
     c                             sxhgrp <> d_fhgr
     c                   leavesr
     c                   endif
      *
     c                   if        d_fugr <> *zero and
     c                             sxugrp <> d_fugr
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
      * Sett OK-indikator - record skal være med
      *
     c                   eval      b_ok_er = *on
      *
     c     end_ktrl_er   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kalkulering av prosentvise grenseverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_grns     begsr
      *
      * Netto omsetn. grenser (A+B)
      *
     c                   eval(h)   w_apro = d_aint
     c                   eval(h)   w_bpro = (d_aint + d_bint)
      *
      * Antall solgt grenser (X+Y)
      *
     c                   eval(h)   w_xpro = d_xint
     c                   eval(h)   w_ypro = (d_xint + d_yint)
      *
     c                   eval      w_wsumn = 0
     c                   eval      w_wanta = 0
     c                   eval      w_wanto = 0
      *
     c                   eval      b_a_ok = *off
     c                   eval      b_b_ok = *off
     c                   eval      b_c_ok = *off
     c                   eval      b_x_ok = *off
     c                   eval      b_y_ok = *off
     c                   eval      b_z_ok = *off
      *
     c     end_kalk      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk grenseverdi for omsetning og oppdater varereg.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_abc      begsr
      *
     c                   eval      w_akkpr = w_akkpr + mwabpr
      *
     c                   clear                   mabdlur
     c                   eval      mabdlu_lagk = d_lagk
     c                   eval      mabdlu_vsor = d_fvso
     c                   eval      mabdlu_ogrp = d_fogr
     c                   eval      mabdlu_hgrp = d_fhgr
     c                   eval      mabdlu_ugrp = d_fugr
     c                   eval      mabdlu_vare = mwbvar
     c     mabdlu_key    chain     mabdlur
      *
     c                   eval      %subst(mbkabc:1:1) = ''
      *
     c                   if        w_akkpr <  w_apro
     c                   eval      %subst(mbkabc:1:1) = 'A'
     c                   eval      b_a_ok = *on
     c                   else
     c                   if        w_akkpr < w_bpro
     c                   eval      %subst(mbkabc:1:1) = 'B'
     c                   eval      b_b_ok = *on
     c                   else
     c                   eval      %subst(mbkabc:1:1) = 'C'
     c                   eval      b_c_ok = *on
     c                   endif
     c                   endif
      *
     c                   exsr      sjk_abc
      *
     c                   eval      mbomse = mwbsmn
     c                   eval      mbanta = mwbant
     c                   eval      mbanto = mwbaol
     c                   eval      mbenhe = mwbenh
     c                   eval      mbabap = w_akkpr
     c                   eval      mbabpr = mwabpr
     c                   eval      mboabc = mbkabc
      * hvis abc-analyse for bestemt lager, så oppdateres lager ellers 0.
     c* dette er fjernet if        d_flag = d_tlag
     c* ingen mening når eval      mblage = d_flag
     c* utvalg på flere  else
      * lager.
     c                   eval      mblage = 0
     c*                  endif
     c     *dmy          move      UDATE         mbodat
     c                   eval      mbuser = l_user
      *
     c                   if        %found(mabdlu)
6.10 c                   time                    mbedat
6.10 c                   time                    mbetim
6.10 c                   eval      mbeusr = l_user
     c                   update    mabdlur
     c                   else
     c                   eval      mbfirm = w_firm
     c                   eval      mblagk = d_lagk
     c                   eval      mbvsor = d_fvso
     c                   eval      mbogrp = d_fogr
     c                   eval      mbhgrp = d_fhgr
     c                   eval      mbugrp = d_fugr
     c                   eval      mbvare = mwbvar
     c     *dmy          move      UDATE         mbodat
6.10 c                   time                    mbotim
6.10 c                   eval      mbuser = l_user
6.10 c                   time                    mbedat
6.10 c                   time                    mbetim
6.10 c                   eval      mbeusr = l_user
     c                   write     mabdlur
     c                   endif
      *
     c     end_abc       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk min. en A- og en B-vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_abc       begsr
      *
     c                   if        b_a_ok = *off
     c                   if        %subst(mbkabc:1:1) > 'A'
     c                   eval      %subst(mbkabc:1:1) = 'A'
     c                   eval      b_a_ok = *on
     c                   eval      b_b_ok = *off
     c                   eval      b_c_ok = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b_b_ok = *off
     c                   if        b_a_ok = *on
     c                   if        %subst(mbkabc:1:1) > 'B'
     c                   eval      %subst(mbkabc:1:1) = 'B'
     c                   eval      b_b_ok = *on
     c                   eval      b_c_ok = *off
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk grenseverdi for antall og oppdater varereg.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_xyz      begsr
      *
     c                   eval      w_akkpr = w_akkpr + mwxypr
      *
     c                   clear                   mabdlur
     c                   eval      mabdlu_lagk = d_lagk
     c                   eval      mabdlu_vsor = d_fvso
     c                   eval      mabdlu_ogrp = d_fogr
     c                   eval      mabdlu_hgrp = d_fhgr
     c                   eval      mabdlu_ugrp = d_fugr
     c                   eval      mabdlu_vare = mwbvar
     c     mabdlu_key    chain     mabdlur
      *
     c                   eval      %subst(mbkabc:2:1) = ''
      *
     c                   if        w_akkpr <  w_xpro
     c                   eval      %subst(mbkabc:2:1) = 'X'
     c                   eval      mbkxyz             = 'X'
     c                   eval      b_x_ok = *on
     c                   else
     c                   if        w_akkpr <  w_ypro
     c                   eval      %subst(mbkabc:2:1) = 'Y'
     c                   eval      mbkxyz             = 'Y'
     c                   eval      b_y_ok = *on
     c                   else
     c                   eval      %subst(mbkabc:2:1) = 'Z'
     c                   eval      mbkxyz             = 'Z'
     c                   eval      b_z_ok = *on
     c                   endif
     c                   endif
      *
     c                   exsr      sjk_xyz
      *
     c                   eval      mbxyap = w_akkpr
     c                   eval      mbxypr = mwxypr
     c                   eval      mbomse = mwbsmn
     c                   eval      mbanta = mwbant
     c                   eval      mbanto = mwbaol
     c                   eval      mboabc = mbkabc
      *
     c                   if        %found(mabdlu)
6.10 c                   time                    mbedat
6.10 c                   time                    mbetim
6.10 c                   eval      mbeusr = l_user
     c                   update    mabdlur
     c                   else
     c                   eval      mblagk = d_lagk
     c                   eval      mbvsor = d_fvso
     c                   eval      mbogrp = d_fogr
     c                   eval      mbhgrp = d_fhgr
     c                   eval      mbugrp = d_fugr
     c                   eval      mbvare = mwbvar
6.10 c                   time                    mbodat
6.10 c                   time                    mbotim
6.10 c                   eval      mbuser = l_user
6.10 c                   time                    mbedat
6.10 c                   time                    mbetim
6.10 c                   eval      mbeusr = l_user
     c                   write     mabdlur
     c                   endif
      *
     c     end_xyz       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk min. en X- og en Y-vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_xyz       begsr
      *
     c                   if        b_x_ok = *off
     c                   if        %subst(mbkabc:2:1) > 'X'
     c                   eval      %subst(mbkabc:2:1) = 'X'
     c                   eval      mbkxyz             = 'X'
     c                   eval      b_x_ok = *on
     c                   eval      b_y_ok = *off
     c                   eval      b_z_ok = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b_y_ok = *off
     c                   if        b_x_ok = *on
     c                   if        %subst(mbkabc:2:1) > 'Y'
     c                   eval      %subst(mbkabc:2:1) = 'Y'
     c                   eval      mbkxyz             = 'Y'
     c                   eval      b_y_ok = *on
     c                   eval      b_z_ok = *off
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finner %vis fordeling av omsetning og antall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pros     begsr
      *
     c     lwb1lu_key    setll     lwb1lu
     c     lwb1lu_key    reade     lwb1lu
     c                   dow       not %eof
     c                   eval(h)   w_pros  = (mwbsmn / w_tsumn) * 100
     c                   eval      mwabpr  = w_pros
     c                   eval(h)   w_pros  = (mwbaol / w_tanto) * 100
     c                   eval      mwxypr  = w_pros
6.10 c                   time                    mwbeda
6.10 c                   time                    mwbeti
6.10 c                   eval      mwbeus = l_user
     c                   update    lwb1lur
     c     lwb1lu_key    reade     lwb1lu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finner %vis fordeling av omsetning og antall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_tidl    begsr
      *
     c                   eval      mabdlu_lagk = d_lagk
     c                   eval      mabdlu_vsor = d_fvso
     c                   eval      mabdlu_ogrp = d_fogr
     c                   eval      mabdlu_hgrp = d_fhgr
     c                   eval      mabdlu_ugrp = d_fugr
     c     mabdlx_key    setll     mabdlu
     c     mabdlx_key    reade     mabdlu
     c                   dow       not %eof
     c                   delete    mabdlur
     c     mabdlx_key    reade     mabdlu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer antall abc / xyz
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_sum       begsr
      *
     c                   eval      w_totax  =  0
     c                   eval      w_totay  =  0
     c                   eval      w_totaz  =  0
     c                   eval      w_totbx  =  0
     c                   eval      w_totby  =  0
     c                   eval      w_totbz  =  0
     c                   eval      w_totcx  =  0
     c                   eval      w_totcy  =  0
     c                   eval      w_totcz  =  0
     *
     c                   eval      mabdl2_lagk = w_lagk
     c                   eval      mabdl2_vsor = w_vsor
     c                   eval      mabdl2_ogrp = w_ogrp
     c                   eval      mabdl2_hgrp = w_hgrp
     c                   eval      mabdl2_ugrp = w_ugrp
     c     mabdl2_key    setll     mabdl2
     c     mabdl2_key    reade     mabdl2
     c                   dow       not %eof
     c                   if        mbkabc = 'AX'
     c                   eval      w_totax = w_totax + 1
     c                   endif
     c                   if        mbkabc = 'AY'
     c                   eval      w_totay = w_totay + 1
     c                   endif
     c                   if        mbkabc = 'AZ'
     c                   eval      w_totaz = w_totaz + 1
     c                   endif
     c                   if        mbkabc = 'BX'
     c                   eval      w_totbx = w_totbx + 1
     c                   endif
     c                   if        mbkabc = 'BY'
     c                   eval      w_totby = w_totby + 1
     c                   endif
     c                   if        mbkabc = 'BZ'
     c                   eval      w_totbz = w_totbz + 1
     c                   endif
     c                   if        mbkabc = 'CX'
     c                   eval      w_totcx = w_totcx + 1
     c                   endif
     c                   if        mbkabc = 'CY'
     c                   eval      w_totcy = w_totcy + 1
     c                   endif
     c                   if        mbkabc = 'CZ'
     c                   eval      w_totcz = w_totcz + 1
     c                   endif
     c     mabdl2_key    reade     mabdl2
     c                   enddo
      *
     c                   eval      mabclu_vsor = w_vsor
     c                   eval      mabclu_ogrp = w_ogrp
     c                   eval      mabclu_hgrp = w_hgrp
     c                   eval      mabclu_ugrp = w_ugrp
     c                   eval      mabclu_lagk = w_lagk
     c     mabclu_key    chain     mabclu
     c                   if        %found(mabclu)
     c                   eval      maanax = w_totax
     c                   eval      maanay = w_totay
     c                   eval      maanaz = w_totaz
     c                   eval      maanbx = w_totbx
     c                   eval      maanby = w_totby
     c                   eval      maanbz = w_totbz
     c                   eval      maancx = w_totcx
     c                   eval      maancy = w_totcy
     c                   eval      maancz = w_totcz
6.10 c                   time                    maedat
6.10 c                   time                    maetim
6.10 c                   eval      maeusr = l_user
     c                   update    mabclur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les av statisstikkfil pr vare
      *
     c     sstala_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstala_paar
     c                   kfld                    sstala_vare
      *
      * Key for les av statisstikkfil pr vare
      *
     c     sstali_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstali_vare
      *
      * Key for les av arbeidsfil på prosentandel
      *
     c     lwb1l5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwb1l5_abpr
      *
      * Key for les av arbeidsfil pr. antall solgt
      *
     c     lwb1l6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwb1l6_xypr
      *
      * Key for les av arbeidsfil pr. antall solgt
      *
     c     lwb1lu_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av arbeidsfil pr. antall solgt
      *
     c     lwb1lv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwb1lv_vare
      *
      * Key for oppdatering av abc detaljer
      *
     c     mabdlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mabdlu_lagk
     c                   kfld                    mabdlu_vsor
     c                   kfld                    mabdlu_ogrp
     c                   kfld                    mabdlu_hgrp
     c                   kfld                    mabdlu_ugrp
     c                   kfld                    mabdlu_vare
      *
      * Key for sletting av utvalget
      *
     c     mabdlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mabdlu_lagk
     c                   kfld                    mabdlu_vsor
     c                   kfld                    mabdlu_ogrp
     c                   kfld                    mabdlu_hgrp
     c                   kfld                    mabdlu_ugrp
      *
      * Key for les på av abc detaljer i abc-kode rekkefølge
      *
     c     mabdl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mabdl2_lagk
     c                   kfld                    mabdl2_vsor
     c                   kfld                    mabdl2_ogrp
     c                   kfld                    mabdl2_hgrp
     c                   kfld                    mabdl2_ugrp
      *
      * Key for oppdatering av abc parameter
      *
     c     mabclu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mabclu_vsor
     c                   kfld                    mabclu_ogrp
     c                   kfld                    mabclu_hgrp
     c                   kfld                    mabclu_ugrp
     c                   kfld                    mabclu_lagk
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på erstatningsvare
      *
     c     vverlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverlr_envr
      *
      * Key for oppslag på erstatningsvare - gammelt varenr
      *
     c     vverl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverl2_egvr
      *
      * Key for oppslag på varesortiment
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
6.20 c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
6.20  *
6.20  * Key for oppslag på varesortiment  hode
6.20  *
6.20 c     vshel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      * Henter parameter
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = l_firm
     c                   exsr      kalk_grns
      *
      * Bygg opp varegruppe fra
      *
     c                   eval      f_ogrp = d_fogr
     c                   eval      f_hgrp = d_fhgr
     c                   eval      f_ugrp = d_fugr
     c                   eval      w_lagk = d_lagk
     c                   eval      w_vsor = d_fvso
     c                   eval      w_ogrp = d_fogr
     c                   eval      w_hgrp = d_fhgr
     c                   eval      w_ugrp = d_fugr
      *
     c                   movel     d_taar        w_tilam
     c                   move      d_tmnd        w_tilam
      *
     c                   movel     d_faar        w_fraam
     c                   move      d_fmnd        w_fraam
     c                   if        d_lager <> 0
     c                   move      d_lager       d_lagr
     c                   endif
      *
     c                   move      *date         w_ddat
6.20  *
6.20  * finner ut om sortiment er bygget opp med lager
6.20  *
6.20 c                   if        w_vsor <> *blank
6.20 c                   eval      vshel1_vsor = w_vsor
6.20 c                   if        w_lagk <> *blank
6.20 c                   move      w_lagk        vshel1_lage
6.20 c                   else
6.20 c                   eval      vshel1_lage = 0
6.20 c                   endif
6.20 c     vshel1_key    chain     vshel1
6.20 c                   if        %found(vshel1)
6.20 c                   eval      vsdtl1_lage = vshel1_lage
6.20 c                   else
6.20 c                   eval      vsdtl1_lage = 0
6.20 c                   endif
6.20 c                   endif
      *
      * Sett startpunkt for les
      *
     c                   eval      sstala_paar = d_faar
     c                   eval      sstala_vare = *blank
     c     sstala_key    setll     sstala
      *
     c                   eval      s_vare = *blank
      *
     c                   endsr
