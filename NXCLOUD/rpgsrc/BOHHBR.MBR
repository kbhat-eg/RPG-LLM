     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BOHHBR
      * Beskrivelse . . : Hentet inn fra butikk-bong-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190301 AMD  Skrevet om til std. ILE
      * R3.31 200301 AMD  Lagt inn mulighet for å kunne skrive ut igjen
      *                   kopier fra Historisk Bongregister
      *                   Det er også lagt på mulighet for å vise bonger
      *                   Endringer ikke anmerket da det ble for omfattende
      * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
5.30  * R5.30 131004 HFL  I bonghode og linje er bongnr. 12A.
6.NU  * R6.30 180614 nho  Endringer grunnet nummerutvidelser
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fbohela    if   e           k disk    rename(bohepfr:bohelar)
     fbohel1    if   e           k disk    rename(bohepfr:bohel1r)
     fbohelo    if   e           k disk    rename(bohepfr:bohelor)
     fbohelv    if   e           k disk    rename(bohepfr:bohelvr)
     fbmhel1    if   e           k disk    rename(bmhepfr:bmhel1r)
     fbwsdl1    if   e           k disk    rename(bwsdpfr:bwsdl1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fbmhelu    uf   e           k disk    rename(bmhepfr:bmhelur)
     fbmdtlu    uf   e           k disk    rename(bmdtpfr:bmdtlur)
     fbohhbd    cf   e             workstn
     f                                     sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
5.30 d a_mld1          s             50    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d lda            uds
     d l_ruti                800    809
     d l_chgv                844    844
     d l_wsid                901    910
     d l_user                911    920
3.15  *
3.15  * Definering av datastruktur for
3.15  * parameter ved utskrift av faktura
3.15  *
3.15 d                 ds
3.15 d  d_parm                 1     29
3.15 d  d_aarr                 1      4  0
3.15 d  d_otyp                 5      6
3.15 d  d_ffnr                 7     12  0
3.15 d  d_tfnr                13     18  0
3.15 d  d_prog                19     28
3.15 d  d_kopi                29     29
      *
      * Definering av parametere
      *
     d p_aarr          s                   like(boaarr)
     d p_firm          s                   like(bofirm)
     d p_retu          s                   like(w_alf1)
     d p_wsid          s                   like(bowsid)
     d p_aar2          s                   like(boaarr)
5.30 d p_num2          s                   like(bmnumm)
5.30 d p_bong          s                   like(bobong)
     d p_wsi2          s                   like(bowsid)
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(bofirm)
     d bohel1_aarr     s                   like(boaarr)
5.30 d bohel1_bong     s                   like(bobong)
     d bohel1_wsid     s                   like(bowsid)
     d bohela_aarr     s                   like(boaarr)
     d bohela_kuna     s                   like(bokuna)
     d bohela_navn     s                   like(bonavn)
5.30 d bohela_bong     s                   like(bobong)
     d bohela_wsid     s                   like(bowsid)
     d bohelo_aarr     s                   like(boaarr)
5.30 d bohelo_bong     s                   like(bobong)
     d bohelo_otyp     s                   like(bootyp)
     d bohelo_wsid     s                   like(bowsid)
     d bohelv_aarr     s                   like(boaarr)
     d bohelv_navn     s                   like(bonavn)
5.30 d bohelv_bong     s                   like(bobong)
     d bohelv_wsid     s                   like(bowsid)
     d bmhel1_aarr     s                   like(bnaarr)
     d bmhel1_numm     s                   like(bnnumm)
     d bmhel1_wsid     s                   like(bnwsid)
     d bmhelu_aarr     s                   like(bnaarr)
     d bmhelu_numm     s                   like(bnnumm)
     d bmhelu_wsid     s                   like(bnwsid)
     d bmdtlu_aarr     s                   like(bmaarr)
     d bmdtlu_numm     s                   like(bmnumm)
     d bmdtlu_wsid     s                   like(bmwsid)
     d bwsdl1_wsid     s                   like(bwwsid)
     d votyl1_otyp     s                   like(vaotyp)
     d aforl1_ruti     s                   like(afruti)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d w_alf1          s              1
     d w_test          s              1
     d w_feil          s              1
     d w_prog          s             10
5.21 d w_in03          s              1
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
     d w_kopi          s              1
     d w_mld1          s             50
5.30 d n               s              2  0
6.NU d w_bong6         s              8
5.30 d wsavli          s              2  0
5.30 d wsavpo          s              3  0
5.30 d w_bong          s                   like(bobong)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(9)
5.30 d nummer          c                   '0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka = *on
     c                   exsr      xh1win
     c                   goto      b2tagb
     c                   when      *inkc = *on
     c                   eval      p_retu = '9'
     c                   goto      avslutt
     c                   when      *inke = *on
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10 = *on
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2wsi2 <> *blank or
     c                             b2numm <> *blank or
     c                             b2kuna <> *blank or
     c                             b2navn <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        w_test = '1'
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'A'
     c     bohela_key    setll     bohela
     c                   when      w_seqe = 'V'
     c     bohelv_key    setll     bohelv
     c                   when      w_seqe = 'O'
     c     bohelo_key    setll     bohelo
     c                   when      w_seqe = 'B'
     c     bohel1_key    setll     bohel1
     c                   other
     c     bohel1_key    setll     bohel1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner
      * startverdi
      * på ordrenummer, men rutinen blar bakfra
      *
     c                   if        b2wsi2 <> *blank
     c                   if        w_seqe = 'B'
     c                   eval      bohel1_wsid = b2wsi2
5.30 c                   eval      bohel1_bong = b2numm
     c     bohel1_key    setll     bohel1
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
5.30 c                   if        b2numm <> *blanks
     c                   if        w_seqe = 'B'
     c                   eval      bohel1_wsid = h1wsid
5.30 c                   eval      bohel1_bong = b2numm
     c     bohel1_key    setll     bohel1
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på ordretype + ordrenummer
      *
     c                   if        b2wsi2 <> *blank
     c                   if        w_seqe = 'O'
     c                   eval      bohelo_wsid = b2wsi2
5.30 c                   eval      bohelo_bong = b2numm
     c     bohelo_key    setll     bohelo
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
5.30 c                   if        b2numm <> *blanks
     c                   if        w_seqe = 'O'
     c                   eval      bohelo_wsid = h1wsid
5.30 c                   eval      bohelo_bong = b2numm
     c     bohelo_key    setll     bohelo
     c                   goto      end_pos
     c                   endif
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på ordrenummer
      *
     c                   if        b2wsi2 <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      bohel1_wsid = b2wsi2
5.30 c                   eval      bohel1_bong = b2numm
     c     bohel1_key    setll     bohel1
     c                   goto      end_pos
     c                   endif
      *
5.30 c                   if        b2numm <> *blanks
     c                   eval      w_seqe = ' '
     c                   eval      bohel1_wsid = h1wsid
5.30 c                   eval      bohel1_bong = b2numm
     c     bohel1_key    setll     bohel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på kundenavn+vareadresse
      *
     c                   if        b2kuna <> *blank
     c                   eval      w_seqe = 'A'
5.30 c                   eval      bohela_bong = *blanks
     c                   movel     b2kuna        bohela_kuna
     c                   move      b2navn        bohela_navn
     c     bohela_key    setll     bohela
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner
      * startverdi
      * på vareadresse
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'V'
5.30 c                   eval      bohelv_bong = *blanks
     c                   eval      bohelv_navn = b2navn
     c     bohelv_key    setll     bohelv
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2wsi2 = *blank
5.30 c                   eval      b2numm = *blanks
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_test = ' '
     c                   eval      w_feil = ' '
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16 = *on
     c                   leave
     c                   endif
      *
      * Valgt post taes med tilbake til hovedprogram
      *
     c                   if        b1valg = '1'
      *
5.30 c                   exsr      chk_bong
      *                                                                                      .
5.30 c                   if        n = *zeros
     c                   eval      p_aar2 = b1aarr
     c                   eval      p_wsi2 = b1wsid
5.30 c                   move      w_bong6       p_num2
5.30 c                   endif
     c                   leave
     c                   endif
      *
      * Vise bong fra historisk bongregister
      *
     c                   if        b1valg = '5'
      *
5.30 c                   movel     b1numm        w_bong
     c                   call      'BO310R'
     c                   parm                    w_firm
     c                   parm                    b1aarr
     c                   parm                    b1wsid
5.30 c                   parm                    w_bong
     c                   eval      w_test = '1'
     c                   eval      b1valg = ' '
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Utskrift av kopi fra historisk bongregister
      *
     c                   if        b1valg = '6'
      *
5.30 c                   exsr      chk_bong
      *                                                                                      .
5.30 c                   if        n = *zeros
     c                   eval      p_retu = '6'
     c                   eval      p_aar2 = b1aarr
     c                   eval      p_wsi2 = b1wsid
5.30 c                   move      w_bong6       p_num2
     c                   exsr      utskrift
5.30 c                   endif
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
5.30  *
5.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.30  * Subrutine for kontroll av bong (nytt/gammelt system)
5.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.30  *
5.30 c     chk_bong      begsr
5.30  *
5.30  * Sjekker om 6-sifret bongnr. (gammelt butikksystem)
5.30  * Dersom N > 0 finnes ikke-nummeriske, dvs. nytt system.
5.30  * De skal ikke behandles her.
5.30  *
5.30 c                   movel     b1numm        w_bong6
5.30 c     nummer        check     w_bong6       n                                           .
5.30 c                   if        n = *zeros
5.30 c                   move      w_bong6       p_num2
5.30 c                   else
5.30 c                   move      *zeros        p_num2
5.30 c                   movel     a_mld1(2)     m1mld1
5.30 c                   movel     a_mld1(3)     m1mld2
5.30 c                   move      'N'           m1svar
5.30 c                   exsr      xm1win
5.30 c                   endif
5.30  *
5.30 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * H1 Behandle bilde for bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
      * Send Alt
      *
     c                   write     h1bld
     c                   exfmt     h1bld
      *
      * F3=Avslutt subrutinen
      *
     c     *inkc         cabeq     *on           xh1slt
      *
      * Legger inn årstall i nøkklene
      *
     c                   if        h1aarr <> b2aarr
     c                   eval      bohel1_aarr = h1aarr
     c                   eval      bohela_aarr = h1aarr
     c                   eval      bohelo_aarr = h1aarr
     c                   eval      bohelv_aarr = h1aarr
     c                   eval      b2aarr      = h1aarr
     c                   endif
      *
      * Legger inn skjerm/kasse i nøkklene
      *
     c                   if        h1wsid <> b2wsid
     c                   eval      bohel1_wsid = h1wsid
     c                   eval      bohela_wsid = h1wsid
     c                   eval      bohelo_wsid = h1wsid
     c                   eval      bohelv_wsid = h1wsid
     c                   eval      b2wsid      = h1wsid
     c                   endif
      *
      * Kun en bestemt ordre-type
      *
     c                   if        h1otyp <> *blank
     c                   eval      w_seqe = 'O'
5.30 c                   eval      bohelo_bong = *blanks
     c                   eval      bohelo_otyp = h1otyp
     c     bohelo_key    setll     bohelo
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
5.30 c                   eval      b2numm = *blanks
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
     c                   eval      h1otyp = *blank
     c                   goto      xh1slt
     c                   endif
      *
      * Ved blaing i BUTIKK-BONG-REGISTER skal det blas bakfra
      *
     c                   if        h1valg = '1'
     c                   eval      w_seqe = 'B'
     c                   if        h1wsid <> *blank
     c                   eval      bohel1_wsid = h1wsid
     c                   else
     c                   eval      bohel1_wsid = *hival
     c                   endif
     c                   eval      bohel1_aarr = h1aarr
5.30 c                   eval      bohel1_bong = *hival
     c     bohel1_key    setll     bohel1
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
5.30 c                   eval      b2numm = *blanks
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
     c                   eval      h1valg = *blank
     c                   goto      xh1slt
     c                   endif
      *
      * Dersom ingen er valgt, men ENTER er trykket
      * skal startverdi settes til bongnummer
      *
     c                   eval      w_seqe = ' '
5.30 c                   eval      bohel1_bong = *blanks
     c     bohel1_key    setll     bohel1
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
5.30 c                   eval      b2numm = *blanks
     c                   eval      b2kuna = *blank
     c                   eval      b2navn = *blank
      *
     c     xh1slt        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift fra historisk bongregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Henter inn fra Bongregister
      * til Arbeidsfil i Butikk
      *
     c                   call      'BOHNBR'
     c                   parm                    p_retu
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    p_aar2
     c                   parm                    p_wsi2
     c                   parm                    p_num2
      *
      * Hent inn opplysninger fra skjerm/kasse-register
      *
     c                   eval      bwsdl1_wsid = p_wsid
     c     bwsdl1_key    chain     bwsdl1r                            95
      *
      * Hent inn opplysninger om butikk-hode-register
      *
     c                   eval      bmhel1_aarr = p_aar2
     c                   eval      bmhel1_wsid = p_wsid
     c                   eval      bmhel1_numm = p_num2
     c     bmhel1_key    chain     bmhel1r                            95
      *
      * Hent inn opplysninger om ordretypen
      *
     c                   eval      votyl1_otyp = bnotyp
     c     votyl1_key    chain     votyl1r                            95
     c                   eval      l_ruti = vaorub
      *
      * Sjekk om denne ordretype kan skrives ut
      *
     c                   if        bnotyp <> baaokd and
     c                             bnotyp <> baaokk and
     c                             bnotyp <> baaofd and
     c                             bnotyp <> baaofk
     c                   eval      w_mld1 = a_mld1(1)
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   goto      dlt_bong
     c                   endif
      *
      * Legg inn verdi for bare henting av opplysninger
      *
     c                   eval      l_chgv = *blank
      *
      * Legg inn verdi for utskrift av kopi
      *
     c                   eval      w_kopi = '1'
      *
      * Er type bong overstyrt til A4-format (valg = 7)
      * (droppes inntil videre)
      *
     c*                  if        b1valg = '7'
     c*                  eval      bwkbon = 1
     c*                  endif
      *
      * Dersom alternativ Kasse-bong-type for kontant
      * legg inn riktig rutine
      *
     c                   if        bnotyp = baaokd
     c                   if        bwkbon = 1
     c                   movel     'BKONTA4'     l_ruti
     c                   endif
     c                   endif
      *
     c                   if        bnotyp = baaokk
     c                   if        bwkbon = 1
     c                   movel     'BKONTA4R'    l_ruti
     c                   endif
     c                   endif
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
      * Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   parm                    l_ruti
5.21 c                   parm                    l_chgv
5.21 c                   parm                    w_tvbt
5.21 c                   parm                    w_in03
5.21 c                   parm                    w_btch
      *
      * Hent inn opplysninger om formularet
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r                            95
     c                   eval      w_prog = afprog
      *
      * Kaller opp utskriftsprogram
      * (dersom det ikke er faktura)
      *
     c                   if        bnbinr = 0
     c                   call      w_prog
     c                   parm                    bwkbon
     c                   parm                    bnaarr
     c                   parm                    bnwsid
     c                   parm                    bnnumm
     c                   parm                    w_kopi
     c                   endif
      *
      * Kaller opp utskriftsprogram
      * (dersom det er faktura)
      *
     c                   if        bnbinr <> 0
     c                   eval      d_aarr = bnaarr
     c                   eval      d_otyp = bnotyp
     c                   eval      d_ffnr = bnbinr
     c                   eval      d_tfnr = bnbinr
     c                   eval      d_kopi = '1'
     c                   call      w_prog
     c                   parm                    d_parm
     c                   endif
      *
      * Sletter igjen bong fra
      * arbeidsfil i Butikk
      *
     c     dlt_bong      tag
     c                   exsr      dlt_trans
      *
      * Setter returkode = 9
      *
     c                   eval      p_retu = '9'
      *
     c     end_utskr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av linjer i butikk-hode og linje-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * For å kunne benytte standardprogrammmene i butikk for utskrift
      * av kopier fra bongregister, ble bongen hentet inn til butikk-
      * registeret.  Denne subrutine sletter dette igjen fra butikkhode og
      * linje-register.
      *
     c     dlt_trans     begsr
      *
      * Sletter records i BUTIKK-BONG-LINJE-REGISTER
      *
     c                   eval      bmdtlu_aarr = p_aar2
     c                   eval      bmdtlu_wsid = p_wsid
     c                   eval      bmdtlu_numm = p_num2
     c     bmdtlu_key    setll     bmdtlu
     c     bmdtlu_key    reade     bmdtlur                                90
      *
     c                   dow       *in90 = *off
     c                   delete    bmdtlu
     c     bmdtlu_key    reade     bmdtlur                                90
     c                   enddo
      *
      *
      *  Sletter records i BUTIKK-BONG-HODE-REGISTER
      *
     c                   eval      bmhelu_aarr = p_aar2
     c                   eval      bmhelu_wsid = p_wsid
     c                   eval      bmhelu_numm = p_num2
     c     bmhelu_key    chain     bmhelur                            91
     c                   if        *in91 = *off
     c                   delete    bmhelur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
      * Velg logisk fil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      bohela                                 15
     c                   when      w_seqe = 'V'
     c                   read      bohelv                                 15
     c                   when      w_seqe = 'O'
     c                   read      bohelo                                 15
     c                   when      w_seqe = 'B'
     c                   readp     bohel1                                 15
     c                   other
     c                   read      bohel1                                 15
     c                   endsl
      *
      * End of file
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
      * Kommet til neste firma ?
      *
     c                   if        bofirm <> w_firm
     c                   eval      *in15  = *on
     c                   goto      end_crt
     c                   endif
      *
      * Uaktuelt år ?
      *
     c                   if        boaarr <> h1aarr
     c                   eval      *in15  = *on
     c                   goto      end_crt
     c                   endif
      *
      * Uaktuell skjerm ?
      *
     c                   if        h1wsid <> *blank and
     c                             h1wsid <> bowsid
     c                   iter
     c                   endif
      *
      * Uaktuell ordretype ?
      *
     c                   if        h1otyp <> *blank and
     c                             h1otyp <> bootyp
     c                   iter
     c                   endif
      *
      * Fyll opp subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1aarr = boaarr
     c                   eval      b1wsid = bowsid
5.30 c                   eval      b1numm = bobong
     c                   eval      b1otyp = bootyp
     c                   eval      b1navn = bonavn
      *
     c                   eval      b1kuna = *blank
     c                   eval      rkunl1_kund = bokund
     c     rkunl1_key    chain     rkunl1r                            90
     c                   if        *in90 = *on
     c                   movel     bokuna        b1kuna
     c                   endif
      *
     c                   if        *in90 = *off
     c                   movel     rknavn        b1kuna
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
5.30  * M1WIN Behandle bilde for melding med svar                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
5.30 c     xm1win        begsr
      *
5.30 c                   eval      wsavli = 6
5.30 c                   eval      wsavpo = 22
5.30 c                   eval      w_arow = wsavli
5.30 c                   eval      w_acol = wsavpo
      *
5.30 c                   exfmt     m1win
      *
5.30 c                   if        *inkc = *on
5.30 c                   move      'N'           m1svar
5.30 c                   endif
      *
5.30 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for file : BONG-HODE-REGISTER
      *
     c     bohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohel1_aarr
     c                   kfld                    bohel1_wsid
5.30 c                   kfld                    bohel1_bong
      *
      * Key for file : BONG-HODE-KUNDENAVN-VAREADRESSE-REGISTER
      *
     c     bohela_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohela_aarr
     c                   kfld                    bohela_wsid
     c                   kfld                    bohela_kuna
     c                   kfld                    bohela_navn
5.30 c                   kfld                    bohela_bong
      *
      * Key for file : BONG-HODE-ORDRETYPE-REGISTER
      *
     c     bohelo_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelo_aarr
     c                   kfld                    bohelo_wsid
     c                   kfld                    bohelo_otyp
5.30 c                   kfld                    bohelo_bong
      *
      * Key for file : BONG-HODE-VAREADRESSE-REGISTER
      *
     c     bohelv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelv_aarr
     c                   kfld                    bohelv_wsid
     c                   kfld                    bohelv_navn
5.30 c                   kfld                    bohelv_bong
      *
      * Key for file : BUTIKK-HODE-REGISTER
      *
     c     bmhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmhel1_aarr
     c                   kfld                    bmhel1_wsid
     c                   kfld                    bmhel1_numm
      *
      * Key for file : BUTIKK-HODE-REGISTER
      *
     c     bmhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmhelu_aarr
     c                   kfld                    bmhelu_wsid
     c                   kfld                    bmhelu_numm
      *
      * Key for file : BUTIKK-LINJE-REGISTER
      *
     c     bmdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmdtlu_aarr
     c                   kfld                    bmdtlu_wsid
     c                   kfld                    bmdtlu_numm
      *
      * Key for file : SKJERM/KASSE-REGISTER
      *
     c     bwsdl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bwsdl1_wsid
      *
      * Key for file : BUTIKK/STATUS-REGISTER
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : FORMULAR/RUTINE-REGISTER
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_retu
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    p_aar2
     c                   parm                    p_wsi2
     c                   parm                    p_num2
      *
      * Legger inn firma i nøkklene
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn årstall i nøkklene
      *
     c                   eval      bohel1_aarr = p_aarr
     c                   eval      bohela_aarr = p_aarr
     c                   eval      bohelo_aarr = p_aarr
     c                   eval      bohelv_aarr = p_aarr
     c                   eval      b2aarr      = p_aarr
      *
      * Legger inn skjerm/kasse i nøkklene
      *
     c                   eval      bohel1_wsid = p_wsid
     c                   eval      bohela_wsid = p_wsid
     c                   eval      bohelo_wsid = p_wsid
     c                   eval      bohelv_wsid = p_wsid
     c                   eval      b2wsid      = p_wsid
      *
      * Nullstiller felter som alltid skal være blanke/null
      *
5.30 c                   eval      bohel1_bong = *blanks
5.30 c                   eval      bohela_bong = *blanks
5.30 c                   eval      bohelo_bong = *blanks
5.30 c                   eval      bohelv_bong = *blanks
      *
      * Legger inn standardverdier for valg (H1BLD)
      *
     c                   eval      h1aarr = p_aarr
     c                   eval      h1wsid = p_wsid
     c                   eval      h1otyp = ' '
     c                   eval      h1valg = ' '
      *
      * Fyller opp skjermbildet for første visning
      *
     c     bohel1_key    setll     bohel1
     c                   exsr      crt_subfile
      *
      * Henter inn butikk-status-register
      *
     c     bstsl1_key    chain     bstsl1r                            91
      *
     c                   endsr
**     M e l d i n g e r
Utskrift gjelder bare for kontant eller faktura   <---
Denne bongen kan ikke behandles
Salget er foretatt i nytt butikksystem
