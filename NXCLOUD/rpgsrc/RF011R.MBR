      /title  Dokumentscanning, Utskrift av etikett
7.03 h option(*nodebugio) datedit(*dmy-) decedit('0.')
7.03 h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      *
7.03  /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System  . . . . : ASOKON                                       *
      *  Program . . . . : RF011R                                       *
      *  Beskrivelse . . : Dokumentscanning, Utskrift av etikett        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      * --------- ------  ----------------------------------------------*
      * V5.50     201207   Endret utparm fra 16 til 13 pos            *
      * V5.60     030408   Endret utparm tilbake til 16 pos           *
6.nu  * V6.30     xxxxxx   Endret mtp nummerutvidelser                *
7.01  * V6.10     081010   Endret tilbake til 13 pga scanning til
7.01  *                    multiarkiv.
7.02  * V6.30 ask 260615   Legger ut bare bilagsnummer på etikett       *
7.03  * V6.30 bsa 200418   Innfører xml som metode for etikett
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
7.03 fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frf011p    o    e             printer
      *
      * Nummerutskrift
      *
     d                 ds
7.02 d*utparm                        18
7.02 d* utfirm                       03  0 overlay(utparm:01)
7.02 d* utaarr                       04  0 overlay(utparm:04)
7.02 d* utbinr                       08  0 overlay(utparm:08)
7.02 d* utbity                       03  0 overlay(utparm:16)
      *
      * Nummerutskrift
      *
     d                 ds
7.02 d utparm                         8
7.02 d* utfirm                       03  0 overlay(utparm:01)
7.02 d* utaarr                       04  0 overlay(utparm:04)
6.NU d  utbinr                       08  0 overlay(utparm:01)
      *
      * Variable statuskoder
      *
7.03 d b_pdfp          s              1    inz(*off)
      *
7.03 d w_kode          s              1
7.03 d w_fell          s              6
7.03 d w_type          s              2
7.03 d w_fast          s             10
7.03 d w_ruti          s             10
7.03 d w_numt          s              8  0
7.03 d w_pdf           s              1  0
7.03 d w_jnav          s             15
7.03 d w_jkey          s             41
7.03 d w_jtxt          s             35
7.03 d w_pdf_ds        s            512
7.03 d w_mtxt          s            200
7.03 d w_jstat         s              2  0
7.03 d w_jtext         s            200
7.03 d w_mark          s              5
7.03 d w_spol          s              5
7.03 d w_land          s              9
7.03 d w_str_in        s            512
7.03 d w_str_out       s            512
7.03 d w_x             s              3  0
7.03 d w_path          s            256
7.03 d w_emal          s             50
7.03 d w_draw1         s             10
7.03 d w_draw2         s             10
7.03 d w_dspl          s            256
7.03 d w_copy          s              1  0
7.03 d w_date          s               d   datfmt(*iso)
7.03 d w_time          s               t   timfmt(*iso)
7.03 d w_tidp          s              6  0
7.03 d w_fnav          s            100
7.03 d w_fad1          s            100
7.03 d w_fad2          s            100
7.03 d w_fste          s            100
7.03 d w_retx          s            100
      *
7.03 d p_ok            s              1
7.03 d p_lr            s              1
7.03 d p_filg          s             10
7.03 d p_jkey          s             41
7.03 d p_xmlf          s            256
7.03 d p_str_in        s            512
7.03 d p_str_out       s            512
      *
      * Definering av Local Data Area (LDA)
      *
     d                uds
     d  l_sfir               944    946  0
7.03 d l_poff                584    584
7.03 d l_utqm                585    594
7.03 d l_bach                595    635
7.03 d l_arch                636    636  0
7.03 d l_skje                637    637  0
7.03 d l_land                638    638  0
7.03 d l_exlp                639    639  0
7.03 d l_mail                640    640  0
7.03 d l_ruti                800    809
7.03 d  l_outq               810    819
7.03 d l_copy                838    843  0
7.03 d l_alin                856    858  0
7.03 d l_wsid                901    910
7.03 d l_user                911    920
7.03 d l_filg                931    933
7.03 d l_firm                944    946  0
7.03 D  l_avde               947    950  0
      *
7.03 d XML_Output      s            256a   Varying
7.03 d XML_path        s            256a   Varying
7.03 d                                     Inz('/home/asp/print/adb')
7.03 d jasper_mal      s            256a   inz('file:/home/asp/print/maler/+
7.03 d                                     ordrebekreftelse/ORDREBEKREF.jasper')
7.03 d jasper_logo     s            256a   Varying
7.03 d tmpdate         s               D
7.03 d tmptime         s               T
7.03 d crlf            c                   const(X'0d25')
7.03  *
7.03 d d_pdf_ds        ds
7.03 d  d_xmlm                       75
7.03 d  d_jasm                       75
7.03 d  d_logo                       75
7.03 d  d_path                      100
7.03 d  d_emal                       50
7.03 d  d_fifo                       75
7.03 d  d_fkop                        1
7.03 d  d_dtaq                        1
7.03 d  d_sign                        1
7.03  /copy prototypeb
7.03  /copy usec
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Utskrift av produkt-etikett Produktinformasjon                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legger opplysninger ut i hjelpe-felter
      *
7.02 c*                  move      l_sfir        utfirm
7.02 c*                  movel     w_aarr        utaarr
     c                   move      w_numm        utbinr
7.02 c*                  move      *zeros        utbity
     c                   movel     utparm        p1bila
      *
     c                   move      w_numm        p1binr
     c                   movel     'BILAGSNR'    p1doct
      *
      *  Skriver ut linje
      *
7.03 c                   if        b_pdfp = *off
     c                   write     label                                99
7.03 c                   else
7.03 c                   exsr      xml_bilagsnr
7.03 c                   exsr      xml_end
7.03 c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   move      *on           *inlr
     c                   return
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.03  * Subrutiner for XML/jasper integrasjon - Miljø variabler         *
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.03 c     xml_envm      begsr
7.03  * Hvis skjermvisning skal vi ikke lagre i spoolfile
7.03 c                   if        l_skje = 1
7.03 c                   eval      w_spol = 'true'
7.03 c                   else
7.03 c                   eval      w_spol = 'false'
7.03 c                   endif
7.03  *
7.03  /Free
7.03       // Finne nytt batch nummer
7.03           UpdHTMLVar('BatchNo':               w_jkey);
7.03           UpdHTMLVar('Refbatchno':             ' ');
7.03           UpdHTMLVar('Batchtype':    'BILAGSETIKETT');
7.03           UpdHTMLVar('Username':              l_user);
7.03           WrtSection('Topp');
7.03
7.03       // Finn credetial variabler
7.03           UpdHTMLVar('IPAdr':                  ' ');
7.03           UpdHTMLVar('User':                l_user);
7.03           UpdHTMLVar('PW':                     ' ');
7.03           UpdHTMLVar('Schema':        'ADB'+l_filg);
7.03           UpdHTMLVar('Companyno':    %char(l_firm));
7.03           UpdHTMLVar('Departmentnoarkiv': %char(l_avde));
7.03           WrtSection('Credential');
7.03
7.03       // Finn report command variabler
7.03           UpdHTMLVar('Jaspertemplate':      d_jasm);
7.03           UpdHTMLVar('Logo':           jasper_logo);
7.03           UpdHTMLVar('Keepspool':           w_spol);
7.03           WrtSection('ReportCommand');
7.03  /End-free
7.03  *
7.03  * Hvis *ARKIV er valgt som skriver, skal det ikke skrives ut
7.03  *
7.03 c                   if        l_utqm <> '*ARKIV'
7.03  *
7.03 c                   if        l_land = 1
7.03 c                   eval      w_land = 'LANDSCAPE'
7.03 c                   else
7.03 c                   eval      w_land = 'PORTRAIT'
7.03 c                   endif
7.03  * Sjekk at valgt outq støttes av skriveren. Kan skli gjennom hvis
7.03  * jobben legges på jobbkø.
7.03 c                   eval      p_ok = '0'
7.03 c                   call      'AP612R'
7.03 c                   parm                    l_filg
7.03 c                   parm                    l_outq
7.03 c                   parm                    p_ok
7.03  * Hvis feil utkø, logges dette i transaksjonsloggen
7.03 c                   if        p_ok = '0'
7.03 c                   eval      w_jstat = 20
7.03 c                   eval      w_jtext = 'Ugyldig utkø '+ l_outq + ' er val+
7.03 c                             gt. Ingen utskrift er generert. Utskrift kan+
7.03 c                              arkiveres.'
7.03 c                   call      'AB705R'
7.03 c                   parm                    l_bach
7.03 c                   parm                    w_jstat
7.03 c                   parm                    w_jtext
7.03 c                   else
7.03  * Hent inn skuffinformasjon for skriveren
7.03 c                   eval      w_draw1 = *blank
7.03 c                   eval      w_draw2 = *blank
7.03 c                   call      'AP611R'
7.03 c                   parm                    l_filg
7.03 c                   parm                    l_outq
7.03 c                   parm                    w_draw1
7.03 c                   parm                    w_draw2
7.03  *
7.03 c                   if        l_copy > 0
7.03 c                   eval      w_copy = l_copy
7.03 c                   else
7.03 c                   eval      w_copy = 1
7.03 c                   endif
7.03  *
7.03  /Free
7.03       // Finn print informasjon
7.03           UpdHTMLVar('Printername':         l_outq);
7.03           UpdHTMLVar('Copies':       %char(w_copy));
7.03           UpdHTMLVar('Papersource':        w_draw1);
7.03           UpdHTMLVar('Lastpagedrawer':         ' ');
7.03           UpdHTMLVar('Duplex':             'false');
7.03           UpdHTMLVar('Quality':                ' ');
7.03           UpdHTMLVar('Sorting':                ' ');
7.03           UpdHTMLVar('Staple':              'true');
7.03           UPDHTMLVar('Alignment':           w_land);
7.03           WrtSection('PrintCommand');
7.03  /End-free
7.03 c                   endif
7.03 c                   endif
7.03  *
7.03  * Vi må oversette aktuelle tekster til "xml" tegn - Firma
7.03  *
7.03 c                   eval      w_fnav = *blank
7.03 c                   eval      w_fad1 = *blank
7.03 c                   eval      w_fad2 = *blank
7.03 c                   eval      w_fste = *blank
7.03  *
7.03 c                   eval      p_str_in = *blank
7.03 c                   eval      p_lr = '0'
7.03 c                   movel     aafnav        p_str_in
7.03 c                   call      'AP613R'
7.03 c                   parm                    p_str_in
7.03 c                   parm                    p_str_out
7.03 c                   parm                    p_lr
7.03 c                   movel     p_str_out     w_fnav
7.03  *
7.03 c                   eval      p_str_in = *blank
7.03 c                   eval      p_lr = '0'
7.03 c                   movel     aafad1        p_str_in
7.03 c                   call      'AP613R'
7.03 c                   parm                    p_str_in
7.03 c                   parm                    p_str_out
7.03 c                   parm                    p_lr
7.03 c                   movel     p_str_out     w_fad1
7.03  *
7.03 c                   eval      p_str_in = *blank
7.03 c                   eval      p_lr = '0'
7.03 c                   movel     aafad2        p_str_in
7.03 c                   call      'AP613R'
7.03 c                   parm                    p_str_in
7.03 c                   parm                    p_str_out
7.03 c                   parm                    p_lr
7.03 c                   movel     p_str_out     w_fad2
7.03  *
7.03 c                   eval      p_str_in = *blank
7.03 c                   eval      p_lr = '0'
7.03 c                   movel     aafste        p_str_in
7.03 c                   call      'AP613R'
7.03 c                   parm                    p_str_in
7.03 c                   parm                    p_str_out
7.03 c                   parm                    p_lr
7.03 c                   movel     p_str_out     w_fste
7.03  *
7.03 c                   eval      w_retx = 'Bilagsetiketter'
7.03  *
7.03  /Free
7.03
7.03       // Skriv firmavariabler
7.03           UpdHTMLVar('Reporttext':     %trim(w_retx));
7.03           UpdHTMLVar('Subheader':                ' ');
7.03           UpdHTMLVar('CompanyName':           w_fnav);
7.03           UpdHTMLVar('Companyadress1':        w_fad1);
7.03           UpdHTMLVar('Companyadress2':        w_fad2);
7.03           UpdHTMLVar('Companypostalcode':        ' ');
7.03           UpdHTMLVar('Companypostoffice':     w_fste);
7.03           UpdHTMLVar('Companyphone':          aaftlf);
7.03           UpdHTMLVar('Companyfax':            aaffax);
7.03           UpdHTMLVar('Companyorgno':   %char(aafftn));
7.03           UpdHTMLVar('CompanyGLN':               '0');
7.03           UpdHTMLVar('Companygiro':    %char(aafbgi));
7.03           UpdHTMLVar('Companyemail':   %trim(aamail));
7.03           UpdHTMLVar('Companywebpage': %trim(aaweba));
7.03           WrtSection('Company');
7.03  *
7.03 c                   endsr
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.03  * Subrutiner for XML/jasper integrasjon - Bilagsnummer            *
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.03 c     xml_bilagsnr  begsr
7.03  *
7.03  /Free
7.03       // Skrive bilagsnummer
7.03           UpdHTMLVar('Bilagsnummer':   %char(p1binr));
7.03           WrtSection('FreeDocument');
7.03
7.03  /End-free
7.03  *
7.03 c                   endsr
7.03  *
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  * Subrutine for avsluttning av XML
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  *
7.03 c     xml_end       begsr
7.03  *
7.03  /Free
7.03       // Skriv siste tagg
7.03           WrtSection('Footer');
7.03
7.03  /End-free
7.03  *
7.03  * Lag navn til xml fila
7.03  *
7.03 c                   eval      XML_Output = %trim(XML_path) + l_filg +'/'+
7.03 c                             'Bilagsetik_'+ %trim(w_jkey) + '.xml'
7.03  *
7.03 c                   callp     WrtHtmlToStmf(XML_Output: 819)
7.03  * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
7.03 c                   if        d_dtaq = '1'
7.03 c                   eval      p_xmlf = xml_output
7.03 c                   eval      p_filg = 'ADB'+l_filg
7.03 c                   call      'AP629C'
7.03 c                   parm                    p_filg
7.03 c                   parm                    p_xmlf
7.03 c                   endif
7.03  *
7.03 c                   endsr
7.03  *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    w_firm            3 0
     c                   parm                    w_aarr            4 0
6.NU c                   parm                    w_numm            8 0
7.03  *
7.03  * Key for firmaregister
7.03  *
7.03 c     afirl1_key    klist
7.03 c                   kfld                    w_firm
      *
7.03 c                   eval      w_firm = l_firm
      *
7.03 c     afirl1_key    chain     afirl1r                            66
      *
      * Sjekk om vi skal bruke JASPER og XML generering av dokumenter
      *
7.03 c                   eval      b_pdfp = *off
7.03 c                   eval      w_pdf_ds = *blanks
7.03 c                   eval      w_ruti = 'RF011'
7.03  *
7.03 c                   call      'AP609R'
7.03 c                   parm                    w_ruti
7.03 c                   parm                    w_pdf
7.03 c                   parm                    w_pdf_ds
7.03 c                   eval      d_pdf_ds = w_pdf_ds
7.03  * Vi skal kjøre med jasper
7.03 c                   if        w_pdf = 1 and
7.03 c                             %trim(d_xmlm) <> *blanks
7.03 c                   eval      w_jkey = l_bach
7.03  *
7.03  * Hent inn xml templaten
7.03  *
7.03 c                   callp     GetHTMLIFS(d_xmlm :'<AS400>')
7.03 c                   callp     ClrHtmlBuffer
7.03  *
7.03  * Finn hvor xml'en skal legges
7.03  *
7.03 c                   eval      XML_path = *blanks
7.03 c                   eval      XML_path = %trim(d_path)
7.03  *
7.03  * Finn path til logo
7.03  *
7.03 c                   eval      jasper_logo = *blanks
7.03 c                   eval      jasper_logo = %trim(d_logo)
7.03  *
7.03  * Jobbnummer er allerede generert i AP600R. Overføres i *lda
7.03  *
7.03 c                   time                    w_date
7.03 c                   eval      w_numt = *zero
7.03  *
7.03 c                   if        p_jkey <> *blanks
7.03 c                   eval      w_jkey = p_jkey
7.03 c                   else
7.03  *
7.03 c                   eval      w_fell = 'NXTKLI'
7.03 c                   call      'AS100R'
7.03 c                   parm                    w_kode
7.03 c                   parm                    w_firm
7.03 c                   parm                    w_fell
7.03 c                   parm                    w_type
7.03 c                   parm                    w_fast
7.03 c                   parm                    w_numt
7.03  *
7.03  * Vi logger at jobben har startet
7.03  *
7.03 c                   eval      w_jnav = 'PDF' + %char(w_numt)
7.03 c                   eval      w_jtxt = 'Utskrift av '+%trim(l_ruti)
7.03 c                   eval      w_jtxt = w_jtxt + ' har startet !'
7.03  *
7.03 c                   call      'AB700R'
7.03 c                   parm                    w_jnav
7.03 c                   parm                    w_jkey
7.03 c                   parm                    w_jtxt
7.03  *
7.03 c                   endif
7.03  * Vi logger ar utskriftprogrammet har startet
7.03 c                   eval      w_jstat = 10
7.03 c                   eval      w_jtext = 'Utskrift av bilagsnummer etikett'
7.03 c                   call      'AB705R'
7.03 c                   parm                    l_bach
7.03 c                   parm                    w_jstat
7.03 c                   parm                    w_jtext
7.03  * Vi skriver xml for miljø formater
7.03 c                   exsr      xml_envm
7.03 c                   eval      b_pdfp = *on
7.03  *
7.03  * Hvis et eller annet er galt med parametrene, logges og kjør uten PROA
7.03  *
7.03 c                   if        %trim(d_xmlm) = *blanks
7.03 c                   eval      b_pdfp = *off
7.03  * Vi logger at noe er galt med parametrene
7.03 c                   eval      w_jstat = 20
7.03 c                   eval      w_jtext = 'Finner ikke path til xml !!! +
7.03 c                             Sjekk rutineregister !'
7.03 c                   call      'AB705R'
7.03 c                   parm                    l_bach
7.03 c                   parm                    w_jstat
7.03 c                   parm                    w_jtext
7.03 c                   endif
7.03 c                   else
7.03 c                   eval      b_pdfp = *off
7.03 c                   endif
      *
7.03 c                   if        l_bach = *blank
7.03 c                   eval      b_pdfp = *off
7.03 c                   endif
      *
7.03 c                   time                    w_time
7.03 c     *hms          move      w_time        w_tidp
      *
     c                   endsr
