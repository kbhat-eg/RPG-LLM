      /TITLE  ASOFAK Ordreinngang, utskrift av rapport
     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP801R
      * Beskrivelse . . : Ordreinngang, utskrift av rapport
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * R3.11 220900 AMD  Selgernr. utvidet fra 2 til 4 posisjoner
      * R3.31 020101 AMD  Utvidet med en ordretype til + lagt inn mulighet
      *                   for seleksjon også på historisk fakt.reg.
      *                   Endringer ikke anmerket på linjenivå
5.51  * R5.51 020107 AMD  Utvidet med mulighet til også å kunne
      *                   selektere på infokode
6.nu  * R6.30 240614 bof  Gjennomgått mtp. nummerutv.
6.31  * R6.30 060217 bof  Kompilert pga. endring i lista
6.32  * R6.30 180418 nho  Firma ikke hentet inn
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fwohel9    if   e           k disk    rename(wohepfr:wohel9r)
     f                                     prefix(fo:2)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk
      *
     ffp801p    o    e             printer
      *
      * Array
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
6.32 d l_firm                944    946  0
      *
      * Definering av datastrukturer
      *
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
5.51 d d_list                       128
     d  d_firm                        3  0 overlay(d_list)
     d  d_hist                        1    overlay(d_list:4)
     d  d_oiar                        1    overlay(d_list:5)
     d  d_bong                        1    overlay(d_list:6)
3.11 d  d_selg                        4  0 overlay(d_list:7)
3.11 d  d_avde                        4  0 overlay(d_list:11)
3.11 d  d_hot1                        2    overlay(d_list:15)
3.11 d  d_hot2                        2    overlay(d_list:17)
3.11 d  d_hot3                        2    overlay(d_list:19)
3.11 d  d_hot4                        2    overlay(d_list:21)
3.11 d  d_hot5                        2    overlay(d_list:23)
3.11 d  d_hot6                        2    overlay(d_list:25)
3.11 d  d_hvlg                        1    overlay(d_list:27)
3.11 d  d_oot1                        2    overlay(d_list:28)
3.11 d  d_oot2                        2    overlay(d_list:30)
3.11 d  d_oot3                        2    overlay(d_list:32)
3.11 d  d_oot4                        2    overlay(d_list:34)
3.11 d  d_oot5                        2    overlay(d_list:36)
3.11 d  d_oot6                        2    overlay(d_list:38)
3.11 d  d_ovlg                        1    overlay(d_list:40)
3.11 d  d_bot1                        2    overlay(d_list:41)
3.11 d  d_bot2                        2    overlay(d_list:43)
3.11 d  d_bot3                        2    overlay(d_list:45)
3.11 d  d_bot4                        2    overlay(d_list:47)
3.11 d  d_bot5                        2    overlay(d_list:49)
3.11 d  d_bot6                        2    overlay(d_list:51)
3.11 d  d_bvlg                        1    overlay(d_list:53)
5.51 d  d_inf1                        2    overlay(d_list:54)
5.51 d  d_inf2                        2    overlay(d_list:56)
5.51 d  d_inf3                        2    overlay(d_list:58)
5.51 d  d_inf4                        2    overlay(d_list:60)
5.51 d  d_inf5                        2    overlay(d_list:62)
5.51 d  d_inf6                        2    overlay(d_list:64)
5.51 d  d_ivlg                        1    overlay(d_list:66)
5.51 d  d_fdat                         d   overlay(d_list:67) datfmt(*iso)
5.51 d  d_tdat                         d   overlay(d_list:77) datfmt(*iso)
5.51 d  d_dvlg                        1    overlay(d_list:87)
5.51 d  d_dumy                       15    overlay(d_list:88)
5.51 d  d_sort                        1    overlay(d_list:103)
5.51 d  d_ityp                        2    overlay(d_list:104)
      *
      * Definering av variabler i nøkler
      *
     dwohel9_firm      s                   like(fofirm)
     dwohel9_bdat      s                   like(fobdat)
     dwohel9_otyp      s                   like(footyp)
     dwohel9_selg      s                   like(foselg)
     dra09l1_selg      s                   like(foselg)
     drkunl1_kund      s                   like(fokund)
     dvotyl1_otyp      s                   like(vaotyp)
      *
      * Definering av variable
      *
      *
     d gml_selg        s                   like(foselg)
     d w_firm          s                   like(fofirm)
     d w_timestamp     s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initiering og utskrift av hode
      *
     c                   exsr      hode
      *
     c*                  eval      w_forste = *on
      *
      * Behandler ordre-data (fOHEPF)
      *
     c                   exsr      detalje_fohe
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   time                    w_timestamp
     c     *dmy          move      w_timestamp   h1date
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1                             60
     c                   eval      h1fnav = aafnav
      *
      * Skriver header
      *
     c                   write     h1hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av ordre_data
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalje_fohe  begsr
      *
     c                   setoff                                       61
      *
     c                   read      wohel9                                 60
      *
     c                   if        *in60 = *off
     c                   eval      gml_selg = foselg
     c                   endif
      *
     c                   dow       *in60 = *off
      *
      *
     c                   if        gml_selg <> foselg
     c                   exsr      selg_brudd
     c                   endif
      *
      * Hent fortegn fra ordretype
      *
     c                   if        fobolo <> 'H'
     c                   eval      votyl1_otyp = footyp
     c
     c     votyl1_key    chain     votyl1                             61
     c                   if        *in61 = *off and vaokre = '-'
     c                   z-sub     fotots        fotots
     c                   z-sub     fototr        fototr
     c                   z-sub     fototk        fototk
     c                   endif
     c                   endif
      *
      * Skriv ut posten
      *
     c                   eval      d1numm = fonumm
     c                   eval      d1bolo = fobolo
     c                   eval      d1fdat = fobdat
     c                   eval      d1selg = foselg
     c                   eval      d1avde = foavde
     c                   eval      d1otyp = footyp
     c                   eval      d1fsum = fotots - fototr
     c                   eval      d1kstp = fototk
     c                   eval      d1dekb = d1fsum - fototk
     c                   if        d1fsum <> 0
     c                   eval      d1dekp = (d1dekb/d1fsum)*100
     c                   else
     c                   eval      d1dekp = 100
     c                   endif
      *
     c                   eval      s1fsum = s1fsum + d1fsum
     c                   eval      s1kstp = s1kstp + d1kstp
     c                   eval      s1dekb = s1dekb + d1dekb
      *
     c                   eval      t1fsum = t1fsum + d1fsum
     c                   eval      t1kstp = t1kstp + d1kstp
     c                   eval      t1dekb = t1dekb + d1dekb
      * Hent kundenavn
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1                             61
     c                   if        *in61 = *off
     c                   movel     rknavn        d1knav
     c                   else
     c                   eval      d1knav = *blanks
     c                   endif
      *
     c                   write     d1det                                30
     c                   exsr      overflow
      *
     c     ny_les        tag
      *
     c                   read      wohel9                                 60
      *
      *
     c                   enddo
      *
     c     end_les       tag
      *
     c                   exsr      selg_brudd
     c                   if        t1fsum <> 0
     c                   eval      t1dekp = (t1dekb/t1fsum)*100
     c                   else
     c                   eval      t1dekp = 100
     c                   endif
     c                   write     t1sum                                30
     c                   exsr      overflow
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     h1hdr
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av selgerbrudd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     selg_brudd    begsr
      *
      * Hent inn selger navn
      *
     c                   eval      ra09l1_selg = gml_selg
     c     ra09l1_key    chain     ra09l1                             61
     c                   if        *in61  = *off
     c                   eval      s1stxt = raitxt
     c                   else
     c                   eval      s1stxt = *blanks
     c                   endif
     c                   if        s1fsum <> 0
     c                   eval      s1dekp = (s1dekb/s1fsum)*100
     c                   else
     c                   eval      s1dekp = 100
     c                   endif
     c                   write     s1sum                                30
     c                   exsr      overflow
     c                   eval      s1fsum = 0
     c                   eval      s1kstp = 0
     c                   eval      s1dekb = 0
     c                   eval      gml_selg = foselg
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
5.51 c                   parm                    w_list          128
      *
      * Nøkkel for wOHEL9
      *
     c     wohel9_key    klist
     c                   kfld                    wohel9_firm
     c                   kfld                    wohel9_selg
     c                   kfld                    wohel9_bdat
      *
      * Key for oppslag på selgerregister
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_selg
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Key for kunderegisteret
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for ordretype-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
6.32 c                   eval      w_firm = l_firm
     c                   eval      d_list = w_list
      *
     c                   eval      h1fdat      = d_fdat
     c                   eval      h1tdat      = d_tdat
     c                   eval      h1selg      = d_selg
     c                   eval      h1avde      = d_avde
     c                   eval      h1hot1      = d_hot1
     c                   eval      h1hot2      = d_hot2
     c                   eval      h1hot3      = d_hot3
     c                   eval      h1hot4      = d_hot4
     c                   eval      h1hot5      = d_hot5
     c                   eval      h1hot6      = d_hot6
     c                   eval      h1oot1      = d_oot1
     c                   eval      h1oot2      = d_oot2
     c                   eval      h1oot3      = d_oot3
     c                   eval      h1oot4      = d_oot4
     c                   eval      h1oot5      = d_oot5
     c                   eval      h1oot6      = d_oot6
     c                   eval      h1bot1      = d_bot1
     c                   eval      h1bot2      = d_bot2
     c                   eval      h1bot3      = d_bot3
     c                   eval      h1bot4      = d_bot4
     c                   eval      h1bot5      = d_bot5
     c                   eval      h1bot6      = d_bot6

      *
     c                   eval      wohel9_bdat = d_fdat
     c                   eval      wohel9_selg = d_selg
     c                   eval      wohel9_firm = d_firm
     c                   eval      h1firm      = w_firm
     c                   eval      h1user      = l_user
     c                   eval      h1wsid      = l_wsid
     c                   time                    h1time
      *
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            60
     c                   eval      h1fnav = aafnav
      *
     c     wohel9_key    setll     wohel9
      *
     c                   endsr
