      /TITLE  ASLAGR Beregner kostpris og oppdaterer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VL002R
      * Beskrivelse . . : Beregner kostpris og oppdaterer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250699 HKO  Nytt program
5.70  * PRGR  041108 BOF  Utivdet med prisgruppe
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.20  *       220311 BOF  Utvidet med prisgiver på vare-pris
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    uf   e           k disk    rename(vvarpfr:vvarl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s             15
     d p_anta          s             11  3
     d p_inpr          s              9  2
     d p_type          s              1  0
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
5.70 d l_lage               1001   1002  0
      *
      *
      * Definering av variable i nøkler
      *
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_lety     s                   like(vplety)
     d vvprl1_enhe     s                   like(vpenhe)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprlu_prgr     s                   like(vpprgr)
6.20 d vvprlu_ldor     s                   like(vpldor)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_pdat     s                   like(vppdat)
      *
      * Definering av variable
      *
     d w_udat          s               d   datfmt(*iso)
     d w_anta          s             11  3
     d w_suma          s             11  2
     d w_sumb          s             11  2
     d w_sumt          s             11  2
     d w_kopr          s             11  2
     d w_inpr          s             11  2
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_behl          s                   like(vlbehl)
     d w_pakk          s                   like(vlpakk)
      *
8.01 d w_prgr          s              2
5.70 d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
     *
6.21 d w_vtyp          s                   like(vvvtyp)
5.70 d w_lage          s              2  0
6.20 d w_ldor          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tester om gyldig ordretype eller behandlingskode og styrer den
      * videre behandling.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom type 0
      *
     c                   if        p_type = 0
     c                   goto      avslutt
     c                   endif
      *
      * Avbryter dersom diverse-vare eller dersom vare ikke finnes
      *
     c     vvarl1_key    chain     vvarl1                             90
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
6.21 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
     c                   if        *in90 = *on or
6.21 c                             w_vtyp = 'D'
     c                   goto      avslutt
     c                   endif
      *
      * Henter gjeldende pris
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      hent_pris
6.20 c                   endif
6.20 c                   endif
      *
      * Beregner nye priser
      *
     c                   eval      w_kopr = 0
     c                   eval      w_inpr = 0
      *
      * 1=Legger inn som ny kostpris
      *
     c                   if        p_type = 1
     c                   eval      w_kopr = p_inpr
     c                   endif
      *
      * 2=Gjennomsnittsberegning av kostpris
      *
     c                   if        p_type = 2
      *
     c                   exsr      hent_saldo
      *
     c                   eval      w_anta = p_anta + w_behl
     c                   eval      w_suma = p_anta * p_inpr
     c                   eval      w_sumb = w_behl * vpkopr
      *
     c                   eval      w_sumt = w_suma + w_sumb
     c                   if        w_anta <> 0
     c                   eval      w_kopr = w_sumt / w_anta
     c                   endif
      *
     c                   endif
      *
      * 3=Legger inn som ny innkjøpspris (kostpris)
      *
     c                   if        p_type = 3
     c                   eval      w_inpr = p_inpr
     c                   if        vpkopr = 0
     c                   eval      w_kopr = p_inpr
     c                   endif
     c                   endif
      *
      * 4=Legger inn som ny innkjøpspris og kostpris.
      *
     c                   if        p_type = 4
     c                   eval      w_inpr = p_inpr
     c                   eval      w_kopr = p_inpr
     c                   endif
      *
      * Oppdaterer vare-pris
      *
     c                   if        w_kopr <> 0 and
     c                             w_kopr <> vpkopr or
     c                             w_inpr <> 0 and
     c                             w_inpr <> vpinpr
      *
6.20 c                   eval      vvprlu_ldor = vpldor
5.70 c                   eval      vvprlu_prgr = vpprgr
     c                   eval      vvprlu_lety = vplety
     c                   eval      vvprlu_enhe = vpenhe
     c                   eval      vvprlu_pdat = w_udat
     c     vvprlu_key    chain     vvprlur                            90
      *
     c                   eval      vppdat = w_udat
      *
     c                   if        w_kopr <> 0
     c                   eval      vpkopr = w_kopr
     c                   endif
      *
     c                   if        w_inpr <> 0
     c                   eval      vpinpr = w_inpr
     c                   endif
      *
6.10 c                   time                    vpedat
6.10 c                   time                    vpetim
6.10 c                   eval      vpeusr = l_user
     c                   if        *in90 = *off
     c                   update    vvprlur
     c                   else
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   endif
      *
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.20  *----------------------------------------------------------------------------
6.20  * Henter priser
6.20  *----------------------------------------------------------------------------
6.20 c     hent_pris     begsr
6.20  *
6.20  * sjekker først prisgruppe
6.20 c                   eval      b_pris = *off
6.20 c                   eval      vvprl1_prgr = w_prgr
6.20 c                   eval      vvprl1_lety = 0
6.20 c                   eval      vvprl1_enhe = vvenhl
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20 c                   if        w_udat >= vppdat
6.20 c                   eval      b_pris = *on
6.20 c                   leave
6.20 c                   endif
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   if        b_pris = *off
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_lety = 0
6.20 c                   eval      vvprl1_enhe = vvenhl
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20 c                   if        w_udat >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20 c                   endif
6.20  *
6.20 c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av saldo på alle lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_saldo    begsr
      *
      * Initierer arbeidsfelt
      *
     c                   eval      w_behl = 0
      *
      * Leser alle lagerposter på varen, og summerer reell beholdning
      *
     c     vlagl1_key    setll     vlagl1
     c                   read      vlagl1                                 91
     c                   dow       *in91 = *off and
     c                             vlfirm = w_firm and
     c                             vlvare = w_vare
      *
     c                   eval      w_behl = w_behl + vlbehl - vlpakk
      *
     c                   read      vlagl1                                 91
     c                   enddo
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_anta
     c                   parm                    p_inpr
     c                   parm                    p_type
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for les på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
      *
      * Henter nøkkelverdier fra parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
5.70 c                   eval      w_lage = l_lage
      *
     c                   endsr
