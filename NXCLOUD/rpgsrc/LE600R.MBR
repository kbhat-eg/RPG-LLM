     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LE600R
      *  Beskrivelse . . : Utskrift av telle-liste - parameterinntasting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           261007  Lagt inn mulighet for syklisk telling    BØ
6.20  *  6.20     050511  Lagt inn nye sorteringsvalg på liste     ASK
6.20  *                   Skjermbildeendring LE600D
6.21  *  6.20     070115  Lagt inn høyrejustering av felt ved bruk bsa
6.21  *                   av Field Exit. Endring kun i skjermbildetbsa
      *
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  * 800  160124  ask  Lagt inn endringer i forhåndsutfylling bilde D1BILD
8.02  * 800  050624  bsa  Kompilert pga endringer i skjermfil.
8.03  * 800  161024  ask  Lagt inn mulighet for å kalle opp pgm med batchid
8.04  * 800  250325  ask  Endring for å få lagt inn riktig lagerverdi i parameterbilde 2
      *                   NXS-20626
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fle600d    cf   e             workstn
      *
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
8.01 flbc1l1    if   e           k disk    rename(lbc1pfr:lbc1l1r)
      *
      * Datastruktur for file-member
      *
     d                 ds
     d d_memb                        10
     d  d_mbok                        1    overlay(d_memb)
     d  d_mfir                        3  0 overlay(d_memb:2)
     d  d_mnum                        6  0 overlay(d_memb:5)
      *
      * Datastruktur for parameterliste
      *
     d                 ds
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fvar                       15    overlay(d_prec:11)
     d  d_tvar                       15    overlay(d_prec:26)
     d  d_fogr                        2  0 overlay(d_prec:41)
     d  d_togr                        2  0 overlay(d_prec:43)
     d  d_fhgr                        2  0 overlay(d_prec:45)
     d  d_thgr                        2  0 overlay(d_prec:47)
     d  d_fugr                        3  0 overlay(d_prec:49)
     d  d_tugr                        3  0 overlay(d_prec:52)
     d  d_fpla                        8    overlay(d_prec:55)
     d  d_tpla                        8    overlay(d_prec:63)
     d  d_flag                        2  0 overlay(d_prec:71)
     d  d_tlag                        2  0 overlay(d_prec:73)
     d  d_list                        1  0 overlay(d_prec:75)
     d  d_hovd                        1  0 overlay(d_prec:76)
     d  d_val1                        1  0 overlay(d_prec:77)
     d  d_val2                        1  0 overlay(d_prec:78)
     d  d_val3                        1  0 overlay(d_prec:79)
     d  d_opda                        6  0 overlay(d_prec:80)
     d  d_vtyp                        1    overlay(d_prec:86)
     d  d_pans                        4    overlay(d_prec:87)
     d  d_levr                        6  0 overlay(d_prec:92)
     d  d_sykl                        1  0 overlay(d_prec:98)
      *
      * Local Data Area
      *
8.03 d lda            uds
     d l_prtf                800    809
     d l_chgv                844    844
      *
     d l_firm                944    946  0
     d l_fnav                951    980
8.03 d l_batc               1006   1015
      *
      * Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_vare          s             15
     d w_lage          s              2  0
     d w_latx          s             20
     d w_letx          s             30
     d w_satx          s             30
     d w_vtxt          s             35
     d w_ttxt          s             35
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_numm          s              6  0
     d w_qnnn          s              1
      *
     d w_batc          s                   like(lcbatc)
     d w_retu          s              1
     d b_forn          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1 Kaller opp subprogram for inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
8.03 c                   in        *dtaara
8.03 c                   if        l_batc <> ' '
8.03 c                   eval      w_batc = l_batc
8.03 c                   else
     c                   eval      w_retu = 'T'
     c                   call      'LC500R'
     c                   parm                    w_retu
     c                   parm                    w_batc
      *
     c                   if        w_retu <> *blank
     c                   goto      end_pgm
     c                   endif
8.03 c                   endif
      *
      *  Hent opplysninger fra BATCH-REGISTER
      *
     c                   eval      d_batc = w_batc
     c                   eval      lbchl1_batc = w_batc
     c     lbchl1_key    chain     lbchl1
      *
      *  Hent opplysninger fra sidetabell
      *
     c     lbchl1_key    chain     lbc1l1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bilde for valg av sortering/uttrekk osv.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   if        lcstel = 1
     c                   eval      b1list = 1
     c                   else
     c                   eval      b1list = 0
     c                   endif
     c                   if        lcnumm = 0
     c                   eval      b1hovd = 1
     c                   else
     c                   eval      b1hovd = 0
     c                   endif
      *
     c                   eval      b1val1 = 1
     c                   eval      b1val2 = 0
     c                   eval      b1val3 = 1
     c                   eval      b1vtyp = 'L'
     c                   eval      b1stel = lcstel
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      *  Behandle funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on
     c                   goto      end_pgm
     c                   when      *inkd = *on
     c                   exsr      sp_input
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekk om hovedliste er kjørt hvis subliste valgt
      *
     c                   if        b1hovd = 0
     c                   if        lcnumm = 0
     c                   eval      *in31 = *on
     c                   goto      b1taga
     c                   endif
     c                   endif
      *
      * Sjekk om syklisk telling og riktig parametervalg
      *
     c                   if        lcstel = 1
     c                   if        b1list = 0
     c                   eval      *in32 = *on
     c                   goto      b1taga
     c                   endif
     c                   if        b1vtyp <> 'L'
     c                   eval      *in33 = *on
     c                   goto      b1taga
     c                   endif
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_val1 = b1val1
     c                   eval      d_val2 = b1val2
     c                   eval      d_val3 = b1val3
     c                   eval      d_list = b1list
     c                   eval      d_hovd = b1hovd
     c                   eval      d_opda = b1opda
     c                   eval      d_vtyp = b1vtyp
     c                   eval      d_sykl = b1stel
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bilde for registrering av utvalgsparametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     d1tag         tag
      *
8.01  *  Nullstiller eller forhåndsutfyller variable i skjermbildet
      *
8.01 c                   eval      *in39 = *off
8.01 c                   if        b1hovd = 1
8.01 c                   eval      d1tvar = 99999999
8.01 c                   eval      d1togr = 99
8.01 c                   eval      d1thgr = 99
8.01 c                   eval      d1tugr = 999
8.04 c                   eval      d1flag = %editc(l1lage:'X')
8.04 c                   eval      d1tlag = %editc(l1lage:'X')
8.01 c                   eval      *in39 = *on
8.01 c                   else
     c                   eval      d1fvar = 0
     c                   eval      d1tvar = 0
     c                   eval      d1togr = 0
     c                   eval      d1thgr = 0
     c                   eval      d1fugr = 0
     c                   eval      d1tugr = 0
     c                   eval      d1fpla = *blank
     c                   eval      d1tpla = *blank
     c                   eval      d1flag = *blank
     c                   eval      d1tlag = *blank
     c                   endif
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c     d1taga        tag
     c                   exfmt     d1bld
      *
      *  Behandle funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on
     c                   goto      b1taga
     c                   when      *inkd = *on
     c                   exsr      sp_input
     c                   goto      d1taga
     c                   endsl
      *
      *  Sjekk om til-felter skal få standardverdi
      *
     c                   if        d1tvar = 0
     c                   eval      d1tvar = 99999999
     c                   endif
      *
     c                   if        d1togr = 0
     c                   eval      d1togr = 99
     c                   endif
      *
     c                   if        d1thgr = 0
     c                   eval      d1thgr = 99
     c                   endif
      *
     c                   if        d1tugr = 0
     c                   eval      d1tugr = 999
     c                   endif
      *
8.01 c*                  if        d1tpla = *blank
8.01 c*                  eval      d1tpla = '99999999'
8.01 c*                  endif
      *
     c                   if        d1flag = *blank
     c                   move      '00'          d1flag
     c                   endif
      *
     c                   if        d1tlag = *blank
     c                   move      '99'          d1tlag
     c                   endif
      *
     c                   if        d1tlag = *blank
     c                   if         lcstel = 1
     c                   eval       d1tlag = d1flag
     c                   endif
     c                   endif
      *
      * Hvis standardverdier er lagt inn - vis bilde på nytt
      *
     c                   if        b_forn = *off
     c                   eval      b_forn = *on
     c                   goto      d1taga
     c                   endif
      *
      *  Sjekk, Er varenummer fra større en varenummer til
      *
     c                   if        d1fvar <> 0
     c                   if        d1tvar < d1fvar
     c                   eval      *in31 = *on
     c                   goto      d1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Er Overgruppe fra større en overgruppe til
      *
     c                   if        d1fogr <> 0
     c                   if        d1togr < d1fogr
     c                   eval      *in32 = *on
     c                   goto      d1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Er Hovedgruppe fra større en hovedgruppe til
      *
     c                   if        d1fhgr <> 0
     c                   if        d1thgr < d1fhgr
     c                   eval      *in33 = *on
     c                   goto      d1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Er Undergruppe fra større en undergruppe til
      *
     c                   if        d1fugr <> 0
     c                   if        d1tugr < d1fugr
     c                   eval      *in34 = *on
     c                   goto      d1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Er Lagerplass fra større en lagerplass til
      *
     c                   if        d1fpla <> *blank
     c                   if        d1tpla < d1fpla
     c                   eval      *in35 = *on
     c                   goto      d1taga
     c                   endif
     c                   endif
      *
      *  Sjekk, Er Lagernummer fra større en lagernummer til
      *
     c                   if        d1tlag < d1flag
     c                   eval      *in36 = *on
     c                   goto      d1taga
     c                   endif
      *
      *  Sjekk, Er det syklisk telling og valgt flere lager
      *
     c                   if        lcstel = 1 and
     c                             d1tlag <> d1flag
     c                   eval      *in38 = *on
     c                   goto      d1taga
     c                   endif
      *
      *  Sjekk, Er det subliste skal bare et lager brukes
      *
     c                   if        b1hovd = 0 and
     c                             d1flag <> d1tlag
     c                   eval      *in37 = *on
     c                   goto      d1taga
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   if        d1fvar <> 0
     c                   movel     d1fvar        d_fvar
     c                   endif
     c                   if        d1tvar <> 0
     c                   movel     d1tvar        d_tvar
     c                   endif
     c                   eval      d_fogr = d1fogr
     c                   eval      d_togr = d1togr
     c                   eval      d_fhgr = d1fhgr
     c                   eval      d_thgr = d1thgr
     c                   eval      d_fugr = d1fugr
     c                   eval      d_tugr = d1tugr
     c                   eval      d_fpla = d1fpla
     c                   eval      d_tpla = d1tpla
     c                   eval      d_pans = d1pans
     c                   eval      d_levr = d1levr
      *
     c                   if        d1flag = *blank
     c                   eval      d_flag = 0
     c                   else
     c                   move      d1flag        d_flag
     c                   endif
      *
     c                   if        d1tlag = *blank
     c                   eval      d_tlag = 99
     c                   else
     c                   move      d1tlag        d_tlag
     c                   endif
      *
      *  Henter siste nummer for File-member
      *
     c                   call      'LÅ900R'
     c                   parm                    w_qnnn
     c                   parm                    w_numm
      *
     c                   eval      d_mbok = 'K'
     c                   eval      d_mnum = w_numm
      *
      *  Kaller opp program for videre behandling
      *
     c                   call      'LE601C'
     c                   parm                    d_prec
     c                   parm                    d_memb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      * Spørring på varetyper
      *
     c                   if        w_arcd = 'B1BLD' and
     c                             w_afld = 'B1VTYP'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    b1vtyp
     c                   parm                    w_ttxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_arcd = 'D1BLD'
      *
      *  Spørring på vare
      *
     c                   if        w_afld = 'D1FVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        d1fvar
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_afld = 'D1TVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        d1tvar
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på overgruppe
      *
     c                   if        w_afld = 'D1FOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    d1fogr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_afld = 'D1TOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    d1togr
     c                   parm                    w_otxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på hvedgruppe
      *
     c                   if        w_afld = 'D1FHGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    d1fogr
 001 C                   parm                    d1fhgr
 001 C                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_afld = 'D1THGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    d1togr
 001 C                   parm                    d1thgr
 001 C                   parm                    w_htxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på undergruppe
      *
     c                   if        w_afld = 'D1FUGR'
 001 C                   call      'VG512R'
 001 C                   parm                    w_firm
 001 C                   parm                    d1fogr
 001 C                   parm                    d1fhgr
 001 C                   parm                    d1fugr
 001 C                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_afld = 'D1TUGR'
 001 C                   call      'VG512R'
 001 C                   parm                    w_firm
 001 C                   parm                    d1togr
 001 C                   parm                    d1thgr
 001 C                   parm                    d1tugr
 001 C                   parm                    w_utxt
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på lager
      *
     c                   if        w_afld = 'D1FLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   move      w_lage        d1flag
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_afld = 'D1TLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   move      w_lage        d1tlag
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på produktansvarlig
      *
     c                   if        w_afld = 'D1PANS'
     c                   call      'RA526R'
     c                   parm                    w_firm
     c                   parm                    d1pans
     c                   parm                    w_satx
     c                   goto      sp_end
     c                   endif
      *
      *  Spørring på leverandør
      *
     c                   if        w_afld = 'D1LEVR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    d1levr
     c                   parm                    w_letx
     c                   goto      sp_end
     c                   endif
      *
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.03  *
8.03  * Definisjon av local data area for output
8.03  *
8.03 c     *dtaara       define    *lda          lda
      *
      * Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      d_mfir = l_firm
      *
     c                   endsr
