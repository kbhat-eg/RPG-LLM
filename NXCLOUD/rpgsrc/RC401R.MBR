     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RC401R                                              *
      * Beskrivelse . . : Inkasso - sletting av saker                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                       *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frcnfl1    if   e           k disk    rename(rcnfpfr:rcnfl1r)
     frcnflu    uf   e           k disk    rename(rcnfpfr:rcnflur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frc401p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rcnfl1_kund     s                   like(rkkund)
     d rcnflu_nref     s                   like(rcnref)
     d rcnflu_nsak     s                   like(rcnsak)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_type          s              1
     d w_edat          s               d   datfmt(*iso)
      *
     d w_fast          s              6  0
      *
     d w_tota          s              7  0
     d w_anta          s              5  0
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
     d w_slett         s              1
      *
      * Les saksregister
      *
     c     rcnfl1_key    setll     rcnfl1                                                    .
     c     rcnfl1_key    reade     rcnfl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rcnsta = ' '
     c                   goto      neste
     c                   endif
      *
     c                   eval      rkunl1_kund = rcnask
     c     rkunl1_key    chain     rkunl1                             60
      *
     c                   exsr      kontroll
      *
     c                   if        w_slett = ' '
      *
     c                   exsr      utskrift
      *
     c                   if        w_type  = 'N'
      *
     c                   eval      rcnflu_nref = rcnref
     c                   eval      rcnflu_nsak = rcnsak
     c     rcnflu_key    chain     rcnflu                             90
      *
     c                   if        *in90 = *off
     c                   delete    rcnflur
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c     neste         tag
      *
     c     rcnfl1_key    reade     rcnfl1                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   eval      t1tota = w_tota
     c                   eval      t1anta = w_anta
      *
     c                   write     t1tot
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kontroll      begsr
      *
      * Tell opp antall kunder
      *
     c                   eval      w_tota = w_tota + 1
     c                   eval      w_slett = ' '
      *
      * Sjekk dato for avslutning
      *
     c                   if        rcndat  > w_edat
     c                   eval      w_slett = 'X'
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Tell antall kunder som kan/blir slettet
      *
     c                   eval      w_anta = w_anta + 1
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   write     d1det
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_type
     c                   parm                    w_edat
      *
      * Key for saksregister
      *
     c     rcnfl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av saksregister
      *
     c     rcnflu_key    klist
     c                   kfld                    rcnflu_nref
     c                   kfld                    rcnflu_nsak
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   eval      w_firm = l_firm
      *
     c                   if        w_type = 'J'
     c                   eval      *in11 = *on
     c                   else
     c                   eval      *in11 = *off
     c                   endif
      *
      * Skriv heading på første side
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
