      /TITLE  ASOFAK Ordreinngang, hente ordre fra Ofak
     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP802R
      * Beskrivelse . . : Ordreinngang, hente ordre fra Ofak
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * R3.11 220900 AMD  Selgernr. utvidet fra 2 til 4 posisjoner
      * R3.31 020101 AMD  Utvidet med en ordretype til + lagt inn mulighet
      *                   for seleksjon også på historisk fakt.reg.
      *                   Endringer ikke anmerket på linjenivå
5.51  * R5.51 020107 AMD  Utvidet med mulighet til også å kunne
      *                   selektere på infokode
      *
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel9    if   e           k disk    rename(fohepfr:fohel9r)
     fwohepf    o  a e           k disk
      *
      *
      * Array
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av datastrukturer
      *
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
5.51 d d_list                       128
     d  d_firm                        3  0 overlay(d_list)
     d  d_hist                        1    overlay(d_list:4)
     d  d_oiar                        1    overlay(d_list:5)
     d  d_bong                        1    overlay(d_list:6)
3.11 d  d_selg                        4  0 overlay(d_list:7)
3.11 d  d_avde                        4  0 overlay(d_list:11)
3.11 d  d_hot1                        2    overlay(d_list:15)
3.11 d  d_hot2                        2    overlay(d_list:17)
3.11 d  d_hot3                        2    overlay(d_list:19)
3.11 d  d_hot4                        2    overlay(d_list:21)
3.11 d  d_hot5                        2    overlay(d_list:23)
3.11 d  d_hot6                        2    overlay(d_list:25)
3.11 d  d_hvlg                        1    overlay(d_list:27)
3.11 d  d_oot1                        2    overlay(d_list:28)
3.11 d  d_oot2                        2    overlay(d_list:30)
3.11 d  d_oot3                        2    overlay(d_list:32)
3.11 d  d_oot4                        2    overlay(d_list:34)
3.11 d  d_oot5                        2    overlay(d_list:36)
3.11 d  d_oot6                        2    overlay(d_list:38)
3.11 d  d_ovlg                        1    overlay(d_list:40)
3.11 d  d_bot1                        2    overlay(d_list:41)
3.11 d  d_bot2                        2    overlay(d_list:43)
3.11 d  d_bot3                        2    overlay(d_list:45)
3.11 d  d_bot4                        2    overlay(d_list:47)
3.11 d  d_bot5                        2    overlay(d_list:49)
3.11 d  d_bot6                        2    overlay(d_list:51)
3.11 d  d_bvlg                        1    overlay(d_list:53)
5.51 d  d_inf1                        2    overlay(d_list:54)
5.51 d  d_inf2                        2    overlay(d_list:56)
5.51 d  d_inf3                        2    overlay(d_list:58)
5.51 d  d_inf4                        2    overlay(d_list:60)
5.51 d  d_inf5                        2    overlay(d_list:62)
5.51 d  d_inf6                        2    overlay(d_list:64)
5.51 d  d_ivlg                        1    overlay(d_list:66)
5.51 d  d_fdat                         d   overlay(d_list:67) datfmt(*iso)
5.51 d  d_tdat                         d   overlay(d_list:77) datfmt(*iso)
5.51 d  d_dvlg                        1    overlay(d_list:87)
5.51 d  d_dumy                       15    overlay(d_list:88)
5.51 d  d_sort                        1    overlay(d_list:103)
5.51 d  d_ityp                        2    overlay(d_list:104)
      *
      * Definering av variabler i nøkler
      *
     dfohel9_firm      s                   like(fofirm)
     dfohel9_bdat      s                   like(fobdat)
     dfohel9_otyp      s                   like(footyp)
     dfohel9_selg      s                   like(foselg)
     dra09pf_selg      s                   like(foselg)
     drkunpf_kund      s                   like(fokund)
      *
      * Definering av variable
      *
      *
     d gml_selg        s                   like(foselg)
     d w_firm          s                   like(fofirm)
     d w_timestamp     s               d   datfmt(*iso)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler ordre-data (FOHEPF)
      *
     c                   if        d_oiar = '1'
     c                   exsr      detalje_fohe
     c                   endif
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av i ordre-data
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalje_fohe  begsr
      *
     c                   read      fohel9                                 60
      *
     c                   if        *in60 = *off
     c                   eval      gml_selg = foselg
     c                   endif
      *
     c                   dow       *in60 = *off
      *
      *
      * Velg ut riktig firma
      *
     c                   if        fofirm <> d_firm
     c                   goto      end_les
     c                   endif
      *
      * Velg ut riktig selger (enkelt eller alle)
      *
     c                   if        d_selg <> 0
     c                   if        foselg <> d_selg
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
      * Velg ut riktig avdeling (enkelt eller alle)
      *
     c                   if        d_avde <> 0
     c                   if        foavde <> d_avde
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
      * Velg ut riktig informasjonstype/årsak (enkelt eller alle)
      *
     c                   if        d_ityp <> *blank
     c                   if        foityp <> d_ityp
     c                   goto      ny_les
     c                   endif
     c                   endif
      *
      * Velg ut riktig ordretype
      *
     c                   if        d_ovlg <> *blanks
     c                   if        d_ovlg = '1'
     c                   if           footyp = d_oot1
     c                             or footyp = d_oot2
     c                             or footyp = d_oot3
     c                             or footyp = d_oot4
     c                             or footyp = d_oot5
     c                             or footyp = d_oot6
     c                   else
     c                   goto      ny_les
     c                   endif
     c                   endif
     c                   if        d_ovlg = '2'
     c                   if           footyp = d_oot1
     c                             or footyp = d_oot2
     c                             or footyp = d_oot3
     c                             or footyp = d_oot4
     c                             or footyp = d_oot5
     c                             or footyp = d_oot6
     c                   goto      ny_les
     c                   endif
     c                   endif
     c                   endif
5.51  *
5.51  * Behandling av infotype
5.51  *
5.51  *
5.51  * Send først ut de ordre som ikke
5.51  * har infotype, og det er valgt
5.51  * en selektering som sier 'ta med'
5.51  *
5.51 c                   if        d_ivlg = '1' and
5.51 c                             foityp = *blank
5.51 c                   goto      ny_les
5.51 c                   endif
5.51  *
5.51  * Velg ut riktig infotype
5.51  *
5.51 c                   if        d_ivlg <> *blanks
5.51 c                   if        d_ivlg = '1'
5.51 c                   if           foityp = d_inf1
5.51 c                             or foityp = d_inf2
5.51 c                             or foityp = d_inf3
5.51 c                             or foityp = d_inf4
5.51 c                             or foityp = d_inf5
5.51 c                             or foityp = d_inf6
5.51 c                   else
5.51 c                   goto      ny_les
5.51 c                   endif
5.51 c                   endif
5.51 c                   if        foityp <> *blank
5.51 c                   if        d_ivlg = '2'
5.51 c                   if           foityp = d_inf1
5.51 c                             or foityp = d_inf2
5.51 c                             or foityp = d_inf3
5.51 c                             or foityp = d_inf4
5.51 c                             or foityp = d_inf5
5.51 c                             or foityp = d_inf6
5.51 c                   goto      ny_les
5.51 c                   endif
5.51 c                   endif
5.51 c                   endif
5.51 c                   endif
      *
      * Skal leveringsdato benyttes ?
      *
     c                   if        d_dvlg = '1'
     c                   eval      fobdat = foldat
     c                   endif
      *
      * Velg ut riktig dato (enkelt eller fra/til)
      *
     c                   if        d_tdat =  *loval
      *
     c                   if        fobdat <> d_fdat
     c                   goto      ny_les
     c                   endif
     c                   else
     c                   if        fobdat > d_tdat or fobdat < d_fdat
     c                   goto      ny_les
     c                   endif
      *
     c                   endif
      *
      * Skriv ut posten
      *
     c                   write     wohepfr
      *
     c     ny_les        tag
      *
     c                   read      fohel9                                 60
      *
      *
     c                   enddo
      *
     c     end_les       tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
5.51 c                   parm                    w_list          128
      *
      * Nøkkel for FOHEL9
      *
     c     fohel9_key    klist
     c                   kfld                    fohel9_firm
     c                   kfld                    fohel9_selg
     c                   kfld                    fohel9_bdat
      *
     c                   eval      d_list = w_list
      *
     c                   eval      fobolo = 'O'
     c                   eval      fohel9_bdat = d_fdat
     c                   eval      fohel9_firm = d_firm
     c                   eval      fohel9_selg = d_selg
      *
     c     fohel9_key    setll     fohel9
      *
     c                   endsr
