     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JO702R
      * Beskrivelse . . : Matching, Oppdater fra EVR - fjern slettede varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290297 HKO  Nytt program
      *       270298 HKO  Endret logikk for å unngå å fjerne varer som
      *                   er overført EVR fra matche-regsiteret
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmatl1    if   e           k disk    rename(jmatpfr:jmatl1r)
     fjmatlu    uf   e           k disk    rename(jmatpfr:jmatlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jofirm)
      *
      * Definering av variable i nøkkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d jmatlu_var1     s                   like(jovar1)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser matche-register. Dersom varen ikke er overført EVR, og varen
      * ikke finnes i EVR slettes varen fra matche-registeret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     jmatl1_key    setll     jmatl1
     c     jmatl1_key    reade     jmatl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        jovkod = 0
     c                   eval      vvarl1_vare = jovar1
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   eval      jmatlu_var1 = jovar1
     c     jmatlu_key    chain     jmatlur                            91
     c                   delete    jmatlur
     c                   endif
     c                   endif
      *
     c     jmatl1_key    reade     jmatl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for sekvensiell les på matcheregister
      *
     c     jmatl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare i EVR
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppdatering av matcheregister
      *
     c     jmatlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jmatlu_var1
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
