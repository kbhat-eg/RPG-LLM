     H/TITLE            : Utskrift av manglende/matchende ordre/fakturanr.
     H decedit('0,') dftname(fs244) datedit(*dmy.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF444R
      * Beskrivelse . . : Utskrift av manglende/matchende ordre/fakturanr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *              KOB  Nytt program oktober 97
      *
6.NU  * R6.30 200614 nho  Endringer grunnet nummerutvidelser
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     *
     Fskwwpf    if   e           k disk
     Fliste     o    f  132        printer oflind(*inof)
     *
     *************************  T A B E L L E R  ************************
     *
     D txt             s             70    dim(6) ctdata perrcd(1)              HEADINGTEXTER
     *
     * Interne arbeidsfelt
     *
     D fstanr          s                   like(sfbinr)
     D stanr1          s                   like(sfbinr)
     D stanr2          s                   like(sfbinr)
     D diff            s                   like(sfbinr)
     D fra             s                   like(sfbinr)
     D til             s                   like(sfbinr)
     D h1txt           s             70
     D h2txt           s             70
     D kodkey          s              9  0
     D linjer          s              2  0
     D subÅr           s              2  0
     D subdag          s              2  0
     D wwdato          s              6  0
     D l3bida          s              6  0
     *
     * Felt  i local
     *
     D                uds
     D pakode                300    301
     D pakod1                300    300
     D pakod2                301    301
6.nu D paofra                302    309  0
6.nu D paotil                310    317  0
6.nu D patype                318    325
6.nu D patyp1                318    319
6.nu D patyp2                320    321
6.nu D patyp3                312    323
6.nu D patyp4                324    325
6.nu D papaar                326    329  0
     *
     D lcuser                911    920
     D lcfirm                944    946  0
     D lcnavn                951    980
     *
     /EJECT
     ***************************  CALCULATION  **************************
     *
     *  Les register - skriv liste
     *
     C     start         tag
     *
B001 C                   if        sffirm > lcfirm
 001 C                   goto      slutt
E001 C                   endif
     *
     C                   read      skwwpfr                                60
B001 C                   if        *in60 = *on
 001 C                   goto      slutt
E001 C                   endif
     *
     * Flytt ordre/fakturanr. til
     * 1. eller 2. felt for utskrift
     *
B001 C                   if        pakod1 = 'F'
 001 C                   move      sfbinr        stanr1
 001 C                   move      sfornr        stanr2
X001 C                   else
 001 C                   move      sfornr        stanr1
 001 C                   move      sfbinr        stanr2
E001 C                   endif
     *
     * Test om sist leste ordre/fakturanr.
     * > til, hvis ja - avslutt
     *
B001 C                   if        stanr1 > paotil
 001 C                   goto      slutt
E001 C                   endif
     *
     * Test ordre/fakturanr. mot forrige nr.
     * hvis like - les neste
     *
B001 C                   if        stanr1 = fstanr
 001 C                   goto      start
E001 C                   endif
     *
     * Test ordre/fakturanr. mot forrige nr.
     * hvis diff >1, skriv liste                                      E
     *
     C                   eval      diff = stanr1 - fstanr
     *
B001 C                   if        diff > 1
 001 C                   eval      fra = fstanr + 1
 001 C                   eval      til = stanr1 - 1
 001 *
B002 C                   if        fra <> til
 002 C                   except    linje2
E002 C                   endif
 001 *
 001 C                   except    linje1
 001 C                   eval      linjer = linjer + 1
 001 *
E001 C                   endif
     *
     * Skriv linje3 bare hvis valg = match
     *
B001 C                   if        pakod2 = 'M'
     C     *dmy          move      sfbida        l3bida
 001 C                   except    linje3
 001 C                   eval      linjer = linjer + 1
E001 C                   endif
     *
B001 C                   if        linjer = 40
 001 C                   except    head1
 001 C                   eval      linjer = 0
E001 C                   endif
     *
     C                   move      stanr1        fstanr
     *
     C                   goto      start
     /EJECT
     *---------------------------------------------------------------
     * END OF JOB
     *---------------------------------------------------------------
     *
     C     slutt         tag
     C                   eval      *inlr = *on
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *inzsr        begsr
     *
      * Legge inn firma-
      * nummer og navn
      * fra parameter
      *
     C                   eval      fstanr = 0
     C                   eval      fra = 0
     C                   eval      til = 0
     C                   eval      diff = 0
     C                   eval      linjer = 0
     C                   eval      h1txt = *blanks
     C                   eval      h2txt = *blanks
     *
     * Klargjør heading avhengig av parametervalg
     *
B001 C                   if        pakode = 'O '
 001 C                   movel     txt(1)        h1txt
 001 C                   movel     txt(5)        h2txt
E002 C                   endif
     *
B001 C                   if        pakode = 'OM'
 001 C                   movel     txt(2)        h1txt
 001 C                   movel     txt(5)        h2txt
E001 C                   endif
     *
B001 C                   if        pakode = 'F '
 001 C                   movel     txt(3)        h1txt
 001 C                   movel     txt(6)        h2txt
E001 C                   endif
     *
B001 C                   if        pakode = 'FM'
 001 C                   movel     txt(4)        h1txt
 001 C                   movel     txt(6)        h2txt
E001 C                   endif
     *
     C                   move      paofra        stanr1
     *
     C                   except    head1
     *
     C                   endsr
     *
     /EJECT
     *******************  O U T P U T  S E C T I O N  *******************
     *
     *
     Oliste     e            head1          1 03
     O                                            6 'Firma:'
     O                       lcfirm              10
     O                       lcnavn              41
     O                                           64 'Utskrift av ordrenummer/'
     O                                           88 'fakturanummer som matche'
     O                                           97 'r/mangler'
     O                       udate         y    116
     O                                          126 'Side'
     O                       page               132
     *
     O          e            head1          1
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O                                          132 '------------'
     *
     O          e            head1          1
     O                       h1txt               70
     O                                           30 'Fra:'
     O                       paofra              37
6.nu O                                           46 'Til:'
6.nu O                       paotil              53
     O                                           70 'År'
     O                       papaar        z     76
     *
     O          e            head1          1
     O                       h2txt               70
     O                                           60 'Fakturadato'
     O                                           75 'Fakturakunde'
     O                                           85 'Varekunde'
     *
     O          e            head1          1
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O                                          132 '------------'
     *
     O          e            linje1         1
     O                       fra                 10
     O                                           31 'Mangler'
     *
     O          e            linje2         0
     O                                           16 '-----'
     O                       til                 23
     *
     O          e            linje3         1
     O                       stanr1              10
     O                       stanr2              43
     O                       l3bida              58 '  .  .  '
     O                       sffkun              72
     O                       sfvkun              83
     *
     **********************  T A B E L L E R  ***************************
**
   Ordrenr. mangler
   Ordrenr. matcher
   Fakturanr. mangler
   Fakturanr. matcher
   Ordrenr.                       Fakt./Kred.nr.
   Fakt./Kred.nr.                   Ordrenr.
