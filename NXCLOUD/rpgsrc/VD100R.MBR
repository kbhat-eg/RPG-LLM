     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD100R
      * Beskrivelse . . : Varesortiment-linje, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270599 HKO  Nytt program
      *       130803 HKO  Endret rekkefølge på nivå og lagt inn modul.
      *                   Lagt inn plukke-funksjonalitet
      *       120105 HKO  Rettet feil ved posisjonering på varegruppe
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  051110 bof  Utvidet med lager
6.21  * 6.20  221110 bof  Krav om varenr hvis C, leverandør hvis P
7.xx  * 7.00  190220 BKV  Endringer pga Cloud
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvsdtlr    if   e           k disk    rename(vsdtpfr:vsdtlrr)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvsdtl2    if   e           k disk    rename(vsdtpfr:vsdtl2r)
     fvsdtl3    if   e           k disk    rename(vsdtpfr:vsdtl3r)
     fvsdtl4    if   e           k disk    rename(vsdtpfr:vsdtl4r)
     fvsdtl5    if   e           k disk    rename(vsdtpfr:vsdtl5r)
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.21 fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
      *
     fvd100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vdfirm)
     d p_vsor          s                   like(vdvsor)
6.20 d p_lage          s                   like(vdlage)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vsdtlu_ldor     s                   like(vdldor)
     d vsdtlu_ogrp     s                   like(vdogrp)
     d vsdtlu_hgrp     s                   like(vdhgrp)
     d vsdtlu_ugrp     s                   like(vdugrp)
     d vsdtlu_modn     s                   like(vdmodn)
     d vsdtlu_vare     s                   like(vdvare)
     d vsdtlr_ldor     s                   like(vdldor)
     d vsdtlr_ogrp     s                   like(vdogrp)
     d vsdtlr_hgrp     s                   like(vdhgrp)
     d vsdtlr_ugrp     s                   like(vdugrp)
     d vsdtlr_modn     s                   like(vdmodn)
     d vsdtlr_vare     s                   like(vdvare)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
     d vsdtl2_ogrp     s                   like(vdogrp)
     d vsdtl2_hgrp     s                   like(vdhgrp)
     d vsdtl2_ugrp     s                   like(vdugrp)
     d vsdtl3_hgrp     s                   like(vdhgrp)
     d vsdtl4_ugrp     s                   like(vdugrp)
     d vsdtl5_modn     s                   like(vdmodn)
     d vsdtl6_vare     s                   like(vdvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
6.21 d vshel1_vsor     s                   like(vcvsor)
6.21 d vshel1_lage     s                   like(vclage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
      *
     d w_firm          s                   like(vdfirm)
     d w_vsor          s                   like(vdvsor)
6.20 d w_lage          s                   like(vdlage)
     d w_ldor          s                   like(vdldor)
     d w_ogrp          s                   like(vdogrp)
     d w_hgrp          s                   like(vdhgrp)
     d w_ugrp          s                   like(vdugrp)
     d w_modn          s                   like(vdmodn)
     d w_vare          s                   like(vdvare)
     d s_ldor          s                   like(vdldor)
     d s_ogrp          s                   like(vdogrp)
     d s_hgrp          s                   like(vdhgrp)
     d s_ugrp          s                   like(vdugrp)
     d s_modn          s                   like(vdmodn)
     d s_vare          s                   like(vdvare)
     d w_lnav          s                   like(rlnavn)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_mte1          s                   like(vgutxt)
     d w_tek1          s                   like(vvtek1)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/ endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * F13=Utplukk vareeiere
      *
     c                   if        *inkm
     c                   call      'VD110R'
     c                   parm                    w_firm
     c                   parm                    w_vsor
6.20 c                   parm                    w_lage
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F14=Utplukk varer
      *
     c                   if        *inkn
     c                   call      'VV565R'
     c                   parm                    w_firm
     c                   parm                    w_vsor
6.20 c                   parm                    w_lage
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0 or
     c                             b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      vsdtl2_ogrp = b1ogrp
     c                   eval      vsdtl2_hgrp = b1hgrp
     c                   eval      vsdtl2_ugrp = b1ugrp
     c     vsdtl2_key    setll     vsdtl2
     c                   when      w_seqe = 'H'
     c                   eval      vsdtl3_hgrp = b1hgrp
     c     vsdtl3_key    setll     vsdtl3
     c                   when      w_seqe = 'U'
     c                   eval      vsdtl4_ugrp = b1ugrp
     c     vsdtl4_key    setll     vsdtl4
     c                   when      w_seqe = 'M'
     c                   eval      vsdtl5_modn = b1modn
     c     vsdtl5_key    setll     vsdtl5
     c                   when      w_seqe = 'V'
     c                   eval      vsdtl6_vare = b1vare
     c     vsdtl6_key    setll     vsdtl6
     c                   other
     c                   eval      vsdtl1_ldor = b1ldor
     c                   eval      vsdtl1_ogrp = b1ogrp
     c                   eval      vsdtl1_hgrp = b1hgrp
     c                   eval      vsdtl1_ugrp = b1ugrp
     c                   eval      vsdtl1_modn = b1modn
     c                   eval      vsdtl1_vare = b1vare
     c     vsdtl1_key    setll     vsdtl1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vsdtl1_ldor = b2ldor
     c                   eval      vsdtl1_ogrp = b2ogrp
     c                   eval      vsdtl1_hgrp = b2hgrp
     c                   eval      vsdtl1_ugrp = b2ugrp
     c                   eval      vsdtl1_modn = b2modn
     c                   eval      vsdtl1_vare = b2vare
     c     vsdtl1_key    setll     vsdtl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      vsdtl2_ogrp = b2ogrp
     c                   eval      vsdtl2_hgrp = b2hgrp
     c                   eval      vsdtl2_ugrp = b2ugrp
     c     vsdtl2_key    setll     vsdtl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      vsdtl3_hgrp = b2hgrp
     c     vsdtl3_key    setll     vsdtl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vsdtl4_ugrp = b2ugrp
     c     vsdtl4_key    setll     vsdtl4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på modul
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      vsdtl5_modn = b2modn
     c     vsdtl5_key    setll     vsdtl5
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      vsdtl6_vare = b2vare
     c     vsdtl6_key    setll     vsdtl6
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b1ldor
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ldor = b1ldor
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1modn = b1modn
     c                   eval      k1vare = b1vare
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ldor = b1ldor
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1modn = b1modn
     c                   eval      d1vare = b1vare
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b1ldor
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ldor = b2ldor
     c                   eval      c1ogrp = b2ogrp
     c                   eval      c1hgrp = b2hgrp
     c                   eval      c1ugrp = b2ugrp
     c                   eval      c1modn = b2modn
     c                   eval      c1vare = b2vare
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1ldor = 0 and
     c                             c1ogrp = 0 and
     c                             c1hgrp = 0 and
     c                             c1ugrp = 0 and
     c                             c1modn = 0 and
     c                             c1vare = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   eval      w_ldor = c1ldor
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_modn = c1modn
     c                   eval      w_vare = c1vare
      *
     c                   exsr      sjekk_input
      *
     c                   eval      c1ldor = w_ldor
     c                   eval      c1ogrp = w_ogrp
     c                   eval      c1hgrp = w_hgrp
     c                   eval      c1ugrp = w_ugrp
     c                   eval      c1modn = w_modn
     c                   eval      c1vare = w_vare
      *
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vsdtlr_ldor = c1ldor
     c                   eval      vsdtlr_ogrp = c1ogrp
     c                   eval      vsdtlr_hgrp = c1hgrp
     c                   eval      vsdtlr_ugrp = c1ugrp
     c                   eval      vsdtlr_modn = c1modn
     c                   eval      vsdtlr_vare = c1vare
     c     vsdtlr_key    chain     vsdtlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ldor = 0
     c                   eval      c1lnav = *blank
     c                   eval      c1ogrp = 0
     c                   eval      c1otxt = *blank
     c                   eval      c1hgrp = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1ugrp = 0
     c                   eval      c1utxt = *blank
     c                   eval      c1modn = 0
     c                   eval      c1mte1 = *blank
     c                   eval      c1vare = *blank
     c                   eval      c1tek1 = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ldor = 0
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vsdtlr_ldor = c1ldor
     c                   eval      vsdtlr_ogrp = c1ogrp
     c                   eval      vsdtlr_hgrp = c1hgrp
     c                   eval      vsdtlr_ugrp = c1ugrp
     c                   eval      vsdtlr_modn = c1modn
     c                   eval      vsdtlr_vare = c1vare
     c     vsdtlr_key    chain     vsdtlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2eusr = vdeusr
     c     *dmy          move      vdodat        c2odat
     c     *dmy          move      vdedat        c2edat
     c                   else
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ldor = k1ldor
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1modn = k1modn
     c                   eval      c1vare = k1vare
     c                   eval      vsdtlr_ldor = k1ldor
     c                   eval      vsdtlr_ogrp = k1ogrp
     c                   eval      vsdtlr_hgrp = k1hgrp
     c                   eval      vsdtlr_ugrp = k1ugrp
     c                   eval      vsdtlr_modn = k1modn
     c                   eval      vsdtlr_vare = k1vare
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vsdtlur
      *
     c                   eval      vsdtlu_ldor = c1ldor
     c                   eval      vsdtlu_ogrp = c1ogrp
     c                   eval      vsdtlu_hgrp = c1hgrp
     c                   eval      vsdtlu_ugrp = c1ugrp
     c                   eval      vsdtlu_modn = c1modn
     c                   eval      vsdtlu_vare = c1vare
     c     vsdtlu_key    chain     vsdtlur                            90
      *
     c                   eval      vdeusr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
      *
     c                   if        *in90 = *off
     c                   update    vsdtlur
     c                   else
     c                   eval      vdfirm = w_firm
     c                   eval      vdvsor = w_vsor
6.20 c                   eval      vdlage = w_lage
     c                   eval      vdldor = c1ldor
     c                   eval      vdogrp = c1ogrp
     c                   eval      vdhgrp = c1hgrp
     c                   eval      vdugrp = c1ugrp
     c                   eval      vdmodn = c1modn
     c                   eval      vdvare = c1vare
     c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
     c                   write     vsdtlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vsdtlr_ldor = d1ldor
     c                   eval      vsdtlr_ogrp = d1ogrp
     c                   eval      vsdtlr_hgrp = d1hgrp
     c                   eval      vsdtlr_ugrp = d1ugrp
     c                   eval      vsdtlr_modn = d1modn
     c                   eval      vsdtlr_vare = d1vare
     c     vsdtlr_key    chain     vsdtlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      vsdtlu_ldor = d1ldor
     c                   eval      vsdtlu_ogrp = d1ogrp
     c                   eval      vsdtlu_hgrp = d1hgrp
     c                   eval      vsdtlu_ugrp = d1ugrp
     c                   eval      vsdtlu_modn = d1modn
     c                   eval      vsdtlu_vare = d1vare
     c     vsdtlu_key    chain     vsdtlur                            90
     c                   delete    vsdtlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ldor = k1ldor
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_modn = k1modn
     c                   eval      s_vare = k1vare
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vsdtlr_ldor = k1ldor
     c                   eval      vsdtlr_ogrp = k1ogrp
     c                   eval      vsdtlr_hgrp = k1hgrp
     c                   eval      vsdtlr_ugrp = k1ugrp
     c                   eval      vsdtlr_modn = k1modn
     c                   eval      vsdtlr_vare = k1vare
     c     vsdtlr_key    chain     vsdtlr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ldor = k1ldor
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_modn = k1modn
     c                   eval      w_vare = k1vare
      *
     c                   exsr      sjekk_input
      *
     c                   eval      k1ldor = w_ldor
     c                   eval      k1ogrp = w_ogrp
     c                   eval      k1hgrp = w_hgrp
     c                   eval      k1ugrp = w_ugrp
     c                   eval      k1modn = w_modn
     c                   eval      k1vare = w_vare
      *
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vsdtlr_ldor = k1ldor
     c                   eval      vsdtlr_ogrp = k1ogrp
     c                   eval      vsdtlr_hgrp = k1hgrp
     c                   eval      vsdtlr_ugrp = k1ugrp
     c                   eval      vsdtlr_modn = k1modn
     c                   eval      vsdtlr_vare = k1vare
     c     vsdtlr_key    chain     vsdtlr                             90
     c                   if        *in90 = *off
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = s_ldor
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1modn = s_modn
     c                   eval      c1vare = s_vare
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   read      vsdtl2
     c                   when      w_seqe = 'H'
     c                   read      vsdtl3
     c                   when      w_seqe = 'U'
     c                   read      vsdtl4
     c                   when      w_seqe = 'M'
     c                   read      vsdtl5
     c                   when      w_seqe = 'V'
     c                   read      vsdtl6
     c                   other
     c                   read      vsdtl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vdfirm <> p_firm or
6.20 c                             vdvsor <> p_vsor or
6.20 c                             vdlage <> p_lage
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      vsdtl2_ogrp = vdogrp
     c                   eval      vsdtl2_hgrp = vdhgrp
     c                   eval      vsdtl2_ugrp = vdugrp
     c                   when      w_seqe = 'H'
     c                   eval      vsdtl3_hgrp = vdhgrp
     c                   when      w_seqe = 'U'
     c                   eval      vsdtl4_ugrp = vdugrp
     c                   when      w_seqe = 'M'
     c                   eval      vsdtl5_modn = vdmodn
     c                   when      w_seqe = 'V'
     c                   eval      vsdtl6_vare = vdvare
     c                   other
     c                   eval      vsdtl1_ldor = vdldor
     c                   eval      vsdtl1_ogrp = vdogrp
     c                   eval      vsdtl1_hgrp = vdhgrp
     c                   eval      vsdtl1_ugrp = vdugrp
     c                   eval      vsdtl1_modn = vdmodn
     c                   eval      vsdtl1_vare = vdvare
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldor = vdldor
     c                   eval      b1lnav = *blank
     c                   eval      b1ogrp = vdogrp
     c                   eval      b1hgrp = vdhgrp
     c                   eval      b1ugrp = vdugrp
     c                   eval      b1utxt = *blank
     c                   eval      b1modn = vdmodn
     c                   eval      b1mte1 = *blank
     c                   eval      b1vare = vdvare
     c                   eval      b1tek1 = *blank
      *
      * Henter leverandørnavn, varegruppetekster, modultekst og varetekst-1
      *
     c                   if        b1ldor > 0
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1lnav = rlnavn
     c                   endif
     c                   endif
      *
     c                   if        b1ogrp > 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      b1utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        b1hgrp > 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      b1utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        b1ugrp > 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b1utxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   if        b1modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    b1modn
     c                   parm                    w_mte1
     c                   eval      b1mte1 = w_mte1
     c                   endif
      *
     c                   eval      vvarl1_vare = b1vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'O'
     c     vsdtl2_key    setgt     vsdtl2
     c                   readp     vsdtl2
     c                   when      w_seqe = 'H'
     c     vsdtl3_key    setgt     vsdtl3
     c                   readp     vsdtl3
     c                   when      w_seqe = 'U'
     c     vsdtl4_key    setgt     vsdtl4
     c                   readp     vsdtl4
     c                   when      w_seqe = 'M'
     c     vsdtl5_key    setgt     vsdtl5
     c                   readp     vsdtl5
     c                   when      w_seqe = 'V'
     c     vsdtl6_key    setgt     vsdtl6
     c                   readp     vsdtl6
     c                   other
     c     vsdtl1_key    setgt     vsdtl1
     c                   readp     vsdtl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   readp     vsdtl2
     c                   when      w_seqe = 'H'
     c                   readp     vsdtl3
     c                   when      w_seqe = 'U'
     c                   readp     vsdtl4
     c                   when      w_seqe = 'M'
     c                   readp     vsdtl5
     c                   when      w_seqe = 'V'
     c                   readp     vsdtl6
     c                   other
     c                   readp     vsdtl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vdfirm <> p_firm or
6.20 c                             vdvsor <> p_vsor or
6.20 c                             vdlage <> p_lage
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      vsdtl2_ogrp = vdogrp
     c                   eval      vsdtl2_hgrp = vdhgrp
     c                   eval      vsdtl2_ugrp = vdugrp
     c                   when      w_seqe = 'H'
     c                   eval      vsdtl3_hgrp = vdhgrp
     c                   when      w_seqe = 'U'
     c                   eval      vsdtl4_ugrp = vdugrp
     c                   when      w_seqe = 'M'
     c                   eval      vsdtl5_modn = vdmodn
     c                   when      w_seqe = 'V'
     c                   eval      vsdtl6_vare = vdvare
     c                   other
     c                   eval      vsdtl1_ldor = vdldor
     c                   eval      vsdtl1_ogrp = vdogrp
     c                   eval      vsdtl1_hgrp = vdhgrp
     c                   eval      vsdtl1_ugrp = vdugrp
     c                   eval      vsdtl1_modn = vdmodn
     c                   eval      vsdtl1_vare = vdvare
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'O'
     c     vsdtl2_key    setll     vsdtl2
     c                   when      w_seqe = 'H'
     c     vsdtl3_key    setll     vsdtl3
     c                   when      w_seqe = 'U'
     c     vsdtl4_key    setll     vsdtl4
     c                   when      w_seqe = 'M'
     c     vsdtl5_key    setll     vsdtl5
     c                   when      w_seqe = 'V'
     c     vsdtl6_key    setll     vsdtl6
     c                   other
     c     vsdtl1_key    setll     vsdtl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   eval      k1hgrp = 0
     c                   eval      k1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   eval      c1ugrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1utxt = *blank
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   eval      k1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      b2vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1tek1
     c                   eval      c1vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   eval      k1vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
7.xx  *
7.xx  * Modul
7.xx  *
7.xx c                   if        w_afld = 'B2MODN'
7.xx c                   call      'JU500R'
7.xx c                   parm                    w_firm
7.xx c                   parm                    b2modn
7.xx c                   parm                    w_mte1
7.xx c                   leavesr
7.xx c                   endif
7.xx  *
7.xx c                   if        w_afld = 'C1MODN'
7.xx c                   call      'JU500R'
7.xx c                   parm                    w_firm
7.xx c                   parm                    c1modn
7.xx c                   parm                    w_mte1
7.xx c                   leavesr
7.xx c                   endif
7.xx  *
7.xx c                   if        w_afld = 'K1MODN'
7.xx c                   call      'JU500R'
7.xx c                   parm                    w_firm
7.xx c                   parm                    k1modn
7.xx c                   parm                    w_mte1
7.xx c                   leavesr
7.xx c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for validering av nøkkel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil  = *off
      *
     c                   eval      w_lnav = *blank
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_mte1 = *blank
     c                   eval      w_tek1 = *blank
      *
      * Sjekk nivå
      *
     c                   if        w_ldor = 0 and
     c                             w_ogrp = 0 and
     c                             w_vare = *blank
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Vare
      *
     c                   if        w_vare <> *blank
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_ldor = vvldor
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_modn = vvnmod
     c                   eval      w_tek1 = vvtek1
     c                   endif
6.21  *
6.21  * Hvis Type = C, så krav om varenr
6.21  *
6.21 c                   if        vctype = 'C'    and
6.21 c                             w_vare = *blank
6.21 c                   eval      *in40  = *on
6.21 c                   eval      b_feil = *on
6.21 c                   goto      end_sjekk
6.21 c                   endif
      * Leverandør
      *
     c                   if        w_ldor <> 0
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
6.21  *
6.21  * Hvis Type = P, så krav om leverandør
6.21  *
6.21 c                   if        vctype = 'P'    and
6.21 c                             w_ldor = 0
6.21 c                   eval      *in41  = *on
6.21 c                   eval      b_feil = *on
6.21 c                   goto      end_sjekk
6.21 c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0 and
     c                             w_ugrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1lnav = *blank
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1mte1 = *blank
     c                   eval      c1tek1 = *blank
      *
     c                   if        c1ldor <> 0
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c1lnav = rlnavn
     c                   endif
     c                   endif
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1otxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1htxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c1utxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   if        c1modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    w_mte1
     c                   eval      c1mte1 = w_mte1
     c                   endif
      *
     c                   if        c1vare <> *blank
     c                   eval      vvarl1_vare = c1vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c1tek1 = vvtek1
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vsor
6.20 c                   parm                    p_lage
      *
      * Key for les på varesortiment-linje-regsiter
      *
     c     vsdtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtlr_ldor
     c                   kfld                    vsdtlr_ogrp
     c                   kfld                    vsdtlr_hgrp
     c                   kfld                    vsdtlr_ugrp
     c                   kfld                    vsdtlr_modn
     c                   kfld                    vsdtlr_vare
      *
      * Key for oppdatering av varesortiment-linje-register
      *
     c     vsdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtlu_ldor
     c                   kfld                    vsdtlu_ogrp
     c                   kfld                    vsdtlu_hgrp
     c                   kfld                    vsdtlu_ugrp
     c                   kfld                    vsdtlu_modn
     c                   kfld                    vsdtlu_vare
      *
      * Key for posisjonering på leverandør
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
      *
      * Key for posisjonering på overgruppe
      *
     c     vsdtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtl2_ogrp
     c                   kfld                    vsdtl2_hgrp
     c                   kfld                    vsdtl2_ugrp
      *
      * Key for posisjonering på hovedgruppe
      *
     c     vsdtl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtl3_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     vsdtl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtl4_ugrp
      *
      * Key for posisjonering på modul
      *
     c     vsdtl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtl5_modn
      *
      * Key for posisjonering på varenummer
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    w_lage
     c                   kfld                    vsdtl6_vare
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
6.21  *
6.21  * Key for oppslag på varesort. hode
6.21  *
6.21 c     vshel1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vshel1_vsor
6.21 c                   kfld                    vshel1_lage
      *
      *
      * Henter firma og sortiment fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vsor = p_vsor
6.20 c                   eval      w_lage = p_lage
     c                   eval      b2vsor = p_vsor
6.20 c                   eval      b2lage = p_lage
6.21  *
6.21  * leser varesort. hode
6.21  *
6.21 c                   eval      vshel1_vsor = w_vsor
6.21 c                   eval      vshel1_lage = w_lage
6.21 c     vshel1_key    chain     vshel1
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vsdtl1_key    setll     vsdtl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
