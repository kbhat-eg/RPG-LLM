      /TITLE  Fix : sletter varer fra fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASOFAK
      *  Program . . . . : FX840R
      *  Beskrivelse . . : Sletter varer som ligger på fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           110113  Nytt program                                   BOF
8.01  *           291122  Justert for mer fleksible måte å finne varenr  BOF
8.01  *           291122  Utvidet med kall fra parameter                 BOF
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    uf a e           k disk    rename(lexcpfr:lwexpfr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      *  Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      *  Definering av variable
      *
8.01 d len             s              3  0
8.01 d st              s              3  0
8.01 d w_8x            s              8
     d w_posi          s              2  0
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d d_vare          s                   like(vvvare)
     d w_var8          s              8
     d w_inlr          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
     d b_ordr          s              1    inz(*off)
     d b_lagr          s              1    inz(*off)
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese ekstern fil for å hente neste vare for behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Lese file
      *
     c                   read      lwexpf                                 90
     c                   dow       *in90 = *off
8.01 c                   eval      w_posi = %scan(';':exclin)
8.01 c                   if        w_posi > 0
8.01 c                   eval      d_vare = %trim(%subst(exclin:1:w_posi-1))
8.01 c                   else
8.01 c                   eval      d_vare = %trim(%subst(exclin:1:15))
8.01 c                   endif
8.01 c                   if        d_vare <> *blank

8.01 c                   eval      len    = %len(%trim(d_vare))
8.01 c                   eval      st = 9 - len
8.01 c                   eval      w_8x  = *blank
8.01 c                   eval      %subst(w_8x:st:len) = %subst(d_vare:1:len)
8.01 c                   eval      w_var8 = %xlate(' ':'0':w_8x)
      *
      *  Sjekk at ikke heading-rad er lest inn
      *
8.01 c                   if        %check(digits : w_var8) = 0
     c                   exsr      behand
     c                   endif
      *
     c                   endif
      *
     c                   read      lwexpf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behand        begsr
      *
8.01 c                   movel     w_Var8        w_vare
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1r
     c                   if        not %found(vvarl1)
     c                   leavesr
     c                   endif
      *
      * Kaller program for å sjekke om en vare kan slettes eller ikke
      *
     c                   eval      w_vare = vvvare
     c                   eval      b_inlr = *off
     c                   eval      b_ordr = *off
     c                   eval      b_lagr = *off
      *
     c                   call      'VV490R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    b_inlr
     c                   parm                    b_ordr
     c                   parm                    b_lagr
      *
     c                   if        b_ordr = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b_lagr = *on
     c                   leavesr
     c                   endif
      *
     c                   call      'VV400R  '
     C                   parm                    w_firm
     C                   parm                    vvvare
     C                   parm                    w_inlr
      *
     c                   delete    lwexpfr
      *
     c     behand_ut     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
