     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System  . . . . : ASLAGR                                       *
      *  Program . . . . : VG803R                                       *
      *  Beskrivelse . . : Danner økonomitranser utfra lagertranser     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      * --------- ------  ----------------------------------------------*
6.30  *  6.30     060319  Nytt program / Kopi av LQ705R            bof  *
6.30  *  6.30     130218  Oppdat av gj.kostpris + konteringer      nho
      *                   NB! fjernet henting av avdeling fra           *
      *                   lager.  Se org.program SBLAGR funksj     nho  *
8.01  * 800   260521 ask  Lagt inn sjekk på varetype- oppdaterer bare 'L'
8.02  * 800   170122 ask  Lagt inn p_lage i kall til VG701R             '
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fllhel1    if   e           k disk    rename(llhepfr:llhel1r)
     flldtl1    if   e           k disk    rename(lldtpfr:lldtl1r)
6.30 fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
      *
     flovfpf    o  a e           k disk    rename(lovfpfr:lovfpfr)
      *
     d etx             s             20    dim(2) ctdata perrcd(1)
      *
     d                 ds
      *                                                      Parameter-inn
     d d_prec                        64
6.nu d  d_numm                        8  0 overlay(d_prec)
6.nu d  d_kode                        1    overlay(d_prec:9)
6.nu d  d_peri                        6  0 overlay(d_prec:10)
     d  d_dato                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:16)
6.nu d  d_dumm                        1    overlay(d_prec:26) inz('x')
      *
      *  Hjelpfelter for snuing av periode
      *
     d                 ds
     d dsyymm                         4  0
     d  dspeyy                        2  0 overlay(dsyymm)
     d  dspemm                        2  0 overlay(dsyymm:3)
      *
      *  Hjelpfelter for oppdeling av fritekst
      *
     d                 ds
     d d_text                        20
     d  d_flag                        2    overlay(d_text:12)
     d  d_tlag                        2    overlay(d_text:19)
      *
     d                 ds
     d d_komp                        11  0
     d  d_avde                        2  0 overlay(d_komp)
     d  d_kont                        6  0 overlay(d_komp:3)
     d  d_spes                        2  0 overlay(d_komp:9)
     d  d_mkod                        1  0 overlay(d_komp:11)
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
5.20 d d_hele                  1    128
     d d_kav1                  1      4  0
     d d_kko1                  5     10  0
4.01 d d_ksp1                 11     14  0
4.01 d d_kav2                 15     18  0
4.01 d d_kko2                 19     24  0
4.01 d d_ksp2                 25     28  0
4.01 d d_kav3                 29     32  0
4.01 d d_kko3                 33     38  0
4.01 d d_ksp3                 39     42  0
4.01 d d_kav4                 43     46  0
4.01 d d_kko4                 47     52  0
4.01 d d_ksp4                 53     56  0
4.01 d d_kav5                 57     60  0
4.01 d d_kko5                 61     66  0
4.01 d d_ksp5                 67     70  0
4.01 d d_kav6                 71     74  0
4.01 d d_kko6                 75     80  0
4.01 d d_ksp6                 81     84  0
4.01 d d_kav7                 85     88  0
4.01 d d_kko7                 89     94  0
4.01 d d_ksp7                 95     98  0
4.01 d d_khev                 99    102
5.20 d d_nivo                103    103
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  LOCAL DATA AREA                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable
      *
      *
     d w_ltxt          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_mnd           s              2  0
     d b_feil          s              1    inz(*off)
      *
     d whlp20          s              2  2
     d wkbelh          s             11  2
     d wktext          s             20
     d wpstat          s              1
     d wpltyp          s              1  0
     d wpotyp          s              2
     d wpogrp          s              2  0
     d wphgrp          s              2  0
     d wpugrp          s              3  0
     d wpvare          s             15
     d wphele          s            128
     d wsfast          s             10
     d wsfell          s              6
     d wskode          s              1
6.nu d wsnumm          s              8  0
     d wstype          s              2
     d w_firm          s              3  0
     d w_avde          s              4  0
     d w_avdf          s              4  0
     d w_avdt          s              4  0
     d w_lage          s                   like(lslage)
     d p_parm          s             64
     d linumm          s                   like(lsnumm)
      *
6.30 d w_dato          s               d   datfmt(*iso)
6.30 d p_vare          s                   like(lsvare)
6.30 d p_lage          s                   like(lslage)
6.30 d p_cost          s                   like(lskpny)
6.30 d p_dato          s              8  0
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d llhel1_numm     s                   like(lqnumm)
     d lldtl1_line     s                   like(lsline)
     d lldtl1_numm     s                   like(lsnumm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behadnling av fellesopplysninger for BUNTEN                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Hent opplysninger fra LAGERBUNT-HODE-REGISTER                  *
      *
     c     llhel1_key    chain     llhel1
     c                   eval      *in90 = not %found
      *
      *  Legger inn firma i bilagsfirmafelt
      *
     c                   eval      lgfirm = w_firm
      *
      *  Legger inn buntnummer
      *
     c                   eval      lgbunt = lqnumm
      *
      *  Legger inn dato
      *
     c                   eval      lgbdat = d_dato
      *
      *  Legg inn periode
      *
     c                   eval      w_mnd  = *month
     c                   eval      lgperi = *year
     c                   movel     w_mnd         lgperi
      *
      *  Henter inn bilagsnummer
      *
     c     vgkkir_key    chain     vgkkir
      *
     c                   eval      wsfell = vgkbis
     c                   eval      wstype = *blank
     c                   eval      wskode = '0'
     c                   exsr      hntnum
     c                   eval      lgbiln = wsnumm
      *
      *  Legger inn bilagskode
      *
     c                   eval      lgbilk = vgkbik
      *
      *  Nullstiller felter som ikke benyttes i rutinen
      *
6.20 c                   eval      lgfdat = lgbdat
6.21 c                   eval      lgproj = *blank
     c                   eval      lgakti = *blank
     c                   eval      lgkref = *zero
     c                   eval      lgkund = *zero
     c                   eval      lgvalk = *blank
     c                   eval      lgvalb = *zero
     c                   eval      lgmngk = *zero
     c                   eval      lgautx = *blank
     c                   eval      lgkidi = *blank
     c                   eval      lgsfak = *blank
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Lese linje-register og danner økonomi-transaksjoner            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Lese file
      *
     c     lldtl1_key    setll     lldtl1
     c                   read      lldtl1
     c                   eval      *in15 = %eof
      *
     c                   dow       *in15 = *off
      *
      *  Hopper ut dersom ikke samme bunt-nummer
      *
     c                   if        linumm <> lsnumm
     c                   leave
     c                   endif
      *
      *  Legger inn overliggende type dersom blank-type
      *
     c                   if        lstype = *blank
     c                   eval      lstype = lqtype
     c                   endif
      *
      *  Legger inn overliggende lager, dersom blank-lager
      *
     c                   if        lslage = *zero
     c                   eval      lslage = lqlage
     c                   endif
      *
      *  Legger inn overliggende lager til, dersom blank-lager
      *
     c                   if        lslast = *zero
     c                   eval      lslast = lqlast
     c                   endif
      *
      *  Kun linjer som ikke er tekstlinjer skal oppdatere regnskap
      *  Ikke oppdatering dersom antall er null
      *
     c                   if        lstype <> 'TX'
     c                   if        lsanta <> *zero
      *
6.30 c                   if        lstype = 'I ' or
6.30 c                             lstype = 'U ' or
6.30 c                             lstype = 'J '
6.30 c                   exsr      oktran
6.30 c                   endif
      *
     c                   endif
     c                   endif
      *
     c                   read      lldtl1
     c                   eval      *in15 = %eof
     c                   enddo
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Danner Økonomitransaksjoner                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     oktran        begsr
      *
      *  Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = lsvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   endif
8.01  *
8.01  * Bare varetype "L" skal oppdatere
8.01  *
8.01 c                   if        vvvtyp <> 'L'
8.01 c                   goto      sub_end
8.01 c                   endif
      *
      *  Beregner overføringsum
      *
6.30  *  Hent gj.kostpris                                              g
6.30  *
6.30 c     *ymd          move      lqdate        p_dato
6.30 c                   eval      p_vare = lsvare
6.30 c                   eval      p_lage = lslage
6.30 c                   call      'VG701R'
6.30 c                   parm                    p_vare
8.02 c                   parm                    p_lage
6.30 c                   parm                    p_dato
6.30 c                   parm                    p_cost
6.30 c                   eval      lgbelp = lsanta * p_cost
     c                   eval      lgmngd = lsanta
      *
      *  Finner den kontoen som skal benyttes
      *
     c                   exsr      hntkon
      *
6.30  *  Sjekk om gj.kostpris skal hentes
6.30  *
6.30 c                   time                    w_dato
6.30 c     *ymd          move      w_dato        p_dato
6.30 c                   call      'VG701R'
6.30 c                   parm                    p_vare
6.30 c                   parm                    p_lage
6.30 c                   parm                    p_dato
6.30 c                   parm                    p_cost
6.30  *
6.30 c                   eval      lgbelp = lsanta * p_cost
      *
6.30  * Nullstiller før neste post
6.30 c                   eval      lgckto = 0
6.30 c                   eval      lgcsps = 0
6.30  *
6.30  * Danner nye konteringer, J+
6.30 c                   if        lsanta > 0
6.30 c                   eval      lgdkto = d_kko3
6.30 c                   eval      lgdsps = d_ksp3
6.30 c                   endif
6.30  *
6.30  * Danner nye konteringer, J-
6.30 c                   if        lsanta < 0
6.30 c                   eval      lgdkto = d_kko5
6.30 c                   eval      lgdsps = d_ksp5
6.30 c                   mult      -1            lgbelp
6.30 c                   endif
      *
6.30  *
      *  Danner bilagstekst inkl. lager fra/til
      *
     c                   eval      d_text = etx(01)
     c                   eval      d_flag = %editc(lslage:'X')
     c                   eval      d_tlag = %editc(lslast:'X')
     c                   eval      lgtext = d_text
      *
      *  Legger ut kostnadstransaksjon til økonomi
      *
     c                   write     lovfpfr
      *
      * Sjekk om kost skal bokføres og balansekonto er utfylt
      * Nullstiller felter
6.30 c                   eval      lgdkto = 0
6.30 c                   eval      lgdsps = 0
      *
6.30 c                   if        lsanta > 0
6.30 c                   eval      lgckto = d_kko5
6.30 c                   eval      lgcsps = d_ksp5
6.30 c                   endif
      *
6.30 c                   if        lsanta < 0
6.30 c                   eval      lgckto = d_kko3
6.30 c                   eval      lgcsps = d_ksp3
6.30 c                   mult      -1            lgbelp
6.30 c                   endif
      *
      *  Legger ut balansetransaksjon til økonomi
      *
     c                   write     lovfpfr
      *
     c     sub_end       endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter avdeling/konto/spesifikasjon                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hntkon        begsr
      *
      *  Legger ut parametere for konto-henvisnings-program
      *
     c                   eval      wpstat = *blank
     c                   eval      wpltyp = *zero
     c                   eval      wpogrp = vvogrp
     c                   eval      wphgrp = vvhgrp
     c                   eval      wpugrp = vvugrp
     c                   eval      wpvare = vvvare
     c                   move      *zero         wphele
      *
      *  Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    wpstat
5.20 c                   parm                    wpltyp
     c                   parm                    wpotyp
     c                   parm                    wpogrp
     c                   parm                    wphgrp
     c                   parm                    wpugrp
     c                   parm                    wpvare
     c                   parm                    wphele
      *
     c                   eval      d_hele = wphele
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter/Oppdatering av NUMMER-REGISTER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     hntnum        begsr
      *
     c                   eval      wsfast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    wskode
     c                   parm                    w_firm
     c                   parm                    wsfell
     c                   parm                    wstype
     c                   parm                    wsfast
     c                   parm                    wsnumm
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Hent avdeling for lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sblagr        begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_ltxt
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_firm = *zero
      *
     c                   eval      wktext = *blank
      *
     c                   eval      w_avde = *zero
     c                   eval      w_avdf = *zero
     c                   eval      w_avdt = *zero
      *
     c                   move      *zero         wphele
     c                   eval      wpogrp = *zero
     c                   eval      wphgrp = *zero
     c                   eval      wpugrp = *zero
     c                   eval      whlp20 = *zero
     c                   eval      wpvare = *blank
     c                   eval      wpstat = *blank
     c                   eval      wpotyp = *blank
      *
     c                   eval      wskode = *blank
     c                   eval      wsfell = *blank
     c                   eval      wstype = *blank
     c                   eval      wsfast = *blank
     c                   eval      wsnumm = *zero
      *
      *  Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for statusregister
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for LAGERBUNT-HODE-REGISTER
      *
     c     llhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llhel1_numm
      *
      *  Key for LAGERBUNT-LINJE-REGISTER
      *
     c     lldtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtl1_numm
     c                   kfld                    lldtl1_line
      *
6.30  * Key for kostpris parameter
6.30  *
6.30 c     vgkkir_key    klist
6.30 c                   kfld                    w_firm
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
     c                   eval      d_prec = p_parm
      *
      *  Legge inn opplysninger i nøkklene
      *
     c                   eval      linumm = d_numm
     c                   eval      llhel1_numm = d_numm
     c                   eval      lldtl1_numm = d_numm
     c                   eval      lldtl1_line = *zero
      *
     c                   eval      w_firm = l_firm
      *
      *  Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r
     c                   eval      *in90 = not %found
      *
     c                   endsr
      *
**     T Y P E / B I L A G S E K S T R A - T E K S T
Lager.ovf. xx til yy
Ledig
