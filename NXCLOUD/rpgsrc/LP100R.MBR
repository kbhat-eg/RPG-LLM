     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LP100R
      * Beskrivelse . . : Leverandør distribusjonsliste, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R5.51 130907 TSV  Endret til lovlig med FTP/NORGROS/NBS
6.10  *  6.10 230112 bsa  Lagt inn overgang til koder for ny Nordic E-building s
6.20  *  6.20 251110 bof  Utvidet med ordretype på interne leverandører
6.21  *  6.20 240912 bof  Lagt NEB godkent på LLDIPF
6.22  *  6.20 240912 bsa  Vedlikeholdes ikke lenger via F tast
6.23  *  6.20 070212 bof  feilretting vedl. av ordretype
6.24  *  6.20 080212 bof  Hvis lager 1 pos., foranstill med 0.
6.25  *  6.20 031214 bsa  Send "alt" hvis vi har vært innom F6. Feiler i jacada hvis
6.25  *                   b2cmd ikke sendes på nytt.
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  090919 bkv  Tillate kombinasjon for intern bestilling
      *
8.01  * 8.xx  270422 bsa  Ny tabell for å legge ut mer info rundt EDI. Bruker
8.01  * 8.xx              om igjen LLDNPF.
8.02  * 8.xx  031022 bsa  Nytt felt for negativ ordretype på intern leverandører
8.02  * 8.xx              Åpner igjen  loop for LLDN
8.03  * 8.xx  131022 bsa  Nye valg for distribusjon (Ref 8.01 i std)
8.04  *K000   201022 bsa  Nye valg må sjekkes for validering
8.05  *K000   041122 bsa  Vise F tast også for NGN_EDI
8.06  *K000   071122 bsa  Legger inn felt for irdreinfo type.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     flldil1    if   e           k disk    rename(lldipfr:lldil1r)
     flldilu    uf a e           k disk    rename(lldipfr:lldilur)
6.21 f*ldnlu    uf a e           k disk    rename(lldnpfr:lldnlur)
6.21 f*ldnl1    if   e           k disk    rename(lldnpfr:lldnl1r)
8.01 flldnlu    uf a e           k disk    rename(lldnpfr:lldnlur)
8.01 flldnl1    if   e           k disk    rename(lldnpfr:lldnl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.20 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
8.06 fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
      *
     flp100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lldil1_ldor     s                   like(lpldor)
     d lldil1_lage     s                   like(lplage)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d lldilu_ldor     s                   like(lpldor)
     d lldilu_lage     s                   like(lplage)
     d rlevl1_levr     s                   like(rllevr)
6.20 d votyl1_otyp     s                   like(vaotyp)
8.06 d vinfl1_ityp     s                   like(vaityp)
      *
      * Definering av variable
      *
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_spge          s              4  0
     d w_srrn          s              4  0
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(lpfirm)
     d w_ldor          s                   like(lpldor)
     d w_lage          s              2  0
     d w_lagr          s              2
     d w_lage_alf      s              2
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_lnav          s                   like(rlnavn)
     d w_type          s                   like(lptype)
     d w_form          s                   like(lpform)
     d w_mott          s                   like(lpmott)
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for å opprette ny rad
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * F1WIN  - F1 Vindu for spørring på valgalternativer (Hardkodet)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   exsr      forny
6.25 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = 0
     c                   if        b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> 0
     c                   exsr      control
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     lldil1_key    setll     lldil1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på leverandørnummer
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      lldil1_ldor = b2ldor
     c     lldil1_key    setll     lldil1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_feil = *off
      *
      * Validering av nøkkel
      *
     c                   if        b2ldor = 0
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      lldilr_ldor = b2ldor
     c     lldilr_key    chain     lldilr                             90
     C                   if        *in90
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      w_lage_alf = b2lage
     c                   exsr      hent_lag
     c                   if        b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Endre
      *
     c                   if        b2valg = 2
     c                   eval      c1ldor = b2ldor
     c                   eval      c1lage = b2lage
     c                   eval      lldilr_ldor = c1ldor
     c                   eval      lldilr_lage = c1lage
     c                   exsr      xc2bld
     c                   eval      b_forn = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Kopiere
      *
     c                   if        b2valg = 3
     c                   eval      k1ldor = b2ldor
     c                   eval      k1lage = b2lage
     c                   exsr      xk1win
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = 4
     c                   eval      d1ldor = b2ldor
     c                   eval      d1lage = b2lage
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
      * Vise
      *
     C                   if        b2valg = 5
     c                   eval      c1ldor = b2ldor
     c                   eval      c1lage = b2lage
     c                   eval      lldilr_ldor = c1ldor
     c                   eval      lldilr_lage = c1lage
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = 0
     c                   eval      b2ldor = 0
     c                   eval      b2lage = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lage = b1lage
     c                   eval      lldilr_ldor = c1ldor
     c                   eval      lldilr_lage = c1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = 3
     c                   eval      k1ldor = b1ldor
     c                   eval      k1lage = b1lage
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1ldor = b1ldor
     c                   eval      d1lage = b1lage
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lage = b1lage
     c                   eval      lldilr_ldor = c1ldor
     c                   eval      lldilr_lage = c1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      c1ldor = 0
     c                   eval      c1lnav = *blank
     c                   eval      c1lage = *blank
     c                   eval      c1lagn = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Validering
      *
     c                   if        c1ldor = 0
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      c1lnav = *blank
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1lnav = rlnavn
      *
     c                   eval      w_lage_alf = c1lage
     c                   exsr      hent_lag
     c                   if        b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      c1taga
     c                   endif
6.24  *
6.24  * hvis lager er utfylt sett 0 foran
6.24  *
6.24 c                   if        c1lage <> *blank
6.24 c     ' ':'0'       xlate     c1lage        c1lage
6.24 c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      lldilr_ldor = c1ldor
     c                   eval      lldilr_lage = c1lage
     c     lldilr_key    chain     lldilr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      lldilr_ldor = c1ldor
     c                   eval      lldilr_lage = c1lage
     c                   exsr      xc2bld
      *
     c     end_c1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
6.10  *
8.01 c                   eval      *in50  = *off
6.10  *
6.10 c     xc2bld_ny     tag
      *
     c                   eval      w_lage_alf = c1lage
     c                   exsr      hent_lag
     c                   eval      c2lagn = w_latx
      *
     c     lldilr_key    chain     lldilr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2nyen = a_meld(2)
     c                   eval      c2type = lptype
     c                   eval      c2form = lpform
8.05 c                   if        %trim(lpform) = 'EDI' or
8.05 c                             %trim(lpform) = 'NGN_EDI'
8.01 c                   eval      *in50 = *on
8.01 c                   endif
     c                   eval      c2mott = lpmott
     c                   eval      c2lknr = lplknr
     c                   eval      c2ftpr = lpftpr
     c                   eval      c2faxn = lpfaxn
     c                   eval      c2mail = lpmail
6.20 c                   eval      c2otyp = lpotyp
     c                   eval      c2eusr = lpeusr
6.22 c                   eval      c2nebg = lpnebg
     c                   eval      c2dato = 0
     c                   eval      c2time = 0
     c                   if        lpsida <> *loval
     c     *dmy          move      lpsida        c2dato
     c                   endif
     c                   if        lpsikl <> *loval
     c     *hms          move      lpsikl        c2time
     c                   endif
     c     *dmy          move      lpedat        c2edat
     c                   else
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c2type = *blank
     c                   eval      c2form = *blank
     c                   eval      c2mott = *blank
     c                   eval      c2lknr = *blank
     c                   eval      c2ftpr = *blank
     c                   eval      c2faxn = *blank
     c                   eval      c2mail = *blank
6.20 c                   eval      c2otyp = *blank
6.10 c                   eval      c2nebg = *blank
     c                   eval      c2dato = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
6.22 c                   eval      c2nebg = *blank
     c                   endif
8.02  * Hent info om NEB - åpnes igjen
8.02 c                   exsr      hnt_neb
6.21  *
     c                   if        b1valg = 3 or b2valg = 3
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c1ldor = k1ldor
     c                   eval      c1lage = k1lage
     c                   eval      c2dato = 0
     c                   eval      lldilr_ldor = k1ldor
     c                   eval      lldilr_lage = k1lage
     c                   endif
      *
      * Henter beskrivelse av felter
      *
     c                   eval      c1lnav = *blank
      *
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c1lnav = rlnavn
     c                   endif
      *
      * Dersom 'vise' settes protect-indikatoer
      *
     c                   if        b1valg = 5 or b2valg = 5
     c                   eval      *in30 = *on
     c                   eval      c2nyen = a_meld(3)
     c                   endif
      *
     C     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Overgang til FTP rutiner?
      *
     c                   if        *inkh
     c                   call      'AF100C'
     c                   goto      c2tagb
     c                   endif
6.10  *
6.10  * Vedlikehold av NEB parametre
6.10  * Skal ikke kjøres lenger, siden F9 ikke er tillat i skjermbildet
6.10 c                   if        *inki = *on
6.10 c                   exsr      xi2win
6.10 c                   goto      xc2bld_ny
6.10 c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        wactfe = 'C2TYPE'
     c                   eval      f1head = 'type    '
     c                   eval      f1alth = 'Type    '
     c                   eval      w_type = c2type
     c                   exsr      xf1win1
     c                   eval      c2type = w_type
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        wactfe = 'C2FORM'
     c                   eval      f1head = 'format  '
     c                   eval      f1alth = 'Format  '
     c                   eval      w_form = c2form
     c                   exsr      xf1win2
     c                   eval      c2form = w_form
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        wactfe = 'C2MOTT'
     c                   eval      f1head = 'mottaker'
     c                   eval      f1alth = 'Mottaker'
     c                   eval      w_mott = c2mott
     c                   exsr      xf1win3
     c                   eval      c2mott = w_mott
     c                   goto      c2tagb
     c                   endif
6.20  *
6.20 c                   if        wactfe = 'C2OTYP'
6.20 c                   call      'VA550R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    c2otyp
6.20 c                   goto      c2tagb
6.20 c                   endif
8.02  *
8.02 c                   if        wactfe = 'C2NOTY'
8.02 c                   call      'VA550R'
8.02 c                   parm                    w_firm
8.02 c                   parm                    c2noty
8.02 c                   goto      c2tagb
8.02 c                   endif
6.20  *
     c                   endif
6.20  *
6.20  * Valider ordretype
6.20  *
6.20 c                   if        c2otyp <> *blank
6.20 c                   eval      votyl1_otyp = c2otyp
6.20 c     votyl1_key    chain     votyl1
6.20 c                   if        not %found(votyl1)
6.20 c                   eval      *in33 = *on
6.20 c                   goto      c2tagb
6.20 c                   endif
6.20 c                   endif
8.02  *
8.02  * Valider ordretype
8.02  *
8.02 c                   if        c2noty <> *blank
8.02 c                   eval      votyl1_otyp = c2noty
8.02 c     votyl1_key    chain     votyl1
8.02 c                   if        not %found(votyl1)
8.02 c                   eval      *in33 = *on
8.02 c                   goto      c2tagb
8.02 c                   endif
8.02 c                   endif
      *
      * Kontroll av gyldige kombinasjoner
      *
     c                   if        c2type = 'INTERN    ' and
     c                             c2lknr <> *blank
     c                   eval      c2form = 'INTERN    '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = *blank or
     c                             c2form = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2type = 'SNA       ' and
     c                             c2form = 'WEBTRADE  ' and
     c                             c2mott = 'JERNIA-X25     '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'WEBTRADE  ' and
     c                             c2mott = 'INTERNETT      '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'EDI       ' and
     c                             c2mott = 'IBM            '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'EDI       ' and
     c                             c2mott = 'NBS            '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'LU0       ' and
     c                             c2form = 'EDI       ' and
     c                             c2mott = 'IBM            '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'WEBTRADE  ' and
     c                             c2mott = 'INTERNETT      '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'FARVERING ' and
     c                             c2mott = 'INTERNETT      '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'EFONELFO  ' and
     c                             c2mott = 'INTERNETT      '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'LOVENSKIOL' and
     c                             c2mott = 'INTERNETT      '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'WEB       ' and
     c                             c2form = 'NORGROS   ' and
     c                             c2mott = 'INTERNETT      '
     c                   goto      oppdater
     c                   endif
      *
     c                   if        c2type = 'FTP       ' and
     c                             c2form = 'NORGROS   ' and
     c                             c2mott = 'INTERNETT      '
     c                   goto      oppdater
     c                   endif
5.51 c                   if        c2type = 'FTP       ' and
5.51 c                             c2form = 'NORGROS   ' and
5.51 c                             c2mott = 'NBS            '
5.51 c                   goto      oppdater
     c                   endif
      *
7.01  *
7.01 c                   if        u_701 = *on
7.01 c                   if        c2type = 'COPY      ' and
7.01 c                             c2form = 'EDI       ' and
7.01 c                             c2mott = 'A05            '
7.01 c                   goto      oppdater
7.01 c                   endif
7.01 c                   endif
      *
8.04 c                   if        c2type = 'NGN_EDI   ' and
8.04 c                             c2form = 'NGN_EDI   ' and
8.04 c                             c2mott = 'NGN_EDI   '
8.04 c                   goto      oppdater
8.04 c                   endif
      *
     c                   eval      *in32 = *on
     c                   goto      c2tagb
      *
      * Oppdaterer tabell
      *
     c     oppdater      tag
     c                   clear                   lldilur
      *
     c                   eval      lldilu_ldor = c1ldor
     c                   eval      lldilu_lage = c1lage
     c     lldilu_key    chain     lldilur                            90
      *
     c                   eval      lptype = c2type
     c                   eval      lpform = c2form
     c                   eval      lpmott = c2mott
     c                   eval      lplknr = c2lknr
     c                   eval      lpftpr = c2ftpr
     c                   eval      lpfaxn = c2faxn
     c                   eval      lpmail = c2mail
6.22 c                   eval      lpnebg = c2nebg
6.23 c                   eval      lpotyp = c2otyp
     c                   eval      lpeusr = l_user
      *
     c                   time                    lpedat
     c                   time                    lpetim
      *
     c                   if        *in90 = *off
     c                   update    lldilur
     c                   else
     c                   eval      lpfirm = w_firm
     c                   eval      lpldor = c1ldor
     c                   eval      lplage = c1lage
     c                   write     lldilur
     c                   endif
      *
8.02 c     lldilu_key    chain     lldnlu
8.02 c                   if        not %found
8.02 c                   clear                   lldnlur
8.02 c                   eval      lnfirm = w_firm
8.02 c                   eval      lnldor = lldilu_ldor
8.02 c                   eval      lnlage = lldilu_lage
8.02 c                   time                    lnodat
8.02 c                   time                    lnotim
8.02 c                   eval      lnousr = l_user
8.02 c                   endif
8.02  *
8.02 c                   eval      lnnoty = c2noty
8.02  *
8.02 c                   if        %found
8.02 c                   time                    lnedat
8.02 c                   time                    lnetim
8.02 c                   eval      lneusr = l_user
8.02 c                   update    lldnlur
8.02 c                   else
8.02 c                   write     lldnlur
8.02 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ldor = lpldor
     c                   eval      b1type = lptype
     c                   eval      b1form = lpform
     c                   eval      b1mott = lpmott
     c                   eval      b1dato = 0
     c                   eval      b1time = 0
     c                   if        lpsida <> *loval
     c     *dmy          move      lpsida        b1dato
     c                   endif
     c                   if        lpsikl <> *loval
     c     *hms          move      lpsikl        b1time
     c                   endif
     c                   endif
      *
     c                   if        b1valg = 1 or b2valg = 1 or
     c                             b1valg = 3 or b2valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
8.01 c                   eval      *in50  = *off
      *
     c                   endsr
      *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.10  * I2WIN Behandle bilde
8.01  * Gjenninnført F9, brukes til automatiske EDI
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.10  *
6.10 c     xi2win        begsr
6.10  *
8.01 c**                 eval      i2nebg = *blanks
8.01 c                   eval      i2aobk = '0'
8.01 c                   eval      i2apak = '0'
8.01 c                   eval      i2afak = '0'
8.01 c                   eval      i2logn = '0'
8.06 c                   eval      i2ityp = *blanks
8.06 c                   eval      i2itxt = *blanks
6.10  *
8.01 c     lldilr_key    chain     lldnl1
6.21 c                   if        %found
8.01 c**                 eval      i2nebg= lpnebg
8.01 c                   eval      i2aobk= %char(lnaobk)
8.01 c                   eval      i2apak= %char(lnapak)
8.01 c                   eval      i2afak= %char(lnafak)
8.01 c                   eval      i2logn= %char(lnlogn)
8.06 c                   eval      i2ityp= lnityp
8.06  *
8.06 c                   if        i2ityp <> *blank
8.06 c                   eval      vinfl1_ityp = i2ityp
8.06 c     vinfl1_key    chain     vinfl1r
8.06 c                   if        %found
8.06 c                   eval      i2itxt = vaitxt
8.06 c                   endif
8.06 c                   endif
6.10  *
8.06 c                   endif
6.10  *
8.06 c                   eval      w_alin = 6
8.06 c                   eval      w_acol = 10
6.10  *
8.01 c     xi2taga       tag
6.10  *
6.10 c                   exfmt     i2win
6.10  *
6.10 c     *inkc         cabeq     *on           i2tag1
6.10 c     *inkl         cabeq     *on           i2tag1
      *
8.06 c                   if        *inkd = *on
8.06 c                   exsr      spØrring
8.06 c                   goto      xi2taga
8.06 c                   endif
6.10  *
8.01 c                   if        i2aobk <> '0' and
8.01 c                             i2aobk <> '1'
8.01 c                   eval      *in31 = *on
8.01 c                   goto      xi2taga
8.01 c                   endif
8.01  *
8.01 c                   if        i2apak <> '0' and
8.01 c                             i2apak <> '1'
8.01 c                   eval      *in31 = *on
8.01 c                   goto      xi2taga
8.01 c                   endif
8.01  *
8.01 c                   if        i2afak <> '0' and
8.01 c                             i2afak <> '1'
8.01 c                   eval      *in31 = *on
8.01 c                   goto      xi2taga
8.01 c                   endif
8.01  *
8.01 c                   if        i2logn <> '0' and
8.01 c                             i2logn <> '1'
8.01 c                   eval      *in31 = *on
8.01 c                   goto      xi2taga
8.01 c                   endif
8.06  *
8.06 c                   if        i2ityp <> *blank
8.06 c                   eval      vinfl1_ityp = i2ityp
8.06 c     vinfl1_key    chain     vinfl1r
8.06 c                   if        not %found
8.06 c                   eval      *in34 = *on
8.06 c                   goto      xi2taga
8.06 c                   else
8.06 c                   eval      i2itxt = vaitxt
8.06 c                   endif
8.06 c                   endif
6.10  *
6.21 c**   lldilr_key    chain     lldilu
8.01 c     lldilr_key    chain     lldnlu
8.01 c                   if        not %found
8.01 c                   clear                   lldnlur
8.01 c                   eval      lnfirm = w_firm
8.01 c                   eval      lnldor = lldilr_ldor
8.01 c                   eval      lnlage = lldilr_lage
8.01 c                   time                    lnodat
8.01 c                   time                    lnotim
8.01 c                   eval      lnousr = l_user
8.01 c                   endif
8.01  *
8.01 c                   time                    lnedat
8.01 c                   time                    lnetim
8.01 c                   eval      lneusr = l_user
8.01  *
8.01 c**                 eval      lpnebg = i2nebg
8.01 c                   eval      lnaobk = %int(i2aobk)
8.01 c                   eval      lnapak = %int(i2apak)
8.01 c                   eval      lnafak = %int(i2afak)
8.01 c                   eval      lnlogn = %int(i2logn)
8.06 c                   eval      lnityp = i2ityp
6.21  *
6.21 c                   if        %found
8.01 c                   update    lldnlur
8.01 c                   else
8.01 c                   write     lldnlur
6.10 c                   endif
6.10  *
6.10 c     i2tag1        tag
6.10  *
6.10 c                   endsr
6.10  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      lldilr_ldor = d1ldor
     c                   eval      lldilr_lage = d1lage
     c     lldilr_key    chain     lldilr                             90
      *
     c                   eval      rlevl1_levr = d1ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      d1lnav = rlnavn
     c                   eval      d1type = lptype
     c                   eval      d1form = lpform
     c                   eval      d1mott = lpmott
      *
     c                   eval      w_lage_alf = d1lage
     c                   exsr      hent_lag
     c                   eval      d1lagn = w_latx
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      lldilu_ldor = lldilr_ldor
     c                   eval      lldilu_lage = lldilr_lage
     c     lldilu_key    chain     lldilur                            90
     c                   delete    lldilur
      *
8.02 c     lldilu_key    chain     lldnlur
8.02 c                   if        %found
8.02 c                   delete    lldnlur
8.02 c                   endif
8.02  *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av harkodet søk  (F1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1win1       begsr
      *
     c                   exsr      xf1nul
     c                   eval      f1alt1 = 'FTP      '
     c                   eval      f1alt2 = 'LU0      '
     c                   eval      f1alt3 = 'SNA      '
     c                   eval      f1alt4 = 'WEB      '
8.03 c                   eval      f1alt5 = 'NGN_EDI  '
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_f1win1
     c                   endif
      *
     c                   select
     c                   when      f1val1 = '1'
     c                   eval      w_type = f1alt1
     c                   when      f1val2 = '1'
     c                   eval      w_type = f1alt2
     c                   when      f1val3 = '1'
     c                   eval      w_type = f1alt3
     c                   when      f1val4 = '1'
     c                   eval      w_type = f1alt4
     c                   when      f1val5 = '1'
     c                   eval      w_type = f1alt5
     c                   when      f1val6 = '1'
     c                   eval      w_type = f1alt6
     c                   when      f1val7 = '1'
     c                   eval      w_type = f1alt7
     c                   when      f1val8 = '1'
     c                   eval      w_type = f1alt8
     c                   endsl
      *
     c     end_f1win1    endsr
      *
     c     xf1win2       begsr
      *
     c                   exsr      xf1nul
     c                   eval      f1alt1 = 'EDI       '
     c                   eval      f1alt2 = 'WEBTRADE  '
     c                   eval      f1alt3 = 'FARVERING '
     c                   eval      f1alt4 = 'EFONELFO  '
     c                   eval      f1alt5 = 'LOVENSKIOL'
     c                   eval      f1alt6 = 'NORGROS   '
8.03 c                   eval      f1alt7 = 'NGN_EDI   '
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_f1win2
     c                   endif
      *
     c                   select
     c                   when      f1val1 = '1'
     c                   eval      w_form = f1alt1
     c                   when      f1val2 = '1'
     c                   eval      w_form = f1alt2
     c                   when      f1val3 = '1'
     c                   eval      w_form = f1alt3
     c                   when      f1val4 = '1'
     c                   eval      w_form = f1alt4
     c                   when      f1val5 = '1'
     c                   eval      w_form = f1alt5
     c                   when      f1val6 = '1'
     c                   eval      w_form = f1alt6
     c                   when      f1val7 = '1'
     c                   eval      w_form = f1alt7
     c                   when      f1val8 = '1'
     c                   eval      w_form = f1alt8
     c                   endsl
      *
     c     end_f1win2    endsr
      *
     c     xf1win3       begsr
      *
     c                   exsr      xf1nul
     c                   eval      f1alt1 = 'IBM            '
     c                   eval      f1alt2 = 'INTERNETT      '
     c                   eval      f1alt3 = 'JERNIA-X25     '
8.03 c                   eval      f1alt4 = 'NBS            '
8.03 c                   eval      f1alt5 = 'NGN_EDI        '
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_f1win3
     c                   endif
      *
     c                   select
     c                   when      f1val1 = '1'
     c                   eval      w_mott = f1alt1
     c                   when      f1val2 = '1'
     c                   eval      w_mott = f1alt2
     c                   when      f1val3 = '1'
     c                   eval      w_mott = f1alt3
     c                   when      f1val4 = '1'
     c                   eval      w_mott = f1alt4
     c                   when      f1val5 = '1'
     c                   eval      w_mott = f1alt5
     c                   when      f1val6 = '1'
     c                   eval      w_mott = f1alt6
     c                   when      f1val7 = '1'
     c                   eval      w_mott = f1alt7
     c                   when      f1val8 = '1'
     c                   eval      w_mott = f1alt8
     c                   endsl
      *
     c     end_f1win3    endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å blanke ut hardkodet spørrebilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1nul        begsr
      *
     c                   eval      f1alt1 = *blank
     c                   eval      f1alt2 = *blank
     c                   eval      f1alt3 = *blank
     c                   eval      f1alt4 = *blank
     c                   eval      f1alt5 = *blank
     c                   eval      f1alt6 = *blank
     c                   eval      f1alt7 = *blank
     c                   eval      f1alt8 = *blank
     c                   eval      f1val1 = *blank
     c                   eval      f1val2 = *blank
     c                   eval      f1val3 = *blank
     c                   eval      f1val4 = *blank
     c                   eval      f1val5 = *blank
     c                   eval      f1val6 = *blank
     c                   eval      f1val7 = *blank
     c                   eval      f1val8 = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_ldor   = k1ldor
     c                   eval      w_lagr   = k1lage
     c                   eval      k2ldor   = k1ldor
     c                   eval      k2lage   = k1lage
     c                   eval      k1ldor   = 0
     c                   eval      k1lage   = *blank
      *
      * Fyller kopierings-bildet
      *
     c                   eval      lldilr_ldor = k2ldor
     c                   eval      lldilr_lage = k2lage
     c     lldilr_key    chain     lldilr                             90
      *
     c                   eval      rlevl1_levr = k2ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      k2lnav = rlnavn
      *
     c                   eval      w_lage_alf = k2lage
     c                   exsr      hent_lag
     c                   eval      k2lagn = w_latx
     c                   eval      k1lnav = *blank
     c                   eval      k1lagn = *blank
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Validering
      *
     c                   if        k1ldor = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      rlevl1_levr = k1ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      w_lage_alf = k1lage
     c                   exsr      hent_lag
     c                   if        b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      k1tagb
     c                   endif
6.24  *
6.24  * hvis lager er utfylt sett 0 foran
6.24  *
6.24 c                   if        k1lage <> *blank
6.24 c     ' ':'0'       xlate     k1lage        k1lage
6.24 c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      lldilr_ldor = k1ldor
     c                   eval      lldilr_lage = k1lage
     c     lldilr_key    chain     lldilr                             90
     c                   if        *in90 = *off
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = k1ldor
     c                   eval      c1lage = k1lage
     c                   eval      lldilr_ldor = w_ldor
     c                   eval      lldilr_lage = w_lagr
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      lldil1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        lpfirm <> l_firm
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldor = lpldor
     c                   eval      b1lage = lplage
     c                   eval      b1type = lptype
     c                   eval      b1form = lpform
     c                   eval      b1mott = lpmott
     c                   eval      b1dato = 0
     c                   eval      b1time = 0
      *
     c                   eval      b1lnav = *blank
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
     c                   if        lpsida <> *loval
     c     *dmy          move      lpsida        b1dato
     c                   endif
     c                   if        lpsikl <> *loval
     c     *hms          move      lpsikl        b1time
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in12 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        wactfe = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'C1LDOR'
     c                   eval      c1lnav = *blank
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    k1lnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Lager
      *
     c                   if        wactfe = 'B2LAGE'
     c                   eval      w_lage = 0
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    c1lagn
     c                   move      w_lage        b2lage
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'C1LAGE'
     c                   eval      c1lagn = *blank
     c                   eval      w_lage = 0
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    c1lagn
     c                   move      w_lage        c1lage
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'K1LAGE'
     c                   eval      k1lagn = *blank
     c                   eval      w_lage = 0
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    k1lagn
     c                   move      w_lage        k1lage
     c                   goto      end_spØrr
     c                   endif
8.06  * Ordreinfo kode
8.06 c                   if        wactfe = 'I2ITYP'
8.06 c                   eval      i2ityp = *blank
8.06 c                   eval      i2itxt = *blanks
8.06 c                   call      'VA540R'
8.06 c                   parm                    w_firm
8.06 c                   parm                    i2ityp
8.06 c                   parm                    i2itxt
8.06 c                   goto      end_spØrr
8.06 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_lag      begsr
      *
     c                   if        w_lage_alf = *blank
     c                   eval      w_latx = 'Alle lager'
     c                   goto      end_lag
     c                   else
     c                   move      w_lage_alf    w_lage
     c                   endif
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      w_latx = 'Ugyldig lager'
     c                   endif
      *
     c     end_lag       endsr
      *
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * Subrutine for henting av NEB info
6.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  *
8.02 c     hnt_neb       begsr
6.21  *
6.21 c*                  eval      c2nebg = *blank
8.02 c                   eval      c2noty = *blank
6.21  *
8.02 c     lldilr_key    chain     lldnl1
8.02 c                   if        %found
6.21 c*                  eval      c2nebg= lnnebg
8.02 c                   eval      c2noty= lnnoty
8.02 c                   endif
6.21  *
8.02 c                   endsr
6.21  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering leverandør distribusjon
      *
     c     lldil1_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldil1_ldor
     c                   kfld                    lldil1_lage
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      * Key for oppdatering av leverandør distribusjon
      *
     c     lldilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldilu_ldor
     c                   kfld                    lldilu_lage
6.20  *
6.20  * Key for oppslag på ordretyper
6.20  *
6.20 c     votyl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
8.06  *
8.06  * Key for oppslag på ordreinfo
8.06  *
8.06 c     vinfl1_key    klist
8.06 c                   kfld                    w_firm
8.06 c                   kfld                    vinfl1_ityp
      *
      * Kopierer firma til firma-variabel
      *
     c                   eval      w_firm = l_firm
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'LP100R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lldil1_ldor = 0
     c                   eval      lldil1_lage = *blank
     c     lldil1_key    setll     lldil1
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
