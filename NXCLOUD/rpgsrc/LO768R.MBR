     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO768R
      *  Beskrivelse . . : Generere bestilling i EDIFAKT-format
      *                    Kopi til distribusjonssenter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      * 800   060522 ask  Dette er en kopi a LO760R for sende en duplikatbestilling
      *                   till distribusjonssenter
8.01  *800    190822 ask  Lagt inn "versjonsflagg" ved omsending til distribusjonssenter
8.02  *800    121022 bsa  Finner GTIN nummer på nytt, da det kan gi feil nummer tilbake
8.02  *800                hvis logistikkvare eller leverandør ikke er "normal"
8.02  *800                leverandør på varen.
8.02  *800    041022 bsa  Setter leveringsdato på linje hvis det ikke er satt fra før
8.02  *800                ved sending av EDI bestilling. Forenkler sjekken ved
8.02  *800                EDI bekreftelse i NGN.
8.03  *800    041022 bsa  Setter leveringsdato på linje hvis det ikke er satt fra før
8.03  *800                ved sending av EDI bestilling. Forenkler sjekken ved
8.03  *800                EDI bekreftelse i NGN.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    50-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fra09lr    if   e           k disk    rename(ra09pfr:ra09lrr)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flotxl1    if   e           k disk    rename(lotxpfr:lotxl1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
     f                                     prefix(lx:2)
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fmjlol1    if   e           k disk    rename(mjlopfr:mjlol1r)
     f*ldnl1    if   e           k disk    rename(lldnpfr:lldnl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fra31l1    if   e           k disk    rename(ra31pfr:ra31l1r)
     flwedpf    o    e           k disk
     flwelpf    o    e           k disk
      *
      *  Kommunikasjons-linje til IBM
      *
     d                 ds
     d d_ed99                        80
     d  d_fmsr99                      5    overlay(d_ed99)     inz('FMS01')
     d  d_avsn99                      8    overlay(d_ed99:6)
     d  d_skil99                      4    overlay(d_ed99:14)  inz('    ')
     d  d_mott99                      8    overlay(d_ed99:18)  inz('NOIBMDIN')
      *
      *  Definering av datastrukturer for EDI-format
      *
     d                 ds
     d d_ed00                        80
     d  d_id00                        4    overlay(d_ed00)     inz('ED00')
     d  d_decs00                      1    overlay(d_ed00:5)   inz(',')
     d  d_send00                     13  0 overlay(d_ed00:6)
     d  d_mott00                     13  0 overlay(d_ed00:19)
     d  d_gdat00                      6    overlay(d_ed00:32)
     d  d_gtim00                      4    overlay(d_ed00:38)
     d  d_test00                      1    overlay(d_ed00:42)  inz('0')
     d  d_kva100                      3    overlay(d_ed00:43)  inz('137')
     d  d_mdat00                     12    overlay(d_ed00:46)
     d  d_for100                      3    overlay(d_ed00:58)  inz('203')
     d  d_kva200                      3    overlay(d_ed00:61)  inz('2  ')
     d  d_levd00                      8    overlay(d_ed00:64)
     d  d_for200                      3    overlay(d_ed00:72)  inz('102')
      *
     d                 ds
     d d_ed01                        80
     d  d_id01                        4    overlay(d_ed01)     inz('ED01')
     d  d_mref01                     10    overlay(d_ed01:5)
     d  d_mtyp01                      6    overlay(d_ed01:15)  inz('ORDERS')
     d  d_mver01                      3    overlay(d_ed01:21)  inz('D  ')
     d  d_mrel01                      3    overlay(d_ed01:24)  inz('93A')
     d  d_mkon01                      2    overlay(d_ed01:27)  inz('UN')
     d* d_mfor01                      6    overlay(d_ed01:29)  inz('TBF10 ')
     d  d_mfor01                      6    overlay(d_ed01:29)
     d  d_mkod01                      3    overlay(d_ed01:35)  inz('220')
     d  d_benr01                     35    overlay(d_ed01:38)
     d  d_resp01                      3    overlay(d_ed01:73)  inz('AC ')
      *
     d                 ds
     d d_ed02                        80
     d  d_id02                        4    overlay(d_ed02)     inz('ED02')
     d  d_ftxq02                      3    overlay(d_ed02:5)   inz('ORI')
     d  d_ftxt02                     70    overlay(d_ed02:8)
      *
     d                 ds
     d d_ed03                        80
     d  d_id03                        4    overlay(d_ed03)     inz('ED03')
     d  d_refq03                      3    overlay(d_ed03:5)
     d  d_rtx103                      5    overlay(d_ed03:8)
     d  d_ref103                      8    overlay(d_ed03:13)
     d  d_rtx203                      5    overlay(d_ed03:21)
     d  d_ref203                      8    overlay(d_ed03:26)
      *
     d                 ds
     d d_ed04                        80
     d  d_id04                        4    overlay(d_ed04)     inz('ED04')
     d  d_parq04                      3    overlay(d_ed04:5)
     d  d_part04                     13  0 overlay(d_ed04:8)
     d  d_part04a                    13    overlay(d_ed04:8)
     d  d_porg04                      3    overlay(d_ed04:21)  inz('9  ')
      *
     d                 ds
     d d_ed05                        80
     d  d_id05                        4    overlay(d_ed05)     inz('ED05')
     d  d_pnav05                     30    overlay(d_ed05:5)
     d  d_pad105                     30    overlay(d_ed05:35)
      *
     d                 ds
     d d_ed06                        80
     d  d_id06                        4    overlay(d_ed06)     inz('ED06')
     d  d_pad206                     30    overlay(d_ed06:5)
     d  d_ppnu06                      4    overlay(d_ed06:35)
     d  d_ppst06                     30    overlay(d_ed06:39)
     d  d_plkd06                      3    overlay(d_ed06:69)
      *
     d                 ds
     d d_ed07                        80
     d  d_id07                        4    overlay(d_ed07)     inz('ED07')
     d  d_refq07                      3    overlay(d_ed07:5)   inz('VA ')
     d  d_forp07                      2    overlay(d_ed07:8)   inz('NO')
     d  d_fore07                      9    overlay(d_ed07:10)
     d  d_fors07                      3    overlay(d_ed07:19)  inz('MVA')
     d  d_ctfu07                      3    overlay(d_ed07:22)  inz('PD ')
     d  d_kont07                     30    overlay(d_ed07:25)
      *
     d                 ds
     d d_ed08                        80
     d  d_id08                        4    overlay(d_ed08)     inz('ED08')
     d  d_komm08                     70    overlay(d_ed08:5)
     d  d_komq08                      3    overlay(d_ed08:75)
      *
     d                 ds
     d d_ed09                        80
     d  d_id09                        4    overlay(d_ed09)     inz('ED09')
     d  d_trsq09                      3    overlay(d_ed09:5)   inz('20 ')
     d  d_trko09                      3    overlay(d_ed09:8)
     d  d_fmko09                      8    overlay(d_ed09:11)  inz('Z02')
     d  d_lebq09                      3    overlay(d_ed09:19)  inz('6  ')
     d  d_levb09                      3    overlay(d_ed09:22)
     d  d_levq09                      3    overlay(d_ed09:25)  inz('106')
     d  d_lskq09                      3    overlay(d_ed09:28)  inz('   ')
     d  d_lbst09                     13    overlay(d_ed09:31)
     d  d_lsko09                      3    overlay(d_ed09:44)  inz('   ')
      *
     d                 ds
     d d_ed10                        80
     d  d_id10                        4    overlay(d_ed10)     inz('ED10')
     d  d_vlin10                      6  0 overlay(d_ed10:5)
     d  d_eanr10                     15    overlay(d_ed10:11)
     d  d_eank10                      3    overlay(d_ed10:26)  inz('EN ')
     d  d_piq110                      3    overlay(d_ed10:29)  inz('5  ')
     d  d_alt110                     15    overlay(d_ed10:32)
6.   d  d_alk110                      3    overlay(d_ed10:47)  inz('NO1')
     d  d_piq210                      3    overlay(d_ed10:50)  inz('1  ')
 6.  d  d_alt210                     15    overlay(d_ed10:53)
     d  d_alk210                      3    overlay(d_ed10:68)  inz('SA ')
      *
     d                 ds
     d d_ed11                        80
     d  d_id11                        4    overlay(d_ed11)     inz('ED11')
     d  d_txk111                      3    overlay(d_ed11:5)   inz('F  ')
     d  d_txt111                     35    overlay(d_ed11:8)
     d  d_txt211                     35    overlay(d_ed11:43)
      *
     d                 ds
     d d_ed12                        80
     d  d_id12                        4    overlay(d_ed12)     inz('ED12')
     d  d_kvaq12                      3    overlay(d_ed12:5)   inz('21 ')
     d**d_kvah12                     11  0 overlay(d_ed12:8)
     d  d_sign12                      1    overlay(d_ed12:8)
     d  d_kvah12                     10  0 overlay(d_ed12:9)
     d  d_kvas12                      1    overlay(d_ed12:19)  inz(',')
     d  d_kvad12                      3  3 overlay(d_ed12:20)
     d  d_benh12                      3    overlay(d_ed12:23)
      *
     d                 ds
     d d_ed13                        80
     d  d_id13                        4    overlay(d_ed13)     inz('ED13')
     d  d_lftq13                      3    overlay(d_ed13:5)   inz('LIN')
     d  d_lftx13                     70    overlay(d_ed13:8)
      *
      * Datastruktur for mottakers lokasjonsnummer
      *
     d d_lknr          ds
     d  d_eana                       13
     d    d_eann                     13  0 overlay(d_eana:1)
     d  d_fill                        7
      *
      * Definering av LDA
      *
     d                uds
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lotxl1_numm     s                   like(ltnumm)
     d lotxl1_suff     s                   like(ltsuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lldilr_ldor     s                   like(lpldor)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d ra09lr_ikod     s                   like(raikod)
     d vltyl1_ltyp     s                   like(valtyp)
     d mjlol1_lage     s                   like(mjlage)
     d mjlol1_avde     s                   like(mjavde)
     d mjlol1_skaf     s                   like(mjskaf)
     d*lldnl1_ldor     s                   like(lnldor)
     d*lldnl1_lage     s                   like(lnlage)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d ra31l1_ekod     s                   like(reekod)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
      *
      *  Definering av variable i parameter
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_dist          s              1  0
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_dalf          s              8
     d w_balf          s              6
     d w_salf          s              2
     d w_levb          s              3
     d w_lage          s                   like(lplage)
     d w_lako          s              2  0
     d w_dist          s              2  0
     d w_tr01          s              1  0
     d w_refn          s             10    inz('1         ')
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_dato_8        s              8  0
     d w_time_6        s              6  0
     d w_dato_alf      s              8
     d w_time_alf      s              4
     d w_dat6_alf      s              6
     d w_tim4_alf      s              4
     d w_pibm          s              8
     d w_hean          s             13  0
     d w_fean          s             13  0
     d w_lean          s             13  0
     d w_sean          s             13  0
     d w_east          s              1
     d w_eann          s             14  0
     d w_lnvn          s             30
     d w_lgte          s             30
     d w_lad2          s             30
     d w_lpnr          s              4  0
     d w_lstd          s             30
     d w_flvn          s             30
     d w_flte          s             30
     d w_fld2          s             30
     d w_flnr          s              4  0
     d w_fltd          s             30
     d w_anta          s             11  3
     d w_post          s              4
     d w_land          s              2
     d w_ordnr         s                   like(loornr)
     d w_ordsu         s                   like(loorsu)
     d w_lagd          s                   like(folage)
     d w_lmat          s                   like(folmat)
     d w_eand          s             15  0
      *
     d b_text          s              1    inz(*off)
     d b_lnut          s              1    inz(*off)
     d b_ed10          s              1    inz(*off)
     d b_logp          s              1    inz(*off)
     d b_dlev          s              1    inz(*off)
8.01 d b_dist          s              1    inz(*off)
      *
     d digits          c                   ' 0123456789'
      *
     d u_s701          s              1    inz(*off)
     d u_s702          s              1    inz(*off)
      *
      * Definering av eksterne prg
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Uthenting av kode-, ordrehode-, leverandør- og lageropplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Sjekker om distribusjonslager er definert
      *
     c                   exsr      sjekk_dist
     c                   if        p_dist = 0
     c                   goto      end_pgm
     c                   endif
      * Hent faste data
      *
     c     raa8l1_key    chain     raa8l1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      w_pibm = ra8ibm
     c                   eval      w_hean = ra8ean
     c                   eval      w_fean = w_hean
     c                   eval      w_lean = w_hean
     c                   eval      w_lage = *blank
      *
      * Hent bestillingsinfo
      *
     c     lohel1_key    chain     lohel1r
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
     c                   eval      w_levb = %subst(lolbtx:1:3)
      *
      * Hent distribusjonsinformasjon
      *
     c                   move      lolage        w_lage
     c                   eval      lldilr_ldor = loldor
     c     lldilr_key    chain     lldilr
     c**                 if        not %found
     c**                 goto      end_pgm
     c                   if        %found
     c                   goto      ok_dist
     c                   endif
      *
     c                   eval      w_lage = *blank
     c                   eval      lldilr_ldor = loldor
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
     c     ok_dist       tag
      *
     c                   eval      d_lknr = lplknr
      *
      * Hent leverandørinformasjon
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      * Leverandørinfo
     c                   eval      w_sean = rleanl
     c                   eval      w_lnvn = rlnavn
     c                   eval      w_lgte = rlgate
     c                   eval      w_lad2 = rladr2
     c                   eval      w_lpnr = rlponr
     c                   eval      w_lstd = rlsted
     c                   if        rlland = *blanks
     c                   eval      w_land = 'NO'
     c                   else
     c                   eval      w_land = rlland
     c                   endif
      * Fakturaleverandør
     c                   eval      w_flvn = rlnavn
     c                   eval      w_flte = rlgate
     c                   eval      w_fld2 = rladr2
     c                   eval      w_flnr = rlponr
     c                   eval      w_fltd = rlsted
      *
      * Sjekk fakturaleverandør
     c                   if        rlflev <> 0
     c                   eval      rlevl1_levr = rlflev
     c     rlevl1_key    chain     rlevl1
     c                   if        %found and
     c                             rleanl <> 0
     c                   eval      w_fean = rleanl
     c                   eval      w_flvn = rlnavn
     c                   eval      w_flte = rlgate
     c                   eval      w_fld2 = rladr2
     c                   eval      w_flnr = rlponr
     c                   eval      w_fltd = rlsted
     c                   else
     c                   eval      w_flvn = *blanks
     c                   eval      w_flte = *blanks
     c                   eval      w_fld2 = *blanks
     c                   eval      w_flnr = *zeros
     c                   eval      w_fltd = *blanks
     c                   endif
     c                   else
     c                   eval      w_flvn = aafnav
     c                   eval      w_flte = aafad1
      *
     c                   eval      w_fld2 = aafad2
     c                   eval      w_post = %subst(aafste:1:4)
     c                   eval      w_flnr = %int(w_post)
     c                   eval      w_fltd = %subst(aafste:6)
     c                   endif
      *
      * Hent selgerinformasjon  (e-mail)
      *
     c                   eval      ra09lr_ikod = loinnk
     c     ra09lr_key    chain     ra09lr
     c                   if        not %found
     c                   eval      raikod = 0
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av hode-linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * ED00  - Info til tjenestesegment - henter fra distribusjonssenter
      *
     c                   eval      d_part04 = 0
     c                   if        u_s702 = *on
     c                   exsr      finn_logpkt2
     c                   endif
     c                   if        d_part04 = 0
     c                   eval      d_send00 = w_hean
     c                   else
     c                   eval      d_send00 = d_part04
     c                   endif
      *
     c                   eval      d_mott00 = w_eand

     c                   eval      d_gdat00 = w_dat6_alf
     c                   eval      d_gtim00 = w_tim4_alf
     c                   eval      d_mdat00 = w_dato_alf + w_time_alf
      *
     c                   if        loldat <> *loval
     c     *iso          move      loldat        w_dato_8
     c                   move      w_dato_8      d_levd00
     c                   else
     c                   eval      d_levd00 = w_dato_alf
     c                   endif
      *
     c                   eval      edilin = d_ed00
     c                   write     lwedpfr
     c                   eval      logrec = d_ed00
     c                   write     lwelpfr
      *
      * ED01 - Meldings start informasjon
      *
8.01  * Sjekk om distribusjonssenter er satt opp
8.01  *
8.01 c                   exsr      finn_dist
      *
      * Hent inn info om det er en NEB leverandør
     c                   exsr      sjk_neb
      *
8.01 c                   if        lobsen = 1
8.01 c                   movel     lonumm        d_benr01
8.01 c                   eval      d_benr01 = %subst(d_benr01:1:8) + '_ver_2'
8.01 c                   else
8.01 c                   movel     lonumm        d_benr01
8.01 c                   endif
8.01  *
     c                   eval      d_mref01 = w_refn
8.01 c*                  movel     lonumm        d_benr01
     c                   eval      edilin = d_ed01
     c                   write     lwedpfr
     c                   eval      logrec = d_ed01
     c                   write     lwelpfr
      *
      * ED02 - Fritekst på hodenivå
      *
     c                   eval      lotxl1_numm = lonumm
     c                   eval      lotxl1_suff = losuff
     c     lotxl1_key    setll     lotxl1
     c     lotxl1_key    reade     lotxl1
      *
     c                   dow       not %eof
     c                   eval      d_ftxt02 = lttext
     c                   eval      edilin = d_ed02
     c                   write     lwedpfr
     c                   eval      logrec = d_ed02
     c                   write     lwelpfr
      *
     c     lotxl1_key    reade     lotxl1
     c                   enddo
      *
      * ED03 - Referanse fra bestilling og evt. kundeordre
      *
     c                   eval      d_refq03 = 'CR '
     c                   eval      d_rtx103 = 'BEST '
     c                   movel     lonumm        d_ref103
     c                   if        loornr <>  0
     c                   eval      d_rtx203 = '/ONR '
     c                   movel     loornr        d_ref203
     c                   endif
     c                   eval      edilin = d_ed03
     c                   write     lwedpfr
     c                   eval      logrec = d_ed03
     c                   write     lwelpfr
      *
      * ED04 - ED08 - Partsidentifisering
      *
      * BY -kjøper
      *
     c                   eval      d_part04 = 0
     c                   eval      d_parq04 = 'BY '
     c                   if        u_s701 = *on
     c                   exsr      finn_logpkt2
     c                   endif
      * Hvis vi ikke finner logistikkpkt, kjøres std tildeling
     c                   if        d_part04 = 0
     c                   eval      d_part04 = w_hean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
     c                   eval      d_pnav05 = aafnav
     c                   eval      d_pad105 = aafad1
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = aafad2
     c                   eval      d_ppnu06 = %subst(aafste:1:4)
     c                   eval      d_ppst06 = %subst(aafste:6)
     c                   eval      d_plkd06 = 'NO '
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
     c                   else
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_plkd06 = 'NO '
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
     c                   endif
      *
      *  Foretaksnummer og kjøpers referanse
      *
     c                   movel     aafftn        d_fore07
     c                   eval      d_kont07 = lovref
     c                   eval      edilin = d_ed07
     c                   write     lwedpfr
     c                   eval      logrec = d_ed07
     c                   write     lwelpfr
      *
      *  Kommunikasjonsinformasjon
      *
     c                   if        raikod <> 0 and
     c                             raiema <> *blank
     c                   eval      d_komm08 = raiema
     c                   eval      d_komq08 = 'EM '
     c                   eval      edilin = d_ed08
     c                   write     lwedpfr
     c                   eval      logrec = d_ed08
     c                   write     lwelpfr
     c                   endif
      *
      * SU -selger
      *
     c                   eval      d_parq04 = 'SU '
     c                   eval      d_part04 = w_sean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
     c                   eval      d_pnav05 = w_lnvn
     c                   eval      d_pad105 = w_lgte
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = w_lad2
     c                   eval      d_ppnu06 = %char(w_lpnr)
     c                   eval      d_ppst06 = w_lstd
     c**                 eval      d_plkd06 = 'NO '
     c                   eval      d_plkd06 = w_land
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      *
      * IV -fakturammotager
      *
     c                   eval      d_parq04 = 'IV '
     c                   eval      d_part04 = w_fean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
      *
     c                   eval      d_pnav05 = w_flvn
     c                   eval      d_pad105 = w_flte
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = w_fld2
     c                   eval      d_ppnu06 = %char(w_flnr)
     c                   eval      d_ppst06 = w_fltd
     c                   eval      d_plkd06 = 'NO '
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
     c**7.04             endif
      *
      * SF -Hentested
      *
     c                   if        w_levb = 'EXW'
     c                   eval      d_parq04 = 'SF '
     c                   eval      d_part04 = w_sean
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
     c                   eval      d_pnav05 = w_lnvn
     c                   eval      d_pad105 = w_lgte
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = w_lad2
     c                   eval      d_ppnu06 = %char(w_lpnr)
     c                   eval      d_ppst06 = w_lstd
     c                   eval      d_plkd06 = 'NO '
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
     c                   endif
      *
      * DP -varemottaker - bruker logistikkpkt. hvis det er registrert.
      *
      * Sjekk mot ordren for om det skal legges ut adressen fra bestilling som logpkt.
     c                   eval      d_part04a = *blanks
     c                   eval      b_logp = *off
     c                   eval      b_dlev = *off
     c                   eval      fohel1_numm = loornr
     c                   eval      fohel1_suff = loorsu
     c     fohel1_key    chain     fohel1
     c                   if        %found
     c                   if        folety = 1
     c                   eval      b_dlev = *on
     c                   endif
     c                   endif
      *
     c                   if        b_dlev = *off
     c                   exsr      finn_logpkt
     c                   endif
      *
     c                   if        b_logp = *off
     c                   eval      d_parq04 = 'DP '
     c                   if        b_dlev = *off
     c                   eval      d_part04 = w_lean
     c                   endif
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
     c                   eval      d_pnav05 = lonavn
     c                   eval      d_pad105 = loadr1
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = loadr2
     c                   eval      d_ppnu06 = %subst(losted:1:4)
     c                   eval      d_ppst06 = %subst(losted:6)
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
     c                   endif
      *
      * henter EDI kode fra forsendelsesmåte bestilling
     c                   eval      ra31l1_ekod = lolmat
     c     ra31l1_key    chain     ra31l1
     c                   if        %found(ra31l1) and
     c                             reeedi <> 0
     c                   eval      lolmat = reeedi
     c                   endif
      *
      * ED09 - Transport og leveringsinformasjon
      *
     c                   move      lolmat        w_tr01
     c                   movel     w_tr01        d_trko09
     c                   movel     w_tr01        d_fmko09
      *
     c                   if        lolmat = 4
     c                   eval      d_lebq09 = '4  '
     c                   endif
      *
     c                   if        w_levb = 'FCA' or
     c                             w_levb = 'FAS' or
     c                             w_levb = 'FOB' or
     c                             w_levb = 'CFR' or
     c                             w_levb = 'CIF' or
     c                             w_levb = 'CPT' or
     c                             w_levb = 'CIP' or
     c                             w_levb = 'DES' or
     c                             w_levb = 'DEQ' or
     c                             w_levb = 'DDU' or
     c                             w_levb = 'DDP' or
     c                             w_levb = 'EXW' or
     c                             w_levb = 'DAF'
     c                   eval      d_levb09 = w_levb
     c                   else
     c                   eval      d_levb09 = 'FOB'
     c                   endif
      *
     c                   eval      d_lbst09 = *blank
      *
     c                   eval      edilin = d_ed09
     c                   write     lwedpfr
     c                   eval      logrec = d_ed09
     c                   write     lwelpfr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av detalj-linjer, tekstlinjer blir fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
      *
      * Intern linjetype?
      *
     c                   exsr      sjekk_type
     c                   if        b_lnut = *off
     c                   goto      neste_linje
     c                   endif
      *
      * ED13 - Fritekstlinje
      *
     c                   if        b_text = *on
     c                   if        ldtek1 <> *blank or
     c                             ldtek2 <> *blank
     c                   eval      d_lftx13 = ldtek1 + ldtek2
     c                   if        b_ed10 = *on
     c                   eval      edilin = d_ed13
     c                   write     lwedpfr
     c                   eval      logrec = d_ed13
     c                   write     lwelpfr
     c                   eval      d_lftx13 = *blank
     c                   endif
     c                   endif
     c                   goto      neste_linje
     c                   endif
      *
      * ED10 - Nummer identifikasjon
      *
     c                   eval      d_vlin10 = ldline
      *
     c                   exsr      finn_varenr
      *
8.03  * Oppdater med mer er funnet, oppdater bestillingslinje med info
8.03  *
8.03 c                   if        ldldat = *loval and
8.03 c                             loldat <> *loval
8.03 c                   eval      lodtlu_numm = ldnumm
8.03 c                   eval      lodtlu_suff = ldsuff
8.03 c                   eval      lodtlu_line = ldline
8.03 c     lodtlu_key    chain     lodtlu
8.03 c                   if        %found
8.03 c                   eval      lxldat = loldat
8.03 c                   update    lodtlur
8.03 c                   endif
8.03 c                   endif
8.03  *
     c                   eval      edilin = d_ed10
     c                   write     lwedpfr
     c                   eval      logrec = d_ed10
     c                   write     lwelpfr
     c                   eval      b_ed10 = *on
      *
      * ED11 - Varebeskrivelse
      *
     c                   eval      d_txt111 = ldtek1
     c                   eval      d_txt211 = ldtek2
     c                   eval      edilin = d_ed11
     c                   write     lwedpfr
     c                   eval      logrec = d_ed11
     c                   write     lwelpfr
      *
      * ED12 - Mengde/enhetsangivelse
      *
     c*                  eval      d_kvah12 = %inth(ldanta)
     c                   if        ldanta > 0
     c                   eval      d_sign12 = '0'
     c                   else
     c                   eval      d_sign12 = '-'
     c                   endif
     c                   if        ldanta < 0
     c                   eval      w_anta = ldanta * -1
     c                   else
     c                   eval      w_anta = ldanta
     c                   endif
      *
     c**                 eval      d_kvah12 = %int(ldanta)
     c                   eval      d_kvah12 = %int(w_anta)
     c**                 eval      d_kvad12 = ldanta - d_kvah12
     c                   eval      d_kvad12 = w_anta - d_kvah12
     c                   eval      d_benh12 = ldenh1
     c                   eval      edilin = d_ed12
     c                   write     lwedpfr
     c                   eval      logrec = d_ed12
     c                   write     lwelpfr
      *
     c                   if        d_lftx13 <> *blank
     c                   eval      edilin = d_ed13
     c                   write     lwedpfr
     c                   eval      logrec = d_ed13
     c                   write     lwelpfr
     c                   eval      d_lftx13 = *blank
     c                   endif
      *
     c     neste_linje   tag
     c     lodtl1_key    reade     lodtl1r                                94
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne ean-nummer og nobb-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_varenr   begsr
     c                   eval      d_eanr10 = *blank
     c                   eval      w_eann = 0
      *
      * Kaller program for henting av ean-nr
      *
     c**                 call      'VE711R'
8.02 c**                 call      'VE713R'
8.02 c**                 parm                    w_firm
8.02 c**                 parm                    loldor
8.02 c**                 parm                    ldvare
8.02 c**                 parm                    ldenh1
8.02 c**                 parm                    w_eann
8.02 c**                 parm                    w_east
      *
8.02 c                   call      'VE723R'
8.02 c                   parm                    w_firm
8.02 c                   parm                    loldor
8.02 c                   parm                    lolage
8.02 c                   parm                    ldvare
8.02 c                   parm                    ldenh1
8.02 c                   parm                    w_eann
8.02 c                   parm                    w_east
      *
     c                   if        w_east = '1' and
     c                             w_eann <> 0
     c                   movel     w_eann        d_eanr10
     c                   else
     c                   eval      d_eanr10 = *blank
     c                   endif
      * Hvis GTIN nummer er funnet, oppdater bestillingslinje med info
     c                   if        w_eann <> 0
     c                   eval      lodtlu_numm = ldnumm
     c                   eval      lodtlu_suff = ldsuff
     c                   eval      lodtlu_line = ldline
     c     lodtlu_key    chain     lodtlu
     c                   if        %found
     c                   eval      lxeann = w_eann
     c                   update    lodtlur
     c                   endif
     c                   endif
      *
     c                   if        ldnobb = 0
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   movel     vvnobb        d_alt110
     c*                   eval      d_alt210 = vvlvar
     c                   movel     vvlvar        d_alt210
     c                   else
     c                   eval      d_alt110 = *blanks
     c                   eval      d_alt210 = *blanks
     c                   endif
     c                   else
     c                   movel     ldnobb        d_alt110
     c*                   eval      d_alt210 = ldlvar
     c                   movel     ldlvar        d_alt210
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om fritekstlinje som skal sendes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_type    begsr
      *
     c                   eval      b_text = *off
     c                   eval      b_lnut = *on
      *
      * Les linjetype
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        %found and
     c                             valktx = 1
     c                   eval      b_text = *on
     c                   endif
      *
      * Ikke skrives ut?
      *
     c                   if        lootyp = valo01 or
     c                             lootyp = valo02 or
     c                             lootyp = valo03 or
     c                             lootyp = valo04
     c                   eval      b_lnut = *off
     c                   endif
      *
     c                   endsr
      *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *  Subrutine for å sjekke distribusjonssenter
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     finn_dist     begsr
8.01  *
8.01
8.01  // Sjekker om distribusjonssenter er angitt
8.01
8.01     exec SQL
8.01       select folmat into :w_lmat
8.01         from fohepf
8.01         where fofirm = :w_firm
8.01           and fonumm = :loornr
8.01           and fosuff = :loorsu;
8.01
8.01     exec SQL
8.01       select rkrdla into :w_dist
8.01         from ra36st
8.01         where rkrfir = :w_firm
8.01           and rkrfsk = :w_lmat
8.01           and rkrsla = :lolage;
8.01
8.01        if sqlcod = 0;
8.01          w_lako = w_dist;
8.01          b_dist = *on;
8.01        endif;
8.01
8.01  *
8.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne logistikkpunkt (varemottager)
      *
     c     finn_logpkt   begsr
      *
     c**                 eval      b_logp = *off
      *

      // Sjekker om leveringsadresse skal overstyres fra distribusjonssenter

         w_lako = lolage;

         exec SQL
           select rkrdla into :w_dist
             from ra36st
             where rkrfir = :w_firm
               and rkrfsk = :folmat
               and rkrsla = :w_lako;

            if sqlcod = 0;
              w_lako = w_dist;
            endif;

     c                   eval      mjlol1_lage = w_lako
     c                   eval      mjlol1_avde = loavde
      *
     c                   if        loornr <> *zero
     c                   eval      mjlol1_skaf = 'S'
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   else
     c                   eval      mjlol1_avde = *zero
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_logp = *off
     c                   eval      mjlol1_lage = w_lako
     c                   eval      mjlol1_avde = loavde
     c                   eval      mjlol1_skaf = *blank
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   else
     c                   eval      mjlol1_avde = *zero
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_logp = *off
     c                   leavesr
     c                   endif
      *
     c                   eval      d_parq04 = 'DP '
     c                   eval      d_part04 = mjeann
      *
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
     c                   eval      d_pnav05 = mjsted
     c                   eval      d_pad105 = mjadr1
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pad206 = mjadr2
      *
     c                   if        %subst(mjpste:1:4) <> *blank and
     c                             %check(digits : %subst(mjpste:1:4)) = 0
     c                   eval      d_ppnu06 = %subst(mjpste:1:4)
     c                   eval      d_ppst06 = %trim(%subst(mjpste:5))
     c                   else
     c                   eval      d_ppnu06 = *blank
     c                   eval      d_ppst06 = %trim(mjpste)
     c                   endif
      *
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne info som legges ut for BY
      *
     c     finn_logpkt2  begsr
      *
     c                   eval      mjlol1_lage = lolage
     c                   eval      mjlol1_avde = loavde
      *
     c                   if        loornr <> *zero
     c                   eval      mjlol1_skaf = 'S'
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   else
     c                   eval      mjlol1_avde = *zero
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_logp = *off
     c                   eval      mjlol1_lage = lolage
     c                   eval      mjlol1_avde = loavde
     c                   eval      mjlol1_skaf = *blank
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   else
     c                   eval      mjlol1_avde = *zero
     c     mjlol1_key    chain     mjlol1
     c                   if        %found
     c                   eval      b_logp = *on
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        b_logp = *off
     c                   leavesr
     c                   endif
      *
     c                   eval      d_part04 = mjeann
      *
     c                   eval      d_pnav05 = mjsted
     c                   eval      d_pad105 = mjadr1
      *
     c                   eval      d_pad206 = mjadr2
      *
     c                   if        %subst(mjpste:1:4) <> *blank and
     c                             %check(digits : %subst(mjpste:1:4)) = 0
     c                   eval      d_ppnu06 = %subst(mjpste:1:4)
     c                   eval      d_ppst06 = %trim(%subst(mjpste:5))
     c                   else
     c                   eval      d_ppnu06 = *blank
     c                   eval      d_ppst06 = %trim(mjpste)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk om leverandør er NEB standard
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     sjk_neb       begsr
      *
     c                   eval      d_mfor01 = 'TBF10 '
      * Settes kode for NEB formatering ?
     c*                  eval      lldnl1_ldor = loldor
     c*                  move      lolage        lldnl1_lage
     c*    lldnl1_key    chain     lldnl1
     c*                  if        %found and
     c                   if        lpnebg = '1'
     c                   eval      d_mfor01 = 'NEB01 '
     c*                  else
     c*                  eval      lldnl1_lage = *blank
     c*    lldnl1_key    chain     lldnl1
     c*                  if        %found and
     c*                            lnnebg = '1'
     c*                  eval      d_mfor01 = 'NEB01 '
     c*                  endif
     c                   endif
      *
     c                   endsr

      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      // Subrutine for å finne distribusjonslager
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

         begsr sjekk_dist;
          p_dist = 0;
          w_ordnr = 0;
          w_ordsu = 0;
          w_lagd = 0;
          w_lmat = 0;
          w_eand = 0;
          w_dist = 0;

      // Finn ordrenummer på bestilling

          exec sql
           select loornr,loorsu
             into :w_ordnr, :w_ordsu
            from lohepf
             where lofirm = :w_firm and lonumm = :p_numm and losuff = :p_suff;

      // Finn lager og leveringsmåte på ordre

          if w_ordnr > 0;
           exec sql
            select folage, folmat
             into :w_lagd, :w_lmat
            from fohepf
             where fofirm = :w_firm and fonumm = :w_ordnr and fosuff = :w_ordsu;
          endif;


      // Finn finn kobling til distribusjonslager

          if w_lmat > 0 and
             w_lagd > 0;
           exec SQL
            select rkrdla into :w_dist
              from ra36st
              where rkrfir = :w_firm
                and rkrfsk = :w_lmat
                and rkrsla = :w_lagd;
          endif;

      // Hent GLN på distribusjonslager

          if w_dist > 0;
           exec SQL
           select mjeann into :w_eand
             from mjlopf
             where mjfirm = :w_firm
               and mjlage = :w_dist
               and mjavde = 0;
          endif;

          if w_eand > 0;
           p_dist = 1;
          endif;

         endsr;

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_dist
      *
      *  Key for file les FIRMA-OPPLYSNINGER
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les FASTE DATA
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for les av BESTILLING-TEKST-REGISTER
      *
     c     lotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lotxl1_numm
     c                   kfld                    lotxl1_suff
      *
      *  Key for file les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    w_lage
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av innkjøper
      *
     c     ra09lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09lr_ikod
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for posisjonering logistikkpunkt
      *
     c     mjlol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mjlol1_lage
     c                   kfld                    mjlol1_avde
     c                   kfld                    mjlol1_skaf
      *
      * Key for oppslag på NEB leverandør
      *
     c*    lldnl1_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    lldnl1_ldor
     c*                  kfld                    lldnl1_lage
      *
      * Key for oppslag på linjetype
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på linjetype
      *
     c     ra31l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra31l1_ekod
      *
      *  Key for file les BESTILLING-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
      *
      *  Hent opplysninger fra FIRMA og LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c     afirl1_key    chain     afirl1r
      *
      *  Legger inn dagens dato
      *
     C                   time                    w_udat
     C                   time                    w_utim
      *
     c     *iso          move      w_udat        w_dato_8
     c     *hms          move      w_utim        w_time_6
     c                   move      w_dato_8      w_dato_alf
     c                   movel     w_time_6      w_time_alf
     c                   move      w_dato_8      w_dat6_alf
     c                   movel     w_time_6      w_tim4_alf
      *
      *  Bygger nøkkel for loginformasjon
      *
     c                   eval      logind = 'O'
     c                   eval      logtyp = 'ORDR'
     c                   eval      logdat = w_udat
     c                   eval      logtid = w_utim
      *
      *
      * bruker GLN fra logistikkpunkt som betaler.
      *
       CallP CO402R(l_filg:w_firm:0:'LO760_701':
       co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_s701 = *on;
       endif;
      *
      *
      * bruker GLN fra logistikkpunkt som avsender i konvolutten
      *
       CallP CO402R(l_filg:w_firm:0:'LO760_702':
       co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_s702 = *on;
       endif;
      *
      *
     c                   endsr
































