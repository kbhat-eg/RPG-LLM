     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RR100R                                              *
      * Beskrivelse . . : Resultatrapport, vedlikehold rapporthode            *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150902 KOB  Nytt program
      * T5.10 050503 KOB  Lagt inn test på endringsdato
7.xx  * 7.xx  120516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frrf0l1    if   e           k disk    rename(rrf0pfr:rrf0l1r)
     frrf0l2    if   e           k disk    rename(rrf0pfr:rrf0l2r)
     frrf0lr    if   e           k disk    rename(rrf0pfr:rrf0lrr)
     frrf0lu    uf a e           k disk    rename(rrf0pfr:rrf0lur)
     frrf5l1    if   e           k disk    rename(rrf5pfr:rrf5l1r)
     frrf5lu    uf a e           k disk    rename(rrf5pfr:rrf5lur)
     frrf8l1    if   e           k disk    rename(rrf8pfr:rrf8l1r)
     frrf8lu    uf a e           k disk    rename(rrf8pfr:rrf8lur)
     frrf9l1    if   e           k disk    rename(rrf9pfr:rrf9l1r)
     frrf9lu    uf   e           k disk    rename(rrf9pfr:rrf9lur)
      *
     frr100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rrf0l1_0rap     s                   like(rr0rap)
     d rrf0l2_0txt     s                   like(rr0txt)
     d rrf0lr_0rap     s                   like(rr0rap)
     d rrf0lu_0rap     s                   like(rr0rap)
      *
     d rrf5l1_5rap     s                   like(rr5rap)
     d rrf5lu_5rap     s                   like(rr5rap)
     d rrf5lu_5lin     s                   like(rr5lin)
      *
     d rrf8l1_8rap     s                   like(rr8rap)
     d rrf8lu_8rap     s                   like(rr8rap)
     d rrf8lu_8lin     s                   like(rr8lin)
     d rrf8lu_8lØp     s                   like(rr8lØp)
      *
     d rrf9l1_9rap     s                   like(rr9rap)
     d rrf9lu_9rap     s                   like(rr9rap)
     d rrf9lu_9lin     s                   like(rr9lin)
     d rrf9lu_9avd     s                   like(rr9avd)
     d rrf9lu_9aar     s                   like(rr9aar)
     d rrf9lu_9typ     s                   like(rr9typ)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d p_0rap          s                   like(rr0rap)
     d p_0txt          s                   like(rr0txt)
     d p_5lin          s                   like(rr5lin)
     d p_arow          s              3  0
     d p_acol          s              3  0
     d p_mld1          s             30
     d p_mld2          s             30
      *
     d w_firm          s                   like(rr0fir)
     d p_rper          s              2  0
      *
     d s_0rap          s                   like(rr0rap)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
      *
      * F8=Generering
      *
     c                   when      *inkh
     c                   call      'RR634R'
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b20rap <> 0 or
     c                             b20txt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = b10txt
     c     rrf0l2_key    setll     rrf0l2
     c                   other
     c                   eval      rrf0l1_0rap = b10rap
     c     rrf0l1_key    setll     rrf0l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rapport
      *
     c                   if        b20rap <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rrf0l1_0rap = b20rap
     c     rrf0l1_key    setll     rrf0l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b20txt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = b20txt
     c     rrf0l2_key    setll     rrf0l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b20rap = 0
     c                   eval      b20txt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c10rap = b10rap
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k10rap = b10rap
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d10rap = b10rap
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c10rap = b10rap
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *                                                                 .
      * Utskrift av rapport                                             .
      *                                                                 .
     c                   if        b1valg = 6                                          ..... .
     c                   eval      p_0rap = b10rap                                         . .
      *                                                                 .
     c                   call      'RR630R'                                                . .
     c                   parm                    p_0rap                                    . .
      *                                                                 .
     c                   eval      b1valg = 0                                              . .
     c                   update    b1sfl
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *                                                                 .
      * Vedlikeholde linjer                                             .
      *                                                                 .
     c                   if        b1valg = 7                                          ..... .
     c                   eval      p_0rap = b10rap                                         . .
     c                   eval      p_0txt = b10txt                                         . .
      *
     c                   call      'RR105R'
     c                   parm                    p_0rap
     c                   parm                    p_0txt
      *
     c                   eval      b1valg = 0                                              . .
     c                   update    b1sfl
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *                                                                 .
      * Beregning av rapport                                            .
      *                                                                 .
     c                   if        b1valg = 8                                          ..... .
      *
     c                   move      8             p_arow
     c                   move      38            p_acol
     c                   eval      p_mld1 = 'Vennligst vent ......'
     c                   eval      p_mld2 = 'Beregning pågår .....'
      *
     c                   call      'AA006C'
     c                   parm                    p_arow
     c                   parm                    p_acol
     c                   parm                    p_mld1
     c                   parm                    p_mld2
      *
     c                   eval      p_0rap = b10rap                                         . .
      *
     c                   call      'RR109R'
     c                   parm                    p_0rap
     c                   parm                    p_rper
      *
     c                   call      'RR510R'
     c                   parm                    p_0rap
      *
     c                   eval      b1valg = 0                                                .
     c                   update    b1sfl
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *                                                                 .
      * Skriv manglende konti                                           .
      *                                                                 .
     c                   if        b1valg = 9                                          ..... .
     c                   eval      p_0rap = b10rap                                         . .
      *                                                                 .
     c                   call      'RR625C'                                                . .
     c                   parm                    p_0rap
      *                                                                 .
     c                   eval      b1valg = 0                                              . .
     c                   update    b1sfl
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c10rap = b20rap
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c10rap = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rrf0lr_0rap = c10rap
     c     rrf0lr_key    chain     rrf0lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c10rap = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b20rap = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rrf0lr_0rap = c10rap
     c     rrf0lr_key    chain     rrf0lr
      *
     c                   if        %found
     c                   eval      c20txt = rr0txt
     c                   eval      c20bfj = rr0bfj
     c                   eval      c20avv = rr0avv
     c                   eval      c20hel = rr0hel
     c                   eval      c2eusr = rr0usr
      *
T5.10c                   if        rr0dat = *loval
T5.10c                   move      *zeros        c2edat
T5.10c                   else
     c     *dmy          move      rr0dat        c2edat
T5.10c                   endif
      *
     c                   else
     c                   eval      c20txt = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c20bfj = *blank
     c                   eval      c20avv = *blank
     c                   eval      c20hel = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c10rap = k10rap
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c20txt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rrf0lur
      *
     c                   eval      rrf0lu_0rap = c10rap
     c     rrf0lu_key    chain     rrf0lur
      *
     c                   eval      rr0txt = c20txt
     c                   eval      rr0bfj = c20bfj
     c                   eval      rr0avv = c20avv
     c                   eval      rr0hel = c20hel
      *
     c                   time                    rr0dat
     c                   time                    rr0tim
     c                   eval      rr0usr = l_user
      *
     c                   if        %found
     c                   update    rrf0lur
     c                   else
     c                   eval      rr0fir = w_firm
     c                   eval      rr0rap = c10rap
     c                   write     rrf0lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b10txt = rr0txt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rrf0lr_0rap = d10rap
     c     rrf0lr_key    chain     rrf0lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Slett resultatrecords
      *
     c                   eval      *in60 = *off
     c                   eval      *in61 = *off
     c                   eval      *in62 = *off
      *
     c                   eval      rrf9l1_9rap = d10rap
     c     rrf9l1_key    setll     rrf9l1r                                60
     c     rrf9l1_key    reade     rrf9l1r                                60
      *
     c                   dow       *in60 = *off                                 EOF
      *
     c                   eval      rrf9lu_9rap = d10rap
     c                   eval      rrf9lu_9lin = rr9lin
     c                   eval      rrf9lu_9avd = rr9avd
     c                   eval      rrf9lu_9aar = rr9aar
     c                   eval      rrf9lu_9typ = rr9typ
     c     rrf9lu_key    chain     rrf9lur
      *
     c                   if        %found
     c                   delete    rrf9lur
     c                   endif
      *
     c     rrf9l1_key    reade     rrf9l1r                                60
      *
     c                   enddo
      *
      * Slett fra-til begrep
      *
     c                   eval      rrf8l1_8rap = d10rap
     c     rrf8l1_key    setll     rrf8l1r                                61
     c     rrf8l1_key    reade     rrf8l1r                                61
      *
     c                   dow       *in61 = *off                                 EOF
      *
     c                   eval      rrf8lu_8rap = d10rap
     c                   eval      rrf8lu_8lin = rr8lin
     c                   eval      rrf8lu_8lØp = rr8lØp
     c     rrf8lu_key    chain     rrf8lur
      *
     c                   if        %found
     c                   delete    rrf8lur
     c                   endif
      *
     c     rrf8l1_key    reade     rrf8l1r                                61
      *
     c                   enddo
      *
      * Slett rapportlinjer
      *
     c                   eval      rrf5l1_5rap = d10rap
     c     rrf5l1_key    setll     rrf5l1r                                62
     c     rrf5l1_key    reade     rrf5l1r                                62
      *
     c                   dow       *in62 = *off                                 EOF
      *
     c                   eval      rrf5lu_5rap = d10rap
     c                   eval      rrf5lu_5lin = rr5lin
     c     rrf5lu_key    chain     rrf5lur
      *
     c                   if        %found
     c                   delete    rrf5lur
     c                   endif
      *
     c     rrf5l1_key    reade     rrf5l1r                                62
      *
     c                   enddo
      *
      * Sletter rad
      *
     c                   eval      rrf0lu_0rap = d10rap
     c     rrf0lu_key    chain     rrf0lur
      *
     c                   delete    rrf0lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_0rap = k10rap
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rrf0lr_0rap = k10rap
     c     rrf0lr_key    chain     rrf0lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k10rap = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rrf0lr_0rap = k10rap
     c     rrf0lr_key    chain     rrf0lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kopier raportlinjer
      *
     c                   eval      rrf5l1_5rap = s_0rap
     c     rrf5l1_key    setll     rrf5l1r
     c     rrf5l1_key    reade     rrf5l1r                                60
      *
     c                   dow       *in60 = *off                                 EOF
      *
     c                   eval      rrf5lu_5rap = s_0rap
     c                   eval      rrf5lu_5lin = rr5lin
     c     rrf5lu_key    chain     rrf5lur
      *
     c                   if        %found
     c                   eval      rr5rap = k10rap
     c                   write     rrf5lur
     c                   endif
      *
     c     rrf5l1_key    reade     rrf5l1r                                60
      *
     c                   enddo
      *
      * Kopier fra-til begrep
      *
     c                   eval      rrf8l1_8rap = s_0rap
     c     rrf8l1_key    setll     rrf8l1r
     c     rrf8l1_key    reade     rrf8l1r                                60
      *
     c                   dow       *in60 = *off                                 EOF
      *
     c                   eval      rrf8lu_8rap = s_0rap
     c                   eval      rrf8lu_8lin = rr8lin
     c                   eval      rrf8lu_8lØp = rr8lØp
     c     rrf8lu_key    chain     rrf8lur
      *
     c                   if        %found
     c                   eval      rr8rap = k10rap
     c                   write     rrf8lur
     c                   endif
      *
     c     rrf8l1_key    reade     rrf8l1r                                60
      *
     c                   enddo
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c10rap = s_0rap
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rrf0l2
     c                   other
     c                   read      rrf0l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr0fir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = rr0txt
     c                   other
     c                   eval      rrf0l1_0rap = rr0rap
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b10rap = rr0rap
     c                   eval      b10txt = rr0txt
     c                   eval      b10bfj = rr0bfj
     c                   eval      b10avv = rr0avv
     c                   eval      b10hel = rr0hel
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'B' and
     c                             %eof(rrf0l2)
     c     rrf0l2_key    setgt     rrf0l2
     c                   readp     rrf0l2
     c                   when      w_seqe = *blank and
     c                             %eof(rrf0l1)
     c     rrf0l1_key    setgt     rrf0l1
     c                   readp     rrf0l1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rrf0l2
     c                   other
     c                   readp     rrf0l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr0fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = rr0txt
     c                   other
     c                   eval      rrf0l1_0rap = rr0rap
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'B' and
     c                             %eof(rrf0l2)
     c     rrf0l2_key    setll     rrf0l2
     c                   when      w_seqe = *blank and
     c                             %eof(rrf0l1)
     c     rrf0l1_key    setll     rrf0l1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på rapport
      *
     c     rrf0l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0l1_0rap
      *
      * Key for posisjonering på beskrivelse
      *
     c     rrf0l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0l2_0txt
      *
      * Key for les på rapport
      *
     c     rrf0lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0lr_0rap
      *
      * Key for oppdatering av rapport
      *
     c     rrf0lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0lu_0rap
      *
      * Key for les på rapportlinje
      *
     c     rrf5l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf5l1_5rap
      *
      * Key for oppdatering av rapportlinje
      *
     c     rrf5lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf5lu_5rap
     c                   kfld                    rrf5lu_5lin
      *
      * Key for les på fra-til begrep
      *
     c     rrf8l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf8l1_8rap
      *
      * Key for oppdatering av fra-til begrep
      *
     c     rrf8lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf8lu_8rap
     c                   kfld                    rrf8lu_8lin
     c                   kfld                    rrf8lu_8lØp
      *
      * Key for les på resultatrecords
      *
     c     rrf9l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf9l1_9rap
      *
      * Key for oppdatering av resultatrecords
      *
     c     rrf9lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf9lu_9rap
     c                   kfld                    rrf9lu_9lin
     c                   kfld                    rrf9lu_9avd
     c                   kfld                    rrf9lu_9aar
     c                   kfld                    rrf9lu_9typ
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   move      *month        p_rper
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rrf0l1_0rap = 0
     c     rrf0l1_key    setll     rrf0l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
