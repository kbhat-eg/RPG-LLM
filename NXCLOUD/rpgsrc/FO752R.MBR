     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : fo752R
      * Beskrivelse . . : Sjekker om pålogget selger har tilbud
      *                   viser i såfall dette i et bilde, hvor man kan
      *                   videre til tilbudsbilde
      *                   kriterier bestemmer et datointevall på oppfølg.dato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020214 bof  Nytt program
6.3q  *       260515 bof  Datoformat sql
6.30  * 6.30  260516 bsa  Feil i sql for å finne antall tilbud
6.31  * 6.30  200317 bof  Lå ikke sperre vedr. BM - pakke
6.32  * 6.30  150618 nho  Dersom 0 i antall dager skal ikke komme noen
6.32  *                   varsel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    31-79 = Varsle m/feilmeldinger/skvdrmindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
6.31 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
      *
     ffo752d    cf   e             workstn
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d fohelr_numm     s                   like(fonumm)
     d rkunlr_kund     s                   like(rkkund)
     d fusrl1_user     s                   like(fbuser)
6.30 d amodl1_modu     s                   like(axmodu)
      *
      * Definering av variable
      *
     d b_funn          s              1    inz(*off)
      *
     d w_firm          s                   like(fofirm)
     d w_selg          s                   like(foselg)
     d w_fdat          s                   like(foddat)
     d w_tdat          s                   like(foddat)
     d w_antt          s              6  0
     d w_acli          s              2  0
     d w_acpo          s              3  0
     d w_selgx         s              4
      *
      * - - - - - - - - - - - - - - - - -
      *
      * Benyttede skvdrmbilder.
      * - - - - - - - - - - - - - - - - -
      * M1WIN  -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
6.31  *
6.31  * Sjekker om BM Modul
6.31  *
6.31 c                   eval      amodl1_modu = 'BM_MODUL'
6.31 c     amodl1_key    chain     amodl1
6.31 c                   if        not %found(amodl1)
6.31 c                   goto      avslutt
6.31 c                   endif
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
6.32  *
6.32 c                   if        %found(fusrl1) and
6.32 c                             fbko16 = 0
6.32 c                   goto      avslutt
6.32 c                   endif
6.32  *
     c                   if        %found(fusrl1) and
     c                             fbselg <> 0
     c                   eval      w_selg = fbselg
      *
     c                   exsr      finn_tilbud
     c                   if        w_antt > 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1WIN Behandle bilde på startmeny
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      m1mld1 = 'Du har ' + %char(w_antt) +
     c                                      ' tilbud å følge opp'
     c                   eval      m1mld2 = 'Tast F8 for tilbudsoppfølging'
      *
     c                   z-add     10            w_acli
     c                   z-add     12            w_acpo
      *
     c                   exfmt     m1win
      *
     c                   if        *inkh = *on
     c                   move      fbselg        w_selgx
     c                   call      'FO580R  '
     c                   parm                    w_selgx
     c                   endif
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_tilbud   begsr
      *
      *
      * fra dato sette ut fra antall dager på saksbehandler (FUSRPF)
     c                   time                    w_fdat
     c     w_fdat        subdur    fbko16:*days  w_fdat
     c                   time                    w_tdat
      * til dato sette til dagens dato
     c**                 eval      fbko16 = fbko16 * -1
     c**   w_tdat        subdur    fbko16:*days  w_tdat
      *
     c/exec sql
     c+                  declare tilb cursor for
     c+                  select count(*) as w_antt
     c+                  from   fohepf
     c+                  where  fofirm = :w_firm and
     c+                         foselg = :w_selg and
     c+                               footyp in (select vaotyp from votypf
     c+                                      where vaoakk = 0)   and
     c+                         foddat >= :w_fdat and
     c+                         foddat <= :w_tdat and
6.30 c+                        (foityp = '  ' or
     c+                        (foityp <> '  ' and
     c+                         foityp in (select vaityp from vinfpf
6.30 c+                         where vaisti = '1')))
     c+                  for read only
     c/end-exec
      *
     c/exec sql
     c+ open tilb
     c/end-exec
      *
     c/exec sql
     c+ fetch tilb
     c+ into  :w_antt
     c/end-exec
      *
     c/exec sql
     c+ close tilb
     c/end-exec
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      *  Key for file : BRUKER-REGISER OFAK
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
6.31  *
6.31  * Key for les av moduler Nexstep
6.31  *
6.31 c     amodl1_key    klist
6.31 c                   kfld                    amodl1_modu
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.31  *
      *
     c                   endsr
