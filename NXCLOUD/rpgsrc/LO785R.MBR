     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LO785R
      * Beskrivelse . . : Bestilling-hode, henter ant. bestillinger til oppf
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150206 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel4    if   e           k disk    rename(lohepfr:lohel4r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
      *
      * Definering av parametre
      *
     d p_anta          s              6  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d lohel4_innk     s                   like(loinnk)
     d fusrl1_user     s                   like(fbuser)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
      *
     d w_dato          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        l_user = *blank
     c                   goto      avslutt
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_anta = 0
      *
      * Finner selgernr for bruker
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
     c                   if        not %found or
     c                             fbselg = 0
     c                   goto      avslutt
     c                   endif
      *
      * Initierer oppfølgingsdato
      *
     c                   eval      w_dato = %date
     c                   adddur    -2:*d         w_dato
      *
      * Leser bestilling-hode for innkjøper, og finner antall bestillinger
      * hvor bekreftet leveringsdato ikke er utfylt, eller hvr
      * - tilbudet ikke er utgått på dato
      *
     c                   eval      lohel4_innk = fbselg
     c     lohel4_key    setll     lohel4
     c     lohel4_key    reade     lohel4
     c                   dow       not %eof
      *
     c                   if        lomoda = *loval and
     c                             lobdat <= w_dato
     c                   eval      p_anta = p_anta + 1
     c                   endif
      *
     c     lohel4_key    reade     lohel4
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_anta
      *
      * Key for les på bestilling-hode
      *
     c     lohel4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel4_innk
      *
      * Key for oppslag på ofak bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
