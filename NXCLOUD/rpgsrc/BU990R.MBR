     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU990R
      * Beskrivelse . . : Butikk, avslutning av salg
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071004 HKO  Nytt program
      *       161104 HKO  Lagt inn dritt-logikk for å hådtere gammeldags
      *                   håndtering av fortegn. I den gamle løsningen
      *                   er det ordretypen som styrer fortegnet med unntak
      *                   for fakturaen !!!....
      *       161104 HKO  Lagt inn dritt-logikk som legger salgspris etter
      *                   rabatt inn som salgspris samtidig som rabatt
      *                   blankes. Dette for å tilfredsstille gammelt
      *                   kasseoppgjør. Gjelder bong-hode og bong-linje.
      *       230205 HKO  Legger ut totalt beløp fra motatt beløp på hode
      *       190405 HKO  Legger ut tekstlinjer fra ButikkHodeTekst
      *                   Lagt inn håndtering av betaling
      *       140905 MHA  Skriver nå til historikk register for de
      *                   nye kassene. BCHEST og BCDTST.
      *       111005 HKO  Rettet feil ved henting av overstyrt
      *                   rabattkategori fra kundeprosjekt
      *       030106 HKO  Lagt inn test på at det hentede ordrenummeret ikke
      *                   allerede finnes ved henting av neste ledige nummer
      *       070707 ASK  Oppdaterer også bonglinjer med ordrenummer
      *       290807 HFL  Rettet beregning av ordrerabatt.
5.60  * R5.60 061107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
5.61  * R5.61 210208 ASK  Lagt inn oppdatering av ordre med depositum
      *                   innbetaling fra kassen
5.62  * R5.62 110808 MHA  Lagt på feilhåndtering ved fil-errors. Ved fil-feil
      *                   kalles FileErr subprosedyren som igjen setter p_errt.
5.63  * R5.63 091008 MHA  Lagt til mulighet for å styre commitment control med
      *                   ny parameter COMFLAG. COMFLAG er char(1), og hvis satt
      *                   til '1' så er commitment control på.
5.64  * R5.64 150709 MHA  Mer kontroll over commit / rollback. Styres herfra, ik
      *                   fra CL.
6.10  * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
6.12  * 6.10  270510 ASK  Lagt inn håndtering av kortreferanse på transaksjoner
6.13  * 6.10  210111 bsa  Legger ut i kortregister uansett om det er benyttet
6.13  *                   eller ei. (BM Kort)
6.14  * 6.10  070211 bsa  Legger ut i kortregister uansett om det er benyttet
6.14  *                   eller ei. (Maxbo Kort)
6.15  * 6.10  180311 bsa  Ikke avstemt lengde på kortnummer
6.16  * 6.10  100611 bsa  Sjekk om bongen allerde finnes, og at dette er kontant
6.16  *                   Legger ikke ukritisk ut poster.
6.17  * 6.10  190711 bsa  Sjekker ikke lenger mot fullmakt på kreditkort.
6.18  * 6.10  300312 bsa  Innført GE Kort, samme håndtering som BM og Maxbo kort
6.20  * 6.20  021210 NHO  Prosjekt er nå alfa
6.21  * 6.21  180412 AMD  Dersom kjørekontorløsning er aktivert, skal
6.21  *                   leveringsmåte settes lik 99= Lev. fra kasse
6.22  * 6.22  190412 amd  Lagt inn vareadresse lik fakturaadresse
6.22  *                   dersom det er satt opp krav om dette i AVALPF
6.23  * R6.20 140112 bof  Mulighet til å legge ut ByggDok innsamling
6.24  * R6.20 040113 bof  Henter byggdok - kode fra FKPRPF
6.25  * R6.20 060613 amd  Må ha med informasjon (parameter) om offline-
6.25  *                   status ved oppkall av program BO700R
6.26  * R.10  190913 bof  Sjekke fakturakunde vedr. fakt.type
6.27  * R6.20 311013 ask  Oppdaterer med brukt depositum beløp
6.nu  * 6.30  050614 bof  endring i forb. nummerutvidelse
6.31  * R6.30 030914 ask  Lagt ut nye felter i BOHEPF/BODTPF for ny
6.31  *                   oppdatering av regnskap og statistikk
6.32  * 6.30  150914 BKV  Henter overstyrt info fra kundeprosjekt
6.33  * 6.30  221014 BKV  Søtte for cobuilder
6.34  * 6.30  230615 bsa  Henter inn leveringsmåte tekst hvis kode er
6.34  *                   oppgitt og tekst ikke er lagt inn fra kassa.
6.35  * R6.20 030815 bsa  Sjekker om det er krav til utfylt leveringsmåte
6.35  *                   via AVALPF.
6.36  * R6.20 270116 bof  Setter fakt.geb parm. i ordre ihht. standard
6.37  * R6.20 280116 bof  Summerer bong for å legge ut i BORESK
6.38  * 6.30  050716 bof  Byggdok/Cob. skal evt. ta mailadr. fra kunde
6.39  * 6.30  260916 bof  Ordrerabatt ble trukker fra 2 ganger
6.3a  * 6.30  070217 bof  Beh.kode 3 og 4 skal også gi byggdok.
6.3b  * 6.30  071117 mha  Lar FileErr gå i MSGW.
6.3c  * 6.30  260218 bof  Hvis rklety <> 0, så legg denne i bolety
6.3d  * 6.30  130418 bof  Best.nr ikke utvidet på BODTPF,test om > 6 pos
6.3e  * 6.30  161018 bof  Utvidet med trelast sortiment
6.3f  * 6.30  140519 bsa  Justert rettelse 6.3c
6.3g  *K00    190519 bof  Feilretting vedr. skriv til depositum
7.01  * 6.30  051217 bsa  Håndterer KID ved innlesing fra butikk.
7.02  * R6.20 060716 ask  Lagt inn kall til oppdatering av XL Kundeklubb
7.03  * 7.00  240220 bof  Cobuilder, men ikke krav til nobbnummer
7.04  *K00    180320 bsa  Håndterer KID ved innlesing fra butikk - payex
7.05  *K00    240320 bsa  Switchstyring av XL Klubb oppslag
7.06  * K00   060520 bsa  Håndterer KID ved innlesing fra butikk - Vipps
7.07  * K00   150421 bsa  Hvis fakturakunde finnes, hentes betalings-
7.07  * K00               betingelsen herfra.
      *
7.fb  *K000   170419 bof  utvidelser vedr. depositum
      *
8.01  *K000   061223 bsa  Forsøker berike info på logglinje til
8.01  *K000               historisk lagerbok.
8.02  *K000   150824 bsa  Skal ikke sette endringsflagg på bonghode/linjer
8.02  *K000               her. Skal følge timestamp som den har fått i kassa.
8.03  *K000   091024 bsa  Bruker leveringsdato som grunnlag for fakturaberegning.
8.04  *K000   301024 bsa  Reverserer 8.02
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbbhei2    if   e           k disk    rename(bbhest:bbhei2r) infsr(FileErr)
     fbbdti1    if   e           k disk    rename(bbdtst:bbdti1r) infsr(FileErr)
     fbbhti1    if   e           k disk    rename(bbhtst:bbhti1r) infsr(FileErr)
     fbbbli1    if   e           k disk    rename(bbblst:bbbli1r) infsr(FileErr)
     fbbheiu    uf   e           k disk    rename(bbhest:bbheiur)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbbdtiu    uf   e           k disk    rename(bbdtst:bbdtiur)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbbhtiu    uf   e           k disk    rename(bbhtst:bbhtiur)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbbbliu    uf   e           k disk    rename(bbblst:bbbliur)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbcdtst    uf a e           k disk    rename(bcdtst:bcdtstr)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbchest    uf a e           k disk    rename(bchest:bchestr)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbchtst    uf a e           k disk    rename(bchtst:bchtstr)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
      *
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.07 frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
7.07 f                                     prefix(rx:2)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.21 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.22 favall1    if   e           k disk    rename(avalpfr:avall1r)
6.23 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.23 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
6.23 frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
6.34 fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
7.01 fbreflu    uf a e           k disk    rename(brefpfr:breflur)
6.23 ffbdopf    o  a e           k disk
6.23 f                                      commit(COMFLAG)
6.23 f                                      infsr(FileErr)
     fsdeplu    uf a e           k disk    rename(sdeppfr:sdeplur)
     f                                      commit(COMFLAG)
     fbodtl1    if   e           k disk    rename(bodtpfr:bodtl1r)
     f                                      infsr(FileErr)
     fbohelu    uf a e           k disk    rename(bohepfr:bohelur)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbodtlu    uf a e           k disk    rename(bodtpfr:bodtlur)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
     fbobliu    uf a e           k disk    rename(boblst:bobliur)
     f                                      commit(COMFLAG)
     f                                      infsr(FileErr)
6.12 fbbkki1    if   e           k disk    rename(bbkkst:bbkki1r)
6.12 f                                      commit(COMFLAG)
6.12 f                                      infsr(FileErr)
6.12 fbbkkiu    uf   e           k disk    rename(bbkkst:bbkkiur)
6.12 f                                      commit(COMFLAG)
6.12 f                                      infsr(FileErr)
6.12 frukrl4    if   e           k disk    rename(rukrpfr:rukrl4r)
6.12 f                                      infsr(FileErr)
6.13 frukrl5    if   e           k disk    rename(rukrpfr:rukrl5r)
6.13 f                                      infsr(FileErr)
6.12 fbokklu    uf a e           k disk    rename(bokkpfr:bokklur)
6.12 f                                      commit(COMFLAG)
6.12 f                                      infsr(FileErr)
6.11  *
6.11  * Definering av Local data area
6.11  *
6.11 d                uds
6.11 d l_user                911    920
6.23 d l_filg                931    933
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_kass          s              3
     d p_stat          s              8
5.62 d p_errt          s            120
      *
      * Definering av variabler i nøkler
      *
     d aforl1_ruti     s                   like(afruti)
     d apospf_ponr     s                   like(apponr)
     d bbhei2_kass     s                   like(bhkass)
     d bbdti1_bong     s                   like(bibong)
     d bbbli1_bong     s                   like(bebong)
     d bbhti1_hbon     s                   like(bhtbon)
     d bodtl1_aarr     s                   like(bdaarr)
     d bodtl1_wsid     s                   like(bdwsid)
     d bodtl1_bong     s                   like(bdbong)
     d bohelu_aarr     s                   like(boaarr)
     d bohelu_wsid     s                   like(bowsid)
     d bohelu_bong     s                   like(bobong)
     d bodtlu_aarr     s                   like(bdaarr)
     d bodtlu_wsid     s                   like(bdwsid)
     d bodtlu_bong     s                   like(bdbong)
     d bobliu_aarr     s                   like(bjaarr)
     d bobliu_kass     s                   like(bjkass)
     d bobliu_bong     s                   like(bjbong)
     d bobliu_linj     s                   like(bjlinj)
     d bbheiu_bong     s                   like(bhbong)
     d bbdtiu_bong     s                   like(bibong)
     d bbbliu_bong     s                   like(bebong)
     d bbhtiu_hbon     s                   like(bhtbon)
     d votyl1_otyp     s                   like(vaotyp)
     d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(jvvare)
     d fohel1_numm     s                   like(fonumm)
     d sdeplu_numm     s                   like(sknumm)
     d sdeplu_kund     s                   like(skkund)
     d rkunl1_kund     s                   like(rkkund)
7.07 d rkunlr_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkund)
6.12 d rukrl4_kknr     s                   like(rukknr)
6.12 d bbkki1_tbon     s                   like(bktbon)
6.12 d bbkkiu_tbon     s                   like(bktbon)
6.12 d bokklu_bong     s                   like(brbong)
6.12 d bokklu_kort     s                   like(brkort)
6.13 d rukrl5_ktyp     s                   like(ruktyp)
6.13 d rukrl5_kkun     s                   like(rukkun)
6.13 d rukrl5_kpro     s                   like(rukpro)
6.22 d avall1_apgm     s                   like(avapgm)
6.23 d amodl1_modu     s                   like(axmodu)
6.23 d afpslr_ufil     s                   like(l_filg)
6.23 d afpslr_uide     s                   like(afuide)
6.23 d rukpl6_kund     s                   like(rukund)
6.34 d ra17l1_qkod     s                   like(raqkod)
7.01 d breflu_aarr     s                   like(bxaarr)
7.01 d breflu_bong     s                   like(bxbong)
      *
5.62  * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
      * Datastruktur for oppdatering av lager
      *
     d                 ds
6.nu d hlrec                        128
6.11 d  hlvare                       15    overlay(hlrec)
6.11 d  hllage                        2  0 overlay(hlrec:16)
6.11 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
6.11 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
6.11 d  hlotyp                        2    overlay(hlrec:36)
6.11 d  hlltyp                        2    overlay(hlrec:38)
6.11 d  hlanta                       11  3 overlay(hlrec:40)
6.11 d  hlkopr                        9  2 overlay(hlrec:51)
6.11 d  hlrefr                       20    overlay(hlrec:60)
6.11 d  hlsyst                        1    overlay(hlrec:80)
6.11 d  hlaltk                        2    overlay(hlrec:81)
6.11 d  hlrest                        1    overlay(hlrec:83)
6.11 d  hltype                        1    overlay(hlrec:84)
6.11 d  hlenhe                        3    overlay(hlrec:85)
6.11 d  hlinlr                        1    overlay(hlrec:88)
6.11 d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Parameter for utskrift av faktura
      *
     d                 ds
6.nu d d_parm                        34
     d  d_aarr                        4  0 overlay(d_parm:1)
     d  d_otyp                        2    overlay(d_parm:5)
6.nu d  d_ffnr                        8  0 overlay(d_parm:7)
6.nu d  d_tfnr                        8  0 overlay(d_parm:15)
6.nu d  d_prog                       10    overlay(d_parm:23)
      *
      * Definering av variable
      *
     d b_numm          s              1    inz(*off)
6.23 d b_bdo1          s              1    inz(*off)
6.23 d b_bdo2          s              1    inz(*off)
6.23 d b_bdo3          s              1    inz(*off)
6.23 d b_bdo4          s              1    inz(*off)
6.23 d b_kper          s              1    inz(*off)
      *
     d w_fira          s              3
     d w_tnim          s             11  2
     d w_boli          s              4  0
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_kode          s              1
6.nu d w_numm          s                   like(boornr)
     d w_retu          s              1
     d w_btx1          s             20
     d w_btx2          s             20
     d w_chgv          s              1
     d w_tvbt          s              1
     d w_in03          s              1
     d w_btch          s              1
     d w_utsk          s              1  0
     d w_kopi          s              1
     d w_fkda          s               d   datfmt(*iso)
     d w_ffda          s               d   datfmt(*iso)
     d w_dato_8        s              8
      *
     d w_firm          s                   like(bhfirm)
     d w_kass          s                   like(bhkass)
     d w_kund          s                   like(bhkund)
     d w_rkat          s                   like(borkat)
     d w_otyp          s                   like(vaotyp)
     d w_tots          s                   like(botots)
     d w_totr          s                   like(bototr)
     d w_totk          s                   like(bototk)
     d w_proj          s                   like(boproj)
     d w_faty          s                   like(bofaty)
     d w_kjor          s                   like(bokjor)
     d w_kjnr          s                   like(bokjnr)
     d w_sted          s                   like(bosted)
     d w_sald          s                   like(botots)
5.60 d w_sal2          s                   like(botots)
5.60 d w_tota          s                   like(botots)
5.60 d w_totn          s                   like(botots)
5.60 d w_tot1          s                   like(botots)
5.60 d w_tot2          s                   like(botots)
5.61 d w_ornr          s                   like(biornr)
5.61 d w_orsu          s                   like(biorsu)
6.12 d w_kknr          s             25
6.12 d w_klen          s              2  0
6.16 d w_kort          s                   like(brkort)
6.16 d w_strk          s                   like(brstrk)
6.16 d w_ktyp          s                   like(brktyp)
6.16 d w_cust          s                   like(brkund)
6.21 d w_retk          s              1
6.21 d w_qtxt          s             20
6.23 d w_btms          s               Z
6.23 d w_ti26          s             26
6.23 d w_bnum          s              6
6.32 d w_betb          s                   like(rkbetb)
6.32 d w_mkod          s                   like(rkmkod)
6.32 d w_neto          s                   like(rkneto)
6.36 d w_kgfa          s                   like(bokgfa)
6.3c d w_lety          s                   like(bolety)
6.3e d w_lv_nobb       s                   like(vvnobb)
6.3e d w_lv_modn       s                   like(vvnmod)
6.3e d w_lv_lldo       s                   like(vvldor)
6.3e d w_lv_llva       s                   like(vvlvar)
6.3e d w_lv_vtyp       s                   like(vvvtyp)
6.3e d w_lv_udat       s                   like(vvedat)
6.3e d w_lv_ldor       s                   like(vvldor)
6.3e d b_inlr          s              1    inz(*off)

6.22  *
6.22  * Firmastyrt validering av felt
6.22  *
6.22 d u_vref          s              1    inz(*off)
6.22 d u_dref          s              1    inz(*off)
6.22 d u_ldat          s              1    inz(*off)
6.22 d u_navn          s              1    inz(*off)
6.35 d u_lmat          s              1    inz(*off)
6.22  *
6.22  * Definering av save-variable
6.22  *
6.22 d s_navn          s                   like(rknavn)
6.22 d s_gate          s                   like(rkgate)
6.22 d s_adr2          s                   like(rkadr2)
6.22 d s_sted          s                   like(rksted)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d c_digi          c                   '00000000'
      *
7.03 d u_703           s              1    inz(*off)
7.05 d u_705           s              1    inz(*off)
      *
      * Eksternt program
7.03   dcl-s co402_verdi1  char(1);
7.03   dcl-s co402_verdi2  char(2048);
7.03   DCL-PR CO402R EXTPGM('CO402R');
7.03     p_filgrp            char(3)     const;
7.03     p_firm              packed(3:0) const;
7.03     p_lager             packed(2:0) const;
7.03     p_nokkel            char(50)    const;
7.03     p_verdi1            char(1);
7.03     p_verdi2            char(2048);
7.03   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom butikk kode-register ikke ble funnet
      *
     c     bstsl1_key    chain     bstsl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
6.21  *
6.21  * Avbryter dersom ofak status-register ikke ble funnet
6.21  *
6.21 c     fstsl1_key    chain     fstsl1
6.21 c                   if        not %found
6.21 c                   goto      avslutt
6.21 c                   endif
      *
      * Leser alle salg for kasse
      *
     c     bbhei2_key    setll     bbhei2
     c     bbhei2_key    reade     bbhei2
     c                   dow       not %eof
     c                   exsr      behandling
5.64  * Kjører commit på bong hvis den er ok. Den må være ok hvis den kommer
5.64  * gjennom behandling.
     c                   if        COMFLAG = '1'
5.64 c                   commit
     c                   endif
7.02  *
7.02  * Sjekker om kundens ref er fylt ut - kaller XL Kundeklubb
7.02  *
7.05 c                   if        u_705 = *on
7.02 c                   if        bhdref <> *blank
7.02 c                   move      w_firm        w_fira
7.02 c                   call      'BU995C'
7.02 c                   parm                    w_fira
7.02 c                   parm                    bhbong
7.02 c                   endif
7.05 c                   endif
7.02  *
     c     bbhei2_key    reade     bbhei2
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av salg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Avbryter dersom det ikke finnes butikk-linjer for salget
      *
     c                   eval      bbdti1_bong = bhbong
     c     bbdti1_key    chain     bbdti1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * Summerer butikk-linjer
      *
     c                   eval      w_tnim = 0
     c                   eval      w_tots = 0
     c                   eval      w_totr = 0
     c                   eval      w_totk = 0
      *
     c     bbdti1_key    setll     bbdti1
     c     bbdti1_key    reade     bbdti1
     c                   dow       not %eof
      *
      * Total salg skal ikke akkumuleres ved betaling med gavekort
      * eller ved deposituminnbetaling
      *
5.61 c                   if        bivare = *blank and
     c                             bianta < 0
     c                   eval      w_tots = w_tots
     c                   else
     c                   eval      w_tots = w_tots + bisums
     c                   endif
      *
     c                   eval      w_tnim = w_tnim + bisnim
6.39 c                   eval      w_totr = w_totr + bisumr
6.39 c***                                   (bisumn * biorab / 100)
     c                   eval      w_totk = w_totk + bisumk
     c     bbdti1_key    reade     bbdti1
     c                   enddo
      *
      * Snur fortegn dersom sum å betale inkl. mva er negativ
      *
     c                   if        w_tnim < 0
     c                   eval      w_tots = w_tots * -1
     c***                eval      w_totb = w_totb * -1
     c                   eval      w_totr = w_totr * -1
     c                   eval      w_totk = w_totk * -1
     c                   eval      bhmotb = bhmotb * -1
     c                   endif
      *
      * Finner ordretype avhengig av behandlingstype og sum
      * Avbryter dersom ugyldig behandlingstype
      *
     c                   select
     c                   when      bhbtyp = 0
     c                   if        w_tnim >= 0
     c                   eval      w_otyp = baaokd
     c                   else
     c                   eval      w_otyp = baaokk
     c                   endif
     c                   when      bhbtyp = 2
     c                   if        w_tnim >= 0
     c                   eval      w_otyp = baaocd
     c                   else
     c                   eval      w_otyp = baaock
     c                   endif
     c                   when      bhbtyp = 3
     c                   if        w_tnim >= 0
     c                   eval      w_otyp = baaofd
     c                   else
     c                   eval      w_otyp = baaofk
     c                   endif
     c                   when      bhbtyp = 4
     c                   if        w_tnim >= 0
     c                   eval      w_otyp = baaobd
     c                   else
     c                   eval      w_otyp = baaobk
     c                   endif
     c                   when      bhbtyp = 5 and
     c                             w_tnim >= 0
     c                   eval      w_otyp = baaold
     c                   other
     c                   leavesr
     c                   endsl
      *
      * Henter inn opplysninger om ordretypen. Avbryter dersom ordretype
      * ikke ble funnet
      *
     c                   eval      votyl1_otyp = w_otyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
      * Standardkunde benyttes dersom kunde ikke er oppgitt
      * Henter kundeinformasjon og avbryter dersom kunde ikke ble funnet.
      *
6.22 c                   eval      s_navn = *blank
6.22 c                   eval      s_gate = *blank
6.22 c                   eval      s_adr2 = *blank
6.22 c                   eval      s_sted = *blank
6.36 c                   eval      w_kgfa = 0
6.3c c                   eval      w_lety = 0
6.22  *
     c                   if        bhkund <> 0
     c                   eval      w_kund = bhkund
     c                   else
     c                   eval      w_kund = baakun
     c                   endif
      *
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   leavesr
     c                   endif
6.22  *
6.22 c                   eval      s_navn = rknavn
6.22 c                   eval      s_gate = rkgate
6.22 c                   eval      s_adr2 = rkadr2
6.22 c                   eval      s_sted = rksted
      *
     c                   eval      w_rkat = rkrabk
     c                   eval      w_faty = rkfaty
6.32 c                   eval      w_betb = rkbetb
6.32 c                   eval      w_mkod = rkmkod
6.32 c                   eval      w_neto = rkneto
     c                   eval      w_kjor = rkkjØr
     c                   eval      w_kjnr = rkkjnr
6.36 c                   eval      w_kgfa = rkfagb
6.3c c                   eval      w_lety = rklety
      *
      * Henter informasjon fra kundeprosjekt
      * Avbryter dersom kundeprosjekt ikke ble funnet
      *
6.20 c**                 eval      w_proj = 0
6.20 c                   eval      w_proj = *blank
     c                   eval      w_sted = *blank
      *
     c                   if        bhkpro <> 0
      *
     c                   eval      fkprl1_kund = w_kund
     c                   eval      fkprl1_kpro = bhkpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   eval      w_proj = flproj
     c                   eval      w_faty = flfaty
6.32 c                   eval      w_betb = flbetb
6.32 c                   eval      w_mkod = flmkod
6.32 c                   eval      w_neto = flneto
     c                   eval      w_kjor = flkjor
     c                   eval      w_kjnr = flkjnr
6.36 c                   eval      w_kgfa = flfagb
     c                   if        flponr <> 0
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf
     c                   if        %found
     c                   eval      w_sted = %char(flponr) + ' ' + apsted
     c                   endif
     c                   endif
     c                   if        florka <> 0
     c                   eval      w_rkat = florka
     c                   endif
      *
     c                   endif
7.07  *
7.07  * Hvis fakturakunde hentes betalingsbetringelsen herfra.
7.07  *
7.07 c                   if        rkfaku <> 0
7.07 c                   eval      rkunlr_kund = rkfaku
7.07 c     rkunlr_key    chain     rkunlr
7.07 c                   if        %found
7.07 c                   eval      w_betb = rxbetb
7.07 c                   endif
7.07 c                   endif
      *
      * Skriver til bong-hode
      *
     c                   exsr      bong_hode
      *
      * Skriver til bong-linje
      *
     c                   exsr      bong_linje
      *
      * Dersom utskrift kalles program for å sette verdier
      *
     c                   if        bhbtyp = 3 or
     c                             bhbtyp = 5
      *
     c                   eval      aforl1_ruti = vaorub
     c     aforl1_key    chain     aforl1
      *
     c                   eval      w_chgv = *blank
     c                   eval      w_tvbt = *blank
     c                   eval      w_in03 = *blank
     c                   eval      w_btch = *blank
      *
     c                   call      'AP600R'
     c                   parm                    vaorub
     c                   parm                    w_chgv
     c                   parm                    w_tvbt
     c                   parm                    w_in03
     c                   parm                    w_btch
      *
     c                   endif
      *
      * Behandling avhengig av behandlingstype
      *
     c                   select
     c                   when      bhbtyp = 2
     c                   exsr      pakkseddel
     c                   when      bhbtyp = 3
     c                   exsr      faktura
     c                   when      bhbtyp = 5
     c                   exsr      hurtigordre
     c                   endsl
      *
      * Drittlogikk for å legge inn salgspris som pris etter rabatt
      *
     c                   eval      bohelu_aarr = boaarr
     c                   eval      bohelu_wsid = bowsid
     c                   eval      bohelu_bong = bobong
     c     bohelu_key    chain     bohelur
     c                   eval      botots = botots - bototr
     c                   eval      bototr = 0
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelur
      *
5.60 c                   eval      w_tota = 0
     c                   eval      bodtlu_aarr = boaarr
     c                   eval      bodtlu_wsid = bowsid
     c                   eval      bodtlu_bong = bobong
     c     bodtlu_key    setll     bodtlu
     c     bodtlu_key    reade     bodtlur
     c                   dow       not %eof
5.60 c                   eval      w_totn = bdsums
5.60  *
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        bdrab1 <> 0
5.60 c                   eval      w_tot1 = w_totn * bdrab1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        bdrab2 <> 0
5.60 c                   eval      w_tot2 = w_totn * bdrab2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        bdbrr1 <> 0
5.60 c                   eval      w_tot1 = w_totn * bdbrr1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        bdbrr2 <> 0
5.60 c                   eval      w_tot2 = w_totn * bdbrr2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60 c                   eval      w_tota = w_tota + w_tot1
5.60 c                   eval      w_tota = w_tota + w_tot2
5.60  *
6.10 c                   time                    bdedat
6.10 c                   time                    bdetim
6.10 c                   eval      bdeusr = l_user
     c                   update    bodtlur
     c     bodtlu_key    reade     bodtlur
     c                   enddo
5.60  *
5.60  * Pakkseddel eller hurtigordre skal
5.60  * oppdatere memosaldo bruksrettsrabatt . . .
5.60  *
5.60 c                   if        bhbtyp = 2 or
5.60 c                             bhbtyp = 5
5.60 c                   eval      w_sald = 0
5.60 c                   eval      w_sal2 = w_tota
5.60  *
5.60  * . . . og dette er allerede oppdatert
5.60  *       via BO700R som kaller FO730R
5.60  *       som igjen drar igang  FA922R
5.60  *
5.60 c                   endif
5.60  *
5.60  * Kontant eller faktura skal
5.60  * oppdatere akkumulerte rabatter
5.60  *
5.60 c                   if        bhbtyp = 0 or
5.60 c                             bhbtyp = 3
5.60 c                   call      'FA924R'
5.60 c                   parm                    w_firm
5.60 c                   parm                    bootyp
5.60 c                   parm                    bokund
5.60 c                   parm                    w_tota
5.60 c                   endif
      *
      * Sletter ferdig behandlet ButikkHode med tilhørende linjer i
      * ButikkLinje og tekster i ButikkHodeTekst samt Betalingstranser
      *
     c                   eval      bbheiu_bong = bhbong
     c     bbheiu_key    chain     bbheiur
     c                   delete    bbheiur
      *
     c                   eval      bbdtiu_bong = bhbong
     c     bbdtiu_key    setll     bbdtiu
     c     bbdtiu_key    reade     bbdtiur
     c                   dow       not %eof
     c                   delete    bbdtiur
     c     bbdtiu_key    reade     bbdtiur
     c                   enddo
      *
     c                   eval      bbhtiu_hbon = bhbong
     c     bbhtiu_key    setll     bbhtiu
     c     bbhtiu_key    reade     bbhtiur
     c                   dow       not %eof
     c                   delete    bbhtiur
     c     bbhtiu_key    reade     bbhtiur
     c                   enddo
      *
     c                   eval      bbbliu_bong = bhbong
     c     bbbliu_key    setll     bbbliu
     c     bbbliu_key    reade     bbbliur
     c                   dow       not %eof
     c                   delete    bbbliur
     c     bbbliu_key    reade     bbbliur
     c                   enddo
6.12  *
6.12 c                   eval      bbkkiu_tbon = bhbong
6.12 c     bbkkiu_key    setll     bbkkiu
6.12 c     bbkkiu_key    reade     bbkkiur
6.12 c                   dow       not %eof
6.12 c                   delete    bbkkiur
6.12 c     bbkkiu_key    reade     bbkkiur
6.12 c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving til bong-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bong_hode     begsr
      *
     c     *iso0         move      bhodat        w_dato_8
      *
     c                   clear                   bohelur
      *
     c                   eval      bofirm = w_firm
     c***                eval      boaarr = %int(%subst(w_dato_8:1:4))
     c                   movel     w_dato_8      boaarr
     c                   eval      bowsid = bhkass
     c                   eval      bobong = bhbong
     c                   eval      bootyp = w_otyp
6.3f c                   if        w_lety <> 0
6.3c c                   eval      bolety = w_lety
6.3f c                   else
6.3f c                   eval      bolety = bhlety
6.3f c                   endif
     c                   eval      bolage = bhlage
     c                   eval      bobinr = 0
      *--- kref: fakturaen som er betalt.....   (NB! til linje !!!!)
     c                   eval      bokref = 0
     c                   eval      boperi = 0
     c                   eval      bobdat = bhodat
     c                   eval      boldat = bhldat
     c                   eval      bofkda = *loval
     c                   eval      boffda = *loval
6.32 c                   eval      bobetb = w_betb
     c                   eval      boselg = bhselg
     c                   eval      bodist = rkdist
     c                   eval      bolmat = rkform
6.34 c                   eval      bolmtx = bhlmtx
6.21  *
6.21  * Dersom kjørekontorløsning er aktivert
6.21  * og det ikke er leveringsmåte på kunde
6.21  * settes leveringsmåte = 99
6.21  *
6.21 c                   if        rkform = 0 and
6.21 c                             faakjk = 1
6.21 c                   eval      bolmat = 99
6.21 c                   exsr      sbform
6.21 c                   endif
6.35  * Dersom krav til utfylt leveringsmåte
6.35  * og det ikke er leveringsmåte på kunde
6.35  * settes leveringsmåte = 99
6.35  *
6.35 c                   if        bolmat = 0 and
6.35 c                             u_lmat = *on
6.35 c                   eval      bolmat = 99
6.35 c                   exsr      sbform
6.35 c                   endif
6.34 c*
6.34 c* Hvis tekst ikke er funnet, hent den hvis koden er utfylt.
6.34 c*
6.34 c                   if        bolmtx = *blanks and
6.34 c                             bolmat <> 0
6.34 c                   eval      ra17l1_qkod = bolmat
6.34 c     ra17l1_key    chain     ra17l1
6.34 c                   if        not %found
6.34 c                   eval      bolmtx = raqtxt
6.34 c                   endif
6.34 c                   endif
6.34  *
     c                   eval      bolbet = rklbet
     c                   eval      bovalk = bhvalu
     c                   eval      borkat = w_rkat
     c                   eval      bosped = rksped
     c                   eval      bokjor = w_kjor
     c                   eval      bokjnr = w_kjnr
     c                   if        rkfaty <> 0
     c                   eval      bofaty = rkfaty
     c                   else
     c                   eval      bofaty = w_faty
     c                   endif
6.32 c                   eval      bomkod = w_mkod
6.36 c                   eval      bokgfa = w_kgfa
     c                   eval      bolkun = 0
     c                   eval      bokund = w_kund
     c                   eval      bokpro = bhkpro
     c                   eval      bokuna = rkalfa
     c                   eval      bonavn = bhnavn
     c                   eval      boadr1 = bhadr1
     c                   eval      boadr2 = bhadr2
     c                   eval      bosted = %trim(bhponr) +  ' ' +
     c                                      %trim(bhsted)
     c                   eval      boavde = bhavde
     c                   eval      bokont = 0
     c                   eval      bospes = 0
     c                   eval      boproj = w_proj
     c                   eval      boakti = *blank
     c                   eval      bovref = bhvref
     c                   eval      bodref = bhdref
     c                   eval      borekv = bhrekv
     c                   eval      bolbtx = *blank
     c                   eval      borabp = bhorra
6.31 c                   eval      borabb = bhorbe
6.31 c                   eval      boavrb = bhavrb
6.37 c                   eval      boresk = w_tnim
     c                   eval      bopord = 0
     c                   eval      bopsuf = 0
     c***  ?? Arne/ordre eval      bototx = 0
     c                   eval      botots = w_tots
     c                   eval      bototr = w_totr
     c                   eval      bototk = w_totk
     c                   eval      bototb = bhmotb
6.32 c                   eval      boneto = w_neto
     c                   eval      boeksu = bheksu
     c                   eval      bobkrt = bhbkrt
     c                   eval      bobknt = bhbknt
     c                   eval      bobgkv = bhbgkv
     c                   eval      bobdpo = bhbdpo
     c                   eval      botaut = bhtaut
     c                   eval      bottim = bhttim
     c                   eval      bokavs = 0
     c                   eval      bokdet = 0
     c                   eval      bodate = bhodat
     c                   eval      botime = bhotim
     c                   eval      bouser = %xlate(lo:up:bheusr)
      *
     c                   if        bhfkun <> 0
6.26  * tar en les av kunde med fakt.kunde for å hente fakt.type
6.26 c                   eval      rkunl1_kund = bhfkun
6.26 c     rkunl1_key    chain     rkunl1
6.26 c                   if        %found
6.26 c                   if        bofaty = 0
6.26 c                   eval      bofaty = rkfaty
6.26 c                   endif
6.26 c                   endif
      *
     c                   eval      bolkun = bokund
     c                   eval      bokund = bhfkun
     c                   if        bonavn = *blank
      *
     c                   eval      rkunl1_kund = bolkun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      bonavn = rknavn
     c                   eval      boadr1 = rkgate
     c                   eval      boadr2 = rkadr2
     c                   eval      bosted = rksted
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        bokpro <> 0
     c                   if        bonavn = *blank
     c                   eval      bonavn = flnavn
     c                   endif
     c                   if        boadr1 = *blank and
     c                             boadr2 = *blank and
     c                             bosted = *blank
     c                   eval      boadr1 = fladr1
     c                   eval      boadr2 = fladr2
     c                   eval      bosted = w_sted
     c                   endif
     c***                else
     c***                if        bonavn = *blank
     c***                eval      bonavn = rknavn
     c***                endif
     c***                if        boadr1 = *blank and
     c***                          boadr2 = *blank and
     c***                          bosted = *blank
     c***                eval      boadr1 = rkgate
     c***                eval      boadr2 = rkadr2
     c***                eval      bosted = rksted
     c***                endif
     c                   endif
6.22  *
6.22  * Dersom leveringsadresse fortsatt er blank,
6.22  * og det er satt krav til leveringsadresse,
6.22  * legges fakturaadresse til leveringsadresse,
6.22  *
6.22 c                   if        u_navn = *on
6.22 c                   if        bonavn = *blank
6.22 c                   eval      bonavn = s_navn
6.22 c                   eval      boadr1 = s_gate
6.22 c                   eval      boadr2 = s_adr2
6.22 c                   eval      bosted = s_sted
6.22 c                   endif
6.22 c                   endif
6.22  *
      *
     c                   if        baahoy = 1
     c                   eval      bonavn = %xlate(lo:up:bonavn)
     c                   eval      boadr1 = %xlate(lo:up:boadr1)
     c                   eval      boadr2 = %xlate(lo:up:boadr2)
     c                   eval      bosted = %xlate(lo:up:bosted)
     c                   eval      bovref = %xlate(lo:up:bovref)
     c                   eval      bodref = %xlate(lo:up:bodref)
     c                   eval      borekv = %xlate(lo:up:borekv)
     c                   eval      bolmtx = %xlate(lo:up:bolmtx)
     c                   endif
      *
      * Flytter til betalingstrans register
      *
     c                   eval      bbbli1_bong = bhbong
     c     bbbli1_key    setll     bbbli1
     c     bbbli1_key    reade     bbbli1r
     c                   dow       not %eof
      *
     c                   clear                   bobliur
      *
     c                   eval      bjfirm = befirm
     c                   eval      bjkass = bekass
     c                   eval      bjaarr = boaarr
6.12 c**                 eval      bjbong = bebong
6.12 c                   eval      bjbong = bhbong
     c                   eval      bjlinj = belinj
     c                   eval      bjtype = betype
     c                   eval      bjrefn = berefn
     c                   eval      bjtims = betims
     c                   eval      bjsumb = besumb
     c                   eval      bjodat = beodat
     c                   eval      bjedat = beedat
     c                   eval      bjetim = beetim
     c                   eval      bjeusr = beeusr
      *
      * Skriver til betalingstranser
      *
     c                   write     bobliur
      *
     c     bbbli1_key    reade     bbbli1r
     c                   enddo
6.16  *
6.16  * Hvis ikke kontantsalg (0) eller direkte faktura (3), skal det ikke legge
6.16  * post til BOKK
6.16  *
6.16 c                   if        bhbtyp = 0 or
6.16 c                             bhbtyp = 3
6.16  *
6.12  * Flytter til korttrans register
6.12  *
6.12 c                   eval      bbkki1_tbon = bhbong
6.12 c     bbkki1_key    setll     bbkki1
6.12 c     bbkki1_key    reade     bbkki1r
6.12 c                   dow       not %eof
6.12  *
6.12  * Henter kortinformasjon
6.12  *
6.15 c                   eval      rukrl4_kknr = %dec(%trim(bktknr):25:0)
6.12 c     rukrl4_key    chain     rukrl4
6.12  *
6.12  * Sjekker om kort er registrert på kunde, registreres ellers på kunde 0
6.12  *
6.16 c**                 clear                   bokklur
6.12  *
6.16 c                   eval      w_kort = *zero
6.16 c                   eval      w_strk = *zero
6.16 c                   eval      w_ktyp = *zero
6.16 c                   eval      w_cust = *zero
6.12  *
6.12 c                   if        %found
6.12 c*6.16              eval      brkort = rukknr
6.16 c                   eval      w_kort = rukknr
6.12 c*6.16              eval      brstrk = rukknr
6.16 c                   eval      w_strk = rukknr
6.12 c*6.16              eval      brktyp = ruktyp
6.16 c                   eval      w_ktyp = ruktyp
6.12 c*6.16              eval      brkund = rukkun
6.16 c                   eval      w_cust = rukkun
6.12 c                   else
6.12 c*6.16              eval      brkort = rukrl4_kknr
6.16 c                   eval      w_kort = rukrl4_kknr
6.12 c*6.16              eval      brstrk = rukrl4_kknr
6.16 c                   eval      w_strk = rukrl4_kknr
6.12 c*6.16              eval      brktyp = 06
6.16 c                   eval      w_ktyp = 06
6.12 c*6.16              eval      brkund = 0
6.16 c                   eval      w_cust = 0
6.12 c                   endif
6.12  *
6.16  * Legges ikke ut hvis kombinasjon bong/kortnr finnes fra før.
6.12  *
6.16 c                   eval      bokklu_bong =  bhbong
6.16 c                   eval      bokklu_kort =  w_kort
6.16 c     bokklu_key    chain     bokklu
6.16  *
6.16 c                   if        not %found
6.16 c                   clear                   bokklur
6.16  *
6.12 c                   eval      brfirm = w_firm
6.12 c                   eval      brbong = bhbong
6.16 c                   eval      brkort = w_kort
6.16 c                   eval      brstrk = w_strk
6.16 c                   eval      brktyp = w_ktyp
6.16 c                   eval      brkund = w_cust
6.12 c                   eval      brofda = *loval
6.12 c                   eval      brofti = *loval
6.12 c                   time                    brodat
6.12 c                   time                    brotim
6.12 c                   time                    bredat
6.12 c                   time                    bretim
6.12 c                   eval      breusr = bheusr
6.12 c                   write     bokklur
6.16 c                   endif
6.12  *
6.12 c     bbkki1_key    reade     bbkki1r
6.12 c                   enddo
6.13  *
6.13  * Sjekk mot kortregister, og søk etter BM kort på kunden
6.13  *
6.13 c                   eval      rukrl5_ktyp = 5
6.13 c                   eval      rukrl5_kkun = bokund
6.13 c                   eval      rukrl5_kpro = 0
6.13 c     rukrl5_key    chain     rukrl5
6.13 c                   if        %found(rukrl5)
6.13 c** 6.17            if        rukobl = 1
6.16  *
6.16  * Legges ikke ut hvis kombinasjon bong/kortnr finnes fra før.
6.16  *
6.16 c                   eval      bokklu_bong =  bhbong
6.16 c                   eval      bokklu_kort =  rukknr
6.16 c     bokklu_key    chain     bokklu
6.16  *
6.16 c                   if        not %found
6.13 c                   clear                   bokklur
6.13  *
6.13 c                   eval      brkort = rukknr
6.13 c                   eval      brstrk = rukknr
6.13 c                   eval      brktyp = ruktyp
6.13 c                   eval      brkund = rukkun
6.13  *
6.13 c                   eval      brfirm = w_firm
6.13 c                   eval      brbong = bhbong
6.13 c                   eval      brofda = *loval
6.13 c                   eval      brofti = *loval
6.13 c                   time                    brodat
6.13 c                   time                    brotim
6.13 c                   time                    bredat
6.13 c                   time                    bretim
6.13 c                   eval      breusr = bheusr
6.13 c                   write     bokklur
6.16 c                   endif
6.13  *
6.13 c** 6.17            endif
6.13 c                   endif
6.14  *
6.14  * Sjekk mot kortregister, og søk etter Maxbo kort på kunden
6.13  *
6.14 c                   eval      rukrl5_ktyp = 3
6.14 c                   eval      rukrl5_kkun = bokund
6.14 c                   eval      rukrl5_kpro = 0
6.14 c     rukrl5_key    chain     rukrl5
6.14 c                   if        %found(rukrl5)
6.14 c** 6.17            if        rukobl = 1
6.16  *
6.16  * Legges ikke ut hvis kombinasjon bong/kortnr finnes fra før.
6.16  *
6.16 c                   eval      bokklu_bong =  bhbong
6.16 c                   eval      bokklu_kort =  rukknr
6.16 c     bokklu_key    chain     bokklu
6.16  *
6.16 c                   if        not %found
6.14  *
6.14 c                   clear                   bokklur
6.14  *
6.14 c                   eval      brkort = rukknr
6.14 c                   eval      brstrk = rukknr
6.14 c                   eval      brktyp = ruktyp
6.14 c                   eval      brkund = rukkun
6.14  *
6.14 c                   eval      brfirm = w_firm
6.14 c                   eval      brbong = bhbong
6.14 c                   eval      brofda = *loval
6.14 c                   eval      brofti = *loval
6.14 c                   time                    brodat
6.14 c                   time                    brotim
6.14 c                   time                    bredat
6.14 c                   time                    bretim
6.14 c                   eval      breusr = bheusr
6.14 c                   write     bokklur
6.16 c                   endif
6.14  *
6.14 c** 6.17            endif
6.14 c                   endif
6.18  *
6.18  * Sjekk mot kortregister, og søk etter GE kort på kunden
6.18  *
6.18 c                   eval      rukrl5_ktyp = 4
6.18 c                   eval      rukrl5_kkun = bokund
6.18 c                   eval      rukrl5_kpro = 0
6.18 c     rukrl5_key    chain     rukrl5
6.18 c                   if        %found(rukrl5)
6.18  *
6.18  * Legges ikke ut hvis kombinasjon bong/kortnr finnes fra før.
6.18  *
6.18 c                   eval      bokklu_bong =  bhbong
6.18 c                   eval      bokklu_kort =  rukknr
6.18 c     bokklu_key    chain     bokklu
6.18  *
6.18 c                   if        not %found
6.18  *
6.18 c                   clear                   bokklur
6.18  *
6.18 c                   eval      brkort = rukknr
6.18 c                   eval      brstrk = rukknr
6.18 c                   eval      brktyp = ruktyp
6.18 c                   eval      brkund = rukkun
6.18  *
6.18 c                   eval      brfirm = w_firm
6.18 c                   eval      brbong = bhbong
6.18 c                   eval      brofda = *loval
6.18 c                   eval      brofti = *loval
6.18 c                   time                    brodat
6.18 c                   time                    brotim
6.18 c                   time                    bredat
6.18 c                   time                    bretim
6.18 c                   eval      breusr = bheusr
6.18 c                   write     bokklur
6.18 c                   endif
6.18  *
6.18 c                   endif
6.16 c                   endif
6.27  *
6.27  * Trekker brukt depositumsbeløp
6.27  *
6.27 c                   if        bhbdpo > 0
6.27 c                   eval      fohel1_numm = biornr
6.27 c     fohel1_key    chain     fohel1
6.27 c                   if        %found
6.27  *
6.27 c                   eval      sdeplu_numm = biornr
6.27 c                   eval      sdeplu_kund = fokund
6.27 c     sdeplu_key    chain     sdeplu
6.27 c                   if        %found
6.27 c                   eval      skdept = skdept + bhbdpo
7.fb c                   time                    skedat
7.fb c                   time                    sketim
7.fb c                   eval      skeusr = l_user
6.27 c                   update    sdeplur
6.27 c                   end
6.27 c                   end
6.27 c                   end
      *
      * Skriver til BCHEST, historikk for nye kasser
      *
     c                   write     bchestr
      *
8.04 c                   time                    bodate
8.04 c                   time                    botime
8.04 c                   eval      bouser = l_user
8.04 c                   time                    boedat
8.04 c                   time                    boetim
8.04 c                   eval      boeusr = l_user
     c                   write     bohelur
6.23  *
6.23  * Sjekker om kundeprosjekt skal legge ut ByggDok
6.23  *
6.23 c                   eval      b_bdo2 = *off
6.23 c                   eval      b_bdo4 = *off
6.23 c                   if        b_bdo1 = *on
6.24 c                   eval      fkprl1_kund = bokund
6.24 c                   eval      fkprl1_kpro = bokpro
6.24 c     fkprl1_key    chain     fkprl1r
6.38 c                   if        %found(fkprl1)
6.38 c                   if        flbdok = 1 or
6.38 c                             flcobu = 1
6.23 c                   time                    w_btms
6.23 c                   eval      b_bdo2 = *on
6.38 c                   endif
6.23 c                   endif
6.23 c                   eval      b_bdo3 = *on
6.23 c                   endif
7.01  *
7.01  * Skriver til ny tabell hvis det er bong betalt med Santanderkort
7.01  *
7.01 c                   if        betype = '98'
7.01 c                   eval      breflu_bong =  bebong
7.01 c     breflu_key    chain     breflu
7.01  *
7.01 c                   if        not %found
7.01  *
7.01 c                   clear                   breflur
7.01  *
7.01 c                   eval      bxfirm = w_firm
7.01 c                   eval      bxaarr = breflu_aarr
7.01 c                   eval      bxbong = bebong
7.01 c                   eval      bxktyp = betype
7.01  *
7.01 c                   eval      bxkidr = bekidn
7.01 c                   time                    bxodat
7.01 c                   time                    bxotim
7.01 c                   eval      bxousr = bheusr
7.01 c                   time                    bxedat
7.01 c                   time                    bxetim
7.01 c                   eval      bxeusr = bheusr
7.01 c                   write     breflur
7.01 c                   endif
7.01  *
7.01 c                   endif
7.04  *
7.04  * Skriver til ny tabell hvis det er bong betalt med payex
7.04  *
7.04 c                   if        betype = '97'
7.04 c                   eval      breflu_bong =  bebong
7.04 c     breflu_key    chain     breflu
7.04  *
7.04 c                   if        not %found
7.04  *
7.04 c                   clear                   breflur
7.04  *
7.04 c                   eval      bxfirm = w_firm
7.04 c                   eval      bxaarr = breflu_aarr
7.04 c                   eval      bxbong = bebong
7.04 c                   eval      bxktyp = betype
7.04  *
7.04 c                   eval      bxkidr = bekidn
7.04 c                   time                    bxodat
7.04 c                   time                    bxotim
7.04 c                   eval      bxousr = bheusr
7.04 c                   time                    bxedat
7.04 c                   time                    bxetim
7.04 c                   eval      bxeusr = bheusr
7.04 c                   write     breflur
7.04 c                   endif
7.04  *
7.04 c                   endif
7.06  *
7.06  * Skriver til ny tabell hvis det er bong betalt med Vipps
7.06  *
7.06 c                   if        betype = '96'
7.06 c                   eval      breflu_bong =  bebong
7.06 c     breflu_key    chain     breflu
7.06  *
7.06 c                   if        not %found
7.06  *
7.06 c                   clear                   breflur
7.06  *
7.06 c                   eval      bxfirm = w_firm
7.06 c                   eval      bxaarr = breflu_aarr
7.06 c                   eval      bxbong = bebong
7.06 c                   eval      bxktyp = betype
7.06  *
7.06 c                   eval      bxkidr = bekidn
7.06 c                   time                    bxodat
7.06 c                   time                    bxotim
7.06 c                   eval      bxousr = bheusr
7.06 c                   time                    bxedat
7.06 c                   time                    bxetim
7.06 c                   eval      bxeusr = bheusr
7.06 c                   write     breflur
7.06 c                   endif
7.06  *
7.06 c                   endif
7.06  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving til bong-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bong_linje    begsr
      *
     c                   eval      w_boli = 0
      *
      * Leser butikk tekstlinjer og skriver til bong-linje
      *
     c                   eval      bbhti1_hbon = bhbong
     c     bbhti1_key    setll     bbhti1
     c     bbhti1_key    reade     bbhti1
     c                   dow       not %eof
      *
     c                   clear                   bodtlur
      *
     c                   eval      bhttxt = %trim(bhttxt)
      *
     c                   eval      w_boli = w_boli + 10
     c                   eval      bdfirm = bofirm
     c                   eval      bdaarr = boaarr
     c                   eval      bdwsid = bowsid
     c                   eval      bdbong = bobong
     c                   eval      bdline = w_boli
     c                   eval      bdltyp = baatty
     c                   eval      bdtek1 = %subst(bhttxt:1:35)
     c                   eval      bdtek2 = %subst(bhttxt:36:35)
     c                   eval      bdodat = bhodat
     c                   eval      bdotim = bhotim
     c                   eval      bdedat = bhedat
     c                   eval      bdetim = bhetim
     c                   eval      bdeusr = %xlate(lo:up:bheusr)
      *
     c                   if        baahoy = 1
     c                   eval      bdtek1 = %xlate(lo:up:bdtek1)
     c                   eval      bdtek2 = %xlate(lo:up:bdtek2)
     c                   endif
      *
      * Skriver til BCHTST, historisk register
      *
     c                   write     bchtstr
      *
8.04 c                   time                    bdodat
8.04 c                   time                    bdotim
8.04 c                   eval      bdousr = l_user
8.04 c                   time                    bdedat
8.04 c                   time                    bdetim
8.04 c                   eval      bdeusr = l_user
     c                   write     bodtlur
      *
     c     bbhti1_key    reade     bbhti1
     c                   enddo
      *
      * Leser alle butikklinjer og skriver til bong-linje
      * Oppdatere lager dersom behandlingstypen ikke er 4 (Betaling)
      *
     c                   eval      bbdti1_bong = bhbong
     c     bbdti1_key    setll     bbdti1
     c     bbdti1_key    reade     bbdti1
     c                   dow       not %eof
      *
      * Sjekker om deposituminnbetaling
      *
6.3g c                   if        bivare = baadva and
6.3g c                             bissim <> 0
5.61 c                   exsr      depo_inn
5.61 c                   endif
      *
      * Sjekker om dette er behandling av ordre, sletter denne.
      * Funksjon er flyttet fra kassen til server
      *
5.61 c                   if        bivare <> baadva and
     c                             biornr <> 0
5.61 c                   exsr      slett_ordre
5.61 c                   endif
      *
      * Flytter data til bong_linje register
      *
     c                   clear                   bodtlur
      *
     c                   eval      w_boli = w_boli + 10
      *
     c                   eval      bdfirm = bofirm
     c                   eval      bdaarr = boaarr
     c                   eval      bdwsid = bowsid
     c                   eval      bdbong = bobong
     c                   eval      bdline = w_boli
     c                   eval      bdltyp = baalty
6.3f c                   if        w_lety <> 0
6.3c c                   eval      bdlety = w_lety
6.3f c                   else
6.3f c                   eval      bdlety = bilety
6.3f c                   endif
     c                   eval      bdvare = bivare
     c                   eval      bdenh1 = bienhe
     c                   eval      bdtek1 = bitek1
     c                   eval      bdtek2 = bitek2
     c                   eval      bdanta = bianta
6.31 c                   eval(h)   bdoppr = bisaop
     c                   eval      bdsapr = bisapr
6.31 c                   eval(h)   bdkopr = bikopr
6.31 c                   eval(h)   bdselk = bikoop
     c                   eval      bdrab1 = birab1
6.31 c                   eval(h)   bdrbe1 = birbe1
6.31 c                   eval      bdrbi1 = birbi1
     c                   eval      bdrab2 = birab2
6.31 c                   eval(h)   bdrbe2 = birbe2
6.31 c                   eval      bdrbi2 = birbi2
     c                   eval      bdbrr1 = bibrr1
6.31 c                   eval(h)   bdbrb1 = bibrb1
6.31 c                   eval      bdbib1 = bibib1
     c                   eval      bdbrr2 = bibrr2
6.31 c                   eval(h)   bdbrb2 = bibrb2
6.31 c                   eval      bdbib2 = bibib2
     c                   eval(h)   bdorab = biorab
6.31 c                   eval(h)   bdorbe = biorbe
6.31 c                   eval      bdorbi = biorbi
     c                   eval      bdkpri = bikpri
     c                   eval      bdkamp = bikamp
     c                   eval      bdornr = biornr
     c                   eval      bdorsu = biorsu
     c                   eval      bdorli = biorli
6.3d c                   if        bibenr > 999999
6.3d c                   eval      bdbenr = 0
6.3d c                   else
     c                   eval      bdbenr = bibenr
6.3d c                   endif
     c                   eval      bdbesu = bibesu
     c                   eval      bdbeli = bibeli
     c                   eval      bdavde = biavde
     c                   eval      bdlage = bilage
     c                   eval      bdselg = biselg
     c                   eval      bdseri = biseri
     c                   eval      bdkmva = bikmva
6.31 c                   eval      bdgmva = bifmva
6.31 c                   eval      bdsmva = bismva
6.31 c                   eval      bdbmva = bibmva
6.31 c                   eval      bdfmva = bigmva
     c                   eval      bdalme = bialme
     c                   eval      bdbrty = bibrty
     c                   eval      bdpriv = bipriv
     c                   eval      bdtref = bitref
     c                   eval      bdgave = bigave
     c                   eval      bdogrp = 0
     c                   eval      bdhgrp = 0
     c                   eval      bdugrp = 0
     c                   eval      bdlvre = 0
     c                   eval      bdsums = bisums
6.31 c                   eval      bdsumi = bisnim
6.31 c                   eval      bdsumo = bisumo
     c                   eval      bdsumk = bisumk
     c                   eval      bdsuki = biskim
6.31 c                   eval      bdsuok = bisuko
     c                   eval      bdodat = biodat
     c                   eval      bdotim = biotim
     c                   eval      bdedat = biedat
     c                   eval      bdetim = bietim
     c                   eval      bdeusr = %xlate(lo:up:bieusr)
      *
     c                   if        bdgave <> *blank
     c                   eval      bdvare = baagva
     c                   endif
      *
     c                   if        bdselg = 0
     c                   eval      bdselg = boselg
     c                   endif
      *
     c                   if        biovsp = 1
     c                   eval      bdkpri = 'F'
     c                   endif
      *
     c                   if        bdavde = 0
     c                   eval      bdavde = boavde
     c                   endif
      *
     c                   if        bdlage = 0
     c                   eval      bdlage = bolage
     c                   endif
6.3e  * Kaller program for henting av evt. trelast-info
6.3e  *
6.3e c                   call      'VL710R'
6.3e c                   parm                    w_firm
6.3e c                   parm                    bivare
6.3e c                   parm                    bdlage
6.3e c                   parm                    w_lv_vtyp
6.3e c                   parm                    w_lv_udat
6.3e c                   parm                    w_lv_ldor
6.3e c                   parm                    b_inlr
6.3e c                   parm                    w_lv_nobb
6.3e c                   parm                    w_lv_modn
6.3e c                   parm                    w_lv_lldo
6.3e c                   parm                    w_lv_llva
      *
      * Henter vareinformasjon.
      * Dersom vare kun finnes i VA settes lvre-flagg
      *
     c                   eval      vvarl1_vare = bivare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      bdogrp = vvogrp
     c                   eval      bdhgrp = vvhgrp
     c                   eval      bdugrp = vvugrp
6.3e c                   if        w_lv_nobb <> 0
6.3e c                   eval      bdnobb = w_lv_nobb
6.3e c                   eval      bdldor = w_lv_ldor
6.3e c                   eval      bdlvar = w_lv_llva
6.3e c                   else
     c                   eval      bdnobb = vvnobb
     c                   eval      bdldor = vvldor
     c                   eval      bdlvar = vvlvar
6.3e c                   endif
     c                   else
     c                   eval      jvarl1_vare = bivare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      bdogrp = jvogrp
     c                   eval      bdhgrp = jvhgrp
     c                   eval      bdugrp = jvugrp
     c                   eval      bdnobb = jvnobb
     c                   eval      bdlvre = 1
     c                   endif
     c                   endif
      *
     c                   if        baahoy = 1
     c                   eval      bdtek1 = %xlate(lo:up:bdtek1)
     c                   eval      bdtek2 = %xlate(lo:up:bdtek2)
     c                   endif
      *
      * Snur fortegn dersom sum å betale inkl. mva er negativ
      *
     c                   if        w_tnim < 0
     c                   eval      bdanta = bdanta * -1
     c                   eval      bdsums = bdsums * -1
     c                   eval      bdsumk = bdsumk * -1
6.31 c                   eval      bdsumi = bdsumi * -1
6.31 c                   eval      bdsumo = bdsumo * -1
6.31 c                   eval      bdsuki = bdsuki * -1
6.31 c                   eval      bdsuok = bdsuok * -1
6.31 c                   eval      bdgmva = bdgmva * -1
6.31 c                   eval      bdbmva = bdbmva * -1
     c                   endif
      *
      * Skriver til BCDTST, historikk av nye kasser
      *
     c                   write     bcdtstr
      *
      * Skriver til bong-linje
      *
8.04 c                   time                    bdodat
8.04 c                   time                    bdotim
8.04 c                   eval      bdousr = l_user
8.04 c                   time                    bdedat
8.04 c                   time                    bdetim
8.04 c                   eval      bdeusr = l_user
     c                   write     bodtlur
      *
6.23  * Oppdaterer evt. byggdok
6.23  *
6.3a c                   if        vaoakk = 2  or
6.3a c                             vaoakk = 3  or
6.3a c                             vaoakk = 4
6.3a c                   if        vaokre = *blank
6.23 c                   exsr      byggdok
6.23 c                   endif
6.3a c                   endif
      *
      * Oppdaterer lager
      *
     c                   if        baalag <> 0 and
     c                             bhbtyp <> 4
     c                   exsr      lager
     c                   endif
      *
      * Dersom linjen har kommentar legges denne ut som tekst-linje
      *
     c                   if        bikomm <> *blank
     c                   clear                   bodtlur
     c                   eval      w_boli = w_boli + 10
     c                   eval      bdfirm = bofirm
     c                   eval      bdaarr = boaarr
     c                   eval      bdwsid = bowsid
     c                   eval      bdbong = bobong
     c                   eval      bdline = w_boli
     c                   eval      bdltyp = baatty
     c                   eval      bdtek1 = %subst(bikomm:1:35)
     c                   eval      bdtek2 = %subst(bikomm:36:35)
     c                   eval      bdornr = biornr
     c                   eval      bdorsu = biorsu
     c                   eval      bdorli = biorli
6.3d c                   if        bibenr > 999999
6.3d c                   eval      bdbenr = 0
6.3d c                   else
     c                   eval      bdbenr = bibenr
6.3d c                   endif
     c                   eval      bdbesu = bibesu
     c                   eval      bdbeli = bibeli
     c                   eval      bdodat = biodat
     c                   eval      bdotim = biotim
     c                   eval      bdedat = biedat
     c                   eval      bdetim = bietim
     c                   eval      bdeusr = %xlate(lo:up:bieusr)
     c                   if        baahoy = 1
     c                   eval      bdtek1 = %xlate(lo:up:bdtek1)
     c                   eval      bdtek2 = %xlate(lo:up:bdtek2)
     c                   endif
8.04 c                   time                    bdodat
8.04 c                   time                    bdotim
8.04 c                   eval      bdousr = l_user
8.04 c                   time                    bdedat
8.04 c                   time                    bdetim
8.04 c                   eval      bdeusr = l_user
     c                   write     bodtlur
     c                   endif
      *
     c     bbdti1_key    reade     bbdti1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lager         begsr
      *
     c                   eval      hlvare = bdvare
     c                   eval      hllage = bdlage
     c                   eval      hlenhe = bdenh1
     c                   eval      hldato = bdodat
     c                   eval      hltime = bdotim
     c                   eval      hlotyp = bootyp
     c                   eval      hlltyp = bdltyp
     c                   eval      hlanta = bdanta
     c                   eval      hlkopr = bdkopr
6.11 c                   eval      hlrefr = 'Bong: ' + bdbong
6.11 c**                 eval      hlrefr = bdbong
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   eval      hllety = %char(bdlety)
6.11 c                   eval      hlonum = w_numm
8.01 c*                  eval      hlolin = 0
8.01  * hvis w_numm er 0, hent info fra bonglinje.
8.01 c                   if        hlonum = 0
8.01 c                   eval      hlonum = biornr
8.01 c                   endif
8.01 c                   eval      hlolin = biorli
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   call      'VL001R'
     c                   parm                    w_retu
     c                   parm                    hlrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     faktura       begsr
      *
      * Tildeler neste fakturanummer
      *
     c                   eval      w_fell = baabnr
      *
     c                   exsr      hent_nummer
      *
      * Finner fakturadato og forfallsdato
      *
8.03 c                   if        boldat <> *loval
8.03 c                   eval      w_fkda = boldat
8.03 c                   else
     c                   eval      w_fkda = bodate
8.03 c                   endif
      *
     c                   call      'FÅ703R'
     c                   parm                    w_firm
     c                   parm                    bobetb
     c                   parm                    w_fkda
     c                   parm                    w_retu
     c                   parm                    w_ffda
     c                   parm                    w_btx1
     c                   parm                    w_btx2
      *
      * Oppdaterer bong-hode med fakuranr, fakturadato og forfallsdato
      *
     c                   eval      bohelu_aarr = boaarr
     c                   eval      bohelu_wsid = bowsid
     c                   eval      bohelu_bong = bobong
     c     bohelu_key    chain     bohelur
     c                   eval      bobinr = w_numm
     c                   eval      bofkda = w_fkda
     c                   eval      boffda = w_ffda
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelur
      *
      * Skriver til faktura
      *
     c                   call      'BO710R'
     c                   parm                    w_firm
     c                   parm                    boaarr
     c                   parm                    bowsid
     c                   parm                    bobong
     c                   parm                    w_tnim
     c                   parm                    COMFLAG
      *
      * Kaller utskriftsprogram
      *
     c                   eval      d_aarr = boaarr
     c                   eval      d_otyp = bootyp
     c                   eval      d_ffnr = bobinr
     c                   eval      d_tfnr = bobinr
      *
     c                   call      afprog
     c                   parm                    d_parm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av pakkseddel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pakkseddel    begsr
      *
      * Tildeler neste ordrenummer
      * Dersom ordrenummer allerede er tildelt fra kasse benyttes dette
      *
     c                   if        bhornr <> 0
     c                   eval      w_numm = bhornr
     c                   else
     c                   eval      w_fell = baaord
     c                   exsr      hent_nummer
     c                   endif
      *
      * Oppdaterer bong-hode med ordrenr
      *
     c                   eval      bohelu_aarr = boaarr
     c                   eval      bohelu_wsid = bowsid
     c                   eval      bohelu_bong = bobong
     c     bohelu_key    chain     bohelur
     c                   eval      boornr = w_numm
     c                   eval      boorsu = 0
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelur
      *
      * Oppdaterer bong-linje med ordrenr
      *
     c                   eval      bodtlu_aarr = boaarr
     c                   eval      bodtlu_wsid = bowsid
     c                   eval      bodtlu_bong = bobong
     c     bodtlu_key    setll     bodtlu
     c     bodtlu_key    reade     bodtlur
     c                   dow       not %eof
     c                   eval      bdornr = w_numm
6.10 c                   time                    bdedat
6.10 c                   time                    bdetim
6.10 c                   eval      bdeusr = l_user
     c                   update    bodtlur
     c     bodtlu_key    reade     bodtlur
     c                   enddo
      *
      *
      * Skriver til ordre
      *
     c                   call      'BO700R'
     c                   parm                    w_firm
     c                   parm                    boaarr
     c                   parm                    bowsid
     c                   parm                    bobong
     c                   parm                    bhbtyp
6.25 c                   parm                    bhoffl
     c                   parm                    COMFLAG
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hurtigordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hurtigordre   begsr
      *
      * Tildeler neste ordrenummer
      *
     c                   eval      w_fell = baaord
      *
     c                   exsr      hent_nummer
      *
      * Oppdaterer bong-hode med ordrenr
      *
     c                   eval      bohelu_aarr = boaarr
     c                   eval      bohelu_wsid = bowsid
     c                   eval      bohelu_bong = bobong
     c     bohelu_key    chain     bohelur
     c                   eval      boornr = w_numm
     c                   eval      boorsu = 0
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelur
      *
      * Oppdaterer bong-linje med ordrenr
      *
     c                   eval      bodtlu_aarr = boaarr
     c                   eval      bodtlu_wsid = bowsid
     c                   eval      bodtlu_bong = bobong
     c     bodtlu_key    setll     bodtlu
     c     bodtlu_key    reade     bodtlur
     c                   dow       not %eof
     c                   eval      bdornr = w_numm
6.10 c                   time                    bdedat
6.10 c                   time                    bdetim
6.10 c                   eval      bdeusr = l_user
     c                   update    bodtlur
     c     bodtlu_key    reade     bodtlur
     c                   enddo
      *
      * Skriver til ordre
      *
     c                   call      'BO700R'
     c                   parm                    w_firm
     c                   parm                    boaarr
     c                   parm                    bowsid
     c                   parm                    bobong
     c                   parm                    bhbtyp
6.25 c                   parm                    bhoffl
      *
      * Kaller utskriftsprogram
      *
     c                   eval      w_utsk = 0
     c                   eval      w_kopi = *blank
      *
     c                   call      afprog
     c                   parm                    w_utsk
     c                   parm                    boaarr
     c                   parm                    bowsid
     c                   parm                    bobong
     c                   parm                    w_kopi
      *
     c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Sjekk/Hent tekst for leveringsmåte                RS717R
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sbform        begsr
6.21  *
6.21 c                   eval      bolmtx = *blank
6.21 c                   call      'RS717R'
6.21 c                   parm                    w_retk
6.21 c                   parm                    bolmat
6.21 c                   parm                    w_qtxt
6.21  *
6.21 c                   if        w_retk = *blank
6.21 c                   movel     w_qtxt        bolmtx
6.21 c                   endif
6.21  *
6.21 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av neste ledige nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nummer   begsr
      *
     c                   eval      b_numm = *off
      *
      * Hente inn eventuell overstyrt ordretype for nummerserie
      *
     c                   eval      w_type = *blank
      *
     c                   call      'VA752R'
     c                   parm                    w_firm
     c                   parm                    w_otyp
     c                   parm                    w_type
      *
      * Henter neste nummer fra nummer-registeret
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fast = *blank
      *
     c                   if        w_type = *blank
     c                   eval      w_type = w_otyp
     c                   endif
      *
     c                   dou       b_numm = *on
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   if        bhbtyp = 2 or
     c                             bhbtyp = 5
     c                   eval      fohel1_numm = w_numm
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   eval      b_numm = *on
     c                   leave
     c                   endif
     c                   else
     c                   eval      b_numm = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
5.61  *
5.61  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.61  * Subrutine for oppdatering av depositumbeløp på ordre
5.61  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.61  *
5.61 c     depo_inn      begsr
5.61  *
5.61  * Finn kunde via ordrenummer
5.61  *
     c                   eval      fohel1_numm = biornr
     c     fohel1_key    chain     fohel1
     c                   if        %found
5.61  *
5.61 c                   eval      sdeplu_numm = biornr
5.61 c                   eval      sdeplu_kund = fokund
5.61 c     sdeplu_key    chain     sdeplu
5.61 c                   if        %found
5.61 c                   eval      skdepb = skdepb + bissim
7.fb c                   time                    skedat
7.fb c                   time                    sketim
7.fb c                   eval      skeusr = l_user
5.61 c                   update    sdeplur
     c                   else
     c                   eval      skfirm = w_firm
     c                   eval      sknumm = biornr
     c                   eval      skkund = 999999
     c                   eval      skdepb = bissim
     c                   eval      skuser = 'FEIL'
7.fb c                   time                    skedat
7.fb c                   time                    sketim
7.fb c                   eval      skeusr = l_user
7.fb c                   time                    skodat
7.fb c                   time                    skotim
7.fb c                   eval      skousr = l_user
     c                   write     sdeplur
5.61 c                   endif
5.61 c                   endif
5.61  *
5.61 c                   endsr
5.61  *
5.61  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.61  * Subrutine for sletting av ordre behandlet i butikk
5.61  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.61  *
5.61 c     slett_ordre   begsr
5.61  *
     c                   if        biornr <> w_ornr or
     c                             biorsu <> w_orsu
     c                   move      w_firm        w_fira
     c     ' ':'0'       xlate     w_fira        w_fira
      *
     c                   call      'BU925R'
     c                   parm                    w_fira
     c                   parm                    biornr
     c                   parm                    biorsu
      *
     c                   eval      w_ornr = biornr
     c                   eval      w_orsu = biorsu
      *
     c                   endif
5.61  *
5.61 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
5.64  * Ruller tilbake utestående transaksjoner
5.64  *
     c                   if        COMFLAG = '1'
5.64 c                   rolbk
     c                   endif
     c                   eval      p_stat = 'ERROR'
     c                   eval      p_errt = 'Feil i BU990R! Info=' +
     c                             %trim(excp_data) + '. Line=' +
     c                             %trim(line_numm) + '. Job=' +
     c                             %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
     c* Sperret          goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.62  * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = 'DBERR'
     c                   eval      p_errt = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
6.3b c*                  goto      avslutt
      *
     c                   endsr
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for å legge ut i Byggdok
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     byggdok       begsr
6.23  *
6.23  * legger evt. linjer til BygDok
6.23  *
6.23 c                   if        b_bdo1 = *on and
6.23 c                             b_bdo2 = *on and
6.23 c                             b_bdo3 = *on and
7.03 c                             ((bdnobb <> 0) or
7.03 c                             (u_703 = *on and
7.03 c                              bdvare <> *blank))
6.23 c                   eval      rkunl1_kund = bokund
6.23 c     rkunl1_key    chain     rkunl1
6.23  * finner kontaktperson for kunde som har rolle BYGGDOK
6.23 c                   eval      b_kper  = *off
6.23 c                   eval      rukpl6_kund = bokund
6.23 c     rukpl6_key    setll     rukpl6
6.23 c     rukpl6_key    reade     rukpl6
6.23 c                   dow       not %eof
6.23 c     lo:up         xlate     rukrol        rukrol
6.33 c                   if        rukrol  = 'BYGGDOK' or rukrol = 'COBUILDER'
6.23 c                   eval      b_kper  = *on
6.23 c                   goto      fbdo_tag
6.23 c                   endif
6.23 c     rukpl6_key    reade     rukpl6
6.23 c                   enddo
6.23 c     fbdo_tag      tag
6.23 c                   eval      fwbfir = w_firm
6.23 c                   eval      fwbkun = bokund
6.23 c                   eval      fwbkpr = bokpro
6.23 c                   movel     bobong        fwbnum
6.23 c                   eval      fwbnav = rknavn
6.23 c                   if        b_kper  = *on and
6.23 c                             ruenav <> *blank and
6.23 c                             rumail <> *blank
6.23 c                   eval      fwbper = %trim(ruenav) + ',' +
6.23 c                                      %trim(rufnav)
6.23 c                   eval      fwbmai = rumail
6.23 c                   else
6.23 c                   eval      fwbper = rknavn
6.23 c                   eval      fwbmai = rkemal
6.23 c                   endif
6.38  * hvis mail på kunde-prosjekt overstyrer det alt
6.38 c                   if        flmail <> *blank
6.38 c                   eval      fwbmai = flmail
6.38 c                   if        flkprs <> *blank
6.38 c                   eval      fwbper = flkprs
6.38 c                   endif
6.38 c                   endif
6.23 c                   eval      fwbftn = rkftnr
6.23 c                   move      w_btms        w_ti26
6.23 c                   eval      fwbtms = w_ti26
7.03 c                   if        u_703 = *on
7.03 c                   movel     bdvare        fwbnob
7.03 c                   else
6.23 c                   eval      fwbnob = bdnobb
7.03 c                   endif
6.23 c                   time                    fwboda
6.23 c                   time                    fwboti
6.23 c                   time                    fwbeda
6.23 c                   time                    fwbeti
6.23 c                   eval      fwbous = l_user
6.23 c                   eval      fwbeus = l_user
6.23 c                   write     fbdopfr
6.2c c                   move      fwbnum        w_bnum
6.23 c                   eval      b_bdo4 = *on
6.23 c                   endif
6.23  *
6.23 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kass
     c                   parm                    p_stat
5.62 c                   parm                    p_errt
     c                   parm                    COMFLAG
      *
      * Key for oppslag på formular/rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppslag på Butikk koderegister
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på Ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på Vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
      *
      * Key for oppdatering på ordre-hode
      *
     c     sdeplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sdeplu_numm
     c                   kfld                    sdeplu_kund
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
7.07  *
7.07  * Key for oppslag på kunde
7.07  *
7.07 c     rkunlr_key    klist
7.07 c                   kfld                    w_firm
7.07 c                   kfld                    rkunlr_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på ButikkHode
      *
     c     bbhei2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kass
      *
      * Key for les på ButikkLinje
      *
     c     bbdti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbdti1_bong
      *
      * Key for les på Betalingstranser
      *
     c     bbbli1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbbli1_bong
      *
      * Key for les på ButikkHodeTekst
      *
     c     bbhti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbhti1_hbon
6.12  *
6.12  * Key for oppslag mot kortregister på kortnummer
6.12  *
6.12 c     rukrl4_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    rukrl4_kknr
6.13  *
6.13  * Key for oppslag mot kortregister på kortnummer
6.13  *
6.13 c     rukrl5_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    rukrl5_ktyp
6.13 c                   kfld                    rukrl5_kkun
6.13 c                   kfld                    rukrl5_kpro
6.12  *
6.12  * Key for oppslag mot kort transaksjonsregister på bong og kortnummer
6.12  *
6.12 c     bokklu_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    bokklu_bong
6.12 c                   kfld                    bokklu_kort
6.12  *
6.12  * Key for oppslag mot kort transaksjonsregister på bongnummer
6.12  *
6.12 c     bbkki1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    bbkki1_tbon
6.12  *
6.12  * Key for oppdatering av Korttranser
6.12  *
6.12 c     bbkkiu_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    bbkkiu_tbon
      *
      * Key for les på Bong-linje
      *
     c     bodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtl1_aarr
     c                   kfld                    bodtl1_wsid
     c                   kfld                    bodtl1_bong
      *
      * Key for oppdatering av Bong-hode
      *
     c     bohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelu_aarr
     c                   kfld                    bohelu_wsid
     c                   kfld                    bohelu_bong
      *
      * Key for oppdatering av Bong-linje
      *
     c     bodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtlu_aarr
     c                   kfld                    bodtlu_wsid
     c                   kfld                    bodtlu_bong
      *
      * Key for oppdatering av Betalingstranser
      *
     c     bobliu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bobliu_kass
     c                   kfld                    bobliu_aarr
     c                   kfld                    bobliu_bong
     c                   kfld                    bobliu_linj
      *
      * Key for oppdatering av ButikkHode
      *
     c     bbheiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbheiu_bong
      *
      * Key for oppdatering av ButikkLinje
      *
     c     bbdtiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbdtiu_bong
      *
      * Key for oppdatering av Betalingstranser
      *
     c     bbbliu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbbliu_bong
      *
      * Key for oppdatering av ButikkHodeTekst
      *
     c     bbhtiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bbhtiu_hbon
6.21  *
6.21  * Key for oppslag på Ofak status register
6.21  *
6.21 c     fstsl1_key    klist
6.21 c                   kfld                    w_firm
6.22  *
6.22  * Key for file : Firmastyrt validering
6.22  *
6.22 c     avall1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    avall1_apgm
6.23  *
6.23  * Key for les av moduler Nexstep
6.23  *
6.23 c     amodl1_key    klist
6.23 c                   kfld                    amodl1_modu
6.23  *
6.23  * Key for propertiesfil
6.23  *
6.23 c     afpslr_key    klist
6.23 c                   kfld                    afpslr_ufil
6.23 c                   kfld                    afpslr_uide
6.23  *
6.23  * Key for oppslag på kunde - kontaktperson
6.23  *
6.23 c     rukpl6_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    rukpl6_kund
6.34  *
6.34  * Key for oppslag på leveringsmåte
6.34  *
6.34 c     ra17l1_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    ra17l1_qkod
7.01  *
7.01  * Key for referansefil
7.01  *
7.01 c     breflu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    breflu_aarr
7.01 c                   kfld                    breflu_bong
      *
      *
      * Henter firma og kasse fra parameter
      *
     c                   move      p_firm        w_firm
     c***                eval      w_firm = p_firm
     c**                 eval      w_kass = p_kass
     c                   move      p_kass        w_kass
6.22  *
6.22  * Sjekk om det er lagt opp krav til leveringsadresse
6.22  *
6.22  *
6.22 c                   eval      avall1_apgm = 'FO100R'
6.22 c     avall1_key    setll     avall1
6.22 c     avall1_key    reade     avall1
6.22 c                   dow       not %eof(avall1)
6.22 c                   if        avaflt = 'FONAVN'  and
6.22 c                             avaval = 1
6.22 c                   eval      u_navn = *on
6.22 c                   endif
6.22 c     avall1_key    reade     avall1
6.22 c                   enddo
6.35  *
6.35  * Sjekk om det er lagt opp krav til leveringsmåte
6.35  *
6.35 c                   eval      avall1_apgm = 'FO101R'
6.35 c     avall1_key    setll     avall1
6.35 c     avall1_key    reade     avall1
6.35 c                   dow       not %eof(avall1)
6.35 c                   if        avaflt = 'FOLMAT'  and
6.35 c                             avaval = 1
6.35 c                   eval      u_lmat = *on
6.35 c                   endif
6.35 c     avall1_key    reade     avall1
6.35 c                   enddo
6.35  *
6.35  * Sjekk om det er lagt opp krav til leveringsmåte
6.35  *
6.35 c                   eval      avall1_apgm = 'FO102R'
6.35 c     avall1_key    setll     avall1
6.35 c     avall1_key    reade     avall1
6.35 c                   dow       not %eof(avall1)
6.35 c                   if        avaflt = 'FOLMAT'  and
6.35 c                             avaval = 1
6.35 c                   eval      u_lmat = *on
6.35 c                   endif
6.35 c     avall1_key    reade     avall1
6.35 c                   enddo
6.35  * Sjekk om det er lagt opp krav til leveringsmåte
6.35  *
6.35 c                   eval      avall1_apgm = 'FO103R'
6.35 c     avall1_key    setll     avall1
6.35 c     avall1_key    reade     avall1
6.35 c                   dow       not %eof(avall1)
6.35 c                   if        avaflt = 'FOLMAT'  and
6.35 c                             avaval = 1
6.35 c                   eval      u_lmat = *on
6.35 c                   endif
6.35 c     avall1_key    reade     avall1
6.35 c                   enddo
6.23  *
6.23  * Sjekker om Byggdok skal kjøres forutsetter at
6.23  * modul er på plass og at
6.23  *
6.23 c                   eval      b_bdo1 = *off
6.23 c                   eval      amodl1_modu = 'BYGGDOK'
6.23 c     amodl1_key    chain     amodl1
6.23 c                   if        %found(amodl1)
6.23 c                   eval      afpslr_ufil = l_filg
6.23 c                   eval      afpslr_uide = 'BYGGDOK   '
6.23  *
6.33  * Sjekk om byggdok eller cobuilder aktivering er på/av
6.23  *
6.23 c     afpslr_key    chain     afpslrr
6.23 c                   if        %found(afpslr)
6.23 c                   eval      b_bdo1 = *on
6.33 c                   else
6.33 c                   eval      afpslr_uide = 'COBUILDER '
6.33 c     afpslr_key    chain     afpslrr
6.33 c                   if        %found(afpslr)
6.33 c                   eval      b_bdo1 = *on
6.33 c                   endif
6.23 c                   endif
6.23 c                   endif
      *
7.01 c                   eval      breflu_aarr = *year
      *
7.03  *
7.03  * Endring 7.03
7.03  *
7.03   CallP CO402R(l_filg:w_firm:0:'COBU_IKKENOBB':
7.03                    co402_verdi1:co402_verdi2);
7.03   if co402_verdi1 = '1';
7.03     u_703 = *on;
7.03   endif;
7.05  *
7.05  * Endring 7.05
7.05  *
7.05   CallP CO402R(l_filg:w_firm:0:'BRUK_XLKLUBB':
7.05                    co402_verdi1:co402_verdi2);
7.05   if co402_verdi1 = '1';
7.05     u_705 = *on;
7.05   endif;
      *
     c                   endsr
