     H DFTACTGRP(*NO)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV867R_XML
      * Beskrivelse . . : Leser NOBB varegruppe   XML 8.0. Legger data i varegruppe variable.
      *                   Kaller standard rpg program, som gjør sjekk av data
      *                   og oppdatere basen
      *
      *                   Dette programmet er skrevet i free format RPG
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV  Nytt program
6.30  * R6.30 300614 BKV  Korrigering av parameter def
6.31  * R6.30 130814 BKV  Angi xpath i basen
6.32  * R6.30 061014 BKV  Ok hvis ingen moduler, dvs retur dagens dato
6.33  * R6.30 170316 BKV  Lagt inn UNSPSCnr
6.34  * R6.30 170817 BKV  Erstattet  med - (ser nesten helt like ut)
6.35  * R6.30 230921 bof  XML innh.ikke change_dato-->feil, endre logikk
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)

      * Definering av Local Data Area
      *
6.31 d                uds
6.31 d l_user                911    920
6.31 d l_firm                944    946  0
6.31 d l_filg                931    933
      *
      * Definering av variabler i nøkler
      *
6.31 d afpslr_ufil     s                   like(l_filg)
6.31 d afpslr_uide     s                   like(afuide)

     d p_stat          s              1
      * Brukes som parametre til jobblogging
     d p_jnav          s             15
     d p_jbid          s             41
     d p_jtxt          s             35
     d p_jsts          s              2  0
     d p_jmld          s            200

     d p_sistDato      s             10a
6.32 d w_lest          s              1  0 Inz(0)

      * Definering av tabell med meldinger
     d a_meld          s            100    dim(9) ctdata perrcd(1)

      *
      * START NOBB 8.0 XML definisjon
      *
      * Path til varegruppe  taggen
     D varegruppe_path...
     D                 s             66a   Inz(
     d                                     'PartnerVaregruppestrukturResult/+
     d                                     VaregruppestrukturListe/+
     d                                     Overgruppe')

      * taggen <b:Overgruppe>
     d Overgruppe      ds                  Qualified
     d   OvergruppeNr...
     d                                2a   varying
     d   Beskrivelse...
     d                               35a   varying
     d   Hovedgruppe...
     d                                     likeds(Hovedgruppe_t) Dim(50)
     D   countHovedgruppe...
     D                                5i 0
     d   status                            likeds(status_t)

      * taggen <b:Hovedgruppe>
     d Hovedgruppe_t...
     d                 ds                  Qualified
     d                                     based(Template)
     d   HovedgruppeNr...
     d                                4a   varying
     d   Beskrivelse...
     d                               35a   varying
     d   Varegruppe...
     d                                     likeds(Varegruppe_t) Dim(200)
     D   countVaregruppe...
     D                                5i 0
     d   status                            likeds(status_t)

      * taggen <b:Varegruppe>
     d Varegruppe_t    ds                  Qualified
     d                                     based(Template)
     d   VaregruppeNr...
     d                                7a   varying
     d   Beskrivelse...
     d                               35a   varying
     d   PSEPrimar...
     d                               35a   varying
     d   PSESekundar...
     d                               35a   varying
     d   UNSPSCnr...
     d                               35a   varying
     d*   NRFVareTyper...
     d*                                     likeds(NRFVareTyper_t) Dim(10)
     d   status                            likeds(status_t)

      ** taggen <b:NRFVareTyper>
     d* NRFVareTyper_t...
     d*                 ds                  Qualified
     d*                                     based(Template)
     d*   NRFGruppeNr...
     d*                                2a   varying
     d*   Beskrivelse...
     d*                               35a   varying
     d*   status                          likeds(status_t)


      * taggen: <b:status>
     d status_t        ds                  template
     d   created                     25a
     d   updated                     25a

      *
      * stopp nobb 8.0 xml definisjon
      *

      *
      *  Dataelementer overgruppe
      *
     d                 ds
     d d_orec                       100
     d  d_ogrp                        2    overlay(d_orec:1)
     d   d_ogrp_num                   2  0 overlay(d_ogrp:1)
     d  d_obes                       35    overlay(d_orec:4)
     d  d_crea                       10    overlay(d_orec:45)
     d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
     d  d_upda                       10    overlay(d_orec:55)
     d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
      *
      *  Dataelementer hovedgruppe
      *
     d                 ds
     d d_hrec                       100
     d  d_hgrp                        2    overlay(d_hrec:1)
     d   d_hgrp_num                   2  0 overlay(d_hgrp:1)
     d  d_hbes                       35    overlay(d_hrec:4)
      *
      *  Dataelementer undergruppe
      *
     d                 ds
     d d_urec                       100
     d  d_ugrp                        3    overlay(d_urec:1)
     d   d_ugrp_num                   3  0 overlay(d_ugrp:1)
     d  d_ubes                       35    overlay(d_urec:5)
     d  d_ups1                        3    overlay(d_urec:40)
     d  d_ups2                        3    overlay(d_urec:43)
6.33 d  d_ufri                       10    overlay(d_urec:46)

     d                 ds
     d d_job_log                     50
     d  w_ovgnt                       5    overlay(d_job_log:2)
     d  w_ovgn                        5  0 overlay(w_ovgnt:1)
     d  w_hvgnt                       5    overlay(d_job_log:7)
     d  w_hvgn                        5  0 overlay(w_hvgnt:1)
     d  w_uvgnt                       5    overlay(d_job_log:12)
     d  w_uvgn                        5  0 overlay(w_uvgnt:1)
     d  w_ovgut                       5    overlay(d_job_log:17)
     d  w_ovgu                        5  0 overlay(w_ovgut:1)
     d  w_hvgut                       5    overlay(d_job_log:22)
     d  w_hvgu                        5  0 overlay(w_hvgut:1)
     d  w_uvgut                       5    overlay(d_job_log:27)
     d  w_uvgu                        5  0 overlay(w_uvgut:1)
     d  w_ovglt                       5    overlay(d_job_log:32)
     d  w_ovgl                        5  0 overlay(w_ovglt:1)
     d  w_hvglt                       5    overlay(d_job_log:37)
     d  w_hvgl                        5  0 overlay(w_hvglt:1)
     d  w_uvglt                       5    overlay(d_job_log:42)
     d  w_uvgl                        5  0 overlay(w_uvglt:1)

     d b_inlr          s              1

      * info og commarea må være likt def. de brukes ikke i programmet, men må
      * være definert
     d info            s             10a
     d options         s            200a   varying

      * prototype for externt program sjekker varegruppe og lagrer i basen
      *  overfører vara datastruktur som parameter
     d P_BehVgru       pr                  extpgm('JV867R')
     d  d_orec                      100
     d  d_hrec                      100
     d  d_urec                      100
     d  d_job_log                    50
     d  b_inlr                        1

     d p_ab705r        pr                  extpgm('AB705R')
     d  p_jbid                       41
     d  p_jsts                        2  0
     d  p_jmld                      200

     d p_ab710r        pr                  extpgm('AB710R')
     d  p_jnav                       15
     d  p_jbid                       41


     d p_ab700r        pr                  extpgm('AB700R')
     d  p_jnav                       15
     d  p_jbid                       41
     d  p_jtxt                       35

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // ------------------------------------- prototypes
     d JV867R_XML      pr                  extpgm('JV867R_XML')
     d   filnavn                    255a   options(*varsize)
6.30 d   lengde                       3p 0 const
     d   sistedato                   10a
       // ----------------------- main procedure interface
     d JV867R_XML      pi
     d   filnavn                    255    options(*varsize)
6.30 d   lengde                       3p 0 const
     d   sistedato                   10a

     d   navnet        s            255a   varying
6.30 d   len           s              3i 0

6.31  *
6.31  * Key for propertiesfil
6.31  *
6.31 c     afpslr_key    klist
6.31 c                   kfld                    afpslr_ufil
6.31 c                   kfld                    afpslr_uide

      /free

       len = lengde;
       navnet = %trim(%subst(filnavn:1:len));

       // vi starter jobblogging
       p_jnav = 'GRUPPEXML';
       p_jtxt = *blank;
       callp p_ab700r (p_jnav : p_jbid : p_jtxt);

       w_ovgn = *zeros;
       w_hvgn = *zeros;
       w_uvgn = *zeros;
       w_ovgu = *zeros;
       w_hvgu = *zeros;
       w_uvgu = *zeros;
       w_ovgl = *zeros;
       w_hvgl = *zeros;
       w_uvgl = *zeros;
6.32   w_lest = 0;

6.31   //  Hent inn NOBB 80 XML path som skal brukes
6.31   afpslr_ufil = l_filg;
6.31   afpslr_uide = 'NOBB80GRU';
6.31   chain afpslr_key afpslrr;
6.31   if %found;
6.31     varegruppe_path = *blanks;
6.31     varegruppe_path = %trim(afudss);
6.31   else;
6.31     p_stat = '1';
6.31   endif;

6.31   if (p_stat <> '1');

         b_inlr = *on;

         options = 'doc=file +
                    case=any +
                    countprefix=count +
                    datasubf=text +
                    ns=remove +
                    path='+varegruppe_path+' '+
                   'allowmissing=yes +
                    allowextra=yes';

          // Bruker RPG xml-info til å lese xml filen.
          // Behandling av innleste vgrupper gjøres av LesVgrupp
          xml-into %handler(LesVgrupp: info) %xml(navnet: options);


       b_inlr = *off;
       CallP P_BehVgru (d_orec : d_hrec : d_urec : d_job_log : b_inlr);
6.31   endif;

       sisteDato = p_sistDato;
6.32   // hvis w_lest er 0, så var det ingen elementer i XML, dvs tom liste, men det er ok
6.32   if (sisteDato = '' and w_lest = 0);
6.32     sisteDato = %char(%date(): *iso);
6.32   endif;

       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Avslutt program
       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Vi oppsummerer

       if (p_stat = '1');
         p_jsts = 50;
         p_jmld = 'Alvorlig feil oppstod i JV867R_XML';

         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       else;

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = navnet;
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);


         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(1)) + ' ' + %Char(w_ovgn);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(2)) + ' ' + %Char(w_hvgn);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(3)) + ' ' + %Char(w_uvgn);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(4)) + ' ' + %Char(w_ovgu);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(5)) + ' ' + %Char(w_hvgu);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(6)) + ' ' + %Char(w_uvgu);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(7)) + ' ' + %Char(w_ovgl);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(8)) + ' ' + %Char(w_hvgl);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(9)) + ' ' + %Char(w_uvgl);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       endif;



       CallP P_AB710R (p_jnav : p_jbid );

       *inlr = *on;

       /end-free

      *
      * Denne proseduren mottar 5 og 5 Grupper fra XML filen
      *
      *
     P LesVgrupp       B
     D LesVgrupp       pi            10i 0
     D   commArea                    10a
     D   Ogrup                             likeds(Overgruppe)
     D                                     dim(5) const
     D   count                       10u 0 value

     D t               s             10u 0
     D d               s              5i 0
     D e               s              5i 0
     D
      /free

         for t = 1 to count;

           BehOgrup(Ogrup(t));
6.32       w_lest = 1;


         endfor;
         return 0;

      /end-free
     P LesVgrupp       E



      *
      * Behandler 1 overgruppe. Overføre overgruppe data til data struktur
      * og kaller programmet P_BehVgru
     P BehOgrup        B
     D BehOgrup        pi            10i 0
     D   Ogrup                             likeds(Overgruppe) const
     D
     D Hgrup           Ds                  likeds(Hovedgruppe_t)
     D Ugrup           Ds                  likeds(Varegruppe_t)
     D t_teller        s              5i 0
     D t_teller2       s              5i 0
     c
     c
      /free

       OgruInit();
       HgruInit();
       UgruInit();

       d_ogrp_num =  %Int(Ogrup.OvergruppeNr);
6.34   d_obes = %Xlate('': '-': Ogrup.Beskrivelse);
       d_crea = Ogrup.Status.Created;
       d_upda = Ogrup.Status.Updated;
6.35   if d_upda = '';
6.35     d_upda = d_crea;
6.35   endif;

       CallP P_BehVgru (d_orec : d_hrec : d_urec : d_job_log : b_inlr);

       for t_teller = 1 to Ogrup.countHovedgruppe;
         HgruInit();
         UgruInit();
         Hgrup = Ogrup.Hovedgruppe(t_teller);

         d_hgrp_num = %Int(
             %SUBST(Hgrup.HovedgruppeNr:(%Len(Hgrup.HovedgruppeNr)-1):2));
6.34     d_hbes = %Xlate('': '-': Hgrup.Beskrivelse);
         CallP P_BehVgru (d_orec : d_hrec : d_urec : d_job_log : b_inlr);

         for t_teller2 = 1 to Hgrup.countVaregruppe;
           UgruInit();
           Ugrup = Hgrup.Varegruppe(t_teller2);
            d_ugrp_num = %Int(
             %SUBST(Ugrup.VaregruppeNr:(%Len(Ugrup.VaregruppeNr)-2):3));

6.34       d_ubes = %Xlate('': '-': Ugrup.Beskrivelse);
           d_ups1 = Ugrup.PSEPrimar;
           d_ups2 = Ugrup.PSESekundar;
6.33       d_ufri = Ugrup.UNSPSCnr;

           CallP P_BehVgru (d_orec : d_hrec : d_urec : d_job_log : b_inlr);
         endfor;

       endfor;


       p_sistDato = d_upda;

       return 0;

      /end-free

     P BehOgrup        E



      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P OgruInit        B
     D
     D t_teller        s              5i 0
      /free

       d_orec = *blank;
       d_ogrp_num  = *zero;
       d_obes = *blank;

      /end-free
     P OgruInit        E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P HgruInit        B
     D
     D t_teller        s              5i 0
      /free

       d_hrec = *blank;
       d_hgrp_num  = *zero;
       d_hbes = *blank;

      /end-free
     P HgruInit        E

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P UgruInit        B
     D
     D t_teller        s              5i 0
      /free

       d_urec = *blank;
       d_ugrp_num  = *zero;
       d_ubes = *blank;
       d_ups1 = *blank;
       d_ups2 = *blank;
6.33   d_ufri = *blank;

      /end-free
     P UgruInit        E

**
Antall nye overgrupper er:
Antall nye hovedgrupper er:
Antall nye undergrupper er:
Antall oppdaterte overgrupper er:
Antall oppdaterte hovedgrupper er:
Antall oppdaterte undergrupper er:
Antall leste overgrupper er:
Antall leste hovedgrupper er:
Antall leste undergrupper er:
