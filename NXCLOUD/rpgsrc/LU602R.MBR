     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LU602R
      *  Beskrivelse . . : Utskrift av lagertelle-bunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           220900  Nytt program                             ASK
5.70  *  PRGR     021208  Utvidet med prisgruppe                   bof
      *  6.10     170609  Oppdatert med sporingsinformasjon        BSA
6.11  *  6.10     040810  Henter pris fra et annet prishentings-   BSA
6.11  *                   program.
6.12  *  6.10     230412  Hvis div.vare skal verdi hentes fra telling BOF
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  *  6.20     261012  Utvidet vl710r med div. info - rettelse
6.30  * 6.30  030117 bsa  Sender med lager til prisprogram.
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.01  * 7.00  060120 bof  Rettelse på verdi fra telling
8.01  *PRG2   160622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
     1*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     flbhelu    uf   e           k disk    rename(lbhepfr:lbhelur)
     flbdtl1    if   e           k disk    rename(lbdtpfr:lbdtl1r)
     fltell2    if   e           k disk    rename(ltelpfr:ltell2r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     flu602p    o    e             printer
      *
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
     d l_fnav                951    980
     d                 ds
      *
      * Definering av variable i parametre
      *
     d p_prec                        20
     d p_firm                         3  0 overlay(p_prec)
     d p_batc                        10    overlay(p_prec:4)
     d p_numm                         6  0 overlay(p_prec:14)
     d p_kode                         1    overlay(p_prec:20)
      *
     d p_vare          s             15
     d p_lety          s              1  0
     d p_dato          s                   like(lcedat)
     d p_enhe          s              3
     d p_sapr          s              9  2
     d p_gmpr          s              9  2
     d p_kopr          s              9  2
     d p_inpr          s              9  2
6.30 d p_lage          s              2  0
      *
      * Definering av variable i nøkler
      *
     d lbdtl1_batc     s                   like(lvbatc)
     d lbdtl1_line     s                   like(lvline)
     d lbdtl1_numm     s                   like(lvnumm)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
     d ltell2_batc     s                   like(lebatc)
     d ltell2_lage     s                   like(lelage)
     d ltell2_lope     s                   like(ledlop)
     d ltell2_numm     s                   like(lenumm)
     d ltell2_suff     s                   like(lesuff)
     d ltell2_vare     s                   like(levare)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d lbchl1_batc     s                   like(lcbatc)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_kode          s              1
     d w_parm          s             64
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_kopr          s              9  2
     d w_timestamp     s             12  0
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_spny          s                   like(lvspny)
     d w_kpny          s                   like(lvkpny)
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_lety          s              1  0
     d w_inlr          s              1
     d w_time          s               t   timfmt(*iso)
     d w_dato          s               d   datfmt(*iso)
8.01 d w_prgr          s              2
5.70 d w_vtyp          s                   like(vvvtyp)
6.11 d w_inpr          s              9  2
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
      *
6.21 d b_inlr          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Definering av formater
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Benyttede formater
      *  - - - - - - - - - -
      *
      *  A1HDR  - Heading A1 på rapport
      *  A2HDR  - Heading A2 på rapport
      *  D1DET  - Detail  D1 på rapport
      *  T1TOT  - Total   T1 på rapport
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1HDR Behandle heading A1 + A2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent opplysninger fra LAGER-TELLEBUNT-HODE
      *
     c     lbhelu_key    chain     lbhelu                             90
     c                   eval      luskod = 1
     c                   if        *in90 = *off
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
     c                   update    lbhelur
     c                   endif
      *
      *  Hent lager tekst
      *
     c                   exsr      finn_lager
      *
      *  Legger inn opplysninger i utfeltene
      *
     c                   eval      a2batc = lubatc
     c                   eval      a2numm = lunumm
     c                   eval      a2type = lutype
     c                   eval      a2text = lutext
     c                   eval      a2oper = luoper
     c                   eval      a2lage = lulage
     c                   eval      a2user = luuser
     c                   eval      a2wsid = luwsid
     c                   eval      a2latx = w_latx
      *
      * Skrive heading
      *
     c                   write     a1hdr
      *
5.70 c                   exsr      hent_prisgr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  D1DET Behandle file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Lese file
      *
     c     lbdtl1_key    setll     lbdtl1
     c                   read      lbdtl1                                 15
      *
     c                   dow       *in15 = *off
      *
      * Hopper ut dersom ikke samme bunt-nummer
      *
     c                   if        lbdtl1_batc <> lvbatc
     c                   leave
     c                   endif
     c                   if        lbdtl1_numm <> lvnumm
     c                   leave
     c                   endif
      *
      *  Legger inn opplysninger i utfeltene
      *
     c                   exsr      hent_pris
      *
     c                   eval      d1line = lvline
     c                   eval      d1tlin = lvtlin
     c                   movel     lvvare        d1vare
     c                   eval      d1tek1 = lvtek1
     c                   eval      d1antt = lvantt
     c                   eval      d1enhl = lvenh1
     c                   eval      d1spny = w_spny
     c                   eval      d1kpny = w_kpny
      *
      *
      *  Hent innformasjon fra LAGER-TELLE-VARE-REGISTER
      *
     c                   if        d1spny = 0
     c                             or d1kpny = 0
     c                   eval      ltell2_vare = lvvare
     c                   eval      ltell2_lage = lulage
     c                   eval      ltell2_lope = 0
     c     ltell2_key    chain     ltell2                             90
     c                   if        *in90 = *off
     c                   if        d1spny = 0
     c                   eval      d1spny = lespny
     c                   endif
     c                   if        d1kpny = 0
     c                   eval      d1kpny = lekpny
     c                   endif
     c                   endif
     c                   endif
      *
      *  Hent opplysninger fra VARE-REGISTER
      *
     c                   eval      vvarl1_vare = lvvare
     c     vvarl1_key    chain     vvarl1r                            90
     c                   if        d1tek1 = *blank
     c                   movel     vvtek1        d1tek1
     c                   endif
      *
     c                   if        vvenhl = *blank
     c                   eval      vvenhl = vvenhe
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      d1tek1 = '** Finnes ikke i vareregister!'
     c                   eval      d1enhl = *blank
     c                   goto      skriv_ut
     c                   endif
      *
     c                   eval      w_lety = 0
     c                   eval      w_inlr = ' '
     c                   move      udate         w_dato
      *
      * Henter og beregner prisgrunnlag fra vareregisteret
      *
6.11 c**                 call      'VP700R'
6.11 c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    lvvare
5.70 c                   parm                    w_prgr
     c                   parm                    vvenhl
     c                   parm                    w_lety
     c                   parm                    w_dato
6.11 c**                 parm                    w_time
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
6.11 c**                 parm                    w_tipr
     c                   parm                    w_kopr
6.11 c                   parm                    w_inpr
      *
     c                   if        d1enhl = *blank
     c                   movel     vvenhl        d1enhl
     c                   endif
      *
     c                   if        d1spny = 0
     c                   eval      d1spny = w_sapr
     c                   endif
     c                   if        d1kpny = 0
     c                   eval      d1kpny = w_kopr
     c                   endif
6.12  *
6.12  *  Hvis pris fortsatt er 0 og det er div.vare hentes
6.12  *  priser fra telling
6.12  *
6.12  * henter varetype
6.12  *
6.12 c                   call      'VL710R'
6.12 c                   parm                    w_firm
6.12 c                   parm                    lvvare
6.12 c                   parm                    lulage
6.12 c                   parm                    w_vtyp
6.22 c                   parm                    w_udat
6.22 c                   parm                    w_ldor
6.22 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
6.12  *
6.12 c                   if        w_vtyp = 'D'
7.01 c                   if        lvspny <> 0
7.01 c                             and lvkpny <> 0
6.12 c                   eval      d1spny = lvspny
6.12 c                   eval      d1kpny = lvkpny
6.12 c                   endif
6.12 c                   endif
      *
      *  Sjekk, Om enhet er lik lagerenhet fra vare-register,               et
      *  hvis ikke bemerk det. Gjelder ikke diverse-varer.
      *
     c                   exsr      omr_antall
      *
      *  Kontroll-liste eller Kvitteringsliste
      *
     c                   eval(h)   d1spsu = d1spny * d1antt
     c                   eval(h)   d1kpsu = d1kpny * d1antt
      *
      *  Behandle record
      *
     c     skriv_ut      tag
     c                   eval      t1post = t1post + 1
     c                   eval(h)   t1spsu = t1spsu + d1spsu
     c                   eval(h)   t1kpsu = t1kpsu + d1kpsu
     c                   write     d1det                                30
      *
      *  Er det overflow ?
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   read      lbdtl1                                 15
     c                   enddo
      *
     c     xend01        tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  T1TOT Behandle total T1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Skrive total
      *
     c                   write     t1tot                                30
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall fra opptalt til lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Dersom opptalt enhet er lik lagerenhet beholdes antall
      *
     c                   eval      d1anta = 0
     c                   eval      d1enht = *blank
     c                   eval      d1omrf = 0
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      d1merk = *blank
      *
6.12 c*                  call      'VL710R'
6.12 c*                  parm                    w_firm
6.12 c*                  parm                    lvvare
6.12 c*                  parm                    lulage
6.12 c*                  parm                    w_vtyp
6.21 c*                  parm                    w_udat
6.21 c*                  parm                    w_ldor
6.21 c*                  parm                    b_inlr
      *
      *
5.70 c                   if        w_vtyp = 'D'
     c                   goto      end_omr
     c                   endif
      *
     c                   if        d1enhl = vvenhl
     c                   goto      end_omr
     c                   endif
      *
     c                   eval      d1merk = '*'
     c                   eval      *in42 = *on
      *
      * Finner omregningsfaktor volum for lagerenhet og for opptalt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = lvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 90
     c                   dow       *in90 = *off
     c                   if        d1enhl = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 90
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   eval      d1enhl = vvenhl
     c                   eval      d1enht = lvenh1
     c                   eval      d1anta = lvantt
      *
     c                   if        w_omrl <> 0
     c                   eval      d1antt = lvantt * w_omrl / w_omrs
     c                   eval      d1omrf = w_omrl
     c                   eval      *in43 = *on
     c                   eval      *in42 = *off
     c                   else
     c                   eval      d1antt = 0
     c                   endif
      *
     c     end_omr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke lager/hente lagertekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lager    begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    lulage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c     lager_end     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente priser på lagerenhet ved telleslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      p_lety = 0
     c                   eval      p_vare = lvvare
6.30 c                   eval      p_lage = lulage
      *
     c                   call      'VP730R'
     c                   parm                    w_firm
     c                   parm                    p_vare
5.70 c                   parm                    w_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_gmpr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
6.30 c                   parm                    p_lage
      *
     c                   eval      w_spny = p_sapr
     c                   eval      w_kpny = p_kopr
      *
     c     pris_end      endsr
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lulage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av VARE-ENHET
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      *  Key for les av LAGER-TELLE-VARE
      *
     c     ltell2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell2_batc
     c                   kfld                    ltell2_numm
     c                   kfld                    ltell2_suff
     c                   kfld                    ltell2_vare
     c                   kfld                    ltell2_lage
     c                   kfld                    ltell2_lope
      *
      *  Key for oppdatering LAGERBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for les av LAGERBUNT-LINJE
      *
     c     lbdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl1_batc
     c                   kfld                    lbdtl1_numm
     c                   kfld                    lbdtl1_line
      *
      * Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   parm                    w_kode
      *
     c                   eval      p_prec = w_parm
      *
     c                   eval      a1wsid = l_wsid
     c                   eval      a1user = l_user
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      *  Legge inn key
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      ltell2_batc = p_batc
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbdtl1_batc = p_batc
     c                   eval      lbchl1_batc = p_batc
     c                   eval      ltell2_numm = p_numm
     c                   eval      lbhelu_numm = p_numm
     c                   eval      lbdtl1_numm = p_numm
     c                   eval      lbdtl1_line = 0
      *
      *  Hente informasjon fra batch
      *
     c     lbchl1_key    chain     lbchl1
     c                   if        %found
     c                   eval      p_dato = lcbdat
     c                   endif
      *
      *  Kontroll-liste eller Kvitteringsliste
      *
     c                   eval      *in41 = *off
     c                   if        w_kode <> *blank
     c                   eval      *in41 = *on
     c                   endif
      *
     c                   endsr
