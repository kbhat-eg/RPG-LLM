     H DFTACTGRP(*NO)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV863R_XML
      * Beskrivelse . . : Leser NOBB Pris  XML 8.0. Legger data i pris variable.
      *                   Kaller standard rpg program, som gjør sjekk av data
      *                   og oppdatere basen
      *
      *                   Dette programmet er skrevet i free format RPG
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV  Nytt program
6.30  * R6.30 250414 BKV  Bedre logging
6.31  * R6.30 300614 BKV  Korrigering av parameter def
6.32  * R6.30 130814 BKV  Angi xpath i basen
6.33  * R6.30 061014 BKV  Ok hvis ingen moduler, dvs retur dagens dato
6.34  * R6.30 150515 BKV  d_lvar økt til 20
6.35  * R6.30 150515 BKV  rettet feil i job_log størrelse
6.36  * 6.30  250918 bkv  Utvidet med trelast sortiment
8.01  *       221122 bof  Nullstille tildato + endret loop for å behandle
      *                   flere priser.
8.02  *       291122 bof  Test på om vare er undervare før man kaller JK101R
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.32 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.36 fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
      *
      * Definering av Local Data Area
      *
6.32 d                uds
6.32 d l_user                911    920
6.32 d l_firm                944    946  0
6.32 d l_filg                931    933
      *
      * Definering av variabler i nøkler
      *
6.32 d afpslr_ufil     s                   like(l_filg)
6.32 d afpslr_uide     s                   like(afuide)
6.36 d jvkhl1_kvnr     s                   like(jvkvnr)
      *
6.36 d w_firm          s                   like(jvkfir)
8.02 d w_anta          s              6  0
     d p_stat          s              1
      * Brukes som parametre til jobblogging
     d p_jnav          s             15
     d p_jbid          s             41
     d p_jtxt          s             35
     d p_jsts          s              2  0
     d p_jmld          s            200
      *
     d p_sistDato      s             10a
6.33 d w_lest          s              1  0 Inz(0)
      *
      * Definering av tabell med meldinger
6.30 d a_meld          s            100    dim(5) ctdata perrcd(1)
      *
      *  Konstanter
      *
     d c_pris_max      c                   9999999,99
      *
      * START NOBB 8.0 XML definisjon
      *
      * Path til pris  taggen
     D pris_path       s             31a   Inz(
     D                                     'PrisResult/PrisListe/Pris')

      * Taggen <b:Pris>
     D Pris_t          DS                  Qualified
     D   NobbNr...
     D                                8a   varying
     D   NOBBPris                          LikeDS(NOBBPris_t) Dim(5)
     D   countNOBBPris...
     D                                5i 0
     D   DeltagerNr...
     D                                8a   varying
     D   ValutaKode...
     D                                5a   varying
     D   PrisGiversVareNr...
     D                               30a   varying
     D   PrisEnhet...
     D                                5a   varying
     D   Status                            LikeDS(Status_t)

      * Taggen: <b:NOBBPris>
     D NOBBPris_t      DS                  Qualified
     D                                     based(Template)
     D   Pris...
     D                               12a
     D
     D   PrisType                    20a
     D   PrisDatoFOM...
     D                               10a
     D   PrisDatoTOM...
     D                               10a

      * Taggen: <b:Status>
     D Status_t        DS                  Template
     D   Created                     25a
     D   Updated                     25a

      *
      * STOPP NOBB 8.0 XML definisjon
      *

      *
      *  Dataelementer pris
      *
     d                 ds
     d d_prec                       200
     d  d_stat                        1    overlay(d_prec:2)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_ldor                        6    overlay(d_prec:3)
     d   d_ldor_num                   6  0 overlay(d_ldor:1)
6.34 d  d_lvar                       20    overlay(d_prec:9)
6.34 d  d_fdat                       10    overlay(d_prec:29)
6.34 d   d_fdat_iso                    d   overlay(d_fdat:1) datfmt(*iso)
6.34 d  d_tdat                       10    overlay(d_prec:39)
6.34 d   d_tdat_iso                    d   overlay(d_tdat:1) datfmt(*iso)
6.34 d  d_nobp                       12    overlay(d_prec:49)
6.34 d   d_nobp_num                   9  2 overlay(d_nobp:1)
6.34 d  d_valu                        3    overlay(d_prec:61)
6.34 d  d_eann                       13    overlay(d_prec:64)
6.34 d   d_eann_num                  13  0 overlay(d_eann:1)
6.34 d  d_noda                       10    overlay(d_prec:77)
6.34 d   d_noda_iso                    d   overlay(d_noda:1) datfmt(*iso)
6.34 d  d_neda                       10    overlay(d_prec:87)
6.34 d  d_odat                       10    overlay(d_prec:97)
6.34 d  d_edat                       10    overlay(d_prec:107)
6.34 d  d_etim                       10    overlay(d_prec:117)
6.34 d  d_nobb                        8    overlay(d_prec:127)
6.34 d   d_nobb_num                   8  0 overlay(d_nobb:1)
6.34 d  d_crea                       10    overlay(d_prec:135)
6.34 d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
6.34 d  d_upda                       10    overlay(d_prec:145)
6.34 d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
      *
      * job_log
      *
6.30 d d_job_log                     70
     d  w_ojprt                       5    overlay(d_job_log:2)
     d  w_ojpr                        5  0 overlay(w_ojprt:1)
     d  w_jvprt                       5    overlay(d_job_log:7)
     d  w_jvpr                        5  0 overlay(w_jvprt:1)
     d  w_jbid                       41    overlay(d_job_log:12)
6.30 d  w_avprt                       5    overlay(d_job_log:53)
6.30 d  w_avpr                        5  0 overlay(w_avprt:1)
6.30 d  w_lnr                         5  0 overlay(d_job_log:58)
      *
     d b_inlr          s              1
6.30 d w_topr          s              5  0
      *
      * Prototype for procedure LesPriser
      * Leser inn 50 priser, som blir behandlet av procduren LesPriser
     D LesPriser       PR            10i 0
     D   commArea                    10a
     D   Pris                              likeds(Pris_t)
     D                                     dim(50) const
     D   count                       10u 0 value

      * info og commArea må være likt def. De brukes ikke i programmet, men må
      * være definert
     D info            s             10a
     D options         s            200a   varying

      * Prototype for externt program sjekker Pris og lagrer i basen
      *  Overfører vara datastruktur som parameter
     D P_BehPris       PR                  ExtPgm('JV866R_XML')
     d  d_prec                      200
6.35 d  d_job_log                    70
     d  b_inlr                        1

6.36  * Prototype for externt program oppdatere logistikk vare
6.36 D P_OppLogi       PR                  ExtPgm('JK101R')
8.02 d  p_nobb                        8

     D P_AB705R        PR                  ExtPgm('AB705R')
     d  p_jbid                       41
     d  p_jsts                        2  0
     d  p_jmld                      200

     D P_AB710R        PR                  ExtPgm('AB710R')
     d  p_jnav                       15
     d  p_jbid                       41


     D P_AB700R        PR                  ExtPgm('AB700R')
     d  p_jnav                       15
     d  p_jbid                       41
     d  p_jtxt                       35

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // ------------------------------------- Prototypes
     D JV865R_XML      PR                  ExtPgm('JV865R_XML')
     D   filNavn                    255a   options(*varsize)
6.31 D   lengde                       3p 0 const
     D   sisteDato                   10a
       // ----------------------- Main procedure interface
     D JV865R_XML      PI
     D   filNavn                    255    options(*varsize)
6.31 D   lengde                       3p 0 const
     D   sisteDato                   10a

     D   navnet        s            255a   varying
6.31 D   len           s              3i 0

6.32  *
6.32  * Key for propertiesfil
6.32  *
6.32 c     afpslr_key    klist
6.32 c                   kfld                    afpslr_ufil
6.32 c                   kfld                    afpslr_uide
6.36  *
6.36  * Key for les av logistikkvare
6.36  *
6.36 c     jvkhl1_key    klist
6.36 c                   kfld                    w_firm
6.36 c                   kfld                    jvkhl1_kvnr

      /free

6.36   Exec SQL
6.36     Set Option DatFmt=*ISO;


6.30   w_firm = l_firm;

       len = lengde;
       navnet = %trim(%subst(filNavn:1:len));

       // Vi starter jobblogging
       p_jnav = 'PRISXML';
       p_jtxt = *blank;
       CallP P_AB700R (p_jnav : p_jbid : p_jtxt);

       w_jbid = p_jbid;
       w_jvpr = *zeros;
       w_ojpr = *zeros;
6.30   w_avpr = *zeros;
6.30   w_topr = 0;
       w_lnr = 0;
6.33   w_lest = 0;


6.32   //  Hent inn NOBB 80 XML path som skal brukes
6.32   afpslr_ufil = l_filg;
6.32   afpslr_uide = 'NOBB80PRI';
6.32   chain afpslr_key afpslrr;
6.32   if %found;
6.32     pris_path = *blanks;
6.32     pris_path = %trim(afudss);
6.32   else;
6.32     p_stat = '1';
6.32   endif;

6.32   if (p_stat <> '1');

         b_inlr = *on;
         options = 'doc=file +
                    case=any +
                    countprefix=count +
                    datasubf=text +
                    ns=remove +
                    path='+pris_path+' '+
                   'allowmissing=yes +
                    allowextra=yes';

          // Bruker RPG xml-info til å lese xml filen.
          // Behandling av innleste priser gjøres av LesPriser
          xml-into %handler(LesPriser: info) %xml(navnet: options);

       b_inlr = *off;
       CallP P_BehPris (d_prec : d_job_log : b_inlr);

8.02   // kall flyttet
8.02   //CallP P_OppLogi();


6.32   endif;

       sisteDato = p_sistDato;

6.33   // hvis w_lest er 0, så var det ingen elementer i XML, dvs tom liste, men det er ok
6.33   if (%trim(sisteDato) = '' and w_lest = 0);
6.33     sisteDato = %char(%date(): *iso);
6.33   endif;

       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Avslutt program
       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Vi oppsummerer

       if (p_stat = '1');
         p_jsts = 50;
         p_jmld = 'Alvorlig feil oppstod i JV865R_XML';

         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       else;

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(1)) + ' ' + %Char(w_jvpr);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(2)) + ' ' + %Char(w_ojpr);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

6.30     p_jsts = 10;
6.30     p_jmld = *blanks;
6.30     p_jmld = %trim(a_meld(4)) + ' ' + %Char(w_avpr);
6.30     CallP P_AB705R (p_jbid : p_jsts : p_jmld);

6.30     p_jsts = 10;
6.30     p_jmld = *blanks;
6.30     p_jmld = %trim(a_meld(5)) + ' ' + %Char(w_lnr);
6.30     CallP P_AB705R (p_jbid : p_jsts : p_jmld);


6.30     p_jsts = 10;
6.30     p_jmld = *blanks;
6.30     p_jmld = %trim(a_meld(3)) + ' ' + %Char(w_topr);
6.30     CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       endif;


       CallP P_AB710R (p_jnav : p_jbid );

       *inlr = *on;

       /end-free

      *
      * Denne proseduren mottar 5 og 5 Priser fra XML filen
      *
      *
     P LesPriser       B
     D LesPriser       pi            10i 0
     D   commArea                    10a
     D   Priser                            likeds(Pris_t)
     D                                     dim(50) const
     D   count                       10u 0 value

     D t               s             10u 0
      /free

         for t = 1 to count;

           BehPris(Priser(t));
6.36   //    w_lest = 1;

         endfor;
         return 0;

      /end-free
     P LesPriser       E
      *
      * Behandler 1 pris. Overføre pris data til data struktur
      * og kaller programmet P_BehPris
     P BehPris         B
     D BehPris         pi            10i 0
     D   Pris                              likeds(Pris_t) const
     D
     D t               s              5i 0
      /free

       PrisInit();

       d_nobb_num =  %Int(Pris.NobbNr);

6.36  // sjekk om det er logistikkvare
6.36   jvkhl1_kvnr = d_nobb;
6.36   chain jvkhl1_key jvkhl1;
6.36   if (%found(jvkhl1) and (jvknup = 0));
6.36     return 0;  // skal ikke oppdateres fra NOBB
6.36   endif;

6.36   w_lest = 1;

       d_ldor_num = %Int(Pris.DeltagerNr);
       for t = 1 to Pris.countNOBBPris;
         if (Pris.NOBBPris(t).PrisType = 'NOBBIndeksPris');
           if (%Trim(Pris.NOBBPris(t).PrisDatoFOM) <> '');
            d_fdat = Pris.NOBBPris(t).PrisDatoFOM;
           endif;
           if (%Trim(Pris.NOBBPris(t).PrisDatoTOM) <> '');
             d_tdat = Pris.NOBBPris(t).PrisDatoTOM;
8.01       else;
8.01         d_tdat = '0001-01-01';
           endif;
           d_nobp_num  = SjekkPris(Pris.NOBBPris(t).Pris:c_pris_max);
         endif;
8.01   //endfor;
       if (%Trim(Pris.ValutaKode)<>'');
         d_valu = Pris.ValutaKode;
       endif;
       if (%Trim(Pris.PrisGiversVareNr)<>'');
         d_lvar = Pris.PrisGiversVareNr;
       endif;
       d_crea = Pris.Status.Created;
       if (%Trim(Pris.Status.Updated)<>'');
         d_upda = Pris.Status.Updated;
       endif;

       CallP P_BehPris (d_prec : d_job_log : b_inlr);
8.01   endfor;
6.30   w_topr += 1;

8.02   //sjekk om undervare i logistikk, da skal program kalles
8.02   w_anta = 0;
8.02 c/exec sql
8.02 c+ select count(*)
8.02 c+ into   :w_anta
8.02 c+ from   jvklpf
8.02 c+ where  jvquno = :d_nobb
8.02 c/end-exec
8.02   if        w_anta > 0;
8.02   CallP P_OppLogi(d_nobb);
8.02   endif;

       p_sistDato = d_upda;

       return 0;

      /end-free

     P BehPris         E
      *
      * Sjekk Pris
      *
     P SjekkPris       B
     D SjekkPris       pi            16  6
     D   tall                        15a   const
     D   max                          9  2 const
     D
     D   desimal       s             20  6
      /free

         desimal = *zero;

         if (%Trim(tall) <> '') ;

           desimal = %Dech(tall:20:6);
           if ((desimal ) > max);
             desimal = *zero;
             return desimal;
           endif;

           desimal = %Dech(tall:9:2);

         endif;

       return desimal;

      /end-free
     P SjekkPris       E
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P PrisInit        B
     D
     D t               s              5i 0
      /free

       d_prec = *blank;
       d_ldor_num = *zero;
       d_fdat_iso = *loval;
       d_tdat_iso = *loval;
       d_crea_iso = *loval;
       d_upda_iso = *loval;
       d_nobp_num  = *zero;
       d_valu = *blank;
       d_lvar = *blank;
      /end-free
     P PrisInit        E
**
Antall nye priser er:
Antall oppdaterte priser er:
Antall priser i fil:
Antall avvist priser:
Antall priser mangler vare:
