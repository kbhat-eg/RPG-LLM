     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV86AR_XML
      * Beskrivelse . . : Oppdater fra NOBB, vare leverandør
      *
      *                   Oppdaterer/oppretter varer leverandør
      *                   NOBB 80 (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150714 BKV  Nytt program
6.30  * R6.30 050216 BKV  får med varenummer i parameter(NOBB/NIB)
6.31  * R6.30 070416 BKV  Logge utgåtte vare-leverandør
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
     f                                     infsr(FileErr)
     fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
     f                                     infsr(FileErr)
     fjvlmlu    uf a e           k disk    rename(jvlmpfr:jvlmlur)
     f                                     infsr(FileErr)
     fjvlml1    uf a e           k disk    rename(jvlmpfr:jvlml1r)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
      * Definering av tabell med meldinger
     d a_meld          s            100    dim(1) ctdata perrcd(1)
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
      * Brukes som parametre til jobblogging
      *
     d p_jbid          s             41
     d p_jmld          s            200
     d p_jsts          s              2  0
      *
      * Parameter
      *
     d p_lrec          s            200
     d p_lmrec         s            120    dim(25)
6.31 d p_job_log       s            150
     d p_inlr          s              1
      *
     d c_ant_media     c                   25
      *
      * Job log out
      *
     d                 ds
6.31 d d_job_log                    150
     d  w_vare                        8    overlay(d_job_log:2)
     d  w_ojva                        8    overlay(d_job_log:10)
     d   w_ojva_num                   8  0 overlay(w_ojva:1)
     d  w_jvar                        8    overlay(d_job_log:18)
     d   w_jvar_num                   8  0 overlay(w_jvar:1)
     d  w_jvdt                        8    overlay(d_job_log:26)
     d   w_jvdt_num                   8  0 overlay(w_jvdt:1)
     d  w_jvpk                        8    overlay(d_job_log:34)
     d   w_jvpk_num                   8  0 overlay(w_jvpk:1)
     d  w_jvpg                        8    overlay(d_job_log:42)
     d   w_jvpg_num                   8  0 overlay(w_jvpg:1)
     d  w_jvpke                       8    overlay(d_job_log:50)
     d   w_jvpke_num                  8  0 overlay(w_jvpke:1)
     d  w_jvpku                       8    overlay(d_job_log:58)
     d   w_jvpku_num                  8  0 overlay(w_jvpku:1)
     d  w_jvpru                       8    overlay(d_job_log:66)
     d   w_jvpru_num                  8  0 overlay(w_jvpru:1)
     d  w_jvlevn                      8    overlay(d_job_log:74)
     d   w_jvlevn_num                 8  0 overlay(w_jvlevn:1)
     d  w_jvleve                      8    overlay(d_job_log:82)
     d   w_jvleve_num                 8  0 overlay(w_jvleve:1)
6.31 d  w_jvlevu                      8    overlay(d_job_log:90)
6.31 d   w_jvlevu_num                 8  0 overlay(w_jvlevu:1)
6.31 d  w_lnr                         5  0 overlay(d_job_log:98)
6.31 d  w_jbid                       41    overlay(d_job_log:103)
      *
      * Definering av variable
      *
     d n               s              2  0
     d w_meld          s            100
      *
      *  Dataelementer leverandør
      *
     d                 ds
     d d_lrec                       200
     d  d_deltakerNummer...
     d                                6a   overlay(d_lrec)
     d   d_del_num                    6  0 overlay(d_deltakerNummer:1)
     d  d_navn                      100a   overlay(d_lrec:7)
     d  d_erVareeier...
     d                                1    overlay(d_lrec:107)
     d    d_er_eier                   1  0 overlay(d_erVareeier:1)
     d  d_vareNummer                 20a   overlay(d_lrec:108)
     d  d_UtgarDato                  10a   overlay(d_lrec:128)
     d   d_UtgarD_iso                  d   overlay(d_UtgarDato:1) datfmt(*iso)
     d  d_merking1                   15a   overlay(d_lrec:138)
     d  d_merking2                   15a   overlay(d_lrec:153)
     d  d_merking3                   15a   overlay(d_lrec:168)
     d  d_vareL                       8    overlay(d_lrec:183)
     d   d_vare_numL                  8  0 overlay(d_vareL:1)
     d  d_nobbL                       8    overlay(d_lrec:191)
     d   d_nobb_numL                  8  0 overlay(d_nobbL:1)
      *
      *  Dataelementer Leverandør Media
      *
     d                 ds
     d d_lmrec                      120
     d  d_mediaType                  10a   overlay(d_lmrec)
     d  d_mediaUrl                  100a   overlay(d_lmrec:11)

      *
      * Definering av variable i nøkler
      *
     d jvlel1_lvnr     s                   like(jvlvnr)
     d jvlel1_lldo     s                   like(jvlldo)
     d jvlelu_lvnr     s                   like(jvlvnr)
     d jvlelu_lldo     s                   like(jvlldo)
     d jvlmlu_mvnr     s                   like(jvmvnr)
     d jvlmlu_mldo     s                   like(jvmldo)
      *
      *
      * Definering av variable
      *
     d b_vfel          s              1    inz(*off)
     d w_firm          s                   like(jaafir)
      *
      *
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c                   eval      d_job_log = p_job_log
     c                   eval      p_jbid = w_jbid
     c                   if        p_inlr = *on
     c                   eval      d_lrec = p_lrec
     c
     c                   if        (d_nobb_numL <> *zero)
     c                   exsr      skr_leve
     c                   endif
     c                   endif
     c
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c
     c                   return
     c
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer vare leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_leve      begsr
      *
     c                   if        b_vfel = *on
     c                   leavesr
     c                   endif
     c
     c                   clear                   jvlelur
     c
6.30 c                   eval      jvlel1_lvnr = d_vareL
     c                   eval      jvlel1_lldo = d_del_num
     c
     c     jvlel1_key    chain     jvlel1
     c
      *
      * Oppretter eller oppdaterer vare leverandør
      *
6.30 c                   eval      jvlelu_lvnr = d_vareL
     c                   eval      jvlelu_lldo = d_del_num
     c     jvlelu_key    chain     jvlelur
      *
6.30 c                   eval      jvlvnr = d_vareL
     c                   eval      jvlldo = d_del_num
     c                   eval      jvlvei = d_er_eier
     c                   eval      jvllva = d_vareNummer
     c                   eval      jvlmr1 = d_merking1
     c                   eval      jvlmr2 = d_merking2
     c                   eval      jvlmr3 = d_merking3
     c                   eval      jvluda = d_UtgarD_iso
      *
     c                   eval      jvleus = l_user
      *
     c                   time                    jvleda
     c                   time                    jvleti
      *
     c                   if        %found(jvlelu)
     c                   eval      w_jvleve_num = w_jvleve_num + 1
     c                   update    jvlelur
     c                   else
     c                   eval      jvlous = l_user
     c                   time                    jvloda
     c                   time                    jvloti
     c                   eval      w_jvlevn_num = w_jvlevn_num + 1
     c                   write     jvlelur
     c                   endif
     c
      * slette evt media koblinger og legger inn på nytt
     c                   exsr      del_lemed
     c                   for       n = 1 to c_ant_media
     c                   eval      d_lmrec = p_lmrec(n)
     c                   if        (d_mediaType = *blank)
     c                   leave
     c                   endif
     c                   exsr      skr_lemed
     c                   endfor
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling  av Leverandør Media
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_lemed     begsr
      *
     c                   clear                   jvlmlur
     c
6.30 c                   eval                    jvmvnr = d_vareL
     c                   eval                    jvmldo = d_del_num
     c                   eval                    jvmtyp = d_mediaType
     c                   eval                    jvmurl = d_mediaUrl
     c                   eval                    jvmous = l_user
     c                   eval                    jvmeus = l_user
      *
     c                   time                    jvmoda
     c                   time                    jvmoti
     c                   time                    jvmeda
     c                   time                    jvmeti
      *
     c                   write     jvlmlur
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av Leverandør Media
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     del_lemed     begsr
      *
      * Sletter tilhørende media
      *
6.30 c                   eval      jvlmlu_mvnr = d_vareL
     c                   eval      jvlmlu_mldo = d_del_num
     c     jvlmlu_key    setll     jvlmlur
     c     jvlmlu_key    reade     jvlmlur                                91
     c                   dow       *in91 = *off
     c                   delete    jvlmlur
     c     jvlmlu_key    reade     jvlmlur                                91
     c                   enddo
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr

     c
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_lrec
     c                   parm                    p_lmrec
     c                   parm                    p_job_log
     c                   parm                    p_inlr
     c
      *
      * Key for oppslag på vare leverandør
      *
     c     jvlel1_key    klist
     c                   kfld                    jvlel1_lvnr
     c                   kfld                    jvlel1_lldo
     c
      *
      * Key for oppdatering av vare leverandør
      *
     c     jvlelu_key    klist
     c                   kfld                    jvlelu_lvnr
     c                   kfld                    jvlelu_lldo
      *
      * Key for oppdatering av leverandør media
      *
     c     jvlmlu_key    klist
     c                   kfld                    jvlmlu_mvnr
     c                   kfld                    jvlmlu_mldo
      *
      * Key for oppslag på leverandør media
      *
     c*    jvlml2_key    klist
     c*                  kfld                    jvlml2_mvnr
     c*                  kfld                    jvlml2_mldo
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
**
NOBBNummer er ikke opprettet i JVARPF. NOBBnr. :
