     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . :
      *  Program . . . . : AX002R
      *  Beskrivelse . . : KONTOPLAN Legger ut XML til SAF-T
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
Versj *Uts   Dato Usr Referanse Beskrivelse
      *____________________________________________________________________________________________
      *K00-200420               START FOR VERSJON K00
7.00  *K00 200420 SST NXS-4426  Utrulling av SAFT-Rapporter til alle kundene
7.01  *K00 051120 sst NXS-6143  Erstatte § med P ut i fila
7.02  *K00 031121 sst NXS_10680 Utvidet saldofelter for mgr
      *
      *
      *____________________________________________________________________________________________
      *
      *
      *  Dette programmet henter data fra ADBxxx/RHOVPF m/RHFIRM = Firma
      *                                og ADBxxx/RHSAPF m/RSFIRM = Firma
      *
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhstl1    if   e           k disk    rename(rhstpfr:rhstl1r)
     frxmll1    if   e           k disk    rename(rxmlpfr:rxmll1r)
      *
     faxmlut    o    e             disk
      *
      *
     d w_firm          s                       like(rhfirm)
      *
     d rhovl1_kont     s                       like(rhkont)
     d rhovl1_spes     s                       like(rhspes)
     d rhovl1_avde     s                       like(rhavde)
      *
     d rhsal1_kont     s                       like(rikont)
     d rhsal1_spes     s                       like(rispes)
     d rhsal1_avde     s                       like(riavde)
     d rhsal1_raar     s                       like(riraar)
     d rhsal1_type     s                       like(ritype)
     d rhstl1_kont     s                       like(shkont)
      *
     d rxmll1_head     s                       like(axhead)
     d rxmll1_linj     s                       like(axlinj)
      *
     d text            s             50
     d txt2            s             50
     d i               s              9S 0
     d j               s              9S 0
     d f               s              3
     d t               s              3
     d fn              s              3S 0
     d tn              s              3S 0
     d s_head          s                   like(axhead)
     d s_linj          s                   like(axlinj)
     d s_kont          s                   like(rhkont)
     d s_kontx         s              5
     d w_iso_dat       s               d   datfmt(*iso)
     d w_x_dat         s             10A
     d w_salix         s             14
     d w_salux         s             14
     d w_02            s              2S 2
7.02 d w_s             s             13S 2
     d w_110           s             11S 0
     d w_110x          s             11
     d w_02x           s              2
7.02 d w_saldoi        s             13S 2
7.02 d w_saldou        s             13S 2
      *
     d w_str_i         s            512
     d w_str_u         s            512
     d w_lr            s              1
     d w_posi          s              2  0
     d w_posu          s              2  0
     d w_str_x         s              1
     d w_ms1           s             50
     d w_ms2           s             50
     d w_svar          s              1
     d w_f3            s              1  0
     d w_avbryt        s              1
     d c_apos          c                   x'7D'
      *
     d p_perfm         s              2
     d p_perfa         s              4
     d p_pertm         s              2
     d p_perta         s              4
     d p_pfm           s              2S 0
     d p_pfa           s              4S 0
     d p_ptm           s              2S 0
     d p_pta           s              4S 0
     d p_hcom          s             50
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Start
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     start         tag
      *
     c                   read      rxmll1
     c                   if        not %eof(rxmll1 )  and
     c                             axhead = 'KONTOPLAN'
      *
      *  Hvis type er merket med 'X' er ikke denne linja klargjort enda
      *
     c                   if        axtype = 'X'
     c                   goto      lesnext
     c                   endif
      *
      *  Egen logikk for håndtering av loop
      *
     c                   if        axmaop = 'S'
     c                   exsr      LoopA
     c                   goto      start
     c                   endif
      *
      *  Sjekk om bare xml linja skal ut
      *
     c                   if        axtype = 'F'
     c                   eval      xmlrec = axxmlt
     c                   goto      next
     c                   endif
      *
      *  Skal det hentes inn et variabelt felt?
      *
     c                   eval      i = %scan('?V?':axxmlt)
     c                   if        i > 0
      *
      *  Dagens dato hentes dersom axtype = 'C' (current)
      *
     c                   if        axtype = 'C'
     c     *dmy          move      udate         w_iso_dat
     c                   move      w_iso_dat     w_x_dat
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_x_dat) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      next
     c                   endif
      *
      *  Hente inn tekst felt
      *
     c                   if        axtype = 'T'
     c                   eval      text = text
      *
     c                   if        %subst(axfast:1:1) = '('
     c                   eval      j = %scan(',':axfast)
     c                   if        j > 0
     c                   eval      f = %subst(axfast:2:3)
     c                   eval      t = %subst(axfast:6:3)
     c                   move      f             fn
     c                   move      t             tn
     c                   eval      txt2 = %subst(text:fn:tn-fn+1)
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(txt2) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      next
     c                   endif
     c                   endif
      *
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn numerisk felt
      *
     c                   if        axtype = 'N'
     c                   eval      text = text
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn dato felt
      *
     c                   if        axtype = 'D'
      *
     c                   eval      w_x_dat = text
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_x_dat) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
      *  Hente inn parameter via p_xxxxx felt fra axfelt
      *
     c                   if        axtype = 'P'
     c                   eval      text = *blank
     c                   select
     c                   when      axfelt = 'P_PERFM'
     c                   movel     p_perfm       text
     c                   when      axfelt = 'P_PERFA'
     c                   movel     p_perfa       text
     c                   when      axfelt = 'P_PERTM'
     c                   movel     p_pertm       text
     c                   when      axfelt = 'P_PERTA'
     c                   movel     p_perta       text
     c                   when      axfelt = 'P_HCOM'
     c                   movel     p_hcom        text
     c                   when      axfelt = 'L_USER'
     c                   movel     l_user        text
     c                   endsl
      *
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   endif
      *
     c                   goto      next
      *
      *  END ?V?
      *
     c                   endif
      *
      *  Sjekk om faste felter skal legges ut
      *
     c                   eval      i = %scan('?F?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      next
     c                   endif
      *
      *  Sjekk xml linja skal ut med parameter
      *
     c                   if        axtype = 'F'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      next
     c                   endif
     c     next          tag
     c                   write     axmlutr
     c     lesnext       tag
     c                   goto      start
      *
     c                   endif
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  LoopA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     loopA         begsr
      *
     c                   eval      s_head = axhead
     c                   eval      s_linj =axlinj
      *
     c                   eval      rhovl1_kont = *zero
     c                   eval      rhovl1_spes = *zero
     c                   eval      rhovl1_avde = *zero
     c     rhovl1_key    setll     rhovl1
     c                   read      rhovl1
     c                   dow       not %eof(rhovl1)  and
     c                             rhfirm = w_firm
      *
      *  Behandler en konto bare en gang
      *
     c                   if        rhkont = s_kont
     c                   goto      NextKont
     c                   endif
      *
      *   Sjekk om standard konto er registrert og gyldig
      *
     c                   if        rhrefn = *zero
s.ss c**                 goto      NextKont
      *
     c                   eval      w_ms1 = 'Konto ' + %char(rhkont) +
     c                                     ' mangler Standard Konto'
     c                   eval      w_ms2 = 'Jobb avbrytes, reg. standard +
     c                             konto i Kontoplan'
     c                   call      'AA007R'
     c                   parm                    w_ms1
     c                   parm                    w_ms2
     c                   parm                    w_svar
     c                   parm                    w_f3
     c                   eval      w_avbryt = 'J'
     c                   eval      *inlr = *on
     c                   return
     c                   endif
      *
     c                   eval      rhstl1_kont = rhrefn
     c     rhstl1_key    chain     rhstl1
     c                   if        not %found(rhstl1)
     c                   eval      w_ms1 = 'Konto ' + %char(rhrefn) +
     c                                     ' finnes ikke i Standard +
     c                                      Kontoplan'
     c                   eval      w_ms2 = 'Jobb avbrytes, rett standard +
     c                             konto i Kontoplan'
     c                   call      'AA007R'
     c                   parm                    w_ms1
     c                   parm                    w_ms2
     c                   parm                    w_svar
     c                   parm                    w_f3
     c                   eval      w_avbryt = 'J'
     c                   eval      *inlr = *on
     c                   return
     c                   endif
      *
     c                   eval      s_kont = rhkont
      *
     c                   if        rhkont = 8960
     c                   eval      rhkont = rhkont
     c                   endif
     c                   exsr      BerSaldo
      *
     c                   move      s_kont        s_kontx
     c                   dow       %subst(s_kontx:1:1) = '0' or
     c                             s_kont = 0
     c                   eval      %subst(s_kontx:1:4) = %subst(s_kontx:2:4)
     c                   eval      %subst(s_kontx:5:1) = ' '
     c                   enddo
      *
     c                   eval      rxmll1_head = s_head
     c                   eval      rxmll1_linj = s_linj
     c     rxmll1_key    setll     rxmll1
     c                   read      rxmll1
     c                   dow       not %eof(rxmll1 ) and
     c                             axmaop <> 'E'
      *
      *  Sjekk om bare xml linja skal ut
      *
     c                   if        axtype = 'F'
     c                   eval      xmlrec = axxmlt
     c                   goto      txmlut
     c                   endif
      *
      *  Sjekk om linja skal ut med fast parameter
      *
     c                   eval      i = %scan('?F?':axxmlt)
     c                   if        i > 0
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(axfast) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      txmlut
     c                   endif
      *
      *  Sjekk om linja skal ut med variabel som parameter
      *
     c                   eval      i = %scan('?V?':axxmlt)
      *
     c                   if        i > 0 and
     c                             axtype <> 'B'
     c                   eval      text = *blank
     c                   select
     c                   when      axfelt = 'RHKONT'
     c                   movel     s_kontx       text
     c                   if        %subst(axfast:1:1) = '('
     c                   eval      txt2 = text
     c                   eval      text = *blank
     c                   eval      f = %subst(axfast:2:3)
     c                   eval      t = %subst(axfast:6:3)
     c                   move      f             fn
     c                   move      t             tn
     c                   eval      text = %subst(txt2:fn:tn-fn+1)
     c                   endif
     c                   when      axfelt = 'RHTEXT'
     c                   movel     rhtext        text
     c                   exsr      sjk_xml_esc
     c                   when      axfelt = 'RHREFN'
     c                   movel     rhrefn        text
     c                   exsr      sjk_xml_esc
     c                   when      axfelt = 'RHODAT'
     c                   movel     rhodat        text
     c                   endsl
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(text) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      txmlut
     c                   endif
      *
      *  Inng. saldo = 0 eller positiv
      *
     c                   if        w_saldoi >= 0 and
     c                             axfelt = 'INNGSALDOD'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_salix) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      txmlut
     c                   endif
      *
      *  Inng. saldo = negativ
      *
     c                   if        w_saldoi < 0 and
     c                             axfelt = 'INNGSALDOC'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_salix) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      txmlut
     c                   endif
      *
      *  Utg. saldo = 0 eller positiv
      *
     c                   if        w_saldou >= 0 and
     c                             axfelt = 'UTGSALDOD'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_salux) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      txmlut
     c                   endif
      *
      *  Inng. saldo = negativ
      *
     c                   if        w_saldou < 0 and
     c                             axfelt = 'UTGSALDOC'
     c                   eval      xmlrec = %subst(axxmlt:1:i-1) +
     c                             %trim(w_salux) +
     c                             %subst(axxmlt:i+3:50)
     c                   goto      txmlut
     c                   endif
      *
     c                   goto      txmlutl
      *
     c     txmlut        tag
     c                   write     axmlutr
      *
     c     txmlutl       tag
     c                   read      rxmll1
      *
     c                   enddo
     c                   if        axmaop = 'E'
     c                   eval      xmlrec = axxmlt
     c                   write     axmlutr
     c                   endif
      *
     c     NextKont      tag
     c                   read      rhovl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk XML  for spesial-tegn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     sjk_XML_esc   begsr
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      w_str_i = text
      *
     c                   dow       w_posi < 70
     c                   eval      w_posi = w_posi + 1
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 90
     c                   select
     c                   when      w_str_x = '&'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:5) = '&amp;'
     c                   eval      w_posu = w_posu + 4
      *
     c                   when      w_str_x = '<'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&lt;'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = '>'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:4) = '&gt;'
     c                   eval      w_posu = w_posu + 3
      *
     c                   when      w_str_x = c_apos
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&apos;'
     c                   eval      w_posu = w_posu + 5
      *
     c                   when      w_str_x = '"'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:6) = '&quot;'
     c                   eval      w_posu = w_posu + 5
7.01  *
7.01 c                   when      w_str_x = '§'
7.01 c                   eval      w_posu = w_posu + 1
7.01 c                   eval      %subst(w_str_u:w_posu:1) = 'P'
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   enddo
      *
     c                   eval      text = w_str_u
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Beregn saldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     BerSaldo      begsr
      *
     c                   if        s_kont = 4000
     c                   eval      s_kont = s_kont
     c                   endif
      *
     c                   eval      w_saldoi = *zero
     c                   eval      w_saldou = *zero
      *
      *    Inng. Saldo
      *
     c                   eval      rhsal1_kont = s_kont
     c                   eval      rhsal1_spes = *zero
     c                   eval      rhsal1_avde = *zero
     c                   eval      rhsal1_raar = *zero
     c                   eval      rhsal1_type = 0
     c     rhsal1_key    setll     rhsal1
     c                   read      rhsal1
      *
     c                   dow       not %eof(rhsal1)
     c                             and rikont = s_kont
     c                   if        rikont = s_kont  and
     c                             ritype = 1       and
     c                             riraar = p_pfa
     c                   eval      w_saldoi = w_saldoi + risa00
     c                   if        p_pfm > 1
     c                   eval      w_saldoi = w_saldoi + risa01
     c                   endif
     c                   if        p_pfm > 2
     c                   eval      w_saldoi = w_saldoi + risa02
     c                   endif
     c                   if        p_pfm > 3
     c                   eval      w_saldoi = w_saldoi + risa03
     c                   endif
     c                   if        p_pfm > 4
     c                   eval      w_saldoi = w_saldoi + risa04
     c                   endif
     c                   if        p_pfm > 5
     c                   eval      w_saldoi = w_saldoi + risa05
     c                   endif
     c                   if        p_pfm > 6
     c                   eval      w_saldoi = w_saldoi + risa06
     c                   endif
     c                   if        p_pfm > 7
     c                   eval      w_saldoi = w_saldoi + risa07
     c                   endif
     c                   if        p_pfm > 8
     c                   eval      w_saldoi = w_saldoi + risa08
     c                   endif
     c                   if        p_pfm > 9
     c                   eval      w_saldoi = w_saldoi + risa09
     c                   endif
     c                   if        p_pfm > 10
     c                   eval      w_saldoi = w_saldoi + risa10
     c                   endif
     c                   if        p_pfm > 11
     c                   eval      w_saldoi = w_saldoi + risa11
     c                   endif
     c                   if        p_pfm > 12
     c                   eval      w_saldoi = w_saldoi + risa12
     c                   endif
      *
     c                   endif
     c                   read      rhsal1
     c                   enddo

      *
      *    Utg. Saldo
      *
     c                   eval      rhsal1_kont = s_kont
     c                   eval      rhsal1_spes = *zero
     c                   eval      rhsal1_avde = *zero
     c                   eval      rhsal1_raar = *zero
     c                   eval      rhsal1_type = 0
     c     rhsal1_key    setll     rhsal1
     c                   read      rhsal1
      *
     c                   dow       not %eof(rhsal1)
     c                             and rikont = s_kont
     c                   if        rikont = s_kont  and
     c                             ritype = 1       and
     c                             riraar = p_pta
     c                   eval      w_saldou = w_saldou + risa00
     c                   if        p_ptm >= 1
     c                   eval      w_saldou = w_saldou + risa01
     c                   endif
     c                   if        p_ptm >= 2
     c                   eval      w_saldou = w_saldou + risa02
     c                   endif
     c                   if        p_ptm >= 3
     c                   eval      w_saldou = w_saldou + risa03
     c                   endif
     c                   if        p_ptm >= 4
     c                   eval      w_saldou = w_saldou + risa04
     c                   endif
     c                   if        p_ptm >= 5
     c                   eval      w_saldou = w_saldou + risa05
     c                   endif
     c                   if        p_ptm >= 6
     c                   eval      w_saldou = w_saldou + risa06
     c                   endif
     c                   if        p_ptm >= 7
     c                   eval      w_saldou = w_saldou + risa07
     c                   endif
     c                   if        p_ptm >= 8
     c                   eval      w_saldou = w_saldou + risa08
     c                   endif
     c                   if        p_ptm >= 9
     c                   eval      w_saldou = w_saldou + risa09
     c                   endif
     c                   if        p_ptm >= 10
     c                   eval      w_saldou = w_saldou + risa10
     c                   endif
     c                   if        p_ptm >= 11
     c                   eval      w_saldou = w_saldou + risa11
     c                   endif
     c                   if        p_ptm >= 12
     c                   eval      w_saldou = w_saldou + risa12
     c                   endif
      *
     c                   endif
      *
     c                   read      rhsal1
     c                   enddo
      *
     c                   eval      w_s = w_saldoi
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_salix = w_110x + '.' + w_02x
     c                   dow       %subst(w_salix:1:1) = '0'
     c                   eval      %subst(w_salix:1:13) = %subst(w_salix:2:13)
     c                   eval      %subst(w_salix:14:1) = ' '
     c                   enddo
      *
     c                   eval      w_s = w_saldou
     c                   if        w_s < 0
     c                   eval      w_s = w_s * -1
     c                   endif
     c                   eval      w_110 = w_s
     c                   eval      w_02  = w_s - w_110
     c                   move      w_110         w_110x
     c                   move      w_02          w_02x
     c                   eval      w_salux = w_110x + '.' + w_02x
     c                   dow       %subst(w_salux:1:1) = '0'
     c                   eval      %subst(w_salux:1:13) = %subst(w_salux:2:13)
     c                   eval      %subst(w_salux:14:1) = ' '
     c                   enddo
      *
     c                   if        w_saldoi <> 0 or
     c                             w_saldou <> 0
     c                   eval      w_saldoi = w_saldoi
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
     c                   kfld                    rhsal1_spes
     c                   kfld                    rhsal1_avde
     c                   kfld                    rhsal1_raar
     c                   kfld                    rhsal1_type
      *
     c     rhstl1_key    klist
     c                   kfld                    rhstl1_kont
      *
     c     rxmll1_key    klist
     c                   kfld                    rxmll1_head
     c                   kfld                    rxmll1_linj
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter verdier fra LDA / Legger inn verdier i felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    w_avbryt
      *
     c     *dtaara       define                  ax000par        100
     c     *lock         in        *dtaara
     c                   eval      p_perfm = %subst(ax000par:1:2)
     c                   eval      p_perfa = %subst(ax000par:3:4)
     c                   eval      p_pertm = %subst(ax000par:7:2)
     c                   eval      p_perta = %subst(ax000par:9:4)
     c                   eval      p_hcom  = %subst(ax000par:13:50)
      *
     c                   move      p_perfm       p_pfm
     c                   move      p_perfa       p_pfa
     c                   move      p_pertm       p_ptm
     c                   move      p_perta       p_pta
      *
     c                   move      l_firm        w_firm
      *
     c                   movel     'KONTOPLAN'   rxmll1_head
     c                   move      *zero         rxmll1_linj
     c     rxmll1_key    setll     rxmll1
      *
     c                   endsr
