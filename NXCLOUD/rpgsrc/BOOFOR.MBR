      /TITLE  ASSHOP: Overføring fra butikk til ordre-register
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BOOFOR
      * Beskrivelse . . : Overføring fra butikk til ordre-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110101 AMD  Programmet er lagt om til standard ILE
3.31  * R3.31 110101 AMD  Statistikk ble ikke oppdatert med varegruppe
3.31  *                   på varer hentet fra LVR (ordre hentet til butikk)
3.32  * R3.32 290301 AMD  Lagt inn mulighet for å styre om rabatter skal
      *                   vises med prosent, eller som bare et nettobeløp.
3.33  * R3.33 280801 AMD  Dersom det er pakkseddel som skal behandles,
3.33  *                   legges det på dagens dato som leveringsdato.
5.01  * R5.01 241002 AMD  Lagt om rutiner for oppdatering av lager
      *       161203 HKO  Henter inn bruksrett-informasjon
      *                   Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
5.02  * R5.02 180105 AMD  Endringer for å få på kode for netto faktura
      *                   også for registreringer fra radioterminal
5.02  * R5.50 160507 BOF  Fakturatype legger fra kunde til ordre
5.70  * PRGR  121208 bof  Utvidet med prisgruppe
6.10  *       280109 bof  Hvis pakkseddel, oppdateres FOPAKS
6.11  * R6.10 021210 BSA  Vi har tatt i bruk prisliste suffix til korthåndtering
6.11  *                   Feltet kan ikke endres i dette programmet.
6.12  * R6.10 030311 BSA  Vi merker ordren med kortkode hvis kunden har
6.12  *                   kreditkort.
6.13  * R6.10 300312 BSA  Innført GE kort, samme funksjonalitet som BM og Maxbo kort
6.20  * R6.20 031215 bof  Legger dagens dato i pakkseddeldato
6.30  * 6.30  150914 BKV  Henter overstyrt info fra kundeprosjekt
6.31  * R6.20 131014 bof  Merker transer herfra med RT (det er vel bare det her ??)
6.32  * 6.30  210815 bof  feilretting i parameter til VP900R
6.33  * 6.30  180116 bsa  Gebyrinfo hentes fra kunde/kundeprosjekt
6.34  * 6.30  050816 bsa  Legger på mer info på FODT.
6.35  * 6.30  210617 bsa  Må håndtere mvakode på linjenivå avhengig av
6.35  *                   avgiftskode på kunde.
6.36  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
7.01  *K000   050121 bsa  Pakkseddel negativ settes automatsk i hold
7.01  *K000               når de kommer fra POS. Styres av kode i AFPSPF.
7.01  *K000               Der legger vi ordretypen, hardkodes ikke. Krever
7.01  *K000               at den er av type 2 og "-".
7.01  *K000               (ikke fra vanlig switchstyring)
8.03  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     fbmhel1    if   e           k disk    rename(bmhepfr:bmhel1r)
     fbmdtl1    if   e           k disk    rename(bmdtpfr:bmdtl1r)
5.02 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.12 frukrl5    if   e           k disk    rename(rukrpfr:rukrl5r)
6.30 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
6.34 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.35 fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
6.36 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.01 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
7.01 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
7.01 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
      *
      * Definering av data struktur
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.32 d  hinumm                        8  0 overlay(hirec:56)
6.32 d  hikode                        1    overlay(hirec:64)
6.32 d  hidekn                        5  2 overlay(hirec:65)
6.32 d  hiavgf                        1  0 overlay(hirec:70)
6.32 d  hipriv                        1  0 overlay(hirec:71)
6.32 d  hitilb                        1    overlay(hirec:72)
6.32 d  hiskaf                        1    overlay(hirec:73)
6.32 d  hildor                        6  0 overlay(hirec:74)
6.32 d  hisapr                        9  2 overlay(hirec:80)
6.32 d  hikopr                        9  2 overlay(hirec:89)
8.03 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Definering av data struktur
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.03 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(bmfirm)
     d p_aarr          s                   like(bmaarr)
     d p_stat          s                   like(w_alf1)
     d p_wsid          s                   like(bmwsid)
3.33 d p_type          s              1  0
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(bnfirm)
     d bmhel1_aarr     s                   like(bnaarr)
     d bmhel1_numm     s                   like(bnnumm)
     d bmhel1_wsid     s                   like(bnwsid)
     d bmdtl1_aarr     s                   like(bmaarr)
     d bmdtl1_numm     s                   like(bmnumm)
     d bmdtl1_line     s                   like(bmline)
     d bmdtl1_wsid     s                   like(bmwsid)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_line     s                   like(fdline)
     d fodtlu_suff     s                   like(fdsuff)
5.02 d rkunl1_kund     s                   like(rkkund)
6.12 d rukrl5_ktyp     s                   like(ruktyp)
6.12 d rukrl5_kkun     s                   like(rukkun)
6.12 d rukrl5_kpro     s                   like(rukpro)
6.30 d fkprl1_kund     s                   like(flkund)
6.30 d fkprl1_kpro     s                   like(flkpro)
6.34 d vvarl1_vare     s                   like(vvvare)
6.35 d vmval1_mkod     s                   like(vamkod)
7.01 d afpslr_ufil     s                   like(l_filg)
7.01 d afpslr_uide     s                   like(afuide)
7.01 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
8.03 d w_prgr          s              2
     d w_alf1          s              1
     d w_enor          s                   like(w_alf1)
3.33 d w_date          s                   like(bnldat)
5.01 d w_oakk          s              1  0
      *
6.34 d w_vtyp          s                   like(vvvtyp)
6.34 d w_utda          s               d   datfmt(*iso)
6.34 d w_ldor          s                   like(vvldor)
6.34 d b_inlr          s              1    inz(*off)
6.36 d w_lv_nobb       s                   like(vvlnob)
6.36 d w_lv_modn       s                   like(vvlmod)
6.36 d w_lv_lldo       s                   like(vvlnle)
6.36 d w_lv_llva       s                   like(vvllva)
7.01 d w_hkod          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn opplysninger fra BONG-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     bmhel1_key    chain     bmhel1r                            90
      *
      *  Legger inn ordrenummer i nøkklene
      *
     c                   eval      fohelu_numm = bnornr
     c                   eval      fohelu_suff = bnorsu
     c                   eval      fodtlu_numm = bnornr
     c                   eval      fodtlu_suff = bnorsu
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Overføring fra BONG-LINJE-REGISTER til ORDRE-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bmdtl1_key    setll     bmdtl1r
      *
     c     startl        tag
     c                   eval      *in90 = *off
     c                   read      bmdtl1r                                90
      *
     c                   if        *in90 = *off
     c                   if        bmdtl1_aarr = bmaarr
     c                   if        bmdtl1_wsid = bmwsid
     c                   if        bmdtl1_numm = bmnumm
      *
     c                   clear                   fodtlur
      *
     c                   eval      fodtlu_line = bmline
     c     fodtlu_key    chain     fodtlur                            92
      *
     c                   if        *in92 = *off
     c                   unlock    fodtlu
     c                   endif
      *
     c                   if        *in92 = *on
     c                   eval      fdfirm = w_firm
     c                   eval      fdnumm = fodtlu_numm
     c                   eval      fdsuff = fodtlu_suff
     c                   eval      fdline = fodtlu_line
      *
      * Henter priser og rabatter for vare.
      * Benytter hentet kostpris dersom ikke overstyrt
      *
     c                   if        bmvare <> *blank
      *
     c                   exsr      hent_pris
      *
     c                   if        bmkopr = 0
     c                   eval      bmkopr = hokopr
     c                   if        bmkopr = 0
     c                   eval      bmkopr = bmsapr * hopros
     c                   endif
     c                   endif
      *
     c                   endif
      *
6.34  * Henter inn mer informasjon til FODT
6.34  * henter varetype - her prioritert leverandør fra lager
6.34  *
6.34 c                   call      'VL710R'
6.34 c                   parm                    w_firm
6.34 c                   parm                    bmvare
6.34 c                   parm                    bnlage
6.34 c                   parm                    w_vtyp
6.34 c                   parm                    w_utda
6.34 c                   parm                    w_ldor
6.34 c                   parm                    b_inlr
6.36 c                   parm                    w_lv_nobb
6.36 c                   parm                    w_lv_modn
6.36 c                   parm                    w_lv_lldo
6.36 c                   parm                    w_lv_llva
6.34  *
6.34  * Hent NOBBnummer
6.34  *
6.34 c                   eval      vvarl1_vare = bmvare
6.34 c     vvarl1_key    chain     vvarl1
6.34 c                   if        not %found
6.34 c                   eval      vvnobb = 0
6.34 c                   endif
      *
      * Legger over opplysninger om den enkelte linjen
      *
6.36 c                   if        w_lv_nobb > 0
6.36 c                   eval      fdnobb = w_lv_nobb
6.36 c                   else
6.34 c                   eval      fdnobb = vvnobb
6.36 c                   endif
     c                   eval      fdotyp = bnotyp
     c                   eval      fdltyp = bmltyp
     c                   eval      fdlety = bmlety
     c                   eval      fdvare = bmvare
     c                   eval      fdenh1 = bmenh1
     c                   eval      fdlage = bnlage
     c                   eval      fdkakk = *zero
     c                   eval      fdkpri = bmkpri
     c                   eval      fdkgeb = *zero
     c                   eval      fdlass = *zero
     c                   eval      fdkund = bnkund
     c                   eval      fdbenr = *zero
6.34 c**                 eval      fdldor = *zero
6.34 c                   eval      fdldor = w_ldor
     c                   eval      fdldat = bnldat
     c                   eval      fdavde = bnavde
     c                   eval      fdkont = bnkont
     c                   eval      fdspes = bnspes
     c                   eval      fdproj = bnproj
     c                   eval      fdakti = bnakti
     c                   eval      fdlvar = *blank
     c                   eval      fdtek1 = bmtek1
     c                   eval      fdtek2 = bmtek2
     c                   eval      fdanta = bmanta
     c                   eval      fdoppr = bmoppr
     c                   eval      fdsapr = bmsapr
     c                   eval      fdkopr = bmkopr
     c                   if        bmselk <> *zero
     c                   eval      fdkopr = bmselk
     c                   endif
     c                   eval      fdrab1 = bmrab1
     c                   eval      fdrab2 = bmrab2
     c                   eval      fdpasl = *zero
      *
     c                   eval      fdsums = *zero
     c                   eval      fdsumr = *zero
     c                   eval      fdsumk = *zero
3.31  *
3.31 c                   eval      fdogrp = bmogrp
3.31 c                   eval      fdhgrp = bmhgrp
3.31 c                   eval      fdugrp = bmugrp
3.31 c                   eval      fdlvre = bmlvre
      *
     c                   if        bmvare <> *blank
     c                   eval      fdalme = hoalme
     c                   eval      fdbrty = hobrty
     c                   eval      fdbrr1 = hobrr1
     c                   eval      fdbrr2 = hobrr2
6.35 c**                 eval      fdomva = hoomva
     c                   eval      fdpriv = 0
6.35  *
6.35  * Endring av momskode på linjenivå
6.35  *
6.35 c                   if        fomkod = 1
6.35 c                   eval      fdomva = '4'
6.35 c                   else
6.35 c                   eval      vmval1_mkod = vvkmva
6.35 c     vmval1_key    chain     vmval1
6.35 c                   if        %found
6.35 c                   eval      fdomva = vamkof
6.35 c                   if        bnmkod = 2 and
6.35 c                             vamkuo <> *blank
6.35 c                   eval      fdomva = vamkuo
6.35 c                   endif
6.35 c                   else
6.35 c                   select
6.35 c                   when      vvkmva = 1
6.35 c                   eval      fdomva = '4'
6.35 c                   when      vvkmva = 2
6.35 c                   eval      fdomva = 'H'
6.35 c                   endsl
6.35 c                   endif
6.35 c                   endif
6.35  *
     c                   endif
      *
     c                   write     fodtlur
     c                   endif
      *
     c                   goto      startl
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Overføring fra BONG-HODE-REGISTER til ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Legger ut igjen ordren med nytt ordrenummer
      *
     c     fohelu_key    chain     fohelur                            90
      *
     c                   if        *in90 = *off
     c                   unlock    fohelu
     c                   endif
      *
     c                   if        *in90 = *on
     c                   eval      fofirm = w_firm
     c                   eval      fonumm = fohelu_numm
     c                   eval      fosuff = fohelu_suff
5.02  *
5.02  * Henter inn fra kunderegister
5.02  *
5.02 c                   eval      foneto = 0
5.02 c                   eval      fofaty = 0
5.02 c                   eval      rkunl1_kund = bnkund
5.02 c     rkunl1_key    chain     rkunl1r
5.02 c                   if        %found
5.02 c                   eval      foneto = rkneto
5.02 c                   eval      fofaty = rkfaty
5.02 c                   endif
      *
      * Legger over opplysninger
      *
     c                   eval      foline = *zero
      *
6.31 c                   eval      fofsys = 'RT'
     c                   eval      footyp = bnotyp
     c                   eval      folage = bnlage
     c                   eval      fobinr = *zero
     c                   eval      fokref = bnkref
     c                   eval      fobdat = bnbdat
     c                   eval      foldat = bnldat
     c                   eval      fofkda = bnfkda
     c                   eval      foffda = bnffda
     c                   eval      fobetb = bnbetb
     c                   eval      foselg = bnselg
     c                   eval      fodist = bndist
     c                   eval      folmat = bnlmat
     c                   eval      folbet = bnlbet
     c                   eval      fovalk = bnvalk
     c                   eval      forkat = bnrkat
     c                   eval      fosped = bnsped
     c                   eval      fokjor = bnkjor
     c                   eval      fomkod = bnmkod
     c                   eval      folkun = bnlkun
     c                   eval      fokund = bnkund
     c                   eval      fokuna = bnkuna
     c                   eval      fonavn = bnnavn
     c                   eval      foadr1 = bnadr1
     c                   eval      foadr2 = bnadr2
     c                   eval      fosted = bnsted
     c                   eval      foavde = bnavde
     c                   eval      fokont = bnkont
     c                   eval      fospes = bnspes
     c                   eval      foproj = bnproj
     c                   eval      foakti = bnakti
     c                   eval      fokobk = *zero
6.33 c**                 eval      fokgfa = bnkgfa
6.33 c                   eval      fokgfa = rkfagb
6.33 c**                 eval      fokgek = bnkgfa
6.33 c                   eval      fokgek = rkekgb
6.33 c**                 eval      fokgfr = *zero
6.33 c                   eval      fokgfr = rkfrgb
     c                   eval      foksob = *zero
     c                   eval      foktxt = *zero
     c                   eval      fokfak = *zero
     c                   eval      fokspr = *zero
     c                   eval      fovref = bnvref
     c                   eval      fodref = bndref
     c                   eval      forekv = bnrekv
     c                   eval      folmtx = bnlmtx
     c                   eval      folbtx = bnlbtx
     c                   eval      forabp = bnrabp
     c                   eval      fopasl = *zero
     c                   eval      fopask = *blank
     c                   eval      fopord = bnpord
6.11 c**                 eval      fopsuf = bnpsuf
     c                   eval      fokoli = *zero
     c                   eval      fokonr = *zero
     c                   eval      fototx = *zero
     c                   eval      fotots = *zero
     c                   eval      fototr = *zero
     c                   eval      fototk = *zero
     c                   eval      fowsid = bnwsid
     c                   eval      fouser = l_user
     c                   eval      fodate = bndate
     c                   eval      fotime = bntime
     c                   eval      fokode = 0
     c                   eval      fotdat = *loval
     c                   eval      fokpro = bnkpro
      *
6.30  * Slå opp på kundeprosjekt for å finne om prosjekt er
6.30  * overstyrer verdi på kunde
6.30 c                   if        fokpro > 0
6.30 c                   eval      fkprl1_kund = fokund
6.30 c                   eval      fkprl1_kpro = fokpro
6.30 c     fkprl1_key    chain     fkprl1
6.30 c                   if        %found(fkprl1)
6.30 c                   eval      foneto = flneto
6.33 c                   eval      fokgfa = flfagb
6.33 c                   eval      fokgek = flekgb
6.30 c                   endif
6.30 c                   endif
6.30  *
3.33  *
3.33  * Dersom dette er en pakkseddel
3.33  * skal dagens dato legges som
3.33  * leveringsdato.
3.33 c                   if        p_type = 2
3.33 c                   eval      foldat = w_date
6.20 c                   eval      fopada = w_date
3.33 c                   eval      fopdat = bnldat
6.10 c                   eval      fopaks = 1
3.33 c                   endif
3.33  *
5.01  *
5.01  * Dersom behandlingskode 2 eller 3,
5.01  * skal det settes kode for at lager
5.01  * er oppdatert, i motsatt fall, ikke.
5.01  *
5.01 c                   call      'VA750R'
5.01 c                   parm                    w_firm
5.01 c                   parm                    footyp
5.01 c                   parm                    w_oakk
5.01  *
5.01 c                   if        w_oakk = 2 or
5.01 c                             w_oakk = 3
5.01 c                   eval      foolag = 1
5.01 c                   else
5.01 c                   eval      foolag = 0
5.01 c                   endif
6.12  *
6.12  * Sjekk mot kortregister, og søk etter BM kort på kunden
6.12  *
6.12 c                   eval      rukrl5_ktyp = 5
6.12 c                   eval      rukrl5_kkun = fokund
6.12 c                   eval      rukrl5_kpro = 0
6.12 c     rukrl5_key    chain     rukrl5
6.12 c                   if        %found(rukrl5)
6.12 c                   if        rukobl = 1
6.12 c                   eval      fopsuf = 5
6.12 c                   endif
6.12 c                   endif
6.12  *
6.12  * Sjekk mot kortregister, og søk etter Maxbo kort
6.12  *
6.12 c                   eval      rukrl5_ktyp = 3
6.12 c                   eval      rukrl5_kkun = fokund
6.12 c                   eval      rukrl5_kpro = 0
6.12 c     rukrl5_key    chain     rukrl5
6.12 c                   if        %found(rukrl5)
6.12 c                   if        rukobl = 1
6.12 c                   eval      fopsuf = 3
6.12 c                   endif
6.12 c                   endif
6.13  *
6.13  * Sjekk mot kortregister, og søk etter GE Kort
6.13  *
6.13 c                   eval      rukrl5_ktyp = 4
6.13 c                   eval      rukrl5_kkun = fokund
6.13 c                   eval      rukrl5_kpro = 0
6.13 c     rukrl5_key    chain     rukrl5
6.13 c                   if        %found(rukrl5)
6.13 c                   if        rukobl = 1
6.13 c                   eval      fopsuf = 4
6.13 c                   endif
6.13 c                   endif
7.01  *
7.01  * Henter inn opplysninger om ordretypen. Hvis pakkseddel negativ, settes
7.01  * evnt holdekode.
7.01  *
7.01 c                   eval      votyl1_otyp = footyp
7.01 c     votyl1_key    chain     votyl1
7.01 c                   if        %found
7.01 c                   if        vaoakk = 2   and
7.01 c                             vaokre = '-'
7.01 c                   eval      foityp = w_hkod
7.01 c                   endif
7.01 c                   endif
      *
     c                   write     fohelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling  av ordren etter overføringen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Statuskode=Blank, gir beregning av nye priser i Sub-programmet
      *
     c                   eval      w_enor = *blank
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    fohelu_numm
     c                   parm                    fohelu_suff
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter pris og rabatt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      hifirm = bnfirm
     c                   eval      hirkat = bnrkat
      *
     c                   eval      hikund = bnkund
     c                   if        bnlkun <> *zero
     c                   eval      hikund = bnlkun
     c                   endif
      *
     c                   eval      hikpro = bnkpro
     c                   eval      hivare = bmvare
     c                   eval      hilety = bmlety
     c                   eval      hienhe = bmenh1
     c                   eval      hidato = bnbdat
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
5.70 c                   eval      hiprgr = w_prgr
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_alf1 = *blank
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      * Key for file : BONG-HODE-REGISTER
      *
     c     bmhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmhel1_aarr
     c                   kfld                    bmhel1_wsid
     c                   kfld                    bmhel1_numm
      *
      * Key for file : BONG-LINJE-REGISTER
      *
     c     bmdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmdtl1_aarr
     c                   kfld                    bmdtl1_wsid
     c                   kfld                    bmdtl1_numm
     c                   kfld                    bmdtl1_line
5.02  *
5.02  * Key for file : KUNDE-REGISTER
5.02  *
5.02 c     rkunl1_key    klist
5.02 c                   kfld                    w_firm
5.02 c                   kfld                    rkunl1_kund
6.12  *
6.12  * Key for oppslag mot kortregister på kortnummer
6.12  *
6.12 c     rukrl5_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    rukrl5_ktyp
6.12 c                   kfld                    rukrl5_kkun
6.12 c                   kfld                    rukrl5_kpro
6.30  *
6.30  * Key for oppslag på kundeprosjekt
6.30  *
6.30 c     fkprl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    fkprl1_kund
6.30 c                   kfld                    fkprl1_kpro
6.34  *
6.34  * Key for file : VARE-REGISTER
6.34  *
6.34 c     vvarl1_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    vvarl1_vare
6.35  *
6.35  * Key for oppslag på mva.-kode
6.35  *
6.35 c     vmval1_key    klist
6.35 c                   kfld                    w_firm
6.35 c                   kfld                    vmval1_mkod
7.01  *
7.01  * Key for propertiesfil
7.01  *
7.01 c     afpslr_key    klist
7.01 c                   kfld                    afpslr_ufil
7.01 c                   kfld                    afpslr_uide
7.01  *
7.01  * Key for oppslag på Ordre-type
7.01  *
7.01 c     votyl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    votyl1_otyp
7.01  *
7.01  * Key for file : STATUS-KODER
7.01  *
7.01 c     fstsl1_key    klist
7.01 c                   kfld                    w_firm
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
3.33 c                   parm                    p_type
      *
      * Legger inn firmanummer i nøklene
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn årstall og skjerm/kasse i nøklene
      *
     c                   eval      bmhel1_aarr = p_aarr
     c                   eval      bmhel1_wsid = p_wsid
     c                   eval      bmhel1_numm = *zero
      *
     c                   eval      bmdtl1_aarr = p_aarr
     c                   eval      bmdtl1_wsid = p_wsid
     c                   eval      bmdtl1_numm = *zero
     c                   eval      bmdtl1_line = *zero
      *
     c                   eval      fohelu_numm = *zero
     c                   eval      fohelu_suff = *zero
      *
     c                   eval      fodtlu_numm = *zero
     c                   eval      fodtlu_suff = *zero
     c                   eval      fodtlu_line = *zero
3.33  *
3.33  * Legger inn dagens dato
3.33  *
3.33 c                   time                    w_date
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
7.01  *
7.01  * Sjekk om det skal være holdekode
7.01  *
7.01 c                   eval      afpslr_ufil = l_filg
7.01 c                   eval      w_hkod = *blanks
7.01 c                   eval      afpslr_uide = 'POS.PNHOLD'
7.01 c     afpslr_key    chain     afpslrr
7.01 c                   if        %found(afpslr)
7.01 c                   eval      w_hkod = %trim(afudss)
7.01 c                   endif
      *
7.01 c     fstsl1_key    chain     fstsl1r
      *
     c                   endsr
