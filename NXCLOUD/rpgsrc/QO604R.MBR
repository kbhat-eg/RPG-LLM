      /TITLE  ASOFAK: Start utkjøring av ordrepapirer
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
Q.01  * Program . . . . : QO604R
      * Beskrivelse . . : Start utkjøring av ordrepapirer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.11 220800 AMD  Programmet er skrevet om til ny standard
      * R3.12 220800 AMD  Sjekker om fakturering eller overføring til
      *                   økonomi er igang, varsler i så fall.
      *                   Setter status som sier at fakturering er startet.
5.41  * R5.41 010705 AMD  Lagt inn bedre kontroller på periode og
5.41  *                   fakturadato ved utkjøring av fakturering
6.20  * R6.20 151214 bsa  Sjekker om memberet vi finner er brukt før, og ikke
6.20  *                   ryddet.
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.31  * R6.30 210516 bof  Får ikke taste inn lavere periode enn "avsluttet periode"
7.xx  * 7.00  290116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
Q.01  * 7.00  080219 bsa  Quick wins - automatisere utskrifter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.31 fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     ffpm1lu    uf a e           k disk    rename(fpm1pfr:fpm1lur)
      *
Q.01 fqo604d    cf   e             workstn
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_sfir                944    946  0
7.xx d l_fnav                951    980
      *
      * Datastruktur for navn på member
      *
     d                 ds
     d d_memb                        10
     d  d_alf1                        1    overlay(d_memb)
     d  d_firm                        3  0 overlay(d_memb:2)
     d  d_numm                        6  0 overlay(d_memb:5)
      *
      * Datastruktur for periode
      *
     d                 ds
     d d_peri                         4  0
     d  d_mmnd                        2  0 overlay(d_peri)
     d  d_aaar                        2  0 overlay(d_peri:3)
      *
      * Definering av parametere
      *
     d p_dirk          s              1  0
     d p_firm          s              3  0
6.nu d p_numm          s              8  0
     d p_otyp          s              2
     d p_outq          s             10
     d p_skor          s              1
     d p_suff          s              2  0
Q.01 d p_acti          s              1
      *
      * Definering av variable i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d fpm1lu_1lst     s                   like(fk1lst)
     d fpm1lu_1usr     s                   like(fk1usr)
     d fpm1lu_1wsd     s                   like(fk1wsd)
      *
      * Definering av variable
      *
     d w_firm          s                   like(faafir)
     d w_valg          s                   like(fk1vg1)
     d w_numm          s              6  0
     d w_alf1          s              1
     d w_mnd1          s              2  0
     d w_mnd2          s              2  0
5.41 d w_aar1          s              4  0
5.41 d w_aar2          s              4  0
5.41 d w_aar3          s              4  0
5.41 d w_avik          s              4  0
     d w_dato          s               d   datfmt(*iso)
     d w_dato_6        s              6  0
     d w_aaar_4        s              4  0
3.12 d w_kode          s              2  0
3.12 d w_onof          s              1  0
6.20 d w_memb          s             10
6.20 d w_stat          s              1
6.31 d w_sp_aar        s                   like(ra1aar)
6.31 d w_sp_mnd        s                   like(ra1per)
6.31 d w_sp_per        s              6  0
6.31 d w_peri          s              6  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Velg om det skal kjøres direkte utskrift
      * Direkte utskrift kjøres dersom ordrenummer er utfylt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   if        p_numm <> *zero
     c                   eval      f1otyp =  p_otyp
     c                   goto      f2taga
     c                   endif
      *
     c                   eval      p_dirk = *zero
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * F1 Behandler valg av utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     f1taga        tag
     c                   eval      f1valg = *zero
      *
      * Send alt
      *
     c     f1tagb        tag
     c                   exfmt     f1bld
      *
      * F3=Gå tilbake
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * F4=Legg jobb i jobbkø
      *
     c                   if        *inkd  = *on
     c                   eval      p_dirk = 2
     c                   endif
      *
      * Finnes ordretype(til) i kode-register
      *
     c                   if        f1otyp <> *blank
     c                   eval      votyl1_otyp = f1otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   goto      f1tagb
     c                   endif
     c                   endif
      *
      * Benytt ordre-type, eller velg en fra listen
      *        av forhånsdefinerte ordre-typer
      *
     c                   if        f1otyp = *blank
     c                   if        f1valg = 0                                   TILBUD
     c                   eval      f1otyp = f1ot01
     c                   endif
     c                   if        f1valg = 1                                   ORDREBEKREFT.
     c                   eval      f1otyp = f1ot02
     c                   endif
     c                   if        f1valg = 2                                   PAKKSEDDEL
     c                   eval      f1otyp = f1ot03
     c                   endif
     c                   if        f1valg = 3
     c                   eval      f1otyp = f1ot04                              FAKTURA
     c                   endif
     c                   if        f1valg = 4
     c                   eval      f1otyp = f1ot05                              KREDITNOTA
     c                   endif
     c                   endif
      *
     c                   goto      f2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * F2 Innlegging av periode og faktura-dato
      *    Bildet vises bare dersom akk.type=3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     f2taga        tag
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = f1otyp
     c     votyl1_key    chain     votyl1r                            90
      *
      * Er dette en Faktura/Kreditnota, hvis ikke, avslutt
      *
     c                   if        vaoakk <> 3
     c                   goto      start
     c                   endif
      *
      * Nullstiller periode og fakturadato
      *
     c                   move      udate         f2peri
     c                   move      udate         f2dato
      *
      * Send alt
      *
     c     f2tagb        tag
     c                   exfmt     f2bld
      *
      * F3=Gå tilabke
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * F4=Legg jobb i jobbkø
      *
     c                   if        *inkd  = *on
     c                   eval      p_dirk = 2
     c                   endif
5.41  *
5.41  * Er periode blanket ut ?
5.41  *
5.41 c                   if        f2peri = 0
5.41 c                   eval      *in31  = *on
5.41 c                   goto      f2tagb
5.41 c                   endif
      *
      * Er periode en gyldig periode
      *
     c                   eval      w_mnd1 = 0
5.41 c                   eval      w_aar1 = 0
      *
     c                   if        f2peri <> 0
     c                   move      f2peri        w_dato_6
     c                   movel     01            w_dato_6
     c     *dmy          test(d)                 w_dato_6               31
     c                   if        *in31 = *on
     c                   goto      f2tagb
     c                   endif
     c     *dmy          move      w_dato_6      w_dato
     c                   extrct    w_dato:*m     w_mnd1
5.41 c                   extrct    w_dato:*y     w_aar1
     c                   endif
5.41  *
5.41  * Sjekk om årstall avviker med mer enn 1 år
5.41  *
5.41 c                   time                    w_dato
5.41 c                   extrct    w_dato:*y     w_aar3
5.41 c                   eval      w_avik = w_aar3 - w_aar1
5.41 c                   if        w_avik < -1 or
5.41 c                             w_avik >  1
5.41 c                   eval      *in34 = *on
5.41 c                   goto      f2tagb
5.41 c                   endif
6.31  *
6.31  * Sjekk om periode er lavere enn "avsluttet periode" fra status
6.31  *
6.31 c                   if        w_sp_per <> 0
6.31 c                   movel     w_aar1        w_peri
6.31 c                   move      w_mnd1        w_peri
6.31 c                   if        w_peri <= w_sp_per
6.31 c                   eval      *in36 = *on
6.31 c                   goto      f2tagb
6.31 c                   endif
6.31 c                   endif
5.41  *
5.41  * Varsle dersom årstall avviker fra dagens
5.41  * m/mulighet for overstyring
5.41  *
5.41 c                   if        *inks = *off
5.41 c                   if        w_avik <> 0
5.41 c                   eval      *in35 = *on
5.41 c                   goto      f2tagb
5.41 c                   endif
5.41 c                   endif
      *
      * Er fakturadato en gyldig dato
      *
     c                   eval      w_mnd2 = 0
5.41 c                   eval      w_aar2 = 0
      *
     c                   if        f2dato <> 0
     c     *dmy          test(d)                 f2dato                 32
     c                   if        *in32 = *on
     c                   goto      f2tagb
     c                   endif
     c     *dmy          move      f2dato        w_dato
     c                   extrct    w_dato:*m     w_mnd2
5.41 c                   extrct    w_dato:*y     w_aar2
     c                   endif
      *
      * Er fakturadato innefor periode
      *
     c                   if        *inks = *off
     c                   if        w_mnd1 <> w_mnd2
     c                   eval      *in33 = *on
     c                   goto      f2tagb
     c                   endif
     c                   endif
5.41  *
5.41 c                   if        *inks = *off
5.41 c                   if        w_aar1 <> w_aar2
5.41 c                   eval      *in33 = *on
5.41 c                   goto      f2tagb
5.41 c                   endif
5.41 c                   endif
3.12  *
3.12  * Er fakturering igang fra en annen skjerm ?
3.12  *
3.12 c                   eval      w_kode = 01
3.12 c                   eval      w_onof = 0
3.12 c                   call      'FA730R'
3.12 c                   parm                    w_kode
3.12 c                   parm                    w_onof
3.12  *
3.12 c                   if        w_onof = 1
3.12 c                   goto      xslutt
3.12 c                   endif
3.12  *
3.12  * Kjøres overføring til regnskap fra en annen skjerm ?
3.12  *
3.12 c                   eval      w_kode = 02
3.12 c                   eval      w_onof = 0
3.12 c                   call      'FA730R'
3.12 c                   parm                    w_kode
3.12 c                   parm                    w_onof
3.12  *
3.12 c                   if        w_onof = 1
3.12 c                   goto      xslutt
3.12 c                   endif
3.12  *
3.12  * Oppdater jobb-register med aktiv-flagg
3.12  *
3.12 c                   eval      w_kode = 01
3.12 c                   eval      w_onof = 1
3.12 c                   call      'FA720R'
3.12 c                   parm                    w_kode
3.12 c                   parm                    w_onof
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * START Behandler danning av parameter før utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     start         tag
      *
      * Kaller opp subrutine for utlegging av parameter, for
      * start av utskrift (w_valg=Internt listenummer fra 0 til 3)
      *
     c                   eval      w_valg = *zero
     c                   exsr      param
      *
      * Hopp ut og start utkjøring, dersom kun ett ordrenummer er valgt
      *
     c                   if        p_numm <> *zero
     c                   goto      neste
     c                   endif
      *
      * TILBUD : Subrutinen må kjøres 3 ganger
      *
     c                   if        vaoakk = 0
     c                   eval      w_valg = 1
     c                   exsr      param
     c                   eval      w_valg = 2
     c                   exsr      param
     c                   eval      w_valg = 3
     c                   exsr      param
     c                   goto      neste
     c                   endif
      *
      * ORDREPAPIRER : Subrutinen skal kjøres en gang
      *
     c                   if        vaoakk = 1
     c                   eval      w_valg = 1
     c                   exsr      param
     c                   goto      neste
     c                   endif
      *
      * PAKKSEDDEL : Subrutinen skal ikke kjøres
      *
     c                   if        vaoakk = 2
     c                   goto      neste
     c                   endif
      *
      * FAKTURA/KREDITNOTA: Subrutinen skal ikke kjøres
      *
     c                   if        vaoakk = 3
     c                   goto      neste
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * NESTE Kaller opp 'CL'-programmet for å starte utskriften
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     neste         tag
      *
Q.01 c                   call      'QO612C'
     c                   parm                    d_memb
Q.01 c                   parm                    p_acti
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer  ORDRE-PLUKK-PARAM1-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     param         begsr
      *
     c                   eval      fpm1lu_1lst = w_valg
     c     fpm1lu_key    chain     fpm1lur                            90
      *
     c                   eval      fk1utq = p_outq
     c                   eval      fk1oty = f1otyp
     c                   eval      fk1onr = p_numm
     c                   eval      fk1suf = p_suff
     c                   eval      fk1vg0 = p_dirk
     c                   eval      fk1vg1 = w_valg
     c                   eval      fk1vg2 = f1vlg2
     c                   eval      fk1vg3 = f1vlg3
      *
     c                   move      f2peri        d_peri
     c                   move      d_aaar        w_aaar_4
     c                   if        d_aaar < 80
     c                   movel     20            w_aaar_4
     c                   else
     c                   movel     19            w_aaar_4
     c                   endif
     c                   move      d_mmnd        fk1per
     c                   movel     w_aaar_4      fk1per
      *
     c                   if        f2dato <> 0
     c     *dmy          move      f2dato        fk1dat
     c                   endif
      *
     c                   if        *in90 = *off
     c                   update    fpm1lur
     c                   else
     c                   eval      fk1fir = w_firm
     c                   eval      fk1wsd = l_wsid
     c                   eval      fk1usr = l_user
     c                   eval      fk1lst = w_valg
     c                   write     fpm1lur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_alf1 = *zero
     c                   eval      w_numm = *zero
      *
     c                   eval      w_mnd1 = *zero
     c                   eval      w_mnd2 = *zero
      *
      * Key for file : OFAK-KODE-REGISTER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
6.31  *
6.31  * Key for file : Kode - status 1
6.31  *
6.31 c     raa1l1_key    klist
6.31 c                   kfld                    w_firm
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : PLUKK-1-LINJE-REGISTER
      *
     c     fpm1lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpm1lu_1wsd
     c                   kfld                    fpm1lu_1usr
     c                   kfld                    fpm1lu_1lst
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_skor
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_otyp
     c                   parm                    p_dirk
     c                   parm                    p_outq
Q.01 c                   parm                    p_acti
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = p_firm
     c                   eval      d_firm = p_firm
      *
      * Legger inn opplysninger i nøkklene
      *
     c                   eval      fpm1lu_1wsd = l_wsid
     c                   eval      fpm1lu_1usr = l_user
      *
      * Hent opplysninger fra OFAK-STATUS-REGISTER
      *
     c     fstsl1_key    chain     fstsl1r                            90
     c                   if        *in90  = *off
     c                   eval      f1ot01 = faaot1
     c                   eval      f1ot02 = faaot2
     c                   eval      f1ot03 = faaot3
     c                   eval      f1ot04 = faaot4
     c                   eval      f1ot05 = faaot5
     c                   endif
6.31  *
6.31  * Hent opplysninger fra koder -status 1
6.31  *
6.31 c     raa1l1_key    chain     raa1l1r                            90
6.31 c                   if        %found(raa1l1)
6.31 c                   eval      W_sp_mnd = ra1per
6.31 c                   eval      w_sp_aar = ra1aar
6.31 c                   eval      w_sp_per = w_sp_aar * 100 + w_sp_mnd
6.31 c                   else
6.31 c                   eval      w_sp_per = *zero
6.31 c                   endif
      *
      * Henter siste nummer for File-member
      *
6.20 c     new_memb      tag
      *
     c                   call      'FA920R'
     c                   parm                    w_alf1
     c                   parm                    w_numm
      *
     c                   move      'A'           d_alf1
     c                   move      w_numm        d_numm
6.20  * Sjekk om member ikke er brukt tidligere
6.20 c                   eval      w_stat = *blanks
6.20 c                   eval      w_memb = d_memb
6.20 c                   call      'FO604C'
6.20 c                   parm                    w_memb
6.20 c                   parm                    w_stat
6.20  *
6.20 c                   if        w_stat = '1'
6.20 c                   eval      w_numm = *zeros
6.20 c                   goto      new_memb
6.20 c                   endif
      *
     c                   endsr
      *
