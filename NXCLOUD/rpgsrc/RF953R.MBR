      /TITLE  Flytting av posteringer på en journal
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF953R                                              *
      * Beskrivelse . . : Flytting av posteringer på en journal               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato   Endringsbeskrivelse                                Sign *
      * -----  ------ --------------------------------------------------------*
6.10  * V6.10  210909 Lagt inn sporingsfelter                            BSA  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhtrlu    uf   e           k disk    rename(rhtrpfr:rhtrlur)
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
     frltrlu    uf   e           k disk    rename(rltrpfr:rltrlur)
      *
     d p_firm          s              3  0                                      Firma
     d p_navn          s             30                                         Navn
     d p_user          s             10                                         BruerI
     d p_dato          s              6  0                                      Pr. da
     d p_aarf          s              4  0                                      Pr. år
     d p_aart          s              4  0                                      Pr. år
     d p_perf          s              2  0                                      Period
     d p_pert          s              2  0                                      Pr. pe
     d p_jour          s              5  0                                      Pr. pe
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d w_mld1          c                   'Vennligst vent .........     '
     d w_mld2          c                   'Oppdatering pågår ......     '
     d wactli          s              2  0
     d wactpo          s              3  0
     d wmld01          s             30
     d wmld02          s             30
      *
      * Send meldingsbildet
      *
     c                   eval      wactli = 4
     c                   eval      wactpo = 44
     c                   eval      wmld01 = w_mld1
     c                   eval      wmld02 = w_mld2
      *
     c                   call      'AA006C'
     c                   parm                    wactli
     c                   parm                    wactpo
     c                   parm                    wmld01
     c                   parm                    wmld02
      *
      * Oppdaterer hovedboksposter
      *
     c                   read      rhtrlu                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rbraar = p_aarf and
     c                             rbrper = p_perf and
     c                             rbjour = p_jour
     c                   eval      rbrper = p_pert
     c                   eval      rbraar = p_aart
6.10 c                   time                    rbedat
6.10 c                   time                    rbetim
6.10 c                   eval      rbeusr = l_user
     c                   update    rhtrlur
     c                   endif
      *
     c                   read      rhtrlu                                 90
     c                   enddo
      *
      * Oppdaterer kundeposter
      *
     c                   read      rktrlu                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rnraar = p_aarf and
     c                             rnrper = p_perf and
     c                             rnjour = p_jour
     c                   eval      rnrper = p_pert
     c                   eval      rnraar = p_aart
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
     c                   endif
      *
     c                   read      rktrlu                                 90
     c                   enddo
      *
      * Oppdaterer leverandørposter
      *
     c                   read      rltrlu                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        roraar = p_aarf and
     c                             rorper = p_perf and
     c                             rojour = p_jour
     c                   eval      rorper = p_pert
     c                   eval      roraar = p_aart
6.10 c                   time                    roedat
6.10 c                   time                    roetim
6.10 c                   eval      roeusr = l_user
     c                   update    rltrlur
     c                   endif
      *
     c                   read      rltrlu                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_navn
     c                   parm                    p_user
     c                   parm                    p_dato
     c                   parm                    p_aarf
     c                   parm                    p_aart
     c                   parm                    p_perf
     c                   parm                    p_pert
     c                   parm                    p_jour
      *
     c                   endsr
