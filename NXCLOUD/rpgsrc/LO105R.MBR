     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO105R
      * Beskrivelse . . : Samkjøre best. tilbake til basisbestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151004 ASK  Nytt program
5.60  * R5.60 191107 AMD  Dette program oppdaterte memosaldo for kunde-
      *                   ordre ?  Dette er fjernet fra programmet
5.60  * R5.60 220409 BSA  Vi skal ikke rekalkulere priser ved sammeslåing.
      *                   Evnt. overstyrt pris skal benyttes.
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  110610 bof  Justert i utvidelse av parm VL001R
6.nu  *   6.30     280514  Utvidet mtp. nummerutvidelse             bof
6.30  * 6.30  040615 bkv  Bestilling økt fra 6 til 8
6.31  * 6.30  190617 bsa  Slå opp for å hente bestilling
6.32  * 6.30  130118 NHO  Feilmelding dersom linje blir > 990
      *
8.01  *  800   271022 bsa  Slette også oppføring i tilleggsregister
8.02  *K0000   040924 bsa  Oppdaterer linjereferanser på tilhørende
8.02  *K0000               salgsordre.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
8.01 flotii2    uf   e           k disk    rename(lotist:lotii2r)
8.02 ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
      *
     flo105d    cf   e             workstn
      *
      * Datastruktur ved oppdatering av lager
      *
     d                 ds
6.nu d d_lrec                       128
     d  d_vare                       15    overlay(d_lrec)
     d  d_lage                        2  0 overlay(d_lrec:16)
     d  d_dato                         d   overlay(d_lrec:18) datfmt(*iso)
     d  d_time                         t   overlay(d_lrec:28) timfmt(*iso)
     d  d_otyp                        2    overlay(d_lrec:36)
     d  d_ltyp                        2    overlay(d_lrec:38)
     d  d_anta                       11  3 overlay(d_lrec:40)
     d  d_kopr                        9  2 overlay(d_lrec:51)
     d  d_refr                       20    overlay(d_lrec:60)
     d  d_syst                        1    overlay(d_lrec:80)
     d  d_altk                        2    overlay(d_lrec:81)
     d  d_rest                        1    overlay(d_lrec:83)
     d  d_type                        1    overlay(d_lrec:84)
     d  d_enhe                        3    overlay(d_lrec:85)
3.11 d  d_inlr                        1    overlay(d_lrec:88)
3.11 d  d_lety                        1    overlay(d_lrec:89)
6.nu d  d_onum                        8  0 overlay(d_lrec:90)
6.nu d  d_olin                        4  0 overlay(d_lrec:98)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
      *
      * Definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
8.02 d fodtlu_numm     s                   like(fdnumm)
8.02 d fodtlu_suff     s                   like(fdsuff)
8.02 d fodtlu_line     s                   like(fdline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_line          s                   like(ldline)
     d w_enor          s              1
     d w_sald          s             11  2
6.30 d w_numa          s              8
5.02 d w_lina          s              4
6.32 d w_mld1          s             50    inz(*blank)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Klargjøring av data for A1BLD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      a1numm = p_numm
     c                   eval      a1suff = p_suff
     c                   eval      a2suff = *zero
      *
      * Merk av at basisordre nå er til behandling for samkjøring
      *
     c                   eval      lohelu_numm = a1numm
     c                   eval      lohelu_suff = a1suff
     c                   exsr      xmerk_ord
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1 Behandler skjembilde for samkjøring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      * Gå tilbake til forrige bilde
      *
     c                   if        *inkc
     c                   eval      lohelu_numm = a1numm
     c                   eval      lohelu_suff = a1suff
     c                   exsr      xopph_ord
     c                   goto      xslutt
     c                   endif
      *
      * Sjekk om ordre med oppgitt suffix ble funnet
      *
     c                   eval      lohel1_numm = a1numm
     c                   eval      lohel1_suff = a2suff
     c     lohel1_key    chain     lohel1r                            90
      *
     c                   if        *in90  = *on
     c                   eval      *in31  = *on
     c                   goto      a1tagb
     c                   endif
      *
      * Er suffix ordre til behandling ?
      *
     c                   if        lokode <> 0
     c                   eval      *in32  =  *on
     c                   goto      a1tagb
     c                   endif
      *
      * Merk av at suffix-ordre nå er til behandling for samkjøring
      *
     c                   eval      lohelu_numm = a1numm
     c                   eval      lohelu_suff = a2suff
     c                   exsr      xmerk_ord
      *
      * Behandle samkjøringen
      *
     c                   exsr      xoppd_ord
      *
      * Merk av at ordre nå er ferdigbehandlet
      *
     c                   eval      lohelu_numm = a1numm
     c                   eval      lohelu_suff = a1suff
     c                   exsr      xopph_ord
      *
      * Avslutt program
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for merking av at basisordre er i bruk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xmerk_ord     begsr
      *
     C                   call      'LO709R'
     C                   parm                    lohelu_numm
     C                   parm                    lohelu_suff
      *
     C     lohelu_key    chain     lohelur                            91
6.31 C                   if        %found
     C                   eval      lokode = 1
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     C                   update    lohelur
6.31 C                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppheving av merking
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopph_ord     begsr
      *
     C                   call      'LO709R'
     C                   parm                    lohelu_numm
     C                   parm                    lohelu_suff
      *
     C     lohelu_key    chain     lohelur                            91
     C                   eval      lokode = 0
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     C                   update    lohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av ordre (sam-kjøring)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xoppd_ord     begsr
      *
      * Tilbakefør basisordre
      *
     c                   eval      lohel1_numm = a1numm
     c                   eval      lohel1_suff = a1suff
     c     lohel1_key    chain     lohel1r                            90
      *
     c                   eval      lodtl1_numm = a1numm
     c                   eval      lodtl1_suff = a1suff
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1r                                90
      *
     c                   dow       *in90  = *off
3.31 c                   eval      ldanta =  (ldanta * -1)
     c                   exsr      xtilbake
     c     lodtl1_key    reade     lodtl1r                                90
     c                   enddo
      *
      * Oppdatere memosaldo basisordre
      *
5.60  ***>               eval      w_sald = lototr - lototi
      *
5.60  ***>               call      'FA922R'
5.60  ***>               parm                    w_firm
5.60  ***>               parm                    lootyp
5.60  ***>               parm                    loldor
5.60  ***>               parm                    w_sald
      *
      * Tilbakefør suffix-ordre
      *
     c                   eval      lohel1_numm = a1numm
     c                   eval      lohel1_suff = a2suff
     c     lohel1_key    chain     lohel1r                            90
      *
     c                   eval      lodtl1_numm = a1numm
     c                   eval      lodtl1_suff = a2suff
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1r                                90
      *
     c                   dow       *in90  = *off
3.31 c                   eval      ldanta =  (ldanta * -1)
     c                   exsr      xtilbake
     c     lodtl1_key    reade     lodtl1r                                90
     c                   enddo
      *
      * Oppdatere memosaldo suffix-ordre
      *
5.60  ***>               eval      w_sald = lototr - lototi
      *
5.60  ***>               call      'FA922R'
5.60  ***>               parm                    w_firm
5.60  ***>               parm                    lootyp
5.60  ***>               parm                    loldor
5.60  ***>               parm                    w_sald
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hente suffix-linjer inn til basis-ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finn siste linjenr på basisordre
      *
     c                   eval      lodtlu_numm = a1numm
     c                   eval      lodtlu_suff = a1suff
     c                   eval      lodtlu_line = 9999
     c     lodtlu_key    setll     lodtl1r
     c                   eval      *in90 = *off
     c                   readp     lodtl1r                                90
      *
6.32  * Feilmelding dersom linjenr for stort for reversering
6.32 c                   if        ldline > 9900 or
6.32 c                             ldline = 9900
6.32 c                   eval      w_mld1 = 'Best. for stor til å +
6.32 c                                        kunne reversere!    '
6.32 c                   call      'AA005R'
6.32 c                   parm                    w_mld1
6.32 c                   goto      xslutt
6.32 c                   endif
6.32  *
     c                   eval      w_line = 0010
     c                   if        *in90  = *off
     c                   if        ldfirm = w_firm
     c                   if        ldnumm = a1numm
     c                   if        ldsuff = a1suff
     c                   eval      w_line = ldline
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * Les suffixlinjer, og flytt de over til basisordre
      *
     c                   eval      lodtl1_numm = a1numm
     c                   eval      lodtl1_suff = a2suff
     c     lodtl1_key    setll     lodtlu
     c     lodtl1_key    reade     lodtlur                                90
      *
     c                   dow       *in90  = *off
     c                   eval      w_line = w_line + 10
     c                   eval      ldsuff = a1suff
     c                   eval      ldline = w_line
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlur
8.02  * Oppdater tilhørende ordrelinjer med ny bestillingsreferanse
8.02 c                   if        ldornu <> 0
8.02 c                   eval      fodtlu_numm = ldornu
8.02 c                   eval      fodtlu_suff = ldorsu
8.02 c                   eval      fodtlu_line = ldorli
8.02 c     fodtlu_key    chain     fodtlu
8.02 c                   if        %found
8.02 c                   eval      fdbsuf = ldsuff
8.02 c                   eval      fdblin = ldline
8.02 c                   time                    fdedat
8.02 c                   time                    fdetim
8.02 c                   eval      fdeusr = l_user
8.02 c                   update    fodtlur
8.02 c                   endif
8.02 c                   endif
      *
     c     lodtl1_key    reade     lodtlur                                90
     c                   enddo
      *
      * Slett deretter ordrehode på suffix-ordre
      *
     c                   eval      lohelu_numm = a1numm
     c                   eval      lohelu_suff = a2suff
     c     lohelu_key    chain     lohelur                            90
     c                   if        *in90 = *off
     c                   delete    lohelur
     c                   endif
8.01  *
8.01 c     lohelu_key    chain     lotii2
8.01 c                   if        %found
8.01 c                   delete    lotii2r
8.01 c                   endif
3.31  *
3.31  * Oppdatere ny sammenkjørt basisordre
3.31  *
3.31 c                   eval      lohel1_numm = a1numm
3.31 c                   eval      lohel1_suff = a1suff
3.31 c     lohel1_key    chain     lohel1r                            90
3.31  *
3.31 c                   eval      lodtl1_numm = a1numm
3.31 c                   eval      lodtl1_suff = a1suff
3.31 c     lodtl1_key    setll     lodtl1
3.31 c     lodtl1_key    reade     lodtl1r                                90
3.31  *
3.31 c                   dow       *in90  = *off
3.31 c                   exsr      xtilbake
3.31 c     lodtl1_key    reade     lodtl1r                                90
3.31 c                   enddo
3.31  *
3.31  * Oppdater ny sammenkjørt basis-ordre
3.31  *
5.60  ***> Ikke oppdater hent inn pris på nytt
5.60  ***>               eval      w_enor = '1'
5.60 c                   eval      w_enor = ' '
3.31 c                   call      'LO730R'
3.31 c                   parm                    w_enor
3.31 c                   parm                    w_firm
3.31 c                   parm                    a1numm
3.31 c                   parm                    a1suff
3.31  *
3.31  * Oppdatere memosaldo basisordre
3.31  *
5.60  ***>               eval      w_sald = lototi - lototr
3.31  *
5.60  ***>               call      'FA922R'
5.60  ***>               parm                    w_firm
5.60  ***>               parm                    lootyp
5.60  ***>               parm                    loldor
5.60  ***>               parm                    w_sald
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakeføring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xtilbake      begsr
      *
      * Tilbakeføring
      *
     c                   if        laalag <> 0
      *
     c                   eval      d_vare =  ldvare
5.31 c                   eval      d_lage =  ldlage
     c                   eval      d_enhe =  ldenh1
     c                   eval      d_dato =  *loval
     c                   eval      d_time =  *loval
     c                   eval      d_otyp =  lootyp
     c                   eval      d_ltyp =  ldltyp
      *
     c                   eval      d_anta =  ldanta
     c                   eval      d_kopr =  ldkpny
      *
     c                   eval      d_refr =  *blank
     c                   eval      d_syst =  'L'
     c                   eval      d_altk =  *blank
     c                   eval      d_rest =  *blank
     c                   move      ldnumm        w_numa
     c                   move      ldline        w_lina
     c                   eval      d_refr = 'B:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      d_onum = ldnumm
     c                   eval      d_olin = ldline
3.11 c                   move      ldlety        d_lety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec =  d_lrec
      *
     c                   call      'VL001R'
     c                   parm                    w_v001            1
6.nu c                   parm                    w_lrec          128
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for Ordre-hode-tabell les
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for Ordre-hode-tabell update
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for Ordre-linje-tabell les
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for Ordre-linje-tabell update
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
8.02  *
8.02  * Key for Bestillings-linje-tabell update
8.02  *
8.02 c     fodtlu_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    fodtlu_numm
8.02 c                   kfld                    fodtlu_suff
8.02 c                   kfld                    fodtlu_line
      *
      * Key for Status-tabell les
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm      = l_firm
      *
      * Henter informasjon fra status-tabell
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
      *
