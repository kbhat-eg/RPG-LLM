     H/TITLE  ASLAGR: Seleksjon for utskrift av inngående faktura
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO117R                                       *
      *  Beskrivelse . . : Seleksjon for utskrift av inng. faktura      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
6.nu  *  R6.30      110614  Utvidelse i forb. nummerutvidelse      bof  *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     FSIHEL2    IF   E           K DISK
     F                                     RENAME(SIHEPFR:SIHEL2R)
      *
      *  Parameter inn fra startprogram
      *
     D                 DS
      *                                                      Parameter-inn
     D  DSPARM                 1     28
     D  DSAARR                 1      4  0
     D  DSOTYP                 5      6
     D  DSBATC                 7     12  0
     D  DSPROG                19     28
      *
      *  Parameter for utskriftsprogram
      *
     D                 DS
      *                                                      Utskriftsprogram
     D  DXPARM                 1     28
     D  DXAARR                 1      4  0
     D  DXOTYP                 5      6
6.nu D  DXFFNR                 7     14  0
6.nu D  DXTFNR                15     22  0
6.nu D  DXPROG                23     32
     D/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  LOCAL DATA AREA                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                UDS
     D  DSSFIR               944    946  0
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Skrive inng_faktura    Plukker ut bilag                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         SHEBIN
     C                   MOVE      *ZERO         SHENUM
     C                   MOVE      *ZERO         SHESUF
     C     SHEKEY        SETLL     SIHEL2R
      *
     C     TAG01         TAG
     C                   MOVE      *OFF          *IN90
     C                   READ      SIHEL2R                                90
      *
     C     *IN90         IFEQ      *OFF
     C     SHFIRM        IFEQ      SHEFIR
     C     SHAARR        IFEQ      SHEAAR
     C     SHBATC        IFEQ      SHEBAT
      *
      *  Legger inn f.o.m. bilagsnr inn i teller fra
      *
     C     WPFFNR        IFEQ      *ZERO
     C                   Z-ADD     SHBINR        WPFFNR
     C                   Z-ADD     SHBINR        TELLER
     C                   SUB       1             TELLER
     C                   END
      *
      *  Adderer 1 til teller for sjekk av rekkefølge
      *
     C                   ADD       1             TELLER
      *
      *  Dersom neste ikke er i rekkefølge skriv ut
      *
     C     SHBINR        IFNE      TELLER
     C                   SUB       1             TELLER
     C                   Z-ADD     TELLER        WPTFNR
     C                   Z-ADD     SHBINR        TELLER
     C                   EXSR      SKRIV
     C                   Z-ADD     TELLER        WPFFNR
     C                   MOVE      *ZERO         WPTFNR
     C                   END
      *
     C                   GOTO      TAG01
     C                   END
     C                   END
     C                   END
     C                   END
      *
      *  Skriv ut siste fra/til
      *
     C                   Z-ADD     TELLER        WPTFNR
     C                   EXSR      SKRIV
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  SKRIV     Kaller opp program for utskrift av inng.faktura      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   SKRIV         BEGSR
      *
      *  Nullstiller hjelpefelter
      *
     C                   MOVE      WPFFNR        DXFFNR
     C                   MOVE      WPTFNR        DXTFNR
      *
     C                   CALL      DSPROG
     C                   PARM                    DXPARM
      *
     CSR                 ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
6.nu C                   MOVE      *ZERO         WPFFNR            8 0
6.nu C                   MOVE      *ZERO         WPTFNR            8 0
     C                   MOVE      *ZERO         WNESTE            6 0
6.nu C                   MOVE      *ZERO         TELLER            8 0
      *
      *  Redefinering av felter
      *
      *  Redefinering av parameterfeltene
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    SHFIRM        SHEFIR
     C     *LIKE         DEFINE    SHAARR        SHEAAR
     C     *LIKE         DEFINE    SHBATC        SHEBAT
     C     *LIKE         DEFINE    SHBINR        SHEBIN
     C     *LIKE         DEFINE    SHNUMM        SHENUM
     C     *LIKE         DEFINE    SHSUFF        SHESUF
      *
      *  Key for file : BESTILLINGS-INNKJØP-STATISTIKKHODE-REGISTER
      *
     C     SHEKEY        KLIST
     C                   KFLD                    SHEFIR
     C                   KFLD                    SHEAAR
     C                   KFLD                    SHEBAT
     C                   KFLD                    SHEBIN
     C                   KFLD                    SHENUM
     C                   KFLD                    SHESUF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPARM           28
     C                   MOVE      WPPARM        DSPARM
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        SHEFIR
      *
      *  Legger inn år og batchnummer i nøkklene
      *
     C                   MOVE      DSAARR        SHEAAR
     C                   MOVE      DSBATC        SHEBAT
      *
      *  Legger inn år og ordretype og program-navn i ut-parameter
      *
     C                   MOVE      DSAARR        DXAARR
     C                   MOVE      DSOTYP        DXOTYP
     C                   MOVE      DSPROG        DXPROG
      *
     CSR                 ENDSR
