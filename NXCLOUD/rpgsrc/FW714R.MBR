      /title  ASOFAK Lev.vare, henter vareregister - Sandvik
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW714R
      * Beskrivelse . . : Danning av leverandør vareregister, Sandvik
      *
      *                   Enhet settes lik STK, og prisdato settes
      *                   lik dagens dato
      *
      *                   Henter salgspris fra vare dersom denne finnes
      *                   i EVR
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220800 HKO  Nytt program
5.70  * PRGR  211108 BOF  utvidet med prisgruppe
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Sandvik
      *
     d                 ds
     d d_inpu                       124
     d  d_side                        5    overlay(d_inpu:1)                    Side
     d  d_vare                       24    overlay(d_inpu:6)                    Varenr
     d  d_tek1                       30    overlay(d_inpu:30)                   Varetekst
     d  d_eann                       13    overlay(d_inpu:60)                   EAN-nr
     d  d_brpr                        9    overlay(d_inpu:73)                   Grosistpris
     d  d_vgrp                        3    overlay(d_inpu:82)                   Varegruppe
     d  d_vekt                        8    overlay(d_inpu:85)                   Vekt
     d  d_anta                        6    overlay(d_inpu:93)                   Antall i pakning
     d   d_bmen                       6  0 overlay(d_anta:1)                    Antall i pakning
     d  d_inpr                        9    overlay(d_inpu:99)                   Innkjøpspris
     d   d_inpr_kr                    6  0 overlay(d_inpr:1)                    Innkjøpspris
     d   d_inpr_Øre                   2  2 overlay(d_inpr:8)                    Innkjøpspris
     d  d_best                        7    overlay(d_inpu:108)                  BVestillingsnr.
     d  d_rab1                        5    overlay(d_inpu:115)                  Rabatt-1
     d  d_rab2                        5    overlay(d_inpu:120)                  Rabatt-2
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_ldor          s                   like(vvldor)
      *
      * Definering av variabler i nøkler
      *
     d vvarl5_lvar     s                   like(vvlvar)
      *
      * Definering av variable
      *
     d w_lety          s              1  0
     d w_inlr          s              1    inz(*off)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(fwfirm)
     d w_ldor          s                   like(fwldor)
     d w_bupr          s                   like(fwinpr)
     d w_kopr          s                   like(fwinpr)
     d w_inpr          s                   like(fwinpr)
8.01 d w_prgr          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
     c     ' ':'0'       xlate     d_anta        d_anta
     c     ' ':'0'       xlate     d_inpr        d_inpr
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = %trim(d_vare)
     c                   eval      fweann = 0
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = 'STK'
     c                   eval      fwinpr = 0
     c                   eval      fwsapr = 0
     c                   eval      fwdato = w_udat
     c                   eval      fwbmen = 0
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   move      d_eann        fweann
      *
     c                   eval      fwbmen = d_bmen
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
      *
      * Dersom vare finne i EVR, hentes salgspris fra vare
      *
     c                   if        fwlvar <> *blank
      *
     c                   eval      vvarl5_lvar = fwlvar
     c     vvarl5_key    chain     vvarl5                             91
     c                   if        *in91 = *off
     c                   call      'VP701R'
     c                   parm                    vvfirm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    fwenhe
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    w_inlr
     c                   parm                    fwsapr
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
     c                   endif
      *
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på vare-register med leverandørs varenummer
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    vvarl5_lvar
      *
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R'
5.70 c                   parm                    w_prgr
      *
      * Henter firma og leverandør fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
     c                   endsr
