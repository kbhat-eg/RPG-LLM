     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . :NXCLOUD
      * Program . . . . : DANNJVLE
      * Program . . . . : BO.NX20653
      * Beskrivelse . . : Leser lokale priser for alle untatt
      *                   900001, 202251, danne JVLEPF der hvor det mangler
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270125 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *

     fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
     fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
      * Definering av datastruktur
      *
      *
      * Definering av variable i nøkler
     d jvlelu_lvnr     s                   like(jvlvnr)
     d jvlelu_lldo     s                   like(jvlldo)
     d jvlel1_lvnr     s                   like(jvlvnr)
     d jvlel1_lldo     s                   like(jvlldo)
      *

      *
      * Definering av variable
      *

     d b_eof           s              1    inz(*off)
      *
      *
      * Definering av konstanter
      *

      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c/exec sql
     c+ declare pris cursor for
     c+ select jxvare, jxldor, jvlvar
     c+ from   jvprpf
     c+ inner join jvarpf on jxvare = jvvare
     c+ where jxldor not in (900001, 202251) and
     c+ JXRSTS = 1
     c+ for read only
     c/end-exec
     c/exec sql
     c+ open pris
     c/end-exec
      *
     c                   dow       b_EOF = *off
      *
     c/exec sql
     c+ fetch pris
     c+ into  :jxvare, :jxldor, :jvlvar
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c/exec sql
     c+  close pris
     c/end-exec
     c                   leave
     c                   endif
      *
     c                   eval      jvlel1_lvnr = jxvare
     c                   eval      jvlel1_lldo = jxldor
     c     jvlel1_key    chain     jvlel1
     c                   if        not %found(jvlel1)
     c                   exsr      dann_jvle
     c                   endif

     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne std. påslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_jvle     begsr
      *
     c                   clear                   jvlelur
      *
5.70 c                   eval      jvlelu_lvnr = jxvare
5.70 c                   eval      jvlelu_lldo = jxldor
     c     jvlelu_key    chain     jvlelur
     c                   if        not %found(jvlelu)
     c                   eval      jvlvnr = jxvare
     c                   eval      jvlldo = jxldor
     c                   eval      jvllva = jvlvar
     c                   eval      jvlous = l_user
     c                   time                    jvloda
     c                   time                    jvloti
     c                   eval      jvleus = l_user
     c                   time                    jvleda
     c                   time                    jvleti
     c                   write     jvlelur
     c                   endif
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for oppdatering av vare-leverandør
      *
     c     jvlelu_key    klist
     c                   kfld                    jvlelu_lvnr
     c                   kfld                    jvlelu_lldo
      *
      * Key for les av vare-leverandør
      *
     c     jvlel1_key    klist
     c                   kfld                    jvlel1_lvnr
     c                   kfld                    jvlel1_lldo
      *
     c                   endsr
