      /TITLE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL109R                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           310195  Nytt program april -2024                      *
      *                                                                 *
      *           190796  D.S. BATCH INNEHOLDER LØNN OG FAKT.           *
      *                   SPLITTER VED Å LEGGE INN BETFOR99 OG          *
      *                   BETFOR00.  ENDRINGER ER MERKET MED "ASP1".    *
      *                                                                 *
      *           191196  D.S. BATCH INNEHOLDER FORSKJELLIGE            *
      *                   FIRMAER DERSOM DET BRUKES LIKT FORE-          *
      *                   TAKSNUMMER. ULIKE FIRMAER MÅ LEGGES OVER      *
      *                   FORSKJELLIGE BATCHER.                         *
      *                   ENDRINGER ER MERKET MED "ASP2".               *
      *                                                                 *
      *           210597  VED NY RECORDTYPE MÅ DET TESTES PÅ OM         *
      *                   OCR-RETUREN ER MOTTATT TIDLIGERE.             *
      *                   FØR BLE DENNE TESTEN GJORT VED RECORDTYPE     *
      *                   "20", MENS DET RIKTIGE ER VED RECORDTYPE "10" *
      *                   ENDRINGER ER MERKET MED "ASP3".               *
      *                                                                 *
      * 22098     220998  Lagt inn behandling av inkassoreturer.        *
      *                                                                 *
      * 15110     151100  Lagt inn mulighet for at et firma kan ha      *
      *                   flere bankkontoer.                            *
      *                                                                 *
      * 18120     181200  Legger ut FILGRUPPE og FIRMA i nye felter     *
      *                   på file GATRPF.                               *
      *                                                                 *
      * 17011     170101  Legger ut SONEKONTONUMMER i feltet for        *
      *                   BANKGIRONUMMER på file GATRPF.                *
      *                                                                 *
T5.20 * v5.20     081204  Filgruppe/firma ble lagt ut feil når          *
T5.20 *                   flere firma med samme foretaksid. (DnB)       *
      *                                                                 *
 7.01 *  k00  240420 bsa Tillater bredere skjermbilde                   *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    60-69 = Fileindikatorer chain etc.                           *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgl109d    cf   e             workstn
     fgatrl2    if   e           k disk
     f                                     rename(gatrpfr:gatrpf2)
22098fginkpf    uf   e             disk
22098fgisopf    uf a e           k disk
     fgtgipf    uf   e             disk
     fgocrpf    uf   e             disk
     fgavtlu    uf   e           k disk
     f                                     rename(gavtpfr:gavtpfu)
22098fgavtl2    uf   e           k disk
     f                                     rename(gavtpfr:gavtpf2)
     fgavtl4    uf   e           k disk
     f                                     rename(gavtpfr:gavtpf4)
     fgatrpf    o  a e             disk
     f                                     rename(gatrpfr:gatrpfp)
     fgl109p    o    e             printer
      *
      /EJECT
      *
      *  Generell datastruktur for BETFOR-transaksjon på GTGIPF:
      *
     d                 ds
     d dsbexx                        80
     d  dsreko                        2  0 overlay(dsbexx:4)
     d  dsbetf                        8    overlay(dsbexx:41)
ASP1  *
ASP1  *  Generell datastruktur for LAGRING AV FOREGÅENDE LESTE POST:
ASP1  *
ASP1 d                 ds
     d dsprev                        80
     d  dsprre                        2  0 overlay(dsprev:4)
     d  dsprbe                        8    overlay(dsprev:41)
     d  dsprkt                       11    overlay(dsprev:60)
      *
      *  Datastruktur for å hente frem verdien i fra feltet
      *  "Transaksjonstype" på BETFOR21.
      *
     d                 ds
     d dsbe21                        80
     d  dstrty                        1    overlay(dsbe21:27)
      *
      *  Generell datastruktur for BETFOR01- og BETFOR21-
      *  transaksjon på GTGIPF:
      *
     d                 ds
     d dsbeko                        80
     d  dskont                       11  0 overlay(dsbeko:60)
      *
      *  Datastruktur for å hente ut kontonummer i fra sist
      *  lest post:
      *
     d                 ds
     d dsinpo                        80
     d  dsinkt                       11  0 overlay(dsinpo:60)
      *
      *  Datastruktur for Mottaksdato:
      *
     d                 ds
     d dsdato                         8  0
     d  dsmotÅ                        4  0 overlay(dsdato)
     d  dsmotm                        2  0 overlay(dsdato:5)
     d  dsmotd                        2  0 overlay(dsdato:7)
      *
      *  Generell datastruktur for OCR-transaksjon:
      *
     d                 ds
     d dsocrt                       103
     d  dsocrr                        2    overlay(dsocrt)
      *
      *  Datastruktur for OCR-transaksjon fra BBS:
      *
      *  (T=Hele Transaksjonen, R=Recordtype, D=Dato, K=Kontonummer)
      *
     d                 ds
     d dsbbst                        80
     d  dsbbsr                        2  0 overlay(dsbbst:7)
     d  dsbbsf                        7  0 overlay(dsbbst:17)
     d  dsbbso                        7  0 overlay(dsbbst:18)
     d  dsbbsk                       11  0 overlay(dsbbst:25)
      *
      *  Generell datastruktur for OCR-transaksjon fra POST:
      *  (Inneholder 2 poster)
      *
     d                 ds
     d dspos2                       103
     d  dsposa                       51    overlay(dspos2)
     d  dsposb                       51    overlay(dspos2:53)
      *
      *  Datastruktur for OCR-transaksjon fra POST:
      *
      *  (T=Hele Transaksjonen, R=Recordtype, D=Dato, K=Kontonummer)
      *
     d                 ds
     d dspost                        51
     d  dsposr                        1  0 overlay(dspost)
     d  dsposd                        6  0 overlay(dspost:2)
     d  dsposk                        7  0 overlay(dspost:8)
ASP1  *
ASP1  *  Generell datastruktur for BETFOR00-TRANSAKSJON (1 AV 4):
ASP1  *
ASP1 d                 ds
     d dsb00a                        80
     d  dsbe00                        8    overlay(dsb00a:41)
ASP1  *
ASP1  *  Generell datastruktur for BETFOR00-TRANSAKSJON (2 AV 4):
ASP1  *
ASP1 d                 ds
     d dsb00b                        80
ASP1  *
ASP1  *  Generell datastruktur for BETFOR00-TRANSAKSJON (3 AV 4):
ASP1  *
ASP1 d                 ds
     d dsb00c                        80
ASP1  *
ASP1  *  Generell datastruktur for BETFOR00-TRANSAKSJON (4 AV 4):
ASP1  *
ASP1 d                 ds
     d dsb00d                        80
      *
      *  Generell datastruktur for INKASSO-retur:
      *
22098d                 ds
     d dsinkr                        80
     d  dzxcn1                        4    overlay(dsinkr)
     d  dzxcn2                        4    overlay(dsinkr:3)
     d  dzxcn3                        4    overlay(dsinkr:14)
      *
      *  Datastruktur for INKASSO-retur, CN1
      *
22098d                 ds
     d dsc1                          80
     d  dsc1fg                        3    overlay(dsc1:5)
     d  dsc1fi                        3  0 overlay(dsc1:8)
      *
      *  Datastruktur for INKASSO-retur, CN2
      *
22098d                 ds
     d dsc2                          80
     d  dsc2fg                        3    overlay(dsc2:7)
     d  dsc2fi                        3  0 overlay(dsc2:10)
      *
      *  Datastruktur for INKASSO-retur, CN3
      *
22098d                 ds
     d dsc3                          80
     d  dsc3fg                        3    overlay(dsc3:18)
     d  dsc3fi                        3  0 overlay(dsc3:21)
      *
      *  Henter firmanavn i fra LDA:
      *
     d                uds
     d dsfnav                951    980
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d dsbbsx          s              3
     d i               s              6  0
     d w_arow          s              2  0
     d w_acol          s              3  0
     d wpbbsf          s              7  0
     d wpbe22          s              3
     d wpbe99          s              2
     d wperr1          s              1
     d wperr2          s              1
     d wpfØrs          s              3
     d wpfgrp          s              3
     d wpfirm          s              3  0
     d wpinkt          s             11  0
     d wpkonr          s             11  0
     d wpkont          s             11  0
     d wplese          s              2
     d wpprkt          s             11  0
     d wpqity          s              3
     d wpsifg          s              3
     d wpsifi          s              3  0
     d wpsity          s              3
     d wptxt6          s              6
     d wptype          s              4
     d wpwind          s              1  0
     d z               s              6  0
      *---------------------------------------------------------------------
      * Stand Alone Fields - BOTTOM
      *---------------------------------------------------------------------
      *
      /EJECT
      *
      * Behandler transaksjoner fra Telegiro:
      *
     c                   exsr      xtrtgi
      *
      * Behandler transaksjoner fra OCR og BBS.  Initierer
      * hjelpefeltet WPLESE, som inneholde status på om
      * batch er godkjent for behandling eller ikke:
      *
      * Feltet D1KODB skrives bl.a. ut på rapport, og inneholder
      * verdien "X" dersom batchen er oppdatert tidligere.  Hvis
      * dette er tilfelle vil retur som er lest inn på file bli
      * slettet:
      *
     c                   movel     'JA'          wplese
     c                   eval      d1kodb = *blanks
     c                   exsr      xtrocr
      *
      * Behandler inkassoreturer:
      *
22098c                   movel     'JA'          wplese
22098c                   eval      d1kodb = *blanks
22098c                   exsr      xtrink
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
      * Sender infomelding dersom feil mot avtaleregister:
      *
     c                   if        wperr1 = '1'
      *                                                        .
      * Tester på om posisjonering er innenfor skjermbildet:   .
      *                                                        .
     c                   if        w_arow >= 14
     c                   exsr      xinit
     c                   endif
      *                                                        .
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   exfmt     x2win
     c                   endif
      *
      * Sender infomelding dersom oppdrag er lest inn tidligere:
      *
     c                   if        wperr2 = '1'
      *                                                        .
      * Tester på om posisjonering er innenfor skjermbildet:   .
      *                                                        .
     c                   if        w_arow >= 14
     c                   exsr      xinit
     c                   endif
      *                                                        .
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   exfmt     x3win
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å leses returadat fra telegiro.  En record er på  *
      * 80x4, d.v.s. filen må leses 4 ganger for å få inne hele posten: *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xtrtgi        begsr
      *
     c                   eval      wpwind = 0
      *
     c     xttag1        tag
      *
      *  Variabelen "I" inneholder antall poster lest på GTGIPF:
      *
     c                   eval      i = 0
      *
     c                   read      gtgipf                                 60
      *
     c     *in60         cabeq     *on           xttag9
      *
      * Sender ut informasjonsbilde kun en gang dersom det er poster.
      * Tester på om posisjonering er innenfor skjermbildet:
      *
     c                   if        w_arow >= 14
     c                   exsr      xinit
     c                   endif
      *
     c                   if        wpwind = 0
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   write     a1win
     c                   eval      wpwind = 1
     c                   endif
      *
      * Teller antall leste poster:
      *
     c                   eval      i = i + 1
      *
     c     xttag2        tag
      *
      * Legger del-1 av lest record over til datastruktur.  Denne
      * delen inneholder informasjon som forteller bl.a. type record:
      *
     c                   movel     gdqtgi        dsbexx
ASP1  *
ASP1  * Legger del-1 av lest record over til datastruktur FOR Å
ASP1  * TESTE OM LØNN ("L") DERSOM BETFOR21:
ASP1  *
ASP1 c                   movel     gdqtgi        dsbe21
      *
      * Tester om ny batch.  (I såfall var foregående post BETFOR99)
      * Ved ny batch må kontonummer hentes i enten BETFOR01- eller
      * BETFOR21-transaksjonen, for deretter å bygge nøkkel som skal
      * ut på file GATRPF:
      *
     c                   if        dsbetf = 'BETFOR00'
     c                             and wpbe99 = 'JA'
ASP1  *                                                              .
ASP1  * Legger lest record over til datastruktur:                    .
ASP1  *                                                              .
ASP1 c                   movel     gdqtgi        dsb00a
ASP1 c                   eval      z = 1
      *                                                              .
      * Leser inn de 3 neste postene for å få inn hele recorden:     .
      *                                                              .
     c                   do        3
     c                   read      gtgipf                                 60
     c     *in60         cabeq     *on           xttag9
     c                   eval      i = i + 1
ASP1 c                   eval      z = z + 1
      *                                                      .       .
ASP1 c                   if        z = 2
ASP1 c                   movel     gdqtgi        dsb00b
ASP1 c                   endif
      *                                                      .       .
ASP1 c                   if        z = 3
ASP1 c                   movel     gdqtgi        dsb00c
ASP1 c                   endif
      *                                                      .       .
ASP1 c                   if        z = 4
ASP1 c                   movel     gdqtgi        dsb00d
ASP1 c                   endif
      *                                                      .       .
     c                   enddo
      *                                                              .
      * Initierere hjelpefelt:                                       .
      *                                                              .
     c                   eval      wpbe99 = *blanks
      *                                                              .
      * Leser GTGIPF inntil neste post er BETFOR01- eller            .
      * BETFOR21-transaksjon, for å hente kontonummer som            .
      * skal belastes:                                               .
      *                                                              .
     c     xttag3        tag
     c                   read      gtgipf                                 60
     c     *in60         cabeq     *on           xttag9
      *                                                              .
      * Legger lest record over til datastruktur:                    .
      *                                                              .
     c                   movel     gdqtgi        dsbexx
      *                                                              .
      * Teller antall leste poster:                                  .
      *                                                              .
     c                   eval      i = i + 1
      *                                                              .
      * Ved BETFOR01 eller BETFOR21 må kontonr. hentes ut            .
      * v.h.a. datastruktur:                                         .
      *                                                              .
     c                   if        dsbetf = 'BETFOR01'
     c                             or dsbetf = 'BETFOR21'
     c                   movel     gdqtgi        dsbeko
     c                   endif
      *                                                              .
      * Leser inn de 3 neste postene for å få inn hele recorden:     .
      * Legger samtidig lest record over i datastruktur, for         .
      * senere å kunne teste på feltet "Transakssjonstype".          .
      *                                                              .
     c                   do        3
     c                   read      gtgipf                                 60
     c     *in60         cabeq     *on           xttag9
     c                   eval      i = i + 1
     c                   movel     gdqtgi        dsbe21
     c                   enddo
      *                                                              .
      * Henter kontonummer ved BETFOR01/BETFOR21:                    .
      *                                                              .
     c                   if        dsbetf = 'BETFOR01'
     c                             or dsbetf = 'BETFOR21'
      *                                                            . .
      * Ved BETFOR21, vil feltet DSTRTY (Transaksjonstype) ha      . .
      * verdien "F" dersom underliggende poster er Faktura, og     . .
      * verdien "L" dersom underliggende poster er Lønn.           . .
      *                                                            . .
     c                   movel     'NEI'         wpbe22
     c                   if        dsbetf = 'BETFOR21'
     c                             and dstrty = 'L'
     c                   movel     'JA '         wpbe22
     c                   endif
      *                                                            . .
      * Henter avtaleopplysninger.  Starter på les av ny batch     . .
      * dersom avtale ikke finnes.                                 . .
      *                                                            . .
     c     dskont        chain     gavtlu                             61
      *                                                            . .
     c                   if        *in61 = *on
     c                   eval      wplese = *blanks
      *                                                      .     . .
      * Sender ut melding om at avtale må opprettes:         .     . .
      *                                                      .     . .
     c                   eval      d1text = *blanks
     c                   movel     'Telegiro'    d1text
     c                   eval      d1bgir = dskont
     c                   eval      d1pgir = 0
      *                                                      .     . .
      * Legger ut "X" i D1KODA for å fortelle at avtalen for .     . .
      * dette oppdraget ikke finnes i avtaleregisteret.      .     . .
      * Feltet blir skrevet ut på rapporten:                 .     . .
      *                                                      .     . .
     c                   movel     'X'           d1koda
     c                   eval      d1kodb = *blanks
      *                                                      .     . .
      * Kvittering skrives ut for avtaler som må opprettes:  .     . .
      * PS!  SKriver først ut heading dersom dette ikke er   .     . .
      *      gjort tidligere:                                .     . .
      *                                                      .     . .
     c                   if        wperr1 = *blank
     c                   write     a1hdr
     c                   endif
      *                                                      .     . .
     c                   write     d1det                                30
      * Overflow ?                                           .     . .
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *                                                      .     . .
      * Setter "flagg" v/feil mot avtaleregister:            .     . .
      *                                                      .     . .
     c                   movel     '1'           wperr1
      *                                                      .     . .
     c                   goto      xttag1
      *                                                      .     . .
     c                   endif
      *                                                            . .
      * Avtale finnes:                                             . .
      *                                                            . .
     c                   movel     'JA'          wplese
      *                                                            . .
      * Bygger nøkkel som skal legges ut på GATRPF:                . .
      *                                                            . .
     c                   exsr      xavtgi
      *                                                            . .
      * Les records på GTGIPF tilbake til utgangspunktet:          . .
      *                                                            . .
     c                   eval      i = i - 1
      *                                                            . .
     c                   do        i
     c                   readp     gtgipf                                 60
     c     *in60         cabeq     *on           xttag9
     c                   enddo
      *                                                            . .
      * Første post er nå lest inn i minnet, og overføring         . .
      * av poster tilhørende denne batchen kan starte:             . .
      *                                                            . .
     c                   eval      i = 1
     c                   goto      xttag2
      *                                                            . .
     c                   else
      *                                                            . .
      * Fortsetter les inntil tilslag på at det er BETFOR01-       . .
      * eller BETFOR21-transaksjon:                                . .
      *                                                            . .
     c                   goto      xttag3
     c                   endif
      *                                                              .
     c                   endif
      *
      * Initierer hjelpefelt som forteller at inneværende record
      * er (en av mulige flere) sluttrecord(er) på batch.
      *
     c                   if        dsbetf = 'BETFOR99'
     c                   movel     'JA'          wpbe99
     c                   endif
      *
      * Dersom feltet WPLESE IKKE inneholder verdien "JA", så er
      * avtaleopplysninger for firmaet ikke funnet, og GTGIPF
      * skal leses frem til neste batch:
      *
     c     wplese        cabne     'JA'          xttag1
      *
      * ******************************************
      * Oppdaterer GATRPF med poster i fra GTGIPF:
      * ******************************************
      *
ASP1  * TEST PÅ OM BLANDINGSRETUR AV LØNN OG FAKTURAER:
ASP1  * ***********************************************
ASP1  *
ASP1  * ER LEST POST BETFOR21 OG FOREGÅENDE BETFOR22 ?
ASP1  *
ASP1 c                   if        dsbetf = 'BETFOR21'
ASP1 c                             and dsprbe = 'BETFOR22'
ASP1  *                                                      .
ASP1 c                   movel     'TL'          gbtype
ASP1 c                   movel     'BETFOR99'    dsbe00
ASP1  *                                                      .
ASP1 c                   movel     dsb00a        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00b        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00c        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00d        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1  * NY BATCH:                                            .
ASP1  *                                                      .
ASP1  * Henter NYTT LØPENUMMER:                              .
ASP1  *                                                      .
ASP1 c     dskont        chain     gavtlu                             61
ASP1 c                   if        *in61 = *off
ASP1 c                   eval      gclØpn = gclØpn + 1
ASP1 c                   update    gavtpfu
ASP1 c                   eval      gblØpn = gclØpn
ASP1 c                   eval      gbtell = 0
ASP1 c                   endif
ASP1  *                                                      .
ASP1 c                   movel     'TF'          gbtype
ASP1 c                   movel     'BETFOR00'    dsbe00
ASP1  *                                                      .
ASP1 c                   movel     dsb00a        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00b        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00c        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00d        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   endif
ASP1  *
ASP1  * ER LEST POST BETFOR21 FOR LØNN ("L") OG FOREGÅENDE BETFOR23 ?
ASP1  *
ASP1 c                   if        dsbetf = 'BETFOR21'
ASP1 c                             and dstrty = 'L'
ASP1 c                             and dsprbe = 'BETFOR23'
ASP1  *                                                      .
ASP1 c                   movel     'TF'          gbtype
ASP1 c                   movel     'BETFOR99'    dsbe00
ASP1  *                                                      .
ASP1 c                   movel     dsb00a        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00b        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00c        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00d        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1  * NY BATCH:                                            .
ASP1  *                                                      .
ASP1  * Henter NYTT LØPENUMMER:                              .
ASP1  *                                                      .
ASP1 c     dskont        chain     gavtlu                             61
ASP1 c                   if        *in61 = *off
ASP1 c                   eval      gclØpn = gclØpn + 1
ASP1 c                   update    gavtpfu
ASP1 c                   eval      gblØpn = gclØpn
ASP1 c                   eval      gbtell = 0
ASP1 c                   endif
ASP1  *                                                      .
ASP1 c                   movel     'TL'          gbtype
ASP1 c                   movel     'BETFOR00'    dsbe00
ASP1  *                                                      .
ASP1 c                   movel     dsb00a        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00b        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00c        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   movel     dsb00d        gbreco
ASP1 c                   eval      gbtell = gbtell + 1
ASP1 c                   write     gatrpfp
ASP1  *                                                      .
ASP1 c                   endif
      *
ASP2  *
ASP2  * Dersom det er firmaer som har likt foretaksnummer, så vil
ASP2  * batchen inneholde returer for flere firmaer.  Batchen må
ASP2  * splittes til egne Batcher som kun inneholder data for et
ASP2  * firma:
ASP2  *
ASP2  * Dette er tilfelle dersom lest post er en BETFOR21, OG
ASP2  * SAMTIDIG at foregående transaksjon er en BETFOR23 som har
ASP2  * tilhørt et ANNET firma:
ASP2  *
ASP2 c                   if        dsbetf = 'BETFOR21'
ASP2 c                             and dsprbe = 'BETFOR23'
ASP2  *                                                         .
ASP2  * Legger kontonummer i fra foregående post over i et      .
ASP2  * hjelpefelt:                                             .
ASP2  *                                                         .
ASP2 c                   movel     dsprkt        wpprkt
ASP2  *                                                         .
ASP2  * Henter ut kontonummer i fra inneværende lest post:      .
ASP2  *                                                         .
ASP2 c                   movel     gdqtgi        dsinpo
ASP2 c                   movel     dsinkt        wpinkt
ASP2  *                                                         .
ASP2  * Dersom foregående kontonummer er ULIKT kontonummer på   .
ASP2  * inneværende post, så er det nytt firma:                 .
ASP2  *                                                         .
ASP2 c                   if        wpprkt <> wpinkt
ASP2  *                                                      .  .
ASP2 c                   movel     'TF'          gbtype
ASP2 c                   movel     'BETFOR99'    dsbe00
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00a        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00b        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00c        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00d        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2  * Det er nytt firma.  Inneværende post må derfor legges.  .
ASP2  * over i DSBEKO, for å "maskere" ut kontonummer.  Dette.  .
ASP2  * skal videre brukes til oppslag mot avtaleregister:   .  .
ASP2  *                                                      .  .
ASP2 c                   movel     gdqtgi        dsbeko
ASP2  *                                                      .  .
ASP2  * Henter NYTT LØPENUMMER:                              .  .
ASP2  *                                                      .  .
ASP2 c     dskont        chain     gavtlu                             61
ASP2  *                                                      .  .
ASP2 c                   if        *in61 = *off
ASP2 c                   eval      gclØpn = gclØpn + 1
ASP2 c                   update    gavtpfu
ASP2 c                   eval      gblØpn = gclØpn
ASP2 c                   eval      gbtell = 0
ASP2  ************FØR      Z-ADDDSKONT    GBGIRO           O .  .
17011c                   eval      gbgiro = gcskto
T5.20c                   movel     gcfilg        gbfgrp
T5.20c                   eval      gbfirm = gcfirm
ASP2 c                   endif
ASP2  *                                                      .  .
ASP2  * NY BATCH:                                            .  .
ASP2  *                                                      .  .
ASP2 c                   movel     'TF'          gbtype
ASP2 c                   movel     'BETFOR00'    dsbe00
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00a        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00b        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00c        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2 c                   movel     dsb00d        gbreco
ASP2 c                   eval      gbtell = gbtell + 1
ASP2 c                   write     gatrpfp
ASP2  *                                                      .  .
ASP2 c                   endif
ASP2  *                                                         .
ASP2 c                   endif
      *
ASP1  *
ASP1  * "LAGRER" SIST LESTE POST:
ASP1 c                   movel     gdqtgi        dsprev
      *
      * Legger ut den første av 4 records:
      *
     c                   movel     gdqtgi        gbreco
     c                   eval      gbtell = gbtell + 1
     c                   write     gatrpfp
     c                   delete    gtgipfr
      *
      * Legger ut de tre siste av 4 records:
      *
     c                   do        3
     c                   read      gtgipf                                 60
     c     *in60         cabeq     *on           xttag9
     c                   movel     gdqtgi        gbreco
     c                   eval      gbtell = gbtell + 1
     c                   write     gatrpfp
     c                   delete    gtgipfr
     c                   enddo
      *
      * Gjøre deretter klar for les av neste record:
      *
     c                   goto      xttag1
      *
     c     xttag9        tag
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å leses returadata for OCR.  En record er på      *
      * 103x1, d.v.s. filen leses kun en gang for hele poste:           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xtrocr        begsr
      *
     c                   eval      wpwind = 0
      *
     c     xotag1        tag
      *
     c                   read      gocrpf                                 60
      *
     c     *in60         cabeq     *on           xtagut
      *
      * Sender opp infobilde dersom OCR-post finnes.
      * Tester på om posisjonering er innenfor skjermbildet:
      *
     c                   if        w_arow >= 14
     c                   exsr      xinit
     c                   endif
      *
     c                   if        wpwind = 0
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   write     a3win
     c                   eval      wpwind = 1
     c                   endif
      *
      * Leser file GOCRPF intil EOF:
      *
     c                   dow       *in60 = *off
      *                                                              .
      * Legger OCR-posten over i en generell datastruktur:
      *                                                              .
     c                   movel     gdqocr        dsocrt
      *                                                              .
      * Det må testes på om lest post er OCR i fra BBS eller POST.   .
      * Dersom BBS inneholder posten kun en post, mens det for       .
      * POST vil være 2 OCR-poster i recorden:                       .
      *                                                              .
     c                   if        dsocrr = 'NY'
     c                   movel     'BBS '        wptype
     c                   movel     gdqocr        gbreco
     c                   exsr      xbbsre
     c                   exsr      xskriv
      *                                                      .       .
     c                   else
     c                   movel     'POST'        wptype
      *                                                      .       .
      * Legger record over til datastruktur for å splitte ut .       .
      * 2 OCR-returer fra POST:                              .       .
      *                                                      .       .
     c                   movel     gdqocr        dspos2
      *                                                      .       .
      * Behandler post 1:                                    .       .
      *                                                      .       .
     c                   movel     dsposa        dspost
     c                   exsr      xposre
     c                   eval      gbreco = *blanks
     c                   movel     dsposa        gbreco
     c                   exsr      xskriv
      *                                                      .       .
      * Behandler post 2:                                    .       .
      *                                                      .       .
     c                   movel     dsposb        dspost
     c                   exsr      xposre
     c                   movel     dsposb        gbreco
     c                   exsr      xskriv
      *                                                      .       .
     c                   endif
      *                                                              .
      * Sletter post etter at den er lagt ut på GATRPF, ELLER        .
      * at den slettes p.g.a. at den er lagt ut tidligere:           .
      *                                                              .
     c                   if        wplese = 'JA'
     c                             or d1kodb = 'X'
     c                   delete    gocrpfr
     c                   endif
      * Hent neste post:                                             .
     c                   read      gocrpf                                 60
      *                                                              .
     c                   enddo
      *
     c     xtagut        tag
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å skrive ut post på GATRPF:                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xskriv        begsr
      *
      * Tester på om batcher er godkjent for behandling:
      *
     c                   if        wplese = 'JA'
      *                                                      .
      * Legger ut post i fra GOCRPF:                         .
      *                                                      .
     c                   eval      gbtell = gbtell + 1
      *                                                      .
     c                   write     gatrpfp
      *                                                      .
     c                   endif
      *
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av OCR-retur fra BBS:                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xbbsre        begsr
      *
      * Legger record over til datastruktur for OCR-retur BBS:
      *
     c                   movel     gdqocr        dsbbst
      *
      * Ved startrecord må Forsendelsesnummer hentes ut, for
      * senere å kontrollere om retur er mottatt tidligere.
      * Dette må gjøres for å unngå dobbel oppdatering av OCR-retur:
      *
     c                   if        dsbbsr = 10
     c                   eval      wpbbsf = dsbbsf
     c                   endif
      *
      * Start- og sluttrecorder skal IKKE behandles, KUN slettes:
      *
     c                   if        dsbbsr <> 10
     c                             and dsbbsr <> 88
     c                             and dsbbsr <> 89
      *                                                            .
      * Henter løpenummer ut i fra avtaleregister dersom lest      .
      * record er startrecord for oppdraget:                       .
      *                                                            .
     c                   if        dsbbsr = 20
     c                   movel     dsbbsk        wpkont
     c     wpkont        chain     gavtlu                             61
      *                                                          . .
     c                   if        *in61 = *off
      *                                                        . . .
      * Bygger nøkkel som skal legges ut på GATRPF:            . . .
      *                                                        . . .
     c                   exsr      xavocr
      *                                                        . . .
      * Avtale finnes IKKE:                                    . . .
      *                                                        . . .
     c                   else
     c                   eval      wplese = *blanks
     c                   eval      d1text = *blanks
     c                   movel     'OCR'         d1text
     c                   eval      d1bgir = wpkont
     c                   eval      d1pgir = 0
      *                                                        . . .
      * Kvittering skrives ut for avtaler som må opprettes:    . . .
      * PS!  SKriver først ut heading dersom dette ikke er     . . .
      *      gjort tidligere:                                  . . .
      *                                                        . . .
     c                   if        wperr1 = *blank
     c                             and wperr2 = *blank
     c                   write     a1hdr
     c                   endif
      *                                                        . . .
      * Legger ut oppdragsnummer i feltvariabel, rapport:      . . .
      *                                                        . . .
     c                   eval      d1oppd = wpbbsf
      *                                                        . . .
      * Legger ut "X" i D1KODA for å fortelle at avtalen for   . . .
      * dette oppdraget ikke finnes i avtaleregisteret:        . . .
      *                                                        . . .
     c                   movel     'X'           d1koda
     c                   eval      d1kodb = *blanks
      *                                                        . . .
     c                   write     d1det                                30
      * Overflow ?                                             . . .
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *                                                        . . .
      * Setter "flagg" v/feil mot avtaleregister:              . . .
      *                                                        . . .
     c                   movel     '1'           wperr1
      *                                                        . . .
     c                   goto      xbbs01
     c                   endif
     c                   endif
      *                                                            .
     c                   endif
      *
      * Sletter start- og sluttrecord:
      *
     c                   if        dsbbsr = 10
     c                             or dsbbsr = 88
     c                             or dsbbsr = 89
     c                   eval      d1kodb = *blanks
     c                   eval      wplese = *blanks
     c                   delete    gocrpfr
     c                   endif
      *
      * Tester på om OCR-retur allered er overført til GATRPF.
      * I såfall vil postene IKKE overføres.  Testen utføres ved
      * hver ny oppdragsrecord:
      *
ASP3 c                   if        dsbbsr = 10
      *                                                        .
      * D1KODB inneholder verdi dersom batch er lest inn før:  .
      *                                                        .
     c                   eval      d1kodb = *blanks
      * Lager nøkkel:                                          .
     c                   eval      wpkonr = dsbbsk
      ****                 Z-ADDDSBBSO    WPOPPD               .
     c                   eval      wpbbsf = wpbbsf
      *                                                        .
     c     key01         chain     gatrl2                             61
      *                                                        .
     c                   if        *in61 = *off
     c                   eval      wplese = *blanks
      *                                                      . .
      * Kvittering skrives ut for oppdrag som er lest inn    . .
      * tidligere.                                           . .
      * PS!  SKriver først ut heading dersom dette ikke er   . .
      *      gjort tidligere:                                . .
      *                                                      . .
     c                   if        wperr1 = *blank
     c                             and wperr2 = *blank
     c                   write     a1hdr
     c                   endif
      *                                                      . .
      * Legger ut kontonummer i feltvariabel, rapport:       . .
      *                                                      . .
     c                   eval      d1bgir = wpkont
     c                   eval      d1pgir = 0
      *                                                      . .
      * Legger ut oppdragsnummer i feltvariabel, rapport:    . .
      *                                                      . .
     c                   eval      d1oppd = wpbbsf
      *                                                      . .
      * Legger ut "X" i D1KODB for å fortelle at oppdraget   . .
      * allered er lest inn:                                 . .
      *                                                      . .
     c                   eval      d1koda = *blanks
     c                   movel     'X'           d1kodb
      *                                                      . .
      * Skriver detaljlinje på rapport:                      . .
      *                                                      . .
     c                   write     d1det                                30
      * Overflow ?                                           . .
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      * Setter "flagg":                                      . .
     c                   movel     '1'           wperr2
      *                                                      . .
     c                   endif
     c                   endif
      *
     c     xbbs01        tag
      *
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av OCR-retur fra POST:                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xposre        begsr
      *
      * SUMPOST skal ikke behandles, KUN slettes:
      *
     c                   if        dsposr <> 3
      *                                                            .
      * Henter løpenummer ut i fra avtaleregister dersom dette er  .
      * første leste OCR-POST.  Det søkes her opp med postgironr:  .
      *                                                            .
     c                   if        wpfØrs = 'JA '
     c                   movel     'NEI'         wpfØrs
     c                   eval      wpkont = dsposk
      *                                                          . .
     c     wpkont        chain     gavtl4                             61
      *                                                          . .
     c                   if        *in61 = *off
      *                                                        . . .
      * Bygger nøkkel som skal legges ut på GATRPF:            . . .
      *                                                        . . .
     c                   exsr      xavocr
      *                                                        . . .
      * Avtale finnes IKKE:                                    . . .
      *                                                        . . .
     c                   else
     c                   eval      wplese = *blanks
     c                   eval      d1text = *blanks
     c                   movel     'OCR'         d1text
     c                   eval      d1bgir = 0
     c                   eval      d1pgir = wpkont
      *                                                        . . .
      * Kvittering skrives ut for avtaler som må opprettes:    . . .
      * PS!  SKriver først ut heading dersom dette ikke er     . . .
      *      gjort tidligere:                                  . . .
      *                                                        . . .
     c                   if        wperr1 = *blank
     c                             and wperr2 = *blank
     c                   write     a1hdr
     c                   endif
      *                                                        . . .
      * Nullstiller oppdragsnummer.  Brukes kun v/OCR-retur    . . .
      * for bank (BBS):                                        . . .
      *                                                        . . .
     c                   eval      d1oppd = 0
      *                                                        . . .
      * Legger ut "X" i D1KODA for å fortelle at avtalen for   . . .
      * dette oppdraget ikke finnes i avtaleregisteret:        . . .
      *                                                        . . .
     c                   movel     'X'           d1koda
     c                   eval      d1kodb = *blanks
      *                                                        . . .
     c                   write     d1det                                30
      * Overflow ?                                             . . .
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *                                                        . . .
      * Setter "flagg" v/feil mot avtaleregister:              . . .
      *                                                        . . .
     c                   movel     '1'           wperr1
      *                                                        . . .
     c                   goto      xpos01
     c                   endif
     c                   endif
      *                                                            .
      * Dersom SUMPOST, så må det gjøres klar for å ta inn ny      .
      * retur på et annet gironummer/bokføringsdato:               .
      *                                                            .
     c                   else
      *                                                            .
      * WPFØRS indikerer at ved ny les av file GOCRPF vil det være .
      * en ny batch som skal behandles i.o.m. at inneværende post  .
      * er en SUMPOST:                                             .
      *                                                            .
     c                   movel     'JA '         wpfØrs
      *                                                            .
      * Initierer feltet WPLESE for å fortelle at SUMPOST som nå   .
      * behandles IKKE skal legges ut på GATRPF:                   .
      *                                                            .
     c                   eval      wplese = *blanks
      *                                                            .
      * Blanker feltet D1KODB (oppdatert tidligere...):            .
      *                                                            .
     c                   eval      d1kodb = *blanks
      *                                                            .
      * Sletter post dersom den er overført videre, ELLER          .
      * at den er overført tidligere.  Den blir IKKE slettet       .
      * dersom det er feil på avtaleregisteret.  Dersom det        .
      * siste er tilfelle gis det mulighet for å kjøre rutinen     .
      * på nytt uten å måtte legge de samme dataene inn i file     .
      * GOCRPF:                                                    .
      *                                                            .
     c                   if        d1koda <> 'X'
     c                   delete    gocrpfr
     c                   endif
      *                                                            .
      * Initierer feltet D1KODA (feil i avtaleregister...):        .
      *                                                            .
     c                   eval      d1koda = *blanks
      *                                                            .
     c                   endif
      *
      * Tester på om denne OCR-retur allerede finnes på GATRPF.
      * I såfall vil postene IKKE overføres.  Feltet WPFØRS har
      * kun verdien "NEI" under behandling av den første posten
      * i batchen, deretter gis feltet verdien *BLANKS:
      *
     c                   if        wpfØrs = 'NEI'
     c                   eval      wpfØrs = *blanks
      *                                                        .
      * Lager nøkkel (GB-feltene fikk verdi i rutine XAVOCR):  .
      *                                                        .
     c                   eval      wpkonr = gbgiro
     c                   eval      wpbbsf = gboppd
      *                                                        .
     c     key01         chain     gatrl2                             61
      *                                                        .
     c                   if        *in61 = *off
     c                   eval      wplese = *blanks
      *                                                      . .
      * Kvittering skrives ut for oppdrag som er lest inn    . .
      * tidligere.                                           . .
      *                                                      . .
      * PS!  SKriver først ut heading dersom dette ikke er   . .
      *      gjort tidligere:                                . .
      *                                                      . .
     c                   if        wperr1 = *blank
     c                             and wperr2 = *blank
     c                   write     a1hdr
     c                   endif
      *                                                      . .
      * Legger ut kontonummer i feltvariabel, rapport:       . .
      *                                                      . .
     c                   eval      d1bgir = 0
     c                   eval      d1pgir = wpkont
      *                                                      . .
      * Legger ut bokføringsdato i oppdragsnummer:           . .
      *                                                      . .
     c                   eval      d1oppd = dsposd
      *                                                      . .
      * Legger ut "X" i D1KODB for å fortelle at oppdraget   . .
      * allered er lest inn:                                 . .
      *                                                      . .
     c                   eval      d1koda = *blanks
     c                   movel     'X'           d1kodb
     c                   eval      d1text = *blanks
     c                   movel     'OCR'         d1text
      *                                                      . .
      * Skriver detaljlinje på rapport:                      . .
      *                                                      . .
     c                   write     d1det                                30
      * Overflow ?                                           . .
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      * Setter "flagg":                                      . .
     c                   movel     '1'           wperr2
      *                                                      . .
     c                   endif
     c                   endif
      *
     c     xpos01        tag
      *
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å initiere nøkkel + oppdatere avtaleregister      *
      * ved behandling av returer - TELEGIRO:                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xavtgi        begsr
      * Oppdaterer løpenummer:
     c                   eval      gclØpn = gclØpn + 1
     c                   update    gavtpfu
     c                   eval      gblØpn = gclØpn
      * Initierer de andre nøkkelverdiene:
     c                   eval      gbtell = 0
17011 *****FØR             Z-ADDDSKONT    GBGIRO
      *
      * TESTER PÅ OM DET ER 1.GANGS-, 2.GANGS- ELLER FEILRETUR:
      *
      * FAKTURARETUR:
      *
     c                   if        wpbe22 <> 'JA '
      *                                                        .
      * Feilretur:                                             .
      *                                                        .
     c                   if        dsreko <> 1
     c                             and dsreko <> 2
     c                   movel     'TFF'         gbtype
     c                   endif
      *                                                        .
      * 1. gangsretur:                                         .
      *                                                        .
     c                   if        dsreko = 1
     c                   movel     'TF1'         gbtype
     c                   endif
      *                                                        .
      * 2. gangsretur:                                         .
      *                                                        .
     c                   if        dsreko = 2
     c                   movel     'TF2'         gbtype
     c                   endif
      *                                                        .
      * LØNNSRETUR:                                            .
      *                                                        .
     c                   else
      *                                                        .
      * Feilretur:                                             .
      *                                                        .
     c                   if        dsreko <> 1
     c                             and dsreko <> 2
     c                   movel     'TLF'         gbtype
     c                   endif
      *                                                        .
      * 1. gangsretur:                                         .
      *                                                        .
     c                   if        dsreko = 1
     c                   movel     'TL1'         gbtype
     c                   endif
      *                                                        .
      * 2. gangsretur:                                         .
      *                                                        .
     c                   if        dsreko = 2
     c                   movel     'TL2'         gbtype
     c                   endif
      *                                                        .
     c                   endif
      *
      * Dato:
     c                   eval      dsmotÅ = 1900
     c                   if        uyear <= 90
     c                   eval      dsmotÅ = 2000
     c                   endif
     c                   move      uyear         dsmotÅ
     c                   movel     umonth        dsmotm
     c                   movel     uday          dsmotd
     c                   eval      gbdato = dsdato
      * Filgruppe og firma:
18120c                   movel     gcfilg        gbfgrp
18120c                   eval      gbfirm = gcfirm
      *
17011c                   eval      gbgiro = gcskto
      * Klokkeslett:
     c                   time                    gbklok
      *
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å initiere nøkkel + oppdatere avtaleregister      *
      * ved behandling av returer - OCR/BBS:                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xavocr        begsr
      * Avtale er OK:
     c                   movel     'JA'          wplese
      * Oppdaterer løpenummer:
     c                   eval      gclØpn = gclØpn + 1
      *
     c                   if        wptype = 'BBS'
     c                   update    gavtpfu
     c                   else
     c                   update    gavtpf4
     c                   endif
      *
     c                   eval      gblØpn = gclØpn
      *
      * Initierer de andre nøkkelverdiene:
      *
     c                   eval      gbtell = 0
      *
      * Bankgironummer fra avtaleregisteret skal ALLTID benyttes
FØR   * dersom dette er utfylt:
FØR   *
     c                   if        gcbgir <> 0
     c                   eval      gbgiro = gcbgir
     c                   else
     c                   eval      gbgiro = gcpgir
     c                   endif
      *
17011c                   eval      gbgiro = gcskto
      *
      * Dersom OCR-retur fra BBS, så skal forsendelses legges ut.
      * Dersom OCR-retur fra POST, så skal bokføringsdato legges ut.
      *
     c                   if        wptype = 'BBS'
     c                   eval      gboppd = wpbbsf
     c                   else
     c                   eval      gboppd = dsposd
     c                   endif
      * Legger inn kode OCR:
     c                   movel     'OCR'         gbtype
      * Dato:
     c                   eval      dsmotÅ = 1900
     c                   if        uyear <= 90
     c                   eval      dsmotÅ = 2000
     c                   endif
     c                   move      uyear         dsmotÅ
     c                   movel     umonth        dsmotm
     c                   movel     uday          dsmotd
     c                   eval      gbdato = dsdato
      * Filgruppe og firma:
18120c                   movel     gcfilg        gbfgrp
18120c                   eval      gbfirm = gcfirm
      * Klokkeslett:
     c                   time                    gbklok
      *
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
22098 * Subrutine for å leses inkassoreturer:                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xtrink        begsr
      *
     c                   eval      wpwind = 0
      *
     c     xitag1        tag
      *
     c                   read      ginkpf                                 60
      *
      * Leser file GINKPF intil EOF:
      *
     c                   dow       *in60 = *off
      *                                                              .
      * Legger inkassopost over i generell datastruktur for å teste  .
      * på type retur:                                               .
      *                                                              .
     c                   movel     gdqink        dsinkr
     c                   eval      wpqity = *blanks
     c                   eval      wpfgrp = *blanks
     c                   eval      wpfirm = 0
      * CN1 ?                                                        .
     c                   if        dzxcn1 = 'ZXCN'
     c                   movel     gdqink        dsc1
     c                   movel     'CN1'         wpqity
     c                   movel     dsc1fg        wpfgrp
     c                   eval      wpfirm = dsc1fi
     c                   else
      * CN2 ?                                                .       .
     c                   if        dzxcn2 = 'ZXCN'
     c                   movel     gdqink        dsc2
     c                   movel     'CN2'         wpqity
     c                   movel     dsc2fg        wpfgrp
     c                   eval      wpfirm = dsc2fi
     c                   else
      * CN3 ?                                                .       .
     c                   if        dzxcn3 = 'ZXCN'
     c                   movel     gdqink        dsc3
     c                   movel     'CN3'         wpqity
     c                   movel     dsc3fg        wpfgrp
     c                   eval      wpfirm = dsc3fi
     c                   endif
     c                   endif
     c                   endif
      *                                                              .
     c                   movel     gdqink        gdqiso
     c                   movel     wpfgrp        gdqifg
     c                   eval      gdqifi = wpfirm
     c                   movel     wpqity        gdqity
      *                                                              .
      * Sletter post på GINKPF og legger den ut på hjelpefilen       .
      * dersom inkassoposten er OK:                                  .
      *                                                              .
     c                   if        wpqity <> *blanks
     c                   write     gisopfr
     c                   delete    ginkpfr
     c                   endif
      *                                                              .
      * Hent neste post:                                             .
      *                                                              .
     c                   read      ginkpf                                 60
      *                                                              .
     c                   enddo
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * Behandler nå inkassopostene sortert på filguppe/firma/type:
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     c                   eval      wpqity = *blanks
     c                   eval      wpfgrp = *blanks
     c                   eval      wpfirm = 0
      *
     c     key02         setll     gisopf
     c                   read      gisopf                                 60
      *
      * Legger over startverider i felter for "sist leste", og henter
      * løpenummer fra avtaleregister (dersom tilslag):
      *
     c                   movel     gdqifg        wpfgrp
     c                   eval      wpfirm = gdqifi
     c                   movel     gdqity        wpqity
     c                   exsr      xavink
      *
      * Leser file GISOPF intil EOF:
      *
     c                   dow       *in60 = *off
      *                                                              .
      *                                                              .
      * Sender opp infobilde dersom det finnes inkassoreturer:       .
      *                                                              .
     c                   if        w_arow >= 14
     c                   exsr      xinit
     c                   endif
      *                                                              .
     c                   if        wpwind = 0
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   write     a4win
     c                   eval      wpwind = 1
     c                   endif
      *                                                              .
      * Tester på om brudd på enten filgruppe, firma eller type      .
      * sammenlignet med sist leste post.                            .
      *                                                              .
     c                   if        gdqifg <> wpfgrp
     c                             or gdqifi <> wpfirm
     c                             or gdqity <> wpqity
     c                   exsr      xavink
     c                   endif
      *                                                              .
      * Legger over informasjon om filgruppe, firma og type          .
      * transaksjon for "inneværende" post:                          .
      *                                                              .
     c                   movel     gdqifg        wpfgrp
     c                   eval      wpfirm = gdqifi
     c                   movel     gdqity        wpqity
      *OK:                                                           .
     c                   if        *in61 = *off
      *                                                        .     .
      * Overfører post ut på file GATRPF, klar for videre      .     .
      * behandling:                                            .     .
      *                                                        .     .
     c                   eval      gbtell = gbtell + 1
     c                   movel     gdqiso        gbreco
     c                   movel     gdqity        gbtype
     c                   write     gatrpfp
     c                   delete    gisopfr
      *                                                        .     .
     c                   else
      *Feil:                                                   .     .
      *                                                        .     .
      * Bygger infofelt til bruk på kvittering dersom avtale   .     .
      * ikke finnes i avtaleregisteret:                        .     .
      *                                                        .     .
     c                   movel     wpfgrp        wptxt6
     c                   move      wpfirm        wptxt6
     c                   eval      wplese = *blanks
     c                   eval      d1text = *blanks
     c                   movel     'I-'          d1text
     c                   move      wptxt6        d1text
     c                   eval      d1bgir = wpkont
     c                   eval      d1pgir = 0
      *                                                        .     .
      * Kvittering skrives ut for avtaler som må opprettes:    .     .
      * PS!  SKriver først ut heading dersom dette ikke er     .     .
      *      gjort tidligere:                                  .     .
      *                                                        .     .
     c                   if        wperr1 = *blank
     c                             and wperr2 = *blank
     c                   write     a1hdr
     c                   endif
      *                                                        .     .
      * Legger ut oppdragsnummer i feltvariabel, rapport:      .     .
      *                                                        .     .
     c                   eval      d1oppd = 0
      *                                                        .     .
      * Legger ut "X" i D1KODA for å fortelle at avtalen for   .     .
      * dette oppdraget ikke finnes i avtaleregisteret:        .     .
      *                                                        .     .
     c                   movel     'X'           d1koda
     c                   eval      d1kodb = *blanks
      *                                                        .     .
      * Skriver ut informasjon om filgruppe/firma som må       .     .
      * opprettes i avtaleregisteret.  Tester på om det er     .     .
      * skrevet ut en linje for fgr/fir tidligere              .     .
      *                                                        .     .
     c                   if        wpfgrp <> wpsifg
     c                             or wpfirm <> wpsifi
     c                   write     d1det                                30
     c                   endif
      *                                                        .     .
     c                   movel     wpfgrp        wpsifg
     c                   eval      wpsifi = wpfirm
      *                                                        .     .
      * Overflow ?                                             .     .
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *                                                        .     .
      * Setter "flagg" v/feil mot avtaleregister:              .     .
      *                                                        .     .
     c                   movel     '1'           wperr1
      *                                                        .     .
     c                   endif
      *                                                              .
      * Hent neste post:                                             .
      *                                                              .
     c                   read      gisopf                                 60
      *                                                              .
     c                   enddo
      *
     c     xitag3        tag
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å oppdatere avtaleregister ved behandling av      *
      * inkassoreturer:                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xavink        begsr
      *
      * Legger over informasjon om filgruppe og firma for
      * "inneværende" post. Dette må gjøres for å få
      * riktige verdier i KEY02:
      *
     c                   movel     gdqifg        wpfgrp
     c                   eval      wpfirm = gdqifi
      *
     c     key02         chain     gavtl2                             61
      *
      * Oppdaterer løpenummer på faste data:
      *
     c                   if        *in61 = *off
     c                   eval      gclØpn = gclØpn + 1
     c                   update    gavtpf2
     c                   endif
      *
     c                   eval      gblØpn = gclØpn
      * Initierer de andre nøkkelverdiene:
     c                   eval      gbtell = 0
      *
      * Bankgironummer fra avtaleregisteret skal ALLTID benyttes
FØR   * dersom dette er utfylt:
FØR   *
     c                   if        gcbgir <> 0
     c                   eval      gbgiro = gcbgir
     c                   else
     c                   eval      gbgiro = gcpgir
     c                   endif
      *
17011c                   eval      gbgiro = gcskto
      *
     c                   eval      wpkont = gbgiro
     c                   eval      gboppd = 0
      *
      * Dato:
     c                   eval      dsmotÅ = 1900
     c                   if        uyear <= 90
     c                   eval      dsmotÅ = 2000
     c                   endif
     c                   move      uyear         dsmotÅ
     c                   movel     umonth        dsmotm
     c                   movel     uday          dsmotd
     c                   eval      gbdato = dsdato
      * Filgruppe og firma:
18120c                   movel     gcfilg        gbfgrp
18120c                   eval      gbfirm = gcfirm
      * Klokkeslett:
     c                   time                    gbklok
      *
     c                   endsr
      /EJECT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av posisjonering av Window:            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xinit         begsr
      *
      * Initierer posisjonering av vindu.
      *
     c                   eval      w_arow = 2
     c                   eval      w_acol = 43
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste:
      *
     c     *entry        plist
     c                   parm                    w_arow
     c                   parm                    w_acol
      *
      * Hjelpefelter v/les av OCR-retur for POST.  Feltet "flagges"
      * (blankes ut) når første post leses/behandles:
      *
     c                   movel     'JA '         wpfØrs
      *
      * Hjelpefelter v/inkassoposter.  Viser type inkassoretur:
      *
22098c                   eval      wpqity = *blanks
22098c                   eval      wpfgrp = *blanks
22098c                   eval      wpfirm = 0
22098c                   eval      wpsifg = *blanks
22098c                   eval      wpsifi = 0
22098c                   eval      wpsity = *blanks
22098c                   eval      wpsifg = *blanks
22098c                   eval      wpsifi = 0
22098c                   eval      wptxt6 = *blanks
      *
      * Hjelpefelt som sette til "JA" ved tilslag på BETFOR22-
      * transaksjon:
      *
     c                   eval      wpbe22 = *blanks
      *
      * Hjelpefelter v/les av GTGIPF og GOCRPF.  WPLESE er "ja" hvis
      * avtalenummer finnes, og postene er godkjent for oppdatering
      * til file GATRPF.
      *
     c                   movel     'JA'          wplese
      *
      * Legger over firmanavn:
      *
     c                   movel     dsfnav        a1fnav
      *
      * Hjelpefelte for å sende opp informasjon om at avtale
      * mangler på avtaleregister -> ERR1, eller at oppdrag
      * er lest inn tidligere -> ERR2.
      *
     c                   eval      wperr1 = *blanks
     c                   eval      wperr2 = *blanks
      *
      * Hjelpefelt i forbindelse med at info.vindu kun skal sendes
      * opp en gang pr. subrutine:
      *
     c                   eval      wpwind = 0
      *
      * Hjelpefelter for å legge over kontonummer i fra alfa til
      * numerisk.  Feltet brukes til å søke opp i avtaleregister
      * for å kontrollere at avtale finnes + samtidig å hente
      * løpenummer:
      *
     c                   eval      wpkont = 0
      *
      * Hjelpefelter v/les av GTGIPF:  WPBE99 indikerer at foregående
      * post var en BETFOR99-transaksjon.
      *
     c                   movel     'JA'          wpbe99
      *
     c                   eval      i = 0
ASP1 c                   eval      z = 0
ASP2 c                   eval      wpprkt = 0
ASP2 c                   eval      wpinkt = 0
      *
      * Hjelpefelt for å indikerer om lest OCR-transaksjon er en
      * POST-OCR eller BBS-OCR:
      *
     c                   eval      wptype = *blanks
      *
      * Key for file GATRL2 - Kontonr/"OCR"/Oppdragsnr:
      *
     c     key01         klist
     c                   kfld                    wpkonr
     c                   kfld                    dsbbsx
     c                   kfld                    wpbbsf
      *
     c                   movel     'OCR'         dsbbsx
      *
      * Key for oppslag mot avtaleregister via filgruppe og firma:
      *
22089c     key02         klist
22089c                   kfld                    wpfgrp
22089c                   kfld                    wpfirm
      *
     c                   endsr
      *
