     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD115R
      * Beskrivelse . . : Ordre-linje, vedlikehold av lengdespesifikasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130504 GRO  Nytt program
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  260618 bkv  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffvlsl1    if   e           k disk    rename(fvlspfr:fvlsl1r)
     ffvlslu    uf a e           k disk    rename(fvlspfr:fvlslur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     ffd115d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fvfirm)
6.NU d p_numm          s                   like(fvnumm)
     d p_suff          s                   like(fvsuff)
     d p_line          s                   like(fvline)
     d p_vare          s                   like(vvvare)
     d p_totm          s                   like(fdanta)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fodtl1_numm     s                   like(fvnumm)
     d fodtl1_suff     s                   like(fvsuff)
     d fodtl1_line     s                   like(fvline)
     d fvlsl1_numm     s                   like(fvnumm)
     d fvlsl1_suff     s                   like(fvsuff)
     d fvlsl1_line     s                   like(fvline)
     d fvlsl1_num2     s                   like(fvnumm)
     d fvlsl1_suf2     s                   like(fvsuff)
     d fvlsl1_lin2     s                   like(fvline)
     d fvlsl1_lpn2     s                   like(fvlpnr)
     d fvlslu_numm     s                   like(fvnumm)
     d fvlslu_suff     s                   like(fvsuff)
     d fvlslu_line     s                   like(fvline)
     d fvlslu_lpnr     s                   like(fvlpnr)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d b_endr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
5.31 d b_feil          s              1    inz(*off)
5.31 d b_nyrg          s              1    inz(*off)
      *
     d w_firm          s                   like(fvfirm)
6.NU d w_numm          s                   like(fvnumm)
     d w_suff          s                   like(fvsuff)
     d w_line          s                   like(fvline)
     d w_udat          s               d   datfmt(*iso)
     d w_lpnr          s                   like(fvlpnr)
     d w_leng          s              6  0
     d w_anta          s              8  2
     d w_totm          s             11  3
     d w_lpos          s                   like(fvlpnr)
     d w_ltel          s              2  0
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      nxt_key
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
     c                   if        b2leng <> *zero or
     c                             b2anta <> *zero
     c                   if        b2lpnr = *zero
     c                   exsr      ny_linje
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   exsr      tot_lopm
     c                   eval      p_totm = w_totm
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      fvlsl1_num2 = fvlsl1_numm
     c                   eval      fvlsl1_suf2 = fvlsl1_suff
     c                   eval      fvlsl1_lin2 = fvlsl1_line
      *
     c                   eval      fvlsl1_lpn2 = b1lpnr
     c     fvlsl1_key2   setll     fvlsl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        b_nyrg = *off
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
      *
     c                   eval      b2lpnr = b1lpnr
     c                   eval      b2leng = b1leng
     c                   eval      b2anta = b1anta
     c                   eval      b1valg = 0
     c                   exsr      oppd_lin
      *
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      fvlsl1_num2 = fvlsl1_numm
     c                   eval      fvlsl1_suf2 = fvlsl1_suff
     c                   eval      fvlsl1_lin2 = fvlsl1_line
     c                   eval      fvlsl1_lpn2 = b1lpnr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   exsr      forny
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ny linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_linje      begsr
      *
     c                   eval      w_lpnr = *zero
     c                   eval      *in22 = *off
     c                   eval      *in23 = *on
      *
     c     fvlsl1_key    setll     fvlsl1
     c     fvlsl1_key    reade     fvlsl1
      *
     c                   dow       not %eof
     c                   eval      w_lpnr = fvlpnr
     c     fvlsl1_key    reade     fvlsl1
     c                   enddo
      *
     c                   eval      fvfirm = w_firm
     c                   eval      fvnumm = fvlsl1_numm
     c                   eval      fvsuff = fvlsl1_suff
     c                   eval      fvline = fvlsl1_line
     c                   eval      fvlpnr = w_lpnr + 1
      *
     c                   eval      fvleng = b2leng
     c                   eval      fvanta = b2anta
      *
6.10 c                   time                    fvodat
6.10 c                   time                    fvotim
6.10 c                   eval      fvousr = l_user
6.10 c                   time                    fvedat
6.10 c                   time                    fvetim
6.10 c                   eval      fveusr = l_user
     c                   write     fvlslur
      *
     c                   eval      b2lpnr = *zero
     c                   eval      b2anta = *zero
     c                   eval      b2leng = *zero
      *
     c                   exsr      tot_lopm
     c                   eval      b2lopt = w_totm
     c                   eval      p_totm = w_totm
     c                   eval      b_nyrg = *on
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      pos_lin
     c                   exsr      crt_subfile
      *
     c     end_nylin     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_lin      begsr
      *
     c                   eval      *in22 = *off
     c                   eval      *in23 = *on
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in13 = *off
      *
     c                   if        *inkc or *inkl
     c                   goto      end_oppd
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      fvlslu_numm = fvlsl1_numm
     c                   eval      fvlslu_suff = fvlsl1_suff
     c                   eval      fvlslu_line = fvlsl1_line
     c                   eval      fvlslu_lpnr = b1lpnr
     c     fvlslu_key    chain     fvlslur
      *
     c                   if        %found
     c                   eval      fvleng = b2leng
     c                   eval      fvanta = b2anta
6.10 c                   time                    fvedat
6.10 c                   time                    fvetim
6.10 c                   eval      fveusr = l_user
     c                   update    fvlslur
     c                   exsr      tot_lopm
     c                   eval      b2lopt = w_totm
     c                   eval      p_totm = w_totm
     c                   endif
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c     fvlsl1_key2   chain     fvlsl1
      *
     c                   eval      d1leng = fvleng
     c                   eval      d1anta = fvanta
     c                   eval(h)   d1lopm = (fvleng * fvanta) / 1000
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter lengdespesif.linje
      *
     c                   eval      fvlslu_numm = fvlsl1_numm
     c                   eval      fvlslu_suff = fvlsl1_suff
     c                   eval      fvlslu_line = fvlsl1_line
     c                   eval      fvlslu_lpnr = b1lpnr
     c     fvlslu_key    chain     fvlslur
     c                   delete    fvlslur
      *
     c                   exsr      tot_lopm
     c                   eval      b2lopt = w_totm
     c                   eval      p_totm = w_totm
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
     c                   eval      w_stel = 0
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      fvlsl1
      *
     c                   if        %eof or
     c                             fvnumm <> p_numm or
     c                             fvsuff <> p_suff or
     c                             fvline <> p_line
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      b1lpnr = fvlpnr
     c                   eval      b1valg = 0
     c                   eval      b1leng = fvleng
     c                   eval      b1anta = fvanta
     c                   eval(h)   b1lopm = (fvleng * fvanta) / 1000
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   if        w_stel > 0
     c                   eval      *in22 = *on
     c                   eval      *in23 = *off
     c                   else
     c                   eval      *in22 = *off
     c                   eval      *in23 = *on
     c                   endif
      *
     c                   if        b_nyrg = *on
     c                   eval      *in22 = *off
     c                   eval      *in23 = *on
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     fvlsl1_key    setgt     fvlsl1
     c                   readp     fvlsl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     fvlsl1
      *
     c                   if        %eof or                                      EOF
     c                             fvnumm <> p_numm or
     c                             fvsuff <> p_suff or
     c                             fvline <> p_line
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      b1lpnr = fvlpnr
     c                   eval      b1valg = 0
     c                   eval      b1leng = fvleng
     c                   eval      b1anta = fvanta
     c                   eval(h)   b1lopm = (fvleng * fvanta) / 1000
      *
     c                   enddo
      *
     c                   if        %eof
     c     fvlsl1_key    setll     fvlsl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for reset av key
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nxt_key       begsr
      *
     c                   if        *in15 = *off
     c                   eval      fvlsl1_num2 = fvlsl1_numm
     c                   eval      fvlsl1_suf2 = fvlsl1_suff
     c                   eval      fvlsl1_lin2 = fvlsl1_line
     c                   eval      fvlsl1_lpn2 = b1lpnr
     c     fvlsl1_key2   setll     fvlsl1
     c                   endif
      *
     c     end_nxt       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering på riktig side ved ny linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pos_lin       begsr
      *
     c                   eval      w_lpos = *zero
     c                   eval      w_ltel = *zero
      *
     c                   eval      fvlsl1_numm = p_numm
     c                   eval      fvlsl1_suff = p_suff
     c                   eval      fvlsl1_line = p_line
     c     fvlsl1_key    setll     fvlsl1
     c     fvlsl1_key    reade     fvlsl1
      *
     c                   dow       not %eof
     c                   eval      w_ltel = w_ltel + 1
     c                   if        w_ltel = 1
     c                   eval      w_lpos = fvlpnr
     c                   endif
     c                   if        w_ltel = c_sfil
     c                   eval      w_ltel = *zero
     c                   endif
      *
     c     fvlsl1_key    reade     fvlsl1
     c                   enddo
      *
     c                   eval      fvlsl1_num2 = fvlsl1_numm
     c                   eval      fvlsl1_suf2 = fvlsl1_suff
     c                   eval      fvlsl1_lin2 = fvlsl1_line
     c                   eval      fvlsl1_lpn2 = w_lpos
     c     fvlsl1_key2   setll     fvlsl1
      *
     c     end_pos       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulasjon av totalt ant. løpemeter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tot_lopm      begsr
      *
     c                   eval      w_totm = *zero
      *
     c                   eval      fvlsl1_numm = p_numm
     c                   eval      fvlsl1_suff = p_suff
     c                   eval      fvlsl1_line = p_line
     c     fvlsl1_key    setll     fvlsl1
     c     fvlsl1_key    reade     fvlsl1
      *
     c                   dow       not %eof
     c                   eval(h)   w_totm = w_totm +
     c                                      ((fvleng * fvanta) / 1000)
     c     fvlsl1_key    reade     fvlsl1
     c                   enddo
      *
     c     end_totm      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_vare
     c                   parm                    p_totm
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for oppslag på lengdespesif.linje
      *
     c     fvlsl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fvlsl1_numm
     c                   kfld                    fvlsl1_suff
     c                   kfld                    fvlsl1_line
      *
      * Key for start på lengdespesif.linje
      *
     c     fvlsl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fvlsl1_num2
     c                   kfld                    fvlsl1_suf2
     c                   kfld                    fvlsl1_lin2
     c                   kfld                    fvlsl1_lpn2
      *
      * Key for oppdatering av lengdespesif.linje
      *
     c     fvlslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fvlslu_numm
     c                   kfld                    fvlslu_suff
     c                   kfld                    fvlslu_line
     c                   kfld                    fvlslu_lpnr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fvlsl1_numm = p_numm
     c                   eval      fvlsl1_suff = p_suff
     c                   eval      fvlsl1_line = p_line
     c     fvlsl1_key    setll     fvlsl1
      *
     c                   exsr      crt_subfile
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   movel     vvvare        b2vare
     c                   eval      b2vtxt = vvtek1
     c                   endif
      *
     c                   exsr      tot_lopm
     c                   eval      b2lopt = w_totm
     c                   eval      p_totm = w_totm
      *
     c                   endsr
