     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI723R
      * Beskrivelse . . : Automatisk innlesing av fakturaer
      *
      * Spesialversjon. : Gipling
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261018 bsa  Nytt program
7.01  *       221118 bsa  Justert kroneavrunding
7.02  *       271118 bsa  Stoppe hvis ordrenummer finnes. Ikke sjekk
7.02  *                   om den er fakturert.
      *
8.01  *K0000  230621 BSA  Utvidet meldingsnummer fra 6-8 siffer
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledilu    uf   e           k disk    rename(ledipfr:ledilur)
     f                                     prefix(xi:2)
     fledil5    if   e           k disk    rename(ledipfr:ledil5r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter-inn
      *
     d p_stat          s              1
     d p_type          s              4
     d p_mnum          s                   like(limnum)
     d p_mlin          s                   like(limlin)
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
     d  d_fh_bean                    13  0
     d  d_fh_biln                     8  0
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * Fritekst/hodelinje
      *
     d d_fakh3         ds
     d  d_fh_ftxq                     3
     d  d_fh_ftxt                    70
      *
      * Faktura partsinfo./hode
      *
     d d_fakh5         ds
     d  d_fh_part                     3
     d  d_fh_pean                    13  0
     d  d_fh_pnav                    30
     d  d_fh_pad1                    30
     d  d_fh_pad2                    30
     d  d_fh_pste                    30
     d  d_fh_porg                    20
      *
      * Faktura transport/lev. - hode
      *
     d d_fakh6         ds
     d  d_fh_trmo                     3
     d  d_fh_trna                    35
     d  d_fh_lebe                     3
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
     d  d_fl_vean                    13  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
     d  d_fl_vevr                    15
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ledil5_kode     s                   like(likode)
     d ledil5_type     s                   like(litype)
     d ledil5_mnum     s                   like(limnum)
     d ledil5_mlin     s                   like(limlin)
     d ledilu_type     s                   like(litype)
     d ledilu_mnum     s                   like(limnum)
     d ledilu_mlin     s                   like(limlin)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
      *
      * Definering av variable i parametre
      *
      *
      * Definering av variable
      *
     d b_upda          s              1    inz(*off)
     d b_katg          s              1    inz(*off)
     d b_kobl          s              1    inz(*off)
     d b_edim          s              1    inz(*off)
     d*b_levr          s              1    inz(*off)
     d b_head          s              1    inz(*off)
     d b_ordre         s              1    inz(*off)
      *
     d s_mnum          s                   like(limnum)
     d s_numm          s                   like(lonumm)
     d s_suff          s                   like(losuff)
      *
     d w_firm          s                   like(lifirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_year          s              4  0
     d w_posi          s              2  0
     d w_pean          s             13  0
     d x               s              2  0
     d y               s              2  0
     d z               s              2  0
7.01 d w_ftot          s             15  2
7.01 d w_btot          s             15  2
7.01 d w_diff          s             13  2
     d w_stat          s              1
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandler EDI meldinger med "blank" status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Les alle meldinger med godkjent status
      *
     c                   eval      ledil5_kode = *blanks
     c                   eval      ledil5_type = 'FAKT'
     c                   eval      ledil5_mnum = 0
     c                   eval      ledil5_mlin = 0
     c     ledil5_key    setll     ledil5
     c                   read      ledil5r
      *
     c                   dow       not %eof
      *
     c                   if        lifirm <> w_firm
     c                   if        s_mnum <> 0
     c                   if        b_edim = *on
     c                   exsr      sjk_mnum
     c**                 eval      b_levr = *off
     c                   endif
     c                   endif
     c                   goto      avslutt
     c                   endif
9.xx  * Test
9.xx c                   if        limnum = 676382
9.xx c                   goto      neste
9.xx c                   endif
9.xx c     neste         tag
      *
      * Første gang må vi logge ny EDI som gammelt nummer
      *
     c                   if        s_mnum = 0
     c                   eval      s_mnum = limnum
     c                   eval      b_edim = *on
     c                   endif
      *
      * Sjekk om vi leser ny EDI melding.
      *
     c                   if        s_mnum <> limnum
     c                   if        b_edim = *on
     c                   exsr      sjk_mnum
     c                   endif
     c                   eval      s_mnum = limnum
     c                   eval      b_edim = *on
     c**                 eval      b_levr = *off
     c                   endif
      *
      * Har EDI meldingen feilkode, kan vi ikke oppdatere
      *
     c                   if        limlin = 100001 and
     c                             listat <> *blanks or
     c                             limlin = 100001 and
     c                             b_ordre = *on
     c                   eval      b_edim = *off
     c                   endif
      *
      * NB Kun meldinger uten feilkode
      *
     c                   if        b_edim = *on
      *
      * Legg unna info om faktura/kreditnota
      *
     c                   if        limlin = 100001
     c                   eval      d_fakh1 = limeld
     c                   eval      b_head = *on
     c                   eval      s_numm = linumm
     c                   eval      s_suff = lisuff
     c                   endif
      *
      * Finn fakturatotaler
      *
     c                   if        limlin = 900001
     c                   eval      d_fakt = limeld
     c                   eval(h)   w_ftot = d_ft_teks
      * NB Hvis kreditnota, må vi snu beløpet, slik at det blir negativt.
      *    Skal ikke skli gjennom "store" kreditnotaer. (Nedre beløpsgrense)
     c                   if        d_fh_dtyp = '381' and
     c                             w_ftot < 0
     c                   eval      w_ftot = w_ftot * -1
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   read      ledil5r
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker reglene mot automatisk oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_mnum      begsr
      *
     c                   eval      b_upda = *on
     c                   eval      b_ordre = *off
     c                   eval      w_stat = 'X'
      *
      * Sjekk om bestillingen er tilknyttet ordre
      *
     c                   if        b_upda = *on
      *
      * NB Vi forsøker finne bestillingen med riktig suffiks. Dvs at vi forsøker knytte
      * EDI meldingen til riktig suffiks, selv om dette ikke er angitt. NB Kun hvis
      * eksakt beløp.
      *
     c                   exsr      best_match
     c                   eval      w_btot = 0
     c                   eval      lohel1_numm = s_numm
     c                   eval      lohel1_suff = s_suff
     c     lohel1_key    chain     lohel1
     c                   if        %found
     c                   eval(h)   w_btot = lototi - lototr
7.02 c                   if        loornr <> 0
7.02 c*                  eval      fohel1_numm = loornr
7.02 c*                  eval      fohel1_suff = loorsu
7.02 c*    fohel1_key    chain     fohel1
7.02 c*                  if        %found
     c                   eval      b_ordre = *on
     c                   eval      b_upda = *off
     c                   eval      w_stat = 'O'
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Da kjører vi beløpstest
      *
     c                   if        b_upda = *on
      *
7.01 c                   eval      w_diff = 0
7.01 c                   eval      w_diff = w_ftot - w_btot
7.01 c                   if        w_diff <  0
7.01 c                   eval      w_diff = w_diff * -1
7.01 c                   endif
7.01 c*                  if        w_ftot <> btot
7.01 c                   if        w_diff > 1
     c                   eval      w_stat = 'B'
     c                   eval      b_upda = *off
     c                   endif
      *
     c                   endif
      *
      * Merk EDI melding med stoppet i automatisk innlesing
      *
     c                   if        b_upda = *off
     c                   exsr      set_status
     c                   else
     c                   exsr      upd_mnum
     c                   endif
      *
     c                   eval      b_head = *off
     c                   eval      b_ordre = *off
     c                   eval      s_numm = 0
     c                   eval      s_suff = 0
      *
     c     end_mnum      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for nullstilling av meldingsstatus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     set_status    begsr
      *
      * Status skal ikke settes, men fjerner ikke da det kan bli aktuelt etterhvert.
      *
     c                   if        1=2
     c                   eval      ledilu_type = 'FAKT'
     c                   eval      ledilu_mnum = s_mnum
     c                   eval      ledilu_mlin = *zero
      *
     c     ledilu_key    setll     ledilu
     c     ledilu_key2   reade     ledilu
      *
     c                   dow       not %eof
     c                   eval      xistat = w_stat
     c                   time                    xiedat
     c                   time                    xietim
     c                   eval      xieusr = l_user
     c                   update    ledilur
      *
     c     ledilu_key2   reade     ledilu
     c                   enddo
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for matching av bestilling mot EDI melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     best_match    begsr
      *
      * Status skal ikke settes, men fjerner ikke da det kan bli aktuelt etterhvert.
      *
     c                   eval      lohel1_numm = s_numm
     c                   eval      lohel1_suff = 0
     c     lohel1_key    setll     lohel1
     c     lohel1_key2   reade     lohel1
      *
     c                   dow       not %eof
     c                   eval(h)   w_btot = lototi - lototr
     c                   if        w_btot = w_ftot and
     c                             s_suff <> losuff
     c                   eval      s_suff = losuff
     c                   exsr      upd_suff
     c                   endif
      *
     c     lohel1_key2   reade     lohel1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for oppdatering av suffiks på EDI meldingen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_suff      begsr
      *
     c                   eval      ledilu_type = 'FAKT'
     c                   eval      ledilu_mnum = s_mnum
     c                   eval      ledilu_mlin = *zero
      *
     c     ledilu_key    setll     ledilu
     c     ledilu_key2   reade     ledilu
      *
     c                   dow       not %eof
     c                   eval      xisuff = s_suff
     c                   time                    xiedat
     c                   time                    xietim
     c                   eval      xieusr = l_user
     c                   update    ledilur
      *
     c     ledilu_key2   reade     ledilu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for automatisk oppdatering av EDI melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_mnum      begsr
      *
     c                   if        b_head = *on
      *
     c                   eval      p_type = 'FAKT'
     c                   eval      p_mnum = s_mnum
     c                   eval      p_mlin = 100001
     c                   eval      p_stat = *blanks
      *
     c                   call      'LI724R'
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_mlin
     c                   parm                    p_stat
      *
     c                   endif
      *
     c                   eval      w_ftot = 0
     c                   eval      w_btot = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c**   *entry        plist
     c**                 parm                    p_type
     c**                 parm                    p_mnum
     c**                 parm                    p_stat
      *
      *  Key for oppdatering av MELDINGSLINJER
      *
     c     ledilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilu_type
     c                   kfld                    ledilu_mnum
     c                   kfld                    ledilu_mlin
      *
      *  Key for oppdatering av MELDINGSLINJER
      *
     c     ledilu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    ledilu_type
     c                   kfld                    ledilu_mnum
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil5_kode
     c                   kfld                    ledil5_type
     c                   kfld                    ledil5_mnum
     c                   kfld                    ledil5_mlin
      *
      * Key for oppslag på ordrehodet
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på bestillingshodet
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppslag på bestillingshodet
      *
     c     lohel1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
      *
      * Henter verdier fra parmametre
      *
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_firm = l_firm
      *
     c                   endsr
